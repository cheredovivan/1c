#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекстАП", НСтр("ru = 'АП';
																								|en = 'AP'"), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекстА", НСтр("ru = 'А';
																								|en = 'A'"), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекстП", НСтр("ru = 'П';
																								|en = 'П'"), Истина);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТекстОб", " " + НСтр("ru = '(об)';
																									|en = '(turn)'"), Истина);

	Если Параметры.Свойство("ВидыСубконто")
		ИЛИ Параметры.Свойство("ВГруппе") Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Ссылка",
			ПланыСчетов.Хозрасчетный.СчетаДляВыбора(Параметры),
			ВидСравненияКомпоновкиДанных.ВСписке,,
			Истина);
			
	ИначеЕсли Параметры.Отбор.Свойство("Ссылка") Тогда
		
		СчетаОтбораПоУмолчанию = Новый СписокЗначений; 
		
		МассивСсылок = Параметры.Отбор.Ссылка;
		Если ТипЗнч(МассивСсылок) = Тип("ФиксированныйМассив") Тогда
			СчетаОтбораПоУмолчанию.ЗагрузитьЗначения(Новый Массив(МассивСсылок));
		Иначе
			СчетаОтбораПоУмолчанию.ЗагрузитьЗначения(Параметры.Отбор.Ссылка);
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка( Список, "Ссылка",
			СчетаОтбораПоУмолчанию, ВидСравненияКомпоновкиДанных.ВСписке, НСтр("ru = 'Отбор счетов по умолчанию';
																				|en = 'Default account filter'"),
			Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный, "ОтборСчетовПоУмолчанию");
		Параметры.Отбор.Удалить("Ссылка");
		
		Если Параметры.Отбор.Свойство("Валютный") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка( Список, "Валютный",
			Параметры.Отбор.Валютный, ВидСравненияКомпоновкиДанных.Равно, НСтр("ru = 'Отбор счетов по умолчанию';
																				|en = 'Default account filter'"),
			Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный, "ОтборСчетовПоУмолчанию");
		Параметры.Отбор.Удалить("Валютный");
		КонецЕсли;
			
		Если Параметры.Отбор.Свойство("Долгосрочный") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка( Список, "Долгосрочный",
				Параметры.Отбор.Долгосрочный, ВидСравненияКомпоновкиДанных.Равно, 
				НСтр("ru = 'Отбор счетов по умолчанию';
					|en = 'Default account filter'"),
			Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный, "ОтборСчетовПоУмолчанию");

			Параметры.Отбор.Удалить("Долгосрочный");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если Список.Отбор.Элементы.Количество() Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	Для каждого ЭлементНастройки Из Настройки.Элементы Цикл
		Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = "ОтборСчетовПоУмолчанию" Тогда
			ЭлементНастройки.Использование = Истина;
			ИндексЭлементаНастройки = Настройки.Элементы.Индекс(ЭлементНастройки);
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти