
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПланСчетов = Параметры.ПланСчетов;
	Если Параметры.Отбор.Свойство("ПланСчетов") И ЗначениеЗаполнено(Параметры.Отбор.ПланСчетов) Тогда
		ПланСчетов = Параметры.Отбор.ПланСчетов;
		Параметры.Отбор.Удалить("ПланСчетов");
		Элементы.ПланСчетов.Доступность = Ложь;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ПланСчетов) Тогда
		ПланСчетов = Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Актив", НСтр("ru = 'Актив';
																							|en = 'Asset'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Пассив", НСтр("ru = 'Пассив';
																								|en = 'Liability'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "АктивПассив", НСтр("ru = 'Актив/Пассив';
																									|en = 'Asset/Liability'"));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ТолькоОбороты", " ("+НСтр("ru = 'об';
																											|en = 'turn'")+")");
	
	Параметры.Свойство("ТолькоРазрешенные", ТолькоРазрешенные);
	
	Если Параметры.Свойство("ВидыСубконто") Тогда
		ОтображениеСписком = Истина;
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Ссылка",
			ПланыСчетов.Международный.СчетаУчетаСВидомСубконто(Параметры.ВидыСубконто),
			ВидСравненияКомпоновкиДанных.ВСписке,,
			Истина);
	КонецЕсли;
	
	Если НЕ ОтображениеСписком Тогда
		ОтборБезВлиянияНаОтображение = Новый Массив();
		ОтборБезВлиянияНаОтображение.Добавить("ПланСчетов");
		ОтборБезВлиянияНаОтображение.Добавить("Забалансовый");
		
		Для Каждого ЭлементОтбора Из Параметры.Отбор Цикл
			Если ОтборБезВлиянияНаОтображение.Найти(ЭлементОтбора.Ключ) = Неопределено Тогда
				ОтображениеСписком = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Элементы.УчетПоНаправлениямДеятельности.Видимость = Справочники.НаправленияДеятельности.ИспользуетсяУчетПоНаправлениям();
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОтображениеСписком Тогда
		Элементы.Список.Отображение = ОтображениеТаблицы.Список;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПланСчетов", ПланСчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПланСчетов", ПланСчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если ТолькоРазрешенные Тогда
		Если ТипЗнч(Значение) = Тип("ПланСчетовСсылка.Международный") Тогда
			СтандартнаяОбработка = НЕ Элемент.ДанныеСтроки(Значение).ЗапретитьИспользоватьВПроводках;
			
		ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
			Индекс = 0;
			Пока Индекс <= Значение.ВГраница() Цикл
				Счет = Значение[Индекс];
				Если Элемент.ДанныеСтроки(Счет).ЗапретитьИспользоватьВПроводках Тогда
					Значение.Удалить(Индекс);
					Продолжить;
				КонецЕсли;
				Индекс = Индекс + 1;
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;// ТолькоРазрешенные
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланСчетовПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ПланСчетов", ПланСчетов);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПланСчетов", ПланСчетов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
