#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных,
				КомпоновщикНастроек.ПолучитьНастройки(),
				ДанныеРасшифровки);
	
	ЗапросПоПравиламРасчета = КомпоновкаДанныхСервер.ПолучитьЗапросИзМакетаКомпоновки(МакетКомпоновки, "ПравилаРасчета");
	ТаблицаПравилаРасчета = ЗапросПоПравиламРасчета.Выполнить().Выгрузить();
	
	ВнешниеНаборы = Новый Структура;
	ВнешниеНаборы.Вставить("СвязанныеОстаткиИОбороты", СвязанныеОстаткиИОборотыБюджетов(
		ТаблицаПравилаРасчета.ВыгрузитьКолонку("ПравилоРасчета")));
	ВнешниеНаборы.Вставить("НастройкиСтатейБюджетов", НастройкиСтатейБюджетов(ТаблицаПравилаРасчета.ВыгрузитьКолонку(
		"Приемник")));
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборы , ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
	ОтчетПустой = ОтчетыСервер.ОтчетПустой(ЭтотОбъект, ПроцессорКомпоновкиДанных);
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ОтчетПустой", ОтчетПустой);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанныхОтчета

Функция СвязанныеОстаткиИОборотыБюджетов(ПравилаРасчета)
	
	ТаблицаОстатковИОборотов = Новый ТаблицаЗначений;
	Для Каждого Поле Из СхемаКомпоновкиДанных.НаборыДанных.СвязанныеОстаткиИОбороты.Поля Цикл
		ТаблицаОстатковИОборотов.Колонки.Добавить(Поле.Поле, Поле.ТипЗначения, Поле.Заголовок);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Период = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек.ПользовательскиеНастройки,
		"Период").Значение; // -СтандартныйПериод-
	Запрос.УстановитьПараметр("НачалоПериода", Период.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Период.ДатаОкончания);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Данные.СтатьяБюджетов КАК СтатьяБюджетов,
	|	Данные.ПериодДень КАК ПериодДень,
	|	Данные.ПериодПланирования КАК ПериодПланирования,
	|	Данные.Сценарий КАК Сценарий,
	|	Данные.Организация КАК Организация,
	|	Данные.Подразделение КАК Подразделение,
	|	Данные.Валюта КАК Валюта,
	|	&ТекстПолейАналитик,
	|	ВЫБОР
	|		КОГДА Данные.СтатьяБюджетов.УчитыватьПоКоличеству
	|			ТОГДА Данные.КоличествоОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	Данные.СуммаУпрОборот КАК СуммаУпр,
	|	Данные.СуммаРеглОборот КАК СуммаРегл,
	|	Данные.СуммаСценарияОборот КАК СуммаСценария,
	|	ВЫБОР
	|		КОГДА Данные.СтатьяБюджетов.УчитыватьПоВалюте
	|			ТОГДА Данные.СуммаВВалютеОборот
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВВалюте,
	|	&ПравилоРасчета КАК ПравилоРасчета
	|ИЗ
	|	Данные КАК Данные";
	МассивПолейАналитик = Новый Массив;
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Данные.Аналитика%1 КАК Аналитика%1", НомерАналитики));
	КонецЦикла;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик", СтрСоединить(МассивПолейАналитик, "," + Символы.ПС
		+ Символы.Таб));
	
	Для Каждого ПравилоРасчета Из ПравилаРасчета Цикл
		
		ДанныеЗапроса = ПравилоРасчета.ХранилищеЗапроса.Получить();
		Если ТипЗнч(ДанныеЗапроса) <> Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяПараметраИсточника = "Источник_" + ДанныеЗапроса.ИдентификаторПравила;
		ИмяПараметраПриемника = "Приемник_" + ДанныеЗапроса.ИдентификаторПравила;
		
		ТекстыЗапросов = Новый Массив;
		Если Не ПустаяСтрока(ДанныеЗапроса.ТекстыЗапросовБазыРаспределения) Тогда
			Для Каждого КлючИЗначение Из ДанныеЗапроса.ТекстыЗапросовБазыРаспределения Цикл
				ТекстыЗапросов.Добавить(КлючИЗначение.Значение);
			КонецЦикла;
		КонецЕсли;
		Если Не ПустаяСтрока(ДанныеЗапроса.ТекстЗапросаВременныеТаблицы) Тогда
			ТекстыЗапросов.Добавить(ДанныеЗапроса.ТекстЗапросаВременныеТаблицы);
		КонецЕсли;
		ТекстыЗапросов.Добавить(ДанныеЗапроса.ТекстЗапроса);
		ТекстыЗапросов.Добавить(ТекстЗапроса);
		
		Запрос.Текст = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыражениеНачалоПериода", "НачалоПериода");
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВыражениеКонецПериода", "КонецПериода");
		Для Каждого ПараметрЗапроса Из ДанныеЗапроса.ПараметрыЗапроса Цикл
			Если ПараметрЗапроса.Ключ = ДанныеЗапроса.ИдентификаторИсточника Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, ИмяПараметраИсточника, ПараметрЗапроса.Ключ);
			ИначеЕсли ПараметрЗапроса.Ключ = ДанныеЗапроса.ИдентификаторПриемника Тогда
				Запрос.Текст = СтрЗаменить(Запрос.Текст, ИмяПараметраПриемника, ПараметрЗапроса.Ключ);
			КонецЕсли;
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЦикла;
		Запрос.УстановитьПараметр("ПравилоРасчета", ПравилоРасчета);
		РезультатЗапроса = Запрос.Выполнить(); //@skip-check query-in-loop
		Если РезультатЗапроса.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(ТаблицаОстатковИОборотов, РезультатЗапроса.Выгрузить());
		
	КонецЦикла;
	
	Возврат ТаблицаОстатковИОборотов;
	
КонецФункции

Функция НастройкиСтатейБюджетов(СтатьиПоказателиПравилРасчета)
	
	ТаблицаНастроек = Новый ТаблицаЗначений;
	Для Каждого Поле Из СхемаКомпоновкиДанных.НаборыДанных.НастройкиСтатейБюджетов.Поля Цикл
		ТаблицаНастроек.Колонки.Добавить(Поле.Поле, Поле.ТипЗначения, Поле.Заголовок);
	КонецЦикла;
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	
	Для Каждого СтатьяПоказатель Из СтатьиПоказателиПравилРасчета Цикл
		НоваяСтрока = ТаблицаНастроек.Добавить();
		НоваяСтрока.СтатьяБюджетов = СтатьяПоказатель;
		МаксимальныйНомерАналитики = 0;
		ВидыАналитик = БюджетированиеСервер.ВидыАналитик(СтатьяПоказатель);
		Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
			ВидАналитики = ВидыАналитик[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1",
				НомерАналитики)];
			Если ЗначениеЗаполнено(ВидАналитики) Тогда
				МаксимальныйНомерАналитики = НомерАналитики;
				ПредставлениеВидаАналитики = БюджетнаяОтчетностьВызовСервера.ПредставлениеВидаАналитики(ВидАналитики);
				НоваяСтрока[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПредставлениеВидаАналитики%1",
					НомерАналитики)] = ПредставлениеВидаАналитики;
				ПредставлениеПрочейАналитики = БюджетнаяОтчетностьКлиентСервер.ПредставлениеПрочейАналитикиБюджетирования(
					ВидАналитики, "Прочие");
				НоваяСтрока[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПредставлениеПрочейАналитики%1",
					НомерАналитики)] = ПредставлениеПрочейАналитики;
			КонецЕсли;
		КонецЦикла;
		НоваяСтрока.МаксимальныйНомерАналитики = МаксимальныйНомерАналитики;
	КонецЦикла;
	
	Возврат ТаблицаНастроек;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
