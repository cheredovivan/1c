#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// 
// Возвращаемое значение:
//     СтрокаТаблицыЗначений
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ПроверкаСвязанныхОстатковИОборотовБюджетов) 
			И ПолучитьФункциональнуюОпцию("ИспользоватьБюджетирование") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ПроверкаСвязанныхОстатковИОборотовБюджетов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Проверить правило расчета';
											|en = 'Check calculation rule'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ПроверкаСвязанныхОстатковИОборотовБюджетов";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   ВыводитьВариантыОтчетов - Булево - Истина, если нужно вывести вариант отчета по умолчанию.
//
Процедура НастроитьВариантыОтчета(Настройки, ВыводитьВариантыОтчетов) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ПроверкаСвязанныхОстатковИОборотовБюджетов);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОписаниеОтчета, ВыводитьВариантыОтчетов);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'Проверка результата по правилам расчета связанных остатков и оборотов бюджетов.';
									|en = 'Check the result by the rules of calculating related budget balances and turnovers.'");

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли