
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
	НаборДанных.Запрос = ТекстЗапроса_ПлановаяСебестоимостьПродукции();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередФормированиемОтчета = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета.
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//       Пользовательские настройки для загрузки в компоновщик настроек.
//
// См. синтакс-помощник "Расширение управляемой формы для отчета.ПриЗагрузкеПользовательскихНастроекНаСервере"
//    в синтакс-помощнике.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(ЭтаФорма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Параметры = ЭтаФорма.ФормаПараметры;
	Если Параметры.Свойство("ОбъектКалькуляцииОтбор") Тогда
		ОбъектКалькуляцииОтбор = Параметры.ОбъектКалькуляцииОтбор;
		КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек;
		
		ОбъектКалькуляцииОтборПН = ПолучитьЭлементОтбораПоИмени(КомпоновщикНастроекФормы,
			КомпоновщикНастроекФормы.ПользовательскиеНастройки, "ОбъектКалькуляции");
		
		ОбъектКалькуляцииОтборПН.Использование = Истина;
		ОбъектКалькуляцииОтборПН.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОбъектКалькуляцииОтборПН.ПравоеЗначение = ОбъектКалькуляцииОтбор;
		
		НовыеПользовательскиеНастройкиКД = КомпоновщикНастроекФормы.ПользовательскиеНастройки;
	КонецЕсли;
		
КонецПроцедуры

// Перед формированием отчета
//
// Параметры:
//   ФормаОтчета - ФормаКлиентскогоПриложения
//   ДополнительныеПараметры - Структура:
//     * ТекстПредупреждения - Строка
//     * ИмяПараметраХраненияОтказаОтПредупреждения - Строка
//
Процедура ПередФормированиемОтчета(ФормаОтчета, ДополнительныеПараметры) Экспорт
	КомпоновщикНастроекФормы = ФормаОтчета.Отчет.КомпоновщикНастроек;
	ОбъектКалькуляцииОтбор = ПолучитьЭлементОтбораПоИмени(КомпоновщикНастроекФормы,
		КомпоновщикНастроекФормы.ПользовательскиеНастройки, "ОбъектКалькуляции");
		
	Если ОбъектКалькуляцииОтбор <> Неопределено И ОбъектКалькуляцииОтбор.Использование Тогда
		УстановитьОтборПоОбъектуКалькуляции(ФормаОтчета, ОбъектКалькуляцииОтбор.ПравоеЗначение);
	КонецЕсли;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	Если Параметры.Свойство("ОбъектКалькуляцииОтбор") Тогда
		ЭтаФорма.ФормаПараметры.Вставить("ОбъектКалькуляцииОтбор", Параметры.ОбъектКалькуляцииОтбор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

// Функция ищет по имени элемент отбора КД, если элемент не найден возвращает Неопределено
//
// Параметры:
//   КомпоновщикНастроекФормы - КомпоновщикНастроекКомпоновкиДанных
//   НастройкиКД - НастройкиКомпоновкиДанных
//               - ПользовательскиеНастройкиКомпоновкиДанных
//   Имя - Строка - Имя искомого элемента отбора
// Возвращаемое значение:
//  ЭлементОтбораКомпоновкиДанных
//
Функция ПолучитьЭлементОтбораПоИмени(КомпоновщикНастроекФормы, НастройкиКД, Имя)
	Если ТипЗнч(НастройкиКД) = Тип("НастройкиКомпоновкиДанных") Тогда
		ПолеКД = Новый ПолеКомпоновкиДанных(Имя);
		Для Каждого ЭлементОтбора Из НастройкиКД.Отбор.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = ПолеКД Тогда
				Возврат ЭлементОтбора;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(НастройкиКД) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		НастройкиЭлементОтбора = ПолучитьЭлементОтбораПоИмени(КомпоновщикНастроекФормы,
			КомпоновщикНастроекФормы.Настройки, "ОбъектКалькуляции");
		Если НастройкиЭлементОтбора = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		ИдентификаторПользовательскойНастройки = НастройкиЭлементОтбора.ИдентификаторПользовательскойНастройки;
		ЭлементОтбора = НастройкиКД.Элементы.Найти(ИдентификаторПользовательскойНастройки);
		Возврат ЭлементОтбора;
	КонецЕсли;
		
	Возврат Неопределено;
КонецФункции

Процедура УстановитьОтборПоОбъектуКалькуляции(ЭтаФорма, ОбъектКалькуляцииОтбор)

	КомпоновщикНастроекФормы = ЭтаФорма.Отчет.КомпоновщикНастроек; // КомпоновщикНастроекКомпоновкиДанных
	ПлановыеКалькуляции = ПолучитьПоОбъектуКалькуляцииСписокРассчитанныхПлановыхКалькуляций(
			ОбъектКалькуляцииОтбор);
	Если ПлановыеКалькуляции.Количество() = 0 Тогда
		Шаблон = НСтр(
			"ru = 'Для ""%1"" нет успешно рассчитанных плановых калькуляций, формирование отчета невозможно.';
			|en = 'There are no successfully calculated product standard costs for ""%1"". Cannot generate the report.'");
		ТекстСообщения = СтрШаблон(Шаблон, ОбъектКалькуляцииОтбор);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроекФормы.ПользовательскиеНастройки,
		"ПлановыеКалькуляции", ПлановыеКалькуляции);

КонецПроцедуры

// Получить по заказу на производство список рассчитанных плановых калькуляций.
// 
// Параметры:
//  ОбъектКалькуляции - ДокументСсылка.ЗаказНаПроизводство2_2 - документ по которому необходимо сравнить плановые калькуляции
//                    - СправочникСсылка.РесурсныеСпецификации - ресурсная спецификация по которой необходимо сравнить плановые калькуляции
// 
// Возвращаемое значение:
//  СписокЗначений из ДокументСсылка.ПлановаяКалькуляция2_2
Функция ПолучитьПоОбъектуКалькуляцииСписокРассчитанныхПлановыхКалькуляций(ОбъектКалькуляции)
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ОбъектКалькуляции", ОбъектКалькуляции);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыКалькуляции.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ОбъектыКалькуляции
	|
	|ГДЕ
	|	ОбъектыКалькуляции.Объект = &ОбъектКалькуляции
	|	И ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.СостоянияПлановыхКалькуляций КАК СостояниеПК
	|		ГДЕ
	|			СостояниеПК.Калькуляция = ОбъектыКалькуляции.Ссылка
	|			И СостояниеПК.Состояние = ЗНАЧЕНИЕ(перечисление.СостоянияРасчетаПлановойКалькуляции.Рассчитана))
	|";

	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	ПлановыеКалькуляции = Новый СписокЗначений();
	Пока РезультатЗапроса.Следующий() Цикл
		ПлановыеКалькуляции.Добавить(РезультатЗапроса.Ссылка);
	КонецЦикла;
	
	Возврат ПлановыеКалькуляции;
	
КонецФункции

// Функция возвращает запрос формируемого отчета
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапроса_ПлановаяСебестоимостьПродукции()
	
	ТекстыЗапросов = Новый Массив();
	ТекстыЗапросов.Добавить(Отчеты.ПлановаяСебестоимостьПродукции.ТекстЗапроса_ДанныеПлановыхКалькуляций(Истина));
	
	// В тексте запроса ниже написаны комментарии, в конструкторе не менять!
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ВЗ.ЭтоПродукция КАК ЭтоПродукция,
		|	ВЗ.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ВЗ.Организация КАК Организация,
		|	ВЗ.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ВЗ.Калькуляция КАК Калькуляция,
		|	ВЗ.Продукция КАК Продукция,
		|	ВЗ.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВЗ.Подразделение КАК Подразделение,
		|	ВЗ.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ВЗ.Номенклатура КАК Номенклатура,
		|	ВЗ.Характеристика КАК Характеристика,
		|	ВЗ.ТипЗатрат КАК ТипЗатрат,
		|	ВЗ.Полуфабрикат КАК Полуфабрикат,
		|	ВЗ.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ВЗ.НазначениеПродукции КАК НазначениеПродукции,
		|	СУММА(ВЗ.Количество) КАК Количество,
		|	СУММА(ВЗ.Стоимость) КАК Стоимость,
		|	СУММА(ВЗ.СтоимостьМатериальные) КАК СтоимостьМатериальные,
		|	СУММА(ВЗ.СтоимостьТрудозатраты) КАК СтоимостьТрудозатраты,
		|	СУММА(ВЗ.СтоимостьПрочиеПостоянные) КАК СтоимостьПрочиеПостоянные,
		|	СУММА(ВЗ.СтоимостьПрочиеПеременные) КАК СтоимостьПрочиеПеременные,
		|	СРЕДНЕЕ(ВЗ.Цена) КАК Цена,
		|	СУММА(ВЗ.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
		|	СУММА(ВЗ.КоличествоВыпуска) КАК КоличествоВыпуска
		|ПОМЕСТИТЬ РассчитанныеЗатраты
		|ИЗ
		|	(ВЫБРАТЬ
		|		Данные.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ЭтоПродукция,
		|		ЛОЖЬ КАК ЭтоПолуфабрикат,
		|		Данные.Калькуляция.Организация КАК Организация,
		|		Данные.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|		Данные.Калькуляция КАК Калькуляция,
		|		Данные.Продукция КАК Продукция,
		|		Данные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|		Данные.Подразделение КАК Подразделение,
		|		Данные.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		Данные.Номенклатура КАК Номенклатура,
		|		Данные.Характеристика КАК Характеристика,
		|		Данные.ТипЗатрат КАК ТипЗатрат,
		|		СУММА(Данные.Количество) КАК Количество,
		|		СУММА(ВЫБОР
		|				КОГДА Данные.ЭтоПолуфабрикат
		|					ТОГДА 0
		|				ИНАЧЕ Данные.Стоимость
		|			КОНЕЦ) КАК Стоимость,
		|		СУММА(ВЫБОР
		|				КОГДА (Данные.ТипЗатрат = ""Материальные""
		|						ИЛИ Данные.ТипЗатрат = ""ВозвратныеОтходы"")
		|						И НЕ Данные.ЭтоПолуфабрикат
		|					ТОГДА Данные.Стоимость
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК СтоимостьМатериальные,
		|		СУММА(ВЫБОР
		|				КОГДА Данные.ТипЗатрат = ""Трудозатраты""
		|						И НЕ Данные.ЭтоПолуфабрикат
		|					ТОГДА Данные.Стоимость
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК СтоимостьТрудозатраты,
		|		СУММА(ВЫБОР
		|				КОГДА Данные.ТипЗатрат = ""ПрочиеПостоянные""
		|						И НЕ Данные.ЭтоПолуфабрикат
		|					ТОГДА Данные.Стоимость
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК СтоимостьПрочиеПостоянные,
		|		СУММА(ВЫБОР
		|				КОГДА Данные.ТипЗатрат = ""ПрочиеПеременные""
		|						И НЕ Данные.ЭтоПолуфабрикат
		|					ТОГДА Данные.Стоимость
		|				ИНАЧЕ 0
		|			КОНЕЦ) КАК СтоимостьПрочиеПеременные,
		|		ВЫБОР
		|			КОГДА Данные.ЭтоПолуфабрикат
		|				ТОГДА МАКСИМУМ(ЕСТЬNULL(Цены.Цена, 0))
		|			КОГДА СУММА(Данные.Количество) = 0
		|				ТОГДА 0
		|			ИНАЧЕ СУММА(Данные.Стоимость) / СУММА(Данные.Количество)
		|		КОНЕЦ КАК Цена,
		|		СУММА(Данные.Забалансовая) КАК СтоимостьЗабалансовая,
		|		Данные.Полуфабрикат КАК Полуфабрикат,
		|		Данные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|		Данные.Назначение КАК НазначениеПродукции,
		|		Данные.Порядок КАК Порядок,
		|		0 КАК КоличествоВыпуска,
		|		Данные.КлючПартия КАК КлючПартия
		|	ИЗ
		|		Данные КАК Данные
		|			ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПолуфабрикатов КАК Цены
		|			ПО Данные.ОбъектКалькуляции = Цены.ОбъектКалькуляции
		|				И Данные.Калькуляция = Цены.Калькуляция
		|				И Данные.Продукция = Цены.Продукция
		|				И Данные.ХарактеристикаПродукции = Цены.ХарактеристикаПродукции
		|				И Данные.ПартияСпецификацияПродукции = Цены.ПартияПродукции
		|				И Данные.Номенклатура = Цены.Полуфабрикат
		|				И Данные.Характеристика = Цены.ХарактеристикаПолуфабриката
		|				И Данные.Назначение = Цены.НазначениеПродукции
		|	
		|	СГРУППИРОВАТЬ ПО
		|		Данные.ЭтоПродукция,
		|		Данные.ОбъектКалькуляции,
		|		Данные.Характеристика,
		|		Данные.Калькуляция,
		|		Данные.Продукция,
		|		Данные.Номенклатура,
		|		Данные.Подразделение,
		|		Данные.ХарактеристикаПродукции,
		|		Данные.ТипЗатрат,
		|		Данные.СтатьяКалькуляции,
		|		Данные.ЭтоПолуфабрикат,
		|		Данные.Полуфабрикат,
		|		Данные.ХарактеристикаПолуфабриката,
		|		Данные.Назначение,
		|		Данные.Калькуляция.Организация,
		|		Данные.Порядок,
		|		Данные.КлючПартия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ФактическиеЗатраты.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|		НЕ ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL,
		|		ФактическиеЗатраты.Калькуляция.Организация,
		|		ФактическиеЗатраты.ОбъектКалькуляции,
		|		ФактическиеЗатраты.Калькуляция,
		|		ФактическиеЗатраты.Продукция,
		|		ФактическиеЗатраты.ХарактеристикаПродукции,
		|		ФактическиеЗатраты.Подразделение,
		|		ФактическиеЗатраты.СтатьяКалькуляции,
		|		ФактическиеЗатраты.Номенклатура,
		|		ФактическиеЗатраты.Характеристика,
		|		ФактическиеЗатраты.ТипЗатрат,
		|		ФактическиеЗатраты.Количество,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|				ТОГДА 0
		|			ИНАЧЕ ОпределениеПолуфабрикатов.Стоимость * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|					И ФактическиеЗатраты.ТипЗатрат = ""Материальные""
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|				ТОГДА 0
		|			КОГДА ФактическиеЗатраты.ТипЗатрат = ""Материальные""
		|				ТОГДА ОпределениеПолуфабрикатов.СтоимостьМатериальные * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|					И ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|					И ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
		|				ТОГДА 0
		|			КОГДА ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
		|				ТОГДА ОпределениеПолуфабрикатов.СтоимостьТрудозатраты * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|					И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|					И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
		|				ТОГДА 0
		|			КОГДА ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
		|				ТОГДА ОпределениеПолуфабрикатов.СтоимостьПрочиеПостоянные * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|					И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
		|				ТОГДА ФактическиеЗатраты.Стоимость
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|					И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
		|				ТОГДА 0
		|			КОГДА ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
		|				ТОГДА ОпределениеПолуфабрикатов.СтоимостьПрочиеПеременные * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ВЫБОР
		|						КОГДА ФактическиеЗатраты.Количество = 0
		|							ТОГДА 0
		|						ИНАЧЕ ФактическиеЗатраты.Стоимость / ФактическиеЗатраты.Количество
		|					КОНЕЦ
		|			ИНАЧЕ ОпределениеПолуфабрикатов.Стоимость * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество / ФактическиеЗатраты.Количество
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
		|				ТОГДА ФактическиеЗатраты.Забалансовая
		|			КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
		|				ТОГДА 0
		|			ИНАЧЕ ОпределениеПолуфабрикатов.СтоимостьЗабалансовая * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
		|		КОНЕЦ,
		|		ФактическиеЗатраты.Полуфабрикат,
		|		ФактическиеЗатраты.ХарактеристикаПолуфабриката,
		|		ФактическиеЗатраты.Назначение,
		|		ФактическиеЗатраты.Порядок,
		|		0,
		|		ЕСТЬNULL(ОпределениеПолуфабрикатов.КлючПартия, ФактическиеЗатраты.КлючПартия)
		|	ИЗ
		|		ФактическиеЗатраты КАК ФактическиеЗатраты
		|			ЛЕВОЕ СОЕДИНЕНИЕ ОпределениеПолуфабрикатов КАК ОпределениеПолуфабрикатов
		|			ПО ФактическиеЗатраты.ОбъектКалькуляции = ОпределениеПолуфабрикатов.ОбъектКалькуляции
		|				И ФактическиеЗатраты.АналитикаУчетаНоменклатуры = ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры
		|				И (ВЫБОР
		|					КОГДА ФактическиеЗатраты.ЭтоПродукция
		|						ТОГДА ФактическиеЗатраты.ПартияСпецификацияПродукции = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
		|					ИНАЧЕ ФактическиеЗатраты.ПартияПолуфабриката = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
		|				КОНЕЦ)
		|				И (ОпределениеПолуфабрикатов.КлючПартия = ФактическиеЗатраты.КлючПартия)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|		МножителиПартийИПолуфабрикатов.Полуфабрикат <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка),
		|		ДолиСтоимостиОтРассчитанногоКоличества.ОбъектКалькуляции,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Продукция,
		|		ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции,
		|		ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|		ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка),
		|		ВЫРАЗИТЬ("""" КАК СТРОКА(16)),
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ПродукцияПолуфабрикат.Ссылка,
		|		ВЫБОР
		|			КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				ТОГДА МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			ИНАЧЕ МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
		|		КОНЕЦ,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Назначение,
		|																			  
		|		0,
		|		СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката * ДолиСтоимостиОтРассчитанногоКоличества.ДоляСтоимости),
		|		МножителиПартийИПолуфабрикатов.КлючПартия
		|	ИЗ
		|		ДолиСтоимостиОтРассчитанногоКоличества КАК ДолиСтоимостиОтРассчитанногоКоличества
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|			ПО ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|				И ДолиСтоимостиОтРассчитанногоКоличества.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|				И ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|				И ДолиСтоимостиОтРассчитанногоКоличества.Назначение = МножителиПартийИПолуфабрикатов.Назначение
		|				
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ПродукцияПолуфабрикат
		|			ПО (ПродукцияПолуфабрикат.Ссылка = ВЫБОР
		|					КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|						ТОГДА МножителиПартийИПолуфабрикатов.Продукция
		|					ИНАЧЕ МножителиПартийИПолуфабрикатов.Полуфабрикат
		|				КОНЕЦ)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
		|		ДолиСтоимостиОтРассчитанногоКоличества.ОбъектКалькуляции,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Продукция,
		|		ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции,
		|		ПродукцияПолуфабрикат.Ссылка,
		|		ВЫБОР
		|			КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|				ТОГДА МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			ИНАЧЕ МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
		|		КОНЕЦ,
		|		ДолиСтоимостиОтРассчитанногоКоличества.Назначение,
		|		МножителиПартийИПолуфабрикатов.КлючПартия,
		|		МножителиПартийИПолуфабрикатов.Полуфабрикат <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК ВЗ
		|
		|СГРУППИРОВАТЬ ПО
		|	ВЗ.ЭтоПродукция,
		|	ВЗ.Организация,
		|	ВЗ.ОбъектКалькуляции,
		|	ВЗ.Калькуляция,
		|	ВЗ.Продукция,
		|	ВЗ.ХарактеристикаПродукции,
		|	ВЗ.Подразделение,
		|	ВЗ.СтатьяКалькуляции,
		|	ВЗ.Номенклатура,
		|	ВЗ.Характеристика,
		|	ВЗ.ТипЗатрат,
		|	ВЗ.ЭтоПолуфабрикат,
		|	ВЗ.Полуфабрикат,
		|	ВЗ.ХарактеристикаПолуфабриката,
		|	ВЗ.НазначениеПродукции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции,
		|	Калькуляция,
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Номенклатура,
		|	Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Данные
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныПолуфабрикатов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ФактическиеЗатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОпределениеПолуфабрикатов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДолиСтоимостиОтРассчитанногоКоличества
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1316134911
		|	АВТОНОМЕРЗАПИСИ() КАК ИндексПК,
		|	ДД.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ДД.Калькуляция.Дата КАК ДатаПК,
		|	ДД.Калькуляция КАК Калькуляция
		|ПОМЕСТИТЬ ИндексацияПК
		|ИЗ
		|	РассчитанныеЗатраты КАК ДД
		|
		|УПОРЯДОЧИТЬ ПО
		|	ОбъектКалькуляции,
		|	ДатаПК
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции,
		|	Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПорядокПК.ИндексПК КАК ИндексПК,
		|	МАКСИМУМ(ЕСТЬNULL(ДД.ИндексПК, 0)) КАК ПредыдущийИндексПК,
		|	ПорядокПК.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ПорядокПК.Калькуляция КАК Калькуляция,
		|	ПорядокПК.ДатаПК КАК ДатаПК,
		|	ДД.ИндексПК ЕСТЬ NULL КАК ЭтоПерваяПК
		|ПОМЕСТИТЬ ПорядокПК_Предварительная
		|ИЗ
		|	ИндексацияПК КАК ПорядокПК
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИндексацияПК КАК ДД
		|		ПО ПорядокПК.ОбъектКалькуляции = ДД.ОбъектКалькуляции
		|			И ПорядокПК.ИндексПК > ДД.ИндексПК
		|
		|СГРУППИРОВАТЬ ПО
		|	ПорядокПК.ИндексПК,
		|	ПорядокПК.ОбъектКалькуляции,
		|	ПорядокПК.Калькуляция,
		|	ПорядокПК.ДатаПК,
		|	ДД.ИндексПК ЕСТЬ NULL
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции,
		|	Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Данная ВТ необходима для определения верного расположения ПК на оси времени.
		|ВЫБРАТЬ
		|	ПорядокПК.ИндексПК КАК ИндексПК,
		|	ПорядокПК.ПредыдущийИндексПК КАК ПредыдущийИндексПК,
		|	ДД.Калькуляция КАК ПредыдущаяКалькуляция,
		|	ПорядокПК.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ПорядокПК.Калькуляция КАК Калькуляция,
		|	ПорядокПК.ДатаПК КАК ДатаПК,
		|	ПорядокПК.ЭтоПерваяПК КАК ЭтоПерваяПК
		|ПОМЕСТИТЬ ПорядокПК
		|ИЗ
		|	ПорядокПК_Предварительная КАК ПорядокПК
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИндексацияПК КАК ДД
		|		ПО ПорядокПК.ПредыдущийИндексПК = ДД.ИндексПК
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПорядокПК_Предварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(ДД.ИндексПК) КАК ИндексПоследнейПК,
		|	ДД.ОбъектКалькуляции КАК ОбъектКалькуляции
		|ПОМЕСТИТЬ ИндексПоследнихПК
		|ИЗ
		|	ИндексацияПК КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.ОбъектКалькуляции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции,
		|	ИндексПоследнейПК
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ИндексацияПК
		|;
		|
		|//////////////////////////////////////////////////////////////////////////////// 
		// Данная ВТ необходима для определения всех аналитик, используемых во всех ПК, по каждому объекту калькуляции.
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДД.ЭтоПродукция КАК ЭтоПродукция,
		|	ДД.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ДД.Организация КАК Организация,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ДД.ТипЗатрат КАК ТипЗатрат,
		|	ДД.Продукция КАК Продукция,
		|	ДД.Продукция.ЕдиницаИзмерения КАК ПродукцияЕдиницаИзмерения,
		|	ДД.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДД.НазначениеПродукции КАК НазначениеПродукции,
		|	ДД.Полуфабрикат КАК Полуфабрикат,
		|	ДД.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ДД.Полуфабрикат.ЕдиницаИзмерения КАК ПолуфабрикатЕдиницаИзмерения,
		|	ДД.Номенклатура КАК Номенклатура,
		|	ДД.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|	ДД.Характеристика КАК Характеристика
		|ПОМЕСТИТЬ АналитикиЗатрат
		|ИЗ
		|	РассчитанныеЗатраты КАК ДД
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Данная ВТ необходима для того, чтобы у каждой ПК были все аналитики, которые когда-либо использовались для каждого объекта калькуляции.
		// В случае если в отдельной ПК затраты не было, все ресурсы устанавливаются в 0. 
		// Это необходимо далее для верного расчета разниц между ПК.
		|ВЫБРАТЬ
		|	АналитикиЗатрат.Организация КАК Организация,
		|	ПорядокПК.Калькуляция КАК Калькуляция,
		|	ПорядокПК.ДатаПК КАК ДатаПК,
		|	АналитикиЗатрат.ЭтоПродукция КАК ЭтоПродукция,
		|	АналитикиЗатрат.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	АналитикиЗатрат.Подразделение КАК Подразделение,
		|	АналитикиЗатрат.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	АналитикиЗатрат.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	АналитикиЗатрат.ТипЗатрат КАК ТипЗатрат,
		|
		|	АналитикиЗатрат.Продукция КАК Продукция,
		|	АналитикиЗатрат.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	АналитикиЗатрат.ПродукцияЕдиницаИзмерения КАК ПродукцияЕдиницаИзмерения,
		|	АналитикиЗатрат.НазначениеПродукции КАК НазначениеПродукции,
		|
		|	АналитикиЗатрат.Полуфабрикат КАК Полуфабрикат,
		|	АналитикиЗатрат.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	АналитикиЗатрат.ПолуфабрикатЕдиницаИзмерения КАК ПолуфабрикатЕдиницаИзмерения,
		|
		|	АналитикиЗатрат.Номенклатура КАК Номенклатура,
		|	АналитикиЗатрат.Характеристика КАК Характеристика,
		|	АналитикиЗатрат.НоменклатураЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|
		|	ПорядокПК.ЭтоПерваяПК КАК ЭтоПерваяПК,
		|	ПорядокПК.ИндексПК КАК ИндексПК,
		|	ПорядокПК.ПредыдущаяКалькуляция КАК ПредыдущаяКалькуляция,
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			ИндексПоследнихПК КАК ИндексПоследнихПК
		|		ГДЕ
		|			АналитикиЗатрат.ОбъектКалькуляции = ИндексПоследнихПК.ОбъектКалькуляции
		|			И ПорядокПК.ИндексПК = ИндексПоследнихПК.ИндексПоследнейПК) КАК ЭтоПоследняяПК,
		|
		|	ЕСТЬNULL(ДД.КоличествоВыпуска, 0) КАК КоличествоВыпуска,
		|	ЕСТЬNULL(ДД.Количество, 0) КАК Количество,
		|	ЕСТЬNULL(ДД.Цена, 0) КАК Цена,
		|	ЕСТЬNULL(ДД.Стоимость, 0) КАК Стоимость,
		|	ЕСТЬNULL(ДД.СтоимостьМатериальные, 0) КАК СтоимостьМатериальные,
		|	ЕСТЬNULL(ДД.СтоимостьТрудозатраты, 0) КАК СтоимостьТрудозатраты,
		|	ЕСТЬNULL(ДД.СтоимостьПрочиеПостоянные, 0) КАК СтоимостьПрочиеПостоянные,
		|	ЕСТЬNULL(ДД.СтоимостьПрочиеПеременные, 0) КАК СтоимостьПрочиеПеременные,
		|	ЕСТЬNULL(ДД.СтоимостьЗабалансовая, 0) КАК СтоимостьЗабалансовая,
		|	ДД.ОбъектКалькуляции ЕСТЬ NULL КАК ВРамкахПКЗатратыНеБыло
		| 
		|ПОМЕСТИТЬ РассчитанныеЗатратыДополненная
		|ИЗ
		|	АналитикиЗатрат КАК АналитикиЗатрат
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПорядокПК КАК ПорядокПК
		|		ПО АналитикиЗатрат.ОбъектКалькуляции = ПорядокПК.ОбъектКалькуляции
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РассчитанныеЗатраты КАК ДД
		|		ПО (ДД.ОбъектКалькуляции = АналитикиЗатрат.ОбъектКалькуляции)
		|			И (ДД.Калькуляция = ПорядокПК.Калькуляция)
		|			И (ДД.Продукция = АналитикиЗатрат.Продукция)
		|			И (ДД.ХарактеристикаПродукции = АналитикиЗатрат.ХарактеристикаПродукции)
		|			И (ДД.НазначениеПродукции = АналитикиЗатрат.НазначениеПродукции)
		|			И (ДД.Полуфабрикат = АналитикиЗатрат.Полуфабрикат)
		|			И (ДД.ХарактеристикаПолуфабриката = АналитикиЗатрат.ХарактеристикаПолуфабриката)
		|			И (ДД.Номенклатура = АналитикиЗатрат.Номенклатура)
		|			И (ДД.Характеристика = АналитикиЗатрат.Характеристика)
		|			И (ДД.ЭтоПолуфабрикат = АналитикиЗатрат.ЭтоПолуфабрикат)
		|			И (ДД.ЭтоПродукция ИЛИ ДД.ЭтоПолуфабрикат ИЛИ ДД.ТипЗатрат = АналитикиЗатрат.ТипЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЭтоПолуфабрикат,
		|	ОбъектКалькуляции,
		|	Калькуляция,
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Номенклатура,
		|	Характеристика,
		|	ЭтоПродукция,
		|	ТипЗатрат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ АналитикиЗатрат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПорядокПК
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Данная ВТ необходима для расчета коэффициента - Количество затраты / Количество (ПФ/Продукция)
		// Это необходимо далее для верного определения причины изменения текущей затраты - изменение количества потребляющего верхнеуровневого ПФ/Продукции 
		// или для 1 ед. ПФ/Продукции изменилось количество затраты.
		|ВЫБРАТЬ
		|	Затраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	Затраты.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.Продукция КАК Продукция,
		|	Затраты.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Затраты.НазначениеПродукции КАК НазначениеПродукции,
		|	Затраты.Полуфабрикат КАК Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Затраты.Номенклатура КАК Номенклатура,
		|	Затраты.ТипЗатрат КАК ТипЗатрат,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.Количество КАК Количество,
		|	ПФИПродукция.КоличествоВыпуска КАК ВерхнеуровневоеКоличество,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ПФИПродукция.КоличествоВыпуска = 0
		|				ТОГДА 0
		|			
		|			ИНАЧЕ Затраты.Количество / ПФИПродукция.КоличествоВыпуска
		|		КОНЕЦ КАК ЧИСЛО(31, 6)) КАК Коэффициент
		| 
		|ПОМЕСТИТЬ КоэффициентыОтношенияЗатратКПФИПродукции
		|ИЗ
		|	РассчитанныеЗатратыДополненная КАК Затраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РассчитанныеЗатратыДополненная КАК ПФИПродукция
		|		ПО (НЕ Затраты.ЭтоПродукция)
		|			И (НЕ Затраты.ЭтоПолуфабрикат)
		|			И (ПФИПродукция.ЭтоПродукция)
		|			И Затраты.ОбъектКалькуляции = ПФИПродукция.ОбъектКалькуляции
		|			И Затраты.Калькуляция = ПФИПродукция.Калькуляция
		|			И Затраты.Продукция = ПФИПродукция.Продукция
		|			И Затраты.ХарактеристикаПродукции = ПФИПродукция.ХарактеристикаПродукции
		|			И Затраты.НазначениеПродукции = ПФИПродукция.НазначениеПродукции
		|			И Затраты.Полуфабрикат = ПФИПродукция.Полуфабрикат
		|			И Затраты.ХарактеристикаПолуфабриката = ПФИПродукция.ХарактеристикаПолуфабриката
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Затраты.ЭтоПолуфабрикат,
		|	Затраты.ОбъектКалькуляции,
		|	Затраты.Калькуляция,
		|	Затраты.Продукция,
		|	Затраты.ХарактеристикаПродукции,
		|	Затраты.НазначениеПродукции,
		|	Затраты.Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката,
		|	Затраты.Номенклатура,
		|	Затраты.ТипЗатрат КАК ТипЗатрат,
		|	Затраты.Характеристика,
		|	Затраты.Количество,
		|	ПФИПродукция.КоличествоВыпуска,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ПФИПродукция.КоличествоВыпуска = 0
		|				ТОГДА 0
		|			
		|			ИНАЧЕ Затраты.Количество / ПФИПродукция.КоличествоВыпуска
		|		КОНЕЦ КАК ЧИСЛО(31, 6))
		|ИЗ
		|	РассчитанныеЗатратыДополненная КАК Затраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РассчитанныеЗатратыДополненная КАК ПФИПродукция
		|		ПО (НЕ Затраты.ЭтоПродукция)
		|			И (НЕ Затраты.ЭтоПолуфабрикат)
		|			И (ПФИПродукция.ЭтоПолуфабрикат)  
		|			И ПФИПродукция.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			И Затраты.ОбъектКалькуляции = ПФИПродукция.ОбъектКалькуляции
		|			И Затраты.Калькуляция = ПФИПродукция.Калькуляция
		|			И Затраты.Продукция = ПФИПродукция.Продукция
		|			И Затраты.ХарактеристикаПродукции = ПФИПродукция.ХарактеристикаПродукции
		|			И Затраты.НазначениеПродукции = ПФИПродукция.НазначениеПродукции
		|			И Затраты.Полуфабрикат = ПФИПродукция.Полуфабрикат
		|			И Затраты.ХарактеристикаПолуфабриката = ПФИПродукция.ХарактеристикаПолуфабриката
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Затраты.ЭтоПолуфабрикат,
		|	Затраты.ОбъектКалькуляции,
		|	Затраты.Калькуляция,
		|	Затраты.Продукция,
		|	Затраты.ХарактеристикаПродукции,
		|	Затраты.НазначениеПродукции,
		|	Затраты.Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката,
		|	Затраты.Номенклатура,
		|	Затраты.ТипЗатрат КАК ТипЗатрат,
		|	Затраты.Характеристика,
		|	Затраты.Количество,
		|	ПФИПродукция.КоличествоВыпуска,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ПФИПродукция.КоличествоВыпуска = 0
		|				ТОГДА 0
		|			
		|			ИНАЧЕ Затраты.КоличествоВыпуска / ПФИПродукция.КоличествоВыпуска
		|		КОНЕЦ КАК ЧИСЛО(31, 6))
		|
		|ИЗ
		|	РассчитанныеЗатратыДополненная КАК Затраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РассчитанныеЗатратыДополненная КАК ПФИПродукция
		|		ПО (Затраты.ЭтоПолуфабрикат)
		|			И (ПФИПродукция.ЭтоПродукция)
		|			И Затраты.ОбъектКалькуляции = ПФИПродукция.ОбъектКалькуляции
		|			И Затраты.Калькуляция = ПФИПродукция.Калькуляция
		|			И Затраты.Продукция = ПФИПродукция.Продукция
		|			И Затраты.ХарактеристикаПродукции = ПФИПродукция.ХарактеристикаПродукции
		|			И Затраты.НазначениеПродукции = ПФИПродукция.НазначениеПродукции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЭтоПолуфабрикат,
		|	ОбъектКалькуляции,
		|	Калькуляция,
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Номенклатура,
		|	Характеристика,
		|	ТипЗатрат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// В данной ВТ присоединяем рассчитанные выше коэффициенты.
		// Это необходимо далее чтобы и у текущей затраты были коэффициенты и у предыдущей. 
		|ВЫБРАТЬ
		|	ДД.ЭтоПерваяПК КАК ЭтоПерваяПК,
		|	ДД.ИндексПК КАК ИндексПК,
		|	ДД.ЭтоПоследняяПК КАК ЭтоПоследняяПК,
		|	ДД.Организация КАК Организация,
		|	ДД.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ДД.Калькуляция КАК Калькуляция,
		|	ДД.ПредыдущаяКалькуляция КАК ПредыдущаяКалькуляция,
		|	ДД.ДатаПК КАК ДатаПК,
		|	ДД.Продукция КАК Продукция,
		|	ДД.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДД.ЭтоПродукция КАК ЭтоПродукция,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ДД.Номенклатура КАК Номенклатура,
		|	ДД.Характеристика КАК Характеристика,
		|	ДД.ТипЗатрат КАК ТипЗатрат,
		|	ДД.ПродукцияЕдиницаИзмерения КАК ПродукцияЕдиницаИзмерения,
		|	ДД.НоменклатураЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|	ДД.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ДД.Полуфабрикат КАК Полуфабрикат,
		|	ДД.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ДД.НазначениеПродукции КАК НазначениеПродукции,
		|	ДД.ПолуфабрикатЕдиницаИзмерения КАК ПолуфабрикатЕдиницаИзмерения,
		|	ДД.КоличествоВыпуска КАК КоличествоВыпуска,
		|	ДД.Количество КАК Количество,
		|	ДД.Цена КАК Цена,
		|	ДД.Стоимость КАК Стоимость,
		|	ДД.СтоимостьМатериальные КАК СтоимостьМатериальные,
		|	ДД.СтоимостьТрудозатраты КАК СтоимостьТрудозатраты,
		|	ДД.СтоимостьПрочиеПостоянные КАК СтоимостьПрочиеПостоянные,
		|	ДД.СтоимостьПрочиеПеременные КАК СтоимостьПрочиеПеременные,
		|	ДД.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	ДД.ВРамкахПКЗатратыНеБыло КАК ВРамкахПКЗатратыНеБыло,
		|	ЕСТЬNULL(ОЗПИП.Коэффициент, 0) КАК КоэффициентКВерхнеуровневомуКоличеству,
		|	ЕСТЬNULL(ОЗПИП.ВерхнеуровневоеКоличество, 0) КАК ВерхнеуровневоеКоличество
		|ПОМЕСТИТЬ РассчитанныеЗатратыСКоэффициентами
		|ИЗ
		|	РассчитанныеЗатратыДополненная КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыОтношенияЗатратКПФИПродукции КАК ОЗПИП
		|		ПО (НЕ ДД.ЭтоПродукция)
		|			И ДД.ЭтоПолуфабрикат = ОЗПИП.ЭтоПолуфабрикат
		|			И ДД.ОбъектКалькуляции = ОЗПИП.ОбъектКалькуляции
		|			И ДД.Калькуляция = ОЗПИП.Калькуляция
		|			И ДД.Продукция = ОЗПИП.Продукция
		|			И ДД.ХарактеристикаПродукции = ОЗПИП.ХарактеристикаПродукции
		|			И ДД.НазначениеПродукции = ОЗПИП.НазначениеПродукции
		|			И ДД.Полуфабрикат = ОЗПИП.Полуфабрикат
		|			И ДД.ХарактеристикаПолуфабриката = ОЗПИП.ХарактеристикаПолуфабриката
		|			И ДД.Номенклатура = ОЗПИП.Номенклатура
		|			И ДД.Характеристика = ОЗПИП.Характеристика
		|			И (ДД.ЭтоПродукция ИЛИ ДД.ЭтоПолуфабрикат ИЛИ ДД.ТипЗатрат = ОЗПИП.ТипЗатрат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ОбъектКалькуляции,
		|	ПредыдущаяКалькуляция,
		|	Продукция,
		|	ХарактеристикаПродукции,
		|	НазначениеПродукции,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Номенклатура,
		|	Характеристика,
		|	ЭтоПерваяПК,
		|	ТипЗатрат
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ КоэффициентыОтношенияЗатратКПФИПродукции
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РассчитанныеЗатраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РассчитанныеЗатратыДополненная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		// Финальная таблица, где мы присоединяем к каждой ПК, если она не первая, предыдущую ПК.
		// Рассчитываем разницы по ресурсам затрат и рассчитываем разницу по коэффициентам затрат. 
		|ВЫБРАТЬ
		|	ДД.ЭтоПродукция КАК ЭтоПродукция,
		|	ДД.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ДД.ЭтоПерваяПК КАК ЭтоПерваяПК,
		|	ДД.ИндексПК КАК ИндексПК,
		|	ДД.ЭтоПоследняяПК КАК ЭтоПоследняяПК,
		|	ДД.Организация КАК Организация,
		|	ДД.ОбъектКалькуляции КАК ОбъектКалькуляции,
		|	ДД.Калькуляция КАК Калькуляция,
		|	ДД.ДатаПК КАК ДатаПК,
		|	ДД.Продукция КАК Продукция,
		|	ДД.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ДД.Номенклатура КАК Номенклатура,
		|	ДД.Характеристика КАК Характеристика,
		|	ДД.ТипЗатрат КАК ТипЗатрат,
		|	ДД.ТипЗатрат = ""ПрочиеПостоянные"" ИЛИ ДД.ТипЗатрат = ""ПрочиеПеременные"" КАК ЭтоПрочиеЗатраты,
		|	ДД.ПродукцияЕдиницаИзмерения КАК ПродукцияЕдиницаИзмерения,
		|	ДД.НоменклатураЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
		|	ДД.Полуфабрикат КАК Полуфабрикат,
		|	ДД.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ДД.НазначениеПродукции КАК НазначениеПродукции,
		|	ДД.ПолуфабрикатЕдиницаИзмерения КАК ПолуфабрикатЕдиницаИзмерения,
		|	ДД.КоличествоВыпуска КАК КоличествоВыпуска,
		|	ДД.Количество КАК Количество,
		|	ДД.Цена КАК Цена,
		|	ДД.Стоимость КАК Стоимость,
		|	ДД.СтоимостьМатериальные КАК СтоимостьМатериальные,
		|	ДД.СтоимостьТрудозатраты КАК СтоимостьТрудозатраты,
		|	ДД.СтоимостьПрочиеПостоянные КАК СтоимостьПрочиеПостоянные,
		|	ДД.СтоимостьПрочиеПеременные КАК СтоимостьПрочиеПеременные,
		|	ДД.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	
		|	ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Количество, 0) КАК ПредыдущееКоличество,
		|	ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоличествоВыпуска, 0) КАК ПредыдущееКоличествоВыпуска,
		|	ДД.ВРамкахПКЗатратыНеБыло КАК ВРамкахПКЗатратыНеБыло,
		|
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ДД.ЭтоПерваяПК
		|				ТОГДА 0
		|			ИНАЧЕ ДД.КоличествоВыпуска - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоличествоВыпуска, 0)
		|		КОНЕЦ КАК ЧИСЛО(15, 3)) КАК РазницаКоличествоВыпуска,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ДД.ЭтоПерваяПК
		|				ТОГДА 0
		|			ИНАЧЕ ДД.Количество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Количество, 0)
		|		КОНЕЦ КАК ЧИСЛО(15, 3)) КАК РазницаКоличество,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ДД.ЭтоПерваяПК
		|				ТОГДА 0
		|			ИНАЧЕ ДД.Цена - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Цена, 0)
		|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК РазницаЦена,
		|	ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА ДД.ЭтоПерваяПК
		|				ТОГДА 0
		|			ИНАЧЕ ДД.Стоимость - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Стоимость, 0)
		|		КОНЕЦ КАК ЧИСЛО(31, 2)) КАК РазницаСтоимость,
		|		
		|	ВЫБОР
		|		КОГДА ДД.ЭтоПерваяПК
		|			ТОГДА 0
		|		КОГДА НЕ ДД.ЭтоПродукция И ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0) = 0
					// До тек. калькуляции не было верхнеуровневого узла, при появлении отмечаем изменение только верхнеуровневого узла.  
		|			ТОГДА 0		
		|		КОГДА ДД.ЭтоПолуфабрикат ИЛИ ДД.ЭтоПродукция
		|										
		|			ТОГДА   
						// Определяем было ли изменение в верхнеуровневом количестве.
		|				ВЫБОР КОГДА ДД.ВерхнеуровневоеКоличество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0) = 0
		|					ТОГДА 
								// Если в верхнеуровневом количестве изменений не было тогда разница на тек. уровне.
		|						ДД.КоличествоВыпуска - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоличествоВыпуска, 0)
		|					ИНАЧЕ    
								// Может измениться и верхнеуровневое количество и количество тек. затраты.
								// Для того, чтобы верно рассчитать изменилось ли количество затраты для 1 ед. верхнеуровневого ПФ/Продукции
								// считаем коэффициент разницы = (тек. количество затраты - предыдущее) / (тек. верхнеуровневое кол-во ПФ/Продукции - предыдущее).
		|						ВЫРАЗИТЬ(
		|							(ДД.КоличествоВыпуска - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоличествоВыпуска, 0)) / (ДД.ВерхнеуровневоеКоличество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0))
		|						КАК ЧИСЛО(31, 6))   
								// и из текущего коэффициента разниц вычитаем предыдущий коэффициент.
		|				КОНЕЦ -  ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоэффициентКВерхнеуровневомуКоличеству, 0)			  
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК РазницаКоэффициентовПолуфабрикат,   
		|	
		|	ВЫБОР
		|		КОГДА ДД.ЭтоПерваяПК
		|			ТОГДА 0
		|
		|		КОГДА ДД.ЭтоПродукция ИЛИ ДД.ЭтоПолуфабрикат 
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0) = 0
					// До тек. калькуляции не было верхнеуровневого узла, при появлении отмечаем изменение только верхнеуровневого узла.
		|			ТОГДА 0
		|		ИНАЧЕ
					// Логика расчета такая же как написано выше, с одним отличием - количество затраты по номенклатуре в ресурсе - Количество.
		|			ВЫБОР КОГДА ДД.ВерхнеуровневоеКоличество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0) = 0
		|				ТОГДА 
		|					ДД.Количество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Количество, 0)
		|				ИНАЧЕ
		|					ВЫРАЗИТЬ(
		|						(ДД.Количество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.Количество, 0)) / (ДД.ВерхнеуровневоеКоличество - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.ВерхнеуровневоеКоличество, 0))
		|					КАК ЧИСЛО(31, 6))
		|			КОНЕЦ - ЕСТЬNULL(ПредыдущиеРассчитанныеЗатраты.КоэффициентКВерхнеуровневомуКоличеству, 0)
		|	КОНЕЦ КАК РазницаКоэффициентовНоменклатура
		|	
		|ИЗ
		|	РассчитанныеЗатратыСКоэффициентами КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ РассчитанныеЗатратыСКоэффициентами КАК ПредыдущиеРассчитанныеЗатраты
		|		ПО (НЕ ДД.ЭтоПерваяПК)
		|			И ДД.ОбъектКалькуляции = ПредыдущиеРассчитанныеЗатраты.ОбъектКалькуляции
		|			И ДД.ПредыдущаяКалькуляция = ПредыдущиеРассчитанныеЗатраты.Калькуляция
		|			И ДД.Продукция = ПредыдущиеРассчитанныеЗатраты.Продукция
		|			И ДД.ХарактеристикаПродукции = ПредыдущиеРассчитанныеЗатраты.ХарактеристикаПродукции
		|			И ДД.НазначениеПродукции = ПредыдущиеРассчитанныеЗатраты.НазначениеПродукции
		|			И ДД.Полуфабрикат = ПредыдущиеРассчитанныеЗатраты.Полуфабрикат
		|			И ДД.ХарактеристикаПолуфабриката = ПредыдущиеРассчитанныеЗатраты.ХарактеристикаПолуфабриката
		|			И ДД.Номенклатура = ПредыдущиеРассчитанныеЗатраты.Номенклатура
		|			И ДД.Характеристика = ПредыдущиеРассчитанныеЗатраты.Характеристика
		|			И ДД.ЭтоПолуфабрикат = ПредыдущиеРассчитанныеЗатраты.ЭтоПолуфабрикат
		|			И (ДД.ЭтоПродукция ИЛИ ДД.ЭтоПолуфабрикат ИЛИ ДД.ТипЗатрат = ПредыдущиеРассчитанныеЗатраты.ТипЗатрат)
		|";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	Результат = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли