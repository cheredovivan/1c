#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СравнениеПлановыхКалькуляций);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СравнениеПлановыхКалькуляций");
	ОписаниеВарианта.Описание = НСтр("ru = 'Какова разница в плановых затратах по выпускаемой продукции?';
									|en = 'What is the difference in planned costs for the released products?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьУправлениеПроизводством2_2");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета,
		"СравнениеПлановыхКалькуляцийПоСтатьямКалькуляций");
	ОписаниеВарианта.Описание = НСтр("ru = 'Какова разница в плановых затратах, по статьям калькуляций, по выпускаемой продукции?';
									|en = 'What is the difference in planned costs, by product cost elements, and by released products?'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьУправлениеПроизводством2_2");
	
КонецПроцедуры

#КонецОбласти

#Область КомандыПодменюОтчеты
// Добавляет команду отчета в список команд.
// 
// Параметры:
//  КомандыОтчетов - ТаблицаЗначений - описание в ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений - добавленная команда - из:
//    *Менеджер - Строка -
//    *Представление - Строка -
//    *МножественныйВыбор - Булево - 
//    *Важность - Строка -
//    *КлючВарианта - Строка -
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СравнениеПлановыхКалькуляций) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор 			= Метаданные.Отчеты.СравнениеПлановыхКалькуляций.ПолноеИмя();
		КомандаОтчет.Менеджер				= "Отчет.СравнениеПлановыхКалькуляций";
		КомандаОтчет.Представление			= НСтр("ru = 'Сравнение плановых калькуляций';
													|en = 'Product standard cost comparison'");
		КомандаОтчет.МножественныйВыбор		= Ложь;
		КомандаОтчет.Важность				= "Обычное";
		КомандаОтчет.ФункциональныеОпции	= "ИспользоватьУправлениеПроизводством2_2";
		КомандаОтчет.РежимЗаписи			= "Проводить";
		КомандаОтчет.КлючВарианта			= "СравнениеПлановыхКалькуляций";
		КомандаОтчет.Неконтекстный			= Истина;
		КомандаОтчет.ИмяПараметраФормы		= "ОбъектКалькуляцииОтбор";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
