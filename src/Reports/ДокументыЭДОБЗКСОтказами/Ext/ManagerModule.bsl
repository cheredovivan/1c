#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыСОтказамиПодписания");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает документы ЭДО с которыми сотрудники не согласны';
										|en = 'The report shows EDI documents that employees do not agree with'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьКомандыФормированияОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ДокументыЭДОБЗКСОтказами) Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'С отказами подписания';
										|en = 'With refusals to sign'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыЭДОБЗКСОтказами";
	Команда.КлючВарианта       = "ДокументыСОтказамиПодписания";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыЭДОБЗКСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
КонецПроцедуры

Функция ВнешниеНаборыДанных() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КомментарииФайловДокументовЭДОБЗК.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(КомментарииФайловДокументовЭДОБЗК.Исполнитель) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА КомментарииФайловДокументовЭДОБЗК.Исполнитель
		|		ИНАЧЕ КомментарииФайловДокументовЭДОБЗК.Исполнитель.ФизическоеЛицо
		|	КОНЕЦ КАК Исполнитель,
		|	КомментарииФайловДокументовЭДОБЗК.Комментарий КАК Комментарий,
		|	КомментарииФайловДокументовЭДОБЗК.ДатаКомментария КАК ДатаКомментария
		|ИЗ
		|	РегистрСведений.КомментарииФайловДокументовЭДОБЗК КАК КомментарииФайловДокументовЭДОБЗК";
	
	УстановитьПривилегированныйРежим(Истина);
	ВнешниеНаборы = Новый Структура("КомментарииЭлектронныхДокументов",  Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВнешниеНаборы
	
КонецФункции

#КонецОбласти
	
#КонецЕсли