// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СоставРетроБонусовКлиентов);
	НастройкиОтчета.ФункциональныеОпции.Добавить(Метаданные.ФункциональныеОпции.ИспользоватьРетроБонусыКлиентов.Имя);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(НастройкиОтчета);
	
КонецПроцедуры

#КонецОбласти

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//   КлючВарианта - Строка - 
//   
//	Возвращаемое значение:
//		СтрокаТаблицыЗначений - описание созданной команды.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов, Параметры, КлючВарианта = "") Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СоставРетроБонусовКлиентов)
	   И ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыКлиентов") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		Если КлючВарианта = "СоставРетроБонусовКлиентовКонтекст" Тогда
			
			Представление = НСтр("ru = 'Состав документа (актуальные данные)';
								|en = 'Document content (based on document data)'"); 
			
		ИначеЕсли КлючВарианта = "СоставРетроБонусовКлиентовПоДаннымДокументаКонтекст" Тогда
			
			Представление = НСтр("ru = 'Состав документа (по данным документа)';
								|en = 'Document content (based on document data)'");
			
		Иначе
			
			Представление = НСтр("ru = 'Состав документа ретро-бонусов клиентов';
								|en = 'Customer rebate document content'");
			
		КонецЕсли;
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СоставРетроБонусовКлиентов.ПолноеИмя();
		КомандаОтчет.Представление = Представление;
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		
		Если ЗначениеЗаполнено(КлючВарианта) Тогда
			
			КомандаОтчет.КлючВарианта = КлючВарианта;
			
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли