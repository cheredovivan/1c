//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// При компоновке результата.
// 
// Параметры:
//  ДокументРезультат - ТабличныйДокумент - Документ результат
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - Данные расшифровки
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	ДокументУРБ = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ДокументУРБ").Значение;  // ДокументСсылка.УсловияРетроБонусовКлиентов
	ВариантОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ВариантОтчета").Значение;  // Строка 
	ВыводитьАртикул = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ВыводитьАртикул").Значение;  // Булево
	ПоДаннымДокумента = (ВариантОтчета = 2);
	
	СтруктураПараметров = ДополнительныеПараметрыПоУРБ(ДокументУРБ, ПоДаннымДокумента);
	СтруктураПараметров.ВыводитьАртикул = ВыводитьАртикул;
	ОформитьЗаголовки(НастройкиОтчета, СтруктураПараметров, ПоДаннымДокумента);
	
	НаборыДанных = ИнициализироватьВнешнийНаборДанных(СтруктураПараметров, ПоДаннымДокумента);
	
	КомментарийСегментыНеЗафиксированы = НСтр("ru = 'Данные о составе документа не зафиксированы, могут быть изменены по правилам формирования состава сегментов или свойств';
												|en = 'Document is not recorded, it can be changed according to the segment or property generation rules'");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
		НастройкиОтчета,
		"КомментарийСегментыНеЗафиксированы",
		КомментарийСегментыНеЗафиксированы);
	
	КомментарийСегментыЗафиксированы = НСтр("ru = 'Расчет и начисление ретро-бонуса осуществляется по данным зафиксированного состава документа';
											|en = 'Rebate is calculated and accrued according to the data of the recorded document'");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
		НастройкиОтчета,
		"КомментарийСегментыЗафиксированы",
		КомментарийСегментыЗафиксированы);
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, НаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОтчетПустой = ОтчетыСервер.ОтчетПустой(ЭтотОбъект, ПроцессорКомпоновки);
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ОтчетПустой", ОтчетПустой);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - См. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ФормироватьСразу = Истина;
	Настройки.РазрешеноИзменятьВарианты = Ложь;
	Настройки.РазрешеноИзменятьСтруктуру = Ложь;
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета:
//      * Отчет - ОтчетОбъект.РасчетРетроБонусовКлиентов
//      * Параметры - Структура:
//         ** ПараметрКоманды - ДокументСсылка.УсловияРетроБонусовКлиентов
//   Отказ - Булево - Признак отказа от создания формы.
//      См. описание одноименного параметра "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//   СтандартнаяОбработка - Булево - Признак выполнения стандартной (системной) обработки события.
//      См. описание одноименного параметра "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
// См. также:
//   Процедура для вывода добавленных команд в форму: ОтчетыСервер.ВывестиКоманду().
//   Глобальный обработчик этого события: ОтчетыПереопределяемый.ПриСозданииНаСервере().
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = Форма.Параметры;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 
	
	ПоДаннымДокумента = Ложь;
	Если НастройкиОтчета.ДополнительныеСвойства.КлючВарианта = "СоставРетроБонусовКлиентовПоДаннымДокументаКонтекст" Тогда
		
		ПоДаннымДокумента = Истина;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ПараметрКоманды = Параметры.ПараметрКоманды; // ДокументСсылка.УсловияРетроБонусовКлиентов
		ТипПараметраКоманды = ТипЗнч(ПараметрКоманды);
		
		Форма.НастройкиОтчета.РазрешеноВыбиратьВарианты = Ложь;
		
		Если ТипПараметраКоманды = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
			
			СтруктураПараметров = ДополнительныеПараметрыПоУРБ(ПараметрКоманды, ПоДаннымДокумента);
			СоставУчастников = СтруктураПараметров.СоставУчастников;
			СоставТоваров = СтруктураПараметров.СоставТоваров;
			СоставСегментыПоПартнерам = (СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров);
			СоставСегментыСвойствоНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
				ИЛИ СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры);
			УРБСогласован = СтруктураПараметров.УРБСогласован;
			
			Если НЕ УРБСогласован
			   И НЕ ПоДаннымДокумента Тогда
				
				ТекстСообщения = НСтр("ru = 'Формирование отчета предусмотрено только для согласованных документов';
										|en = 'The report can be generated only for approved documents'");
				ВызватьИсключение ТекстСообщения;
					
			ИначеЕсли НЕ СоставСегментыПоПартнерам
			   И НЕ СоставСегментыСвойствоНоменклатуры Тогда
				
				ТекстСообщения = НСтр("ru = 'Формирование отчета предусмотрено только для документов с настройкой по сегментам клиентов/номенклатуры или по свойству номенклатуры';
										|en = 'The report can be generated only for documents with customization by item/customer segments or by item property'");
				ВызватьИсключение ТекстСообщения;
				
			КонецЕсли;
			
			ДобавляемыеРеквизитыФормы = Новый Массив(); // Массив из РеквизитФормы
			
			ТипСтрока = Новый ОписаниеТипов("ПеречислениеСсылка.СоставыТоваровРетроБонусов");
			ИмяРеквизита = "СоставТоваров";
			
			НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, ТипСтрока);
			ДобавляемыеРеквизитыФормы.Добавить(НовыйРеквизит);
			
			Форма.ИзменитьРеквизиты(ДобавляемыеРеквизитыФормы);
			
			ОбщегоНазначенияКлиентСервер.УстановитьРеквизитФормыПоПути(
				Форма, ИмяРеквизита, СоставТоваров);
			
		КонецЕсли;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Параметры.ПредставлениеВарианта) Тогда
		
		Отказ = Истина;
		СинонимДокумента = Метаданные.Документы.УсловияРетроБонусовКлиентов.Синоним;
		ШаблонОшибки = НСтр("ru = 'Отчет предназначен только для открытия из документа ""%1"".';
							|en = 'You can only open the report from the ""%1"" document.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, СинонимДокумента);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;

КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - См. ОбщаяФорма.ФормаОтчета
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже).
//       Может не использоваться, если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено, когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы = КлючВарианта Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Параметры = Контекст.Параметры;
		Если Параметры.Свойство("ПараметрКоманды") Тогда
			
			ПараметрКоманды = Параметры.ПараметрКоманды; // ДокументСсылка.УсловияРетроБонусовКлиентов
			ЗаполнитьПараметрыПоКонтексту(ПараметрКоманды, НовыеНастройкиКД);
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Контекст, "СоставТоваров") Тогда
				
				СоставТоваров = Контекст.СоставТоваров;
				
				Если СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
				   И СоставТоваров <> Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
					
					ВыводитьАртикул = СхемаКомпоновкиДанных.Параметры.ВыводитьАртикул;
					ВыводитьАртикул.ОграничениеИспользования = Истина;
					ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	КлючСхемы = КлючВарианта;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОформитьЗаголовки(НастройкиОтчета, СтруктураПараметров, ПоДаннымДокумента)
	
	СоставУчастников = СтруктураПараметров.СоставУчастников;
	СоставТоваров = СтруктураПараметров.СоставТоваров;
	Участники = СтруктураПараметров.Участники;
	Товары = СтруктураПараметров.Товары;
	СегментыЗафиксированы = СтруктураПараметров.ФиксацияВыполнена;
	ВыводитьАртикул = СтруктураПараметров.ВыводитьАртикул;
	
	Если ПоДаннымДокумента Тогда
		ПредставлениеДокумента = СтруктураПараметров.ДокументУРБПредставление;
	Иначе
		ПредставлениеДокумента = СтруктураПараметров.ДокументУРБПервичныйПредставление;
	КонецЕсли;
		
	ШаблонЗаголовка = НСтр("ru = 'Состав документа %1';
							|en = '%1 document content'");
	ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, ПредставлениеДокумента);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметрВывода(НастройкиОтчета, "Заголовок", ЗаголовокОтчета);
	
	Для Каждого ЭлементСтруктуры Из НастройкиОтчета.Структура Цикл
		
		Если ЭлементСтруктуры.Имя = "Товары" Тогда
			
			Если СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
			  ИЛИ СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры Тогда
				
				Если Товары = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
					
					ЗаголовокГруппировки = НСтр("ru = 'Выбранные товары';
												|en = 'Selected goods'");
					
				Иначе
					
					ЗаголовокГруппировки = НСтр("ru = 'Все, кроме выбранных товаров';
												|en = 'All except for selected goods'");
					
				КонецЕсли;
				КомпоновкаДанныхКлиентСервер.УстановитьПараметрВывода(ЭлементСтруктуры, "Заголовок", ЗаголовокГруппировки);
				
				ПолеКомпоновки = Новый ПолеКомпоновкиДанных("Артикул");
				
				Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				
					Если ТипЗнч(ПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")
					   И ПолеГруппировки.Поле = ПолеКомпоновки Тогда
					
						ПолеГруппировки.Использование = ВыводитьАртикул;
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
				ПолеКомпоновки = Новый ПолеКомпоновкиДанных("Характеристика");
				
				Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				
					Если ТипЗнч(ПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")
					   И ПолеГруппировки.Поле = ПолеКомпоновки Тогда
					
						ПолеГруппировки.Использование =
							(СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры);
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
			Иначе
				
				ЭлементСтруктуры.Использование = Ложь;
				
			КонецЕсли;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "Партнеры" Тогда
			
			Если СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров Тогда
				
				Если Участники = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
					
					ЗаголовокГруппировки = НСтр("ru = 'Выбранные клиенты';
												|en = 'Selected customers'");
					
				Иначе
					
					ЗаголовокГруппировки = НСтр("ru = 'Все, кроме выбранных клиентов';
												|en = 'All except for selected customers'");
					
				КонецЕсли;
				КомпоновкаДанныхКлиентСервер.УстановитьПараметрВывода(ЭлементСтруктуры, "Заголовок", ЗаголовокГруппировки);
				
			Иначе
				
				ЭлементСтруктуры.Использование = Ложь;
				
			КонецЕсли;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийСегментыНеЗафиксированы" Тогда
			
			ИспользоватьГруппировку = (НЕ СегментыЗафиксированы И НЕ ПоДаннымДокумента);
			ЭлементСтруктуры.Использование = ИспользоватьГруппировку; 
			
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийСегментыЗафиксированы" Тогда
			
			ИспользоватьГруппировку = (СегментыЗафиксированы И НЕ ПоДаннымДокумента);
			ЭлементСтруктуры.Использование = ИспользоватьГруппировку;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийПустой" Тогда
			
			ЭлементСтруктуры.Использование = Истина;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Заполняет параметры из переданного контекста.
// 
// Параметры:
//  ПараметрКоманды - ЛюбаяСсылка
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда настройки варианта не надо загружать (уже загружены ранее).
//
Процедура ЗаполнитьПараметрыПоКонтексту(ПараметрКоманды, НовыеНастройкиКД)
	
	ТипПараметраКоманды = ТипЗнч(ПараметрКоманды);
	
	Если ТипПараметраКоманды = Тип("ДокументСсылка.УсловияРетроБонусовКлиентов") Тогда
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НовыеНастройкиКД, "ДокументУРБ", ПараметрКоманды);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИнициализироватьВнешнийНаборДанных(СтруктураПараметров, ПоДаннымДокумента)
	
	НаборДанных = Новый Структура;
	НаборДанных.Вставить("НаборДанныхТовары", Новый ТаблицаЗначений);
	НаборДанных.Вставить("НаборДанныхПартнеры", Новый ТаблицаЗначений);
	
	ДокументУРБ = СтруктураПараметров.ДокументУРБ;
	ДокументУРБПервичный = СтруктураПараметров.ДокументУРБПервичный;
	СоставУчастников = СтруктураПараметров.СоставУчастников;
	СоставТоваров = СтруктураПараметров.СоставТоваров;
	СегментыЗафиксированы = СтруктураПараметров.ФиксацияВыполнена;
	
	СоставСегментыПоПартнерам = (СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров);
	СоставСегментыНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры);
	СоставСвойствоНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры); 
	
	Если СоставСегментыПоПартнерам
	 ИЛИ СоставСегментыНоменклатуры
	 ИЛИ СоставСвойствоНоменклатуры Тогда
	
		Если СегментыЗафиксированы
		   И НЕ ПоДаннымДокумента Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДокументУсловий", ДокументУРБПервичный);
			Запрос.УстановитьПараметр("СоставСегментыПоПартнерам", СоставСегментыПоПартнерам);
			Запрос.УстановитьПараметр("СоставСегментыНоменклатуры",
				Макс(СоставСегментыНоменклатуры, СоставСвойствоНоменклатуры));
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	РетроБонусыКлиентовТовары.Номенклатура КАК Номенклатура,
			|	РетроБонусыКлиентовТовары.Характеристика КАК Характеристика
			|ИЗ
			|	РегистрСведений.РетроБонусыКлиентовТовары КАК РетроБонусыКлиентовТовары
			|ГДЕ
			|	РетроБонусыКлиентовТовары.ДокументУсловий = &ДокументУсловий
			|	И &СоставСегментыНоменклатуры
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	РетроБонусыКлиентовКонтрагенты.Партнер КАК Партнер
			|ИЗ
			|	РегистрСведений.РетроБонусыКлиентовКонтрагенты КАК РетроБонусыКлиентовКонтрагенты
			|ГДЕ
			|	РетроБонусыКлиентовКонтрагенты.ДокументУсловий = &ДокументУсловий
			|	И &СоставСегментыПоПартнерам"; 
			
			Результат = Запрос.ВыполнитьПакет();
			НаборДанных.НаборДанныхТовары = Результат[0].Выгрузить();
			НаборДанных.НаборДанныхПартнеры = Результат[1].Выгрузить();
			
		Иначе 
			
			Если ПоДаннымДокумента Тогда
				
				Если СоставСвойствоНоменклатуры Тогда
					
					НаборДанных.НаборДанныхТовары = ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента);
					
				Иначе
					
					НаборДанных.НаборДанныхТовары = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовТоваров(
						ДокументУРБ, 
						ПоДаннымДокумента);
						
				КонецЕсли;
					
				НаборДанных.НаборДанныхПартнеры = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовПартнеров(
					ДокументУРБ, 
					ПоДаннымДокумента);
				
			Иначе
				
				Если СоставСвойствоНоменклатуры Тогда
					
					НаборДанных.НаборДанныхТовары = ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента);
					
				Иначе
					
					НаборДанных.НаборДанныхТовары = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовТоваров(ДокументУРБПервичный);
					
				КонецЕсли;
				
				НаборДанных.НаборДанныхПартнеры = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовПартнеров(ДокументУРБПервичный);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НаборДанных;
	
КонецФункции

Функция ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента)
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	РезультатПроверки = РетроБонусыСервер.ДанныеПроверкиСвойстваНоменклатуры(СтруктураПараметров.СвойствоНоменклатуры);
	
	Если РезультатПроверки.РазрешеноИспользовать Тогда
		
		ТекстТоварыПоСвойству = РетроБонусыСервер.ТекстЗапросаТоварыПоСвойству(
			Истина,
			СтруктураПараметров.СвойствоНоменклатуры,
			РезультатПроверки.Свойство,
			ПоДаннымДокумента);
		
		ТекстСоставТоваров =
		"ВЫБРАТЬ
		|	ТоварыПоСвойству.ДокументУсловий КАК ДокументУсловий,
		|	ТоварыПоСвойству.Характеристика КАК Характеристика,
		|	ТоварыПоСвойству.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВТ_ТоварыПоСвойству КАК ТоварыПоСвойству
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТоварыПоСвойству.Номенклатура = СправочникНоменклатура.Ссылка
		|			И СправочникНоменклатура.ТипНоменклатуры В (&ТипыНоменклатуры)
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		ТекстРазделитель = ОбщегоНазначения.РазделительПакетаЗапросов();
		
		ФрагментыЗапроса = Новый Массив; // Массив из Строка
		ФрагментыЗапроса.Добавить(ТекстТоварыПоСвойству);
		ФрагментыЗапроса.Добавить(ТекстСоставТоваров);
		
		ТекстЗапроса = СтрСоединить(ФрагментыЗапроса, ТекстРазделитель);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Если ПоДаннымДокумента Тогда
			Запрос.УстановитьПараметр("ИсходныйДокумент", СтруктураПараметров.ДокументУРБ);
		Иначе
			Запрос.УстановитьПараметр("ИсходныйДокумент", СтруктураПараметров.ДокументУРБПервичный);
		КонецЕсли;
		Запрос.УстановитьПараметр("СвойствоНоменклатуры", РезультатПроверки.Свойство);
		ТипыНоменклатуры = РетроБонусыСервер.ПоддерживаемыеТипыНоменклатуры();
		Запрос.УстановитьПараметр("ТипыНоменклатуры", ТипыНоменклатуры);
		
		ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
		
	Возврат ТаблицаТоваров;
	
КонецФункции

Функция ДополнительныеПараметрыПоУРБ(ДокументУРБ, ПоДаннымДокумента = Ложь)
	
	СтруктураПараметров = СтруктураПараметровОтчета();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУРБ);
	Запрос.УстановитьПараметр("ПоДаннымДокумента", ПоДаннымДокумента);
	
	Если ПоДаннымДокумента Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеУсловий.ОтборУчастников КАК Участники,
		|	ДанныеУсловий.ОтборТоваров КАК Товары,
		|	ЕСТЬNULL(ВидыРетроБонусовКлиентов.СоставУчастников, ЗНАЧЕНИЕ(Перечисление.СоставыУчастниковРетроБонусов.ПустаяСсылка)) КАК СоставУчастников,
		|	ЕСТЬNULL(ВидыРетроБонусовКлиентов.СоставТоваров, ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)) КАК СоставТоваров,
		|	ЛОЖЬ КАК ФиксацияВыполнена,
		|	ДанныеУсловий.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовРетроБонусов.Согласован) КАК УРБСогласован,
		|	ВЫБОР
		|		КОГДА ДанныеУсловий.Исправление
		|			ТОГДА ДанныеУсловий.ИсправляемыйДокумент
		|		ИНАЧЕ ДанныеУсловий.Ссылка
		|	КОНЕЦ КАК ДокументУРБПервичный,
		|	ДанныеУсловий.Представление КАК ДокументУРБПредставление,
		|	ВЫБОР
		|		КОГДА ДанныеУсловий.Исправление
		|			ТОГДА ДанныеУсловий.ИсправляемыйДокумент.Представление
		|		ИНАЧЕ ДанныеУсловий.Представление
		|	КОНЕЦ КАК ДокументУРБПервичныйПредставление,
		|	ЕСТЬNULL(ВидыРетроБонусовКлиентов.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры
		|ИЗ
		|	Документ.УсловияРетроБонусовКлиентов КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовКлиентов КАК ВидыРетроБонусовКлиентов
		|		ПО ДанныеУсловий.ВидРетроБонуса = ВидыРетроБонусовКлиентов.Ссылка
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Участники, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)) КАК Участники,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Товары, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)) КАК Товары,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.СоставУчастников, ЗНАЧЕНИЕ(Перечисление.СоставыУчастниковРетроБонусов.ПустаяСсылка)) КАК СоставУчастников,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.СоставТоваров, ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)) КАК СоставТоваров,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ФиксацияВыполнена, ЛОЖЬ) КАК ФиксацияВыполнена,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ДокументУсловий, ЛОЖЬ) <> ЛОЖЬ КАК УРБСогласован,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ДокументУсловий, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовКлиентов.ПустаяСсылка)) КАК ДокументУРБПервичный,
		|	ДанныеУсловий.Представление КАК ДокументУРБПредставление,
		|	ВЫБОР
		|		КОГДА НЕ РетроБонусыКлиентовУсловия.ДокументУсловий ЕСТЬ NULL
		|			ТОГДА РетроБонусыКлиентовУсловия.ДокументУсловий.Представление
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДокументУРБПервичныйПредставление,
		|	ЕСТЬNULL(ВидыРетроБонусовКлиентов.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры
		|ИЗ
		|	Документ.УсловияРетроБонусовКлиентов КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыКлиентовУсловия КАК РетроБонусыКлиентовУсловия
		|		ПО ДанныеУсловий.ИсправляемыйДокумент = РетроБонусыКлиентовУсловия.ДокументУсловий
		|			И (ДанныеУсловий.Исправление)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовКлиентов КАК ВидыРетроБонусовКлиентов
		|		ПО (РетроБонусыКлиентовУсловия.Вид = ВидыРетроБонусовКлиентов.Ссылка)
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий
		|	И НЕ РетроБонусыКлиентовУсловия.ДокументУсловий ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Участники, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.Товары, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.СоставУчастников, ЗНАЧЕНИЕ(Перечисление.СоставыУчастниковРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.СоставТоваров, ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ФиксацияВыполнена, ЛОЖЬ),
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ДокументУсловий, ЛОЖЬ) <> ЛОЖЬ,
		|	ЕСТЬNULL(РетроБонусыКлиентовУсловия.ДокументУсловий, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовКлиентов.ПустаяСсылка)),
		|	ДанныеУсловий.Представление,
		|	ВЫБОР
		|		КОГДА НЕ РетроБонусыКлиентовУсловия.ДокументУсловий ЕСТЬ NULL
		|			ТОГДА РетроБонусыКлиентовУсловия.ДокументУсловий.Представление
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ЕСТЬNULL(ВидыРетроБонусовКлиентов.СвойствоНоменклатуры, """")
		|ИЗ
		|	Документ.УсловияРетроБонусовКлиентов КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыКлиентовУсловия КАК РетроБонусыКлиентовУсловия
		|		ПО ДанныеУсловий.Ссылка = РетроБонусыКлиентовУсловия.ДокументУсловий
		|			И (НЕ ДанныеУсловий.Исправление)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовКлиентов КАК ВидыРетроБонусовКлиентов
		|		ПО (РетроБонусыКлиентовУсловия.Вид = ВидыРетроБонусовКлиентов.Ссылка)
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий
		|	И НЕ РетроБонусыКлиентовУсловия.ДокументУсловий ЕСТЬ NULL";
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка);
		
	КонецЕсли;
	
	СтруктураПараметров.ДокументУРБ = ДокументУРБ;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция СтруктураПараметровОтчета()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДокументУРБ", Документы.УсловияРетроБонусовКлиентов.ПустаяСсылка());
	СтруктураПараметров.Вставить("ДокументУРБПервичный", Документы.УсловияРетроБонусовКлиентов.ПустаяСсылка());
	СтруктураПараметров.Вставить("ДокументУРБПредставление", "");
	СтруктураПараметров.Вставить("ДокументУРБПервичныйПредставление", "");
	СтруктураПараметров.Вставить("СоставУчастников", Перечисления.СоставыУчастниковРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("СоставТоваров", Перечисления.СоставыТоваровРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("Участники", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("Товары", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("ФиксацияВыполнена", Ложь);
	СтруктураПараметров.Вставить("УРБСогласован", Ложь);
	СтруктураПараметров.Вставить("ВыводитьАртикул", Ложь);
	СтруктураПараметров.Вставить("СвойствоНоменклатуры", "");
	
	Возврат СтруктураПараметров;
	
КонецФункции

#КонецОбласти

#КонецЕсли