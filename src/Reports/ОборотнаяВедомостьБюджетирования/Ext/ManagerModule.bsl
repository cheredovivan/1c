#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Для подсистемы "Варианты отчетов" при работе в модели сервиса.
// Возвращаемое значение:
//  Массив из Структура - Содержит массив структур со свойствами:
//     * ВариантОтчета - Структура - Вариант отчета:
//        ** Имя             - Строка - Имя варианта отчета.
//        ** Представление   - Строка - Представление варианта отчета.
//
Функция ВариантыНастроек() Экспорт
	
	Результат = Новый Массив;
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "ОборотнаяВедомость");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотная ведомость по статьям бюджетов';
											|en = 'Turnover balance report of budget flow items'"));
	Результат.Добавить(Вариант);
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "ОборотнаяВедомостьПоСтатьеБюджетов");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотная ведомость по статье бюджетов';
											|en = 'Turnover balance report of a budget flow item'"));
	Результат.Добавить(Вариант);
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "Расшифровка");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотная ведомость по статьям бюджетов';
											|en = 'Turnover balance report of budget flow items'"));
	Результат.Добавить(Вариант);
	
	Возврат Результат;
	
КонецФункции

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   
// Возвращаемое значение:
//     СтрокаТаблицыЗначений
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОборотнаяВедомостьБюджетирования) 
			И ПолучитьФункциональнуюОпцию("ИспользоватьБюджетирование") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ОборотнаяВедомостьБюджетирования.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Оборотная ведомость';
											|en = 'Turnover balance report'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ОборотнаяВедомостьПоСтатьеБюджетов";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   ВыводитьВариантыОтчетов - Булево.
//
Процедура НастроитьВариантыОтчета(Настройки, ВыводитьВариантыОтчетов) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ОборотнаяВедомостьБюджетирования);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОписаниеОтчета, ВыводитьВариантыОтчетов);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'Сравнение запланированных в рамках сценариев сумм с фактическим исполнением за период.';
									|en = 'Compare amounts planned within scenarios with actual execution over the period.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОборотнаяВедомость");
	ОписаниеВарианта.Описание = НСтр("ru = 'Сравнение запланированных в рамках сценариев сумм с фактическим исполнением за период.';
									|en = 'Compare amounts planned within scenarios with actual execution over the period.'");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ОборотнаяВедомостьПоСтатьеБюджетов");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "Расшифровка");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли