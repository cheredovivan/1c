#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   КлючВарианта - Строка - Ключ варианта
//
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - из:
//    *Менеджер - Строка -
//    *Представление - Строка -
//    *МножественныйВыбор - Булево - 
//    *Важность - Строка -
//    *КлючВарианта - Строка -
Функция ДобавитьКомандуОтчета(КомандыОтчетов, КлючВарианта = "") Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ТрудоемкостьЗаказаНаПроизводство) 
			И ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ТрудоемкостьЗаказаНаПроизводство.ПолноеИмя();
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		Если ЗначениеЗаполнено(КлючВарианта) Тогда
			КомандаОтчет.КлючВарианта = КлючВарианта;
		КонецЕсли;
		
		Если КлючВарианта = "ТрудоемкостьПоЗаказу" Тогда
			
			КомандаОтчет.Представление = НСтр("ru = 'Трудоемкость заказа';
												|en = 'Order labor input'");
			
		ИначеЕсли КлючВарианта = "ТрудоемкостьПоПартии" Тогда
			
			КомандаОтчет.Представление = НСтр("ru = 'Трудоемкость по партии';
												|en = 'Labor input by lot'");
			
		ИначеЕсли КлючВарианта = "ТрудоемкостьПоЭтапу" Тогда
			
			КомандаОтчет.Представление = НСтр("ru = 'Трудоемкость по этапу';
												|en = 'Labor input by stage'");
		
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ТрудоемкостьЗаказаНаПроизводство);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;

	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ТрудоемкостьПоЗаказу");
	ОписаниеВарианта.Наименование = НСтр("ru = 'Трудоемкость заказа на производство';
										|en = 'Labor input of production order'");
	ОписаниеВарианта.Описание = НСтр("ru = 'Оценка состояния заказа на производство по плановым и фактическим трудозатратам.';
									|en = 'Evaluate the state of production orders by planned and actual labor costs.'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ТрудоемкостьПоПартии");
	ОписаниеВарианта.Наименование = НСтр("ru = 'Трудоемкость заказов на производство по партии';
										|en = 'Labor input of production orders by lot'");
	ОписаниеВарианта.Описание = НСтр("ru = 'Оценка состояния по плановым и фактическим трудозатратам по партиям производства.';
									|en = 'Evaluate the state by planned and actual labor costs by production lots.'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ТрудоемкостьПоЭтапу");
	ОписаниеВарианта.Наименование = НСтр("ru = 'Трудоемкость заказов на производство по этапам';
										|en = 'Labor input of production orders by stages'");
	ОписаниеВарианта.Описание = НСтр("ru = 'Оценка состояния по плановым и фактическим трудозатратам по этапам производства.';
									|en = 'Evaluate the state by planned and actual labor costs by production stages.'");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли