#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - команда отчета
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеРасчетовСКомиссионерами) 
			И ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеРасчетовСКомиссионерами.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние расчетов с комиссионерами';
											|en = 'Consignee AR/AP'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.ЕстьУсловияВидимости = Истина;
		
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет,
			"ХозяйственнаяОперация",
			Перечисления.ХозяйственныеОперации.РеализацияЧерезКомиссионера, 
			ВидСравненияКомпоновкиДанных.НеРавно);
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ПостфиксСхемыКомиссии = КомиссионнаяТорговляСервер.ПостфиксСхемыКомиссии();
	НаименованиеВарианта = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru = 'Состояние расчетов с комиссионерами (%1)';
		|en = 'Consignee AR/AP (%1)'"),
	ПостфиксСхемыКомиссии);
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СостояниеРасчетовСКомиссионерами);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОписаниеОтчета, Ложь);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Наименование = НаименованиеВарианта;
	ОписаниеВарианта.Описание = НСтр("ru = 'Сколько комиссионер должен нам за проданные товары?
		|Сколько мы должны комиссионеру, например, по вознаграждениям?';
		|en = 'Track how much a consignee owes us for the sold goods.
		|Verify how much we owe a consignee (for example, fees).'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомиссионерами");
	ОписаниеВарианта.Наименование = НаименованиеВарианта;
	ОписаниеВарианта.Описание = НСтр("ru = 'Сколько комиссионер должен нам за проданные товары?
		|Сколько мы должны комиссионеру, например, по вознаграждениям?';
		|en = 'Track how much a consignee owes us for the sold goods.
		|Verify how much we owe a consignee (for example, fees).'");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьКомиссиюПриПродажахНеБазовая");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомиссионерамиБазовая");
	ОписаниеВарианта.ФункциональныеОпции.Добавить("ИспользоватьКомиссиюПриПродажахБазовая");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомиссионерамиКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СостояниеРасчетовСКомиссионерамиКонтекстБазовая");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли