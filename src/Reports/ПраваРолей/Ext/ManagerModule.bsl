///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.
Процедура ПередДобавлениемКомандОтчетов(КомандыОтчетов, Параметры, СтандартнаяОбработка) Экспорт
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ПраваРолей)
	 Или СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
		Возврат;
	КонецЕсли;
	
	КомандаРоли = КомандыОтчетов.Добавить();
	КомандаРоли.МножественныйВыбор = Истина;
	КомандаРоли.Менеджер = "Отчет.ПраваРолей";
	КомандаРоли.КлючВарианта = "ПраваРолей";
	
	Если СтрНачинаетсяС(Параметры.ИмяФормы, "Справочник.ПрофилиГруппДоступа") Тогда
		КомандаПрофили = КомандыОтчетов.Добавить();
		КомандаПрофили.МножественныйВыбор = Истина;
		КомандаПрофили.Менеджер = "Отчет.ПраваРолей";
		КомандаПрофили.КлючВарианта = "ПраваРолиИПрофилейНаОбъектыМетаданных";
		
		Если Параметры.ИмяФормы = "Справочник.ПрофилиГруппДоступа.Форма.ФормаЭлемента" Тогда
			КомандаРоли.Представление    = НСтр("ru = 'Права ролей профиля';
												|en = 'Rights of profile roles'");
			КомандаПрофили.Представление = НСтр("ru = 'Права профиля';
												|en = 'Rights of profile'");
		Иначе
			КомандаРоли.Представление    = НСтр("ru = 'Права ролей профилей';
												|en = 'Rights of profiles roles'");
			КомандаПрофили.Представление = НСтр("ru = 'Права профилей';
												|en = 'Rights of profiles'");
		КонецЕсли;
	Иначе
		КомандаРоли.Представление = НСтр("ru = 'Права ролей и профилей';
										|en = 'Rights of profiles and roles'");
		КомандаРоли.ТолькоВоВсехДействиях = Истина;
		КомандаРоли.Важность = "СмТакже";
		КомандаРоли.КлючВарианта = "ПраваРолейИПрофилейНаОбъектМетаданных";
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ВариантыОтчетов") Тогда
		МодульВариантыОтчетов = ОбщегоНазначения.ОбщийМодуль("ВариантыОтчетов");
	Иначе
		Возврат;
	КонецЕсли;
	
	МодульВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Ложь);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолей");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права ролей на объекты метаданных.';
										|en = 'Shows role rights that apply to metadata objects.'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолиИПрофилейНаОбъектыМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права одной роли и профилей на разные объекты метаданных.';
										|en = 'Shows rights of one role and profiles to different metadata objects.'");
	НастройкиВарианта.Включен = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПраваРолейИПрофилейНаОбъектМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает права разных ролей и профилей на один объект метаданных.';
										|en = 'Shows rights of different roles and profiles to the same metadata object.'");
	НастройкиВарианта.Включен = Ложь;
	
	НастройкиВарианта = МодульВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ПодробныеПраваРолейНаОбъектМетаданных");
	НастройкиВарианта.Описание = НСтр("ru = 'Показывает подробные права разных ролей на один объект метаданных.';
										|en = 'Shows detailed rights of different roles to the same metadata object.'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
