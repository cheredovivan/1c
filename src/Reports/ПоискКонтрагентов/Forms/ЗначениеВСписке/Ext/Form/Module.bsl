///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ПоследнееВремяПодбора;

&НаКлиенте
Перем ИсходныеДанныеВыбора;

&НаКлиенте
Перем ВремяЗадержкиАвтоПодбора;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтоПодборОКВЭД        = Параметры.ЭтоПодборОКВЭД;
	ЭтоВыборИзСправочника = Параметры.ЭтоВыборИзСправочника;
	ИдентификаторКритерия = Параметры.ИдентификаторКритерия;
	
	Если ЭтоПодборОКВЭД Тогда
		
		Элементы.ВыбранныеЗначенияПодборОКВЭД.Видимость      = Истина;
		Элементы.ВыбранныеЗначенияЗначение.КнопкаВыбора      = Истина;
		Элементы.ВыбранныеЗначенияПредставление.КнопкаВыбора = Истина;
		Элементы.ВыбранныеЗначенияЗначение.КнопкаОчистки     = Истина;
		
	ИначеЕсли Не ЭтоВыборИзСправочника Тогда
		
		ОписаниеТиповПоИдентификатору = ПоискКонтрагентовСлужебный.ОписаниеТиповПоИдентификатору();
		ТекущийТип = ОписаниеТиповПоИдентификатору.Получить(Параметры.ТипЗначения);
		Если ТекущийТип = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Неизвестный тип значения критерия отбора.';
									|en = 'Unknown type of filter criterion value.'");
		КонецЕсли;
		Элементы.ВыбранныеЗначенияЗначение.ОграничениеТипа = ТекущийТип;
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ИсходныйСписок) = Тип("СписокЗначений") Тогда
		
		Если ЭтоВыборИзСправочника Или ЭтоПодборОКВЭД Тогда
	
			Для Каждого ВыбранноеЗначение Из Параметры.ИсходныйСписок Цикл
				
				ПравильноеЗначение = ПоискКонтрагентовСлужебныйКлиентСервер.ПравильноеЗначениеИзСправочника(
					ВыбранноеЗначение.Значение);
				Если ПравильноеЗначение <> Неопределено Тогда
					НоваяСтрока               = ВыбранныеЗначения.Добавить();
					НоваяСтрока.Значение      = ПравильноеЗначение;
					НоваяСтрока.Представление = ПравильноеЗначение.Представление;
				КонецЕсли;
					
			КонецЦикла;
	
		Иначе
		
			Для Каждого ВыбранноеЗначение Из Параметры.ИсходныйСписок Цикл
				НоваяСтрока               = ВыбранныеЗначения.Добавить();
				НоваяСтрока.Значение      = ВыбранноеЗначение.Значение;
				НоваяСтрока.Представление = ВыбранноеЗначение.Значение;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЭтоВыборИзСправочника Или ЭтоПодборОКВЭД Тогда
		Элементы.ВыбранныеЗначенияПредставление.Видимость     = Истина;
		Элементы.ВыбранныеЗначенияПредставление.КнопкаОчистки = Истина;
		Элементы.ВыбранныеЗначенияЗначение.Видимость          = Ложь;
	Иначе
		Элементы.ВыбранныеЗначенияПредставление.Видимость = Ложь;
		Элементы.ВыбранныеЗначенияЗначение.Видимость      = Истина;
	КонецЕсли;
	
	РазрешеныПустые = (Параметры.ТипЗначения <> "TEXT"
		И Не ЭтоПодборОКВЭД
		И Не ЭтоВыборИзСправочника);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПоследнееВремяПодбора    = 0;
	ИсходныеДанныеВыбора     = Неопределено;
	ВремяЗадержкиАвтоПодбора = ПоискКонтрагентовКлиент.ВремяЗадержкиАвтоПодбораИзСправочника();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыбранныеЗначения

#Область КолонкаПредставление

&НаКлиенте
Процедура ВыбранныеЗначенияПредставлениеНачалоВыбора(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	Если ЭтоПодборОКВЭД Тогда
		СтандартнаяОбработка = Ложь;
		ВыбратьОКВЭД(ВариантПодбораОКВЭД_ВыборВПоле());
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеЗначенияПредставлениеОчистка(
		Элемент,
		СтандартнаяОбработка)

	ВыбранноеЗначениеВСписок(
		Неопределено,
		"");
		
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеЗначенияПредставлениеОбработкаВыбора(
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка = Ложь)
	
	Если ЭтоВыборИзСправочника Или ЭтоПодборОКВЭД Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПравильноеЗначение = ПоискКонтрагентовСлужебныйКлиентСервер.ПравильноеЗначениеИзСправочника(
			ВыбранноеЗначение);
		Если ПравильноеЗначение = Неопределено Тогда
			Возврат;
		Иначе
			ВыбранноеЗначениеВСписок(
				ПравильноеЗначение,
				ПравильноеЗначение.Представление);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеЗначенияПредставлениеАвтоПодбор(
		Элемент,
		Текст,
		ДанныеВыбора,
		ПараметрыПолученияДанных,
		Ожидание,
		СтандартнаяОбработка)
	
	Если ЭтоВыборИзСправочника Тогда
		
		ТекущееВремя = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
		Если ТекущееВремя < ПоследнееВремяПодбора + ВремяЗадержкиАвтоПодбора Тогда
			СтандартнаяОбработка = Ложь;
			ДанныеВыбора         = ИсходныеДанныеВыбора;
			Возврат;
		КонецЕсли;
		
		АвтоПодборИзСправочника(
			Текст,
			ДанныеВыбора,
			СтандартнаяОбработка);
		ИсходныеДанныеВыбора  = ДанныеВыбора;
		ПоследнееВремяПодбора = ТекущаяУниверсальнаяДатаВМиллисекундах();
		Возврат;
		
	ИначеЕсли ЭтоПодборОКВЭД Тогда
		
		АвтоПодборОКВЭД(
			Текст,
			ДанныеВыбора,
			СтандартнаяОбработка);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныеЗначенияПредставлениеОкончаниеВводаТекста(
		Элемент,
		Текст,
		ДанныеВыбора,
		ПараметрыПолученияДанных,
		СтандартнаяОбработка)
	
	Если ЭтоПодборОКВЭД Тогда
		
		// Сюда попадаем, если ввели значение, которого нет в списке
		ТекстДляПроверки = СтрЗаменить(Текст, ".", "");
		Если Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстДляПроверки) Тогда
			// Можно вводить только код ОКВЭД
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
		ДанныеОКВЭД = ПоискКонтрагентовКлиент.ЗначениеОКВЭДНетВКлассификаторе(Текст);
		
		ВыбранноеЗначениеВСписок(
			ДанныеОКВЭД,
			ДанныеОКВЭД.Представление);
		
		Возврат;
		
	ИначеЕсли ЭтоВыборИзСправочника Тогда
		
		СтандартнаяОбработка = Ложь;
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборОКВЭД(Команда)
	
	ВыбратьОКВЭД(ВариантПодбораОКВЭД_Подбор());
	
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	
	Результат = Новый СписокЗначений;
	
	Если РазрешеныПустые Тогда
		
		Для Каждого Стр Из ВыбранныеЗначения Цикл
			Результат.Добавить(
				Стр.Значение,
				Стр.Значение);
		КонецЦикла;
		
	Иначе
	
		ИмяПоляПроверки = ?(ЭтоВыборИзСправочника Или ЭтоПодборОКВЭД, "Представление", "Значение");
		
		Для Каждого ЭлементСписка Из ВыбранныеЗначения Цикл
			
			Если Не ЗначениеЗаполнено(ЭлементСписка[ИмяПоляПроверки]) Тогда
				Продолжить;
			КонецЕсли;
			
			Результат.Добавить(
				ЭлементСписка.Значение,
				ЭлементСписка.Представление);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантПодбораОКВЭД_ВыборВПоле()
	
	Возврат "ВыборВПоле";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВариантПодбораОКВЭД_Подбор()
	
	Возврат "Подбор";
	
КонецФункции

&НаКлиенте
Процедура АвтоПодборОКВЭД(
		Текст,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	ПоискКонтрагентовКлиент.АвтоПодборОКВЭД(
		Текст,
		ДанныеВыбора,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоПодборИзСправочника(
		Текст,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	ПоискКонтрагентовКлиент.АвтоПодборИзСправочника(
		ИдентификаторКритерия,
		Текст,
		ДанныеВыбора,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОКВЭД(Вариант)
	
	ТекущиеДанные = Элементы.ВыбранныеЗначения.ТекущиеДанные;
	Если Вариант = ВариантПодбораОКВЭД_ВыборВПоле()
		И ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока", Элементы.ВыбранныеЗначения.ТекущаяСтрока);
	ДополнительныеПараметры.Вставить("Вариант",       Вариант);
	
	ОбработкаЗакрытия = Новый ОписаниеОповещения(
		"ПодборИзКлассификатораОКВЭДЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);

	ПараметрыФормы = КлассификаторОКВЭДКлиент.НовыйПараметрыВыбора();
	ПараметрыФормы.ОписаниеОповещенияОЗакрытии = ОбработкаЗакрытия;
	
	Если ТекущиеДанные <> Неопределено
		И ЗначениеЗаполнено(ТекущиеДанные.Значение) Тогда
		
		ПравильноеЗначение = ПоискКонтрагентовСлужебныйКлиентСервер.НовыйОписаниеЗначенияВыбораИзСправочника();
		ЗаполнитьЗначенияСвойств(ПравильноеЗначение, ТекущиеДанные.Значение);
		
		ПараметрыФормы.ТекущийКод = ПравильноеЗначение.Данные;
		
	КонецЕсли;
	
	Если Вариант = ВариантПодбораОКВЭД_Подбор() Тогда
		ПараметрыФормы.МножественныйВыбор = Истина;
	КонецЕсли;
	
	КлассификаторОКВЭДКлиент.Выбрать(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборИзКлассификатораОКВЭДЗавершение(
		РезультатВыбора,
		ДополнительныеПараметры) Экспорт

	Если Не ПоискКонтрагентовСлужебныйКлиентСервер.ЭтоСтруктура(РезультатВыбора) Тогда
		Возврат;
	КонецЕсли;
	
	ПравильноеЗначение = Новый Структура("ВыбранныеЗначения");
	ЗаполнитьЗначенияСвойств(ПравильноеЗначение, РезультатВыбора);
	Если ТипЗнч(ПравильноеЗначение.ВыбранныеЗначения) <> Тип("Массив")
		Или ПравильноеЗначение.ВыбранныеЗначения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Вариант = ВариантПодбораОКВЭД_ВыборВПоле() Тогда
		
		ВыбранноеЗначение = ПоискКонтрагентовКлиент.ВыбранныйОКВЭДВЗначениеСправочника(
			ПравильноеЗначение.ВыбранныеЗначения[0]);
		
		ВыбранноеЗначениеВСписок(
			ВыбранноеЗначение,
			ВыбранноеЗначение.Представление);
		
	ИначеЕсли ДополнительныеПараметры.Вариант = ВариантПодбораОКВЭД_Подбор() Тогда
		Для Каждого ЗначениеОКВЭД Из ПравильноеЗначение.ВыбранныеЗначения Цикл
			
			ВыбранноеЗначение = ПоискКонтрагентовКлиент.ВыбранныйОКВЭДВЗначениеСправочника(
				ЗначениеОКВЭД);
			
			НоваяСтрока               = ВыбранныеЗначения.Добавить();
			НоваяСтрока.Значение      = ВыбранноеЗначение;
			НоваяСтрока.Представление = ВыбранноеЗначение.Представление;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранноеЗначениеВСписок(Значение, Представление)
	
	ТекущиеДанные = Элементы.ВыбранныеЗначения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные.Значение      = Значение;
	ТекущиеДанные.Представление = Представление;
	
КонецПроцедуры

#КонецОбласти
