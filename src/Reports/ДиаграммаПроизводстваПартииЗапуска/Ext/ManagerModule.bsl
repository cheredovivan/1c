#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует отчет.
//
// Параметры:
//  Параметры  - Структура - параметры формирования отчета:
//		* ПартияПроизводства - СправочникСсылка.ПартииПроизводства - партия производства,
//			которую необходимо вывести в отчет.
//		* СтатусГрафика - Число - статус отображаемого графика.
//		* ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2
//		* КлючПартия - УникальныйИдентификатор
//  АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат формирования отчета.
//
Процедура СформироватьОтчет(Параметры, АдресХранилища) Экспорт
	
	ДиаграммаГанта = Новый ДиаграммаГанта;
	НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта);
	
	Границы = Новый Структура("Начало, Окончание", '39991231', '00010101');
	
	ПолучитьДанныеИВывестиВДиаграмму(
		ДиаграммаГанта,
		Параметры,
		Границы);
	
	НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы);
	
	ПоместитьВоВременноеХранилище(ДиаграммаГанта, АдресХранилища);
	
КонецПроцедуры

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений.
//  - Неопределено.
//
Функция ДобавитьКомандуГрафикПартииЗапуска(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДиаграммаПроизводстваПартииЗапуска)
		И УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор      = Метаданные.Отчеты.ДиаграммаПроизводстваПартииЗапуска.ПолноеИмя();
		КомандаОтчет.Представление      = НСтр("ru = 'График партии запуска';
												|en = 'Starting lot schedule'");
		КомандаОтчет.ИмяФормы           = "Отчет.ДиаграммаПроизводстваПартииЗапуска.Форма";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность           = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ДиаграммаПроизводстваПартииЗапуска);
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПолучитьДанныеИВывестиВДиаграмму(ДиаграммаГанта, Параметры, Границы)

	ДиаграммаГанта.Обновление = Ложь;
	
	Данные = ГрафикИЗависимостиПоЭтапамЦепочки(Параметры);
	
	Если Данные.Этапы.Количество() > 0 Тогда
		
		Серия = СерияПоУмолчанию(ДиаграммаГанта);
		
		Интервалы = Новый Соответствие;
		Для каждого Строка Из Данные.Этапы Цикл
			
			Границы.Начало = МИН(Границы.Начало, Строка.Начало);
			Границы.Окончание = МАКС(Границы.Окончание, Строка.Окончание);
			
			Точка = УстановитьТочкуЭтапЦепочки(ДиаграммаГанта, Строка);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			Интервал = ДобавитьИнтервалЭтапЦепочки(Значение, Строка);
			
			Интервалы.Вставить(Строка.Этап, Интервал);
			
		КонецЦикла;
		
		// Связи между этапами.
		Для каждого Строка Из Данные.Зависимости Цикл
			
			Интервал = Интервалы.Получить(Строка.Этап); // ИнтервалДиаграммыГанта - 
			СледующийИнтервал = Интервалы.Получить(Строка.СледующийЭтап);
			
			Если НЕ Интервал = Неопределено
				И НЕ СледующийИнтервал = Неопределено Тогда
				
				Связь = Интервал.Добавить(СледующийИнтервал);
				Связь.ТипСвязи = ТипСвязиДиаграммыГанта.КонецНачало;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	
КонецПроцедуры

// Возвращает график и зависимости по этапам цепочки
// 
// Параметры:
//  Параметры - Структура - Параметры:
// * ПартияПроизводства - СправочникСсылка.ПартииПроизводства - партия.
// * СтатусГрафика - Число - статус графика производства.
// * ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - 
// * КлючПартия - УникальныйИдентификатор - 
// 
// Возвращаемое значение:
//  Структура - содержит:
//   * Этапы - ТаблицаЗначений - содержит:
//   * Зависимости - ТаблицаЗначений - 
Функция ГрафикИЗависимостиПоЭтапамЦепочки(Параметры)
	
	ТекстыЗапросовПакета = Новый Массив;
	
	Если Параметры.СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		ТекстЗапроса =	
		"ВЫБРАТЬ
		|	Т.Этап,
		|	Т.Начало КАК Начало,
		|	Т.Окончание КАК Окончание,
		|	&КлючПартия КАК КлючПартия,
		|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
		|	ВЫБОР
		|		КОГДА Т.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Завершен,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Т.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА Т.Этап.РучноеРазмещениеВГрафике
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучноеРазмещение,
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.Порядок) КАК ПолеУпорядочивания
		|ПОМЕСТИТЬ ДанныеЭтапов
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.Этап = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
		|ГДЕ
		|	(Т.Этап.ПартияПроизводства = &ПартияПроизводства
		|	ИЛИ Т.ЗаказНаПроизводство = &ЗаказНаПроизводство И Т.КлючПартия = &КлючПартия)
		|	И Т.Этап <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|	И Т.СтатусГрафика = &СтатусГрафика";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап,
		|	ГрафикЭтаповПроизводства2_2.Начало КАК Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание КАК Окончание,
		|	ГрафикЭтаповПроизводства2_2.Порядок
		|ПОМЕСТИТЬ ВТГрафикПоСтатусу
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	(ГрафикЭтаповПроизводства2_2.Этап.ПартияПроизводства = &ПартияПроизводства
		|	ИЛИ ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство И ГрафикЭтаповПроизводства2_2.КлючПартия = &КлючПартия)
		|	И ГрафикЭтаповПроизводства2_2.Этап <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Этап,
		|	Т.Начало,
		|	Т.Окончание,
		|	&КлючПартия КАК КлючПартия,
		|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
		|	Т.Завершен,
		|	Т.РучноеРазмещение,
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.Порядок)
		|ПОМЕСТИТЬ ДанныеЭтапов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТГрафикПоСтатусу.Этап КАК Этап,
		|		ВТГрафикПоСтатусу.Начало КАК Начало,
		|		ВТГрафикПоСтатусу.Окончание КАК Окончание,
		|		ВЫБОР
		|			КОГДА ВТГрафикПоСтатусу.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК Завершен,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ВТГрафикПоСтатусу.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|				ТОГДА ВТГрафикПоСтатусу.Этап.РучноеРазмещениеВГрафике
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК РучноеРазмещение,
		|		ВТГрафикПоСтатусу.Порядок КАК Порядок
		|	ИЗ
		|		ВТГрафикПоСтатусу КАК ВТГрафикПоСтатусу
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГрафикЭтаповПроизводства2_2.Этап,
		|		ГрафикЭтаповПроизводства2_2.Начало,
		|		ГрафикЭтаповПроизводства2_2.Окончание,
		|		ВЫБОР
		|			КОГДА ГрафикЭтаповПроизводства2_2.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ГрафикЭтаповПроизводства2_2.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|				ТОГДА ГрафикЭтаповПроизводства2_2.Этап.РучноеРазмещениеВГрафике
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ГрафикЭтаповПроизводства2_2.Порядок
		|	ИЗ
		|		РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|	ГДЕ
		|		ГрафикЭтаповПроизводства2_2.Этап.ПартияПроизводства = &ПартияПроизводства
		|		И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик
		|		И НЕ ГрафикЭтаповПроизводства2_2.Этап В
		|					(ВЫБРАТЬ
		|						ВТГрафикПоСтатусу.Этап
		|					ИЗ
		|						ВТГрафикПоСтатусу)) КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.Этап = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа("Т.Этап"));
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("КлючПартия", ?(ЗначениеЗаполнено(Параметры.КлючПартия), Параметры.КлючПартия, Параметры.ПартияПроизводства.УникальныйИдентификатор()));
	Запрос.УстановитьПараметр("ПартияПроизводства", Параметры.ПартияПроизводства);
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", Параметры.ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", Параметры.СтатусГрафика);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик());
	
	Запрос.Выполнить();
	
	УправлениеПроизводством.СоздатьВТСвязиЭтапов(МенеджерВременныхТаблиц, "ДанныеЭтапов", "Этап", "КлючПартия", Истина, Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеЭтапов.Этап,
	|	ДанныеЭтапов.Начало КАК Начало,
	|	ДанныеЭтапов.Окончание КАК Окончание,
	|	ДанныеЭтапов.ПредставлениеЭтапа КАК ПредставлениеЭтапа,
	|	ДанныеЭтапов.Завершен КАК Завершен,
	|	ДанныеЭтапов.РучноеРазмещение КАК РучноеРазмещение
	|ИЗ
	|	ДанныеЭтапов КАК ДанныеЭтапов
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеЭтапов.ПолеУпорядочивания";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТСвязиЭтапов.Этап КАК Этап,
	|	ВТСвязиЭтапов.СледующийЭтап КАК СледующийЭтап
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Разделитель =
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
	
	Запрос.Текст = ТекстЗапроса;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	Результат.Вставить("Этапы", МассивРезультатов[МассивРезультатов.ВГраница()-1].Выгрузить());
	Результат.Вставить("Зависимости", МассивРезультатов[МассивРезультатов.ВГраница()].Выгрузить());
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьТочкуЭтапЦепочки(ДиаграммаГанта, Данные)
	
	Результат = ДиаграммаГанта.УстановитьТочку(Данные.Этап);
	Результат.Текст = Данные.ПредставлениеЭтапа;
	Результат.Расшифровка = Данные.Этап;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьИнтервалЭтапЦепочки(Значение, Данные)
	
	Результат = Значение.Добавить();
	Результат.Начало = Данные.Начало;
	Результат.Конец = Данные.Окончание;
	Результат.Текст = ТекстИнтервалаДиаграммы(
		Данные.Начало, Данные.Окончание);
	Результат.Расшифровка = Данные.Этап;
	Результат.Цвет = ЦветИнтервалаЭтапЦепочки(Данные.Завершен, Данные.РучноеРазмещение);
	
	Возврат Результат;
	
КонецФункции

Функция ЦветИнтервалаЭтапЦепочки(ЭтапЗавершен, РучноеРазмещение)
	
	Если ЭтапЗавершен Тогда
		Результат = ЦветаСтиля.ЦветФонаЭтапЗавершен;
	Иначе
		Если НЕ РучноеРазмещение Тогда
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированАвтоматически;
		Иначе
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированВручную;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта)
	
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	ДиаграммаГанта.Окантовка = Ложь;
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДиаграммаГанта.ВертикальнаяПрокрутка = Истина;
	ДиаграммаГанта.ОтображатьПустыеЗначения = Ложь;
	ДиаграммаГанта.ОтображатьЗаголовок = Ложь;
	ДиаграммаГанта.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОбластьПостроения.Право = 1;
	ДиаграммаГанта.ОтображениеТекстаИнтервала = ОтображениеТекстаИнтервалаДиаграммыГанта.НеОтображать;
	
КонецПроцедуры

Процедура НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы)
	
	Если НЕ ЗначениеЗаполнено(Границы.Окончание) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиШкалы = УправлениеПроизводствомКлиентСервер.НастройкиШкалыДиаграммыГантаВРежимеВсеДанные(
		Границы.Начало, Границы.Окончание, 9);
	
	ШкалаВремениЭлементы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	
	Для Индекс = 1 По ШкалаВремениЭлементы.Количество()-1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[Индекс]);
	КонецЦикла;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = НастройкиШкалы.Единица;
	ЭлементШкалы.Формат = НастройкиШкалы.Формат;
	
	Если ШкалаВремениЭлементы.Количество() = 2 Тогда
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[0]);
	КонецЕсли;
	
	ДиаграммаГанта.УстановитьПолныйИнтервал(
		НастройкиШкалы.НачалоПолногоИнтервала,
		НастройкиШкалы.ОкончаниеПолногоИнтервала);
	
КонецПроцедуры

Функция СерияПоУмолчанию(ДиаграммаГанта)
	
	Возврат ДиаграммаГанта.УстановитьСерию("ГрафикПроизводства");
	
КонецФункции

Функция ТекстИнтервалаДиаграммы(Начало, Окончание)
	
	ФорматнаяСтрока = УправлениеПроизводством.ФорматнаяСтрокаДляДатыГрафикаПроизводства();
	ФорматНачало = Формат(Начало, ФорматнаяСтрока);
	ФорматОкончание = Формат(Окончание, ФорматнаяСтрока);
	
	Если ФорматНачало = ФорматОкончание Тогда
		Результат = ФорматНачало;
	Иначе
		Результат = ФорматНачало + " - " + ФорматОкончание;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

#КонецОбласти

#КонецЕсли