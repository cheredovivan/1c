#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - команда отчета
Функция ДобавитьКомандуКонтрольПередачТоваров(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.КонтрольПередачТоваровМеждуОрганизациями) 
			И (ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями")
			//++ НЕ УТ

			//++ Устарело_Переработка24
				ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне")
			//-- Устарело_Переработка24

			//-- НЕ УТ
				) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.КонтрольПередачТоваровМеждуОрганизациями.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Контроль передач товаров';
											|en = 'Goods transfer control'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ОформлениеПередачТоваровКонтекст";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  Неопределено, СтрокаТаблицыЗначений - команда отчета
Функция ДобавитьКомандуОформленияОтчетов(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.КонтрольПередачТоваровМеждуОрганизациями) 
			И (ПолучитьФункциональнуюОпцию("ИспользоватьПередачиТоваровМеждуОрганизациями")
			//++ НЕ УТ

			//++ Устарело_Переработка24
				ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоНаСтороне")
			//-- Устарело_Переработка24

			//-- НЕ УТ
				) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.КонтрольПередачТоваровМеждуОрганизациями.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Оформление отчетов';
											|en = 'Register reports'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ОформлениеОтчетовПоКомиссииКонтекст";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.КонтрольПередачТоваровМеждуОрганизациями);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'Все ли передачи товаров оформлены?
		|Все ли отчеты комиссии оформлены?
		|Каковы остатки комиссионных товаров?';
		|en = 'Check if all goods transfers are completed.
		|Verify if all consignment notifications have been completed.
		|Track the consignment goods balances.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОформлениеОтчетовПоКомиссии");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ товаров, которые были переданы на комиссию другой организации.
		|Какие получатели еще не отчитались?';
		|en = 'Analyze consignment goods handed over to another company.
		|Verify which recipients have not reported yet.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОформлениеПередачТоваров");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ товаров, проданных по схеме интеркампани.
		|Кто отправитель и получатель?';
		|en = 'Analysis of goods sold under the intercompany scheme. 
		|Who is the sender and the recipient?'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ТоварыНаКомиссии");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ имеющихся остатков комиссионных товаров организации.
		|От каких организаций приняты товары на комиссию?';
		|en = 'Analyze available stock of company consignment goods.
		|Verify, from which companies consignment goods have been received.'");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ОформлениеПередачТоваровКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ОформлениеОтчетовПоКомиссииКонтекст");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли