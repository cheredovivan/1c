#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчет "Состояние выполнения документов" путем выполнения пакета запросов. 
// 
// Параметры:
// ВходящиеДокументы - СписокЗначений из ДокументСсылка - Список ссылок на документы.
// ТаблицаОтчета - ТабличныйДокумент - Табличный документ отчета.
//
Процедура СформироватьОтчетСостояниеВыполненияДокументов(ВходящиеДокументы, ТаблицаОтчета) Экспорт
	
	ТаблицаОтчета.АвтоМасштаб = Истина;
	ТаблицаОтчета.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СОСТОЯНИЕВЫПОЛНЕНИЯДОКУМЕНТОВ";
	
	ИмяТаблицы = ОбщегоНазначения.ИмяТаблицыПоСсылке(ВходящиеДокументы[0].Значение);
	МенеджерОбъекта = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяТаблицы);
	СтруктураПараметров = МенеджерОбъекта.СтруктураСостояниеВыполненияДокумента(); //см. ИнициализироватьСтруктуруСостояниеВыполненияДокумента
	
	Макет = ПолучитьМакет("Макет");
	
	Запрос = Новый Запрос;
	
	// Установка параметров запроса.
	Запрос.УстановитьПараметр("МассивДокументов", ВходящиеДокументы.ВыгрузитьЗначения());
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	Запрос.УстановитьПараметр("ТекстПоляСписаниеВПути", НСтр("ru = 'Списано в пути';
															|en = 'Written off in transit'"));
	Запрос.УстановитьПараметр("ТекстПоляПоступлениеИзПути", НСтр("ru = 'Возвращено из пути';
																|en = 'Returned from transit'"));
	
	// Формирование текстов запроса.
	ТекстыЗапроса = Новый СписокЗначений;
	ТекстыЗапроса.Добавить(ТекстЗапросаВтДокументы(ИмяТаблицы, СтруктураПараметров), "ВтДокументы");
	ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаДокументы(СтруктураПараметров), "ТаблицаДокументы");
	ТекстыЗапроса.Добавить(ТекстЗапросаВТУпорядочиваниеНХС(), "ВТУпорядочиваниеНХС");
	ТекстыЗапроса.Добавить(ТекстЗапросаВТУпорядочиваниеНХ(), "ВТУпорядочиваниеНХ");
	
	ВсегоТаблиц = 0;
	ВыводитьТаблицуРасчетов = Ложь;
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	ТипыЗаписейВзаиморасчетовЗаполнены = Константы.УдалитьТипыЗаписейВзаиморасчетовЗаполнены.Получить();
	
	Если СтруктураПараметров.ВыводитьТаблицуРасчетыСКлиентами <> 0
		 И ЕстьПравоДоступаКНаборуДанных("РасчетыСКлиентами", СтруктураПараметров) Тогда
		
		ТекстыЗапроса.Добавить(ТекстЗапросаВтОбъектыРасчетов(ИмяТаблицы, СтруктураПараметров), "ВтОбъектыРасчетов");
		Если Не (НоваяАрхитектураВзаиморасчетов 
				И ТипыЗаписейВзаиморасчетовЗаполнены) Тогда
			Запрос.УстановитьПараметр("Отгрузка", НСтр("ru = 'Отгрузка';
														|en = 'Shipment'"));
			ТекстыЗапроса.Добавить(ТекстЗапросаВтДанныеПоРасчетамСКлиентами(ИмяТаблицы, СтруктураПараметров), "ВтДанныеПоРасчетамСКлиентами");
			ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаРасчетыСКлиентами(СтруктураПараметров), "ТаблицаРасчетыСКлиентами");
		КонецЕсли;
		ВыводитьТаблицуРасчетов = Истина;
		ВсегоТаблиц = ВсегоТаблиц + 1;
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицуРасчетыСПоставщиками <> 0
		 И ЕстьПравоДоступаКНаборуДанных("РасчетыСПоставщиками", СтруктураПараметров) Тогда
		
		Если НЕ ВыводитьТаблицуРасчетов Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаВтОбъектыРасчетов(ИмяТаблицы, СтруктураПараметров), "ВтОбъектыРасчетов");
		КонецЕсли;
		
		Если Не (НоваяАрхитектураВзаиморасчетов 
				И ТипыЗаписейВзаиморасчетовЗаполнены) Тогда
			Запрос.УстановитьПараметр("Поступление", НСтр("ru = 'Поступление';
															|en = 'Receipt'"));
			ТекстыЗапроса.Добавить(ТекстЗапросаВтДанныеПоРасчетамСПоставщиками(ИмяТаблицы, СтруктураПараметров), "ВтДанныеПоРасчетамСПоставщиками");
			ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаРасчетыСПоставщиками(СтруктураПараметров), "ТаблицаРасчетыСПоставщиками");
		КонецЕсли;
		ВыводитьТаблицуРасчетов = Истина;
		ВсегоТаблиц = ВсегоТаблиц + 1;
	КонецЕсли;
	
	Если ВыводитьТаблицуРасчетов Тогда
		
		Если Не НоваяАрхитектураВзаиморасчетов
			Или Не ТипыЗаписейВзаиморасчетовЗаполнены Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаГрафикиОплаты(ИмяТаблицы, СтруктураПараметров), "ТаблицаГрафикиОплаты");
		КонецЕсли;
		Если СтруктураПараметров.ЭтоЗаказ Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаПодчиненныеДокументы(ИмяТаблицы, СтруктураПараметров), "ТаблицаПодчиненныеДокументы");
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицыОбеспечение <> 0 Тогда
		Запрос.УстановитьПараметр("МассивСтатусовБезОтгрузки", СтатусыБезОтгрузки(ИмяТаблицы));
		
		ТекстЗапросаТаблицаНеобеспеченныеТовары = ТекстЗапросаТаблицаНеобеспеченныеТовары(ИмяТаблицы, СтруктураПараметров);
		ТекстЗапросаТаблицаСостояниеОбеспечения = ТекстЗапросаТаблицаСостояниеОбеспечения(ИмяТаблицы, СтруктураПараметров);
		
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаНеобеспеченныеТовары, "ТаблицаНеобеспеченныеТовары");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаСостояниеОбеспечения, "ТаблицаСостояниеОбеспечения");
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицыОтгрузка <> 0 И ЕстьПравоДоступаКНаборуДанных("Отгрузка", СтруктураПараметров) Тогда
		Запрос.УстановитьПараметр("ИспользоватьРасширенныеВозможностиЗаказаКлиента",
			ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
		
		ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыКОтгрузке(СтруктураПараметров), "ВТТоварыКОтгрузке");
		Если СтруктураПараметров.ЭтоЗаказ Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыРаспоряжениеЗаказОтгружено(ИмяТаблицы, СтруктураПараметров, ТекстыЗапроса), "ВТТоварыРаспоряжениеЗаказОтгружено");
		КонецЕсли;
		Если СтруктураПараметров.ЭтоНакладная Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено(ИмяТаблицы, СтруктураПараметров), "ВТТоварыРаспоряжениеНакладнаяОтгружено");
		КонецЕсли;
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаОтгрузка(СтруктураПараметров), "ТаблицаОтгрузка");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаОтгружено(СтруктураПараметров), "ТаблицаОтгружено");
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности")
		И СтруктураПараметров.ВыводитьТаблицуППС Тогда
		
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаППС(СтруктураПараметров), "ТаблицаППС");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаППСОформлено(СтруктураПараметров), "ТаблицаППСОформлено");
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицуЗаказано Тогда
		
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаЗаказано(ИмяТаблицы, СтруктураПараметров), "ТаблицаЗаказано");
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицыПоступление <> 0 И ЕстьПравоДоступаКНаборуДанных("Поступление", СтруктураПараметров) Тогда
		ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыПоступление(СтруктураПараметров), "ВТТоварыПоступление");
		Если СтруктураПараметров.ЭтоЗаказ Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыРаспоряженияПоступлениеЗаказ(ИмяТаблицы, СтруктураПараметров, ТекстыЗапроса), "ВТТоварыРаспоряженияПоступлениеЗаказ");
		КонецЕсли;
		Если СтруктураПараметров.ЭтоНакладная Тогда
			ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыРаспоряженияПоступлениеНакладная(ИмяТаблицы, СтруктураПараметров), "ВТТоварыРаспоряженияПоступлениеНакладная");
		КонецЕсли;
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаПоступление(СтруктураПараметров), "ТаблицаПоступление");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаПоступило(СтруктураПараметров), "ТаблицаПоступило");
		ВсегоТаблиц = ВсегоТаблиц + 1;
		Запрос.УстановитьПараметр("СписокХозоперацийРаздельнойЗакупки", ЗакупкиСервер.ХозяйственныеОперацииРаздельнойЗакупкиБезОтборов());
	КонецЕсли;
	
	//++ НЕ УТ

	//++ НЕ УТКА

	Если СтруктураПараметров.ВыводитьТаблицуУслугДавальцуКОформлению <> 0
		И ЕстьПравоДоступаКНаборуДанных("УслугиДавальцуКОформлению", СтруктураПараметров) Тогда
		ТекстыЗапроса.Добавить(ТекстЗапросаВТПродукцияПоЗаказуДавальца(), "ВТПродукцияПоЗаказуДавальца");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаУслугиДавальцуКОформлению(), "ТаблицаУслугиДавальцуКОформлению");
		ВсегоТаблиц = ВсегоТаблиц + 1;
	КонецЕсли;
	
	//-- НЕ УТКА

	//-- НЕ УТ
	
	Если СтруктураПараметров.ВыводитьТаблицыОтмененоОтгрузка <> 0 Тогда
		
		ТекстЗапросаТаблицаВТОтменено = ТекстЗапросаТаблицаВТОтмененоОтгрузка(ИмяТаблицы, СтруктураПараметров);
		ТекстЗапросаТаблицаОтменено = ТекстЗапросаТаблицаОтмененоОтгрузка(ИмяТаблицы, СтруктураПараметров);
		
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаВТОтменено, "ВТОтмененоОтгрузка");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаОтменено, "ТаблицаОтмененоОтгрузка");

		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицыОтмененоПоступление <> 0 Тогда
		
		ТекстЗапросаТаблицаОтменено = ТекстЗапросаТаблицаОтмененоПоступление(ИмяТаблицы, СтруктураПараметров);
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаОтменено, "ТаблицаОтмененоПоступление");
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	Если СтруктураПараметров.ВыводитьТаблицыВозврат <> 0
		И ЕстьПравоДоступаКНаборуДанных("ВозвратыТоваров", СтруктураПараметров) Тогда
		
		ТекстЗапросаТаблицаВозвратТоваров = ТекстЗапросаТаблицаВозвратТоваров(ИмяТаблицы, СтруктураПараметров);
		ТекстЗапросаТаблицаВозвращеноТоваров = ТекстЗапросаТаблицаВозвращеноТоваров(ИмяТаблицы, СтруктураПараметров);
		
		ТекстыЗапроса.Добавить(ТекстЗапросаВТВозвращаемыеТовары(СтруктураПараметров), "ВТВозвращаемыеТовары");
		ТекстыЗапроса.Добавить(ТекстЗапросаВТТоварыКВозврату(СтруктураПараметров), "ВТТоварыКВозврату");
		
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаВозвратТоваров, "ТаблицаВозвратТоваров");
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаВозвращеноТоваров, "ТаблицаВозвращеноТоваров");
		
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	
	//++ НЕ УТ
	Если СтруктураПараметров.ВыводитьТаблицуПродукцияКОформлениюВОтчетеПереработчику <> 0 
		И ЕстьПравоДоступаКНаборуДанных("ПродукцияКОформлениюВОтчетеПереработчику", СтруктураПараметров) Тогда
		ТекстыЗапроса.Добавить(ТекстЗапросаТаблицаПродукцияКОформлениюВОтчетеПереработчику(СтруктураПараметров), "ТаблицаПродукцияКОформлениюВОтчетеПереработчику");
		ВсегоТаблиц = ВсегоТаблиц + 1;
	КонецЕсли;
	Если СтруктураПараметров.ВыводитьТаблицуСырьеУПереработчика <> 0
		И ЕстьПравоДоступаКНаборуДанных("СырьеУПереработчика", СтруктураПараметров) Тогда
		
		ТекстЗапросаТаблицаСырьеУПереработчика(ТекстыЗапроса, СтруктураПараметров);
		
		ВсегоТаблиц = ВсегоТаблиц + 1;
		
	КонецЕсли;
	//-- НЕ УТ
	
	Для каждого ТекстЗапроса Из ТекстыЗапроса Цикл
		Если ПустаяСтрока(ТекстЗапроса.Значение) Тогда
			ТекстыЗапроса.Удалить(ТекстЗапроса);
		КонецЕсли;
	КонецЦикла; 
	
	// Инициализация таблиц запроса.
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТаблицыРезультатов = ПолучитьТаблицуРезультатовЗапроса(Запрос, ТекстыЗапроса);
	
	Если НоваяАрхитектураВзаиморасчетов
		И ТипыЗаписейВзаиморасчетовЗаполнены
		И (СтруктураПараметров.ВыводитьТаблицуРасчетыСКлиентами <> 0
			Или СтруктураПараметров.ВыводитьТаблицуРасчетыСПоставщиками <> 0) Тогда
		ТаблицыРасчетов = ПолучитьДанныеРасчетов(
			Запрос.МенеджерВременныхТаблиц,
			СтруктураПараметров.ВыводитьТаблицуРасчетыСКлиентами <> 0
				И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСКлиентами),
			СтруктураПараметров.ВыводитьТаблицуРасчетыСПоставщиками <> 0
				И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСПоставщиками));
	КонецЕсли;
	
	// Вывод результатов в табличный документ.
	НомерТипаДокумента = 0;
	ТаблицаДокументов = ТаблицыРезультатов.ТаблицаДокументы.Строки;
	
	Для каждого СтрокаДокумент Из ТаблицаДокументов Цикл
		
		НомерТипаДокумента = НомерТипаДокумента + 1;
		Если НомерТипаДокумента > 1 Тогда
			ТаблицаОтчета.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ЕстьДанные = Ложь;
		
		ВывестиЗаголовокОтчета(ТаблицаОтчета, Макет, СтрокаДокумент);
		Для НомерТаблицы = 1 По ВсегоТаблиц Цикл
			
			Если СтруктураПараметров.ВыводитьТаблицуРасчетыСКлиентами = НомерТаблицы Тогда
				
				Если НоваяАрхитектураВзаиморасчетов
					И ТипыЗаписейВзаиморасчетовЗаполнены Тогда
					ЕстьДанные = ВывестиТаблицуРасчетовПоОплатамОтгрузкам(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРасчетов, Истина)
						Или ЕстьДанные;
				Иначе
				ЕстьДанные = ВывестиТаблицуРасчетов(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, "ТаблицаРасчетыСКлиентами")
					Или ЕстьДанные;
				КонецЕсли;
				ЕстьДанные = ВывестиТаблицуПодчиненныеДокументы(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов) 
					Или ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуРасчетыСПоставщиками = НомерТаблицы Тогда
				
				Если НоваяАрхитектураВзаиморасчетов
					И ТипыЗаписейВзаиморасчетовЗаполнены Тогда
					ЕстьДанные = ВывестиТаблицуРасчетовПоОплатамОтгрузкам(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРасчетов, Ложь)
						Или ЕстьДанные;
				Иначе
				ЕстьДанные = ВывестиТаблицуРасчетов(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, "ТаблицаРасчетыСПоставщиками")
					Или ЕстьДанные;
				КонецЕсли;
				ЕстьДанные = ВывестиТаблицуПодчиненныеДокументы(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов)
					Или ЕстьДанные;
					
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуЗаказано = НомерТаблицы Тогда
				
				ЕстьДанные = ВывестиТаблицуЗаказано(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицыПоступление = НомерТаблицы Тогда
				
				ЕстьДанныеПоступление = ВывестиТаблицуПоступление(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				ЕстьДанныеПоступление = ВывестиТаблицуПоступило(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанныеПоступление;
				ЕстьДанныеПоступление = ВывестиТаблицуОтмененоПоступление(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров) 
					Или ЕстьДанныеПоступление;
				
				ЕстьДанные = ЕстьДанныеПоступление ИЛИ ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицыОтгрузка = НомерТаблицы Тогда
				
				ЕстьДанныеОтгрузка = ВывестиТаблицуОтгрузка(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				ЕстьДанныеОтгрузка = ВывестиТаблицуОтгружено(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров) 
					Или ЕстьДанныеОтгрузка;
				ЕстьДанныеОтгрузка = ВывестиТаблицуОтмененоОтгрузка(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанныеОтгрузка;
				
				ЕстьДанные = ЕстьДанныеОтгрузка ИЛИ ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуППС = НомерТаблицы Тогда
				
				ЕстьДанныеППС = ВывестиТаблицуППС(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				ЕстьДанныеППС = ВывестиТаблицуППСОформлено(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанныеППС;
				
				Если ЕстьДанныеППС Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли; 
				
				ЕстьДанные = ЕстьДанныеППС
					Или ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицыОбеспечение = НомерТаблицы Тогда	
				
				ЕстьДанныеОбеспечение = ВывестиТаблицуНеобеспеченныеТовары(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				ЕстьДанныеОбеспечение = ВывестиТаблицуСостояниеОбеспечения(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					ИЛИ ЕстьДанныеОбеспечение;
					
				Если ЕстьДанныеОбеспечение Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли;
				
				ЕстьДанные = ЕстьДанныеОбеспечение ИЛИ ЕстьДанные;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицыВозврат = НомерТаблицы Тогда
				
				ЕстьДанныеВозврат = ВывестиТаблицуВозвратТоваров(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				ЕстьДанныеВозврат = ВывестиТаблицуВозвращеноТоваров(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанныеВозврат;
				ЕстьДанныеВозврат = ВывестиТаблицуОтмененоПоступление(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров)
					Или ЕстьДанныеВозврат;
				Если ЕстьДанныеВозврат Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли;
				
				ЕстьДанные = ЕстьДанныеВозврат ИЛИ ЕстьДанные;
				
			//++ НЕ УТ

			//++ НЕ УТКА
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуУслугДавальцуКОформлению = НомерТаблицы Тогда	
			
				ЕстьДанныеДавальцы = ВывестиТаблицуУслугиДавальцуКОформлению(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				Если ЕстьДанныеДавальцы Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли;
				ЕстьДанные = ЕстьДанныеДавальцы ИЛИ ЕстьДанные;
			//-- НЕ УТКА
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуПродукцияКОформлениюВОтчетеПереработчику = НомерТаблицы Тогда	
				
				ЕстьДанные = ВывестиТаблицуПродукцияКОформлениюВОтчетеПереработчику(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				Если ЕстьДанные Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли;
				
			ИначеЕсли СтруктураПараметров.ВыводитьТаблицуСырьеУПереработчика = НомерТаблицы Тогда	
			
				ЕстьДанные = ВывестиТаблицуСырьеУПереработчика(ТаблицаОтчета, Макет, СтрокаДокумент.Ссылка, ТаблицыРезультатов, СтруктураПараметров);
				Если ЕстьДанные Тогда
					ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
				КонецЕсли;
			//-- НЕ УТ
			КонецЕсли;
			
		КонецЦикла;
		
		ВывестиТаблицуНетДанных(ТаблицаОтчета, Макет, СтрокаДокумент, ЕстьДанные);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
// 	Запрос - Запрос
// 	ТекстыЗапроса - СписокЗначений
// 	
// Возвращаемое значение:
// 	Структура:
// 		* ТаблицаДокументы                                  - ДеревоЗначений:
// 			** Ссылка - ДокументСсылка
// 		* ТаблицаРасчетыСПоставщиками                       - ДеревоЗначений
// 		* ТаблицаРасчетыСКлиентами                          - ДеревоЗначений
// 		* ТаблицаГрафикиОплаты                              - ДеревоЗначений
// 		* ТаблицаПодчиненныеДокументы                       - ДеревоЗначений
// 		* ТаблицаНеобеспеченныеТовары                       - ДеревоЗначений
// 		* ТаблицаСостояниеОбеспечения                       - ДеревоЗначений
// 		* ТаблицаОтгрузка                                   - ДеревоЗначений
// 		* ТаблицаОтгружено                                  - ДеревоЗначений
// 		* ТаблицаППС                                        - ДеревоЗначений
// 		* ТаблицаППСОформлено                               - ДеревоЗначений
// 		* ТаблицаЗаказано                                   - ДеревоЗначений
// 		* ТаблицаПоступление                                - ДеревоЗначений
// 		* ТаблицаПоступило                                  - ДеревоЗначений
// 		* ТаблицаУслугиДавальцуКОформлению                  - ДеревоЗначений
// 		* ТаблицаПродукцияКОформлениюВОтчетеПереработчику   - ДеревоЗначений
// 		* ТаблицаОтмененоОтгрузка                           - ДеревоЗначений
// 		* ТаблицаОтмененоПоступление                        - ДеревоЗначений
// 		* ТаблицаВозвратТоваров                             - ДеревоЗначений
// 		* ТаблицаВозвращеноТоваров                          - ДеревоЗначений
//
Функция ПолучитьТаблицуРезультатовЗапроса(Запрос, ТекстыЗапроса)

	Возврат ОбщегоНазначенияУТ.ВыгрузитьРезультатыЗапроса(Запрос, ТекстыЗапроса, ОбходРезультатаЗапроса.ПоГруппировкам);
	
КонецФункции

// Осуществляет инициализацию структуры состояния выполнения документа.
// 
// Возвращаемое значение:
//  Структура - Инициализировать структуру состояние выполнения документа:
// * ВыводитьТаблицуРасчетыСКлиентами - Число -
// * ВыводитьТаблицуРасчетыСПоставщиками - Число -
// * ВыводитьТаблицыОбеспечение - Число -
// * ВыводитьТаблицыОтгрузка - Число -
// * ВыводитьТаблицыВозврат - Число -
// * ВыводитьТаблицыПоступление - Число -
// * ВыводитьТаблицыОтмененоОтгрузка - Число -
// * ВыводитьТаблицыОтмененоПоступление - Число -
// * ВыводитьТаблицуППС - Число -
// * ВыводитьТаблицуЗаказано - Число -
// * ВыводитьТаблицуУслугДавальцуКОформлению - Число -
// * ВыводитьТаблицуПродукцияКОформлениюВОтчетеПереработчику - Число -
// * ВыводитьТаблицуСырьеУПереработчика - Число -
// * ЭтоЗаказ - Булево -
// * ЭтоНакладная - Булево -
// * ЭтоПоступлениеТоваров - Булево -
// * СкладВШапке - Булево -
// * ЕстьСуммовыеПоказателиОтгрузки - Булево -
// * ЕстьСуммовыеПоказателиПоступления - Булево -
// * ЕстьПричиныОтменыОтгрузки - Булево -
// * ЕстьПричиныОтменыПоступления - Булево -
// * ИмяТЧТоварыОтгрузка - Строка -
// * ИмяТЧТоварыПоступление - Строка -
// * ИмяПоляДатаОтгрузки - Строка -
// * ИмяПоляСклад - Строка -
// * ИмяПоляСкладПолучатель - Строка -
// * ИмяПоляСумма - Строка -
// * ИмяРегистраОтгрузкаУслуг - Неопределено -
// * ИмяРегистраПоступлениеУслуг - Неопределено -
// * ИмяПоляНакладнаяПоЗаказам - Строка, Неопределено -
// * СтруктураДопЗапросов - Структура, Неопределено -
// * ДействиеРасшифровкаКоличествоОтгружено - Неопределено -
// * ДействиеРасшифровкаКоличествоПоступило - Неопределено -
// * ТекстТоварУслугаОтгрузка - Строка -
// * ТекстТоварУслугаПоступление - Строка -
// * ТекстОтмененоОтгрузка - Строка -
// * ТекстОтмененоПоступление - Строка -
Функция ИнициализироватьСтруктуруСостояниеВыполненияДокумента() Экспорт
	
	СтруктураСостояние = Новый Структура;
	
	СтруктураСостояние.Вставить("ВыводитьТаблицуРасчетыСКлиентами",           0); // 0 - не выводить, другое значение указывает порядок вывода таблицы
	СтруктураСостояние.Вставить("ВыводитьТаблицуРасчетыСПоставщиками",        0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыОбеспечение",                 0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыОтгрузка",                    0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыВозврат",                     0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыПоступление",                 0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыОтмененоОтгрузка",            0);
	СтруктураСостояние.Вставить("ВыводитьТаблицыОтмененоПоступление",         0);
	СтруктураСостояние.Вставить("ВыводитьТаблицуППС",                         0);
	СтруктураСостояние.Вставить("ВыводитьТаблицуЗаказано",                    0);
	//++ НЕ УТ

	//++ НЕ УТКА
	СтруктураСостояние.Вставить("ВыводитьТаблицуУслугДавальцуКОформлению",                 0);
	//-- НЕ УТКА
	СтруктураСостояние.Вставить("ВыводитьТаблицуПродукцияКОформлениюВОтчетеПереработчику", 0);
	СтруктураСостояние.Вставить("ВыводитьТаблицуСырьеУПереработчика", 0);
	//-- НЕ УТ
	
	СтруктураСостояние.Вставить("ЭтоЗаказ",                                   Ложь);
	СтруктураСостояние.Вставить("ЭтоНакладная",                               Ложь);
	СтруктураСостояние.Вставить("ЭтоПоступлениеТоваров",                      Ложь);

	СтруктураСостояние.Вставить("СкладВШапке",                                Ложь);
	
	СтруктураСостояние.Вставить("ЕстьСуммовыеПоказателиОтгрузки",             Ложь);
	СтруктураСостояние.Вставить("ЕстьСуммовыеПоказателиПоступления",          Ложь);
	СтруктураСостояние.Вставить("ЕстьПричиныОтменыОтгрузки",                  Ложь);
	СтруктураСостояние.Вставить("ЕстьПричиныОтменыПоступления",               Ложь);
	
	СтруктураСостояние.Вставить("ИмяТЧТоварыОтгрузка",                        "Товары");
	СтруктураСостояние.Вставить("ИмяТЧТоварыПоступление",                     "Товары");
	СтруктураСостояние.Вставить("ИмяПоляДатаОтгрузки",                        "ДатаОтгрузки");
	СтруктураСостояние.Вставить("ИмяПоляСклад",                               "Склад");
	СтруктураСостояние.Вставить("ИмяПоляСкладПолучатель",                     "Склад");
	СтруктураСостояние.Вставить("ИмяПоляСумма",                               "СуммаСНДС");
	
	СтруктураСостояние.Вставить("ИмяРегистраОтгрузкаУслуг",                   Неопределено);
	СтруктураСостояние.Вставить("ИмяРегистраПоступлениеУслуг",                Неопределено);
	СтруктураСостояние.Вставить("ИмяПоляНакладнаяПоЗаказам",                  Неопределено);
	
	СтруктураСостояние.Вставить("СтруктураДопЗапросов",                       Неопределено);
	
	СтруктураСостояние.Вставить("ДействиеРасшифровкаКоличествоОтгружено",     Неопределено);
	СтруктураСостояние.Вставить("ДействиеРасшифровкаКоличествоПоступило",     Неопределено);
	
	СтруктураСостояние.Вставить("СуффиксМакетаТаблицыПродукцияКОформлениюВОтчетеПереработчику", "");
	СтруктураСостояние.Вставить("СуффиксМакетаТаблицыСырьеУПереработчика", "");
	
	СтруктураСостояние.Вставить("ТекстТоварУслугаОтгрузка",    НСтр("ru = 'Товар (услуга)';
																	|en = 'Goods (service)'"));
	СтруктураСостояние.Вставить("ТекстТоварУслугаПоступление", НСтр("ru = 'Товар (услуга)';
																	|en = 'Goods (service)'"));
	СтруктураСостояние.Вставить("ТекстОтмененоОтгрузка",     НСтр("ru = 'Отменено (%1%)';
																	|en = 'Canceled (%1%)'"));
	СтруктураСостояние.Вставить("ТекстОтмененоПоступление",  НСтр("ru = 'Отменено (%1%)';
																	|en = 'Canceled (%1%)'"));
	
	Возврат СтруктураСостояние
	
КонецФункции

#Область СтандартныеПодсистемы
// СтандартныеПодсистемы.ВариантыОтчетов

// Настройки размещения в панели отчетов.
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки
//   ОписаниеОтчета - СтрокаДереваЗначений - Настройки этого отчета,
//       уже сформированные при помощи функции ВариантыОтчетов.ОписаниеОтчета() и готовые к изменению.
//       См. "Свойства для изменения" процедуры ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов().
//
// Описание:
//   См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов
//
// Вспомогательные методы:
//	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, Истина/Ложь);
//
// Примеры:
//
//  1. Установка описания варианта.
//	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//	НастройкиВарианта.Описание = НСтр("ru = '<Описание>'");
//
//  2. Отключение варианта отчета.
//	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "<ИмяВарианта>");
//	НастройкиВарианта.Включен = Ложь;
//
Процедура НастроитьВариантыОтчета(Настройки, ОписаниеОтчета) Экспорт
	
	// Отключение контекстных вариантов
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияЗаказКлиента(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.ФункциональныеОпции = "ИспользоватьРасширенныеВозможностиЗаказаКлиента";
		
		КомандаОтчет.ЕстьУсловияВидимости = Истина;
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет,
			"ЭтоЗаказКакСчет",
			Ложь, 
			ВидСравненияКомпоновкиДанных.Равно);
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияЗаказаДавальца(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияЗаявокНаВозврат(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.ФункциональныеОпции = "ИспользоватьРасширенныеВозможностиЗаказаКлиента";
		
		КомандаОтчет.ЕстьУсловияВидимости = Истина;
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(КомандаОтчет,
			"ЭтоЗаказКакСчет",
			Ложь, 
			ВидСравненияКомпоновкиДанных.Равно);
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияРеализацииАкта(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Обработчик = "МенюОтчетыУТКлиент.СостояниеВыполненияРеализацииАкта";
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияВнутреннихРаспоряжений(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияЗаказПереработчику(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияДокументЗакупки(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Fulfillment state'");
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.Важность = "Важное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений
Функция ДобавитьКомандуСостояниеВыполненияДоговора(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов) Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СостояниеВыполненияДокументов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Состояние выполнения';
											|en = 'Execution state'");
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Важное";
		ПодключаемыеКоманды.ДобавитьУсловиеВидимостиКоманды(
			КомандаОтчет, 
			"ГрафикИсполненияДоговора", 
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросов

Функция ТекстЗапросаВтДокументы(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВтДокументы") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВтДокументы;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Документ.Ссылка КАК ДокументСсылка,
	|	ТИПЗНАЧЕНИЯ(Документ.Ссылка) КАК ТипДокумента,
	|	Документ.Дата КАК ДатаДокумента,
	|	Документ.Проведен КАК Проведен,
	|	Документ.Номер КАК НомерДокумента
	|ПОМЕСТИТЬ
	|	ВтДокументы
	|ИЗ
	|	&ИмяТаблицы КАК Документ
	|ГДЕ
	|	Документ.Ссылка В (&МассивДокументов);
	|	
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаДокументы(СтруктураПараметров)
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаДокументы") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаДокументы;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документы.ДокументСсылка КАК Ссылка,
	|	Документы.ДатаДокумента КАК ДатаДокумента,
	|	Документы.Проведен КАК ДокументПроведен,
	|	Документы.НомерДокумента КАК НомерДокумента
	|ИЗ
	|	ВтДокументы КАК Документы;
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаПодчиненныеДокументы(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаПодчиненныеДокументы") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаПодчиненныеДокументы;
	КонецЕсли;
	
	ТекстЗапроса = "";
	МассивТекстовЗапроса = Новый Массив();
	
	Если (ИмяТаблицы = "Документ.ЗаказКлиента" 
			ИЛИ ИмяТаблицы = "Документ.ЗаявкаНаВозвратТоваровОтКлиента")
		И ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТчДокументРеализациии.Ссылка.Валюта КАК Валюта,
		|	ТчДокументРеализациии.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	ТчДокументРеализациии.Ссылка КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.РеализацияТоваровУслуг.Товары КАК ТчДокументРеализациии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТчДокументРеализациии.ЗаказКлиента = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ВтДокументы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТчДокументРеализациии.Ссылка.ПометкаУдаления
		|";

		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	Если (ИмяТаблицы = "Документ.ЗаказКлиента" 
			ИЛИ ИмяТаблицы = "Документ.ЗаявкаНаВозвратТоваровОтКлиента") 
		И ПравоДоступа("Чтение", Метаданные.Документы.АктВыполненныхРабот) Тогда
			
		ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка КАК Документ,
			|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
			|	ТчДокументРеализациии.Ссылка.Валюта КАК Валюта,
			|	ТчДокументРеализациии.Ссылка.СуммаДокумента КАК СуммаДокумента,
			|	ТчДокументРеализациии.Ссылка КАК ПодчиненныйДокумент
			|ИЗ
			|	Документ.АктВыполненныхРабот.Услуги КАК ТчДокументРеализациии
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
			|		ПО ТчДокументРеализациии.ЗаказКлиента = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	ВтДокументы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
			|	И НЕ ТчДокументРеализациии.Ссылка.ПометкаУдаления";
			
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" 
		И ПравоДоступа("Чтение", Метаданные.Документы.ПриобретениеТоваровУслуг) Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТчДокументРеализациии.Ссылка.Валюта КАК Валюта,
		|	ТчДокументРеализациии.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	ТчДокументРеализациии.Ссылка КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг.Товары КАК ТчДокументРеализациии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТчДокументРеализациии.ЗаказПоставщику = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ВтДокументы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТчДокументРеализациии.Ссылка.ПометкаУдаления
		|";

		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;

	//++ НЕ УТ

	//++ НЕ УТКА

	//++ Устарело_Переработка24
	Если ИмяТаблицы = "Документ.ЗаказДавальца" 
		И ПравоДоступа("Чтение", Метаданные.Документы.ОтчетДавальцу) Тогда

		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТчДокументРеализациии.Ссылка.Валюта КАК Валюта,
		|	ТчДокументРеализациии.Ссылка.СуммаДокумента КАК СуммаДокумента,
		|	ТчДокументРеализациии.Ссылка КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.ОтчетДавальцу.Продукция КАК ТчДокументРеализациии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТчДокументРеализациии.ЗаказДавальца = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ВтДокументы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТчДокументРеализациии.Ссылка.ПометкаУдаления";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	//-- Устарело_Переработка24
	
	Если ИмяТаблицы = "Документ.ЗаказДавальца2_5"
		И ПравоДоступа("Чтение", Метаданные.Документы.ОтчетДавальцу2_5) Тогда

		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка                         КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТчДокументРеализациии.Ссылка.Валюта                КАК Валюта,
		|	ТчДокументРеализациии.Ссылка.СуммаДокумента        КАК СуммаДокумента,
		|	ТчДокументРеализациии.Ссылка                       КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.ОтчетДавальцу2_5.Продукция КАК ТчДокументРеализациии
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТчДокументРеализациии.ЗаказДавальца = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ВтДокументы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТчДокументРеализациии.Ссылка.ПометкаУдаления";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;
	//-- НЕ УТКА
	
	//++ Устарело_Переработка24
	Если ИмяТаблицы = "Документ.ЗаказПереработчику"
		И ПравоДоступа("Чтение", Метаданные.Документы.ОтчетПереработчика) Тогда
		
		ТекстЗапроса ="
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	ТаблицаДокумента.СуммаДокумента КАК СуммаДокумента,
		|	ТаблицаДокумента.Ссылка КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.ОтчетПереработчика КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТаблицаДокумента.ЗаказПереработчику = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ТаблицаДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТаблицаДокумента.ПометкаУдаления
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	//-- Устарело_Переработка24
	
	Если ИмяТаблицы = "Документ.ЗаказПереработчику2_5"
	   И ПравоДоступа("Чтение", Метаданные.Документы.ОтчетПереработчика2_5) Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ВтДокументы.ДокументСсылка КАК Документ,
		|	ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным) КАК ПорядокРасчетов,
		|	ТаблицаДокумента.Валюта КАК Валюта,
		|	ТаблицаДокумента.СуммаДокумента КАК СуммаДокумента,
		|	ТаблицаДокумента.Ссылка КАК ПодчиненныйДокумент
		|ИЗ
		|	Документ.ОтчетПереработчика2_5 КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы 
		|		ПО ТаблицаДокумента.ЗаказПереработчику = ВтДокументы.ДокументСсылка
		|ГДЕ
		|	ТаблицаДокумента.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
		|	И НЕ ТаблицаДокумента.ПометкаУдаления
		|";
	
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;
	//-- НЕ УТ
	
	КоличествоЗапросов = МассивТекстовЗапроса.Количество();
	Если КоличествоЗапросов Тогда
		Пока КоличествоЗапросов > 1 Цикл
			КоличествоЗапросов = КоличествоЗапросов - 1;
			МассивТекстовЗапроса[КоличествоЗапросов] = СтрЗаменить(МассивТекстовЗапроса[КоличествоЗапросов], "РАЗРЕШЕННЫЕ", "");
		КонецЦикла;
		
		ТекстЗамены = СтрСоединить(МассивТекстовЗапроса, Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);

		ТекстЗапроса = "
			|//////////////////////////////////////////////////////
			|&ТекстЗамены
			|
			|ИТОГИ
			|	МАКСИМУМ(ПорядокРасчетов),
			|	МАКСИМУМ(Валюта)
			|ПО
			|	Документ;
			|";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗамены", ТекстЗамены);
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаНеобеспеченныеТовары(ИмяТаблицы, СтруктураПараметров)
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаНеобеспеченныеТовары") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаНеобеспеченныеТовары;
	КонецЕсли;
	
	ТекстЗапроса =  "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка КАК Документ,
	|	ДокументТовары.НомерСтроки КАК НомерСтроки,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.КоличествоУпаковок КАК Количество,
	|	ВЫБОР 
	|		КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) 
	|	ТОГДА
	|		ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|	ИНАЧЕ
	|		ДокументТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	&ИмяПоляСумма КАК Сумма
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|	ПО ДокументТовары.Ссылка = ВТУпорядочивание.Документ
	|		И ДокументТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|		И ДокументТовары.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	ДокументТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.НеТребуется)
	|	И НЕ ДокументТовары.Отменено
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыОтгрузка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаТаблицаСостояниеОбеспечения(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаСостояниеОбеспечения") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаСостояниеОбеспечения;
	КонецЕсли;
	
	ТекстЗапроса =  "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка КАК Документ,
	|	ДокументТовары.НомерСтроки КАК НомерСтроки,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.Серия КАК Серия,
	|	ДокументТовары.КоличествоУпаковок КАК Количество,
	|	ВЫБОР 
	|		КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) 
	|	ТОГДА
	|		ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|	ИНАЧЕ
	|		ДокументТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	&ИмяПоляДатаОтгрузки КАК ДатаОтгрузки,
	|	ДокументТовары.ВариантОбеспечения КАК Действие,
	|	&ИмяПоляСумма КАК Сумма
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|	ПО ДокументТовары.Ссылка = ВТУпорядочивание.Документ
	|		И ДокументТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|		И ДокументТовары.Характеристика = ВТУпорядочивание.Характеристика
	|		И ДокументТовары.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	(ДокументТовары.ВариантОбеспечения В(
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.КОбеспечению),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.РезервироватьПоМереПоступления),
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.СоСклада))
	|	ИЛИ ВЫБОР КОГДА ДокументТовары.Ссылка.Статус В (&МассивСтатусовБезОтгрузки) ТОГДА
	|		ДокументТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	КОНЕЦ)
	|	И НЕ ДокументТовары.Отменено
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыОтгрузка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляДатаОтгрузки", "ДокументТовары." + СтруктураПараметров.ИмяПоляДатаОтгрузки);
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВТТоварыКОтгрузке(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыКОтгрузке") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыКОтгрузке;
	КонецЕсли;
	
	ТекстЗапроса = "";
	МассивТекстовЗапроса = Новый Массив();
	
	Если СтруктураПараметров.ЕстьПравоНаТаблицуОтгрузки Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Подзапрос.ДокументОтгрузки КАК ДокументОтгрузки,
		|	Подзапрос.Номенклатура КАК Номенклатура,
		|	Подзапрос.Характеристика КАК Характеристика,
		|	Подзапрос.Склад КАК Склад,
		|	Подзапрос.Серия КАК Серия,
		|	Подзапрос.ОрдернаяСхемаПриОтгрузке КАК ОрдернаяСхемаПриОтгрузке,
		|	СУММА(Подзапрос.КОтгрузке) КАК КОтгрузке,
		|	СУММА(Подзапрос.Собирается) КАК Собирается,
		|	СУММА(Подзапрос.Собрано) КАК Собрано,
		|	СУММА(Подзапрос.Оформить) КАК Оформить,
		|	СУММА(Подзапрос.Оформлено) КАК Оформлено,
		|	СУММА(Подзапрос.Отгрузить) КАК Отгрузить,
		|	СУММА(Подзапрос.Отгружено) КАК Отгружено
		|ПОМЕСТИТЬ ВТТоварыКОтгрузке
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТоварыКОтгрузкеОстатки.ДокументОтгрузки КАК ДокументОтгрузки,
		|		ТоварыКОтгрузкеОстатки.Номенклатура КАК Номенклатура,
		|		ТоварыКОтгрузкеОстатки.Характеристика КАК Характеристика,
		|		ТоварыКОтгрузкеОстатки.Склад КАК Склад,
		|		ТоварыКОтгрузкеОстатки.Серия КАК Серия,
		|		ТоварыКОтгрузкеОстатки.КОтгрузкеПриход КАК КОтгрузке,
		|		ТоварыКОтгрузкеОстатки.СобираетсяКонечныйОстаток КАК Собирается,
		|		ТоварыКОтгрузкеОстатки.СобраноКонечныйОстаток КАК Собрано,
		|		ТоварыКОтгрузкеОстатки.КОформлениюКонечныйОстаток КАК Оформить,
		|		ТоварыКОтгрузкеОстатки.КОформлениюРасход КАК Оформлено,
		|		ТоварыКОтгрузкеОстатки.КОтгрузкеКонечныйОстаток КАК Отгрузить,
		|		ТоварыКОтгрузкеОстатки.КОтгрузкеРасход КАК Отгружено,
		|		ВЫБОР
		|			КОГДА ТоварыКОтгрузкеОстатки.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
		|			И &ТекущаяДата >= ТоварыКОтгрузкеОстатки.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке
		|	ИЗ
		|		РегистрНакопления.ТоварыКОтгрузке.ОстаткиИОбороты(,,,, ДокументОтгрузки В
		|			(ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)) КАК ТоварыКОтгрузкеОстатки
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ТаблицаТоварыКОтгрузке.ДокументОтгрузки КАК ДокументОтгрузки,
		|		ТаблицаТоварыКОтгрузке.Номенклатура КАК Номенклатура,
		|		ТаблицаТоварыКОтгрузке.Характеристика КАК Характеристика,
		|		ТаблицаТоварыКОтгрузке.Склад КАК Склад,
		|		ТаблицаТоварыКОтгрузке.Серия КАК Серия,
		|		-ТаблицаТоварыКОтгрузке.КОтгрузке КАК КОтгрузке,
		|		0 КАК Собирается,
		|		0 КАК Собрано,
		|		0 КАК Оформить,
		|		0 КАК Оформлено,
		|		0 КАК Отгрузить,
		|		0 КАК Отгружено,
		|		ВЫБОР
		|			КОГДА ТаблицаТоварыКОтгрузке.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
		|			И &ТекущаяДата >= ТаблицаТоварыКОтгрузке.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ОрдернаяСхемаПриОтгрузке
		|	ИЗ
		|		РегистрНакопления.ТоварыКОтгрузке КАК ТаблицаТоварыКОтгрузке
		|	ГДЕ
		|		ТаблицаТоварыКОтгрузке.ДокументОтгрузки В
		|			(ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)
		|		И ТаблицаТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И ТИПЗНАЧЕНИЯ(ТаблицаТоварыКОтгрузке.Регистратор) В (ТИП(Документ.СписаниеОтгруженныхТоваров),
		|			ТИП(Документ.ПоступлениеТоваровОтКлиента))) КАК Подзапрос
		|СГРУППИРОВАТЬ ПО
		|	Подзапрос.ДокументОтгрузки,
		|	Подзапрос.Номенклатура,
		|	Подзапрос.Характеристика,
		|	Подзапрос.Склад,
		|	Подзапрос.Серия,
		|	Подзапрос.ОрдернаяСхемаПриОтгрузке";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
	КонецЕсли;

	Если СтруктураПараметров.Свойство("ЕстьПравоДоступаРегистрОтгрузкаУслуг") 
		И СтруктураПараметров.ЕстьПравоДоступаРегистрОтгрузкаУслуг 
		И СтруктураПараметров.ИмяРегистраОтгрузкаУслуг = "РаспоряженияНаОтгрузкуИВозврат" Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК ДокументОтгрузки,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
		|	МАКСИМУМ(ЛОЖЬ) КАК ОрдернаяСхемаПриОтгрузке,
		|	СУММА(РаспоряженияНаОтгрузкуОбороты.ЗаказаноКоличествоОборот) КАК КОтгрузке,
		|	СУММА(0) КАК Собирается,
		|	СУММА(0) КАК Собрано,
		|	СУММА(РаспоряженияНаОтгрузкуОбороты.КОтгрузкеКоличествоОборот) -
		|		СУММА(РаспоряженияНаОтгрузкуОбороты.ОтгруженоКоличествоОборот) КАК Оформить,
		|	СУММА(РаспоряженияНаОтгрузкуОбороты.ОтгруженоКоличествоОборот) КАК Оформлено,
		|	СУММА(0) КАК Отгрузить,
		|	СУММА(0) КАК Отгружено
		|ИЗ
		|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОбороты
		|	
		|СГРУППИРОВАТЬ ПО
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Серия
		|
		|ИМЕЮЩИЕ
		|	СУММА(РаспоряженияНаОтгрузкуОбороты.ЗаказаноКоличествоОборот) <> 0
		|	ИЛИ (СУММА(РаспоряженияНаОтгрузкуОбороты.КОтгрузкеКоличествоОборот) -
		|		СУММА(РаспоряженияНаОтгрузкуОбороты.ОтгруженоКоличествоОборот)) <> 0
		|	ИЛИ СУММА(РаспоряженияНаОтгрузкуОбороты.ОтгруженоКоличествоОборот) <> 0
		|";
		
		// подзапрос #РаспоряженияНаОтгрузкуОборотыРазвернутая
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия =
			"Распоряжение В
			|		(ВЫБРАТЬ
			|				ВтДокументы.ДокументСсылка
			|			ИЗ
			|				ВтДокументы КАК ВтДокументы)
			|		И Номенклатура.ТипНоменклатуры В
			|			(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
			|			ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))";
		ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, Склад, Серия";
		ПараметрыФормированияТаблицы.Показатели = "Заказано, КОтгрузке, Отгружено";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество, Сумма";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;

	Если СтруктураПараметров.Свойство("ЕстьПравоДоступаРегистрОтгрузкаУслуг") 
		И СтруктураПараметров.ЕстьПравоДоступаРегистрОтгрузкаУслуг 
		И СтруктураПараметров.ИмяРегистраОтгрузкаУслуг = "ЗаказыНаВнутреннееПотребление" Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЗаказыНаВнутреннееПотреблениеОстатки.ЗаказНаВнутреннееПотребление КАК ДокументОтгрузки,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.Номенклатура КАК Номенклатура,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.Характеристика КАК Характеристика,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.Склад КАК Склад,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.Серия КАК Серия,
		|	ЛОЖЬ КАК ОрдернаяСхемаПриОтгрузке,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.ЗаказаноПриход КАК КОтгрузке,
		|	0 КАК Собирается,
		|	0 КАК Собрано,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.КОформлениюКонечныйОстаток КАК Оформить,
		|	ЗаказыНаВнутреннееПотреблениеОстатки.КОформлениюРасход КАК Оформлено,
		|	0 КАК Отгрузить,
		|	0 КАК Отгружено
		|ИЗ
		|	РегистрНакопления.ЗаказыНаВнутреннееПотребление.ОстаткиИОбороты(
		|			,
		|			,
		|			,
		|			,
		|			ЗаказНаВнутреннееПотребление В
		|				(ВЫБРАТЬ
		|					ВтДокументы.ДокументСсылка
		|				ИЗ
		|					ВтДокументы КАК ВтДокументы)
		|			И Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|		)КАК ЗаказыНаВнутреннееПотреблениеОстатки
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	КонецЕсли;

	КоличествоЗапросов = МассивТекстовЗапроса.Количество();
	Если КоличествоЗапросов Тогда
		Пока КоличествоЗапросов > 1 Цикл
			КоличествоЗапросов = КоличествоЗапросов - 1;
			МассивТекстовЗапроса[КоличествоЗапросов] = СтрЗаменить(МассивТекстовЗапроса[КоличествоЗапросов], "РАЗРЕШЕННЫЕ", "");
		КонецЦикла;
		
		ТекстЗамены = СтрСоединить(МассивТекстовЗапроса, Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС);

		ТекстЗапроса = "
			|//////////////////////////////////////////////////////
			|&ТекстЗамены;";
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗамены", ТекстЗамены);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТоварыРаспоряжениеЗаказОтгружено(ИмяТаблицы,СтруктураПараметров, ТекстыЗапроса)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыРаспоряжениеЗаказОтгружено") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыРаспоряжениеЗаказОтгружено;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(&ИмяПоляСумма) КАК СуммаСНДС,
	|	ДокументТовары.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА ДокументТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ &СкладРасположение
	|	КОНЕЦ КАК Склад,
	|	ВтДокументы.ДокументСсылка КАК Ссылка
	|,&ВТТоварыРаспоряженияОтгружено
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	НЕ ДокументТовары.Отменено
	|	И ДокументТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
	|	И ВтДокументы.Проведен
	|	И НЕ ДокументТовары.Ссылка.Статус В (&МассивСтатусовБезОтгрузки)
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	ДокументТовары.Серия,
	|	ВЫБОР
	|		КОГДА ДокументТовары.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		ИНАЧЕ &СкладРасположение
	|	КОНЕЦ,
	|	ВтДокументы.ДокументСсылка
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыОтгрузка);
	
	ЕстьСуммовыеПоказатели = СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(ЕстьСуммовыеПоказатели, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	Если СтруктураПараметров.СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Ссылка." + СтруктураПараметров.ИмяПоляСклад);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Склад");
	КонецЕсли;
	Если (ИмяТаблицы = "Документ.ЗаказКлиента"
			Или ИмяТаблицы = "Документ.ЗаявкаНаВозвратТоваровОтКлиента")
		И (ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваровУслуг)
			Или ПравоДоступа("Чтение", Метаданные.Документы.ПередачаТоваровХранителю)) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ВТТоварыРаспоряженияОтгружено", "ПОМЕСТИТЬ ВТПредварительноТоварыРаспоряженияОтгружено");
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	СУММА(ДокументТовары.Количество),
			|	СУММА(ДокументТовары.СуммаСНДС),
			|	ДокументТовары.Серия,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК ДокументТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказКлиента)
			|ГДЕ
			|	ДокументТовары.Ссылка.Проведен
			|	И ДокументТовары.КодСтроки = 0
			|	И ДокументТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереходПраваСобственности)
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	ДокументТовары.Упаковка,
			|	ДокументТовары.Серия,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка";
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.ОтгрузкаТоваровКлиенту) Тогда
			
			ТекстЗапроса = ТекстЗапроса +
				ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() +
				"ВЫБРАТЬ
				|	ДокументТовары.Номенклатура,
				|	ДокументТовары.Характеристика,
				|	СУММА(ДокументТовары.Количество),
				|	СУММА(ДокументТовары.Сумма),
				|	ДокументТовары.Серия,
				|	ДокументТовары.Склад,
				|	ВтДокументы.ДокументСсылка
				|ИЗ
				|	Документ.ОтгрузкаТоваровКлиенту.Товары КАК ДокументТовары
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
				|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказКлиента)
				|ГДЕ
				|	ДокументТовары.Ссылка.Проведен
				|	И ДокументТовары.КодСтроки = 0
				|
				|СГРУППИРОВАТЬ ПО
				|	ДокументТовары.Номенклатура,
				|	ДокументТовары.Характеристика,
				|	ДокументТовары.Упаковка,
				|	ДокументТовары.Серия,
				|	ДокументТовары.Склад,
				|	ВтДокументы.ДокументСсылка";
			
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.ПередачаТоваровХранителю) Тогда
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	СУММА(ДокументТовары.Количество),
			|	СУММА(ДокументТовары.Сумма),
			|	ДокументТовары.Серия,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|ИЗ
			|	Документ.ПередачаТоваровХранителю.Товары КАК ДокументТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказКлиента)
			|ГДЕ
			|	ДокументТовары.Ссылка.Проведен
			|	И ДокументТовары.КодСтроки = 0
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	ДокументТовары.Упаковка,
			|	ДокументТовары.Серия,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + ";";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТоварыРаспоряжения.Номенклатура КАК Номенклатура,
		|	ТоварыРаспоряжения.Характеристика КАК Характеристика,
		|	СУММА(ТоварыРаспоряжения.Количество) КАК Количество,
		|	СУММА(ТоварыРаспоряжения.СуммаСНДС) КАК СуммаСНДС,
		|	ТоварыРаспоряжения.Серия КАК Серия,
		|	ТоварыРаспоряжения.Склад КАК Склад,
		|	ТоварыРаспоряжения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
		|ИЗ
		|	ВТПредварительноТоварыРаспоряженияОтгружено КАК ТоварыРаспоряжения
		|СГРУППИРОВАТЬ ПО
		|	ТоварыРаспоряжения.Номенклатура,
		|	ТоварыРаспоряжения.Характеристика,
		|	ТоварыРаспоряжения.Серия,
		|	ТоварыРаспоряжения.Склад,
		|	ТоварыРаспоряжения.Ссылка;
		|";
	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	ИначеЕсли ИмяТаблицы = "Документ.ЗаказПереработчику"
		И ПравоДоступа("Чтение", Метаданные.Документы.ПередачаСырьяПереработчику) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ВТТоварыРаспоряженияОтгружено", "ПОМЕСТИТЬ ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	СУММА(ДокументТовары.Количество),
		|	СУММА(ДокументТовары.Сумма),
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|ИЗ
		|	Документ.ПередачаСырьяПереработчику.Товары КАК ДокументТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
		|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказПереработчику)
		|ГДЕ
		|	ДокументТовары.Ссылка.Проведен
		|	И ДокументТовары.КодСтроки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	ДокументТовары.Упаковка,
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка;";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТоварыРаспоряжения.Номенклатура КАК Номенклатура,
		|	ТоварыРаспоряжения.Характеристика КАК Характеристика,
		|	СУММА(ТоварыРаспоряжения.Количество) КАК Количество,
		|	СУММА(ТоварыРаспоряжения.СуммаСНДС) КАК СуммаСНДС,
		|	ТоварыРаспоряжения.Серия КАК Серия,
		|	ТоварыРаспоряжения.Склад КАК Склад,
		|	ТоварыРаспоряжения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
		|ИЗ
		|	ВТПредварительноТоварыРаспоряженияОтгружено КАК ТоварыРаспоряжения
		|СГРУППИРОВАТЬ ПО
		|	ТоварыРаспоряжения.Номенклатура,
		|	ТоварыРаспоряжения.Характеристика,
		|	ТоварыРаспоряжения.Серия,
		|	ТоварыРаспоряжения.Склад,
		|	ТоварыРаспоряжения.Ссылка;
		|";
	//-- Устарело_Переработка24
	ИначеЕсли ИмяТаблицы = "Документ.ЗаказПереработчику2_5"
			И ПравоДоступа("Чтение", Метаданные.Документы.ПередачаТоваровХранителю) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ВТТоварыРаспоряженияОтгружено", "ПОМЕСТИТЬ ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		//++ НЕ УТКА
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	СУММА(ДокументТовары.Количество),
		|	0,
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ДокументТовары
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
		|	ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка.ЗаказПереработчику
		|
		|ГДЕ
		|	ДокументТовары.Ссылка.Проведен
		|	И НЕ ДокументТовары.Отменено
		|	И ДокументТовары.ВариантОбеспечения = ЗНАЧЕНИЕ(Перечисление.ВариантыОбеспечения.Отгрузить)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	ДокументТовары.Упаковка,
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		//-- НЕ УТКА
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	СУММА(ДокументТовары.Количество),
		|	СУММА(ДокументТовары.Сумма),
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|ИЗ
		|	Документ.ПередачаТоваровХранителю.Товары КАК ДокументТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
		|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказКлиента)
		|ГДЕ
		|	ДокументТовары.Ссылка.Проведен
		|	И ДокументТовары.КодСтроки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	ДокументТовары.Упаковка,
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|;";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТоварыРаспоряжения.Номенклатура КАК Номенклатура,
		|	ТоварыРаспоряжения.Характеристика КАК Характеристика,
		|	СУММА(ТоварыРаспоряжения.Количество) КАК Количество,
		|	СУММА(ТоварыРаспоряжения.СуммаСНДС) КАК СуммаСНДС,
		|	ТоварыРаспоряжения.Серия КАК Серия,
		|	ТоварыРаспоряжения.Склад КАК Склад,
		|	ТоварыРаспоряжения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
		|ИЗ
		|	ВТПредварительноТоварыРаспоряженияОтгружено КАК ТоварыРаспоряжения
		|СГРУППИРОВАТЬ ПО
		|	ТоварыРаспоряжения.Номенклатура,
		|	ТоварыРаспоряжения.Характеристика,
		|	ТоварыРаспоряжения.Серия,
		|	ТоварыРаспоряжения.Склад,
		|	ТоварыРаспоряжения.Ссылка;
		|";
	//++ НЕ УТКА
	ИначеЕсли ИмяТаблицы = "Документ.ЗаказДавальца2_5"
		И ПравоДоступа("Чтение", Метаданные.Документы.ОтчетДавальцу2_5) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ВТТоварыРаспоряженияОтгружено", "ПОМЕСТИТЬ ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	СУММА(ДокументТовары.Количество),
		|	СУММА(ДокументТовары.СуммаСНДС),
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|ИЗ
		|	Документ.ОтчетДавальцу2_5.Продукция КАК ДокументТовары
		|	
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
		|	ПО ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказДавальца
		|
		|ГДЕ
		|	ДокументТовары.Ссылка.Проведен
		|	И ДокументТовары.КодСтроки = 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументТовары.Номенклатура,
		|	ДокументТовары.Характеристика,
		|	ДокументТовары.Упаковка,
		|	ДокументТовары.Серия,
		|	ДокументТовары.Склад,
		|	ВтДокументы.ДокументСсылка
		|;";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТПредварительноТоварыРаспоряженияОтгружено");
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТоварыРаспоряжения.Номенклатура КАК Номенклатура,
		|	ТоварыРаспоряжения.Характеристика КАК Характеристика,
		|	СУММА(ТоварыРаспоряжения.Количество) КАК Количество,
		|	СУММА(ТоварыРаспоряжения.СуммаСНДС) КАК СуммаСНДС,
		|	ТоварыРаспоряжения.Серия КАК Серия,
		|	ТоварыРаспоряжения.Склад КАК Склад,
		|	ТоварыРаспоряжения.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
		|ИЗ
		|	ВТПредварительноТоварыРаспоряженияОтгружено КАК ТоварыРаспоряжения
		|СГРУППИРОВАТЬ ПО
		|	ТоварыРаспоряжения.Номенклатура,
		|	ТоварыРаспоряжения.Характеристика,
		|	ТоварыРаспоряжения.Серия,
		|	ТоварыРаспоряжения.Склад,
		|	ТоварыРаспоряжения.Ссылка;
		|";
	//-- НЕ УТКА

	//-- НЕ УТ
		
	ИначеЕсли Не ИмяТаблицы = "Документ.ЗаказНаВнутреннееПотребление"
		//++ НЕ УТКА
		И Не ИмяТаблицы = "Документ.ЗаказНаРемонт"
		//-- НЕ УТКА
		И Не СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки Тогда
		
		ТекстЗапроса = "
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыКОтгрузке.Номенклатура     КАК Номенклатура,
		|	ТоварыКОтгрузке.Характеристика   КАК Характеристика,
		|	ТоварыКОтгрузке.Серия            КАК Серия,
		|	ТоварыКОтгрузке.Склад            КАК Склад,
		|	ТоварыКОтгрузке.ДокументОтгрузки КАК Ссылка,
		|	ТоварыКОтгрузке.КОтгрузкеПриход КАК Количество,
		|	0 КАК СуммаСНДС
		|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Обороты(,,,
		|		ДокументОтгрузки В(
		|			ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)) КАК ТоварыКОтгрузке
		|ГДЕ
		|	ТоварыКОтгрузке.КОтгрузкеПриход > 0
		|;
		|";
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ВТТоварыРаспоряженияОтгружено", "ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено");
		ТекстЗапроса = ТекстЗапроса + ";";
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено(ИмяТаблицы,СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыРаспоряжениеНакладнаяОтгружено;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(&ИмяПоляСумма) КАК СуммаСНДС,
	|	ДокументТовары.Серия КАК Серия,
	|	&СкладРасположение КАК Склад,
	|	ВтДокументы.ДокументСсылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТоварыРаспоряженияОтгружено
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	ВтДокументы.Проведен
	|	И &ДопУсловия
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	ДокументТовары.Серия,
	|	&СкладРасположение,
	|	ВтДокументы.ДокументСсылка
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыОтгрузка);
	Если СтруктураПараметров.СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Ссылка." + СтруктураПараметров.ИмяПоляСклад);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Склад");
	КонецЕсли;
	
	ЕстьСуммовыеПоказатели = СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(ЕстьСуммовыеПоказатели, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	ИмяПоляНакладнаяПоЗаказам = СтруктураПараметров.ИмяПоляНакладнаяПоЗаказам;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "И НЕ ДокументТовары.Ссылка." + ИмяПоляНакладнаяПоЗаказам);
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтгрузка(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаОтгрузка") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаОтгрузка;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТТоварыРаспоряженияОтгружено.Ссылка КАК Документ,
	|	ВТТоварыКОтгрузке.Склад КАК Склад,
	|	ВТТоварыКОтгрузке.Номенклатура КАК Номенклатура,
	|	ВТТоварыКОтгрузке.Характеристика КАК Характеристика,
	|	ВТТоварыРаспоряженияОтгружено.СуммаСНДС КАК СуммаСНДС,
	|	ВТТоварыРаспоряженияОтгружено.Серия КАК Серия,
	|	ВТТоварыКОтгрузке.КОтгрузке КАК КОтгрузке,
	|	ВТТоварыКОтгрузке.Собирается КАК Собирается,
	|	ВТТоварыКОтгрузке.Собрано КАК Собрано,
	|	ВТТоварыКОтгрузке.Оформить КАК Оформить,
	|	ВТТоварыКОтгрузке.Оформлено КАК Оформлено,
	|	ВТТоварыКОтгрузке.Отгрузить КАК Отгрузить,
	|	ВТТоварыКОтгрузке.Отгружено КАК Отгружено,
	|	ВТТоварыКОтгрузке.ОрдернаяСхемаПриОтгрузке КАК ОрдернаяСхемаПриОтгрузке
	|ИЗ
	|	ВТТоварыКОтгрузке КАК ВТТоварыКОтгрузке
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыРаспоряженияОтгружено КАК ВТТоварыРаспоряженияОтгружено
	|		ПО ВТТоварыКОтгрузке.ДокументОтгрузки = ВТТоварыРаспоряженияОтгружено.Ссылка
	|			И ВТТоварыКОтгрузке.Номенклатура = ВТТоварыРаспоряженияОтгружено.Номенклатура
	|			И ВТТоварыКОтгрузке.Характеристика = ВТТоварыРаспоряженияОтгружено.Характеристика
	|			И ВТТоварыКОтгрузке.Склад = ВТТоварыРаспоряженияОтгружено.Склад
	|			И ВТТоварыКОтгрузке.Серия = ВТТоварыРаспоряженияОтгружено.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|		ПО ВТТоварыКОтгрузке.ДокументОтгрузки = ВТУпорядочивание.Документ
	|			И ВТТоварыКОтгрузке.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТТоварыКОтгрузке.Характеристика = ВТУпорядочивание.Характеристика
	|			И ВТТоварыРаспоряженияОтгружено.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	НЕ ВТТоварыРаспоряженияОтгружено.Номенклатура ЕСТЬ NULL 
	|	И (ВТТоварыКОтгрузке.Отгрузить <> 0
	|			ИЛИ ВТТоварыКОтгрузке.Оформить <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ
	|	МАКСИМУМ(ОрдернаяСхемаПриОтгрузке)
	|ПО
	|	Документ,
	|	Склад
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаОтгружено(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаОтгружено") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаОтгружено;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТТоварыРаспоряженияОтгружено.Ссылка КАК Документ,
	|	ВТТоварыРаспоряженияОтгружено.Номенклатура КАК Номенклатура,
	|	ВТТоварыРаспоряженияОтгружено.Характеристика КАК Характеристика,
	|	ВТТоварыРаспоряженияОтгружено.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	СУММА(ВТТоварыРаспоряженияОтгружено.Количество) КАК Количество,
	|	СУММА(ВТТоварыРаспоряженияОтгружено.СуммаСНДС) КАК Сумма,
	|	ВТТоварыРаспоряженияОтгружено.Серия КАК Серия
	|ИЗ
	|	ВТТоварыРаспоряженияОтгружено КАК ВТТоварыРаспоряженияОтгружено
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыКОтгрузке КАК ВТТоварыКОтгрузке
	|		ПО (ВТТоварыКОтгрузке.ДокументОтгрузки = ВТТоварыРаспоряженияОтгружено.Ссылка)
	|			И (ВТТоварыКОтгрузке.Номенклатура = ВТТоварыРаспоряженияОтгружено.Номенклатура)
	|			И (ВТТоварыКОтгрузке.Характеристика = ВТТоварыРаспоряженияОтгружено.Характеристика)
	|			И (ВТТоварыКОтгрузке.Склад = ВТТоварыРаспоряженияОтгружено.Склад)
	|			И (ВТТоварыКОтгрузке.Серия = ВТТоварыРаспоряженияОтгружено.Серия)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|		ПО ВТТоварыРаспоряженияОтгружено.Ссылка = ВТУпорядочивание.Документ
	|			И ВТТоварыРаспоряженияОтгружено.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТТоварыРаспоряженияОтгружено.Характеристика = ВТУпорядочивание.Характеристика
	|			И ВТТоварыРаспоряженияОтгружено.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	(ВТТоварыКОтгрузке.Отгрузить = 0
	|	И ВТТоварыКОтгрузке.Оформить = 0)
	|		ИЛИ ВТТоварыКОтгрузке.ДокументОтгрузки ЕСТЬ NULL
	|СГРУППИРОВАТЬ ПО
	|	ВТТоварыРаспоряженияОтгружено.Ссылка,
	|	ВТТоварыРаспоряженияОтгружено.Номенклатура,
	|	ВТТоварыРаспоряженияОтгружено.Характеристика,
	|	ВТТоварыРаспоряженияОтгружено.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(ВТУпорядочивание.Порядок)
	|ИТОГИ ПО
	|	Документ;
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаППС(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаППС") Тогда
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаППС;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспоряженияОборотыОтгрузка.Распоряжение КАК Документ,
	|	РаспоряженияОборотыОтгрузка.Номенклатура КАК Номенклатура,
	|	РаспоряженияОборотыОтгрузка.Характеристика КАК Характеристика,
	|	РаспоряженияОборотыОтгрузка.Серия КАК Серия,
	|
	|	СУММА(ЕСТЬNULL(РаспоряженияОборотыОформление.КОформлениюКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0)) КАК Оформить,
	|
	|	СУММА(ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0)) КАК Оформлено,
	|
	|	СУММА(РаспоряженияОборотыОтгрузка.КОтгрузкеКоличествоОборот
	|		- РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот) КАК Отгрузить,
	|
	|	СУММА(РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ПринятоКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.СписаноДоППСКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ОприходованоДоППСКоличествоОборот, 0)) КАК Отгружено,
	|
	|	СУММА((РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ПринятоКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.СписаноДоППСКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ОприходованоДоППСКоличествоОборот, 0))
	|		- (ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОформление.ПринятоКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОформление.СписаноДоППСКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОформление.ОприходованоДоППСКоличествоОборот, 0))) КАК КПереходуПраваСобственности
	|ИЗ
	|	#РаспоряженияНаОтгрузкуОборотыРазвернутая1 КАК РаспоряженияОборотыОтгрузка
	|		ЛЕВОЕ СОЕДИНЕНИЕ #РаспоряженияНаОтгрузкуОборотыРазвернутая2 КАК РаспоряженияОборотыОформление
	|		ПО РаспоряженияОборотыОтгрузка.Распоряжение = РаспоряженияОборотыОформление.Распоряжение
	|		И РаспоряженияОборотыОтгрузка.Номенклатура = РаспоряженияОборотыОформление.Номенклатура
	|		И РаспоряженияОборотыОтгрузка.Характеристика = РаспоряженияОборотыОформление.Характеристика
	|		И РаспоряженияОборотыОтгрузка.Серия = РаспоряженияОборотыОформление.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|		ПО РаспоряженияОборотыОтгрузка.Распоряжение = ВТУпорядочивание.Документ
	|			И РаспоряженияОборотыОтгрузка.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И РаспоряженияОборотыОтгрузка.Характеристика = ВТУпорядочивание.Характеристика
	|			И РаспоряженияОборотыОтгрузка.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	РаспоряженияОборотыОтгрузка.Распоряжение В
	|		(ВЫБРАТЬ
	|			ДвиженияРаспоряжений.Распоряжение КАК Распоряжение
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК ДвиженияРаспоряжений)
	|	И РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот
	|		- ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0) <> 0
	|СГРУППИРОВАТЬ ПО
	|	РаспоряженияОборотыОтгрузка.Распоряжение,
	|	РаспоряженияОборотыОтгрузка.Номенклатура,
	|	РаспоряженияОборотыОтгрузка.Характеристика,
	|	РаспоряженияОборотыОтгрузка.Серия
	|ИМЕЮЩИЕ
	|	СУММА((РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ПринятоКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОтгрузка.СписаноДоППСКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ОприходованоДоППСКоличествоОборот, 0))
	|		- (ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОформление.ПринятоКоличествоОборот, 0)
	|		+ ЕСТЬNULL(РаспоряженияОборотыОформление.СписаноДоППСКоличествоОборот, 0)
	|		- ЕСТЬNULL(РаспоряженияОборотыОформление.ОприходованоДоППСКоличествоОборот, 0))) <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(ВТУпорядочивание.Порядок)
	|ИТОГИ
	|ПО
	|	Документ";
	
	// РаспоряженияНаОтгрузкуДвиженияРазвернутая
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И (ВЫРАЗИТЬ(Регистратор КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация =
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|	ИЛИ ТИПЗНАЧЕНИЯ(Регистратор) = ТИП(Документ.ОтгрузкаТоваровКлиенту))"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Регистратор, Распоряжение";
	ПараметрыФормированияТаблицы.Показатели = "Оформлено, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);
	
	// РаспоряженияНаОтгрузкуОборотыРазвернутая 1
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|			ВтДокументы.ДокументСсылка
		|		ИЗ
		|			ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, Серия";
	ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено, СписаноДоППС, ОприходованоДоППС, Принято";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая1 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая1",
		РаспоряженияНаОтгрузкуОборотыРазвернутая1);
	
	// РаспоряженияНаОтгрузкуОборотыРазвернутая 2
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|			ВтДокументы.ДокументСсылка
		|		ИЗ
		|			ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И ВЫБОР
		|		КОГДА Показатель В
		|		(ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.СписаноДоППС),
		|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ОприходованоДоППС),
		|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Принято))
		|			ТОГДА Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, Серия";
	ПараметрыФормированияТаблицы.Показатели = "КОформлению, Оформлено, СписаноДоППС, ОприходованоДоППС, Принято";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая2 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая2",
		РаспоряженияНаОтгрузкуОборотыРазвернутая2);
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

Функция ТекстЗапросаТаблицаППСОформлено(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаППСОформлено") Тогда
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаППСОформлено;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспоряженияОборотыОтгрузка.Распоряжение КАК Документ,
	|	РаспоряженияОборотыОтгрузка.Номенклатура КАК Номенклатура,
	|	РаспоряженияОборотыОтгрузка.Характеристика КАК Характеристика,
	|	РаспоряженияОборотыОтгрузка.Серия КАК Серия,
	|	СУММА(РаспоряженияОборотыОформление.ОформленоКоличествоОборот) КАК Количество,
	|	СУММА(РаспоряженияОборотыОформление.ОформленоСуммаОборот) КАК Сумма
	|ИЗ
	|	#РаспоряженияНаОтгрузкуОборотыРазвернутая1 КАК РаспоряженияОборотыОтгрузка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #РаспоряженияНаОтгрузкуОборотыРазвернутая2 КАК РаспоряженияОборотыОформление
	|		ПО РаспоряженияОборотыОтгрузка.Распоряжение = РаспоряженияОборотыОформление.Распоряжение
	|		И РаспоряженияОборотыОтгрузка.Номенклатура = РаспоряженияОборотыОформление.Номенклатура
	|		И РаспоряженияОборотыОтгрузка.Характеристика = РаспоряженияОборотыОформление.Характеристика
	|		И РаспоряженияОборотыОтгрузка.Серия = РаспоряженияОборотыОформление.Серия
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|		ПО РаспоряженияОборотыОтгрузка.Распоряжение = ВТУпорядочивание.Документ
	|			И РаспоряженияОборотыОтгрузка.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И РаспоряженияОборотыОтгрузка.Характеристика = ВТУпорядочивание.Характеристика
	|			И РаспоряженияОборотыОтгрузка.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	РаспоряженияОборотыОтгрузка.Распоряжение В
	|		(ВЫБРАТЬ
	|			ДвиженияРаспоряжений.Распоряжение КАК Распоряжение
	|		ИЗ
	|			#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК ДвиженияРаспоряжений)
	|СГРУППИРОВАТЬ ПО
	|	РаспоряженияОборотыОтгрузка.Распоряжение,
	|	РаспоряженияОборотыОтгрузка.Номенклатура,
	|	РаспоряженияОборотыОтгрузка.Характеристика,
	|	РаспоряженияОборотыОтгрузка.Серия
	|ИМЕЮЩИЕ
	|	СУММА(РаспоряженияОборотыОтгрузка.ОтгруженоКоличествоОборот -
	|		ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ПринятоКоличествоОборот, 0) -
	|		ЕСТЬNULL(РаспоряженияОборотыОтгрузка.СписаноДоППСКоличествоОборот, 0) +
	|		ЕСТЬNULL(РаспоряженияОборотыОтгрузка.ОприходованоДоППСКоличествоОборот, 0) -
	|		(ЕСТЬNULL(РаспоряженияОборотыОформление.ОформленоКоличествоОборот, 0) +
	|		ЕСТЬNULL(РаспоряженияОборотыОформление.ПринятоКоличествоОборот, 0) +
	|		ЕСТЬNULL(РаспоряженияОборотыОформление.СписаноДоППСКоличествоОборот, 0) -
	|		ЕСТЬNULL(РаспоряженияОборотыОформление.ОприходованоДоППСКоличествоОборот, 0))) = 0
	|	И СУММА(РаспоряженияОборотыОформление.ОформленоСуммаОборот) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(ВТУпорядочивание.Порядок)
	|ИТОГИ
	|ПО
	|	Документ";
	
	// РаспоряженияНаОтгрузкуДвиженияРазвернутая
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И (ВЫРАЗИТЬ(Регистратор КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация =
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
		|	ИЛИ ТИПЗНАЧЕНИЯ(Регистратор) = ТИП(Документ.ОтгрузкаТоваровКлиенту))"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Регистратор, Распоряжение";
	ПараметрыФормированияТаблицы.Показатели = "Оформлено, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);
	
	// РаспоряженияНаОтгрузкуОборотыРазвернутая 1
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|			ВтДокументы.ДокументСсылка
		|		ИЗ
		|			ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, Серия";
	ПараметрыФормированияТаблицы.Показатели = "КОтгрузке, Отгружено, СписаноДоППС, ОприходованоДоППС, Принято";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая1 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая1",
		РаспоряженияНаОтгрузкуОборотыРазвернутая1);
	
	// РаспоряженияНаОтгрузкуОборотыРазвернутая 2
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|			ВтДокументы.ДокументСсылка
		|		ИЗ
		|			ВтДокументы КАК ВтДокументы)
		|	И Номенклатура.ТипНоменклатуры НЕ В
		|		(ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга),
		|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа))
		|	И ВЫБОР
		|		КОГДА Показатель В
		|		(ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.СписаноДоППС),
		|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ОприходованоДоППС),
		|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Принято))
		|			ТОГДА Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ"; // @query-part
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, Серия";
	ПараметрыФормированияТаблицы.Показатели = "КОформлению, Оформлено, СписаноДоППС, ОприходованоДоППС, Принято";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество, Сумма";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая2 =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая2",
		РаспоряженияНаОтгрузкуОборотыРазвернутая2);
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

Функция ТекстЗапросаВТТоварыПоступление(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыПоступление") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыПоступление;
	КонецЕсли;
	
	ТекстЗапроса  = "";
	ТекстыЗапроса = Новый Массив;
	
	Если СтруктураПараметров.ЕстьПравоНаТаблицуПоступления Тогда
		
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТоварыКПоступлению.ДокументПоступления КАК ДокументПоступления,
		|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлению.Характеристика КАК Характеристика,
		|	ТоварыКПоступлению.Склад КАК Склад,
		|	ЛОЖЬ КАК РаздельнаяЗакупка,
		|	0 КАК КПоступлению,
		|	ТоварыКПоступлению.ПринимаетсяКонечныйОстаток КАК Принимается,
		|	ТоварыКПоступлению.КОформлениюОрдеровРасход КАК Принято,
		|	0 КАК Оформить,
		|	0 КАК Оформлено,
		|	ТоварыКПоступлению.КОформлениюОрдеровКонечныйОстаток КАК Принять,
		|	ТоварыКПоступлению.КОформлениюОрдеровРасход КАК Поступило
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.ОстаткиИОбороты(
		|			,,,,
		|			ДокументПоступления В
		|				(ВЫБРАТЬ
		|					ВтДокументы.ДокументСсылка
		|				ИЗ
		|					ВтДокументы КАК ВтДокументы)
		|			И ХозяйственнаяОперация НЕ В (&СписокХозоперацийРаздельнойЗакупки)) КАК ТоварыКПоступлению
		|");
		
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ //При операциях раздельной закупки вместо информации по ордеру, выводится информации об оформлении документа Поступление товаров
		|	ТоварыКПоступлению.ДокументПоступления КАК ДокументПоступления,
		|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлению.Характеристика КАК Характеристика,
		|	ТоварыКПоступлению.Склад КАК Склад,
		|	ИСТИНА КАК РаздельнаяЗакупка,
		|	0 КАК КПоступлению,
		|	0 КАК Принимается,
		|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюРасход КАК Принято,
		|	0 КАК Оформить,
		|	0 КАК Оформлено,
		|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюКонечныйОстаток КАК Принять,
		|	ТоварыКПоступлению.КОформлениюПоступленийПоРаспоряжениюРасход КАК Поступило
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.ОстаткиИОбороты(
		|			,,,,
		|			ДокументПоступления В
		|				(ВЫБРАТЬ
		|					ВтДокументы.ДокументСсылка
		|				ИЗ
		|					ВтДокументы КАК ВтДокументы)
		|			И ХозяйственнаяОперация В (&СписокХозоперацийРаздельнойЗакупки)) КАК ТоварыКПоступлению
		|");
		
	КонецЕсли;
	
	Если СтруктураПараметров.ЕстьПравоДоступаЗаказыПоставщикам Тогда
		
		ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ЗаказыПоставщикам.ЗаказПоставщику            КАК ДокументПоступления,
		|	ЗаказыПоставщикам.Номенклатура               КАК Номенклатура,
		|	ЗаказыПоставщикам.Характеристика             КАК Характеристика,
		|	ЗаказыПоставщикам.Склад                      КАК Склад,
		|	ВЫБОР
		|		КОГДА ЗаказыПоставщикам.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
		|			И ВЫРАЗИТЬ(ЗаказыПоставщикам.ЗаказПоставщику КАК Документ.ЗаказПоставщику).ХозяйственнаяОперация В (&СписокХозоперацийРаздельнойЗакупки)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ
		|			ЛОЖЬ
		|	КОНЕЦ                                        КАК РаздельнаяЗакупка,
		|	ВЫБОР
		//++ НЕ УТ
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщику) = ТИП(Документ.ЗаказПереработчику2_5)
		|			И ЗаказыПоставщикам.КодСтроки = 0
		|			ТОГДА 0
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщику) = ТИП(Документ.ЗаказПереработчику2_5)
		|			ТОГДА ЗаказыПоставщикам.КПоступлениюПриход
		//-- НЕ УТ
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыПоставщикам.КОформлениюПриход
		|	КОНЕЦ                                        КАК КПоступлению,
		|	0                                            КАК Принимается,
		|	0                                            КАК Принято,
		|	ВЫБОР
		//++ НЕ УТ
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщику) = ТИП(Документ.ЗаказПереработчику2_5)
		|			ТОГДА ЗаказыПоставщикам.КПоступлениюКонечныйОстаток
		//-- НЕ УТ
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыПоставщикам.КОформлениюКонечныйОстаток
		|	КОНЕЦ                                        КАК Оформить,
		|	ВЫБОР
		//++ НЕ УТ
		|		КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщику) = ТИП(Документ.ЗаказПереработчику2_5)
		|			ТОГДА ЗаказыПоставщикам.КПоступлениюРасход
		//-- НЕ УТ
		|		КОГДА ИСТИНА
		|			ТОГДА ЗаказыПоставщикам.КОформлениюРасход
		|	КОНЕЦ                                        КАК Оформлено,
		|	0                                            КАК Принять,
		|	0                                            КАК Поступило
		|ИЗ
		|	РегистрНакопления.ЗаказыПоставщикам.ОстаткиИОбороты(
		|				,,,,
		//++ НЕ УТ
		|				ВЫБОР
		|					КОГДА ТИПЗНАЧЕНИЯ(ЗаказПоставщику) = ТИП(Документ.ЗаказПереработчику2_5)
		|						ТОГДА Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|					ИНАЧЕ ИСТИНА
		|				КОНЕЦ
		|				И
		//-- НЕ УТ
		|				ЗаказПоставщику В
		|					(ВЫБРАТЬ
		|						ВтДокументы.ДокументСсылка
		|					ИЗ
		|						ВтДокументы КАК ВтДокументы)) КАК ЗаказыПоставщикам
		|");
		
	КонецЕсли;
	
	Если Не ТекстыЗапроса.Количество() = 0 Тогда
		
		ТекстЗапроса =
		"
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТоварыКПоступлению.ДокументПоступления КАК ДокументПоступления,
		|	ТоварыКПоступлению.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлению.Характеристика КАК Характеристика,
		|	ТоварыКПоступлению.Склад КАК Склад,
		|	МАКСИМУМ(ТоварыКПоступлению.РаздельнаяЗакупка) КАК РаздельнаяЗакупка,
		|	СУММА(ТоварыКПоступлению.КПоступлению) КАК КПоступлению,
		|	СУММА(ТоварыКПоступлению.Принимается) КАК Принимается,
		|	СУММА(ТоварыКПоступлению.Принято) КАК Принято,
		|	СУММА(ТоварыКПоступлению.Оформить) КАК Оформить,
		|	СУММА(ТоварыКПоступлению.Оформлено) КАК Оформлено,
		|	СУММА(ТоварыКПоступлению.Принять) КАК Принять,
		|	СУММА(ТоварыКПоступлению.Поступило) КАК Поступило,
		|	ВЫБОР
		|		КОГДА ТоварыКПоступлению.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
		|				И &ТекущаяДата >= ТоварыКПоступлению.Склад.ДатаНачалаОрдернойСхемыПриПоступлении
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ОрдернаяСхемаПриПоступлении
		|ПОМЕСТИТЬ ВТТоварыПоступление
		|ИЗ
		|	&ВложенныйЗапрос КАК ТоварыКПоступлению
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКПоступлению.ДокументПоступления,
		|	ТоварыКПоступлению.Номенклатура,
		|	ТоварыКПоступлению.Характеристика,
		|	ТоварыКПоступлению.Склад,
		|	ВЫБОР
		|		КОГДА ТоварыКПоступлению.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
		|				И &ТекущаяДата >= ТоварыКПоступлению.Склад.ДатаНачалаОрдернойСхемыПриПоступлении
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВложенныйЗапрос", "(" + СтрСоединить(ТекстыЗапроса, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()) + ")");
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТоварыРаспоряженияПоступлениеЗаказ(ИмяТаблицы,СтруктураПараметров,ТекстыЗапроса)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыРаспоряженияПоступлениеЗаказ") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыРаспоряженияПоступлениеЗаказ;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(&ИмяПоляСумма) КАК СуммаСНДС,
	|	&СкладРасположение КАК Склад,
	|	ВтДокументы.ДокументСсылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТоварыРаспоряженияПоступлениеБезОбъединения
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	НЕ ДокументТовары.Отменено
	|	И ВтДокументы.Проведен
	|	И &ДопУсловия
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	&СкладРасположение,
	|	ВтДокументы.ДокументСсылка
	|";
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		Если ПравоДоступа("Чтение", Метаданные.Документы.ПриобретениеТоваровУслуг) Тогда
		
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	СУММА(ДокументТовары.Количество),
			|	СУММА(ДокументТовары.СуммаСНДС),
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|ИЗ
			|	Документ.ПриобретениеТоваровУслуг.Товары КАК ДокументТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказПоставщику)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|		ПО ДокументТовары.Ссылка = РеестрДокументов.СторнируемыйДокумент
			|			И РеестрДокументов.Проведен
			|			И НЕ РеестрДокументов.ДополнительнаяЗапись
			|ГДЕ
			|	ДокументТовары.Ссылка.Проведен
			|	И ДокументТовары.КодСтроки = 0
			|	И РеестрДокументов.Ссылка ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	ДокументТовары.Упаковка,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|";
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.ПриемкаТоваровНаХранение) Тогда
		
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	СУММА(ДокументТовары.Количество),
			|	СУММА(ДокументТовары.Сумма),
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|ИЗ
			|	Документ.ПриемкаТоваровНаХранение.Товары КАК ДокументТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказПоставщику)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|		ПО ДокументТовары.Ссылка = РеестрДокументов.СторнируемыйДокумент
			|			И РеестрДокументов.Проведен
			|			И НЕ РеестрДокументов.ДополнительнаяЗапись
			|ГДЕ
			|	ДокументТовары.Ссылка.Проведен
			|	И ДокументТовары.КодСтроки = 0
			|	И РеестрДокументов.Ссылка ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	ДокументТовары.Упаковка,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|";
		КонецЕсли;
		
		Если ПравоДоступа("Чтение", Метаданные.Документы.АктОРасхожденияхПослеПриемки) Тогда
		
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	СУММА(ДокументТовары.Количество - ДокументТовары.КоличествоПоДокументу),
			|	СУММА(ДокументТовары.Сумма - ДокументТовары.СуммаПоДокументу),
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|ИЗ
			|	Документ.АктОРасхожденияхПослеПриемки.Товары КАК ДокументТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.ЗаказПоставщику)
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
			|		ПО ДокументТовары.Ссылка = РеестрДокументов.СторнируемыйДокумент
			|			И РеестрДокументов.Проведен
			|			И НЕ РеестрДокументов.ДополнительнаяЗапись
			|ГДЕ
			|	ДокументТовары.Ссылка.Проведен
			|	И ДокументТовары.Действие <>
			|		ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ОформитьНедостачу)
			|	И РеестрДокументов.Ссылка ЕСТЬ NULL
			|
			|СГРУППИРОВАТЬ ПО
			|	ДокументТовары.Номенклатура,
			|	ДокументТовары.Характеристика,
			|	ДокументТовары.Упаковка,
			|	ДокументТовары.Склад,
			|	ВтДокументы.ДокументСсылка
			|";
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ";";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыПоступление);
	
	ЕстьСуммовыеПоказатели = СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(ЕстьСуммовыеПоказатели, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	Если СтруктураПараметров.СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Ссылка." + СтруктураПараметров.ИмяПоляСкладПолучатель);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Склад");
	КонецЕсли;
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "
			|	И ДокументТовары.Ссылка.Статус В (
			|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Подтвержден),
			|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт))");
	//++ НЕ УТ
	ИначеЕсли ИмяТаблицы = "Документ.ЗаказПереработчику2_5"
		//++ Устарело_Переработка24
		Или ИмяТаблицы = "Документ.ЗаказПереработчику"
		//-- Устарело_Переработка24
		Или Ложь Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "
			|	И ДокументТовары.Ссылка.Статус В (
			|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.КИсполнению),
			|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПереработчикам.Закрыт))");
	//-- НЕ УТ
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "");
		
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТТоварыРаспоряженияПоступлениеБезОбъединения");

	ТекстЗапроса = "
		   	|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	       	|	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Номенклатура КАК Номенклатура,
	       	|	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Характеристика КАК Характеристика,
		    |	СУММА(ВТТоварыРаспоряженияПоступлениеБезОбъединения.Количество) КАК Количество,
		    |	СУММА(ВТТоварыРаспоряженияПоступлениеБезОбъединения.СуммаСНДС) КАК СуммаСНДС,
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Склад КАК Склад,
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Ссылка КАК Ссылка
		    |ПОМЕСТИТЬ ВТТоварыРаспоряженияПоступление
		    |ИЗ
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения КАК ВТТоварыРаспоряженияПоступлениеБезОбъединения
		    |
		    |СГРУППИРОВАТЬ ПО
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Номенклатура,
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Характеристика,
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Склад,
		    |	ВТТоварыРаспоряженияПоступлениеБезОбъединения.Ссылка
			|;
			|";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВТТоварыРаспоряженияПоступлениеНакладная(ИмяТаблицы,СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыРаспоряженияПоступлениеНакладная") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыРаспоряженияПоступлениеНакладная;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(&ИмяПоляСумма) КАК СуммаСНДС,
	|	&СкладРасположение КАК Склад,
	|	ВтДокументы.ДокументСсылка КАК Ссылка
	|ПОМЕСТИТЬ ВТТоварыРаспоряженияПоступление
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	ВтДокументы.Проведен
	|	И &ДопУсловия
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	&СкладРасположение,
	|	ВтДокументы.ДокументСсылка
	|;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыПоступление);
	Если СтруктураПараметров.СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Ссылка." + СтруктураПараметров.ИмяПоляСкладПолучатель);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Склад");
	КонецЕсли;
	
	ЕстьСуммовыеПоказатели = СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(ЕстьСуммовыеПоказатели, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	ИмяПоляНакладнаяПоЗаказам = СтруктураПараметров.ИмяПоляНакладнаяПоЗаказам;
	Если ЗначениеЗаполнено(ИмяПоляНакладнаяПоЗаказам) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "И НЕ ДокументТовары.Ссылка." + ИмяПоляНакладнаяПоЗаказам);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "");
	КонецЕсли;
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаЗаказано(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаЗаказано") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаЗаказано;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) 
	|	ТОГДА
	|		ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|	ИНАЧЕ
	|		ДокументТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(ДокументТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	СУММА(&ИмяПоляСумма) КАК Сумма,
	|	&СкладРасположение КАК Склад,
	|	ВтДокументы.ДокументСсылка КАК Документ
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ДокументТовары.Ссылка = ВТУпорядочивание.Документ
	|			И ДокументТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ДокументТовары.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	НЕ ДокументТовары.Отменено
	|	И ВтДокументы.Проведен
	|	И &ДопУсловия
	|СГРУППИРОВАТЬ ПО
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	ДокументТовары.Упаковка,
	|	&СкладРасположение,
	|	ВтДокументы.ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	МИНИМУМ(ВТУпорядочивание.Порядок)
	|ИТОГИ ПО
	|	Документ
	|";
	
	ТекстЗапроса = ТекстЗапроса + ";";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыПоступление);
	
	ЕстьСуммовыеПоказатели = СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(ЕстьСуммовыеПоказатели, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	
	Если СтруктураПараметров.СкладВШапке Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Ссылка." + СтруктураПараметров.ИмяПоляСкладПолучатель);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СкладРасположение", "ДокументТовары.Склад");
	КонецЕсли;
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "И НЕ ДокументТовары.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Подтвержден),
			|	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовПоставщикам.Закрыт))");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловия", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПоступление(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаПоступление") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаПоступление;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТТоварыРаспоряженияПоступление.Ссылка КАК Документ,
	|	ВТТоварыПоступление.Склад КАК Склад,
	|	ВТТоварыПоступление.Номенклатура КАК Номенклатура,
	|	ВТТоварыПоступление.Характеристика КАК Характеристика,
	|	ВТТоварыРаспоряженияПоступление.СуммаСНДС КАК СуммаСНДС,
	|	ВТТоварыПоступление.КПоступлению КАК КПоступлению,
	|	ВТТоварыПоступление.Принимается КАК Принимается,
	|	ВТТоварыПоступление.Принято КАК Принято,
	|	ВТТоварыПоступление.Оформить КАК Оформить,
	|	ВТТоварыПоступление.Оформлено КАК Оформлено,
	|	ВТТоварыПоступление.Принять КАК Принять,
	|	ВТТоварыПоступление.Поступило КАК Поступило,
	|	ВТТоварыПоступление.ОрдернаяСхемаПриПоступлении КАК ОрдернаяСхемаПриПоступлении,
	|	ВТТоварыПоступление.РаздельнаяЗакупка КАК РаздельнаяЗакупка
	|ИЗ
	|	ВТТоварыПоступление КАК ВТТоварыПоступление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыРаспоряженияПоступление КАК ВТТоварыРаспоряженияПоступление
	|		ПО ВТТоварыПоступление.ДокументПоступления = ВТТоварыРаспоряженияПоступление.Ссылка
	|			И ВТТоварыПоступление.Номенклатура = ВТТоварыРаспоряженияПоступление.Номенклатура
	|			И ВТТоварыПоступление.Характеристика = ВТТоварыРаспоряженияПоступление.Характеристика
	|			И ВТТоварыПоступление.Склад = ВТТоварыРаспоряженияПоступление.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ВТТоварыПоступление.ДокументПоступления = ВТУпорядочивание.Документ
	|			И ВТТоварыПоступление.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТТоварыПоступление.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	НЕ ВТТоварыРаспоряженияПоступление.Номенклатура ЕСТЬ NULL 
	|	И (ВТТоварыПоступление.Принять <> 0
	|			ИЛИ ВТТоварыПоступление.Оформить <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ
	|	МАКСИМУМ(ОрдернаяСхемаПриПоступлении),
	|	МАКСИМУМ(РаздельнаяЗакупка)
	|ПО
	|	Документ,
	|	Склад
	|;
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаПоступило(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаПоступило") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаПоступило;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТТоварыРаспоряженияПоступление.Ссылка КАК Документ,
	|	ВТТоварыРаспоряженияПоступление.Номенклатура КАК Номенклатура,
	|	ВТТоварыРаспоряженияПоступление.Характеристика КАК Характеристика,
	|	ВТТоварыРаспоряженияПоступление.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВТТоварыРаспоряженияПоступление.Количество КАК Количество,
	|	ВТТоварыРаспоряженияПоступление.СуммаСНДС КАК Сумма
	|ИЗ
	|	ВТТоварыРаспоряженияПоступление КАК ВТТоварыРаспоряженияПоступление
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыПоступление КАК ВТТоварыПоступление
	|		ПО (ВТТоварыПоступление.ДокументПоступления = ВТТоварыРаспоряженияПоступление.Ссылка)
	|			И (ВТТоварыПоступление.Номенклатура = ВТТоварыРаспоряженияПоступление.Номенклатура)
	|			И (ВТТоварыПоступление.Характеристика = ВТТоварыРаспоряженияПоступление.Характеристика)
	|			И (ВТТоварыПоступление.Склад = ВТТоварыРаспоряженияПоступление.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ВТТоварыРаспоряженияПоступление.Ссылка = ВТУпорядочивание.Документ
	|			И ВТТоварыРаспоряженияПоступление.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТТоварыРаспоряженияПоступление.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	(ВТТоварыПоступление.Принять = 0
	|	И ВТТоварыПоступление.Оформить = 0)
	|		ИЛИ ВТТоварыПоступление.ДокументПоступления ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ;
	|";
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаВТОтмененоОтгрузка(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаВТОтмененоОтгрузка") Тогда
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаВТОтмененоОтгрузка;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка КАК Документ,
	|	ДокументТовары.НомерСтроки КАК НомерСтроки,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.КоличествоУпаковок КАК Количество,
	|	ВЫБОР
	|		КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ДокументТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	&ИмяПоляПричинаОтмены КАК ПричинаОтмены,
	|	&ИмяПоляСумма КАК Сумма
	|ПОМЕСТИТЬ ВТОтмененоОтгрузка
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|ГДЕ
	|	ДокументТовары.Отменено
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РаспоряженияНаОтгрузку.Распоряжение КАК Документ,
	|	""-"" КАК НомерСтроки,
	|	РаспоряженияНаОтгрузку.Номенклатура КАК Номенклатура,
	|	РаспоряженияНаОтгрузку.Характеристика КАК Характеристика,
	|	СУММА(РаспоряженияНаОтгрузку.КоличествоОборот) КАК Количество,
	|	РаспоряженияНаОтгрузку.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВЫБОР
	|		КОГДА Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.СписаноДоППС)
	|			ТОГДА &ТекстПоляСписаниеВПути
	|		ИНАЧЕ &ТекстПоляПоступлениеИзПути
	|	КОНЕЦ КАК ПричинаОтмены,
	|	СУММА(РаспоряженияНаОтгрузку.СуммаОборот) КАК Сумма
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.Обороты(,,, Распоряжение В
	|		(ВЫБРАТЬ
	|			ВтДокументы.ДокументСсылка КАК ДокументСсылка
	|		ИЗ
	|			ВтДокументы КАК ВтДокументы)
	|	И Показатель В (ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.СписаноДоППС),
	|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Принято))) КАК РаспоряженияНаОтгрузку
	|СГРУППИРОВАТЬ ПО
	|	РаспоряженияНаОтгрузку.Распоряжение,
	|	РаспоряженияНаОтгрузку.Номенклатура,
	|	РаспоряженияНаОтгрузку.Характеристика,
	|	ВЫБОР
	|		КОГДА Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.СписаноДоППС)
	|			ТОГДА &ТекстПоляСписаниеВПути
	|		ИНАЧЕ &ТекстПоляПоступлениеИзПути
	|	КОНЕЦ,
	|	РаспоряженияНаОтгрузку.Номенклатура.ЕдиницаИзмерения";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыОтгрузка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПричинаОтмены", ?(СтруктураПараметров.ЕстьПричиныОтменыОтгрузки, "ДокументТовары.ПричинаОтмены", """"""));
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

Функция ТекстЗапросаТаблицаОтмененоОтгрузка(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаОтмененоОтгрузка") Тогда
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаОтмененоОтгрузка;
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТОтмененоОтгрузка.Документ КАК Документ,
	|	ВТОтмененоОтгрузка.НомерСтроки КАК НомерСтроки,
	|	ВТОтмененоОтгрузка.Номенклатура КАК Номенклатура,
	|	ВТОтмененоОтгрузка.Характеристика КАК Характеристика,
	|	ВТОтмененоОтгрузка.Количество КАК Количество,
	|	ВТОтмененоОтгрузка.Упаковка КАК Упаковка,
	|	ВТОтмененоОтгрузка.ПричинаОтмены КАК ПричинаОтмены,
	|	ВТОтмененоОтгрузка.Сумма КАК Сумма
	|ИЗ
	|	ВТОтмененоОтгрузка КАК ВТОтмененоОтгрузка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ВТОтмененоОтгрузка.Документ = ВТУпорядочивание.Документ
	|			И ВТОтмененоОтгрузка.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТОтмененоОтгрузка.Характеристика = ВТУпорядочивание.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ВТУпорядочивание.Порядок
	|ИТОГИ
	|ПО
	|	Документ";
	
	Возврат ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	
КонецФункции

Функция ТекстЗапросаТаблицаОтмененоПоступление(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаОтмененоПоступление") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаОтмененоПоступление;
	КонецЕсли;
	
	ТекстЗапроса =  "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка КАК Документ,
	|	ДокументТовары.НомерСтроки КАК НомерСтроки,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.КоличествоУпаковок КАК Количество,
	|	ВЫБОР 
	|		КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) 
	|	ТОГДА
	|		ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|	ИНАЧЕ
	|		ДокументТовары.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	&ИмяПоляПричинаОтмены КАК ПричинаОтмены,
	|	&ИмяПоляСумма КАК Сумма
	|ИЗ
	|	&ИмяТаблицы КАК ДокументТовары
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|	ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|	ПО ДокументТовары.Ссылка = ВТУпорядочивание.Документ
	|		И ДокументТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|		И ДокументТовары.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	ДокументТовары.Отменено
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ;
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + "." + СтруктураПараметров.ИмяТЧТоварыПоступление);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляСумма", ?(СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления, "ДокументТовары." + СтруктураПараметров.ИмяПоляСумма, 0));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяПоляПричинаОтмены", ?(СтруктураПараметров.ЕстьПричиныОтменыПоступления, "ДокументТовары.ПричинаОтмены", """"""));
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВТТоварыКВозврату(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТТоварыКОтгрузке") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТТоварыКОтгрузке;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ЗаявкиОстатки.Заявка КАК Заявка,
	|	ЗаявкиОстатки.Номенклатура КАК Номенклатура,
	|	ЗаявкиОстатки.Характеристика КАК Характеристика,
	|	ЗаявкиОстатки.Склад КАК Склад,
	|	СУММА(ЗаявкиОстатки.КОформлению) КАК КОформлению,
	|	СУММА(ЗаявкиОстатки.Принимается) КАК Принимается,
	|	СУММА(ЗаявкиОстатки.Принято) КАК Принято,
	|	СУММА(ЗаявкиОстатки.Оформить) КАК Оформить,
	|	СУММА(ЗаявкиОстатки.Оформлено) КАК Оформлено,
	|	СУММА(ЗаявкиОстатки.Принять) КАК Принять,
	|	СУММА(ЗаявкиОстатки.Поступило) КАК Поступило,
	|	ВЫБОР
	|		КОГДА ЗаявкиОстатки.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И &ТекущаяДата >= ЗаявкиОстатки.Склад.ДатаНачалаОрдернойСхемыПриПоступлении
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдернаяСхемаПриПоступлении
	|ПОМЕСТИТЬ ВТТоварыКВозврату
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыКПоступлению.ДокументПоступления КАК Заявка,
	|		ТоварыКПоступлению.Номенклатура КАК Номенклатура,
	|		ТоварыКПоступлению.Характеристика КАК Характеристика,
	|		ТоварыКПоступлению.Склад КАК Склад,
	|		0 КАК КОформлению,
	|		ТоварыКПоступлению.ПринимаетсяКонечныйОстаток КАК Принимается,
	|		ТоварыКПоступлению.КОформлениюОрдеровРасход КАК Принято,
	|		0 КАК Оформить,
	|		0 КАК Оформлено,
	|		ТоварыКПоступлению.КОформлениюОрдеровКонечныйОстаток КАК Принять,
	|		ТоварыКПоступлению.КОформлениюОрдеровРасход КАК Поступило
	|	ИЗ
	|		РегистрНакопления.ТоварыКПоступлению.ОстаткиИОбороты(
	|				,
	|				,
	|				,
	|				,
	|				ДокументПоступления В
	|					(ВЫБРАТЬ
	|						ВтДокументы.ДокументСсылка
	|					ИЗ
	|						ВтДокументы КАК ВтДокументы)) КАК ТоварыКПоступлению
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТоварыКВозврату.Распоряжение КАК Заявка,
	|		ТоварыКВозврату.Номенклатура КАК Номенклатура,
	|		ТоварыКВозврату.Характеристика КАК Характеристика,
	|		ТоварыКВозврату.Распоряжение.Склад КАК Склад,
	|		ТоварыКВозврату.ЗаявленоНаВозвратКоличествоОборот КАК КОформлению,
	|		0,
	|		0,
	|		(ТоварыКВозврату.КОформлениюПоВозвратамКоличествоОборот -
	|			ТоварыКВозврату.ОформленоПоВозвратамКоличествоОборот) КАК Оформить,
	|		ТоварыКВозврату.ОформленоПоВозвратамКоличествоОборот КАК Оформлено,
	|		0,
	|		0
	|	ИЗ
	|		#ЗаявкиНаВозвратТоваровОборотыРазвернутая КАК ТоварыКВозврату
	|	ГДЕ
	|		(ТоварыКВозврату.КОформлениюПоВозвратамКоличествоОборот -
	|			ТоварыКВозврату.ОформленоПоВозвратамКоличествоОборот) <> 0
	|		ИЛИ ТоварыКВозврату.ОформленоПоВозвратамКоличествоОборот <> 0) КАК ЗаявкиОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаявкиОстатки.Заявка,
	|	ЗаявкиОстатки.Номенклатура,
	|	ЗаявкиОстатки.Характеристика,
	|	ЗаявкиОстатки.Склад,
	|	ВЫБОР
	|		КОГДА ЗаявкиОстатки.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|				И &ТекущаяДата >= ЗаявкиОстатки.Склад.ДатаНачалаОрдернойСхемыПриПоступлении
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|;";
	
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В
		|		(ВЫБРАТЬ
		|				ВтДокументы.ДокументСсылка
		|			ИЗ
		|				ВтДокументы КАК ВтДокументы)";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика";
	ПараметрыФормированияТаблицы.Показатели = "ЗаявленоНаВозврат, КОформлениюПоВозвратам, ОформленоПоВозвратам";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	ЗаявкиНаВозвратТоваровОборотыРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#ЗаявкиНаВозвратТоваровОборотыРазвернутая",
		ЗаявкиНаВозвратТоваровОборотыРазвернутая);
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВТВозвращаемыеТовары(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВТВозвращаемыеТовары") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВТВозвращаемыеТовары;
	КонецЕсли;
	
	ТекстЗапроса = "
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА
	|			ДокументТовары.Порча
	|				И ДокументТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА
	|			ДокументТовары.НоменклатураОприходование
	|		ИНАЧЕ ДокументТовары.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	ВЫБОР КОГДА
	|			ДокументТовары.Порча
	|				И ДокументТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА
	|			ДокументТовары.ХарактеристикаОприходование
	|		ИНАЧЕ ДокументТовары.Характеристика
	|	КОНЕЦ КАК Характеристика,
	|	СУММА(ДокументТовары.Количество) КАК Количество,
	|	СУММА(ДокументТовары.СуммаСНДС) КАК СуммаСНДС,
	|	ВтДокументы.ДокументСсылка КАК Ссылка
	|ПОМЕСТИТЬ ВТВозвращаемыеТовары
	|ИЗ
	|	Документ.ЗаявкаНаВозвратТоваровОтКлиента.ВозвращаемыеТовары КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|ГДЕ
	|	НЕ ДокументТовары.Отменено
	|	И ВтДокументы.Проведен
	|	И НЕ ДокументТовары.Ссылка.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВозвратТоваровОтКлиентов.НеСогласована), 
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЗаявокНаВозвратТоваровОтКлиентов.Отклонена))
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА
	|			ДокументТовары.Порча
	|				И ДокументТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА
	|			ДокументТовары.НоменклатураОприходование
	|		ИНАЧЕ ДокументТовары.Номенклатура
	|	КОНЕЦ,
	|	ВЫБОР КОГДА
	|			ДокументТовары.Порча
	|				И ДокументТовары.Ссылка.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратОтКомиссионера) ТОГДА
	|			ДокументТовары.ХарактеристикаОприходование
	|		ИНАЧЕ ДокументТовары.Характеристика
	|	КОНЕЦ,
	|	ДокументТовары.Порча,
	|	ДокументТовары.Ссылка.ХозяйственнаяОперация,
	|	ВтДокументы.ДокументСсылка
	|;
	|";
	Возврат ТекстЗапроса
	
	
КонецФункции

Функция ТекстЗапросаТаблицаВозвратТоваров(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаВозвратТоваров") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаВозвратТоваров;
	КонецЕсли;
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТТоварыКВозврату.Заявка КАК Документ,
	|	ВТТоварыКВозврату.Склад КАК Склад,
	|	ВТТоварыКВозврату.Принимается КАК Принимается,
	|	ВТТоварыКВозврату.Принято КАК Принято,
	|	ВТТоварыКВозврату.КОформлению КАК КОформлению,
	|	ВТТоварыКВозврату.Оформлено КАК Оформлено,
	|	ВТТоварыКВозврату.Оформить КАК Оформить,
	|	ВТТоварыКВозврату.Принять КАК Принять,
	|	ВТТоварыКВозврату.Поступило КАК Поступило,
	|	ВТТоварыКВозврату.ОрдернаяСхемаПриПоступлении КАК ОрдернаяСхемаПриПоступлении,
	|	ВТТоварыКВозврату.Номенклатура КАК Номенклатура,
	|	ВТТоварыКВозврату.Характеристика КАК Характеристика
	|ИЗ
	|	ВТТоварыКВозврату КАК ВТТоварыКВозврату
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВозвращаемыеТовары КАК ВТВозвращаемыеТовары
	|		ПО ВТТоварыКВозврату.Заявка = ВТВозвращаемыеТовары.Ссылка
	|			И ВТТоварыКВозврату.Номенклатура = ВТВозвращаемыеТовары.Номенклатура
	|			И ВТТоварыКВозврату.Характеристика = ВТВозвращаемыеТовары.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ВТТоварыКВозврату.Заявка = ВТУпорядочивание.Документ
	|			И ВТТоварыКВозврату.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТТоварыКВозврату.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	НЕ ВТВозвращаемыеТовары.Номенклатура ЕСТЬ NULL 
	|	И (ВТТоварыКВозврату.Принять <> 0
	|			ИЛИ ВТТоварыКВозврату.Оформить <> 0
	|			ИЛИ ВТТоварыКВозврату.Оформлено <> ВТТоварыКВозврату.Поступило
	|				И ВТТоварыКВозврату.ОрдернаяСхемаПриПоступлении)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ
	|	МАКСИМУМ(ОрдернаяСхемаПриПоступлении)
	|ПО
	|	Документ,
	|	Склад
	|;";
	
	Возврат ТекстЗапроса
	
	
КонецФункции

Функция ТекстЗапросаТаблицаВозвращеноТоваров(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаВозвращеноТоваров") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаВозвращеноТоваров;
	КонецЕсли;
	
	ТекстЗапроса =" 
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТВозвращаемыеТовары.Ссылка КАК Документ,
	|	ВТВозвращаемыеТовары.Номенклатура КАК Номенклатура,
	|	ВТВозвращаемыеТовары.Характеристика КАК Характеристика,
	|	ВТВозвращаемыеТовары.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
	|	ВТВозвращаемыеТовары.Количество КАК Количество,
	|	ВТВозвращаемыеТовары.СуммаСНДС КАК Сумма
	|ИЗ
	|	ВТВозвращаемыеТовары КАК ВТВозвращаемыеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТТоварыКВозврату КАК ВТТоварыКВозврату
	|		ПО (ВТТоварыКВозврату.Заявка = ВТВозвращаемыеТовары.Ссылка)
	|			И (ВТТоварыКВозврату.Номенклатура = ВТВозвращаемыеТовары.Номенклатура)
	|			И (ВТТоварыКВозврату.Характеристика = ВТВозвращаемыеТовары.Характеристика)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ВТВозвращаемыеТовары.Ссылка = ВТУпорядочивание.Документ
	|			И ВТВозвращаемыеТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ВТВозвращаемыеТовары.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	(ВТТоварыКВозврату.Принять = 0
	|	И ВТТоварыКВозврату.Оформить = 0
	|	И (ВТТоварыКВозврату.Оформлено = ВТТоварыКВозврату.Поступило
	|		ИЛИ НЕ ВТТоварыКВозврату.ОрдернаяСхемаПриПоступлении))
	|		ИЛИ ВТТоварыКВозврату.Заявка ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ;
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

//++ НЕ УТ

//++ НЕ УТКА

Функция ТекстЗапросаВТПродукцияПоЗаказуДавальца()
	
	ТекстЗапроса = 
	"
	|ВЫБРАТЬ
	|	ЗаказДавальца2_5Продукция.Номенклатура КАК Номенклатура,
	|	ЗаказДавальца2_5Продукция.Характеристика КАК Характеристика,
	|	МАКСИМУМ(ЗаказДавальца2_5Продукция.Упаковка) КАК Упаковка,
	|	СУММА(ЗаказДавальца2_5Продукция.Количество) КАК Количество,
	|	СУММА(ЗаказДавальца2_5Продукция.СуммаСНДС) КАК Сумма,
	|	ЗаказДавальца2_5Продукция.Ссылка КАК Ссылка,
	|	МИНИМУМ(ЗаказДавальца2_5Продукция.НомерСтроки) КАК НомерСтроки
	|ПОМЕСТИТЬ ВТПродукцияПоЗаказуДавальца
	|ИЗ
	|	Документ.ЗаказДавальца2_5.Продукция КАК ЗаказДавальца2_5Продукция
	|ГДЕ
	|	ЗаказДавальца2_5Продукция.Ссылка В(&МассивДокументов)
	|	И НЕ ЗаказДавальца2_5Продукция.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказДавальца2_5Продукция.Номенклатура,
	|	ЗаказДавальца2_5Продукция.Характеристика,
	|	ЗаказДавальца2_5Продукция.Ссылка
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказДавальцаПродукция.Номенклатура КАК Номенклатура,
	|	ЗаказДавальцаПродукция.Характеристика КАК Характеристика,
	|	МАКСИМУМ(ЗаказДавальцаПродукция.Упаковка) КАК Упаковка,
	|	СУММА(ЗаказДавальцаПродукция.Количество) КАК Количество,
	|	СУММА(ЗаказДавальцаПродукция.СуммаСНДС) КАК Сумма,
	|	ЗаказДавальцаПродукция.Ссылка КАК Ссылка,
	|	МИНИМУМ(ЗаказДавальцаПродукция.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	Документ.ЗаказДавальца.Продукция КАК ЗаказДавальцаПродукция
	|ГДЕ
	|	ЗаказДавальцаПродукция.Ссылка В(&МассивДокументов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаказДавальцаПродукция.Номенклатура,
	|	ЗаказДавальцаПродукция.Характеристика,
	|	ЗаказДавальцаПродукция.Ссылка
	//-- Устарело_Переработка24
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика
	|;";

	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаТаблицаУслугиДавальцуКОформлению()
	
	ТекстЗапроса = 
	"
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспоряженияНаОтгрузкуОбороты.Распоряжение                                                       КАК Документ,
	|	РаспоряженияНаОтгрузкуОбороты.Номенклатура                                                       КАК Номенклатура,
	|	РаспоряженияНаОтгрузкуОбороты.Характеристика                                                     КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка
	|	КОНЕЦ                                                                                    КАК Упаковка,
	|	(РаспоряженияНаОтгрузкуОбороты.КОформлениюКоличествоОборот -
	|		РаспоряженияНаОтгрузкуОбороты.ОформленоКоличествоОборот) * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Количество,
	|	ВЫБОР
	|		КОГДА (РаспоряженияНаОтгрузкуОбороты.КОформлениюКоличествоОборот -
	|		РаспоряженияНаОтгрузкуОбороты.ОформленоКоличествоОборот) = ВложенныйЗапрос.Количество
	|			ТОГДА ВложенныйЗапрос.Сумма
	|		ИНАЧЕ ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма
	|			/ ВложенныйЗапрос.Количество
	|			* (РаспоряженияНаОтгрузкуОбороты.КОформлениюКоличествоОборот -
	|				РаспоряженияНаОтгрузкуОбороты.ОформленоКоличествоОборот) КАК ЧИСЛО(31, 2))
	|	КОНЕЦ                                                                                    КАК Сумма,
	|	ВТУпорядочивание.Порядок                                                                 КАК НомерСтроки
	|ИЗ
	|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОбороты
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПродукцияПоЗаказуДавальца КАК ВложенныйЗапрос
	|		ПО РаспоряженияНаОтгрузкуОбороты.Распоряжение   = ВложенныйЗапрос.Ссылка
	|			И РаспоряженияНаОтгрузкуОбороты.Номенклатура   = ВложенныйЗапрос.Номенклатура
	|			И РаспоряженияНаОтгрузкуОбороты.Характеристика = ВложенныйЗапрос.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО РаспоряженияНаОтгрузкуОбороты.Распоряжение = ВТУпорядочивание.Документ
	|			И РаспоряженияНаОтгрузкуОбороты.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И РаспоряженияНаОтгрузкуОбороты.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	(РаспоряженияНаОтгрузкуОбороты.КОформлениюКоличествоОборот -
	|		РаспоряженияНаОтгрузкуОбороты.ОформленоКоличествоОборот) > 0
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УслугиДавальцуКОформлениюОстатки.ЗаказДавальца КАК Документ,
	|	УслугиДавальцуКОформлениюОстатки.Номенклатура КАК Номенклатура,
	|	УслугиДавальцуКОформлениюОстатки.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|			ТОГДА ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	УслугиДавальцуКОформлениюОстатки.КОформлениюОстаток * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Количество,
	|	ВЫБОР
	|		КОГДА УслугиДавальцуКОформлениюОстатки.КОформлениюОстаток = ВложенныйЗапрос.Количество
	|			ТОГДА ВложенныйЗапрос.Сумма
	|		ИНАЧЕ ВЫРАЗИТЬ(ВложенныйЗапрос.Сумма
	|						/ ВложенныйЗапрос.Количество
	|						* УслугиДавальцуКОформлениюОстатки.КОформлениюОстаток КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК Сумма,
	|	ВТУпорядочивание.Порядок КАК НомерСтроки
	|ИЗ
	|	РегистрНакопления.УслугиДавальцуКОформлению.Остатки(, 
	|				(ЗаказДавальца, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ 
	|						ВТПродукцияПоЗаказуДавальца.Ссылка КАК Ссылка,
	|						ВТПродукцияПоЗаказуДавальца.Номенклатура КАК Номенклатура,
	|						ВТПродукцияПоЗаказуДавальца.Характеристика КАК Характеристика
	|					ИЗ ВТПродукцияПоЗаказуДавальца КАК ВТПродукцияПоЗаказуДавальца)) КАК УслугиДавальцуКОформлениюОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПродукцияПоЗаказуДавальца КАК ВложенныйЗапрос
	|		ПО УслугиДавальцуКОформлениюОстатки.ЗаказДавальца = ВложенныйЗапрос.Ссылка
	|			И УслугиДавальцуКОформлениюОстатки.Номенклатура = ВложенныйЗапрос.Номенклатура
	|			И УслугиДавальцуКОформлениюОстатки.Характеристика = ВложенныйЗапрос.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО УслугиДавальцуКОформлениюОстатки.ЗаказДавальца = ВТУпорядочивание.Документ
	|			И УслугиДавальцуКОформлениюОстатки.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И УслугиДавальцуКОформлениюОстатки.Характеристика = ВТУпорядочивание.Характеристика
	|ГДЕ
	|	УслугиДавальцуКОформлениюОстатки.КОформлениюОстаток > 0
	//-- Устарело_Переработка24
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ
	|;";
	
	// подзапрос #РаспоряженияНаОтгрузкуОборотыРазвернутая
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"(Распоряжение, Номенклатура, Характеристика) В
		|		(ВЫБРАТЬ
		|				Ссылка,
		|				Номенклатура,
		|				Характеристика
		|			ИЗ
		|				ВТПродукцияПоЗаказуДавальца)
		|		И ТИПЗНАЧЕНИЯ(Распоряжение) = ТИП(Документ.ЗаказДавальца2_5)";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика";
	ПараметрыФормированияТаблицы.Показатели = "КОформлению, Оформлено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
		РаспоряженияНаОтгрузкуОборотыРазвернутая);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ВложенныйЗапрос.Упаковка",
			"ВложенныйЗапрос.Номенклатура"));
	
	Возврат ТекстЗапроса;
	
КонецФункции

//-- НЕ УТКА

Функция ТекстЗапросаТаблицаПродукцияКОформлениюВОтчетеПереработчику(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
	   И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаПродукцияКОформлениюВОтчетеПереработчику") Тогда
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаПродукцияКОформлениюВОтчетеПереработчику;
	КонецЕсли;
	
	ТекстЗапроса = "";
	
	//++ Устарело_Переработка24
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Т.Распоряжение КАК Документ,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура.Представление КАК НоменклатураПредставление,
	|	Т.АналитикаУчетаНоменклатуры.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	Т.АналитикаУчетаНоменклатуры.Серия.Представление КАК СерияПредставление,
	|	Т.АналитикаУчетаНоменклатуры.Номенклатура.ЕдиницаИзмерения.Представление КАК Упаковка,
	|	Т.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПолученныеОтПереработчика.Остатки(, Распоряжение В (&МассивДокументов)) КАК Т
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХС КАК ВТУпорядочивание
	|		ПО Т.Распоряжение = ВТУпорядочивание.Документ
	|			И Т.АналитикаУчетаНоменклатуры.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И Т.АналитикаУчетаНоменклатуры.Характеристика = ВТУпорядочивание.Характеристика
	|			И Т.АналитикаУчетаНоменклатуры.Серия = ВТУпорядочивание.Серия
	|ГДЕ
	|	Т.КоличествоОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|ИТОГИ ПО
	|	Документ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	//-- Устарело_Переработка24
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ТекстЗапросаТаблицаСырьеУПереработчика(ТекстыЗапроса, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
	   И СтруктураДопЗапросов.Свойство("ТаблицаСырьеУПереработчика") Тогда
	   	
		ТекстЗапроса = СтруктураДопЗапросов.ТаблицаСырьеУПереработчика;
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаСырьеУПереработчика");
		
		Возврат;
		
	КонецЕсли;

	ТекстЗапроса =  
	// 0
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Документ,
	|	ВложенныйЗапрос.Переработчик,
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КоличествоЕдиницИзмерения > 1
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ КАК КоэффициентУпаковки,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КоличествоЕдиницИзмерения > 1
	|			ТОГДА ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
	|ПОМЕСТИТЬ ДокументТовары
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВтДокументы.ДокументСсылка КАК Документ,
	|		ТаблицаДокумент.Партнер КАК Переработчик,
	|		ДокументТовары.Номенклатура КАК Номенклатура,
	|		ДокументТовары.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ КАК Упаковка,
	|		МИНИМУМ(ДокументТовары.НомерСтроки) КАК НомерСтроки,
	|		СУММА(ДокументТовары.Количество) КАК Количество,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументТовары.Упаковка) КАК КоличествоЕдиницИзмерения
	|	ИЗ
	|		ВтДокументы КАК ВтДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5 КАК ТаблицаДокумент
	|			ПО (ТаблицаДокумент.Ссылка = ВтДокументы.ДокументСсылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5.ОбеспечениеМатериаламиИРаботами КАК ДокументТовары
	|			ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|				И НЕ ДокументТовары.Отменено
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВтДокументы.ДокументСсылка,
	|		ТаблицаДокумент.Партнер,
	|		ДокументТовары.Номенклатура,
	|		ДокументТовары.Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ
	//++ Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВтДокументы.ДокументСсылка КАК Документ,
	|		ТаблицаДокумент.Партнер КАК Переработчик,
	|		ДокументТовары.Номенклатура КАК Номенклатура,
	|		ДокументТовары.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ КАК Упаковка,
	|		МИНИМУМ(ДокументТовары.НомерСтроки) КАК НомерСтроки,
	|		СУММА(ДокументТовары.Количество) КАК Количество,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументТовары.Упаковка) КАК КоличествоЕдиницИзмерения
	|	ИЗ
	|		ВтДокументы КАК ВтДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ТаблицаДокумент
	|			ПО (ТаблицаДокумент.Ссылка = ВтДокументы.ДокументСсылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Материалы КАК ДокументТовары
	|			ПО ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка
	|				И НЕ ДокументТовары.Отменено
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ВтДокументы.ДокументСсылка,
	|		ТаблицаДокумент.Партнер,
	|		ДокументТовары.Номенклатура,
	|		ДокументТовары.Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ
	//-- Устарело_Переработка24
	|
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВтДокументы.ДокументСсылка КАК Документ,
	|		ТаблицаДокумент.Партнер КАК Переработчик,
	|		ДокументТовары.Номенклатура КАК Номенклатура,
	|		ДокументТовары.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ КАК Упаковка,
	|		МИНИМУМ(ДокументТовары.НомерСтроки) КАК НомерСтроки,
	|		СУММА(ДокументТовары.Количество) КАК Количество,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументТовары.Упаковка) КАК КоличествоЕдиницИзмерения
	|	ИЗ
	|		ВтДокументы КАК ВтДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ТаблицаДокумент
	|			ПО (ТаблицаДокумент.Ссылка = ВтДокументы.ДокументСсылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
	|			ПО ТаблицаДокумент.Ссылка = ЭтапПроизводства.ЗаказПереработчику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ДокументТовары
	|			ПО ЭтапПроизводства.Ссылка = ДокументТовары.Ссылка
	|				И НЕ ДокументТовары.Отменено
	|
	|	СГРУППИРОВАТЬ ПО
	|		ВтДокументы.ДокументСсылка,
	|		ТаблицаДокумент.Партнер,
	|		ДокументТовары.Номенклатура,
	|		ДокументТовары.Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ
	//-- Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВтДокументы.ДокументСсылка КАК Документ,
	|		ТаблицаДокумент.Партнер КАК Переработчик,
	|		ДокументТовары.Номенклатура КАК Номенклатура,
	|		ДокументТовары.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ КАК Упаковка,
	|		МИНИМУМ(ДокументТовары.НомерСтроки) КАК НомерСтроки,
	|		СУММА(ДокументТовары.Количество) КАК Количество,
	|		КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДокументТовары.Упаковка) КАК КоличествоЕдиницИзмерения
	|	ИЗ
	|		ВтДокументы КАК ВтДокументы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5 КАК ТаблицаДокумент
	|			ПО (ТаблицаДокумент.Ссылка = ВтДокументы.ДокументСсылка)
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства
	|			ПО ТаблицаДокумент.Ссылка = ЭтапПроизводства.ЗаказПереработчику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ДокументТовары
	|			ПО ЭтапПроизводства.Ссылка = ДокументТовары.Ссылка
	|				И НЕ ДокументТовары.Отменено
	|
	|	СГРУППИРОВАТЬ ПО
	|		ВтДокументы.ДокументСсылка,
	|		ТаблицаДокумент.Партнер,
	|		ДокументТовары.Номенклатура,
	|		ДокументТовары.Характеристика,
	|		ВЫБОР
	|			КОГДА ДокументТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|				ТОГДА ДокументТовары.Номенклатура.ЕдиницаИзмерения
	|			ИНАЧЕ ДокументТовары.Упаковка
	|		КОНЕЦ
	//-- НЕ УТКА
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Документ,
	|	ВложенныйЗапрос.Переработчик,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КоличествоЕдиницИзмерения > 1
	|			ТОГДА 1
	|		ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВложенныйЗапрос.КоличествоЕдиницИзмерения > 1
	|			ТОГДА ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения
	|		ИНАЧЕ ВложенныйЗапрос.Упаковка
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"ВложенныйЗапрос.Упаковка",
		"ВложенныйЗапрос.Номенклатура"));
		
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТ_ДокументТовары");
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументТовары.Переработчик,
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	ДокументТовары КАК ДокументТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Переработчик,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТ_Товары");
	
	ТекстЗапроса = "
	// 2
	|ВЫБРАТЬ
	|	АналитикаНоменклатуры.КлючАналитики КАК КлючАналитики,
	|	АналитикаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаНоменклатуры.Характеристика КАК Характеристика,
	|	ВЫРАЗИТЬ(АналитикаНоменклатуры.МестоХранения КАК Справочник.Партнеры) КАК Переработчик
	|ПОМЕСТИТЬ АналитикаНоменклатуры
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаНоменклатуры
	|ГДЕ
	|	(АналитикаНоменклатуры.Номенклатура, АналитикаНоменклатуры.Характеристика, АналитикаНоменклатуры.МестоХранения) В
	|			(ВЫБРАТЬ
	|				ВТ_Товары.Номенклатура,
	|				ВТ_Товары.Характеристика,
	|				ВТ_Товары.Переработчик
	|			ИЗ
	|				ВТ_Товары)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючАналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТ_АналитикаНоменклатуры");
	
	ТекстЗапроса = "
	// 3
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 0
	|	АналитикаНоменклатуры.Переработчик КАК Переработчик,
	|	АналитикаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаНоменклатуры.Характеристика КАК Характеристика,
	|	0 КАК Количество
	|ПОМЕСТИТЬ ТаблицаОстатки
	|ИЗ
	|	АналитикаНоменклатуры КАК АналитикаНоменклатуры
	//++ Устарело_Переработка24
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	АналитикаНоменклатуры.Переработчик КАК Переработчик,
	|	АналитикаНоменклатуры.Номенклатура КАК Номенклатура,
	|	АналитикаНоменклатуры.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыПереданныеПереработчику.Остатки(
	|			,
	|			АналитикаУчетаНоменклатуры В
	|				(ВЫБРАТЬ
	|					АналитикаНоменклатуры.КлючАналитики
	|				ИЗ
	|					АналитикаНоменклатуры)) КАК ТаблицаОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ АналитикаНоменклатуры КАК АналитикаНоменклатуры
	|		ПО (АналитикаНоменклатуры.КлючАналитики = ТаблицаОстатки.АналитикаУчетаНоменклатуры)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ 
	|	ТаблицаОстатки.Партнер КАК Переработчик,
	|	ТаблицаОстатки.Номенклатура КАК Номенклатура,
	|	ТаблицаОстатки.Характеристика КАК Характеристика,
	|	ТаблицаОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара.Остатки(
	|			,
	|			(Номенклатура, Характеристика, Партнер) В
	|				(ВЫБРАТЬ
	|					ВТ_Товары.Номенклатура,
	|					ВТ_Товары.Характеристика,
	|					ВТ_Товары.Переработчик
	|				ИЗ
	|					ВТ_Товары)
	|			И ДокументПередачи ССЫЛКА Документ.ПередачаСырьяПереработчику) КАК ТаблицаОстатки
	//-- Устарело_Переработка24
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТ_ТаблицаОстатки");
	
	ТекстЗапроса = "
	// 4
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Переработчик,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	СУММА(ВложенныйЗапрос.Заказано - ВложенныйЗапрос.Оформлено) КАК Потребность
	|ПОМЕСТИТЬ ТаблицаПотребность
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ВТ_Товары.Переработчик КАК Переработчик,
	|		ВТ_Товары.Номенклатура КАК Номенклатура,
	|		ВТ_Товары.Характеристика КАК Характеристика,
	|		ЗаказМатериалов.Количество КАК Заказано,
	|		0 КАК Оформлено
	|	ИЗ
	|		ВТ_Товары КАК ВТ_Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5.ОбеспечениеМатериаламиИРаботами КАК ЗаказМатериалов
	|			ПО ЗаказМатериалов.Номенклатура = ВТ_Товары.Номенклатура
	|				И ЗаказМатериалов.Характеристика = ВТ_Товары.Характеристика
	|				И ЗаказМатериалов.Ссылка.Партнер = ВТ_Товары.Переработчик
	|				И ЗаказМатериалов.Ссылка.Проведен
	|				И ЗаказМатериалов.Ссылка.ГруппировкаЗатрат <> ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//++ Устарело_Переработка24
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Товары.Переработчик КАК Переработчик,
	|		ВТ_Товары.Номенклатура КАК Номенклатура,
	|		ВТ_Товары.Характеристика КАК Характеристика,
	|		ЗаказМатериалов.Количество КАК Заказано,
	|		0 КАК Оформлено
	|	ИЗ
	|		ВТ_Товары КАК ВТ_Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику.Материалы КАК ЗаказМатериалов
	|			ПО ЗаказМатериалов.Номенклатура = ВТ_Товары.Номенклатура
	|				И ЗаказМатериалов.Характеристика = ВТ_Товары.Характеристика
	|				И ЗаказМатериалов.Ссылка.Партнер = ВТ_Товары.Переработчик
	|				И ЗаказМатериалов.Ссылка.Проведен
	|				И ЗаказМатериалов.Ссылка.ГруппировкаЗатрат <> ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	//-- Устарело_Переработка24
	
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Товары.Переработчик КАК Переработчик,
	|		ВТ_Товары.Номенклатура КАК Номенклатура,
	|		ВТ_Товары.Характеристика КАК Характеристика,
	|		ЗаказМатериалов.Количество КАК Заказано,
	|		0 КАК Оформлено
	|	ИЗ
	|		ВТ_Товары КАК ВТ_Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику КАК ДокЗаказПереработчику
	|			ПО ДокЗаказПереработчику.Партнер = ВТ_Товары.Переработчик
	|				И ДокЗаказПереработчику.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|				И ДокЗаказПереработчику.Проведен
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДокЭтап
	|			ПО ДокЗаказПереработчику.Ссылка = ДокЭтап.ЗаказПереработчику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЗаказМатериалов
	|			ПО ЗаказМатериалов.Ссылка = ДокЭтап.Ссылка
	|				И ЗаказМатериалов.Номенклатура = ВТ_Товары.Номенклатура
	|				И ЗаказМатериалов.Характеристика = ВТ_Товары.Характеристика
	|				И НЕ ЗаказМатериалов.Отменено
	//-- Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Товары.Переработчик КАК Переработчик,
	|		ВТ_Товары.Номенклатура КАК Номенклатура,
	|		ВТ_Товары.Характеристика КАК Характеристика,
	|		ЗаказМатериалов.Количество КАК Заказано,
	|		0 КАК Оформлено
	|	ИЗ
	|		ВТ_Товары КАК ВТ_Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказПереработчику2_5 КАК ДокЗаказПереработчику
	|			ПО ДокЗаказПереработчику.Партнер = ВТ_Товары.Переработчик
	|				И ДокЗаказПереработчику.ГруппировкаЗатрат = ЗНАЧЕНИЕ(Перечисление.ГруппировкиЗатратВЗаказеПереработчику.ПоЭтапамПроизводства)
	|				И ДокЗаказПереработчику.Проведен
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДокЭтап
	|			ПО ДокЗаказПереработчику.Ссылка = ДокЭтап.ЗаказПереработчику
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ЗаказМатериалов
	|			ПО ЗаказМатериалов.Ссылка = ДокЭтап.Ссылка
	|				И ЗаказМатериалов.Номенклатура = ВТ_Товары.Номенклатура
	|				И ЗаказМатериалов.Характеристика = ВТ_Товары.Характеристика
	|				И НЕ ЗаказМатериалов.Отменено
	//-- НЕ УТКА
	
	//++ Устарело_Переработка24
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТ_Товары.Переработчик,
	|		ВТ_Товары.Номенклатура,
	|		ВТ_Товары.Характеристика,
	|		0,
	|		ОтчетПереработчикаМатериалы.Количество
	|	ИЗ
	|		ВТ_Товары КАК ВТ_Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Материалы КАК ОтчетПереработчикаМатериалы
	|			ПО ОтчетПереработчикаМатериалы.Номенклатура = ВТ_Товары.Номенклатура
	|				И ОтчетПереработчикаМатериалы.Характеристика = ВТ_Товары.Характеристика
	|				И ОтчетПереработчикаМатериалы.Ссылка.Партнер = ВТ_Товары.Переработчик
	|				И ОтчетПереработчикаМатериалы.Ссылка.Проведен
	|				И ОтчетПереработчикаМатериалы.Ссылка.ПоЗаказам
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратСырья.АналитикаУчетаНоменклатуры.МестоХранения КАК Переработчик,
	|		ВозвратСырья.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|		ВозвратСырья.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|		0 КАК Заказано,
	|		ВЫБОР ВозвратСырья.ВидДвижения
	|			КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА ВозвратСырья.Количество
	|			КОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ВозвратСырья.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК Оформлено
	|	ИЗ
	|		РегистрНакопления.ТоварыПереданныеПереработчику КАК ВозвратСырья
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ АналитикаНоменклатуры КАК АналитикаНоменклатуры
	|			ПО ВозвратСырья.АналитикаУчетаНоменклатуры = АналитикаНоменклатуры.КлючАналитики
	|	ГДЕ
	|		ВозвратСырья.Регистратор ССЫЛКА Документ.ВозвратСырьяОтПереработчика
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВозвратТары.Партнер КАК Переработчик,
	|		ВозвратТары.Номенклатура КАК Номенклатура,
	|		ВозвратТары.Характеристика КАК Характеристика,
	|		ЕСТЬNULL(-ВозвратТары.КоличествоРасход, 0) КАК Заказано,
	|		0 КАК Оформлено
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара.Обороты(, , ,
	|			(Номенклатура, Характеристика, Партнер) В
	|				(ВЫБРАТЬ
	|					ВТ_Товары.Номенклатура,
	|					ВТ_Товары.Характеристика,
	|					ВТ_Товары.Переработчик
	|				ИЗ
	|					ВТ_Товары)
	|			И ДокументПередачи ССЫЛКА Документ.ПередачаСырьяПереработчику) КАК ВозвратТары
	//-- Устарело_Переработка24
	|
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Переработчик,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Переработчик,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ВТ_ТаблицаПотребность");
	
	ТекстЗапроса = "
	// 5
	|ВЫБРАТЬ
	|	ДокументТовары.Документ КАК Документ,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ДокументТовары.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ДокументТовары.Упаковка КАК Упаковка,
	|	ЕСТЬNULL(ТаблицаОстатки.Количество, 0) * ДокументТовары.КоэффициентУпаковки КАК ВНаличии,
	|	ЕСТЬNULL(ТаблицаПотребность.Потребность, 0) * ДокументТовары.КоэффициентУпаковки КАК Потребность,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаПотребность.Потребность, 0) >= 0
	|			ТОГДА (ЕСТЬNULL(ТаблицаОстатки.Количество, 0) - ЕСТЬNULL(ТаблицаПотребность.Потребность, 0)) * ДокументТовары.КоэффициентУпаковки
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Излишек
	|ИЗ
	|	ДокументТовары КАК ДокументТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПотребность КАК ТаблицаПотребность
	|		ПО (ТаблицаПотребность.Номенклатура = ДокументТовары.Номенклатура)
	|			И (ТаблицаПотребность.Характеристика = ДокументТовары.Характеристика)
	|			И (ТаблицаПотребность.Переработчик = ДокументТовары.Переработчик)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаОстатки КАК ТаблицаОстатки
	|		ПО (ТаблицаОстатки.Номенклатура = ДокументТовары.Номенклатура)
	|			И (ТаблицаОстатки.Характеристика = ДокументТовары.Характеристика)
	|			И (ТаблицаОстатки.Переработчик = ДокументТовары.Переработчик)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУпорядочиваниеНХ КАК ВТУпорядочивание
	|		ПО ДокументТовары.Документ = ВТУпорядочивание.Документ
	|			И ДокументТовары.Номенклатура = ВТУпорядочивание.Номенклатура
	|			И ДокументТовары.Характеристика = ВТУпорядочивание.Характеристика
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТУпорядочивание.Порядок
	|
	|ИТОГИ ПО
	|	Документ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ТекстыЗапроса.Добавить(ТекстЗапроса, "ТаблицаСырьеУПереработчика");
	
КонецПроцедуры

//-- НЕ УТ

Функция ТекстЗапросаВТУпорядочиваниеНХС()

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВтДокументы.ДокументСсылка КАК Документ,
	|	МИНИМУМ(ДокументТовары.НомерСтроки) КАК Порядок,
	|	ДокументТовары.Номенклатура КАК Номенклатура,
	|	ДокументТовары.Характеристика КАК Характеристика,
	|	ДокументТовары.Серия КАК Серия
	|ПОМЕСТИТЬ ВТУпорядочиваниеНХС
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ДокументТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
	|		ПО (ВтДокументы.ДокументСсылка = ДокументТовары.Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВтДокументы.ДокументСсылка,
	|	ДокументТовары.Номенклатура,
	|	ДокументТовары.Характеристика,
	|	ДокументТовары.Серия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаВТУпорядочиваниеНХ()

	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТУпорядочиваниеНХС.Документ КАК Документ,
	|	МИНИМУМ(ВТУпорядочиваниеНХС.Порядок) КАК Порядок,
	|	ВТУпорядочиваниеНХС.Номенклатура КАК Номенклатура,
	|	ВТУпорядочиваниеНХС.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ ВТУпорядочиваниеНХ
	|ИЗ
	|	ВТУпорядочиваниеНХС КАК ВТУпорядочиваниеНХС
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТУпорядочиваниеНХС.Документ,
	|	ВТУпорядочиваниеНХС.Номенклатура,
	|	ВТУпорядочиваниеНХС.Характеристика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////";

	Возврат ТекстЗапроса;

КонецФункции

#КонецОбласти

#Область ВыводДанных

// Параметры:
// 	ТаблицаОтчета - ТабличныйДокумент
// 	Макет - ТабличныйДокумент
// 	Выборка - СтрокаТаблицыЗначений - должна содержать:
// 		* Ссылка - ДокументСсылка
// 		* НомерДокумента - Строка
// 		* ДатаДокумента - Дата
//
Процедура ВывестиЗаголовокОтчета(ТаблицаОтчета, Макет, Выборка)
	
	Область = Макет.ПолучитьОбласть("Заголовок");
	
	СтруктураПараметров = Новый Структура;
	Если ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ГрафикИсполненияДоговора") Тогда
		СтруктураПараметров.Вставить("ИмяДокумента", Строка(Выборка.Ссылка));
	//++ НЕ УТКА
	ИначеЕсли ТипЗнч(Выборка.Ссылка) = Тип("ДокументСсылка.ПриемкаТоваровНаХранение")
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.Ссылка, "ХозяйственнаяОперация") = 
				Перечисления.ХозяйственныеОперации.ПоступлениеОтДавальца2_5 Тогда
		СтруктураПараметров.Вставить(
			"ИмяДокумента",
			СтрШаблон(НСтр("ru = '%1 %2 от %3';
							|en = '%1 %2 dated %3'"),
					  ДавальческаяСхемаКлиентСервер.СинонимДокументаПриемкаТоваровНаХранение(),
					  Выборка.НомерДокумента,
					  Формат(Выборка.ДатаДокумента, "ДЛФ=D")));
	//-- НЕ УТКА
	Иначе
		СтруктураПараметров.Вставить("ИмяДокумента", ОбщегоНазначенияУТ.ПолучитьПредставлениеДокумента(Выборка.Ссылка, 
		Выборка.НомерДокумента, 
		Выборка.ДатаДокумента));
	КонецЕсли;
	Область.Параметры.Заполнить(СтруктураПараметров);
	Область.Параметры.СтруктураПараметров = Новый Структура("Заказ, Действие", Выборка.Ссылка, "ОткрытьЗначение");
	
	Если КлиентскоеПриложение.ТекущийВариантИнтерфейса() = ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		ОбластьЗаголовок = Область.Области.ОбластьЗаголовок; // ОбластьЯчеекТабличногоДокумента
		ОбластьЗаголовок.ЦветТекста = ЦветаСтиля.ЦветТекстаЗаголовокОтчетаВТакси;
	КонецЕсли;
	
	ТаблицаОтчета.Вывести(Область);
	
КонецПроцедуры

Функция ВывестиТаблицуПодчиненныеДокументы(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаПодчиненныеДокументы") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПодчиненныеДокументы = ТаблицыРезультатов.ТаблицаПодчиненныеДокументы.Строки.Найти(Документ, "Документ");
	Если ПодчиненныеДокументы = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ПорядокРасчетов";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.Заполнить(ПодчиненныеДокументы);
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);

	Заголовок = НСтр("ru = 'Объекты расчетов (%1%)';
					|en = 'AR/AP objects (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", ПодчиненныеДокументы.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "СтрокаПодчиненныйДокумент";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	Для каждого СтрокаДокумент Из ПодчиненныеДокументы.Строки Цикл
		Область.Параметры.ПредставлениеДокумента = Строка(СтрокаДокумент.ПодчиненныйДокумент)
			+ " " + НСтр("ru = 'на сумму';
						|en = 'in the amount of'") + " " + Формат(СтрокаДокумент.СуммаДокумента,"ЧДЦ=2") + " " 
			+ СтрокаДокумент.Валюта;
		Область.Параметры.СтруктураПараметров = Новый Структура("Заказ, Действие", 
			СтрокаДокумент.ПодчиненныйДокумент,
			"ОткрытьЗначение");
		ТаблицаОтчета.Вывести(Область);
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуНеобеспеченныеТовары(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаНеобеспеченныеТовары") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НеобеспеченныеТовары = ТаблицыРезультатов.ТаблицаНеобеспеченныеТовары.Строки.Найти(Документ, "Документ");
	Если НеобеспеченныеТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Необеспеченные товары и услуги (%1%)';
					|en = 'Not supplied goods and services (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", НеобеспеченныеТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	Если СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыНеОбеспечено";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыНеОбеспечено";
		ИмяОбластиТаблицСтрока = "ТаблицаНеОбеспеченоСтрока";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыНеОбеспеченоБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыНеОбеспеченоБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаНеОбеспеченоСтрокаБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента

	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из НеобеспеченныеТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина
	
КонецФункции

Функция ВывестиТаблицуСостояниеОбеспечения(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаСостояниеОбеспечения") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	КОбеспечениюТовары = ТаблицыРезультатов.ТаблицаСостояниеОбеспечения.Строки.Найти(Документ, "Документ");
	Если КОбеспечениюТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Обеспечение (%1%)';
					|en = 'Supply (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", КОбеспечениюТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОбеспечение";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОбеспечение";
		ИмяОбластиТаблицСтрока = "ТаблицаОбеспечениеСтрока";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОбеспечениеБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОбеспечениеБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаОбеспечениеСтрокаБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из КОбеспечениюТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика,
					,
					СтрокаТовар.Серия);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	Если ЕстьПравоДоступаКНаборуДанных("ОбеспечениеДополнительно", СтруктураПараметров) Тогда
		ИмяОбласти = "СтрокаТаблицыОбеспечениеДополнительно";
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.СтруктураПараметров = Новый Структура("Заказ, Действие", 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "Ссылка"), "ОткрытьСостояниеОбеспечения");
		ТаблицаОтчета.Вывести(Область);
	КонецЕсли;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуОтгрузка(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаОтгрузка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтгрузкаТовары = ТаблицыРезультатов.ТаблицаОтгрузка.Строки.Найти(Документ, "Документ");
	Если ОтгрузкаТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого ОтгрузкаТоварыПоСкладу Из ОтгрузкаТовары.Строки Цикл
		
		ВывестиТаблицуОтгрузкиПоСкладу(ТаблицаОтчета, Макет, ОтгрузкаТоварыПоСкладу, СтруктураПараметров);
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуОтгружено(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаОтгружено") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтгруженоТовары = ТаблицыРезультатов.ТаблицаОтгружено.Строки.Найти(Документ, "Документ");
	Если ОтгруженоТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Отгружено (%1%)';
					|en = 'Shipped (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", ОтгруженоТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОтгружено";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОтгружено";
		ИмяОбластиТаблицСтрока = "ТаблицаОтгруженоСтрока";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОтгруженоБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОтгруженоБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаОтгруженоСтрокаБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ОтгруженоТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика,
					,
					СтрокаТовар.Серия);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		Если СтруктураПараметров.ДействиеРасшифровкаКоличествоОтгружено <> Неопределено Тогда
			РасшифровкаКоличество = Новый Структура("Номенклатура,Характеристика,Серия");
			ЗаполнитьЗначенияСвойств(РасшифровкаКоличество, СтрокаТовар);
			РасшифровкаКоличество.Вставить("Документ", Документ);
			РасшифровкаКоличество.Вставить("Действие", СтруктураПараметров.ДействиеРасшифровкаКоличествоОтгружено);
			Область.Параметры.РасшифровкаКоличество = РасшифровкаКоличество;
		КонецЕсли; 
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуППС(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если Не ТаблицыРезультатов.Свойство("ТаблицаППС") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТоварыКПереходуПраваСобственности = ТаблицыРезультатов.ТаблицаППС.Строки.Найти(Документ, "Документ");
	Если ТоварыКПереходуПраваСобственности = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВывестиТаблицуПереходаПраваСобственности(ТаблицаОтчета, Макет, ТоварыКПереходуПраваСобственности, СтруктураПараметров);
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуППСОформлено(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если Не ТаблицыРезультатов.Свойство("ТаблицаППСОформлено") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ТоварыКПереходуПраваСобственности = ТаблицыРезультатов.ТаблицаППСОформлено.Строки.Найти(Документ, "Документ");
	Если ТоварыКПереходуПраваСобственности = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВывестиТаблицуПереходаПраваСобственностиОформлено(ТаблицаОтчета, Макет, ТоварыКПереходуПраваСобственности, СтруктураПараметров);
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуПоступление(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаПоступление") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоступлениеТовары = ТаблицыРезультатов.ТаблицаПоступление.Строки.Найти(Документ, "Документ");
	Если ПоступлениеТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого ПоступлениеТоварыПоСкладу Из ПоступлениеТовары.Строки Цикл
		
		ВывестиТаблицуПоступленияПоСкладу(ТаблицаОтчета, Макет, ПоступлениеТоварыПоСкладу, СтруктураПараметров);
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуПоступило(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаПоступило") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоступилоТовары = ТаблицыРезультатов.ТаблицаПоступило.Строки.Найти(Документ, "Документ");
	Если ПоступилоТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Поступило (%1%)';
					|en = 'Received (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", ПоступилоТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступило";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступило";
		ИмяОбластиТаблицСтрока = "ТаблицаПоступилоСтрока";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступилоБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступилоБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаПоступилоСтрокаБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ПоступилоТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		Если СтруктураПараметров.ДействиеРасшифровкаКоличествоПоступило <> Неопределено Тогда
			РасшифровкаКоличество = Новый Структура("Номенклатура,Характеристика,Серия");
			ЗаполнитьЗначенияСвойств(РасшифровкаКоличество, СтрокаТовар);
			РасшифровкаКоличество.Вставить("Документ", Документ);
			РасшифровкаКоличество.Вставить("Действие", СтруктураПараметров.ДействиеРасшифровкаКоличествоПоступило);
			Область.Параметры.РасшифровкаКоличество = РасшифровкаКоличество;
		КонецЕсли; 
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуЗаказано(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаЗаказано") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоступилоТовары = ТаблицыРезультатов.ТаблицаЗаказано.Строки.Найти(Документ, "Документ");
	Если ПоступилоТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Заказано (%1%)';
					|en = 'Ordered (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", ПоступилоТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступило";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступило";
		ИмяОбластиТаблицСтрока = "ТаблицаПоступилоСтрока";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступилоБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступилоБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаПоступилоСтрокаБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ПоступилоТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		Если ЗначениеЗаполнено(СтрокаТовар.Упаковка) Тогда
			Область.Параметры.Количество = СтрокаТовар.КоличествоУпаковок;
		КонецЕсли;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Процедура ВывестиТаблицуОтгрузкиПоСкладу(ТаблицаОтчета, Макет, ТоварыПоСкладу, СтруктураПараметров)
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Если ЗначениеЗаполнено(ТоварыПоСкладу.Склад) Тогда 
		ЗаголовокТаблицы = НСтр("ru = 'В процессе отгрузки на складе ""%СкладОтгрузки%"" (%1%)';
								|en = 'While shipping in warehouse ""%СкладОтгрузки%"" (%1%)'");
		ЗаголовокТаблицы = СтрЗаменить(ЗаголовокТаблицы, "%СкладОтгрузки%", ТоварыПоСкладу.Склад);
	Иначе
		ЗаголовокТаблицы = НСтр("ru = 'Услуги и работы в процессе оформления (%1%)';
								|en = 'Services and labor under registration (%1%)'");
	КонецЕсли;
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(ЗаголовокТаблицы, "%1%", ТоварыПоСкладу.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	Если СтруктураПараметров.ЭтоНакладная Тогда
		ШапкаТаблицыОтгрузка = "ШапкаТаблицыОтгрузкаНакладная";
		СтрокаТаблицыОтгрузка = "СтрокаТаблицыОтгрузкаНакладная";
		ТаблицаОтгрузкаСтрока = "ТаблицаОтгрузкаСтрокаНакладная";
	ИначеЕсли Не (СтруктураПараметров.ЭтоЗаказ И ТоварыПоСкладу.ОрдернаяСхемаПриОтгрузке) Тогда
		ШапкаТаблицыОтгрузка = "ШапкаТаблицыОтгрузкаБезОрдера";
		СтрокаТаблицыОтгрузка = "СтрокаТаблицыОтгрузкаБезОрдера";
		ТаблицаОтгрузкаСтрока = "ТаблицаОтгрузкаСтрокаБезОрдера";
	Иначе
		ШапкаТаблицыОтгрузка = "ШапкаТаблицыОтгрузка";
		СтрокаТаблицыОтгрузка = "СтрокаТаблицыОтгрузка";
		ТаблицаОтгрузкаСтрока = "ТаблицаОтгрузкаСтрока";
	КонецЕсли;
		
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = ШапкаТаблицыОтгрузка;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = СтрокаТаблицыОтгрузка;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ИмяОбластьСтроки = ТаблицаОтгрузкаСтрока;
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ТоварыПоСкладу.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика,
					,
					СтрокаТовар.Серия);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Процедура ВывестиТаблицуПереходаПраваСобственности(ТаблицаОтчета, Макет, ТоварыПоДокументу, СтруктураПараметров)
	
	Область = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	ЗаголовокТаблицы = НСтр("ru = 'Ожидает перехода права собственности ([Количество])';
							|en = 'Awaits transfer of title to goods ([Количество])'");
	
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("Количество", ТоварыПоДокументу.Строки.Количество());
	
	ЗаголовокТаблицы = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ЗаголовокТаблицы,
		ПараметрыШаблона);
	
	СтруктураЗаголовка = Новый Структура("Заголовок", ЗаголовокТаблицы);
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Область = Макет.ПолучитьОбласть("ШапкаТаблицыППС");
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("СтрокаТаблицыППС");
	ИмяОбластьСтроки = "ТаблицаППССтрока";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	
	Для Каждого СтрокаТовар Из ТоварыПоДокументу.Строки Цикл
		
		Если ЧетнаяСтрока Тогда
			ЦветФона = ЦветаСтиля.АльтернативныйЦветФонаПоля;
		Иначе
			ЦветФона = ЦветаСтиля.ЦветФонаПоля;
		КонецЕсли;
		
		Область.Параметры.Заполнить(СтрокаТовар);
		
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			СтрокаТовар.Номенклатура,
			СтрокаТовар.Характеристика,,
			СтрокаТовар.Серия);
		
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ЦветФона;
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = Не ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Процедура ВывестиТаблицуПереходаПраваСобственностиОформлено(ТаблицаОтчета, Макет, ТоварыПоДокументу, СтруктураПараметров)
	
	Область = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	
	ЗаголовокТаблицы = НСтр("ru = 'Оформлен переход права собственности ([Количество])';
							|en = 'Transfer of title to goods is registered ([Количество])'");
	
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("Количество", ТоварыПоДокументу.Строки.Количество());
	
	ЗаголовокТаблицы = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(
		ЗаголовокТаблицы,
		ПараметрыШаблона);
	
	СтруктураЗаголовка = Новый Структура("Заголовок", ЗаголовокТаблицы);
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Область = Макет.ПолучитьОбласть("ШапкаТаблицыППСОформлено");
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть("СтрокаТаблицыППСОформлено");
	ИмяОбластьСтроки = "ТаблицаППССтрокаОформлено";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	
	Для Каждого СтрокаТовар Из ТоварыПоДокументу.Строки Цикл
		
		Если ЧетнаяСтрока Тогда
			ЦветФона = ЦветаСтиля.АльтернативныйЦветФонаПоля;
		Иначе
			ЦветФона = ЦветаСтиля.ЦветФонаПоля;
		КонецЕсли;
		
		Область.Параметры.Заполнить(СтрокаТовар);
		
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			СтрокаТовар.Номенклатура,
			СтрокаТовар.Характеристика,,
			СтрокаТовар.Серия);
		
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ЦветФона;
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = Не ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Процедура ВывестиТаблицуПоступленияПоСкладу(ТаблицаОтчета, Макет, ТоварыПоСкладу, СтруктураПараметров)
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Если ЗначениеЗаполнено(ТоварыПоСкладу.Склад) Тогда 
		ЗаголовокТаблицы = НСтр("ru = 'В процессе поступления на склад ""%СкладОтгрузки%"" (%1%)';
								|en = 'Goods received / Invoicing reconciliation ""%СкладОтгрузки%"" (%1%)'");
		ЗаголовокТаблицы = СтрЗаменить(ЗаголовокТаблицы, "%СкладОтгрузки%", ТоварыПоСкладу.Склад);
	Иначе
		ЗаголовокТаблицы = НСтр("ru = 'Услуги и работы в процессе оформления (%1%)';
								|en = 'Services and labor under registration (%1%)'");
	КонецЕсли;
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(ЗаголовокТаблицы, "%1%", ТоварыПоСкладу.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступление";
	ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступление";
	ИмяОбластиТаблицСтрока = "ТаблицаПоступлениеСтрока";
	
	Если СтруктураПараметров.ЭтоЗаказ Или СтруктураПараметров.ЭтоПоступлениеТоваров Тогда
		Если ТоварыПоСкладу.ОрдернаяСхемаПриПоступлении Или ТоварыПоСкладу.РаздельнаяЗакупка Тогда
			ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступление";
			ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступление";
			ИмяОбластиТаблицСтрока = "ТаблицаПоступлениеСтрока";
		Иначе
			ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступлениеБезОрдера";
			ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступлениеБезОрдера";
			ИмяОбластиТаблицСтрока = "ТаблицаПоступлениеСтрокаБезОрдера";
		КонецЕсли;
	ИначеЕсли СтруктураПараметров.ЭтоНакладная Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыПоступлениеНакладная";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыПоступлениеНакладная";
		ИмяОбластиТаблицСтрока = "ТаблицаПоступлениеСтрокаНакладная";
	КонецЕсли;
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ТоварыПоСкладу.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Функция ВывестиТаблицуОтмененоОтгрузка(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаОтмененоОтгрузка") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтмененоТовары = ТаблицыРезультатов.ТаблицаОтмененоОтгрузка.Строки.Найти(Документ, "Документ");
	Если ОтмененоТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ВывестиТаблицуОтменено(
				ТаблицаОтчета, 
				Макет, 
				ОтмененоТовары, 
				СтруктураПараметров, 
				СтруктураПараметров.ЕстьПричиныОтменыОтгрузки,
				СтруктураПараметров.ТекстОтмененоОтгрузка);
	
КонецФункции

Функция ВывестиТаблицуОтмененоПоступление(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаОтмененоПоступление") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОтмененоТовары = ТаблицыРезультатов.ТаблицаОтмененоПоступление.Строки.Найти(Документ, "Документ");
	Если ОтмененоТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ВывестиТаблицуОтменено(
				ТаблицаОтчета, 
				Макет, 
				ОтмененоТовары, 
				СтруктураПараметров, 
				СтруктураПараметров.ЕстьПричиныОтменыПоступления,
				СтруктураПараметров.ТекстОтмененоПоступление,
				Ложь);
	
КонецФункции

Функция ВывестиТаблицуОтменено(ТаблицаОтчета, Макет, ОтмененоТовары, СтруктураПараметров, ИспользоватьПричиныОтмены, ЗаголовокТаблицы, Отгрузка = Истина)
	
	ЕстьСуммовыеПоказатели = Отгрузка И СтруктураПараметров.ЕстьСуммовыеПоказателиОтгрузки
	                             Или Не Отгрузка И СтруктураПараметров.ЕстьСуммовыеПоказателиПоступления;

	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(ЗаголовокТаблицы, "%1%", ОтмененоТовары.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если ИспользоватьПричиныОтмены И ЕстьСуммовыеПоказатели Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОтменено";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОтменено";
		ИмяОбластиТаблицСтрока = "ТаблицаОтмененоСтрока";
	ИначеЕсли Не ИспользоватьПричиныОтмены И ЕстьСуммовыеПоказатели Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОтмененоБезПричины";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОтмененоБезПричины";
		ИмяОбластиТаблицСтрока = "ТаблицаОтмененоСтрокаБезПричины";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыОтмененоБезПричиныБезСумм";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыОтмененоБезПричиныБезСумм";
		ИмяОбластиТаблицСтрока = "ТаблицаОтмененоСтрокаБезПричиныБезСумм";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = ?(Отгрузка, СтруктураПараметров.ТекстТоварУслугаОтгрузка, СтруктураПараметров.ТекстТоварУслугаПоступление);
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ОтмененоТовары.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Функция ВывестиТаблицуВозвратТоваров(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаВозвратТоваров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПоступлениеТовары = ТаблицыРезультатов.ТаблицаВозвратТоваров.Строки.Найти(Документ, "Документ");
	Если ПоступлениеТовары = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Для каждого ВозвратТоваровПоСкладу Из ПоступлениеТовары.Строки Цикл
		
		ВывестиТаблицуВозвратТоваровПоСкладу(ТаблицаОтчета, Макет, ВозвратТоваровПоСкладу, СтруктураПараметров);
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ВывестиТаблицуВозвратТоваровПоСкладу(ТаблицаОтчета, Макет, ВозвратТоваровПоСкладу, СтруктураПараметров)
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Если ЗначениеЗаполнено(ВозвратТоваровПоСкладу.Склад) Тогда 
		ЗаголовокТаблицы = НСтр("ru = 'В процессе возврата на склад ""%СкладОтгрузки%"" (%1%)';
								|en = 'While returning to warehouse ""%СкладОтгрузки%"" (%1%)'");
		ЗаголовокТаблицы = СтрЗаменить(ЗаголовокТаблицы, "%СкладОтгрузки%", ВозвратТоваровПоСкладу.Склад);
	КонецЕсли;
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(ЗаголовокТаблицы, "%1%", ВозвратТоваровПоСкладу.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	Если СтруктураПараметров.ЭтоНакладная Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыВозврат";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыВозврат";
		ИмяОбластиТаблицСтрока = "ТаблицаВозвратСтрока";
	ИначеЕсли Не (СтруктураПараметров.ЭтоЗаказ И ВозвратТоваровПоСкладу.ОрдернаяСхемаПриПоступлении) Тогда
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыВозвратБезОрдера";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыВозвратБезОрдера";
		ИмяОбластиТаблицСтрока = "ТаблицаВозвратСтрокаБезОрдера";
	Иначе
		ИмяОбластиШапкаТаблицы = "ШапкаТаблицыВозврат";
		ИмяОбластиСтрокаТаблицы = "СтрокаТаблицыВозврат";
		ИмяОбластиТаблицСтрока = "ТаблицаВозвратСтрока";
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиШапкаТаблицы);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	Область = Макет.ПолучитьОбласть(ИмяОбластиСтрокаТаблицы);
	ОбластьСтроки = Область.Области[ИмяОбластиТаблицСтрока]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ВозвратТоваровПоСкладу.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Функция ВывестиТаблицуВозвращеноТоваров(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаВозвращеноТоваров") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВозвращеноТоваров = ТаблицыРезультатов.ТаблицаВозвращеноТоваров.Строки.Найти(Документ, "Документ");
	Если ВозвращеноТоваров = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗаголовокТаблицы = НСтр("ru = 'Возвращено товаров клиентом (%1%)';
							|en = 'Goods returned by a customer (%1%)'");
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(ЗаголовокТаблицы, "%1%", ВозвращеноТоваров.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыВозвращено";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыВозвращено";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ИмяОбластьСтроки = "ТаблицаВозвращеноСтрока";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ВозвращеноТоваров.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.Номенклатура,
					СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;
	
КонецФункции

Процедура ВывестиПоследнююСтроку(ТаблицаОтчета, Макет)
	
	Область = Макет.ПолучитьОбласть("СтрокаТаблицыПоследняя");
	ТаблицаОтчета.Вывести(Область);
	
КонецПроцедуры

Процедура ВывестиТаблицуНетДанных(ТаблицаОтчета, Макет, Выборка, ЕстьДанные)
	
	Если ЕстьДанные Тогда
		Возврат
	КонецЕсли;
	
	Если НЕ Выборка.ДокументПроведен Тогда
		ТекстСообщения = НСтр("ru = 'Документ не проведен. Нет данных для заполнения.';
								|en = 'Document is not posted. No data for population.'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Нет данных для заполнения.';
								|en = 'No data for population.'");
	КонецЕсли;
	
	Область = Макет.ПолучитьОбласть("СтрокаТаблицыНетДанных");
	Область.Параметры.ТекстСообщения = ТекстСообщения;
	ТаблицаОтчета.Вывести(Область);
	
	ВывестиПоследнююСтроку(ТаблицаОтчета, Макет);
	
КонецПроцедуры

//++ НЕ УТ

//++ НЕ УТКА

Функция ВывестиТаблицуУслугиДавальцуКОформлению(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)
	
	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаУслугиДавальцуКОформлению") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	УслугиДавальцуКОформлению = ТаблицыРезультатов.ТаблицаУслугиДавальцуКОформлению.Строки.Найти(Документ, "Документ");
	Если УслугиДавальцуКОформлению = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Услуги давальцу к оформлению (%1%)';
					|en = 'Services to material provider for registration (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", УслугиДавальцуКОформлению.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыУслугиДавальцуКОформлению";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыУслугиДавальцуКОформлению";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ИмяОбластьСтроки = "ТаблицаУслугиДавальцуКОформлениюСтрока";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из УслугиДавальцуКОформлению.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		ПредставлениеНоменклатурыДляПечати = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
		СтрокаТовар.Номенклатура,
		СтрокаТовар.Характеристика);
		Область.Параметры.Товар = ПредставлениеНоменклатурыДляПечати;
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина
	
КонецФункции

//-- НЕ УТКА

Функция ВывестиТаблицуПродукцияКОформлениюВОтчетеПереработчику(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)

	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаПродукцияКОформлениюВОтчетеПереработчику") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПродукцияКОформлению = ТаблицыРезультатов.ТаблицаПродукцияКОформлениюВОтчетеПереработчику.Строки.Найти(Документ, "Документ");
	Если ПродукцияКОформлению = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Заголовок = НСтр("ru = 'Продукция и услуги к оформлению в отчете переработчику (%1%)';
					|en = 'Manufactured products and services for registration in subcontractor report (%1%)'");
	СтруктураЗаголовка = Новый Структура("Заголовок", 
							СтрЗаменить(Заголовок, "%1%", ПродукцияКОформлению.Строки.Количество()));
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	СуффиксМакета = СтруктураПараметров.СуффиксМакетаТаблицыПродукцияКОформлениюВОтчетеПереработчику;
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыКОформлениюВОтчетеПереработчику" + СуффиксМакета;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаПоступление;
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыКОформлениюВОтчетеПереработчику" + СуффиксМакета;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	ИмяОбластьСтроки = "ТаблицаКОформлениюВОтчетеПереработчикуСтрока" + СуффиксМакета;
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из ПродукцияКОформлению.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.НоменклатураПредставление,
					СтрокаТовар.ХарактеристикаПредставление,
					,
					СтрокаТовар.СерияПредставление);
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;

КонецФункции

Функция ВывестиТаблицуСырьеУПереработчика(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, СтруктураПараметров)

	Если НЕ ТаблицыРезультатов.Свойство("ТаблицаСырьеУПереработчика") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СырьеУПереработчика = ТаблицыРезультатов.ТаблицаСырьеУПереработчика.Строки.Найти(Документ, "Документ");
	Если СырьеУПереработчика = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	Заголовок = НСтр("ru = 'Сырье и материалы у переработчика (%1%)';
					|en = 'Raw and consumable materials held by subcontractor (%1%)'");
	СтруктураЗаголовка = Новый Структура;
	СтруктураЗаголовка.Вставить(
		"Заголовок",
		СтрЗаменить(Заголовок, "%1%", СырьеУПереработчика.Строки.Количество()));
	
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	СуффиксМакета = СтруктураПараметров.СуффиксМакетаТаблицыСырьеУПереработчика;
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыСырьеУПереработчика" + СуффиксМакета;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ТекстТоварУслуга = СтруктураПараметров.ТекстТоварУслугаОтгрузка;
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыСырьеУПереработчика" + СуффиксМакета;
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	ИмяОбластьСтроки = "ТаблицаСырьеУПереработчикаСтрока" + СуффиксМакета;
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаТовар Из СырьеУПереработчика.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаТовар);
		Область.Параметры.Товар = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаТовар.НоменклатураПредставление,
					СтрокаТовар.ХарактеристикаПредставление);
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла;
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
	Возврат Истина;

КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область СостояниеВзаиморасчетов

Функция ТекстЗапросаВтОбъектыРасчетов(ИмяТаблицы, СтруктураПараметров)
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаВтОбъектыРасчетов") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаВтОбъектыРасчетов;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Документы.ДокументСсылка КАК Документ,
	|	
	|	ВЫБОР
	|		КОГДА Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|			ТОГДА Документы.ДокументСсылка.Договор
	|		КОГДА (Документы.ТипДокумента = ТИП(Документ.РеализацияТоваровУслуг)
	|			ИЛИ Документы.ТипДокумента = ТИП(Документ.АктВыполненныхРабот))
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	|		КОГДА (Документы.ТипДокумента = ТИП(Документ.ЗаказКлиента) 
	|			ИЛИ Документы.ТипДокумента = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	|		КОГДА (Документы.ТипДокумента = ТИП(Документ.ЗаказКлиента) 
	|			ИЛИ Документы.ТипДокумента = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным)
	|			ТОГДА Документы.ДокументСсылка.Договор
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказПоставщику) 
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|			ТОГДА Документы.ДокументСсылка
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказПоставщику)
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным)
	|			ТОГДА Документы.ДокументСсылка.Договор
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ПриобретениеТоваровУслуг)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|			ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетДавальцу)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказДавальца)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	//-- Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетДавальцу2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов В (
	|											ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным),
	|											ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказДавальца2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|			ТОГДА Документы.ДокументСсылка
	//-- НЕ УТКА

	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетПереработчика)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	//-- Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетПереработчика2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|			ТОГДА Документы.ДокументСсылка
	//++ Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказПереработчику)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|			ТОГДА Документы.ДокументСсылка
	//-- Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказПереработчику2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|			ТОГДА Документы.ДокументСсылка
	//-- НЕ УТ
	|	КОНЕЦ КАК ОбъектРасчетов,
	|	ВЫБОР
	|		КОГДА Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов)
	|			И Документы.ТипДокумента <> ТИП(Документ.ГрафикИсполненияДоговора)
	|			ТОГДА ЛОЖЬ
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ГрафикИсполненияДоговора)
	|		ТОГДА ИСТИНА
	|		КОГДА (Документы.ТипДокумента = ТИП(Документ.РеализацияТоваровУслуг)
	|			ИЛИ Документы.ТипДокумента = ТИП(Документ.АктВыполненныхРабот))
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|		ТОГДА ИСТИНА
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ПриобретениеТоваровУслуг)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|		ТОГДА ИСТИНА
	|		КОГДА (Документы.ТипДокумента = ТИП(Документ.ЗаказКлиента)
	|			ИЛИ Документы.ТипДокумента = ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|		ТОГДА ИСТИНА
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказПоставщику) 
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|		ТОГДА ИСТИНА
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетДавальцу)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|		ТОГДА ИСТИНА
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказДавальца)
	|			И (Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
	|				ИЛИ Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
	|		ТОГДА ИСТИНА
	//-- Устарело_Переработка24
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ОтчетДавальцу2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов В (
	|											ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоНакладным),
	|											ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным))
	|				ТОГДА ИСТИНА
	|		КОГДА Документы.ТипДокумента = ТИП(Документ.ЗаказДавальца2_5)
	|			И Документы.ДокументСсылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
	|				ТОГДА ИСТИНА
	//-- НЕ УТКА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ТребуетсяГрафик, 
	|	Документы.ДокументСсылка.ПорядокРасчетов КАК ПорядокРасчетов
	|ИЗ
	|	ВтДокументы КАК Документы
	|";
	
	Если ИмяТаблицы = "Документ.РеализацияТоваровУслуг" Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка,
			|	Истина,
			|	Документ.ПорядокРасчетов
			|ИЗ
			|	Документ.РеализацияТоваровУслуг КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И НЕ Документ.РеализацияПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.ЗаказКлиента,
			|	Истина,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И Документ.Ссылка.РеализацияПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка.Договор,
			|	Ложь,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.РеализацияТоваровУслуг.Товары КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным)
			|	И НЕ Документ.Ссылка.РеализацияПоЗаказам
			|";
	КонецЕсли;
	
	Если ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг" Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка,
			|	Истина,
			|	Документ.ПорядокРасчетов
			|ИЗ
			|	Документ.ПриобретениеТоваровУслуг КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И НЕ Документ.ПоступлениеПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.ЗаказПоставщику,
			|	Истина,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.ПриобретениеТоваровУслуг.Товары КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И Документ.Ссылка.ПоступлениеПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка.Договор,
			|	Ложь,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.ПриобретениеТоваровУслуг.Товары КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным)
			|	И НЕ Документ.Ссылка.ПоступлениеПоЗаказам
			|";
	КонецЕсли;
	
	Если ИмяТаблицы = "Документ.АктВыполненныхРабот" Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка,
			|	Истина,
			|	Документ.ПорядокРасчетов
			|ИЗ
			|	Документ.АктВыполненныхРабот КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И НЕ Документ.АктПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.ЗаказКлиента,
			|	Истина,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.АктВыполненныхРабот.Услуги КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	(Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным))
			|	И Документ.Ссылка.АктПоЗаказам
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	Документ.Ссылка.Договор,
			|	Ложь,
			|	Документ.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.АктВыполненныхРабот.Услуги КАК Документ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО Документ.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	Документ.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоДоговорамНакладным)
			|		И НЕ Документ.Ссылка.АктПоЗаказам 
			|";
	КонецЕсли;
	
	//++ НЕ УТКА

	//++ Устарело_Переработка24
	Если ИмяТаблицы = "Документ.ОтчетДавальцу" Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	ОтчетыДавальцу.ЗаказДавальца,
			|	Истина,
			|	ОтчетыДавальцу.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.ОтчетДавальцу.Продукция КАК ОтчетыДавальцу
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО ОтчетыДавальцу.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	ОтчетыДавальцу.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказам)
			|		ИЛИ ОтчетыДавальцу.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
			|";
	КонецЕсли;
	//-- Устарело_Переработка24
	
	Если ИмяТаблицы = "Документ.ОтчетДавальцу2_5" Тогда
		ТекстЗапроса = ТекстЗапроса + "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВтДокументы.ДокументСсылка,
			|	ОтчетыДавальцу.ЗаказДавальца,
			|	Истина,
			|	ОтчетыДавальцу.Ссылка.ПорядокРасчетов
			|ИЗ
			|	Документ.ОтчетДавальцу2_5.Продукция КАК ОтчетыДавальцу
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументы КАК ВтДокументы
			|		ПО ОтчетыДавальцу.Ссылка = ВтДокументы.ДокументСсылка
			|ГДЕ
			|	ОтчетыДавальцу.Ссылка.ПорядокРасчетов = ЗНАЧЕНИЕ(Перечисление.ПорядокРасчетов.ПоЗаказамНакладным)
			|";
	КонецЕсли;

	//-- НЕ УТКА
	
	ТекстЗапросаОбъектовРасчета = "
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Документ КАК Документ,
	|	ОбъектРасчетов КАК ОбъектРасчетовОбъект,
	|	ДанныеСправочника.Ссылка КАК ОбъектРасчетов,
	|	ДанныеСправочника.ВалютаВзаиморасчетов КАК Валюта,
	|	ТребуетсяГрафик КАК ТребуетсяГрафик,
	|	ПорядокРасчетов КАК ПорядокРасчетов
	|ПОМЕСТИТЬ ОбъектыРасчетов
	|	ИЗ
	|	&ТаблицаЗапроса КАК ТаблицаОбъектовРасчета
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ДанныеСправочника
	|		ПО ТаблицаОбъектовРасчета.ОбъектРасчетов = ДанныеСправочника.Объект
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов
	|;";
	
	ТекстЗапросаОбъектовРасчета = СтрЗаменить(ТекстЗапросаОбъектовРасчета, "&ТаблицаЗапроса", "("+ ТекстЗапроса +")");
	
	Возврат ТекстЗапросаОбъектовРасчета;
	
КонецФункции

#Область ТипыЗаписейВзаиморасчетовЗаполнены

Функция ТекстЗапросаВтСвязьЗаказовНакладных(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ОбъектыРасчетов.Документ КАК Документ,
		|	ОбъектыРасчетов.ОбъектРасчетов КАК ИсходныйОбъектРасчетов,
		|	ОбъектыРасчетов.ОбъектРасчетовОбъект КАК ИсходныйОбъектРасчетовОбъект,
		|	ОбъектыРасчетовНакладные.Ссылка КАК ОбъектРасчетов,
		|	ОбъектыРасчетов.Валюта КАК Валюта,
		|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
		|	ОбъектыРасчетов.ОбъектРасчетов КАК ОбъектРасчетовЗаказ,
		|	СУММА(ВЫРАЗИТЬ(ВЫБОР
		|				КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА ВЫБОР
		|							КОГДА ОбъектыРасчетовНакладные.СуммаВзаиморасчетов = 0
		|								ТОГДА ВЫБОР
		|										КОГДА ОбъектыРасчетовНакладные.Сумма = 0
		|											ТОГДА 0
		|										ИНАЧЕ РасчетыСКлиентами.КОтгрузке / ОбъектыРасчетовНакладные.Сумма
		|									КОНЕЦ
		|							ИНАЧЕ РасчетыСКлиентами.КОтгрузке / ОбъектыРасчетовНакладные.СуммаВзаиморасчетов
		|						КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК ЧИСЛО(20, 10))) КАК ДоляЗаказа
		|ПОМЕСТИТЬ ВтСвязьЗаказовНакладныхРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО РасчетыСКлиентами.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
		|			И (РасчетыСКлиентами.Активность)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетовНакладные
		|		ПО РасчетыСКлиентами.Регистратор = ОбъектыРасчетовНакладные.Объект
		|			И (НЕ ОбъектыРасчетовНакладные.ПометкаУдаления)
		|			И ОбъектыРасчетовНакладные.Ссылка <> РасчетыСКлиентами.ОбъектРасчетов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетовЗаказы
		|		ПО ОбъектыРасчетов.ОбъектРасчетов = ОбъектыРасчетовЗаказы.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбъектыРасчетов.Документ,
		|	ОбъектыРасчетов.ОбъектРасчетов,
		|	ОбъектыРасчетов.ОбъектРасчетовОбъект,
		|	ОбъектыРасчетовНакладные.Ссылка,
		|	ОбъектыРасчетов.Валюта,
		|	ОбъектыРасчетов.ПорядокРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫРАЗИТЬ(ВЫБОР
		|				КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА ВЫБОР
		|							КОГДА ОбъектыРасчетовНакладные.СуммаВзаиморасчетов = 0
		|								ТОГДА ВЫБОР
		|										КОГДА ОбъектыРасчетовНакладные.Сумма = 0
		|											ТОГДА 0
		|										ИНАЧЕ РасчетыСКлиентами.КОтгрузке / ОбъектыРасчетовНакладные.Сумма
		|									КОНЕЦ
		|							ИНАЧЕ РасчетыСКлиентами.КОтгрузке / ОбъектыРасчетовНакладные.СуммаВзаиморасчетов
		|						КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ КАК ЧИСЛО(20, 10))) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ОбъектыРасчетов.Документ КАК Документ,
		|	ОбъектыРасчетов.ОбъектРасчетов КАК ИсходныйОбъектРасчетов,
		|	ОбъектыРасчетов.ОбъектРасчетовОбъект КАК ИсходныйОбъектРасчетовОбъект,
		|	ОбъектыРасчетов.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ОбъектыРасчетов.Валюта КАК Валюта,
		|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
		|	ОбъектыРасчетов.ОбъектРасчетов КАК ОбъектРасчетовЗаказ,
		|	1 КАК ДоляЗаказа
		|ИЗ
		|	ОбъектыРасчетов КАК ОбъектыРасчетов
		|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КОтгрузке", "КПоступлению");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Приход", "Расход");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОплатамПоСрокам(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Документ                     КАК Документ,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетов       КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект КАК ОбъектРасчетовОбъект,
	|	РасчетыПоСрокам.ДокументРегистратор          КАК ДокументРегистратор,
	|	ОбъектыРасчетов.ПорядокРасчетов              КАК ПорядокРасчетов,
	|	РасчетыПоСрокам.Валюта                       КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты)
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты)
	|				ТОГДА - Предоплата
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа)     КАК СуммаФакт,
	|	НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ) КАК ДатаФакт,
	|	СУММА(ВЫБОР
	|		КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|			ТОГДА РасчетыПоСрокам.Долг
	|		ИНАЧЕ 0
	|	КОНЕЦ)                                      КАК ЗачетАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|	КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа) КАК КорректировкаГрафика
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.ОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
	|ГДЕ
	|	РасчетыПоСрокам.ОбъектРасчетов В
	|			(ВЫБРАТЬ
	|				Т.ОбъектРасчетов
	|			ИЗ
	|				ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыПоСрокам.ДокументРегистратор,
	|	РасчетыПоСрокам.Валюта,
	|	ОбъектыРасчетов.Документ,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетов,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект,
	|	ОбъектыРасчетов.ПорядокРасчетов,
	|	НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты)
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеПредоплаты)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты)
	|				ТОГДА - Предоплата
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа) <> 0
	|	ИЛИ СУММА(ВЫБОР
	|		КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|			И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|			ТОГДА РасчетыПоСрокам.Долг
	|		ИНАЧЕ 0
	|	КОНЕЦ) <> 0
	|	ИЛИ СУММА(ВЫБОР
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|	КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа) <> 0
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОплатамПоПлану(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Документ КАК Документ,
	|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
	|	ВЫБОР 
	|		КОГДА ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|			ТОГДА ПланОплат.ДокументРегистратор
	|		ИНАЧЕ ПланОплат.ДокументОплаты
	|	КОНЕЦ КАК ДокументРегистратор,
	|	ПланОплат.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОплат.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОплат.КОплате > 0
	|				ТОГДА ПланОплат.КОплате
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОплат.КОплате < 0
	|				ТОГДА ПланОплат.КОплате
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФакт,
	|	СУММА(ВЫБОР
	|			КОГДА ПланОплат.НераспределенныйАванс
	|				И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|				И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|				И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И ПланОплат.КОплате < 0
	|			ТОГДА -ПланОплат.КОплате
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК ЗачетАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ПланОплат.КОплате < 0
	|				ТОГДА -ПланОплат.КОплате
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК КорректировкаГрафика
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОплат КАК ПланОплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.ОбъектРасчетов = ПланОплат.ОбъектРасчетов
	|ГДЕ
	|	ПланОплат.ОбъектРасчетов В
	|			(ВЫБРАТЬ
	|				Т.ОбъектРасчетов
	|			ИЗ
	|				ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыРасчетов.Документ,
	|	ОбъектыРасчетов.ПорядокРасчетов,
	|	ПланОплат.ОбъектРасчетов,
	|	ПланОплат.Валюта,
	|	ВЫБОР 
	|		КОГДА ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|			ТОГДА ПланОплат.ДокументРегистратор
	|		ИНАЧЕ ПланОплат.ДокументОплаты
	|	КОНЕЦ
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОплатам(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ДанныеРасчетов.Документ                    КАК Документ,
	|	МАКСИМУМ(ДанныеРасчетов.ОбъектРасчетов)    КАК ОбъектРасчетов,
	|	ДанныеРасчетов.ОбъектРасчетовОбъект        КАК ОбъектРасчетовОбъект,
	|	ДанныеРасчетов.ДокументРегистратор         КАК ДокументРегистратор,
	|	ДанныеРасчетов.ПорядокРасчетов             КАК ПорядокРасчетов,
	|	ДанныеРасчетов.Валюта                      КАК Валюта,
	|	ДанныеРасчетов.ВариантОплатыОтгрузки       КАК ВариантОплатыОтгрузки,
	|	ДанныеРасчетов.ДатаПлан                    КАК ДатаПлан,
	|	
	|	СУММА(ДанныеРасчетов.СуммаПлан)            КАК СуммаПлан,
	|	СУММА(ДанныеРасчетов.КорректировкаГрафика) КАК КорректировкаГрафика,
	|	
	|	ДанныеРасчетов.ДатаФакт                    КАК ДатаФакт,
	|	МАКСИМУМ(ДанныеРасчетов.Период)            КАК Период,
	|	СУММА(ДанныеРасчетов.СуммаФакт)            КАК СуммаФакт,
	|	
	|	СУММА(0)                                   КАК Остаток,
	|	СУММА(0)                                   КАК ОстатокПросрочено,
	|	СУММА(0)                                   КАК ДнейПросрочено,
	|	
	|	ЛОЖЬ                                       КАК ТребуетсяГрафик
	|ИЗ (
	|	// План оплат
	|	ВЫБРАТЬ
	|		ОбъектыРасчетов.Документ         КАК Документ,
	|		ОбъектыРасчетов.ОбъектРасчетов   КАК ОбъектРасчетов,
	|		ОбъектыРасчетов.ОбъектРасчетовОбъект КАК ОбъектРасчетовОбъект,
	|		ПланОплат.ДокументРегистратор    КАК ДокументРегистратор,
	|		ОбъектыРасчетов.ПорядокРасчетов  КАК ПорядокРасчетов,
	|		ОбъектыРасчетов.Валюта           КАК Валюта,
	|		ПланОплат.ВариантОплаты          КАК ВариантОплатыОтгрузки,
	|		ПланОплат.ДатаПлановогоПогашения КАК ДатаПлан,
	|		ВЫБОР
	|			КОГДА ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ПланОплат.КОплате > 0
	|				ТОГДА ПланОплат.КОплате
	|			// Списание отгруженных товаров
	|			КОГДА ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ПланОплат.КОплате < 0
	|				ТОГДА ПланОплат.КОплате
	|			ИНАЧЕ 0
	|		КОНЕЦ                            КАК СуммаПлан,
	|		ВЫБОР
	|			// Отгрузка
	|			КОГДА НЕ ПланОплат.НераспределенныйАванс
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				ТОГДА -ПланОплат.КОплате
	|			// Зачет аванса
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					И ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) <> ТИП(Документ.Сторно)
	|					И ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) <> ТИП(Документ.КорректировкаЗадолженности)
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ПланОплат.КОплате < 0
	|				ТОГДА -ПланОплат.КОплате
	|			ИНАЧЕ 0
	|		КОНЕЦ                            КАК КорректировкаГрафика,
	|		ВЫБОР
	|			// Аванс
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОплат.КОплате > 0
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланОплат.Период, ДЕНЬ)
	|			// Возврат или сторно аванса
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.КорректировкаЗадолженности))
	|					И ПланОплат.КОплате < 0
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланОплат.Период, ДЕНЬ)
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ                            КАК ДатаФакт,
	|		НАЧАЛОПЕРИОДА(ПланОплат.Период, ДЕНЬ) КАК Период,
	|		ВЫБОР
	|			// Аванс
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОплат.КОплате > 0
	|				ТОГДА ПланОплат.КОплате
	|			// Возврат или сторно аванса
	|			КОГДА ПланОплат.НераспределенныйАванс
	|					И ПланОплат.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И (ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.Сторно)
	|						ИЛИ ТИПЗНАЧЕНИЯ(ПланОплат.ДокументРегистратор) = ТИП(Документ.КорректировкаЗадолженности))
	|					И ПланОплат.КОплате < 0
	|				ТОГДА ПланОплат.КОплате
	|			ИНАЧЕ 0
	|		КОНЕЦ                            КАК СуммаФакт
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПланОплат КАК ПланОплат
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ПланОплат.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	// Фактические отгрузки и постоплаты
	|	ВЫБРАТЬ
	|		ОбъектыРасчетов.Документ                                           КАК Документ,
	|		ОбъектыРасчетов.ИсходныйОбъектРасчетов                             КАК ОбъектРасчетов,
	|		ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект                       КАК ОбъектРасчетовОбъект,
	|		РасчетыПоСрокам.ДокументРегистратор                                КАК ДокументРегистратор,
	|		ОбъектыРасчетов.ПорядокРасчетов                                    КАК ПорядокРасчетов,
	|		ОбъектыРасчетов.Валюта                                             КАК Валюта,
	|		ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыКлиентом.ПустаяСсылка) КАК ВариантОплатыОтгрузки,
	|		РасчетыПоСрокам.ДатаПлановогоПогашения                             КАК ДатаПлан,
	|		0                                                                  КАК СуммаПлан,
	|		// Учитываем новую задолженность по отгрузке
	|		ВЫБОР
	|			// Восстановление долга при сторно платежки корректировкой не считаем
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга)
	|				И РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерезачетДолгаПриСторноВзаиморасчетов),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерезачетАвансаПриСторноВзаиморасчетов))
	|				ТОГДА 0
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеДолга), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУвеличениеДолга))
	|				ТОГДА РасчетыПоСрокам.Долг
	|			// Зачтенные авансы уменьшают корректировку, так как учтены уже в плановых оплатах
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов <> ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеДолга)
	|				И ТИПЗНАЧЕНИЯ(РасчетыПоСрокам.ДокументРегистратор) = ТИП(Документ.КорректировкаГрафикаВзаиморасчетов)
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа                                 КАК КорректировкаГрафика,
	|		// Оплаты берем только по части долга, так как аванс учтен в плане оплат
	|		ВЫБОР 
	|			КОГДА ВЫБОР
	|					КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга)
	|							ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеДолга)
	|							ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеДолга)
	|							ИЛИ ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|								И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга)
	|						ТОГДА РасчетыПоСрокам.Долг
	|					КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУвеличениеДолга)
	|							ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеДолга)
	|						ТОГДА -РасчетыПоСрокам.Долг
	|					КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга)
	|						И РасчетыПоСрокам.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерезачетДолгаПриСторноВзаиморасчетов),
	|															ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПерезачетАвансаПриСторноВзаиморасчетов))
	|						ТОГДА -РасчетыПоСрокам.Долг
	|					ИНАЧЕ 0
	|				КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа <> 0
	|				ТОГДА НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ)
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ                                                              КАК ДатаФакт,
	|		НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ)                        КАК Период,
	|		ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеДолга)
	|					ИЛИ ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|						И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга)
	|				ТОГДА РасчетыПоСрокам.Долг
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУвеличениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеДолга)
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			// Сторно зачтеннной платежки
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеДолга)
	|				И РасчетыПоСрокам.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВосстановлениеДолгаАвансаПриСторноВзаиморасчетов)
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа                                 КАК СуммаФакт
	|	ИЗ
	|		ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|			ПО ОбъектыРасчетов.ОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
	|	) КАК ДанныеРасчетов
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасчетов.Документ,
	|	ДанныеРасчетов.ОбъектРасчетовОбъект,
	|	ДанныеРасчетов.ДокументРегистратор,
	|	ДанныеРасчетов.ПорядокРасчетов,
	|	ДанныеРасчетов.Валюта,
	|	ДанныеРасчетов.ВариантОплатыОтгрузки,
	|	ДанныеРасчетов.ДатаПлан,
	|	ДанныеРасчетов.ДатаФакт
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДанныеРасчетов.СуммаПлан) <> 0
	|		ИЛИ СУММА(ДанныеРасчетов.КорректировкаГрафика) <> 0
	|		ИЛИ СУММА(ДанныеРасчетов.СуммаФакт) <> 0)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПлан,
	|	ВариантОплатыОтгрузки,
	|	ДатаФакт
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОтгрузкамПоСрокам(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Документ                     КАК Документ,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетов       КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект КАК ОбъектРасчетовОбъект,
	|	РасчетыПоСрокам.ДокументРегистратор          КАК ДокументРегистратор,
	|	ОбъектыРасчетов.ПорядокРасчетов              КАК ПорядокРасчетов,
	|	РасчетыПоСрокам.Валюта                       КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга), ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеДолга))
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа)               КАК СуммаФакт,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК ЗачетАванса,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты))
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК КорректировкаГрафика,
	|	НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ) КАК ДатаФакт,
	|	4                                                     КАК ВариантОплатыОтгрузки
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.ОбъектРасчетов = РасчетыПоСрокам.ОбъектРасчетов
	|ГДЕ
	|	РасчетыПоСрокам.ОбъектРасчетов В
	|			(ВЫБРАТЬ
	|				Т.ОбъектРасчетов
	|			ИЗ
	|				ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыРасчетов.Документ,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетов,
	|	ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект ,
	|	ОбъектыРасчетов.ПорядокРасчетов,
	|	РасчетыПоСрокам.ДокументРегистратор,
	|	РасчетыПоСрокам.Валюта,
	|	НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ)
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга), ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеДолга))
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа) <> 0
	|	ИЛИ СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|	КОНЕЦ) <> 0
	|	ИЛИ СУММА(ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты))
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			ИНАЧЕ 0
	|	КОНЕЦ) <> 0
	|
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОтгрузкамПоПлану(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ОбъектыРасчетов.Документ КАК Документ,
	|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
	|	ПланОтгрузок.ДокументРегистратор КАК ДокументРегистратор,
	|	ПланОтгрузок.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ПланОтгрузок.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма > 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			КОГДА НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма < 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаФакт, 
	|	СУММА(ВЫБОР
	|			КОГДА ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма < 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК ЗачетАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ПланОтгрузок.НераспределенныйАванс
	|				И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|				И ПланОтгрузок.Сумма > 0 
	|				ТОГДА ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|	КОНЕЦ) КАК КорректировкаГрафика,
	|	НАЧАЛОПЕРИОДА(ПланОтгрузок.Период, ДЕНЬ) КАК ДатаФакт
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК ПланОтгрузок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|		ПО ОбъектыРасчетов.ОбъектРасчетов = ПланОтгрузок.ОбъектРасчетов
	|ГДЕ
	|	ПланОтгрузок.ОбъектРасчетов В
	|			(ВЫБРАТЬ
	|				Т.ОбъектРасчетов
	|			ИЗ
	|				ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК Т)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОбъектыРасчетов.Документ,
	|	ОбъектыРасчетов.ПорядокРасчетов,
	|	ПланОтгрузок.ОбъектРасчетов,
	|	ПланОтгрузок.Валюта,
	|	ПланОтгрузок.ДокументРегистратор,
	|	НАЧАЛОПЕРИОДА(ПланОтгрузок.Период, ДЕНЬ)
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентамиПланОтгрузок", "РасчетыСПоставщикамиПланПоставок");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПоОтгрузкам(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = " 
	|ВЫБРАТЬ
	|	ДанныеРасчетов.Документ                    КАК Документ,
	|	МАКСИМУМ(ДанныеРасчетов.ОбъектРасчетов)    КАК ОбъектРасчетов,
	|	ДанныеРасчетов.ОбъектРасчетовОбъект        КАК ОбъектРасчетовОбъект,
	|	ДанныеРасчетов.ДокументРегистратор         КАК ДокументРегистратор,
	|	ДанныеРасчетов.ПорядокРасчетов             КАК ПорядокРасчетов,
	|	ДанныеРасчетов.Валюта                      КАК Валюта,
	|	ДанныеРасчетов.ВариантОплатыОтгрузки       КАК ВариантОплатыОтгрузки,
	|	ДанныеРасчетов.ДатаПлан                    КАК ДатаПлан,
	|	
	|	СУММА(ДанныеРасчетов.СуммаПлан)            КАК СуммаПлан,
	|	СУММА(ДанныеРасчетов.КорректировкаГрафика) КАК КорректировкаГрафика,
	|	
	|	ДанныеРасчетов.ДатаФакт                    КАК ДатаФакт,
	|	МАКСИМУМ(ДанныеРасчетов.Период)            КАК Период,
	|	СУММА(ДанныеРасчетов.СуммаФакт)            КАК СуммаФакт,
	|	
	|	СУММА(0)                                   КАК Остаток,
	|	СУММА(0)                                   КАК ОстатокПросрочено,
	|	СУММА(0)                                   КАК ДнейПросрочено
	|	
	|ИЗ (
	|	// План отгрузок
	|	ВЫБРАТЬ
	|		ОбъектыРасчетов.Документ                 КАК Документ,
	|		ОбъектыРасчетов.ОбъектРасчетов           КАК ОбъектРасчетов,
	|		ОбъектыРасчетов.ОбъектРасчетовОбъект     КАК ОбъектРасчетовОбъект,
	|		ПланОтгрузок.ДокументРегистратор         КАК ДокументРегистратор,
	|		ОбъектыРасчетов.ПорядокРасчетов          КАК ПорядокРасчетов,
	|		ОбъектыРасчетов.Валюта                   КАК Валюта,
	|		ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументПлан) = ТИП(Документ.ГрафикИсполненияДоговора)
	|				ТОГДА 2
	|			ИНАЧЕ 3
	|		КОНЕЦ                                    КАК ВариантОплатыОтгрузки,
	|		ПланОтгрузок.ДатаПлановогоПогашения      КАК ДатаПлан,
	|		ВЫБОР
	|			КОГДА ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ПланОтгрузок.Сумма > 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			// Списание отгруженных товаров
	|			КОГДА ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ПланОтгрузок.Сумма < 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ                                    КАК СуммаПлан,
	|		ВЫБОР
	|			// Оплата аванса
	|			КОГДА ПланОтгрузок.НераспределенныйАванс
	|				И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|				И ПланОтгрузок.Сумма > 0 
	|				ТОГДА -ПланОтгрузок.Сумма
	|			// Сторно аванса
	|			КОГДА ПланОтгрузок.НераспределенныйАванс
	|				И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И (ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					ИЛИ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					ИЛИ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.КорректировкаЗадолженности))
	|				И ПланОтгрузок.Сумма < 0 
	|				ТОГДА -ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ                                    КАК КорректировкаГрафика,
	|		ВЫБОР
	|			// Отгрузка
	|			КОГДА НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма > 0
	|			// Зачет аванса уменьшает сумму факт, так как в этом запросе только отгрузки в долг
	|				ИЛИ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.КорректировкаЗадолженности)
	|					И ПланОтгрузок.Сумма < 0
	|			// Сторно отгрузки
	|				ИЛИ НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма < 0
	|				ТОГДА НАЧАЛОПЕРИОДА(ПланОтгрузок.Период, ДЕНЬ)
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ                                    КАК ДатаФакт,
	|		НАЧАЛОПЕРИОДА(ПланОтгрузок.Период, ДЕНЬ) КАК Период,
	|		ВЫБОР
	|			// Отгрузка
	|			КОГДА НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма > 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			// Зачет аванса уменьшает сумму факт, так как в этом запросе только отгрузки в долг
	|			КОГДА ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) В (&ТипыПлатежныхДокументов)
	|					И НЕ ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.КорректировкаЗадолженности)
	|					И ПланОтгрузок.Сумма < 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			// Сторно отгрузки
	|			КОГДА НЕ ПланОтгрузок.НераспределенныйАванс
	|					И ПланОтгрузок.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|					И ТИПЗНАЧЕНИЯ(ПланОтгрузок.ДокументРегистратор) = ТИП(Документ.Сторно)
	|					И ПланОтгрузок.Сумма < 0
	|				ТОГДА ПланОтгрузок.Сумма
	|			ИНАЧЕ 0
	|		КОНЕЦ                                    КАК СуммаФакт
	|	ИЗ
	|		ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентамиПланОтгрузок КАК ПланОтгрузок
	|			ПО ОбъектыРасчетов.ОбъектРасчетов = ПланОтгрузок.ОбъектРасчетов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	// Фактические оплаты и отгрузки
	|	ВЫБРАТЬ
	|		ОбъектыРасчетов.Документ                     КАК Документ,
	|		ОбъектыРасчетов.ИсходныйОбъектРасчетов       КАК ОбъектРасчетов,
	|		ОбъектыРасчетов.ИсходныйОбъектРасчетовОбъект КАК ОбъектРасчетовОбъект,
	|		РасчетыПоСрокам.ДокументРегистратор          КАК ДокументРегистратор,
	|		ОбъектыРасчетов.ПорядокРасчетов              КАК ПорядокРасчетов,
	|		ОбъектыРасчетов.Валюта                       КАК Валюта,
	|		ВЫБОР
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И (РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга)) 
	|				ТОГДА 5
	|			ИНАЧЕ 0
	|		КОНЕЦ                                        КАК ВариантОплатыОтгрузки,
	|		ВЫБОР
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И (РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга)) 
	|				ТОГДА РасчетыПоСрокам.ДатаВозникновения
	|			ИНАЧЕ РасчетыПоСрокам.ДатаПлановогоПогашения
	|		КОНЕЦ                                        КАК ДатаПлан,
	|		0                                            КАК СуммаПлан,
	|		ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУвеличениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУвеличениеПредоплаты))
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов В (
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ВозвратДенежныхСредствУменьшениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ПереносВзаиморасчетовУменьшениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаЗадолженностиУменьшениеПредоплаты), 
	|				ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.КорректировкаНакладнойУменьшениеПредоплаты))
	|				ТОГДА -РасчетыПоСрокам.Предоплата
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			КОГДА ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга) 
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа           КАК КорректировкаГрафика,
	|		ВЫБОР 
	|			КОГДА (ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|					ИЛИ РасчетыПоСрокам.Сторно)
	|				И (РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга)
	|					ИЛИ РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга))
	|				И РасчетыПоСрокам.Долг <> 0
	|				ТОГДА РасчетыПоСрокам.ДатаВозникновения
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеПредоплаты)
	|				И РасчетыПоСрокам.Предоплата  <> 0
	|				ТОГДА НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ)
	|			ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
	|		КОНЕЦ                                        КАК ДатаФакт,
	|		НАЧАЛОПЕРИОДА(РасчетыПоСрокам.Период, ДЕНЬ) КАК Период,
	|		ВЫБОР
	|			КОГДА РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеПредоплаты)
	|				ТОГДА РасчетыПоСрокам.Предоплата
	|			КОГДА (ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|					ИЛИ РасчетыПоСрокам.Сторно)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУменьшениеДолга) 
	|				ТОГДА -РасчетыПоСрокам.Долг
	|			КОГДА (ОбъектыРасчетов.ИсходныйОбъектРасчетов.ТипОбъектаРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовРасчетов.Накладная)
	|					ИЛИ РасчетыПоСрокам.Сторно)
	|				И РасчетыПоСрокам.ТипЗаписиВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОтгрузкаУвеличениеДолга) 
	|				ТОГДА РасчетыПоСрокам.Долг
	|			ИНАЧЕ 0
	|		КОНЕЦ * ОбъектыРасчетов.ДоляЗаказа           КАК СуммаФакт
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыПоСрокам
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтСвязьЗаказовНакладныхРасчетыСКлиентами КАК ОбъектыРасчетов
	|			ПО РасчетыПоСрокам.ОбъектРасчетов = ОбъектыРасчетов.ОбъектРасчетов
	|	) КАК ДанныеРасчетов
	|	
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРасчетов.Документ,
	|	ДанныеРасчетов.ОбъектРасчетовОбъект,
	|	ДанныеРасчетов.ДокументРегистратор,
	|	ДанныеРасчетов.ПорядокРасчетов,
	|	ДанныеРасчетов.Валюта,
	|	ДанныеРасчетов.ВариантОплатыОтгрузки,
	|	ДанныеРасчетов.ДатаПлан,
	|	ДанныеРасчетов.ДатаФакт
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДанныеРасчетов.СуммаПлан) <> 0
	|		ИЛИ СУММА(ДанныеРасчетов.КорректировкаГрафика) <> 0
	|		ИЛИ СУММА(ДанныеРасчетов.СуммаФакт) <> 0
	|			И ДанныеРасчетов.ВариантОплатыОтгрузки <> 4
	|		ИЛИ СУММА(ДанныеРасчетов.СуммаФакт) > 0
	|			И ДанныеРасчетов.ВариантОплатыОтгрузки = 4)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаПлан,
	|	ВариантОплатыОтгрузки,
	|	ДатаФакт
	|";
	
	Если Не ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентамиПланОтгрузок", "РасчетыСПоставщикамиПланПоставок");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РасчетыСКлиентами", "РасчетыСПоставщиками");
	КонецЕсли;
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТипыПлатежныхДокументов()
	
	СписокТипов = Новый СписокЗначений;
	СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеБезналичныхДенежныхСредств"));
	СписокТипов.Добавить(Тип("ДокументСсылка.СписаниеБезналичныхДенежныхСредств"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ПриходныйКассовыйОрдер"));
	СписокТипов.Добавить(Тип("ДокументСсылка.РасходныйКассовыйОрдер"));
	СписокТипов.Добавить(Тип("ДокументСсылка.ОперацияПоПлатежнойКарте"));
	//++ Локализация
	СписокТипов.Добавить(Тип("ДокументСсылка.ОперацияПоЯндексКассе"));
	//-- Локализация
	
	
	Возврат СписокТипов;
	
КонецФункции

Функция ПолучитьДанныеРасчетов(МенеджерВТ, РасчетыСКлиентами, РасчетыСПоставщиками)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ТипыПлатежныхДокументов", ТипыПлатежныхДокументов());
	Запрос.УстановитьПараметр("ДанныеОтчета", 4);
	
	ТекстыЗапроса = Новый Массив;
	
	Если РасчетыСКлиентами Тогда
		
		ТекстыЗапроса.Добавить(ТекстЗапросаВтСвязьЗаказовНакладных(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатамПоСрокам(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатамПоПлану(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатам(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкамПоСрокам(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкамПоПлану(Истина));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкам(Истина));
		
	КонецЕсли;
	
	Если РасчетыСПоставщиками Тогда
		
		ТекстыЗапроса.Добавить(ТекстЗапросаВтСвязьЗаказовНакладных(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатамПоСрокам(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатамПоПлану(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОплатам(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкамПоСрокам(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкамПоПлану(Ложь));
		ТекстыЗапроса.Добавить(ТекстЗапросаПоОтгрузкам(Ложь));
		
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеРасчетов = Новый Структура;
	ДанныеДляВывода = Новый Структура;
	ИндексПакета = 0;
	Если РасчетыСКлиентами Тогда
		ДанныеРасчетов.Вставить("ОплатыПоСрокам",   РезультатЗапроса[1].Выгрузить());
		ДанныеРасчетов.Вставить("ОплатыПоПлану",    РезультатЗапроса[2].Выгрузить());
		ДанныеРасчетов.Вставить("Оплаты",           РезультатЗапроса[3].Выгрузить());
		ДанныеРасчетов.Вставить("ОтгрузкиПоСрокам", РезультатЗапроса[4].Выгрузить());
		ДанныеРасчетов.Вставить("ОтгрузкиПоПлану",  РезультатЗапроса[5].Выгрузить());
		ДанныеРасчетов.Вставить("Отгрузки",         РезультатЗапроса[6].Выгрузить());
		ДанныеДляВывода.Вставить("РасчетыСКлиентамиОплаты", ОбработатьДанныеПередВыводом(ДанныеРасчетов, Истина, Истина));
		ДанныеДляВывода.Вставить("РасчетыСКлиентамиОтгрузки", ОбработатьДанныеПередВыводом(ДанныеРасчетов, Ложь, Истина));
		ИндексПакета = 7;
	КонецЕсли;
	
	Если РасчетыСПоставщиками Тогда
		ДанныеРасчетов.Вставить("ОплатыПоСрокам",   РезультатЗапроса[ИндексПакета + 1].Выгрузить());
		ДанныеРасчетов.Вставить("ОплатыПоПлану",    РезультатЗапроса[ИндексПакета + 2].Выгрузить());
		ДанныеРасчетов.Вставить("Оплаты",           РезультатЗапроса[ИндексПакета + 3].Выгрузить());
		ДанныеРасчетов.Вставить("ОтгрузкиПоСрокам", РезультатЗапроса[ИндексПакета + 4].Выгрузить());
		ДанныеРасчетов.Вставить("ОтгрузкиПоПлану",  РезультатЗапроса[ИндексПакета + 5].Выгрузить());
		ДанныеРасчетов.Вставить("Отгрузки",         РезультатЗапроса[ИндексПакета + 6].Выгрузить());
		ДанныеДляВывода.Вставить("РасчетыСПоставщикамиОплаты", ОбработатьДанныеПередВыводом(ДанныеРасчетов, Истина, Ложь));
		ДанныеДляВывода.Вставить("РасчетыСПоставщикамиОтгрузки", ОбработатьДанныеПередВыводом(ДанныеРасчетов, Ложь, Ложь));
	КонецЕсли;
	
	Возврат ДанныеДляВывода;
	
КонецФункции

Функция ОбработатьДанныеПередВыводом(ДанныеРасчетов, ЭтоОплаты, ЭтоРасчетыСКлиентом)
	
	ТаблицаРасчетов = ДанныеРасчетов[?(ЭтоОплаты, "Оплаты", "Отгрузки")];
	План = ДанныеРасчетов[?(ЭтоОплаты, "ОплатыПоПлану", "ОтгрузкиПоПлану")];
	Факт = ДанныеРасчетов[?(ЭтоОплаты, "ОплатыПоСрокам", "ОтгрузкиПоСрокам")];
	
	Факт.Индексы.Добавить("Документ, ОбъектРасчетов, ДокументРегистратор");
	ТаблицаРасхождений = Факт.Скопировать();
	ОбъектыРасчетовСПланом = Новый Соответствие;
	Для Каждого СтрокаПлана Из План Цикл
		НоваяСтрока = ТаблицаРасхождений.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПлана);
		НоваяСтрока.СуммаФакт = ?(ЭтоОплаты, -НоваяСтрока.СуммаФакт, 0);
		НоваяСтрока.ЗачетАванса          = -НоваяСтрока.ЗачетАванса;
		НоваяСтрока.КорректировкаГрафика = -НоваяСтрока.КорректировкаГрафика;
		Если ОбъектыРасчетовСПланом[СтрокаПлана.ОбъектРасчетов] = Неопределено Тогда
			ОбъектыРасчетовСПланом[СтрокаПлана.ОбъектРасчетов] = Истина;
		КонецЕсли;
	КонецЦикла;
	Если Не ЭтоОплаты Тогда
		Для Каждого СтрокаРасчетов Из ТаблицаРасчетов Цикл
			НоваяСтрока = ТаблицаРасхождений.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРасчетов);
			НоваяСтрока.СуммаФакт            = -НоваяСтрока.СуммаФакт;
			НоваяСтрока.ЗачетАванса          = 0;
			НоваяСтрока.КорректировкаГрафика = 0;
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаРасхождений.Свернуть("Документ, ОбъектРасчетов, ДокументРегистратор, ПорядокРасчетов", "СуммаФакт, ЗачетАванса, КорректировкаГрафика");
	Отбор = Новый Структура("Документ, ОбъектРасчетов, ДокументРегистратор"); 
	
	ТаблицаРасчетов.Индексы.Добавить("Документ, ОбъектРасчетов, ВариантОплатыОтгрузки, ДатаПлан, ДокументРегистратор");
	ОтборПоЭтапуИРегистратору = Новый Структура("Документ, ОбъектРасчетов, ВариантОплатыОтгрузки, ДатаПлан, ДокументРегистратор");
	
	Если ЭтоОплаты Тогда
		
		Для Каждого ДопСтрокаОплаты Из ТаблицаРасхождений Цикл
			Если ОбъектыРасчетовСПланом[ДопСтрокаОплаты.ОбъектРасчетов]= Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Отбор, ДопСтрокаОплаты);
			Если ДопСтрокаОплаты.СуммаФакт > 0 Тогда
				СтрокиФакта = Факт.НайтиСтроки(Отбор);
				Если СтрокиФакта.Количество() > 0 Тогда
					// Оплата сверх плана
					НоваяСтрока = ТаблицаРасчетов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиФакта[0]);
					НоваяСтрока.СуммаФакт = ДопСтрокаОплаты.СуммаФакт;
					НоваяСтрока.КорректировкаГрафика = ДопСтрокаОплаты.СуммаФакт;
					НоваяСтрока.ВариантОплатыОтгрузки = ?(
						ЭтоРасчетыСКлиентом,
						Перечисления.ВариантыКонтроляОплатыКлиентом.ПустаяСсылка(),
						Перечисления.ВариантыКонтроляОплатыПоставщику.ПустаяСсылка());
				КонецЕсли;
			КонецЕсли;
			// Разница в зачетах аванса для детализаций с авансов по заказам или договорам
			Если ДопСтрокаОплаты.ЗачетАванса <> 0
				И (ДопСтрокаОплаты.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
					Или ДопСтрокаОплаты.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным) Тогда
				СтрокиФакта = ТаблицаРасчетов.НайтиСтроки(Отбор);
				СуммаРасхождения = -ДопСтрокаОплаты.ЗачетАванса;
				Для Каждого СтрокаФакта Из СтрокиФакта Цикл
					СуммаКСписанию = Мин(СуммаРасхождения, СтрокаФакта.КорректировкаГрафика);
					СтрокаФакта.КорректировкаГрафика = СтрокаФакта.КорректировкаГрафика - СуммаКСписанию;
					СуммаРасхождения = СуммаРасхождения - СуммаКСписанию;
					Если СуммаРасхождения = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		СтрокиАвансаСверхПланаОтгрузки = Новый Массив;
		Для Каждого ДопСтрокаОтгрузки Из ТаблицаРасхождений Цикл
			Если ОбъектыРасчетовСПланом[ДопСтрокаОтгрузки.ОбъектРасчетов]= Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если ДопСтрокаОтгрузки.КорректировкаГрафика > 0 Тогда
				СтрокиАвансаСверхПланаОтгрузки.Добавить(ДопСтрокаОтгрузки);
			КонецЕсли;
		КонецЦикла;
		Для Каждого ДопСтрокаОтгрузки Из ТаблицаРасхождений Цикл
			Если ОбъектыРасчетовСПланом[ДопСтрокаОтгрузки.ОбъектРасчетов]= Неопределено Тогда
				Продолжить;
			КонецЕсли;
			// Разница в зачетах аванса в плане и факте
			Если ДопСтрокаОтгрузки.ЗачетАванса < 0 Тогда
				// Уменьшим корректировку аванса, образовавшего расхождение
				СуммаРасхожденияКорректировки = -ДопСтрокаОтгрузки.ЗачетАванса;
				Для Каждого СтрокаАвансаСверхПланаОтгрузки Из СтрокиАвансаСверхПланаОтгрузки Цикл
					ЗаполнитьЗначенияСвойств(Отбор, СтрокаАвансаСверхПланаОтгрузки);
					СтрокиКорректировки = ТаблицаРасчетов.НайтиСтроки(Отбор);
					Для Каждого СтрокаКорректировки Из СтрокиКорректировки Цикл
						СуммаКСписаниюКорректировки = Мин(СуммаРасхожденияКорректировки, СтрокаКорректировки.КорректировкаГрафика, СтрокаАвансаСверхПланаОтгрузки.КорректировкаГрафика);
						СтрокаКорректировки.КорректировкаГрафика = СтрокаКорректировки.КорректировкаГрафика - СуммаКСписаниюКорректировки;
						СтрокаАвансаСверхПланаОтгрузки.КорректировкаГрафика = СтрокаАвансаСверхПланаОтгрузки.КорректировкаГрафика - СуммаКСписаниюКорректировки;
						СуммаРасхожденияКорректировки = СуммаРасхожденияКорректировки - СуммаКСписаниюКорректировки;
						
						// Найдем строки фактической отгрузки, которые зачли это расхождение
						ЗаполнитьЗначенияСвойств(ОтборПоЭтапуИРегистратору, СтрокаКорректировки);
						ОтборПоЭтапуИРегистратору.ДокументРегистратор = ДопСтрокаОтгрузки.ДокументРегистратор;
						СтрокиФакта = ТаблицаРасчетов.НайтиСтроки(ОтборПоЭтапуИРегистратору);
						СуммаРасхождения = СуммаКСписаниюКорректировки;
						Для Каждого СтрокаФакта Из СтрокиФакта Цикл
							СуммаКСписанию = Мин(СуммаРасхождения, СтрокаФакта.СуммаФакт);
							СтрокаФакта.СуммаФакт = СтрокаФакта.СуммаФакт - СуммаКСписанию;
							СуммаРасхождения = СуммаРасхождения - СуммаКСписанию;
							Если СуммаРасхождения = 0 Тогда
								Прервать;
							КонецЕсли;
						КонецЦикла;
						Если СуммаРасхожденияКорректировки = 0 Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		Для Каждого ДопСтрокаОтгрузки Из ТаблицаРасхождений Цикл
			Если ОбъектыРасчетовСПланом[ДопСтрокаОтгрузки.ОбъектРасчетов]= Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Отбор, ДопСтрокаОтгрузки);
			// Разница в зачетах аванса для детализаций с авансов по заказам или договорам
			Если (ДопСтрокаОтгрузки.СуммаФакт <> 0 
					И ЭтоОплаты
						Или ДопСтрокаОтгрузки.СуммаФакт > 0
							И Не ЭтоОплаты)
				И (ДопСтрокаОтгрузки.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
					Или ДопСтрокаОтгрузки.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным) Тогда
				СтрокиФакта = ТаблицаРасчетов.НайтиСтроки(Отбор);
				СуммаРасхождения = ДопСтрокаОтгрузки.СуммаФакт;
				Для Каждого СтрокаФакта Из СтрокиФакта Цикл
					Если ДопСтрокаОтгрузки.СуммаФакт < 0 Тогда
						СуммаКСписанию = Мин(-СуммаРасхождения, СтрокаФакта.СуммаФакт);
						СтрокаФакта.СуммаФакт = СтрокаФакта.СуммаФакт - СуммаКСписанию;
						ДопСтрокаОтгрузки.СуммаФакт = ДопСтрокаОтгрузки.СуммаФакт + СуммаКСписанию;
					Иначе
						СуммаКНачислению = ДопСтрокаОтгрузки.СуммаФакт;
						СтрокаФакта.СуммаФакт = СтрокаФакта.СуммаФакт + СуммаКНачислению;
						ДопСтрокаОтгрузки.СуммаФакт = ДопСтрокаОтгрузки.СуммаФакт - СуммаКНачислению;
					КонецЕсли;
					Если ДопСтрокаОтгрузки.СуммаФакт = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		// Положительный остаток суммы факт - это отгрузка сверх плана
		ТаблицаРасхождений.Свернуть("Документ, ОбъектРасчетов", "СуммаФакт");
		Для Каждого СтрокаСверхПлана Из ТаблицаРасхождений Цикл
			СтрокиРасчетов = Факт.НайтиСтроки(Новый Структура("Документ, ОбъектРасчетов, ВариантОплатыОтгрузки", СтрокаСверхПлана.Документ, СтрокаСверхПлана.ОбъектРасчетов, 4));
			Если СтрокиРасчетов.Количество() > 0 
				И СтрокаСверхПлана.СуммаФакт > 0 Тогда
				НоваяСтрока = ТаблицаРасчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокиРасчетов[0]);
				НоваяСтрока.СуммаФакт = СтрокаСверхПлана.СуммаФакт;
				НоваяСтрока.КорректировкаГрафика = 0;
				НоваяСтрока.ДатаФакт = Дата(1, 1, 1);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ТаблицаРасчетов.Индексы.Добавить("Документ, ОбъектРасчетов, ДатаПлан, ВариантОплатыОтгрузки");
	
	ГруппировкаПоДокументу = ТаблицаРасчетов.Скопировать(, "Документ, ОбъектРасчетов, ОбъектРасчетовОбъект, Валюта, ПорядокРасчетов, СуммаПлан, СуммаФакт, КорректировкаГрафика");
	ГруппировкаПоДокументу.Свернуть("Документ, ОбъектРасчетов, ОбъектРасчетовОбъект, Валюта, ПорядокРасчетов", "СуммаПлан, СуммаФакт, КорректировкаГрафика");
	
	ГруппировкаПоЭтапуОплатыОтгрузки = ТаблицаРасчетов.Скопировать(, "Документ, ОбъектРасчетов, Валюта, ПорядокРасчетов, ДатаПлан, ВариантОплатыОтгрузки, СуммаПлан, СуммаФакт, КорректировкаГрафика");
	ГруппировкаПоЭтапуОплатыОтгрузки.Свернуть("Документ, ОбъектРасчетов, Валюта, ПорядокРасчетов, ДатаПлан, ВариантОплатыОтгрузки", "СуммаПлан, СуммаФакт, КорректировкаГрафика");
	ГруппировкаПоЭтапуОплатыОтгрузки.Индексы.Добавить("Документ, ОбъектРасчетов");
	ГруппировкаПоЭтапуОплатыОтгрузки.Индексы.Добавить("Документ, ОбъектРасчетов, ДатаПлан");
	
	ОтборЭтапаОплатыОтгрузки = Новый Структура("Документ, ОбъектРасчетов, ДатаПлан, ВариантОплатыОтгрузки");
	ОтборДокумента = Новый Структура("Документ, ОбъектРасчетов");
	
	// Строки сверх плана неактуальны для документов без плана
	Для Каждого СтрокаДокумента Из ГруппировкаПоДокументу Цикл
		Если СтрокаДокумента.СуммаПлан = 0 Тогда
			ЗаполнитьЗначенияСвойств(ОтборДокумента, СтрокаДокумента);
			СтрокиЭтаповОплатыОтгрузки = ГруппировкаПоЭтапуОплатыОтгрузки.НайтиСтроки(ОтборДокумента);
			Для Каждого СтрокаЭтапаОплатыОтгрузки Из СтрокиЭтаповОплатыОтгрузки Цикл
				Если СтрокаЭтапаОплатыОтгрузки.ВариантОплатыОтгрузки = 4
					Или ЭтоОплаты 
						И Не ЗначениеЗаполнено(СтрокаЭтапаОплатыОтгрузки.ДатаПлан) Тогда
					ЗаполнитьЗначенияСвойств(ОтборЭтапаОплатыОтгрузки, СтрокаЭтапаОплатыОтгрузки);
					ДетальныеЗаписи = ТаблицаРасчетов.НайтиСтроки(ОтборЭтапаОплатыОтгрузки);
					Для Каждого СтрокаТаблицы Из ДетальныеЗаписи Цикл
						ТаблицаРасчетов.Удалить(СтрокаТаблицы);
					КонецЦикла;
					СтрокаДокумента.СуммаФакт = СтрокаДокумента.СуммаФакт - СтрокаЭтапаОплатыОтгрузки.СуммаФакт;
					СтрокаДокумента.КорректировкаГрафика = СтрокаДокумента.КорректировкаГрафика - СтрокаЭтапаОплатыОтгрузки.КорректировкаГрафика;
					ГруппировкаПоЭтапуОплатыОтгрузки.Удалить(СтрокаЭтапаОплатыОтгрузки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	// Строки без варианта оплаты отгрузки необходимо объединить со строками плана, у которых есть отрицательные корректировки
	// таким образом, чтобы у итоговой строки корректировка стремилась к 0, если строка факта остается, то устанавливается вариант оплаты отгрузки по умолчанию
	// Если в реализации не изменили план оплаты, то вариант оплаты унаследуется для графика погашения фактической задолженности
	// Если в авансовой платежке не указали дату погашения, отличную от даты плановой отгрузки, то вариант отгрузки унаследуется
	СтрокиФакта = Новый Массив;
	Для Каждого СтрокаРасчетов Из ГруппировкаПоЭтапуОплатыОтгрузки Цикл
		Если Не ЗначениеЗаполнено(СтрокаРасчетов.ВариантОплатыОтгрузки) Тогда
			СтрокиФакта.Добавить(СтрокаРасчетов);
		КонецЕсли;
	КонецЦикла;
	
	ОтборПлановыхСтрок = Новый Структура("Документ, ОбъектРасчетов, ДатаПлан");
	
	Для Каждого СтрокаФакта Из СтрокиФакта Цикл
		Если СтрокаФакта.КорректировкаГрафика > 0 Тогда
			ЗаполнитьЗначенияСвойств(ОтборПлановыхСтрок, СтрокаФакта);
			СтрокиПлана = ГруппировкаПоЭтапуОплатыОтгрузки.НайтиСтроки(ОтборПлановыхСтрок);
			// Если в текущую плановую дату есть строки планового графика, то перенесем в них суммы корректировок, остатков и факта текущей строки
			Для Каждого СтрокаПлана Из СтрокиПлана Цикл
				Если ЗначениеЗаполнено(СтрокаПлана.ВариантОплатыОтгрузки)
					И СтрокаПлана.КорректировкаГрафика < 0 Тогда
					
					// Ограничим возможное уменьшение сумм фактической строки суммой уменьшения планового графика
					СуммаПереноса = Мин(СтрокаФакта.КорректировкаГрафика, - СтрокаПлана.КорректировкаГрафика);
					СтрокаФакта.КорректировкаГрафика = СтрокаФакта.КорректировкаГрафика - СуммаПереноса;
					СтрокаПлана.КорректировкаГрафика = СтрокаПлана.КорректировкаГрафика + СуммаПереноса;
					
					СуммаПереносаФакта = Мин(СуммаПереноса, СтрокаФакта.СуммаФакт);
					СтрокаПлана.СуммаФакт = СтрокаПлана.СуммаФакт + СуммаПереносаФакта;
					СтрокаФакта.СуммаФакт = СтрокаФакта.СуммаФакт - СуммаПереносаФакта;
					
					// Найдем строки факта и укажем им вариант оплаты отгрузки в соответствии со строкой плана
					ЗаполнитьЗначенияСвойств(ОтборЭтапаОплатыОтгрузки, СтрокаФакта);
					ДетальныеСтрокиФакта = ТаблицаРасчетов.НайтиСтроки(ОтборЭтапаОплатыОтгрузки);
					
					Для Каждого ДетальнаяСтрокаФакта Из ДетальныеСтрокиФакта Цикл
						Если ДетальнаяСтрокаФакта.СуммаФакт > 0 Тогда
							Если ДетальнаяСтрокаФакта.СуммаФакт <= СуммаПереноса Тогда
								ДетальнаяСтрокаФакта.ВариантОплатыОтгрузки = СтрокаПлана.ВариантОплатыОтгрузки;
								СуммаПереноса = СуммаПереноса - ДетальнаяСтрокаФакта.СуммаФакт;
							ИначеЕсли СуммаПереноса = 0 Тогда
								Прервать;
							Иначе
								НоваяСтрока = ТаблицаРасчетов.Вставить(ТаблицаРасчетов.Индекс(ДетальнаяСтрокаФакта) + 1);
								ЗаполнитьЗначенияСвойств(НоваяСтрока, ДетальнаяСтрокаФакта);
								ДетальнаяСтрокаФакта.ВариантОплатыОтгрузки = СтрокаПлана.ВариантОплатыОтгрузки;
								ДетальнаяСтрокаФакта.СуммаФакт = СуммаПереноса;
								НоваяСтрока.СуммаФакт = НоваяСтрока.СуммаФакт - СуммаПереноса;
								СуммаПереноса = 0;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаРасчетов.Свернуть(
		"Валюта, ВариантОплатыОтгрузки, ДатаПлан, ДатаФакт, ДнейПросрочено, Документ, ОбъектРасчетов, ОбъектРасчетовОбъект, ПорядокРасчетов",
		"КорректировкаГрафика, Остаток, ОстатокПросрочено, СуммаПлан, СуммаФакт");
	
	ТаблицаРасчетов.Сортировать("ДатаПлан, ВариантОплатыОтгрузки, ДатаФакт");
	
	Результат = Новый ДеревоЗначений;
	Для Каждого Колонка Из ТаблицаРасчетов.Колонки Цикл
		Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	// Структура дерева:
	//		СтрокаДокумента - группировка по полям: Документ, ОбъектРасчетов
	//			СтрокаЭтапаОплатыОтгрузки - группировка по полям: ДатаПлан, ВариантОплатыОтгрузки
	//				СтрокаФакт - фактические даты и суммы оплат или отгрузок
	
	Для Каждого СтрокаДокумента Из ГруппировкаПоДокументу Цикл
		
		СтрокаДереваПоДокументу = Результат.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДереваПоДокументу, СтрокаДокумента);
		
		ЗаполнитьЗначенияСвойств(ОтборДокумента, СтрокаДокумента);
		СтрокиЭтаповОплатыОтгрузки = ГруппировкаПоЭтапуОплатыОтгрузки.НайтиСтроки(ОтборДокумента);
		
		Для Каждого СтрокаЭтапаОплатыОтгрузки Из СтрокиЭтаповОплатыОтгрузки Цикл
			
			Если СтрокаЭтапаОплатыОтгрузки.СуммаПлан = 0
				И СтрокаЭтапаОплатыОтгрузки.СуммаФакт = 0
				И СтрокаЭтапаОплатыОтгрузки.КорректировкаГрафика = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаДереваПоЭтапуОплатыОтгрузки = СтрокаДереваПоДокументу.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаДереваПоЭтапуОплатыОтгрузки, СтрокаЭтапаОплатыОтгрузки);
			
			ЗаполнитьЗначенияСвойств(ОтборЭтапаОплатыОтгрузки, СтрокаЭтапаОплатыОтгрузки);
			ДетальныеЗаписи = ТаблицаРасчетов.НайтиСтроки(ОтборЭтапаОплатыОтгрузки);
			
			Если Не ЗначениеЗаполнено(СтрокаДереваПоЭтапуОплатыОтгрузки.ВариантОплатыОтгрузки) 
				И ТипЗнч(СтрокаДереваПоЭтапуОплатыОтгрузки.ВариантОплатыОтгрузки) <> Тип("ПеречислениеСсылка.ВариантыКонтроляОплатыКлиентом")
				И ТипЗнч(СтрокаДереваПоЭтапуОплатыОтгрузки.ВариантОплатыОтгрузки) <> Тип("ПеречислениеСсылка.ВариантыКонтроляОплатыПоставщику") Тогда
				СтрокаДереваПоЭтапуОплатыОтгрузки.ВариантОплатыОтгрузки = 1;
			КонецЕсли;
			
			Для Каждого СтрокаТаблицы Из ДетальныеЗаписи Цикл
				
				Если ЗначениеЗаполнено(СтрокаТаблицы.ДатаФакт)
					И (СтрокаТаблицы.СуммаПлан <> 0
						Или СтрокаТаблицы.СуммаФакт <> 0
						Или СтрокаТаблицы.КорректировкаГрафика <> 0) Тогда
					СтрокаДерева = СтрокаДереваПоЭтапуОплатыОтгрузки.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДерева, СтрокаТаблицы);
					Если Не ЗначениеЗаполнено(СтрокаДерева.ВариантОплатыОтгрузки) Тогда
						СтрокаДерева.ВариантОплатыОтгрузки = СтрокаДереваПоЭтапуОплатыОтгрузки.ВариантОплатыОтгрузки;
					КонецЕсли;
					Если СтрокаДереваПоДокументу.СуммаПлан = 0 Тогда
						СтрокаДерева.СуммаПлан = СтрокаДерева.КорректировкаГрафика;
						СтрокаДерева.КорректировкаГрафика = 0;
					КонецЕсли;
				КонецЕсли;
				
			КонецЦикла;
			
			Если СтрокаДереваПоДокументу.СуммаПлан = 0 Тогда
				СтрокаДереваПоЭтапуОплатыОтгрузки.СуммаПлан = СтрокаДереваПоЭтапуОплатыОтгрузки.КорректировкаГрафика;
				СтрокаДереваПоЭтапуОплатыОтгрузки.КорректировкаГрафика = 0;
			КонецЕсли;
			
		КонецЦикла;
		
		Если СтрокаДереваПоДокументу.СуммаПлан = 0 Тогда
			СтрокаДереваПоДокументу.СуммаПлан = СтрокаДереваПоДокументу.КорректировкаГрафика;
			СтрокаДереваПоДокументу.КорректировкаГрафика = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	// Расчет остатков
	Для Каждого СтрокаДереваПоДокументу Из Результат.Строки Цикл
		
		Для Каждого СтрокаДереваПоЭтапуОплатыОтгрузки Из СтрокаДереваПоДокументу.Строки Цикл
			
			СтрокаДереваПоЭтапуОплатыОтгрузки.Остаток = Макс(
				СтрокаДереваПоЭтапуОплатыОтгрузки.СуммаПлан
					+ СтрокаДереваПоЭтапуОплатыОтгрузки.КорректировкаГрафика
					- СтрокаДереваПоЭтапуОплатыОтгрузки.СуммаФакт,
				0);
			Если СтрокаДереваПоЭтапуОплатыОтгрузки.Остаток > 0
				И ЗначениеЗаполнено(СтрокаДереваПоЭтапуОплатыОтгрузки.ДатаПлан)
				И СтрокаДереваПоЭтапуОплатыОтгрузки.ДатаПлан < НачалоДня(ТекущаяДатаСеанса()) Тогда
				
				СтрокаДереваПоЭтапуОплатыОтгрузки.ОстатокПросрочено = СтрокаДереваПоЭтапуОплатыОтгрузки.Остаток;
				СтрокаДереваПоЭтапуОплатыОтгрузки.ДнейПросрочено = ОбщегоНазначенияУТ.РазностьДат(
					СтрокаДереваПоЭтапуОплатыОтгрузки.ДатаПлан,
					НачалоДня(ТекущаяДатаСеанса()),
					Перечисления.Периодичность.День);
				
			КонецЕсли;
			
		КонецЦикла;
		
		СтрокаДереваПоДокументу.Остаток           = СтрокаДереваПоДокументу.Строки.Итог("Остаток");
		СтрокаДереваПоДокументу.ОстатокПросрочено = СтрокаДереваПоДокументу.Строки.Итог("ОстатокПросрочено");
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ВывестиТаблицуРасчетовПоОплатамОтгрузкам(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, ЭтоРасчетыСКлиентом)
	
	Перем ТаблицаОплат, ТаблицаОтгрузок;
	
	ИмяТаблицыОплат = ?(
		ЭтоРасчетыСКлиентом,
		"РасчетыСКлиентамиОплаты",
		"РасчетыСПоставщикамиОплаты");
	ИмяТаблицыОтгрузок = ?(
		ЭтоРасчетыСКлиентом,
		"РасчетыСКлиентамиОтгрузки",
		"РасчетыСПоставщикамиОтгрузки");
	
	ЕстьТаблицаОплат = ТаблицыРезультатов.Свойство(ИмяТаблицыОплат, ТаблицаОплат);
	ЕстьТаблицаОтгрузок = ТаблицыРезультатов.Свойство(ИмяТаблицыОтгрузок, ТаблицаОтгрузок);
	Если НЕ (ЕстьТаблицаОплат
			Или ЕстьТаблицаОтгрузок) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОплатыПоДокументу = ?(
		ЕстьТаблицаОплат,
		ТаблицаОплат.Строки.НайтиСтроки(Новый Структура("Документ", Документ)),
		Неопределено);
	ОтгрузкиПоДокументу = ?(
		ЕстьТаблицаОтгрузок,
		ТаблицаОтгрузок.Строки.НайтиСтроки(Новый Структура("Документ", Документ)),
		Неопределено);
	Если ОплатыПоДокументу = Неопределено
		И ОтгрузкиПоДокументу = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ОбъектыРасчетов = Новый Массив;
	// Для порядков расчетов Аванс по заказам, долг по накладным и Аванс по договорам, долг по накладным 
	// не выводим из накладной объекты расчетов заказы и договоры, а из заказа не выводим объекты расчетов накладные, так как они уже учтены в заказе,
	Если ОплатыПоДокументу <> Неопределено Тогда
		Для Каждого ОплатыПоОбъектуРасчетов Из ОплатыПоДокументу Цикл
			Если ОплатыПоОбъектуРасчетов.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
					И ОплатыПоОбъектуРасчетов.Документ <> ОплатыПоОбъектуРасчетов.ОбъектРасчетовОбъект
				Или ОплатыПоОбъектуРасчетов.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным
					И ОплатыПоОбъектуРасчетов.Документ <> ОплатыПоОбъектуРасчетов.ОбъектРасчетовОбъект
					И ТипЗнч(ОплатыПоОбъектуРасчетов.ОбъектРасчетовОбъект) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Продолжить;
			КонецЕсли;
			ОбъектыРасчетов.Добавить(ОплатыПоОбъектуРасчетов.ОбъектРасчетов);
		КонецЦикла;
	КонецЕсли;
	Если ОтгрузкиПоДокументу <> Неопределено Тогда
		Для Каждого ОтгрузкиПоОбъектуРасчетов Из ОтгрузкиПоДокументу Цикл
			Если ОтгрузкиПоОбъектуРасчетов.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоЗаказамНакладным
					И ОтгрузкиПоОбъектуРасчетов.Документ <> ОтгрузкиПоОбъектуРасчетов.ОбъектРасчетовОбъект 
				Или ОтгрузкиПоОбъектуРасчетов.ПорядокРасчетов = Перечисления.ПорядокРасчетов.ПоДоговорамНакладным
					И ОтгрузкиПоОбъектуРасчетов.Документ <> ОтгрузкиПоОбъектуРасчетов.ОбъектРасчетовОбъект
					И ТипЗнч(ОтгрузкиПоОбъектуРасчетов.ОбъектРасчетовОбъект) <> Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда
				Продолжить;
			КонецЕсли;
			ОбъектыРасчетов.Добавить(ОтгрузкиПоОбъектуРасчетов.ОбъектРасчетов);
		КонецЦикла;
	КонецЕсли;
	ОбъектыРасчетов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбъектыРасчетов);
	
	ВыводитьОтступ = Ложь;
	ЕстьДанные = Ложь;
	Для Каждого ОбъектРасчетов Из ОбъектыРасчетов Цикл
		
		ОплатыПоОбъектуРасчетов = Неопределено;
		Если ТаблицаОплат <> Неопределено Тогда
			СтрокиОплатыПоОбъектуРасчетов = ТаблицаОплат.Строки.НайтиСтроки(Новый Структура("Документ, ОбъектРасчетов", Документ, ОбъектРасчетов));
			Если СтрокиОплатыПоОбъектуРасчетов.Количество() > 0 Тогда
				ОплатыПоОбъектуРасчетов = СтрокиОплатыПоОбъектуРасчетов[0];
			КонецЕсли;
		КонецЕсли;
		ОтгрузкиПоОбъектуРасчетов = Неопределено;
		Если ТаблицаОтгрузок <> Неопределено Тогда
			СтрокиОтгрузкиПоОбъектуРасчетов = ТаблицаОтгрузок.Строки.НайтиСтроки(Новый Структура("Документ, ОбъектРасчетов", Документ, ОбъектРасчетов));
			Если СтрокиОтгрузкиПоОбъектуРасчетов.Количество() > 0 Тогда
				ОтгрузкиПоОбъектуРасчетов = СтрокиОтгрузкиПоОбъектуРасчетов[0];
			КонецЕсли;
		КонецЕсли;
		
		ВыведенОбъектРасчетов = Ложь;
		Если ОплатыПоОбъектуРасчетов <> Неопределено Тогда
			Если ОплатыПоОбъектуРасчетов.Строки.Количество() > 0 Тогда
				ВывестиТаблицуОплатыОтгрузкиПоОбъектуРасчетов(ТаблицаОтчета, Макет, ОплатыПоОбъектуРасчетов, ВыводитьОтступ, Ложь, ЭтоРасчетыСКлиентом, Истина);
				ВыведенОбъектРасчетов = Истина;
				ЕстьДанные = Истина;
			КонецЕсли;
		КонецЕсли;
		Если ОтгрузкиПоОбъектуРасчетов <> Неопределено Тогда
			Если ОтгрузкиПоОбъектуРасчетов.Строки.Количество() > 0 Тогда
				ВывестиТаблицуОплатыОтгрузкиПоОбъектуРасчетов(ТаблицаОтчета, Макет, ОтгрузкиПоОбъектуРасчетов, ВыводитьОтступ, Истина, ЭтоРасчетыСКлиентом, Не ВыведенОбъектРасчетов);
				ЕстьДанные = Истина;
			КонецЕсли;
		КонецЕсли;
		ВыводитьОтступ = Истина;
	КонецЦикла;
	
	Возврат ЕстьДанные;
	
КонецФункции

// Выводит таблицу по оплате или отгрузке в табличный документ
// 
// Параметры:
//  ТаблицаОтчета - ТабличныйДокумент - Табличный документ для вывода
//  Макет - ТабличныйДокумент - Макет отчета
//  РасчетыПоДокументу - ДеревоЗначений - Данные для вывода в отчет
//  ВыводитьОтступ - Булево - Выводить отступ перед текущей таблицей
//  ЭтоОтгрузка - Булево - Выводится таблица оплаты или отгрузки
//  ЭтоРасчетыСКлиентом - Булево - Расчеты с клиентом или поставщиком
Процедура ВывестиТаблицуОплатыОтгрузкиПоОбъектуРасчетов(ТаблицаОтчета, Макет, РасчетыПоДокументу, ВыводитьОтступ, ЭтоОтгрузка, ЭтоРасчетыСКлиентом, ВывестиОбъектРасчетов)
	
	Если ВыводитьОтступ Тогда
		
		ИмяОбласти = "СтрокаТаблицыПоследняя";
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(РасчетыПоДокументу);
		ТаблицаОтчета.Вывести(Область);
		
	КонецЕсли;
	
	Если Не ЭтоОтгрузка Тогда
	
		ИмяОбласти = "ПорядокРасчетов";
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(РасчетыПоДокументу);
		ТаблицаОтчета.Вывести(Область);
		
	КонецЕсли;
	
	Если ВывестиОбъектРасчетов Тогда
		ИмяОбласти = "ОбъектРасчетов";
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(РасчетыПоДокументу);
		Область.Параметры.СтруктураПараметров = Новый Структура("Заказ, Действие", РасчетыПоДокументу.ОбъектРасчетов, "ОткрытьЗначение");
		ТаблицаОтчета.Вывести(Область);
	КонецЕсли;
	
	Если ЭтоОтгрузка Тогда
		Если ЭтоРасчетыСКлиентом Тогда
			ЗаголовокТаблицы = НСтр("ru = 'Отгрузки клиенту';
									|en = 'Shipments to customer'");
		Иначе
			ЗаголовокТаблицы = НСтр("ru = 'Поставки от поставщика';
									|en = 'Deliveries from vendor'");
		КонецЕсли;
	Иначе
		Если ЭтоРасчетыСКлиентом Тогда
			ЗаголовокТаблицы = НСтр("ru = 'Оплаты клиента';
									|en = 'Payments from customer'");
		Иначе
			ЗаголовокТаблицы = НСтр("ru = 'Оплаты поставщику';
									|en = 'Payments to vendor'");
		КонецЕсли;
	КонецЕсли; 
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	СтруктураЗаголовка = Новый Структура("Заголовок", ЗаголовокТаблицы);
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыРасчеты";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ЗаголовокОплатаОтгрузка = ?(
		ЭтоОтгрузка,
		?(ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка (факт)';
				|en = 'Shipment (actual)'"),
			НСтр("ru = 'Поставка (факт)';
				|en = 'Delivery (actual)'")),
		НСтр("ru = 'Оплата (факт)';
			|en = 'Payment (actual)'"));
	Область.Параметры.ЗаголовокЭтапОплатыОтгрузки = ?(
		ЭтоОтгрузка,
		?(ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Этап отгрузки';
				|en = 'Shipment step'"),
			НСтр("ru = 'Этап поставки';
				|en = 'Delivery step'")),
		НСтр("ru = 'Этап оплаты';
			|en = 'Payment milestone'"));
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыРасчеты";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ИмяОбластьСтроки = "ТаблицаРасчетыСтрока";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	
	Для каждого СтрокаЭтапаОплатыОтгрузки Из РасчетыПоДокументу.Строки Цикл
		
		// Если есть строки факта, то сразу в строке этапа оплаты/отгрузки указываем первую дату факт с суммой
		ОбъединитьСтрокуФактаСоСтрокойЭтапаОплатыОтгрузки = Ложь;
		// Остатки выводим в последней строке факта по этапу оплаты/отгрузки
		Остатки = Неопределено;
		Если СтрокаЭтапаОплатыОтгрузки.Строки.Количество() > 0 Тогда
			Остатки = Новый Структура;
			Остатки.Вставить("Остаток",           СтрокаЭтапаОплатыОтгрузки.Остаток);
			Остатки.Вставить("ОстатокПросрочено", СтрокаЭтапаОплатыОтгрузки.ОстатокПросрочено);
			Остатки.Вставить("ДнейПросрочено",    СтрокаЭтапаОплатыОтгрузки.ДнейПросрочено);
			ОбъединитьСтрокуФактаСоСтрокойЭтапаОплатыОтгрузки = Истина;
		Иначе
			
			ВывестиСтрокуРасчетов(Область, ОбластьСтроки, СтрокаЭтапаОплатыОтгрузки, ТаблицаОтчета, ЧетнаяСтрока, ЭтоРасчетыСКлиентом); 
			
		КонецЕсли;
		
		Для Каждого СтрокаФакта Из СтрокаЭтапаОплатыОтгрузки.Строки Цикл
			
			Если ОбъединитьСтрокуФактаСоСтрокойЭтапаОплатыОтгрузки Тогда 
				ЗаполнитьЗначенияСвойств(СтрокаФакта, СтрокаЭтапаОплатыОтгрузки, "СуммаПлан, КорректировкаГрафика");
				ОбъединитьСтрокуФактаСоСтрокойЭтапаОплатыОтгрузки = Ложь;
			КонецЕсли;
			
			Если СтрокаЭтапаОплатыОтгрузки.Строки.Индекс(СтрокаФакта) + 1 = СтрокаЭтапаОплатыОтгрузки.Строки.Количество() Тогда
				ЗаполнитьЗначенияСвойств(СтрокаФакта, Остатки);
			КонецЕсли;
			
			ВывестиСтрокуРасчетов(Область, ОбластьСтроки, СтрокаФакта, ТаблицаОтчета, ЧетнаяСтрока, ЭтоРасчетыСКлиентом);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИмяОбласти = "СтрокаТаблицыРасчетыИтого";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	Область.Параметры.Заполнить(РасчетыПоДокументу);
	Область.Параметры.СуммаПланИтого = РасчетыПоДокументу.СуммаПлан;
	Область.Параметры.СуммаФактИтого = РасчетыПоДокументу.СуммаФакт;
	Область.Параметры.КорректировкаГрафикаИтого = РасчетыПоДокументу.КорректировкаГрафика;
	Область.Параметры.ОстатокИтого = РасчетыПоДокументу.Остаток;
	Область.Параметры.ОстатокПросроченоИтого = РасчетыПоДокументу.ОстатокПросрочено;
	
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Процедура ВывестиСтрокуРасчетов(Область, ОбластьСтроки, СтрокаРасчетов, ТаблицаОтчета, ЧетнаяСтрока, ЭтоРасчетыСКлиентом)
	
	Если ЧетнаяСтрока Тогда
		ЦветФона = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	Иначе
		ЦветФона = ЦветаСтиля.ЦветФонаПоля;
	КонецЕсли;
	
	Область.Параметры.Заполнить(СтрокаРасчетов);
	Область.Параметры.ДнейПросрочено = ?(
		ЗначениеЗаполнено(СтрокаРасчетов.ДнейПросрочено),
		СтрШаблон("%1 %2",
			Формат(СтрокаРасчетов.ДнейПросрочено,"ЧН=; ЧГ="),
				ОбщегоНазначенияУТКлиентСервер.ФормаМножественногоЧисла(
					НСтр("ru = 'день';
						|en = 'Day'"),
					НСтр("ru = 'дня';
						|en = 'day'"),
					НСтр("ru = 'дней';
						|en = 'days'"),
			СтрокаРасчетов.ДнейПросрочено)),
		"");
	
	Если СтрокаРасчетов.ВариантОплатыОтгрузки = Перечисления.ВариантыКонтроляОплатыКлиентом.ПустаяСсылка()
		Или СтрокаРасчетов.ВариантОплатыОтгрузки = Перечисления.ВариантыКонтроляОплатыПоставщику.ПустаяСсылка() Тогда
		Область.Параметры.ВариантОплатыОтгрузки = ?(
		ЗначениеЗаполнено(СтрокаРасчетов.ДатаПлан),
		НСтр("ru = 'Оплата по графику накладной';
			|en = 'Payment according to the invoice schedule'"),
		НСтр("ru = 'Оплата сверх плана';
			|en = 'Payment in excess of the plan'"));
	ИначеЕсли ТипЗнч(СтрокаРасчетов.ВариантОплатыОтгрузки) = Тип("Число") Тогда 
		Если СтрокаРасчетов.ВариантОплатыОтгрузки = 1 Тогда
			Область.Параметры.ВариантОплатыОтгрузки = ?(
			ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка по графику погашения аванса';
				|en = 'Shipment according to the advance payment repayment schedule'"),
			НСтр("ru = 'Поставка по графику погашения аванса';
				|en = 'Delivery according to the advance payment repayment schedule'"));
		ИначеЕсли СтрокаРасчетов.ВариантОплатыОтгрузки = 2 Тогда
			Область.Параметры.ВариантОплатыОтгрузки = ?(
			ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка по графику исполнения договора';
				|en = 'Shipment under contract schedule'"),
			НСтр("ru = 'Поставка по графику исполнения договора';
				|en = 'Delivery under the contract schedule'"));
		ИначеЕсли СтрокаРасчетов.ВариантОплатыОтгрузки = 3 Тогда
			Область.Параметры.ВариантОплатыОтгрузки = ?(
			ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка по графику заказа';
				|en = 'Shipment under the order schedule'"),
			НСтр("ru = 'Поставка по графику заказа';
				|en = 'Delivery under the order schedule'"));
		ИначеЕсли СтрокаРасчетов.ВариантОплатыОтгрузки = 4 Тогда
			Область.Параметры.ВариантОплатыОтгрузки = ?(
			ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка сверх плана';
				|en = 'Shipment in excess of the plan'"),
			НСтр("ru = 'Поставка сверх плана';
				|en = 'Delivery in excess of the plan'"));
			// Для строки сверх заказа не выводим дату факт, так как там дата последней накладной,
			// не гарантированно той, в которой были строки сверх заказа.
			Область.Параметры.ДатаФакт = "";
		ИначеЕсли СтрокаРасчетов.ВариантОплатыОтгрузки = 5 Тогда
			Область.Параметры.ВариантОплатыОтгрузки = ?(
			ЭтоРасчетыСКлиентом,
			НСтр("ru = 'Отгрузка';
				|en = 'Shipment'"),
			НСтр("ru = 'Поставка';
				|en = 'Delivery'"));
		КонецЕсли;
	КонецЕсли;
	
	ОбластьСтроки.ЦветФона = ЦветФона;
	
	ТаблицаОтчета.Вывести(Область);
	ЧетнаяСтрока = НЕ ЧетнаяСтрока;

КонецПроцедуры

#КонецОбласти

Функция ТекстЗапросаТаблицаГрафикиОплаты(ИмяТаблицы, СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТаблицаГрафикиОплаты") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаРасчетыСКлиентами;
	КонецЕсли;
	
	Если СтруктураПараметров.ЭтоЗаказ Тогда
		
		СуммаПлатежа = "ДокументЭтапыГрафикаОплаты.СуммаПлатежа + ДокументЭтапыГрафикаОплаты.СуммаЗалогаЗаТару";
		
		//++ НЕ УТ
		Если ИмяТаблицы = "Документ.ЗаказПереработчику2_5"
			//++ Устарело_Переработка24
			Или ИмяТаблицы = "Документ.ЗаказПереработчику"
			//-- Устарело_Переработка24
			Или Ложь Тогда
			СуммаПлатежа = "ДокументЭтапыГрафикаОплаты.СуммаПлатежа";
		КонецЕсли; 
		//-- НЕ УТ
		
		//++ НЕ УТКА
		Если ИмяТаблицы = "Документ.ЗаказДавальца2_5"
			//++ Устарело_Переработка24
			Или ИмяТаблицы = "Документ.ЗаказДавальца"
			//-- Устарело_Переработка24
			Или Ложь Тогда
			СуммаПлатежа = "ДокументЭтапыГрафикаОплаты.СуммаПлатежа";
		КонецЕсли; 
		//-- НЕ УТКА
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументЭтапыГрафикаОплаты.Ссылка КАК Документ,
		|	ДокументЭтапыГрафикаОплаты.НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕ(ДокументЭтапыГрафикаОплаты.ВариантОплаты) КАК ВариантОплаты,
		|	ДокументЭтапыГрафикаОплаты.ДатаПлатежа,
		|	&СуммаПлатежа КАК СуммаПлатежа
		|ИЗ
		|	&ИмяТаблицы КАК ДокументЭтапыГрафикаОплаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО (ОбъектыРасчетов.ОбъектРасчетовОбъект = ДокументЭтапыГрафикаОплаты.Ссылка)
		|		И ОбъектыРасчетов.ТребуетсяГрафик";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СуммаПлатежа", СуммаПлатежа);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + ".ЭтапыГрафикаОплаты");
		
	ИначеЕсли СтруктураПараметров.ЭтоНакладная Тогда
		
		Если ЗначениеЗаполнено(СтруктураДопЗапросов)
			И СтруктураДопЗапросов.Свойство("ТаблицаГрафикОплатыПоНакладной") Тогда
			
			ТекстЗапроса = СтруктураДопЗапросов.ТаблицаГрафикОплатыПоНакладной;
			
		ИначеЕсли ИмяТаблицы = "Документ.РеализацияТоваровУслуг"
			ИЛИ ИмяТаблицы = "Документ.АктВыполненныхРабот" 
			ИЛИ ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг"
			
			//++ НЕ УТ
			ИЛИ ИмяТаблицы = "Документ.ОтчетПереработчика2_5"
			//-- НЕ УТ
			
			//++ НЕ УТКА

			//++ Устарело_Переработка24
			ИЛИ ИмяТаблицы = "Документ.ОтчетДавальцу"
			//-- Устарело_Переработка24
			ИЛИ ИмяТаблицы = "Документ.ОтчетДавальцу2_5"
			//-- НЕ УТКА
			Тогда
			
			ТекстЗапроса = "
			|ВЫБРАТЬ
			|	ДокументЭтапыГрафикаОплаты.Ссылка                       КАК Документ,
			|	ДокументЭтапыГрафикаОплаты.НомерСтроки                  КАК НомерСтроки,
			|	ПРЕДСТАВЛЕНИЕ(ДокументЭтапыГрафикаОплаты.ВариантОплаты) КАК ВариантОплаты,
			|	ДокументЭтапыГрафикаОплаты.ДатаПлатежа                  КАК ДатаПлатежа,
			|	ДокументЭтапыГрафикаОплаты.СуммаПлатежа                 КАК СуммаПлатежа
			|ИЗ
			|	&ИмяТаблицы КАК ДокументЭтапыГрафикаОплаты
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
			|		ПО (ОбъектыРасчетов.ОбъектРасчетовОбъект = ДокументЭтапыГрафикаОплаты.Ссылка)
			|		И ОбъектыРасчетов.ТребуетсяГрафик";

			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + ".ЭтапыГрафикаОплаты");
			
		Иначе
			
			ТекстЗапроса = "
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ДокументНакладная.Ссылка КАК Документ,
			|	1 КАК НомерСтроки,
			|	ПРЕДСТАВЛЕНИЕ(ЗНАЧЕНИЕ(Перечисление.ВариантыКонтроляОплатыКлиентом.КредитСдвиг))  КАК ВариантОплаты,
			|	ДокументНакладная.ДатаПлатежа КАК ДатаПлатежа,
			|	ДокументНакладная.СуммаДокумента КАК СуммаПлатежа
			|ИЗ
			|	&ИмяТаблицы КАК ДокументНакладная
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
			|		ПО (ОбъектыРасчетов.ОбъектРасчетовОбъект = ДокументНакладная.Ссылка)
			|		И ОбъектыРасчетов.ТребуетсяГрафик";

			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
		
		КонецЕсли;
		
		Если ИмяТаблицы = "Документ.РеализацияТоваровУслуг"
			ИЛИ ИмяТаблицы = "Документ.АктВыполненныхРабот" Тогда
			
			ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоГрафикуОплатыОбъектРасчетов("ЗаказКлиента");
			ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоГрафикуОплатыОбъектРасчетов("ЗаявкаНаВозвратТоваровОтКлиента");
			
		КонецЕсли;
		
		Если ИмяТаблицы = "Документ.ПриобретениеТоваровУслуг" Тогда
			
			ТекстЗапроса = ТекстЗапроса + ТекстЗапросаПоГрафикуОплатыОбъектРасчетов("ЗаказПоставщику");
			
		КонецЕсли;
		
	ИначеЕсли СтруктураПараметров.Свойство("ЭтоДоговор") И СтруктураПараметров.ЭтоДоговор Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДокументЭтапыГрафикаОплаты.Ссылка.Договор КАК Документ,
		|	ДокументЭтапыГрафикаОплаты.НомерСтроки,
		|	ДокументЭтапыГрафикаОплаты.ВариантОплаты КАК ВариантОплаты,
		|	ДокументЭтапыГрафикаОплаты.ДатаПлатежа КАК ДатаПлатежа,
		|	ДокументЭтапыГрафикаОплаты.СуммаПлатежа КАК СуммаПлатежа
		|ИЗ
		|	&ИмяТаблицы КАК ДокументЭтапыГрафикаОплаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО ОбъектыРасчетов.ОбъектРасчетовОбъект = ДокументЭтапыГрафикаОплаты.Ссылка.Договор
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы + ".ЭтапыГрафикаОплаты");
		
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИТОГИ ПО
	|	Ссылка;";
	
	Возврат ТекстЗапроса

КонецФункции

Функция ТекстЗапросаПоГрафикуОплатыОбъектРасчетов(ИмяДокумента)

	Если Не ПравоДоступа("Чтение", Метаданные.Документы.Найти(ИмяДокумента)) Тогда
		Возврат "";
	КонецЕсли;
	
	ТекстЗапроса = "
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДокументЭтапыГрафикаОплаты.Ссылка КАК Документ,
	|	ДокументЭтапыГрафикаОплаты.НомерСтроки,
	|	ПРЕДСТАВЛЕНИЕ(ДокументЭтапыГрафикаОплаты.ВариантОплаты) КАК ВариантОплаты,
	|	ДокументЭтапыГрафикаОплаты.ДатаПлатежа,
	|	ДокументЭтапыГрафикаОплаты.СуммаПлатежа + ДокументЭтапыГрафикаОплаты.СуммаЗалогаЗаТару КАК СуммаПлатежа
	|ИЗ
	|	&ИмяТаблицы КАК ДокументЭтапыГрафикаОплаты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО (ОбъектыРасчетов.ОбъектРасчетовОбъект = ДокументЭтапыГрафикаОплаты.Ссылка)
	|		И ОбъектыРасчетов.ТребуетсяГрафик";
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы","Документ." + ИмяДокумента + ".ЭтапыГрафикаОплаты");
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеПоРасчетамСКлиентами(ИмяТаблицы, СтруктураПараметров)
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ВтДанныеПоРасчетамСКлиентами") Тогда
		
		Возврат СтруктураДопЗапросов.ВтДанныеПоРасчетамСКлиентами;
	КонецЕсли;
	
	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеПоРасчетам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеПоРасчетам.Период КАК Период,
	|	СУММА(ДанныеПоРасчетам.ОплатаПлан) КАК ОплатаПлан,
	|	СУММА(ДанныеПоРасчетам.ОплатаФакт) КАК ОплатаФакт
	|ПОМЕСТИТЬ ВтДанныеПоРасчетамСКлиентами
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСКлиентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|		НАЧАЛОПЕРИОДА(ВЫБОР КОГДА РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|					ТОГДА РасчетыСКлиентами.ДатаРегистратора
	|					ИНАЧЕ РасчетыСКлиентами.Период
	|					КОНЕЦ, ДЕНЬ) КАК Период,
	|		РасчетыСКлиентами.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		ВЫБОР
	|			КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И НЕ РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|			И НЕ РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
	|			И НЕ РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|				ТОГДА РасчетыСКлиентами.КОплате
	|			КОГДА РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|				И НЕ РасчетыСКлиентами.ОбъектРасчетов.Объект = РасчетыСКлиентами.Регистратор
	|				И РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И РасчетыСКлиентами.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|					ТОГДА -РасчетыСКлиентами.КОплате
	|			КОГДА РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиенту)
	|					ТОГДА ВЫБОР
	|							КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|							ТОГДА РасчетыСКлиентами.КОплате
	|							ИНАЧЕ -РасчетыСКлиентами.КОплате
	|					КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОплатаПлан,
	|		ВЫБОР
	|			КОГДА РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
	|				ТОГДА ВЫБОР
	|					КОГДА РасчетыСКлиентами.КорОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|						И РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА -РасчетыСКлиентами.Сумма
	|					КОГДА РасчетыСКлиентами.КорОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|						И РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						ТОГДА РасчетыСКлиентами.Сумма
	|					ИНАЧЕ РасчетыСКлиентами.Сумма
	|				КОНЕЦ
	|			ИНАЧЕ ВЫБОР
	|					КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ИЛИ РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРеализации
	|						ИЛИ РасчетыСКлиентами.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
	|					ТОГДА 0
	|					ИНАЧЕ РасчетыСКлиентами.Сумма
	|				КОНЕЦ
	|		КОНЕЦ КАК ОплатаФакт
	|	ИЗ
	|		ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК СправочникОбъектыРасчетов
	|			ПО ОбъектыРасчетов.ОбъектРасчетовОбъект = СправочникОбъектыРасчетов.Объект
	|			И НЕ СправочникОбъектыРасчетов.ПометкаУдаления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|		ПО СправочникОбъектыРасчетов.Ссылка = РасчетыСКлиентами.ОбъектРасчетов
	|	ГДЕ 
	|		РасчетыСКлиентами.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
	|	) КАК ДанныеПоРасчетам
	|
	|	СГРУППИРОВАТЬ ПО
	|	ДанныеПоРасчетам.Период,
	|	ДанныеПоРасчетам.ОбъектРасчетов
	|;";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСКлиентами(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаРасчетыСКлиентами") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаРасчетыСКлиентами;
	КонецЕсли;
	
	ТекстЗапроса = "
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОбъектыРасчетов.Документ КАК Документ,
		|	РасчетыСКлиентами.ОбъектРасчетов.Объект КАК ОбъектРасчетов,
		|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
		|	РасчетыСКлиентами.Валюта КАК Валюта,
		|	ОбъектыРасчетов.ТребуетсяГрафик КАК ТребуетсяГрафик,
		|	ВЫБОР
		|		КОГДА ДанныПоРасчетамПоХО.ОплатаПлан > 0
		|			ТОГДА ВЫРАЗИТЬ(""ОплатаПоГрафику"" КАК СТРОКА(150))
		|		КОГДА ДанныПоРасчетамПоХО.ОплатаПлан < 0
		|			ТОГДА ВЫРАЗИТЬ(""ПереносАванса"" КАК СТРОКА(150))
		|		ИНАЧЕ ВЫБОР
		|			КОГДА ДанныПоРасчетамПоХО.ОплатаФакт <> 0
		|				ТОГДА ВЫРАЗИТЬ(""ОплатаНеПоГрафику"" КАК СТРОКА(150))
		|			ИНАЧЕ ВЫРАЗИТЬ(""Отгрузка"" КАК СТРОКА(150))
		|		КОНЕЦ
		|	КОНЕЦ КАК ЭтапОплатыОтгрузки,
		|	РасчетыСКлиентами.Период КАК ДатаПлатежа,
		|	ВЫБОР КОГДА ОбъектыРасчетов.ТребуетсяГрафик
		|			ТОГДА ЕСТЬNULL(ДанныПоРасчетамПоХО.ОплатаПлан, 0)
		|			ИНАЧЕ 0
		|	КОНЕЦ КАК ОплатаПлан,
		|	ЕСТЬNULL(ДанныПоРасчетамПоХО.ОплатаФакт, 0) КАК ОплатаФакт,
		|	ВЫБОР
		|		КОГДА РасчетыСКлиентами.Период < &ТекущаяДата
		|		И РасчетыСКлиентами.КОплатеКонечныйОстаток > 0
		|			ТОГДА РасчетыСКлиентами.КОплатеКонечныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОплатаПросрочено,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(РасчетыСКлиентамиОстатки.КОплатеОстаток, 0) - ЕСТЬNULL(РасчетыСКлиентамиОбороты.КОплатеРасход, 0) > 0
		|			ТОГДА ЕСТЬNULL(РасчетыСКлиентамиОстатки.КОплатеОстаток, 0) - ЕСТЬNULL(РасчетыСКлиентамиОбороты.КОплатеРасход, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОплатаПросроченоИтого,
		|	РасчетыСКлиентами.КОтгрузкеРасход КАК ОтгрузкаПлан,
		|	РасчетыСКлиентами.КОтгрузкеПриход КАК ОтгрузкаФакт,
		|	ВЫБОР
		|		КОГДА РасчетыСКлиентами.Период < &ТекущаяДата
		|		И РасчетыСКлиентами.КОтгрузкеКонечныйОстаток < 0
		|			ТОГДА -РасчетыСКлиентами.КОтгрузкеКонечныйОстаток
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОтгрузкаПросрочено,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(-РасчетыСКлиентамиОстатки.КОтгрузкеОстаток, 0) - ЕСТЬNULL(РасчетыСКлиентамиОбороты.КОтгрузкеПриход,
		|			0) > 0
		|			ТОГДА ЕСТЬNULL(-РасчетыСКлиентамиОстатки.КОтгрузкеОстаток, 0) - ЕСТЬNULL(РасчетыСКлиентамиОбороты.КОтгрузкеПриход,
		|				0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ОтгрузкаПросроченоИтого
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами.ОстаткиИОбороты(
		|			,
		|			,
		|			День,
		|			,
		|			ОбъектРасчетов В
		|				(ВЫБРАТЬ
		|					ОбъектыРасчетов.ОбъектРасчетов
		|				ИЗ
		|					ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСКлиентами
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ПО (ОбъектыРасчетов.ОбъектРасчетов = РасчетыСКлиентами.ОбъектРасчетов)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Остатки(
		|				&ТекущаяДата,
		|				ОбъектРасчетов В
		|					(ВЫБРАТЬ
		|						ОбъектыРасчетов.ОбъектРасчетов
		|					ИЗ
		|						ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСКлиентамиОстатки
		|		ПО РасчетыСКлиентами.ОбъектРасчетов = РасчетыСКлиентамиОстатки.ОбъектРасчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами.Обороты(
		|				&ТекущаяДата,
		|				,
		|				,
		|				ОбъектРасчетов В
		|					(ВЫБРАТЬ
		|						ОбъектыРасчетов.ОбъектРасчетов
		|					ИЗ
		|						ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСКлиентамиОбороты
		|		ПО (РасчетыСКлиентами.ОбъектРасчетов = РасчетыСКлиентамиОстатки.ОбъектРасчетов)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеПоРасчетамСКлиентами КАК ДанныПоРасчетамПоХО
		|		ПО РасчетыСКлиентами.ОбъектРасчетов = ДанныПоРасчетамПоХО.ОбъектРасчетов
		|		И РасчетыСКлиентами.Период = ДанныПоРасчетамПоХО.Период
		|УПОРЯДОЧИТЬ ПО
		|	РасчетыСКлиентами.Период
		|ИТОГИ
		|	МАКСИМУМ(ПорядокРасчетов),
		|	МАКСИМУМ(Валюта),
		|	МАКСИМУМ(ТребуетсяГрафик),
		|	СУММА(ОплатаПлан),
		|	СУММА(ОплатаФакт),
		|	МАКСИМУМ(ОплатаПросроченоИтого),
		|	СУММА(ОтгрузкаПлан),
		|	СУММА(ОтгрузкаФакт),
		|	МАКСИМУМ(ОтгрузкаПросроченоИтого)
		|ПО
		|	Документ,
		|	ОбъектРасчетов
		|;";


	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВтДанныеПоРасчетамСПоставщиками(ИмяТаблицы, СтруктураПараметров)
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ВтДанныеПоРасчетамСПоставщиками") Тогда
		
		Возврат СтруктураДопЗапросов.ВтДанныеПоРасчетамСПоставщиками;
	КонецЕсли;
	
	ТекстЗапроса = "
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДанныеПоРасчетам.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ДанныеПоРасчетам.Период КАК Период,
		|	СУММА(ДанныеПоРасчетам.ОплатаПлан) КАК ОплатаПлан,
		|	СУММА(ДанныеПоРасчетам.ОплатаФакт) КАК ОплатаФакт
		|ПОМЕСТИТЬ ВтДанныеПоРасчетамСПоставщиками
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
		|		НАЧАЛОПЕРИОДА(ВЫБОР КОГДА РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|					ТОГДА РасчетыСПоставщиками.ДатаРегистратора
		|					ИНАЧЕ РасчетыСПоставщиками.Период
		|				КОНЕЦ, ДЕНЬ) КАК Период,
		|		ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И НЕ РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
		|			И НЕ РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|				ТОГДА РасчетыСПоставщиками.КОплате
		|			КОГДА РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
		|				И НЕ РасчетыСПоставщиками.ОбъектРасчетов.Объект = РасчетыСПоставщиками.Регистратор
		|				И РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|					ТОГДА -РасчетыСПоставщиками.КОплате
		|			КОГДА РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика)
		|					ТОГДА ВЫБОР
		|							КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|							ТОГДА РасчетыСПоставщиками.КОплате
		|							ИНАЧЕ -РасчетыСПоставщиками.КОплате
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ОплатаПлан,
		|		ВЫБОР
		|			КОГДА РасчетыСПоставщиками.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносАванса)
		|				ТОГДА ВЫБОР
		|						КОГДА НЕ РасчетыСПоставщиками.КорОбъектРасчетов = ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|							ТОГДА РасчетыСПоставщиками.Сумма
		|							ИНАЧЕ -РасчетыСПоставщиками.Сумма
		|						КОНЕЦ
		|				ИНАЧЕ ВЫБОР
		|						КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|							ИЛИ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаПриобретения
		|							ТОГДА 0
		|							ИНАЧЕ РасчетыСПоставщиками.Сумма
		|						КОНЕЦ
		|			КОНЕЦ КАК ОплатаФакт
		|	ИЗ
		|		ОбъектыРасчетов КАК ОбъектыРасчетов
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК СправочникОбъектыРасчетов
		|			ПО ОбъектыРасчетов.ОбъектРасчетовОбъект = СправочникОбъектыРасчетов.Объект
		|			И НЕ СправочникОбъектыРасчетов.ПометкаУдаления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|		ПО СправочникОбъектыРасчетов.Ссылка = РасчетыСПоставщиками.ОбъектРасчетов
		|	ГДЕ 
		|		РасчетыСПоставщиками.ХозяйственнаяОперация <> ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПереносПлатежаМеждуФилиалами)
		|	) КАК ДанныеПоРасчетам
		|	СГРУППИРОВАТЬ ПО
		|	ДанныеПоРасчетам.Период,
		|	ДанныеПоРасчетам.ОбъектРасчетов
		|;";
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицаРасчетыСПоставщиками(СтруктураПараметров)
	
	СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
	
	Если ЗначениеЗаполнено(СтруктураДопЗапросов)
		И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаРасчетыСПоставщиками") Тогда
		
		Возврат СтруктураДопЗапросов.ТекстЗапросаТаблицаРасчетыСПоставщиками;
	КонецЕсли;
	
	ТекстЗапроса = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектыРасчетов.Документ КАК Документ,
	|	РасчетыСПоставщиками.ОбъектРасчетов.Объект КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.ПорядокРасчетов КАК ПорядокРасчетов,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	ОбъектыРасчетов.ТребуетсяГрафик,
	|	ВЫБОР
	|		КОГДА ДанныПоРасчетамПоХО.ОплатаПлан > 0
	|			ТОГДА ВЫРАЗИТЬ(""ОплатаПоГрафику"" КАК СТРОКА(150))
	|		КОГДА ДанныПоРасчетамПоХО.ОплатаПлан < 0
	|			ТОГДА ВЫРАЗИТЬ(""ПереносАванса"" КАК СТРОКА(150))
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ДанныПоРасчетамПоХО.ОплатаФакт <> 0
	|				ТОГДА ВЫРАЗИТЬ(""ОплатаНеПоГрафику"" КАК СТРОКА(150))
	|			ИНАЧЕ ВЫРАЗИТЬ(""Поступление"" КАК СТРОКА(150))
	|		КОНЕЦ
	|	КОНЕЦ КАК ЭтапОплатыОтгрузки,
	|	РасчетыСПоставщиками.Период КАК ДатаПлатежа,
	|	ЕСТЬNULL(ДанныПоРасчетамПоХО.ОплатаПлан, 0) КАК ОплатаПлан,
	|	ЕСТЬNULL(ДанныПоРасчетамПоХО.ОплатаФакт, 0) КАК ОплатаФакт,
	|	ВЫБОР
	|		КОГДА РасчетыСПоставщиками.Период < &ТекущаяДата
	|				И РасчетыСПоставщиками.КОплатеКонечныйОстаток < 0
	|			ТОГДА -РасчетыСПоставщиками.КОплатеКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОплатаПросрочено,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РасчетыСПоставщикамиОбороты.КОплатеПриход, 0) + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.КОплатеОстаток, 0) < 0
	|			ТОГДА -(ЕСТЬNULL(РасчетыСПоставщикамиОбороты.КОплатеПриход, 0) + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.КОплатеОстаток, 0))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОплатаПросроченоИтого,
	|	РасчетыСПоставщиками.КПоступлениюРасход КАК ОтгрузкаФакт,
	|	РасчетыСПоставщиками.КПоступлениюПриход КАК ОтгрузкаПлан,
	|	ВЫБОР
	|		КОГДА РасчетыСПоставщиками.Период < &ТекущаяДата
	|				И РасчетыСПоставщиками.КПоступлениюКонечныйОстаток > 0
	|			ТОГДА РасчетыСПоставщиками.КПоступлениюКонечныйОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ОтгрузкаПросрочено,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.КПоступлениюОстаток, 0) - ЕСТЬNULL(РасчетыСПоставщикамиОбороты.КПоступлениюРасход, 0) > 0 ТОГДА
	|			ЕСТЬNULL(РасчетыСПоставщикамиОстатки.КПоступлениюОстаток, 0) - ЕСТЬNULL(РасчетыСПоставщикамиОбороты.КПоступлениюРасход, 0)
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК ОтгрузкаПросроченоИтого
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.ОстаткиИОбороты(
	|			,
	|			,
	|			День,
	|			,
	|			ОбъектРасчетов В
	|				(ВЫБРАТЬ
	|					ОбъектыРасчетов.ОбъектРасчетов
	|				ИЗ
	|					ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСПоставщиками
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО (ОбъектыРасчетов.ОбъектРасчетов = РасчетыСПоставщиками.ОбъектРасчетов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|				&ТекущаяДата,
	|				ОбъектРасчетов В
	|					(ВЫБРАТЬ
	|						ОбъектыРасчетов.ОбъектРасчетов
	|					ИЗ
	|						ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСПоставщикамиОстатки
	|		ПО РасчетыСПоставщиками.ОбъектРасчетов = РасчетыСПоставщикамиОстатки.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Обороты(
	|				&ТекущаяДата,
	|				,
	|				,
	|				ОбъектРасчетов В
	|					(ВЫБРАТЬ
	|						ОбъектыРасчетов.ОбъектРасчетов
	|					ИЗ
	|						ОбъектыРасчетов КАК ОбъектыРасчетов)) КАК РасчетыСПоставщикамиОбороты
	|		ПО РасчетыСПоставщиками.ОбъектРасчетов = РасчетыСПоставщикамиОстатки.ОбъектРасчетов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеПоРасчетамСПоставщиками КАК ДанныПоРасчетамПоХО
	|		ПО РасчетыСПоставщиками.ОбъектРасчетов = ДанныПоРасчетамПоХО.ОбъектРасчетов
	|		И РасчетыСПоставщиками.Период = ДанныПоРасчетамПоХО.Период
	|УПОРЯДОЧИТЬ ПО
	|	РасчетыСПоставщиками.Период
	|ИТОГИ
	|	МАКСИМУМ(Валюта),
	|	МАКСИМУМ(ПорядокРасчетов),
	|	СУММА(ОплатаПлан),
	|	СУММА(ОплатаФакт),
	|	МАКСИМУМ(ОплатаПросроченоИтого),
	|	СУММА(ОтгрузкаПлан),
	|	СУММА(ОтгрузкаФакт),
	|	МАКСИМУМ(ОтгрузкаПросроченоИтого),
	|	МАКСИМУМ(ТребуетсяГрафик)
	|ПО
	|	Документ,
	|	ОбъектРасчетов
	|;";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ВывестиТаблицуРасчетов(ТаблицаОтчета, Макет, Документ, ТаблицыРезультатов, ИмяТаблицы)
	Перем ТаблицаРасчетов;
	
	Если НЕ ТаблицыРезультатов.Свойство(ИмяТаблицы, ТаблицаРасчетов) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗадолженностьПоДокументу = ТаблицаРасчетов.Строки.Найти(Документ, "Документ");
	Если ЗадолженностьПоДокументу = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоОтгрузка = (ИмяТаблицы = "ТаблицаРасчетыСКлиентами");
	
	ВыводитьОтступ = Ложь;
	Для каждого ЗадолженностьПоОбъектуРасчетов Из ЗадолженностьПоДокументу.Строки Цикл 
		
		ТребуетсяГрафик = ЗадолженностьПоОбъектуРасчетов.ТребуетсяГрафик;
		
		Если ТребуетсяГрафик Тогда
			
			Если НЕ ТаблицыРезультатов.Свойство("ТаблицаГрафикиОплаты") Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ГрафикОплаты = ТаблицыРезультатов.ТаблицаГрафикиОплаты.Строки.Найти(ЗадолженностьПоОбъектуРасчетов.ОбъектРасчетов, "Документ");
			Если ГрафикОплаты = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ДатаПредыдущегоЭтапа = Дата(1,1,1);
			ИмяЭтапа = "";
			ПлановыеПлатежиНаДень = 0;
			
			Для Каждого ЭтапГрафикаОплаты Из ГрафикОплаты.Строки Цикл
				
				ДатаПлатежа = НачалоДня(ЭтапГрафикаОплаты.ДатаПлатежа);
				
				Если ДатаПредыдущегоЭтапа = ДатаПлатежа Тогда
					ПлановыеПлатежиНаДень = ПлановыеПлатежиНаДень + ЭтапГрафикаОплаты.СуммаПлатежа;
					Если СтрЧислоВхождений(ИмяЭтапа, ЭтапГрафикаОплаты.ВариантОплаты) = 0 Тогда
						ИмяЭтапа  = ?(ПустаяСтрока(ИмяЭтапа), ЭтапГрафикаОплаты.ВариантОплаты, ИмяЭтапа + Символы.ПС + ЭтапГрафикаОплаты.ВариантОплаты);
					КонецЕсли;
				Иначе
					ПлановыеПлатежиНаДень = ЭтапГрафикаОплаты.СуммаПлатежа;
					ИмяЭтапа = ЭтапГрафикаОплаты.ВариантОплаты;
				КонецЕсли;
				ДатаПредыдущегоЭтапа = ДатаПлатежа;
				
				НайденныеСтрокиРасчетов = ЗадолженностьПоОбъектуРасчетов.Строки.НайтиСтроки(Новый Структура("ДатаПлатежа, ОплатаПлан",
				                                                                            ДатаПлатежа,
				                                                                            ПлановыеПлатежиНаДень));
				
				Если НайденныеСтрокиРасчетов.Количество() > 0 Тогда
					Для Каждого НайденнаяСтрокаРасчетов Из НайденныеСтрокиРасчетов Цикл
						Если СокрЛП(НайденнаяСтрокаРасчетов.ЭтапОплатыОтгрузки) = "ОплатаПоГрафику" Тогда
							НайденнаяСтрокаРасчетов.ЭтапОплатыОтгрузки = ИмяЭтапа;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			РаспределитьОплатыПоГрафику(ЗадолженностьПоОбъектуРасчетов.Строки, ГрафикОплаты.Строки);
			
		КонецЕсли;
		
		ВывестиТаблицуРасчетовПоОбъектуРасчетов(ТаблицаОтчета, Макет, ЗадолженностьПоОбъектуРасчетов, ВыводитьОтступ, ЭтоОтгрузка);
		ВыводитьОтступ = Истина;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

Процедура ВывестиТаблицуРасчетовПоОбъектуРасчетов(ТаблицаОтчета, Макет, ЗадолженностьПоДокументу, ВыводитьОтступ, ЭтоОтгрузка)
	
	Если ВыводитьОтступ Тогда
		
		ИмяОбласти = "СтрокаТаблицыПоследняя";
		Область = Макет.ПолучитьОбласть(ИмяОбласти);
		Область.Параметры.Заполнить(ЗадолженностьПоДокументу);
		ТаблицаОтчета.Вывести(Область);
		
	КонецЕсли;
	
	ИмяОбласти = "ПорядокРасчетов";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.Заполнить(ЗадолженностьПоДокументу);
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "ОбъектРасчетов";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.Заполнить(ЗадолженностьПоДокументу);
	Область.Параметры.СтруктураПараметров = Новый Структура("Заказ, Действие", ЗадолженностьПоДокументу.ОбъектРасчетов, "ОткрытьЗначение");
	ТаблицаОтчета.Вывести(Область);
	
	Если ЭтоОтгрузка Тогда
		ЗаголовокТаблицы = НСтр("ru = 'Задолженность клиента';
								|en = 'Customer AR/AP open items'");
	Иначе
		ЗаголовокТаблицы = НСтр("ru = 'Наша задолженность';
								|en = 'Payments / Invoices reconciliation'");
	КонецЕсли; 
	
	ИмяОбласти = "ЗаголовокТаблицы";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	СтруктураЗаголовка = Новый Структура("Заголовок", ЗаголовокТаблицы);
	Область.Параметры.Заполнить(СтруктураЗаголовка);
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.НачатьГруппуСтрок();
	
	ИмяОбласти = "ШапкаТаблицыЗадолженность";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	Область.Параметры.ЗаголовокОтгрузкаПоступление = ?(ЭтоОтгрузка, НСтр("ru = 'Отгрузка';
																		|en = 'Shipment'"), НСтр("ru = 'Поступление';
																									|en = 'Receipt'"));
	Область.Параметры.ЗаголовокЭтапОплаты = ?(ЭтоОтгрузка, НСтр("ru = 'Этап оплаты/отгрузки';
																|en = 'Payment/shipment step'"), НСтр("ru = 'Этап оплаты/поступления';
																									|en = 'Payment/receipt step'"));
	ТаблицаОтчета.Вывести(Область);
	
	ИмяОбласти = "СтрокаТаблицыЗадолженность";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	ИмяОбластьСтроки = "ТаблицаЗадолженностьСтрока";
	ОбластьСтроки = Область.Области[ИмяОбластьСтроки]; // ОбластьЯчеекТабличногоДокумента
	
	ЧетнаяСтрока = Ложь;
	АльтернативныйЦветФонаПоля = ЦветаСтиля.АльтернативныйЦветФонаПоля;
	ЦветФонаПоля               = ЦветаСтиля.ЦветФонаПоля;
	
	Для каждого СтрокаЗадолженность Из ЗадолженностьПоДокументу.Строки Цикл
		
		Область.Параметры.Заполнить(СтрокаЗадолженность);
		Область.Параметры.ЭтапОплатыОтгрузки = ЭтапОплатыОтгрузкиСтрокой(СтрокаЗадолженность.ЭтапОплатыОтгрузки);
		
		ОбластьСтроки.ЦветФона = ?(ЧетнаяСтрока, АльтернативныйЦветФонаПоля, ЦветФонаПоля);
		
		ТаблицаОтчета.Вывести(Область);
		ЧетнаяСтрока = НЕ ЧетнаяСтрока;
		
	КонецЦикла; 
	
	ИмяОбласти = "СтрокаТаблицыЗадолженностьИтого";
	Область = Макет.ПолучитьОбласть(ИмяОбласти);
	
	Область.Параметры.Заполнить(ЗадолженностьПоДокументу);
	Область.Параметры.ОплатаПланИтого = ЗадолженностьПоДокументу.ОплатаПлан;
	Область.Параметры.ОплатаФактИтого = ЗадолженностьПоДокументу.ОплатаФакт;
	Область.Параметры.ОтгрузкаПланИтого = ЗадолженностьПоДокументу.ОтгрузкаПлан;
	Область.Параметры.ОтгрузкаФактИтого = ЗадолженностьПоДокументу.ОтгрузкаФакт;
	
	ТаблицаОтчета.Вывести(Область);
	
	ТаблицаОтчета.ЗакончитьГруппуСтрок();
	
КонецПроцедуры

Процедура РаспределитьОплатыПоГрафику(Оплаты, ЭтапыГрафика)
	
	КоличествоЭтаповГрафикаОплаты = ЭтапыГрафика.Количество();
	Если КоличествоЭтаповГрафикаОплаты = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтрокСФактическимиОплатами = Новый Массив;
	ЕстьОплатыНеПоГрафику = Ложь;
	Для Каждого Оплата Из Оплаты Цикл
		Если Оплата.ОплатаФакт <> 0 Тогда
			МассивСтрокСФактическимиОплатами.Добавить(Оплата);
			Если Оплата.ЭтапОплатыОтгрузки = "ОплатаНеПоГрафику" Тогда
				ЕстьОплатыНеПоГрафику = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЕстьОплатыНеПоГрафику Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрокаГрафикаОплат = 0;
	Для Каждого СтрокаСФактическойОплатой Из МассивСтрокСФактическимиОплатами Цикл
		
		Если ТекущаяСтрокаГрафикаОплат > КоличествоЭтаповГрафикаОплаты - 1 Тогда
			Возврат;
		КонецЕсли;
		
		Если СтрокаСФактическойОплатой.ОплатаФакт = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа Тогда
			
			Если СокрЛП(СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки) = "ОплатаНеПоГрафику" Тогда
				СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты;
			КонецЕсли;
			
			ТекущаяСтрокаГрафикаОплат = ТекущаяСтрокаГрафикаОплат + 1;
			
		ИначеЕсли СтрокаСФактическойОплатой.ОплатаФакт < ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа Тогда
			
			Если СокрЛП(СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки) = "ОплатаНеПоГрафику" Тогда
				СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты;
			КонецЕсли;
			ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа - СтрокаСФактическойОплатой.ОплатаФакт;
			
		Иначе
			
			ОсталосьРаспределить = СтрокаСФактическойОплатой.ОплатаФакт;
			МассивВидовОплатыПоПлатежу = Новый Массив;
			
			Пока ОсталосьРаспределить >= ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа Цикл
				ОсталосьРаспределить = ОсталосьРаспределить - ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа;
				
				ВариантОплатыЭтапа = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты;
				Если МассивВидовОплатыПоПлатежу.Найти(ВариантОплатыЭтапа) = Неопределено Тогда
					МассивВидовОплатыПоПлатежу.Добавить(ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты);
				КонецЕсли;
				 
				ТекущаяСтрокаГрафикаОплат = ТекущаяСтрокаГрафикаОплат + 1;
				Если ТекущаяСтрокаГрафикаОплат > КоличествоЭтаповГрафикаОплаты - 1 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если ОсталосьРаспределить > 0 И (НЕ ТекущаяСтрокаГрафикаОплат > КоличествоЭтаповГрафикаОплаты - 1) Тогда
				ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].СуммаПлатежа - ОсталосьРаспределить;
				ВариантОплатыЭтапа = ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты;
				Если МассивВидовОплатыПоПлатежу.Найти(ВариантОплатыЭтапа) = Неопределено Тогда
					МассивВидовОплатыПоПлатежу.Добавить(ЭтапыГрафика[ТекущаяСтрокаГрафикаОплат].ВариантОплаты);
				КонецЕсли;
			КонецЕсли;
			
			Если МассивВидовОплатыПоПлатежу.Количество() = 1
				И СокрЛП(СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки) = "ОплатаНеПоГрафику" Тогда
				
				СтрокаСФактическойОплатой.ЭтапОплатыОтгрузки = МассивВидовОплатыПоПлатежу[0];
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ЭтапОплатыОтгрузкиСтрокой(ИдентификаторЭтапаОплатыОтгрузки)
	
	Если ИдентификаторЭтапаОплатыОтгрузки = "ОплатаПоГрафику" Тогда
		Возврат НСтр("ru = 'Оплата по графику';
					|en = 'Payment on schedule'");
	ИначеЕсли ИдентификаторЭтапаОплатыОтгрузки = "ОплатаНеПоГрафику" Тогда
		Возврат НСтр("ru = 'Оплата не по графику';
					|en = 'Not scheduled payment'");
	ИначеЕсли ИдентификаторЭтапаОплатыОтгрузки = "Отгрузка" Тогда
		Возврат НСтр("ru = 'Отгрузка';
					|en = 'Shipment'");
	ИначеЕсли ИдентификаторЭтапаОплатыОтгрузки = "Поступление" Тогда
		Возврат НСтр("ru = 'Поступление';
					|en = 'Receipt'");
	ИначеЕсли ИдентификаторЭтапаОплатыОтгрузки = "ПереносАванса" Тогда
		Возврат НСтр("ru = 'Перенос аванса';
					|en = 'Prepayment offset'");
	Иначе
		Возврат ИдентификаторЭтапаОплатыОтгрузки;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ЕстьПравоДоступаКНаборуДанных(ИмяНабораДанных, СтруктураПараметров)
	
	ЕстьПравоДоступа = Ложь;
	
	Если ИмяНабораДанных = "РасчетыСКлиентами" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСКлиентами);
		
	ИначеЕсли ИмяНабораДанных = "ВозвратыТоваров" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат);
		ЕстьПравоДоступа = ЕстьПравоДоступа И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
		
	ИначеЕсли ИмяНабораДанных = "РасчетыСПоставщиками" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РасчетыСПоставщиками);
		
	ИначеЕсли ИмяНабораДанных = "Отгрузка" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКОтгрузке);
		СтруктураПараметров.Вставить("ЕстьПравоНаТаблицуОтгрузки", ЕстьПравоДоступа);
		
		Если СтруктураПараметров.ИмяРегистраОтгрузкаУслуг = "РаспоряженияНаОтгрузкуИВозврат" Тогда
			
			ЕстьПравоДоступаРегистрОтгрузкаУслуг = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат);
			ЕстьПравоДоступа = ЕстьПравоДоступа Или ЕстьПравоДоступаРегистрОтгрузкаУслуг;
			СтруктураПараметров.Вставить("ЕстьПравоДоступаРегистрОтгрузкаУслуг", ЕстьПравоДоступаРегистрОтгрузкаУслуг);
			
		ИначеЕсли СтруктураПараметров.ИмяРегистраОтгрузкаУслуг = "ЗаказыНаВнутреннееПотребление" Тогда
			
			ЕстьПравоДоступаРегистрОтгрузкаУслуг = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыНаВнутреннееПотребление);
			ЕстьПравоДоступа = ЕстьПравоДоступа Или ЕстьПравоДоступаРегистрОтгрузкаУслуг;
			СтруктураПараметров.Вставить("ЕстьПравоДоступаРегистрОтгрузкаУслуг", ЕстьПравоДоступаРегистрОтгрузкаУслуг);
			
		КонецЕсли;
		
	ИначеЕсли ИмяНабораДанных = "Поступление" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКПоступлению);
		СтруктураПараметров.Вставить("ЕстьПравоНаТаблицуПоступления", ЕстьПравоДоступа);
		
		ЕстьПравоДоступаЗаказыПоставщикам = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыПоставщикам);
		СтруктураПараметров.Вставить("ЕстьПравоДоступаЗаказыПоставщикам", ЕстьПравоДоступаЗаказыПоставщикам);
		
		Если СтруктураПараметров.ИмяРегистраПоступлениеУслуг = "ЗаказыПоставщикам" Тогда
			
			ЕстьПравоДоступа = ЕстьПравоДоступа Или ЕстьПравоДоступаЗаказыПоставщикам;
			СтруктураПараметров.Вставить("ЕстьПравоДоступаРегистрПоступлениеУслуг", ЕстьПравоДоступаЗаказыПоставщикам);
			
		ИначеЕсли СтруктураПараметров.ИмяРегистраПоступлениеУслуг = "ЗаказыНаСборку" Тогда
			
			ЕстьПравоДоступаРегистрПоступлениеУслуг = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыНаСборку);
			ЕстьПравоДоступа = ЕстьПравоДоступа И ЕстьПравоДоступаРегистрПоступлениеУслуг;
			СтруктураПараметров.Вставить("ЕстьПравоДоступаРегистрПоступлениеУслуг", ЕстьПравоДоступаРегистрПоступлениеУслуг);
			
		ИначеЕсли СтруктураПараметров.ИмяРегистраПоступлениеУслуг = "ЗаказыНаПеремещение" Тогда
			
			ЕстьПравоДоступаРегистрПоступлениеУслуг = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ЗаказыНаПеремещение);
			ЕстьПравоДоступа = ЕстьПравоДоступа И ЕстьПравоДоступаРегистрПоступлениеУслуг;
			СтруктураПараметров.Вставить("ЕстьПравоДоступаРегистрПоступлениеУслуг", ЕстьПравоДоступаРегистрПоступлениеУслуг);
			
		КонецЕсли;
		
	ИначеЕсли ИмяНабораДанных = "ОбеспечениеДополнительно" Тогда
		
		ЕстьПравоДоступа = ПравоДоступа("Просмотр", Метаданные.Обработки.СостояниеОбеспеченияЗаказов);
		
	//++ НЕ УТКА

	ИначеЕсли ИмяНабораДанных = "УслугиДавальцуКОформлению" Тогда
		
		ЕстьПравоДоступа =
			ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат)
			//++ Устарело_Переработка24
			И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.УслугиДавальцуКОформлению)
			//-- Устарело_Переработка24
			И Истина;
	
	//-- НЕ УТКА

	//++ НЕ УТ
	ИначеЕсли ИмяНабораДанных = "ПродукцияКОформлениюВОтчетеПереработчику" Тогда
		
		СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
		
		ЕстьПравоДоступа = ЗначениеЗаполнено(СтруктураДопЗапросов)
						   И СтруктураДопЗапросов.Свойство("ТекстЗапросаТаблицаПродукцияКОформлениюВОтчетеПереработчику")
						   //++ Устарело_Переработка24
						   Или ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыПолученныеОтПереработчика)
						   //-- Устарело_Переработка24
						   Или Ложь;
		
	ИначеЕсли ИмяНабораДанных = "СырьеУПереработчика" Тогда
		
		СтруктураДопЗапросов = СтруктураПараметров.СтруктураДопЗапросов;
		
		ЕстьПравоДоступа = ЗначениеЗаполнено(СтруктураДопЗапросов)
						   И СтруктураДопЗапросов.Свойство("ТаблицаСырьеУПереработчика")
						   //++ Устарело_Переработка24
						   Или ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыПереданныеПереработчику)
						   //-- Устарело_Переработка24
						   Или Ложь;
	//-- НЕ УТ
	КонецЕсли;
	
	Возврат ЕстьПравоДоступа;
	
КонецФункции

Функция СтатусыБезОтгрузки(ИмяТаблицы)
	
	МассивСтатусов = Новый Массив();
	
	МассивСтатусов.Добавить(Перечисления.СтатусыЗаказовКлиентов.НеСогласован);
	
	МассивСтатусов.Добавить(Перечисления.СтатусыЗаявокНаВозвратТоваровОтКлиентов.НеСогласована);
	МассивСтатусов.Добавить(Перечисления.СтатусыЗаявокНаВозвратТоваровОтКлиентов.КВозврату);
	МассивСтатусов.Добавить(Перечисления.СтатусыЗаявокНаВозвратТоваровОтКлиентов.Отклонена);
	МассивСтатусов.Добавить(Перечисления.СтатусыВнутреннихЗаказов.НаСогласовании);
	//++ НЕ УТ
	МассивСтатусов.Добавить(Перечисления.СтатусыЗаказовНаРемонт.Создан);
	//-- НЕ УТ
	Возврат МассивСтатусов
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
