#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(254));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2025Кв4";
	НоваяФорма.ОписаниеОтчета     = "Утверждена приказом ФНС России от 20.10.2025 № ЕД-7-11/913@.";
	НоваяФорма.РедакцияФормы      = "от 20.10.2025 № ЕД-7-11/913@.";
	Если ТекущаяДатаСеанса() >= ДатаПримененияФормыВНовойРедакции() Тогда
		НоваяФорма.ДатаНачалоДействия = '2025-01-01';
	Иначе
		НоваяФорма.ДатаНачалоДействия = '2026-01-01';
	КонецЕсли;
	НоваяФорма.ДатаКонецДействия = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета) Экспорт
	
	ТаблицаДанныхРеглОтчета = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	
	ИмяФормы = ЭкземплярРеглОтчета.ВыбраннаяФорма;
	
	Если ВРег(ИмяФормы) = ВРег("ФормаОтчета2025Кв4") Тогда
		
		ДанныеОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		
		Если ДанныеОтчета.Свойство("ОкружениеСохранения") Тогда // отчет сохранен в 2.0
			
		Иначе
			
			Период = ЭкземплярРеглОтчета.ДатаОкончания;
			
			КБКИП = "";
			ОКТМО = "";
			
			ДеревоРаздела = ДанныеОтчета.ДанныеМногоуровневыхРазделов["Приложение3"];
			Дан = ДеревоРаздела.Строки[0].Данные;
			УплаченныеАвансы = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П000П30007003"));
			
			ДеревоРаздела = ДанныеОтчета.ДанныеМногоуровневыхРазделов["Раздел1"];
			
			ДеревоМнЧ = ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей["П00001М1"];
			Для Каждого СтрокаМнЧ Из ДеревоМнЧ.Строки Цикл
				Дан = СтрокаМнЧ.Данные;
				
				ТекКБК   = СокрЛП(Дан["П00001М102003"]);
				ТекОКТМО = СокрЛП(Дан["П00001М103003"]);
				
				Если ЗначениеЗаполнено(ТекКБК) И ЗначениеЗаполнено(ТекОКТМО) Тогда
					
					ТекСуммаНалога      = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М104003"));
					ТекСуммаВозврНалога = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М105003"));
					
					Если ТекСуммаНалога > 0 Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБК, ТекОКТМО);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, ТекСуммаНалога, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
					Если ТекСуммаВозврНалога > 0 Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБК, ТекОКТМО);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, -ТекСуммаВозврНалога, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ДеревоМнЧ = ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей["П00001М2"];
			Для Каждого СтрокаМнЧ Из ДеревоМнЧ.Строки Цикл
				Дан = СтрокаМнЧ.Данные;
				
				ТекКБКИП = СокрЛП(Дан["П00001М206003"]);
				
				Если ЗначениеЗаполнено(ТекКБКИП) Тогда
					
					СумНалИПКУпл1Кв   = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М208003"));
					СумНалИПКУплПол   = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М210003"));
					СумНалИПКУменПол  = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М211003"));
					СумНалИПКУпл9Мес  = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М213003"));
					СумНалИПКУмен9Мес = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М214003"));
					СумНалИПКУплГод   = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М216003"));
					СумНалИПКУменГод  = Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П00001М217003"));
					
					ТекОКТМО = СокрЛП(Дан["П00001М207003"]);
					Если ЗначениеЗаполнено(ТекОКТМО) Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБКИП, ТекОКТМО);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, СумНалИПКУпл1Кв, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
					ТекОКТМО = СокрЛП(Дан["П00001М209003"]);
					Если ЗначениеЗаполнено(ТекОКТМО) Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБКИП, ТекОКТМО);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, СумНалИПКУплПол - СумНалИПКУменПол, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
					ТекОКТМО = СокрЛП(Дан["П00001М212003"]);
					Если ЗначениеЗаполнено(ТекОКТМО) Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБКИП, ТекОКТМО);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, СумНалИПКУпл9Мес - СумНалИПКУмен9Мес, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
					ТекОКТМОГод = СокрЛП(Дан["П00001М215003"]);
					Если ЗначениеЗаполнено(ТекОКТМОГод) Тогда
						ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, ТекКБКИП, ТекОКТМОГод);
						ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, СумНалИПКУплГод - СумНалИПКУменГод, ТаблицаДанныхРеглОтчета, ИмяФормы);
					КонецЕсли;
					
					// В случае прогрессивной шкалы доход ИП разделяется на соответствующие КБК по виду налога.
					// Вычитываем авансы по начисленному налогу для текущей страницы.
					
					ИтогоСтрокаМнЧ = СумНалИПКУпл1Кв + СумНалИПКУплПол - СумНалИПКУменПол
						+ СумНалИПКУпл9Мес - СумНалИПКУмен9Мес + СумНалИПКУплГод - СумНалИПКУменГод;
					
					АвансовСтрокаМнЧ = Мин(УплаченныеАвансы, ИтогоСтрокаМнЧ);
					УплаченныеАвансы = УплаченныеАвансы - АвансовСтрокаМнЧ;
					КБКИП = ТекКБКИП;
					ОКТМО = ТекОКТМОГод;
					
					ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, КБКИП, ОКТМО);
					ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, -АвансовСтрокаМнЧ, ТаблицаДанныхРеглОтчета, ИмяФормы);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если УплаченныеАвансы > 0 Тогда
				ПараметрыОтбора = Новый Структура("Период,КБК,ОКАТО", Период, КБКИП, ОКТМО);
				ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, -УплаченныеАвансы, ТаблицаДанныхРеглОтчета, ИмяФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТаблицаДанныхРеглОтчета;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма2025Кв4 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы,
		"1151020", '2025-10-20', "ЕД-7-11/913@", "ФормаОтчета2025Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма2025Кв4, "5.20");
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "",
			ИмяОбъекта = "", ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ТекстПоясненияРасширенныйНалоговыйПериод(Организация, КонецПериода) Экспорт
	
	Если РегламентированнаяОтчетностьПереопределяемый.СистемаНалогообложения(Организация, КонецПериода) <> "Общая" Тогда
		Возврат "";
	КонецЕсли;
	
	НалоговыйПериод = ИнтерфейсыВзаимодействияБРО.БлижайшийНалоговыйПериод(
		Организация,
		КонецПериода,
		Перечисления.ВариантыРасширенногоПервогоНалоговогоПериода.РегистрацияВДекабре);
	
	Если КонецПериода < НалоговыйПериод.Начало Тогда
		Возврат "";
	КонецЕсли;
	
	НалоговыйПериодПропущен = (КонецПериода < НалоговыйПериод.Период);
	НалоговыйПериодРасширен = (НалоговыйПериод.Начало < НалоговыйПериод.Период);
	
	ГодОтчета = Год(КонецПериода);
	
	Если НалоговыйПериодПропущен Тогда
		ТекстПояснения = СтрШаблон(НСтр(
			"ru = 'Декларировать доходы, полученные от предпринимательской деятельности, в отчете за %1 год не нужно. Период с даты регистрации %2 по %3 включается в отчет за %4 год.';
			|en = 'Декларировать доходы, полученные от предпринимательской деятельности, в отчете за %1 год не нужно. Период с даты регистрации %2 по %3 включается в отчет за %4 год.'"),
			Формат(ГодОтчета, "ЧГ=0"),
			Формат(НалоговыйПериод.Начало, "ДЛФ=D"),
			Формат(НалоговыйПериод.Период - 1, "ДЛФ=D"),
			Формат(ГодОтчета + 1, "ЧГ=0"));
	ИначеЕсли НалоговыйПериодРасширен Тогда
		ТекстПояснения = СтрШаблон(НСтр(
			"ru = 'Доходы, полученные от предпринимательской деятельности с даты регистрации %1 по %2, включаются в отчет за %3 год.';
			|en = 'Доходы, полученные от предпринимательской деятельности с даты регистрации %1 по %2, включаются в отчет за %3 год.'"),
			Формат(НалоговыйПериод.Начало, "ДЛФ=D"),
			Формат(НалоговыйПериод.Период - 1, "ДЛФ=D"),
			Формат(ГодОтчета, "ЧГ=0"));
	Иначе
		ТекстПояснения = "";
	КонецЕсли;
	
	Возврат ТекстПояснения;
	
КонецФункции

Процедура ДобавитьДанныеРеглОтчетаВТаблицу(ПараметрыОтбора, Сумма, ТаблицаДанныхРеглОтчета, ИмяФормы)
	
	Если Сумма = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НайденныеСтроки = ТаблицаДанныхРеглОтчета.НайтиСтроки(ПараметрыОтбора);
	Если НайденныеСтроки.Количество() = 0 Тогда
		СтрокаДанных = ТаблицаДанныхРеглОтчета.Добавить();
		СтрокаДанных.Период    = ПараметрыОтбора.Период;
		СтрокаДанных.ВидНалога = ВидНалогаПоКБК(ПараметрыОтбора.КБК, ИмяФормы);
		СтрокаДанных.КБК       = ПараметрыОтбора.КБК;
		СтрокаДанных.ОКАТО     = ПараметрыОтбора.ОКАТО;
	Иначе
		СтрокаДанных = НайденныеСтроки[0]
	КонецЕсли;
	
	СтрокаДанных.Сумма = СтрокаДанных.Сумма + Сумма;
	
КонецПроцедуры

Функция ВидНалогаПоКБК(КБК, ИмяФормы)
	
	Соотв = Новый Соответствие;
	
	ИмяМакета = "";
	
	Если ВРег(ИмяФормы) = ВРег("ФормаОтчета2025Кв4") Тогда
		ИмяМакета = "СпискиВыбора2025Кв4";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяМакета) Тогда
		
		МакетСпискиВыбора = Отчеты.РегламентированныйОтчет3_НДФЛ.ПолучитьМакет(ИмяМакета);
		Область = МакетСпискиВыбора.Области.Найти("КБК");
		Если Область <> Неопределено Тогда
			ВерхОбласти = Область.Верх;
			НизОбласти  = Область.Низ;
			Для НомСтр = ВерхОбласти По НизОбласти Цикл
				ТекКБК = СокрП(МакетСпискиВыбора.Область(НомСтр, 1).Текст);
				ТекВН  = СокрП(МакетСпискиВыбора.Область(НомСтр, 3).Текст);
				Если ЗначениеЗаполнено(ТекКБК) Тогда
					Соотв.Вставить("_" + ТекКБК, ТекВН);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ВидНалога = Соотв.Получить("_" + КБК);
		
		Если ВидНалога <> Неопределено Тогда
			Если ВидНалога = "НДФЛ" Тогда
				Возврат Перечисления.ВидыНалогов.НДФЛ;
			ИначеЕсли ВидНалога = "НДФЛ_ИП" Тогда
				Возврат Перечисления.ВидыНалогов.НДФЛ_ИП;
			ИначеЕсли ВидНалога = "НДФЛ_Дивиденды" Тогда
				Возврат Перечисления.ВидыНалогов.НДФЛ_Дивиденды;
			ИначеЕсли ВидНалога = "НДФЛ_ФизЛицо" Тогда
				Возврат Перечисления.ВидыНалогов.НДФЛ_ФизЛицо;
			КонецЕсли;
		КонецЕсли;
		
		Возврат Перечисления.ВидыНалогов.НДФЛ_ФизЛицо;// значение по умолчанию
		
	КонецЕсли;
	
	Возврат Перечисления.ВидыНалогов.ПустаяСсылка();
	
КонецФункции

Функция ДатаПримененияФормыВНовойРедакции() Экспорт
	
	Возврат '20260101';
	
КонецФункции

#Область ПоказателиОценкиРискаВыезднойНалоговойПроверки

Процедура ПоказателиОценкиРискаВыезднойНалоговойПроверки(ТаблицаПоказателей, ОписаниеОтчета) Экспорт
	
	СуммаНДФЛКУплате = 0;
	СуммаДоходовИП = 0;
	СуммаРасходовИП = 0;
	
	ИмяФормы = ОписаниеОтчета.ВыбраннаяФорма;
	
	Если ВРег(ИмяФормы) = ВРег("ФормаОтчета2025Кв4") Тогда
		
		ДанныеОтчета = ОписаниеОтчета.РегламентированныйОтчет.ДанныеОтчета.Получить();
		
		Если ДанныеОтчета.Свойство("ОкружениеСохранения") Тогда // отчет сохранен в 2.0
			
		Иначе
			
			ДеревоРаздела = ДанныеОтчета.ДанныеМногоуровневыхРазделов["Раздел1"];
			ДеревоМнЧ = ДеревоРаздела.Строки[0].ДанныеМногострочныхЧастей["П00001М2"];
			Для Каждого СтрокаМнЧ Из ДеревоМнЧ.Строки Цикл
				ТекКБК = СокрЛП(СтрокаМнЧ.Данные["П00001М206003"]);
				Если ВидНалогаПоКБК(ТекКБК, ИмяФормы) = Перечисления.ВидыНалогов.НДФЛ_ИП Тогда
					СуммаНДФЛКУплате = СуммаНДФЛКУплате
						+ Окр(РегламентированнаяОтчетность.ПоказательОтчета(СтрокаМнЧ.Данные, "П00001М216003"));
				КонецЕсли;
			КонецЦикла;
			
			ДеревоРаздела = ДанныеОтчета.ДанныеМногоуровневыхРазделов["Приложение3"];
			Дан = ДеревоРаздела.Строки[0].Данные;
			СуммаДоходовИП  = СуммаДоходовИП  + Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П000П30005003"), 2);
			СуммаРасходовИП = СуммаРасходовИП + Окр(РегламентированнаяОтчетность.ПоказательОтчета(Дан, "П000П30006003"), 2);
			
		КонецЕсли;
		
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаНДФЛКУплате";
	НоваяСтрока.ЗначениеПоказателя = СуммаНДФЛКУплате;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаДоходовИП";
	НоваяСтрока.ЗначениеПоказателя = СуммаДоходовИП;
	
	НоваяСтрока = ТаблицаПоказателей.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОтчета);
	НоваяСтрока.Показатель = "СуммаПрофессиональныхВычетовПоНДФЛ";
	НоваяСтрока.ЗначениеПоказателя = СуммаРасходовИП;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли