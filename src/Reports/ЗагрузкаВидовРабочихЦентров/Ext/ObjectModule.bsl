#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы);
	
	Если ЗначенияОтбораДанных.НачалоПериода = '000101010000' 
		ИЛИ ЗначенияОтбораДанных.ОкончаниеПериода = '000101010000' Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Период"" не заполнено.';
								|en = '""Period"" is not filled in.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;

	Если ЗначенияОтбораДанных.ПериодичностьОтчета = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Периодичность отчета"" не заполнено.';
								|en = '""Report frequency"" is required.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
	КонецЕсли;

КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ВнешниеНаборыДанных = СформироватьВнешниеНаборыДанных(НастройкиОсновнойСхемы);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОсновнойСхемы, ДанныеРасшифровки);	
	
	ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	ПроцессорВыводаВТабличныйДокумент = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВыводаВТабличныйДокумент.УстановитьДокумент(ДокументРезультат);	
	ПроцессорВыводаВТабличныйДокумент.Вывести(ПроцессорКомпоновкиДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьВнешниеНаборыДанных(НастройкиОсновнойСхемы)
	
	МассивТипов = Новый Массив;
	//++ Локализация
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
	//-- Локализация
	МассивТипов.Добавить(Тип("ДокументСсылка.ЭтапПроизводства2_2"));
	МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство2_2"));
	ОТРаспоряжение = Новый ОписаниеТипов(МассивТипов);
	
	ДанныеОЗагрузке = Новый ТаблицаЗначений;
	ДанныеОЗагрузке.Колонки.Добавить("Подразделение",              Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	ДанныеОЗагрузке.Колонки.Добавить("ВидРабочегоЦентра",          Новый ОписаниеТипов("СправочникСсылка.ВидыРабочихЦентров"));
	ДанныеОЗагрузке.Колонки.Добавить("ВариантНаладки",             Новый ОписаниеТипов("СправочникСсылка.ВариантыНаладки"));
	ДанныеОЗагрузке.Колонки.Добавить("Доступно",                   Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15)));
	ДанныеОЗагрузке.Колонки.Добавить("Занято",                     Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15)));
	ДанныеОЗагрузке.Колонки.Добавить("ДоступноСводно",             Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15)));
	ДанныеОЗагрузке.Колонки.Добавить("ЗанятоСводно",               Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15)));
	ДанныеОЗагрузке.Колонки.Добавить("Номенклатура",               Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ДанныеОЗагрузке.Колонки.Добавить("Характеристика",             Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ДанныеОЗагрузке.Колонки.Добавить("Количество",                 Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15,3)));
	ДанныеОЗагрузке.Колонки.Добавить("ПериодГрафика",              Новый ОписаниеТипов("Дата"));
	ДанныеОЗагрузке.Колонки.Добавить("ПериодГрафикаПредставление", Новый ОписаниеТипов("Строка"));
	ДанныеОЗагрузке.Колонки.Добавить("ПараллельнаяЗагрузка",       Новый ОписаниеТипов("Булево"));
	ДанныеОЗагрузке.Колонки.Добавить("УчитыватьДоступность",       Новый ОписаниеТипов("Булево"));
	ДанныеОЗагрузке.Колонки.Добавить("Этап",                       Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2, СправочникСсылка.ЭтапыПроизводства"));
	ДанныеОЗагрузке.Колонки.Добавить("Распоряжение",        	   ОТРаспоряжение);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	//0
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДоступностьВидовРабочихЦентровОбороты.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ВЫБОР &ПериодичностьОтчета
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День) ТОГДА
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя) ТОГДА
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, НЕДЕЛЯ)
	|		ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодГрафика,	
	|	ВидыРабочихЦентров.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
	|	ВидыРабочихЦентров.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
	|	СУММА(ВЫБОР
	|			КОГДА ВидыРабочихЦентров.ВводитьДоступностьДляВидаРЦ
	|				ТОГДА ДоступностьВидовРабочихЦентровОбороты.ДоступностьПоВидуРЦОборот
	|			ИНАЧЕ ДоступностьВидовРабочихЦентровОбороты.ДоступностьПоРЦОборот
	|		КОНЕЦ) КАК Доступно
	|ИЗ
	|	РегистрНакопления.ДоступностьВидовРабочихЦентров.Обороты(
	|			,
	|			,
	|			,
	|			ДатаИнтервала МЕЖДУ &НачалоПериода И &КонецПериода
	|				И (&ЛюбойВидРабочегоЦентра
	|					ИЛИ ВидРабочегоЦентра В (&СписокВидовРабочихЦентров))) КАК ДоступностьВидовРабочихЦентровОбороты
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|	ПО ВидыРабочихЦентров.Ссылка = ДоступностьВидовРабочихЦентровОбороты.ВидРабочегоЦентра
	|ГДЕ
	|	(&ЛюбоеПодразделение
	|			ИЛИ ВидыРабочихЦентров.Подразделение В (&СписокПодразделений))
	|	И ВЫБОР
	|			КОГДА (ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
	|					ИЛИ ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
	|					ИЛИ ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День))
	|					И (ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала МЕЖДУ &НачалоДня И &КонецДня)
	|				ТОГДА ИСТИНА
	|			КОГДА ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
	|					И (ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала МЕЖДУ &НачалоНедели И &КонецНедели)
	|				ТОГДА ИСТИНА
	|			КОГДА ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
	|					И (ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала МЕЖДУ &НачалоМесяца И &КонецМесяца)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоступностьВидовРабочихЦентровОбороты.ВидРабочегоЦентра,
	|	ВидыРабочихЦентров.ПараллельнаяЗагрузка,
	|	ВидыРабочихЦентров.УчитыватьДоступностьПоГрафикуРаботы,
	|	ВЫБОР &ПериодичностьОтчета
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День) ТОГДА
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя) ТОГДА
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, НЕДЕЛЯ)
	|		ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ДоступностьВидовРабочихЦентровОбороты.ДатаИнтервала, МЕСЯЦ)
	|	КОНЕЦ	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
	//++ Локализация
	|				ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство 
	//-- Локализация
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор
	//++ Локализация
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаЗанятостьВидовРабочихЦентров.Регистратор КАК Документ.МаршрутныйЛистПроизводства).Распоряжение
	//-- Локализация
	|	КОНЕЦ КАК Распоряжение,
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Этап
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Этап,
	|	ТаблицаЗанятостьВидовРабочихЦентров.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВариантНаладки КАК ВариантНаладки,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала КАК ДатаИнтервала,
	|	ВидыРабочихЦентров.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
	|	ВидыРабочихЦентров.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
	|	СУММА(ТаблицаЗанятостьВидовРабочихЦентров.Занято) КАК Занято
	|ПОМЕСТИТЬ ЗанятостьВидовРабочихЦентров
	|ИЗ
	|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ТаблицаЗанятостьВидовРабочихЦентров
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|	ПО ВидыРабочихЦентров.Ссылка = ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра
	|ГДЕ
	|	(ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
	|			ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
	//++ Локализация
	|			ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство			 
	|			ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.МаршрутныйЛистПроизводства
	//-- Локализация
	|	)
	|	И (&ЛюбойВидРабочегоЦентра
	|			ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра В (&СписокВидовРабочихЦентров))
	|	И ВЫБОР
	|			КОГДА (ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
	|					ИЛИ ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
	|					ИЛИ ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День))
	|					И (ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала МЕЖДУ &НачалоДня И &КонецДня)
	|				ТОГДА ИСТИНА
	|			КОГДА ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
	|					И (ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала МЕЖДУ &НачалоНедели И &КонецНедели)
	|				ТОГДА ИСТИНА
	|			КОГДА ВидыРабочихЦентров.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
	|					И (ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала МЕЖДУ &НачалоМесяца И &КонецМесяца)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
	//++ Локализация
	|				ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство 
	//-- Локализация
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор
	//++ Локализация
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаЗанятостьВидовРабочихЦентров.Регистратор КАК Документ.МаршрутныйЛистПроизводства).Распоряжение
	//-- Локализация
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
	|			ТОГДА ТаблицаЗанятостьВидовРабочихЦентров.Этап
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаЗанятостьВидовРабочихЦентров.КодСтрокиПродукция,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВариантНаладки,
	|	ВидыРабочихЦентров.ПараллельнаяЗагрузка,
	|	ВидыРабочихЦентров.УчитыватьДоступностьПоГрафикуРаботы,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение КАК Распоряжение,
	|	ТаблицаЗанятостьВидовРабочихЦентров.Этап КАК Этап,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВариантНаладки КАК ВариантНаладки,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение КАК Подразделение,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
	|	ТаблицаЗанятостьВидовРабочихЦентров.УчитыватьДоступность КАК УчитыватьДоступность,
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
	|				ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
	|		ИНАЧЕ ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования
	|	КОНЕЦ КАК ИнтервалПланирования,
	|	ВЫБОР &ПериодичностьОтчета
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День) ТОГДА
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя) ТОГДА
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, НЕДЕЛЯ)
	|		ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодГрафика,	
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	0 КАК Количество,
	|	ТаблицаЗанятостьВидовРабочихЦентров.Занято КАК Занято
	|ИЗ
	|	ЗанятостьВидовРабочихЦентров КАК ТаблицаЗанятостьВидовРабочихЦентров
	|ГДЕ
	|	ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение ССЫЛКА Документ.ЭтапПроизводства2_2
	|	ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|
	//++ Локализация
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение КАК Распоряжение,
	|	ТаблицаЗанятостьВидовРабочихЦентров.Этап КАК Этап,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВариантНаладки КАК ВариантНаладки,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение КАК Подразделение,
	|	ТаблицаЗанятостьВидовРабочихЦентров.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
	|	ТаблицаЗанятостьВидовРабочихЦентров.УчитыватьДоступность КАК УчитыватьДоступность,
	|	ВЫБОР
	|		КОГДА ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
	|				ИЛИ ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования = ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
	|		ИНАЧЕ ТаблицаЗанятостьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования
	|	КОНЕЦ КАК ИнтервалПланирования,
	|	ВЫБОР &ПериодичностьОтчета
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День) ТОГДА
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
	|		КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя) ТОГДА
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, НЕДЕЛЯ)
	|		ИНАЧЕ
	|			НАЧАЛОПЕРИОДА(ТаблицаЗанятостьВидовРабочихЦентров.ДатаИнтервала, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодГрафика,	
	|	ТаблицаПродукция.Номенклатура КАК Номенклатура,
	|	ТаблицаПродукция.Характеристика КАК Характеристика,
	|	ТаблицаПродукция.Количество КАК Количество,
	|	ТаблицаЗанятостьВидовРабочихЦентров.Занято КАК Занято
	|ИЗ
	|	ЗанятостьВидовРабочихЦентров КАК ТаблицаЗанятостьВидовРабочихЦентров
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказНаПроизводство.Продукция КАК ТаблицаПродукция
	|		ПО ((ВЫРАЗИТЬ(ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение КАК Документ.ЗаказНаПроизводство)) = ТаблицаПродукция.Ссылка)
	|			И ТаблицаЗанятостьВидовРабочихЦентров.КодСтрокиПродукция = ТаблицаПродукция.КодСтроки
	|ГДЕ
	|	ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение ССЫЛКА Документ.ЗаказНаПроизводство
	|	И ВЫРАЗИТЬ(ТаблицаЗанятостьВидовРабочихЦентров.Распоряжение КАК Документ.ЗаказНаПроизводство).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
	|	И ТаблицаПродукция.ГрафикРассчитан
	//-- Локализация
	|";
	
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы);
	
	Запрос.УстановитьПараметр("ЛюбоеПодразделение",        ЗначенияОтбораДанных.СписокПодразделений.Количество() = 0);
	Запрос.УстановитьПараметр("ЛюбойВидРабочегоЦентра",    ЗначенияОтбораДанных.СписокВидовРабочихЦентров.Количество() = 0);
	Запрос.УстановитьПараметр("СписокПодразделений",       ЗначенияОтбораДанных.СписокПодразделений);
	Запрос.УстановитьПараметр("СписокВидовРабочихЦентров", ЗначенияОтбораДанных.СписокВидовРабочихЦентров);
	Запрос.УстановитьПараметр("ПериодичностьОтчета",  ЗначенияОтбораДанных.ПериодичностьОтчета); 
	
	// В зависимости от интервала планирования используется нужный период
	Запрос.УстановитьПараметр("НачалоДня",    НачалоДня(ЗначенияОтбораДанных.НачалоПериода));
	Запрос.УстановитьПараметр("КонецДня",     КонецДня(ЗначенияОтбораДанных.ОкончаниеПериода));
	Запрос.УстановитьПараметр("НачалоНедели", НачалоНедели(ЗначенияОтбораДанных.НачалоПериода));
	Запрос.УстановитьПараметр("КонецНедели",  КонецНедели(ЗначенияОтбораДанных.ОкончаниеПериода));
	Запрос.УстановитьПараметр("НачалоМесяца", НачалоМесяца(ЗначенияОтбораДанных.НачалоПериода));
	Запрос.УстановитьПараметр("КонецМесяца",  КонецМесяца(ЗначенияОтбораДанных.ОкончаниеПериода));
	Запрос.УстановитьПараметр("НачалоПериода",
		Мин(НачалоНедели(ЗначенияОтбораДанных.НачалоПериода), НачалоМесяца(ЗначенияОтбораДанных.НачалоПериода)));
	Запрос.УстановитьПараметр("КонецПериода",
		Макс(КонецНедели(ЗначенияОтбораДанных.ОкончаниеПериода), КонецМесяца(ЗначенияОтбораДанных.ОкончаниеПериода)));
	
	Результат = Запрос.ВыполнитьПакет();  
	
	ДоступностьВРЦ = Результат[0].Выгрузить();
	ДоступностьВРЦ.Индексы.Добавить("ВидРабочегоЦентра,ПериодГрафика");
	
	ЗанятостьВРЦ = Результат[2].Выгрузить();
	ЗанятостьВРЦ.Индексы.Добавить("ВидРабочегоЦентра,ПериодГрафика");

	СводнаяДоступностьВРЦ = ДоступностьВРЦ.Скопировать();
	СводнаяДоступностьВРЦ.Свернуть("ВидРабочегоЦентра, ПериодГрафика", "Доступно");
	СводнаяДоступностьВРЦ.Индексы.Добавить("ВидРабочегоЦентра,ПериодГрафика");
	
	СводнаяЗанятостьВРЦ = ЗанятостьВРЦ.Скопировать();
	СводнаяЗанятостьВРЦ.Свернуть("ВидРабочегоЦентра, ПериодГрафика", "Занято");
	СводнаяЗанятостьВРЦ.Индексы.Добавить("ВидРабочегоЦентра,ПериодГрафика");
	
	СписокВидовРЦ = ЗанятостьВРЦ.Скопировать(,"Подразделение,ВидРабочегоЦентра,ИнтервалПланирования,ПараллельнаяЗагрузка,УчитыватьДоступность");
	СписокВидовРЦ.Свернуть("Подразделение,ВидРабочегоЦентра,ИнтервалПланирования,ПараллельнаяЗагрузка,УчитыватьДоступность");
	
	Для каждого СтрокаВидРЦ Из СписокВидовРЦ Цикл
		
		ИнтервалПланированияПоПериодичностиОтчета = ИнтервалПланированияПоПериодичностиОтчета(ЗначенияОтбораДанных.ПериодичностьОтчета);
		ТекущийПериод = ПланированиеПроизводстваКлиентСервер.НачалоИнтервалаПланирования(
										ЗначенияОтбораДанных.НачалоПериода, 
										ИнтервалПланированияПоПериодичностиОтчета);
										
		Пока ТекущийПериод <= ЗначенияОтбораДанных.ОкончаниеПериода Цикл
			
			СтруктураПоиска = Новый Структура("ВидРабочегоЦентра,ПериодГрафика", СтрокаВидРЦ.ВидРабочегоЦентра, ТекущийПериод);
		 	НайденнаяЗанятостьВРЦ = ЗанятостьВРЦ.НайтиСтроки(СтруктураПоиска);
		 	НайденнаяДоступностьВРЦ = ДоступностьВРЦ.НайтиСтроки(СтруктураПоиска);
		 	НайденнаяСводнаяЗанятостьВРЦ = СводнаяЗанятостьВРЦ.НайтиСтроки(СтруктураПоиска);
		 	НайденнаяСводнаяДоступностьВРЦ = СводнаяДоступностьВРЦ.НайтиСтроки(СтруктураПоиска);
			
			Если НайденнаяЗанятостьВРЦ.Количество() <> 0 Тогда
				
				Для каждого ВыборкаЗанятостьВРЦ Из НайденнаяЗанятостьВРЦ Цикл
					СтрокаДанные = ДанныеОЗагрузке.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаДанные, ВыборкаЗанятостьВРЦ);
					СтрокаДанные.ПериодГрафика = ТекущийПериод;
					
					СтрокаДанные.ПериодГрафикаПредставление = ОперативныйУчетПроизводства.ИнтервалПланированияСтрокой(
																					СтрокаДанные.ПериодГрафика, 
																					ИнтервалПланированияПоПериодичностиОтчета);
					Если НайденнаяДоступностьВРЦ.Количество() <> 0 Тогда
						СтрокаДанные.Доступно = НайденнаяДоступностьВРЦ[0].Доступно;
					КонецЕсли;
					
					Если НайденнаяСводнаяЗанятостьВРЦ.Количество() <> 0 Тогда
						СтрокаДанные.ЗанятоСводно = НайденнаяСводнаяЗанятостьВРЦ[0].Занято;
					КонецЕсли; 

					Если НайденнаяСводнаяДоступностьВРЦ.Количество() <> 0 Тогда
						СтрокаДанные.ДоступноСводно = НайденнаяСводнаяДоступностьВРЦ[0].Доступно;
					КонецЕсли; 

				КонецЦикла;
				
			КонецЕсли;
			
			СтрокаДанные = ДанныеОЗагрузке.Добавить();
			СтрокаДанные.ВидРабочегоЦентра = СтрокаВидРЦ.ВидРабочегоЦентра;
			СтрокаДанные.Подразделение = СтрокаВидРЦ.Подразделение;
			СтрокаДанные.ПериодГрафика = ТекущийПериод;
			СтрокаДанные.ПараллельнаяЗагрузка = СтрокаВидРЦ.ПараллельнаяЗагрузка;
			СтрокаДанные.УчитыватьДоступность = СтрокаВидРЦ.УчитыватьДоступность;
			
			СтрокаДанные.ПериодГрафикаПредставление = ОперативныйУчетПроизводства.ИнтервалПланированияСтрокой(
																			СтрокаДанные.ПериодГрафика, 
																			ИнтервалПланированияПоПериодичностиОтчета);
			Если НайденнаяДоступностьВРЦ.Количество() <> 0 Тогда
				СтрокаДанные.Доступно = НайденнаяДоступностьВРЦ[0].Доступно;
			КонецЕсли; 

			Если НайденнаяСводнаяЗанятостьВРЦ.Количество() <> 0 Тогда
				СтрокаДанные.ЗанятоСводно = НайденнаяСводнаяЗанятостьВРЦ[0].Занято;
			КонецЕсли; 

			Если НайденнаяСводнаяДоступностьВРЦ.Количество() <> 0 Тогда
				СтрокаДанные.ДоступноСводно = НайденнаяСводнаяДоступностьВРЦ[0].Доступно;
			КонецЕсли; 
			
			ТекущийПериод = ПланированиеПроизводстваКлиентСервер.ОкончаниеИнтервалаПланирования(ТекущийПериод, ИнтервалПланированияПоПериодичностиОтчета) + 1;
			
		КонецЦикла;
		
	КонецЦикла;

	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ДанныеОЗагрузке", ДанныеОЗагрузке);
	
	Возврат ВнешниеНаборыДанных;

КонецФункции
 
Функция ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы)

	Период = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Период").Значение; // СтандартныйПериод - 
	ПериодичностьОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "ПериодичностьОтчета").Значение;
	
	ЭлементыОтбора = НастройкиОсновнойСхемы.Отбор.Элементы;
	
	СписокПодразделений = Новый СписокЗначений;
	СписокВидовРабочихЦентров = Новый СписокЗначений;
	
	Если ЭлементыОтбора <> Неопределено Тогда
		Подразделение  = ЗначениеОтбора("Подразделение", ЭлементыОтбора);
		Если Подразделение <> Неопределено И ТипЗнч(Подразделение) = Тип("СписокЗначений") Тогда
			СписокПодразделений.ЗагрузитьЗначения(Подразделение.ВыгрузитьЗначения());
		ИначеЕсли Подразделение <> Неопределено Тогда
			СписокПодразделений.Добавить(Подразделение);
		КонецЕсли;
		
		ВидРабочегоЦентра = ЗначениеОтбора("ВидРабочегоЦентра", ЭлементыОтбора);
		Если ВидРабочегоЦентра <> Неопределено И ТипЗнч(ВидРабочегоЦентра) = Тип("СписокЗначений") Тогда
			СписокВидовРабочихЦентров.ЗагрузитьЗначения(ВидРабочегоЦентра.ВыгрузитьЗначения());
		ИначеЕсли ВидРабочегоЦентра <> Неопределено Тогда
			СписокВидовРабочихЦентров.Добавить(ВидРабочегоЦентра);
		КонецЕсли;
	КонецЕсли; 
	
	ЗначенияОтбораДанных = Новый Структура;
	ЗначенияОтбораДанных.Вставить("СписокПодразделений",          СписокПодразделений);
	ЗначенияОтбораДанных.Вставить("СписокВидовРабочихЦентров",    СписокВидовРабочихЦентров);
	ЗначенияОтбораДанных.Вставить("НачалоПериода",                Период.ДатаНачала);
	ЗначенияОтбораДанных.Вставить("ОкончаниеПериода",             Период.ДатаОкончания);
	ЗначенияОтбораДанных.Вставить("ПериодичностьОтчета", 		  ПериодичностьОтчета);
	
	Возврат ЗначенияОтбораДанных;
	
КонецФункции

Функция ЗначениеОтбора(ИмяПоля, ЭлементыОтбора)

	Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	Для каждого ЭлементКоллекции Из ЭлементыОтбора Цикл
		Если ТипЗнч(ЭлементКоллекции) = Тип("ЭлементОтбораКомпоновкиДанных") 
			И ЭлементКоллекции.ЛевоеЗначение = Поле 
			И ЭлементКоллекции.Использование Тогда
			
			Возврат ЭлементКоллекции.ПравоеЗначение;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Неопределено;

КонецФункции

Функция ИнтервалПланированияПоПериодичностиОтчета(Периодичность)
	
	Результат = Перечисления.ТочностьГрафикаПроизводства.День;
	Если Периодичность = Перечисления.Периодичность.Неделя Тогда
		Результат = Перечисления.ТочностьГрафикаПроизводства.Неделя;
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		Результат = Перечисления.ТочностьГрафикаПроизводства.Месяц;
	ИначеЕсли Периодичность = Перечисления.Периодичность.Квартал Тогда
		Результат = Перечисления.ТочностьГрафикаПроизводства.Месяц;
	Иначе
		Результат = Перечисления.ТочностьГрафикаПроизводства.День;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
