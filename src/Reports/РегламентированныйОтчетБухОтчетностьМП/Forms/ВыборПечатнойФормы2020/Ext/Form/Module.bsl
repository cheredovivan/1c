#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность.Интерфейс85") Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 = ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьНазначениеДействияКоманды(Команды.ПоказатьБланк);
	КонецЕсли;
	
	ПараметрыПечати = Параметры.ПараметрыПечати;
	
	Если ЗначениеЗаполнено(Параметры.РедакцияФормы) Тогда
		Элементы.Переключатель2.СписокВыбора[0].Представление =
			"Формы в редакции приказа Минфина России " + Параметры.РедакцияФормы;
	КонецЕсли;
	
	// Настройки по умолчанию.
	НастройкиВФорме = Новый Структура;
	НастройкиВФорме.Вставить("АктивныйПункт", 2);
	НастройкиВФорме.Вставить("ДоступенПункт1", Истина);
	НастройкиВФорме.Вставить("ДоступенПункт2", Истина);
	НастройкиВФорме.Вставить("ВключатьКодыСтрок", Истина);
	НастройкиВФорме.Вставить("РазрешитьВключатьКодыСтрок", Истина);
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(НастройкиВФорме, ПараметрыПечати);
	КонецЕсли;
	
	ДоступныеПункты = Новый Массив;
	Если НастройкиВФорме.ДоступенПункт1 Тогда
		ДоступныеПункты.Добавить(1);
	КонецЕсли;
	Если НастройкиВФорме.ДоступенПункт2 Тогда
		ДоступныеПункты.Добавить(2);
	КонецЕсли;
	
	ИндексДоступногоПункта = ДоступныеПункты.Найти(НастройкиВФорме.АктивныйПункт);
	Если ИндексДоступногоПункта = Неопределено Тогда
		ИндексДоступногоПункта = 0;
	КонецЕсли;
	Переключатель1 = ДоступныеПункты[ИндексДоступногоПункта];
	
	Элементы.Переключатель1.Доступность = НастройкиВФорме.ДоступенПункт1;
	Элементы.Переключатель2.Доступность = НастройкиВФорме.ДоступенПункт2;
	
	Элементы.ГруппаФлажкаВыводаКодовСтрок.Видимость = НастройкиВФорме.РазрешитьВключатьКодыСтрок;
	ВыводитьКолонкуСКодамиСтрок = НастройкиВФорме.ВключатьКодыСтрок;
	УстановитьДоступностьВключенияКодов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Переключатель1ПриИзменении(Элемент)
	
	УстановитьДоступностьВключенияКодов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура Переключатель2ПриИзменении(Элемент)
	
	УстановитьДоступностьВключенияКодов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьБланк(Команда)
	
	НастройкиВФорме.Вставить("ВключатьКодыСтрок", ВыводитьКолонкуСКодамиСтрок);
	НастройкиВФорме.Вставить("АктивныйПункт", Переключатель1);
	НастройкиВФорме.Вставить("Команда", Команда.Имя);
	Закрыть(НастройкиВФорме);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьВключенияКодов(Форма)
	
	Форма.Элементы.ВыводитьКолонкуСКодамиСтрок.Доступность = (Форма.Переключатель1 = 2);
	
КонецПроцедуры

#КонецОбласти
