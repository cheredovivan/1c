#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
// 	КомандыОтчетов - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений:
// 		* Менеджер - Строка
// 		* Представление - Строка
// 		* МножественныйВыбор - Булево
// 		* Важность - Строка
// 		* КлючВарианта - Строка
// 		* ЕстьУсловияВидимости - Булево
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ВедомостьПоРаспоряжениямНаОтгрузкуИВозврат) 
		И ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ВедомостьПоРаспоряжениямНаОтгрузкуИВозврат.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Ведомость по распоряжениям на отгрузку';
											|en = 'Shipment reference statement'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ВедомостьПоРаспоряжениямНаОтгрузкуИВозврат";
		КомандаОтчет.ЕстьУсловияВидимости = Ложь;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета.
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ВедомостьПоРаспоряжениямНаОтгрузкуИВозврат);
	ОписаниеОтчета.Включен = Истина;
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Включен = Истина;
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ показателей распоряжений на отгрузку за период.
		|Какие товары были заказаны, находятся в пути, требуют отгрузки?';
		|en = 'Analysis of shipment reference indicators for the period.
		|What goods were ordered, are in transit, or require shipment?'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ВедомостьПоРаспоряжениямНаОтгрузку");
	ОписаниеВарианта.Включен = Истина;
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ показателей распоряжений на отгрузку за период.
		|Какие товары были заказаны, находятся в пути, требуют отгрузки?';
		|en = 'Analysis of shipment reference indicators for the period.
		|What goods were ordered, are in transit, or require shipment?'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ВедомостьПоРаспоряжениямНаВозврат");
	ОписаниеВарианта.Включен = Истина;
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ показателей распоряжений на возврат за период.
		|Какие товары были заявлены к возврату, ожидают приемки, возвращены?';
		|en = 'Analysis of return reference indicators for the period.
		|Which goods are requested for return, pending receipt, or returned?'");
	ОписаниеВарианта.ФункциональныеОпции = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Метаданные.ФункциональныеОпции.ИспользоватьЗаявкиНаВозвратТоваровОтКлиентов.Имя);
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОтмененныеСтрокиРаспоряжений");
	ОписаниеВарианта.Включен = Истина;
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ отмененных строк распоряжений на отгрузку и возврат за период.
		|Какие товары были отменены, в каком количестве и на какую сумму?';
		|en = 'Analysis of cancelled shipment and return reference lines for the period.
		|What goods were canceled, in what quantity, and for what amount?'");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли