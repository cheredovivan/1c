#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
// 	СтрокаТаблицыЗначений -
//
Функция ДобавитьКомандуОтчетаПоОС(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СправкаРасчетАмортизацииОС_МУ)
		И ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4()
		И НЕ ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СправкаРасчетАмортизацииОС_МУ.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Справка-расчет амортизации';
											|en = 'Sheet of fixed assets depreciation charges'");
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.МножественныйВыбор = Истина;
		КомандаОтчет.ДополнительныеПараметры.Вставить("ИмяКоманды", "СправкаРасчетПоОС");
		КомандаОтчет.КлючВарианта = "СправкаРасчетАмортизацииОС";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений - Настройки команды.
Функция ДобавитьКомандуОтчетаПоДокументуАмортизацииОС(КомандыОтчетов) Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.СправкаРасчетАмортизацииОС_МУ)
		И ВнеоборотныеАктивы.ИспользуетсяУправлениеВНА_2_4()
		И НЕ ВнеоборотныеАктивыСлужебный.ВедетсяРегламентированныйУчетВНА() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.СправкаРасчетАмортизацииОС_МУ.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Справка-расчет амортизации';
											|en = 'Sheet of fixed assets depreciation charges'");
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.КлючВарианта = "СправкаРасчетПоДокументуАмортизацииОСКонтекст";
		КомандаОтчет.ДополнительныеПараметры.Вставить("ИмяКоманды", "СправкаРасчетПоДокументуАмортизацииОС");
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   ВедетсяРегламентированныйУчетВНА - Булево.
//
Процедура НастроитьВариантыОтчета(Настройки, ВедетсяРегламентированныйУчетВНА) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.СправкаРасчетАмортизацииОС_МУ);
	ОписаниеОтчета.Включен = НЕ ВедетсяРегламентированныйУчетВНА;
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Включен = НЕ ВедетсяРегламентированныйУчетВНА;
	ОписаниеВарианта.Описание = НСтр("ru = 'По какой формуле рассчитана амортизация?';
									|en = 'Analyze accrued depreciation of the assets.'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "СправкаРасчетАмортизацииОС");
	ОписаниеВарианта.Включен = НЕ ВедетсяРегламентированныйУчетВНА;
	ОписаниеВарианта.Описание = НСтр("ru = 'По какой формуле рассчитана амортизация?';
									|en = 'Analyze accrued depreciation of the assets.'");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "СправкаРасчетПоДокументуАмортизацииОСКонтекст");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли