#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПользовательскиеНастройкиМодифицированы = Ложь;
	
	// Сформируем отчет
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	СписокПолей = Новый СписокЗначений();
	СоответствиеГруппировок = Новый Соответствие();
	УстановленныеКодыОКВЭД = ПолучитьУстановленныеКодыОКВЭД(НастройкиОтчета, СписокПолей, СоответствиеГруппировок);
	
	ПолеНоменклатура = СписокПолей.НайтиПоЗначению("Номенклатура");
	Если ПолеНоменклатура <> Неопределено Тогда
		НомерПоляНоменклатура = СписокПолей.Индекс(ПолеНоменклатура) + 1;
	Иначе
		НомерПоляНоменклатура = 0;
	КонецЕсли;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ИнициализироватьВременныеТаблицы(МенеджерВременныхТаблиц, УстановленныеКодыОКВЭД, СписокПолей);
	
	ДоработатьТекстЗапроса(СписокПолей, СхемаКомпоновкиДанных.НаборыДанных.ОсновныеДанные.Запрос);
	ДоработатьСхемуКомпоновкиДанных(СписокПолей, СоответствиеГруппировок, НомерПоляНоменклатура);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки,,, МенеджерВременныхТаблиц);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	// Сообщим форме отчета, что настройки модифицированы
	Если ПользовательскиеНастройкиМодифицированы Тогда
		КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ПользовательскиеНастройкиМодифицированы", Истина);
	КонецЕсли;
	
КонецПроцедуры

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт

	Настройки.События.ПриОпределенииПараметровВыбора = Истина;
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
	Настройки.События.ПриЗагрузкеПользовательскихНастроекНаСервере = Истина;

КонецПроцедуры

// Вызывается в форме отчета перед выводом настройки.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   СвойстваНастройки - Структура - Описание настройки отчета, которая будет выведена в форме отчета:
//       * ОписаниеТипов - ОписаниеТипов -
//           Тип настройки.
//       * ЗначенияДляВыбора - СписокЗначений -
//           Объекты, которые будут предложены пользователю в списке выбора.
//           Дополняет список объектов, уже выбранных пользователем ранее.
//       * ЗапросЗначенийВыбора - Запрос -
//           Возвращает объекты, которыми необходимо дополнить ЗначенияДляВыбора.
//           Первой колонкой (с 0м индексом) должен выбираться объект,
//           который следует добавить в ЗначенияДляВыбора.Значение.
//           Для отключения автозаполнения
//           в свойство ЗапросЗначенийВыбора.Текст следует записать пустую строку.
//       * ОграничиватьВыборУказаннымиЗначениями - Булево -
//           Когда Истина, то выбор пользователя будет ограничен значениями,
//           указанными в ЗначенияДляВыбора (его конечным состоянием).
//
Процедура ПриОпределенииПараметровВыбора(Форма, СвойстваНастройки) Экспорт
	
	Если СвойстваНастройки.Тип = "ЗначениеПараметраНастроек"
		И СвойстваНастройки.ПолеКД = Новый ПолеКомпоновкиДанных("ПараметрыДанных.Организация") Тогда

		ЗаполнитьСписокВыбораОрганизаций(СвойстваНастройки);

	КонецЕсли;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета и формы настройки отчета.
// См. "Расширение управляемой формы для отчета.ПередЗагрузкойВариантаНаСервере" в синтакс-помощнике.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - форма отчета или настроек отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки для загрузки в компоновщик настроек.
//
Процедура ПередЗагрузкойВариантаНаСервере(ЭтаФорма, НовыеНастройкиКД) Экспорт

	НовыеНастройкиКД.ДополнительныеСвойства.Вставить("КлючТекущегоВарианта", ЭтаФорма.КлючТекущегоВарианта);

КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета, состоит из:
//   *Параметры - ДанныеФормыСтруктура - состоит из:
//    **ДатаОкончания - Дата -
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных -
//       Пользовательские настройки для загрузки в компоновщик настроек.
//
// См. синтакс-помощник "Расширение управляемой формы для отчета.ПриЗагрузкеПользовательскихНастроекНаСервере"
//    в синтакс-помощнике.
//
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Форма, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если Форма.Параметры.Свойство("Организация") Тогда
		СписокОП = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьСписокОбособленныхПодразделенийОрганизации(Форма.Параметры.Организация);
		ЕстьОбособленныеПодразделения = СписокОП.Количество() > 0;
		Ключ  = СтрЗаменить(Строка(ЕстьОбособленныеПодразделения) + Форма.Параметры.Организация.УникальныйИдентификатор(), "-", "");
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек.Настройки,
			"Организация",
			Ключ);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек.ПользовательскиеНастройки,
			"Организация",
			Ключ);
	ИначеЕсли НовыеПользовательскиеНастройкиКД <> Неопределено Тогда
		ПараметрОрганизация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
			НовыеПользовательскиеНастройкиКД, "Организация").Значение; // Строка - Ключ - СоответствиеОрганизаций
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек.Настройки,
			"Организация",
			ПараметрОрганизация);
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек.ПользовательскиеНастройки,
			"Организация",
			ПараметрОрганизация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет список выбора организациями с обособленными подразделениями.
//
// Параметры: 
//  СвойстваНастройки - Строка - Поле для выбора организации.
//  СоответствиеОрганизаций - Структура - содержит список организаций с обособленными подразделениями.
//
Процедура ЗаполнитьСписокВыбораОрганизаций(СвойстваНастройки)
	
	СоответствиеОрганизацийДляВыбора = СоответствиеОрганизацийДляВыбора();
	
	СвойстваНастройки.ЗначенияДляВыбора.Очистить();

	Для каждого ТекущаяОрганизация Из СоответствиеОрганизацийДляВыбора Цикл

		ОрганизацияПредставление = Строка(ТекущаяОрганизация.Значение.Организация);

		Если ТекущаяОрганизация.Значение.ВключатьОбособленныеПодразделения Тогда
			ОрганизацияПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 с обособленными подразделениями';
					|en = '%1 with branch offices'"),
				ОрганизацияПредставление);
		Иначе
			ОрганизацияПредставление = Строка(ТекущаяОрганизация.Значение.Организация);
		КонецЕсли;

		СвойстваНастройки.ЗначенияДляВыбора.Добавить(ТекущаяОрганизация.Ключ, ОрганизацияПредставление);

	КонецЦикла;

КонецПроцедуры

Функция СоответствиеОрганизацийДляВыбора()

	СоответствиеОрганизацийДляВыбора = Новый Структура;
	
	ИспользоватьУправленческуюОрганизацию = БухгалтерскийУчетПереопределяемый.ИспользоватьУправленческуюОрганизацию();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИспользоватьУправленческуюОрганизацию", ИспользоватьУправленческуюОрганизацию);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НаборОрганизаций.Организация КАК Организация,
	|	НаборОрганизаций.ОрганизацияПредставление КАК ОрганизацияПредставление,
	|	НаборОрганизаций.ВключатьОбособленныеПодразделения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Организации.Ссылка КАК Организация,
	|		Организации.Наименование КАК ОрганизацияПредставление,
	|		ЛОЖЬ КАК ВключатьОбособленныеПодразделения
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		НЕ Организации.Предопределенный ИЛИ &ИспользоватьУправленческуюОрганизацию
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Организации.ГоловнаяОрганизация,
	|		Организации.ГоловнаяОрганизация.Наименование,
	|		ИСТИНА
	|	ИЗ
	|		Справочник.Организации КАК Организации
	|	ГДЕ
	|		Организации.ОбособленноеПодразделение) КАК НаборОрганизаций
	|УПОРЯДОЧИТЬ ПО
	|	ОрганизацияПредставление";

	Выборка = Запрос.Выполнить().Выбрать();

	МаксКоличествоСимволов = 40;
	Пока Выборка.Следующий() Цикл
		Ключ     = СтрЗаменить(Строка(Выборка.ВключатьОбособленныеПодразделения) + Выборка.Организация.УникальныйИдентификатор(), "-", "");
		Значение = Новый Структура("Организация, ВключатьОбособленныеПодразделения", Выборка.Организация, Выборка.ВключатьОбособленныеПодразделения);
		СоответствиеОрганизацийДляВыбора.Вставить(Ключ, Значение);
	КонецЦикла;

	Возврат СоответствиеОрганизацийДляВыбора;
	
КонецФункции

Функция ПолучитьУстановленныеКодыОКВЭД(НастройкиОтчета, СписокПолей, СоответствиеГруппировок)
	
	ПараметрОрганизация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
			КомпоновщикНастроек.ПользовательскиеНастройки, "Организация").Значение; // Строка - Ключ - СоответствиеОрганизаций
			
	СоответствиеОрганизацийДляВыбора = СоответствиеОрганизацийДляВыбора();
		
	ВключатьОбособленныеПодразделения = Ложь;
	Если ЗначениеЗаполнено(ПараметрОрганизация) Тогда
		Если НЕ ТипЗнч(ПараметрОрганизация) = Тип("СправочникСсылка.Организации") 
			И СоответствиеОрганизацийДляВыбора.Свойство(ПараметрОрганизация) Тогда
			СоответствиеОрганизаций = СоответствиеОрганизацийДляВыбора[ПараметрОрганизация];
			Организация = СоответствиеОрганизаций.Организация;  
			ВключатьОбособленныеПодразделения = СоответствиеОрганизаций.ВключатьОбособленныеПодразделения;
		Иначе
			Организация = Справочники.Организации.ПустаяСсылка();
		КонецЕсли;
	Иначе
		Организация = Справочники.Организации.ПустаяСсылка();
	КонецЕсли;
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "ОрганизацияСправочник",             Организация);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НастройкиОтчета, "ВключатьОбособленныеПодразделения", ВключатьОбособленныеПодразделения);
		
	Период = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		НастройкиОтчета,
		"Период").Значение;
	
	СохраненныеНастройки = РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ПолучитьНастройкиОКВЭД(Организация, Период.ДатаОкончания);
	СписокГруппировок = КомпоновкаДанныхКлиентСервер.ПолучитьГруппировки(КомпоновщикНастроек);
	
	НомерПоля = 1;
	
	Для Каждого ГруппировкаКомпоновкиДанных Из СписокГруппировок Цикл
		
		ПолеГруппировки = ""; 
		
		Если ГруппировкаКомпоновкиДанных.Значение.Использование Тогда
			
			ЭлементыГруппировки = ГруппировкаКомпоновкиДанных.Значение.ПоляГруппировки.Элементы;
		
			Если ЭлементыГруппировки.Количество() > 0 Тогда
				
				ЭлементГруппировки = ЭлементыГруппировки[0];
				Если ЭлементГруппировки.Использование Тогда
					
					ПолеГруппировки = ЭлементГруппировки.Поле;
					
					Если СтрНайти(Строка(ПолеГруппировки), "ОКВЭД") > 0 Тогда
						Прервать ; // Настройка вывода итоговых данных по ОКВЭД
					КонецЕсли;
					
					СписокПолей.Добавить(Строка(ПолеГруппировки));
					СоответствиеГруппировок.Вставить("Поле"+НомерПоля,СтрЗаменить(Строка(ПолеГруппировки), ".", ""));
					
					НомерПоля = НомерПоля + 1;
					
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЦикла;
	
	СтрокаПравилОтчета = СтрСоединить(СписокПолей.ВыгрузитьЗначения(), ",");
	
	КоличествоГруппировок = СписокПолей.Количество();
	Если СохраненныеНастройки = Неопределено Тогда
		УстановленныеКоды = ТаблицаНастроек(КоличествоГруппировок);
		СтрокаПравилНастройки = СтрокаПравилОтчета;
		Если ЗначениеЗаполнено(Организация) Тогда
			ДобавитьНастройкиПоОрганизации(Организация, Период.ДатаНачала, УстановленныеКоды, СтрокаПравилНастройки); // Техническая запись для ОКВЭД организации
		КонецЕсли;
	Иначе
		УстановленныеКоды = СохраненныеНастройки.УстановленныеКоды;
		СтрокаПравилНастройки = СохраненныеНастройки.СтрокаПравилНастройки;
		Если СтрокаПравилОтчета <> СтрокаПравилНастройки Тогда // Получаем ОКВЭД только, если группировки отчета и сохраненных настроек совпадают
			УстановленныеКоды = ТаблицаНастроек(КоличествоГруппировок);
		КонецЕсли;
	КонецЕсли;
		
	ПереименоватьКолонки(УстановленныеКоды, СоответствиеГруппировок);
	
	Возврат УстановленныеКоды;
	
КонецФункции

Функция ТаблицаНастроек(КоличествоПолей)
	
	ТаблицаНастроек = Новый ТаблицаЗначений();
	Массив = Новый Массив;
	Массив.Добавить(Тип("СправочникСсылка.Номенклатура"));
	НомерПоля = 1;
	Пока НомерПоля <= КоличествоПолей Цикл
		ТаблицаНастроек.Колонки.Добавить("Поле"+НомерПоля, Новый ОписаниеТипов(Массив));
		НомерПоля = НомерПоля + 1;
	КонецЦикла;
	
	Массив.Очистить();
	Массив.Добавить(Тип("Число"));
	ТаблицаНастроек.Колонки.Добавить("Приоритет",         Новый ОписаниеТипов(Массив));
	
	Массив.Очистить();
	Массив.Добавить(Тип("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ОКВЭД",             Новый ОписаниеТипов(Массив));
	ТаблицаНастроек.Колонки.Добавить("ОКВЭДНаименование", Новый ОписаниеТипов(Массив));
		
	Возврат ТаблицаНастроек;
	
КонецФункции

Процедура ПереименоватьКолонки(Таблица, СоответвиеГруппировок)
	
	Для Каждого Колонка Из Таблица.Колонки Цикл
		
		НовоеИмя = СоответвиеГруппировок[Колонка.Имя];
		Если НовоеИмя <> Неопределено Тогда
			Колонка.Имя = НовоеИмя;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьНастройкиПоОрганизации(Организация, Период, УстановленныеКоды, СтрокаПравилНастройки)
	
	Настройка = Новый Структура();
	Настройка.Вставить("СтрокаПравилНастройки", СтрокаПравилНастройки);
	
	Настройка.Вставить("УстановленныеКоды", УстановленныеКоды);
	НастройкаСтрокой = ОбщегоНазначения.ЗначениеВСтрокуXML(Настройка);
	РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ДобавитьНастройкиПоОрганизации(Организация, Период, НастройкаСтрокой);
	УстановленныеКоды.Очистить();
	
КонецПроцедуры

Процедура ИнициализироватьВременныеТаблицы(МенеджерВременныхТаблиц, УстановленныеКодыОКВЭД, Группировки)
	
	Запрос = Новый Запрос;
	Если УстановленныеКодыОКВЭД.Количество() > 0 Тогда // Определим типы колонок таблицы по их значениям
		УстановленныеКодыОКВЭД = ТипизироватьКолонкиТаблицы(УстановленныеКодыОКВЭД, Группировки);
	КонецЕсли;
	Запрос.УстановитьПараметр("УстановленныеКодыОКВЭД",УстановленныеКодыОКВЭД);
	
	Запрос.Текст ="ВЫБРАТЬ
	              |	УстановленныеКодыОКВЭД.Поле КАК Поле,
	              |	УстановленныеКодыОКВЭД.Приоритет КАК Приоритет,
	              |	УстановленныеКодыОКВЭД.ОКВЭД КАК ОКВЭД,
	              |	УстановленныеКодыОКВЭД.ОКВЭДНаименование КАК ОКВЭДНаименование
	              |ПОМЕСТИТЬ ВТ_УстановленныеКодыОКВЭД
	              |ИЗ
	              |	&УстановленныеКодыОКВЭД КАК УстановленныеКодыОКВЭД";
	
	// Переименуем поля запроса в соответвии с Группировками данных
	СтрокаПоляЗапроса = "";
	СтрокаПоиска = " УстановленныеКодыОКВЭД.Поле КАК Поле,"; 
	
	Для Каждого Строка Из Группировки Цикл
		ПолеЗапроса = СтрЗаменить(СтрокаПоиска, "Поле", СтрЗаменить(Строка.Значение, ".", ""));
		СтрокаПоляЗапроса = СтрокаПоляЗапроса + ПолеЗапроса;
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, СокрЛП(СтрокаПоиска), СтрокаПоляЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ДоработатьТекстЗапроса(Группировки, Текст)
	
	Приоритет = 1;
	ТекстСвязи = " ПО ";
	СвязиГруппировок = Новый Структура();
	Для Каждого Строка Из Группировки Цикл
		
		ТекстСвязи = ТекстСвязи + ?(Приоритет > 1, " И ", "") + "ДанныеВыручки." + Строка.Значение + " = КодыОКВЭДПриоритет."+ СтрЗаменить(Строка.Значение, ".", "");
		СвязиГруппировок.Вставить(СтрЗаменить(Строка.Значение, ".", ""), ТекстСвязи);
		Приоритет = Приоритет + 1;
		
	КонецЦикла;
	
	Приоритет = 1;
	ТекстЗамены = "";
	ПоляЗамены = "";
	ПоляКомпоновки = "";
	Для Каждого Строка Из Группировки Цикл
		
		НазваниеТаблицы = "КодыОКВЭДПриоритет" + Приоритет;
		ТекстСвязи = СтрЗаменить(СвязиГруппировок[СтрЗаменить(Строка.Значение, ".", "")], "КодыОКВЭДПриоритет", НазваниеТаблицы);
		Связь = " ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УстановленныеКодыОКВЭД КАК КодыОКВЭДПриоритет" + Приоритет;
		
		ТекстЗамены = ТекстЗамены + Связь + ТекстСвязи + " И " + НазваниеТаблицы + ".Приоритет = " + Приоритет;
		
		ПолеИсходное = "КодыОКВЭДПриоритет.ОКВЭД КАК ОКВЭД";
		ПоляЗамены = ПоляЗамены + СтрЗаменить(ПолеИсходное, "КодыОКВЭДПриоритет", НазваниеТаблицы) + Приоритет + ", ";
		
		ПолеИсходное = "КодыОКВЭДПриоритет.ОКВЭДНаименование КАК ОКВЭДНаименование";
		ПоляЗамены = ПоляЗамены + СтрЗаменить(ПолеИсходное, "КодыОКВЭДПриоритет", НазваниеТаблицы) + Приоритет + ", ";
		
		ПоляКомпоновки = ПоляКомпоновки + "ОКВЭД" + Приоритет + ", " + "ОКВЭДНаименование" + Приоритет + ", ";
		
		Приоритет = Приоритет + 1;
		
	КонецЦикла;
	
	СтрокаПоиска = " ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УстановленныеКодыОКВЭД КАК КодыОКВЭДПриоритет ПО (ИСТИНА)";
	Текст = СтрЗаменить(Текст, СокрЛП(СтрокаПоиска), ТекстЗамены);
	
	СтрокаПоиска = " КодыОКВЭДПриоритет.ОКВЭД КАК ОКВЭД,";
	Текст = СтрЗаменить(Текст, СокрЛП(СтрокаПоиска), ПоляЗамены);
	
	СтрокаПоиска = "ОКВЭД.*,";
	Текст = СтрЗаменить(Текст, СокрЛП(СтрокаПоиска), ПоляКомпоновки);
	
КонецПроцедуры

Функция ТипизироватьКолонкиТаблицы(УстановленныеКодыОКВЭД, Группировки)
	
	ТаблицаНастроек = Новый ТаблицаЗначений();
	Массив = Новый Массив;
	Приоритет = Группировки.Количество();
	КоличествоКолонок = УстановленныеКодыОКВЭД.Колонки.Количество();
	СписокОбработанныхКолонок = Новый СписокЗначений();
	Пока Приоритет > 0 Цикл
		Если КоличествоКолонок = СписокОбработанныхКолонок.Количество() Тогда
			Прервать;
		КонецЕсли;
		Отбор = Новый Структура();
		Отбор.Вставить("Приоритет", Приоритет);
		СтрокиПриоритета = УстановленныеКодыОКВЭД.НайтиСтроки(Отбор);
		Если СтрокиПриоритета.Количество() <> 0 Тогда
			Для Каждого Колонка Из УстановленныеКодыОКВЭД.Колонки Цикл
				Если КоличествоКолонок = СписокОбработанныхКолонок.Количество() Тогда
					Прервать;
				КонецЕсли;
				Если СписокОбработанныхКолонок.НайтиПоЗначению(Колонка.Имя) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого Строка Из СтрокиПриоритета Цикл
					Если КоличествоКолонок = СписокОбработанныхКолонок.Количество() Тогда
						Прервать;
					КонецЕсли;
					Если Строка[Колонка.Имя] <> Неопределено Тогда
						СписокОбработанныхКолонок.Добавить(Колонка.Имя);
						Массив.Очистить();
						Массив.Добавить(ТипЗнч(Строка[Колонка.Имя]));
						ТаблицаНастроек.Колонки.Добавить(Колонка.Имя, Новый ОписаниеТипов(Массив));
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Приоритет = Приоритет - 1;
	КонецЦикла;
	
	МассивКУдалению = Новый Массив;
	Для Каждого Группировка Из Группировки Цикл
		Если СписокОбработанныхКолонок.НайтиПоЗначению(СтрЗаменить(Группировка.Значение, ".", "")) = Неопределено Тогда
			МассивКУдалению.Добавить(Группировка); // Для группировки не задано значение
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Строка Из МассивКУдалению Цикл
		Группировки.Удалить(Строка);
	КонецЦикла;
	
	Для Каждого СтараяСтрока Из УстановленныеКодыОКВЭД Цикл
		НоваяСтрока = ТаблицаНастроек.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтараяСтрока);
	КонецЦикла;
		
	Возврат ТаблицаНастроек;
	
КонецФункции

Процедура ДоработатьСхемуКомпоновкиДанных(СписокПолей, СоответствиеГруппировок, НомерПоляНоменклатура)
	
	КоличествоГруппировок = СоответствиеГруппировок.Количество();
	
	// Настроим итоги в ресурсах под группировки отчета
	МассивКУдалению = Новый Массив;
	ПолеИтогаУстановлен = СхемаКомпоновкиДанных.ПоляИтога.Найти("УстановленОКВЭД");
	ПолеИтогаОКВЭД = СхемаКомпоновкиДанных.ПоляИтога.Найти("КодОКВЭД");
	Номер = 5;
	Пока Номер > КоличествоГруппировок Цикл
		
		Если ПолеИтогаУстановлен <> Неопределено Тогда
			СтрокаЗамены = "Когда Уровень() = 4 Тогда УстановленоПоле5";
			СтрокаЗамены = СтрЗаменить(СтрокаЗамены, "4", Номер - 1);
			СтрокаЗамены = СтрЗаменить(СтрокаЗамены, "5", Номер);
			ПолеИтогаУстановлен.Выражение = СтрЗаменить(ПолеИтогаУстановлен.Выражение, СтрокаЗамены, "");
		КонецЕсли;
		
		Если ПолеИтогаОКВЭД <> Неопределено Тогда
			СтрокаЗамены = "Когда Уровень() = 4 Тогда ОКВЭДПоле5";
			СтрокаЗамены = СтрЗаменить(СтрокаЗамены, "4", Номер - 1);
			СтрокаЗамены = СтрЗаменить(СтрокаЗамены, "5", Номер);
			ПолеИтогаОКВЭД.Выражение = СтрЗаменить(ПолеИтогаОКВЭД.Выражение, СтрокаЗамены, "");
		КонецЕсли;
		
		Номер = Номер - 1;
		
	КонецЦикла;
	
	Номер = КоличествоГруппировок + 1; // Количество группировок из отчета + 1
	КоличествоГруппировокУстановленногоОКВЭД = СписокПолей.Количество();
	Номер = КоличествоГруппировокУстановленногоОКВЭД + 1; // Количество группировок из отчета + 1
	ПолеЗамены = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("ОКВЭДПоле" + КоличествоГруппировокУстановленногоОКВЭД);
	МассивКУдалению = Новый Массив;
	Пока Номер <= 5 Цикл
		
		ИмяПоля = "ОКВЭДПоле" + Номер;
		ПолеКомпоновки = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(ИмяПоля);
		Если ПолеКомпоновки <> Неопределено И Номер > КоличествоГруппировок Тогда
			МассивКУдалению.Добавить(ПолеКомпоновки); // Если количество полей группировки меньше 5 (ограничение проекта), удалим лишние строки из Схемы
		КонецЕсли;
		Если ПолеЗамены <> Неопределено Тогда
			ПолеКомпоновки.Выражение = ПолеЗамены.Выражение;
		КонецЕсли;
		
		ИмяПоля = "УстановленоПоле" + Номер;
		ПолеКомпоновки = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти(ИмяПоля);
		Если ПолеКомпоновки <> Неопределено И Номер > КоличествоГруппировок Тогда
			МассивКУдалению.Добавить(ПолеКомпоновки); // Если количество полей группировки меньше 5 (ограничение проекта), удалим лишние строки из Схемы
		КонецЕсли;
		Если ПолеЗамены <> Неопределено Тогда
			ПолеКомпоновки.Выражение = "ЛОЖЬ";
		КонецЕсли;
		
		Номер = Номер + 1;
		
	КонецЦикла;
	
	Для Каждого Запись Из МассивКУдалению Цикл
		СхемаКомпоновкиДанных.ВычисляемыеПоля.Удалить(Запись);
	КонецЦикла;
	
	// Если используются контролируемые сделки, для неустановленных строк по услугам ОКВЭД берем из Номенклатуры
	ИспользоватьУведомленияОКонтролируемыхСделках = Константы.ИспользоватьУведомленияОКонтролируемыхСделках.Получить();
	Если НомерПоляНоменклатура > 0 И ИспользоватьУведомленияОКонтролируемыхСделках Тогда
		Для Каждого ВычисляемогоПоля Из СхемаКомпоновкиДанных.ВычисляемыеПоля Цикл
			Если ВычисляемогоПоля.ПутьКДанным = "ОКВЭДПоле"+НомерПоляНоменклатура Тогда
				СтрокаПоиска = "КОГДА НЕ ОКВЭД4 ЕСТЬ NULL ТОГДА ОКВЭД4+ "" ""+ ОКВЭДНаименование4";
				// ОКВЭД может быть назначен только на группировки верхних уровней
				Если НомерПоляНоменклатура > КоличествоГруппировокУстановленногоОКВЭД Тогда
					ПолеПоиска = КоличествоГруппировокУстановленногоОКВЭД;
					СтрокаПоиска = СтрЗаменить(СтрокаПоиска, 4, ПолеПоиска);
					СтрокаЗамены = " КОГДА ЭтоУслуга И ОКВЭДНоменклатурыКод <> """" ТОГДА ОКВЭДНоменклатурыКод + "" "" + ОКВЭДНоменклатурыНаименование " + СтрокаПоиска;
				Иначе
					ПолеПоиска = НомерПоляНоменклатура;
					СтрокаПоиска = СтрЗаменить(СтрокаПоиска, 4, ПолеПоиска);
					СтрокаЗамены = СтрокаПоиска + " КОГДА ЭтоУслуга И ОКВЭДНоменклатурыКод <> """" ТОГДА ОКВЭДНоменклатурыКод + "" "" + ОКВЭДНоменклатурыНаименование ";
				КонецЕсли;
				ВычисляемогоПоля.Выражение = СтрЗаменить(ВычисляемогоПоля.Выражение, СтрокаПоиска, СтрокаЗамены);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Переопределим поле КодОКВЭДИтог
	Поле = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("КодОКВЭДИтог");
	Если Поле <> Неопределено Тогда
		ПолеЗамены = СхемаКомпоновкиДанных.ВычисляемыеПоля.Найти("ОКВЭДПоле" + КоличествоГруппировок);
		Если ПолеЗамены <> Неопределено Тогда
			Поле.Выражение = ПолеЗамены.Выражение;
		КонецЕсли;
	КонецЕсли;
	
	// Добавим поля ОКВЭД по количеству группировок
	ТаблицаПолейИзСКД = СхемаКомпоновкиДанных.НаборыДанных.ОсновныеДанные.Поля;
	ПолеСКД = ТаблицаПолейИзСКД.Найти("ОКВЭД");
	Если ПолеСКД <> Неопределено Тогда
		Приоритет = 1;
		Пока Приоритет <= КоличествоГруппировок Цикл
			
			НоваяСтрока = СхемаКомпоновкиДанных.НаборыДанных.ОсновныеДанные.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПолеСКД);
			ИмяПоля = "ОКВЭД"+Приоритет;
			ПолеСКД.Заголовок = ИмяПоля;
			ПолеСКД.Поле = ИмяПоля;
			ПолеСКД.ПутьКДанным = ИмяПоля;
			
			НоваяСтрока = СхемаКомпоновкиДанных.НаборыДанных.ОсновныеДанные.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ПолеСКД);
			ИмяПоля = "ОКВЭДНаименование"+Приоритет;
			ПолеСКД.Заголовок = ИмяПоля;
			ПолеСКД.Поле = ИмяПоля;
			ПолеСКД.ПутьКДанным = ИмяПоля;
			
			Приоритет = Приоритет + 1;
		КонецЦикла;
		СхемаКомпоновкиДанных.НаборыДанных.ОсновныеДанные.Поля.Удалить(ПолеСКД);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли