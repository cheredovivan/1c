
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокГруппировокКомпоновки = КомпоновкаДанныхКлиентСервер.ПолучитьГруппировки(Параметры.КомпоновщикНастроек);
	
	Для Каждого ГруппировкаКомпоновкиДанных Из СписокГруппировокКомпоновки Цикл
		
		Если ГруппировкаКомпоновкиДанных.Значение.Использование Тогда
			ЭлементыГруппировки = ГруппировкаКомпоновкиДанных.Значение.ПоляГруппировки.Элементы;
		
			Если ЭлементыГруппировки.Количество() > 0 Тогда
			
				ЭлементГруппировки = ЭлементыГруппировки[0];
				Если ЭлементГруппировки.Использование Тогда
					
					ПолеГруппировки = ЭлементГруппировки.Поле;
					
					Если СтрНайти(Строка(ПолеГруппировки), "ОКВЭД") > 0 Тогда
						Прервать ; // Настройка вывода итоговых данных по ОКВЭД
					КонецЕсли;
					
					Группировки.Добавить(Строка(ПолеГруппировки));
				
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДанныеРасшифровки = ПолучитьИзВременногоХранилища(Параметры.АдресРасшифровки);
	ЭлементРасшифровки = ДанныеРасшифровки.Элементы[Параметры.ИдентификаторРасшифровки];

	ПоляРасшифровкиРодителей = Новый Структура;
	Для каждого Родитель Из ЭлементРасшифровки.ПолучитьРодителей() Цикл
		ЗаполнитьПоляРодительскихЭлементовРасшифровки(Родитель, ПоляРасшифровкиРодителей);
	КонецЦикла;
	
	Приоритет = ПоляРасшифровкиРодителей.Количество();
	
	Организация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		ДанныеРасшифровки.Настройки,
		"ОрганизацияСправочник").Значение;
		
	ОбъектНаблюденияПараметр = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		ДанныеРасшифровки.Настройки,
		"ОбъектНаблюдения");
		
	Если ОбъектНаблюденияПараметр <> Неопределено Тогда
		ОбъектНаблюдения = ОбъектНаблюденияПараметр.Значение;
	КонецЕсли;
		
	ДатаОкончания = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		ДанныеРасшифровки.Настройки,
		"Период").Значение.ДатаОкончания;
		
	Период = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(
		ДанныеРасшифровки.Настройки,
		"Период").Значение.ДатаНачала;
		
	ДобавляемыеРеквизиты = Новый Массив;
	КоличествоГруппировок = ПоляРасшифровкиРодителей.Количество();
	
	// Создание реквизитов
	НомерПоля = 1;
	Для Каждого Родитель Из ПоляРасшифровкиРодителей Цикл
		Если Родитель.Ключ = "КодОКВЭДИтог" Тогда
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		МассивТипов = Новый Массив();
		МассивТипов.Добавить(ТипЗнч(Родитель.Значение));
		НовыйРеквизит = Новый РеквизитФормы("Поле" + КоличествоГруппировок, Новый ОписаниеТипов(МассивТипов));
		КоличествоГруппировок = КоличествоГруппировок - 1;
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
	КонецЦикла;
	
	Если Не Отказ Тогда
		
		ЭтотОбъект.ИзменитьРеквизиты(ДобавляемыеРеквизиты); // Применяем реквизиты
		
		// Создание полей
		КоличествоГруппировок = ПоляРасшифровкиРодителей.Количество(); 
		СледующийЭлемент = "КодОКВЭД";
		
		Для Каждого Родитель Из ПоляРасшифровкиРодителей Цикл
			
			ИмяРеквизита = "Поле" + КоличествоГруппировок;
			НовыйЭлемент = ЭтаФорма.Элементы.Вставить(ИмяРеквизита, Тип("ПолеФормы"),, Элементы[СледующийЭлемент]);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.ПутьКДанным = ИмяРеквизита;
			НовыйЭлемент.ТолькоПросмотр = Истина;
			
			Если ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Родитель.Значение)) Тогда
				Если Не ОбщегоНазначения.ЭтоПеречисление(Родитель.Значение.Метаданные()) Тогда
					НовыйЭлемент.Заголовок = Родитель.Значение.Метаданные().ПредставлениеОбъекта;
				Иначе
					Если ТипЗнч(Родитель.Значение) = Тип("ПеречислениеСсылка.ТипыНоменклатуры") Тогда
						НовыйЭлемент.Заголовок = "Тип номенклатуры";
					Иначе
						НовыйЭлемент.Заголовок = Родитель.Значение.Метаданные().Синоним;
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли Родитель.Значение = Неопределено Тогда
				НовыйЭлемент.Видимость = Ложь;
			Иначе
				НовыйЭлемент.Заголовок = Родитель.Ключ;
			КонецЕсли;
			
			КоличествоГруппировок = КоличествоГруппировок - 1;
			ЭтотОбъект[ИмяРеквизита] = Родитель.Значение;
			СледующийЭлемент = ИмяРеквизита;
			
		КонецЦикла;
		
		Заголовок = НСтр("ru = 'Установка кода ОКВЭД';
						|en = 'Установка кода ОКВЭД'") + НСтр("ru = ' на ';
																|en = ' to'") + Формат(Период, "ДЛФ=D");
		
		Настройка = РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ПолучитьНастройкиОКВЭД(Организация, Период);
		
		Если Настройка = Неопределено Тогда
			ГруппировкиСовпадают = Истина;
		Иначе
			Если Настройка.СтрокаПравилНастройки = "" Тогда
				ГруппировкиСовпадают = Истина;
			Иначе
				ГруппировкиНастроекСтрокой = СтрСоединить(Группировки.ВыгрузитьЗначения(), ",");
				ГруппировкиСовпадают = ГруппировкиНастроекСтрокой = Настройка.СтрокаПравилНастройки;
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ГруппировкиСовпадают Тогда
		
		ОповещениеВопрос = Новый ОписаниеОповещения("ОбработчикОповещенияВопрос", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Структура группировок изменена. Коды ОКВЭД, указанные для прежней структуры группировок, будут очищены. Продолжить?';
							|en = 'Структура группировок изменена. Коды ОКВЭД, указанные для прежней структуры группировок, будут очищены. Продолжить?'");
		ПоказатьВопрос(ОповещениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодОКВЭДПриИзменении(Элемент)
	
	НаименованиеОКВЭД();
	
КонецПроцедуры

&НаКлиенте
Процедура КодОКВЭД2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаЗакрытия = Новый ОписаниеОповещения(
		"ПодборИзОКВЭДЗавершение",
		ЭтотОбъект);
	
	ПараметрыФормы = КлассификаторОКВЭДКлиент.НовыйПараметрыВыбора();
	ПараметрыФормы.ОписаниеОповещенияОЗакрытии = ОбработкаЗакрытия;
	ПараметрыФормы.Разрешенные = Истина;
	ПараметрыФормы.ТекущийКод = ОКВЭД;
	
	КлассификаторОКВЭДКлиент.Выбрать(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура КодОКВЭД2АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеПодбора = ПодборОКВЭДСервер(Текст);
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Для Каждого КодОКВЭД Из ДанныеПодбора Цикл
			ДанныеВыбора.Добавить(КодОКВЭД.Код, КодОКВЭД.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если Не ЗначениеЗаполнено(ОКВЭД) Тогда
		
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Поле ""ОКВЭД"" не заполнено';
							|en = 'Поле ""ОКВЭД"" не заполнено'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки,, "ОКВЭД");
			
		Возврат;
		
	КонецЕсли;
	
	СохранитьНаСервере();
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Организация",      Организация);
	ДополнительныеПараметры.Вставить("ДатаОкончания",    ДатаОкончания);
	ДополнительныеПараметры.Вставить("ДатаНачала",       Период);
	ДополнительныеПараметры.Вставить("ОбъектНаблюдения", ОбъектНаблюдения);
	
	ДлительнаяОперация = СохранитьДанные(ДополнительныеПараметры);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СохранитьЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьНаСервере()
	
	Если ГруппировкиСовпадают Тогда
		
		Настройка = РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ПолучитьНастройкиОКВЭД(Организация, Период);
		
		Если Настройка = Неопределено Тогда
			Настройка = СоздатьНастройкуПравил();
		Иначе
			ГруппировкиНастроекСтрокой = СтрСоединить(Группировки.ВыгрузитьЗначения(), ",");
			УстановленныеКоды = Настройка.УстановленныеКоды;
			Если УстановленныеКоды.Количество() > 0 Тогда
				Отбор = ОтборНастроек(Группировки.Количество());
				СтрокиПредыдущихНастроек = УстановленныеКоды.НайтиСтроки(Отбор);
				Если СтрокиПредыдущихНастроек.Количество() > 0 Тогда
					Строка = СтрокиПредыдущихНастроек[0];
				Иначе
					Строка = УстановленныеКоды.Добавить();
				КонецЕсли;
			Иначе
				Настройка.УстановленныеКоды = ТаблицаНастроек(Группировки.Количество());
				Строка = Настройка.УстановленныеКоды.Добавить();
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(Строка, ЭтотОбъект);
			Строка.ОКВЭДНаименование = Элементы.КодОКВЭДРасширеннаяПодсказка.Заголовок;
		КонецЕсли;
		
	Иначе
		
		Настройка = СоздатьНастройкуПравил();
		
	КонецЕсли;
	
	НастройкаСтрокой = ОбщегоНазначения.ЗначениеВСтрокуXML(Настройка);
	
	РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ДобавитьПравилаЗаполненияОКВЭД(Организация, Период, НастройкаСтрокой, Настройка.СтрокаПравилНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	Оповестить("ИзмененКодОКВЭД", Истина);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция СохранитьДанные(ДополнительныеПараметры)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	ИмяФункции = "Отчеты.КлассификацияВыручкиПоОКВЭД.СохранитьДанные";
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
		ИмяФункции, ДополнительныеПараметры);
	
КонецФункции
	
&НаСервереБезКонтекста
Функция ТаблицаНастроек(КоличествоПолей)
	
	ТаблицаНастроек = Новый ТаблицаЗначений();
	
	НомерПоля = 1;
	Пока НомерПоля <= КоличествоПолей Цикл
		ТаблицаНастроек.Колонки.Добавить("Поле"+НомерПоля);
		НомерПоля = НомерПоля + 1;
	КонецЦикла;
	
	Массив = Новый Массив;
	Массив.Добавить(Тип("Число"));
	ТаблицаНастроек.Колонки.Добавить("Приоритет",         Новый ОписаниеТипов(Массив));
	
	Массив.Очистить();
	Массив.Добавить(Тип("Строка"));
	ТаблицаНастроек.Колонки.Добавить("ОКВЭД",             Новый ОписаниеТипов(Массив));
	ТаблицаНастроек.Колонки.Добавить("ОКВЭДНаименование", Новый ОписаниеТипов(Массив));
	
	Возврат ТаблицаНастроек;
	
КонецФункции

&НаСервере
Функция ОтборНастроек(КоличествоПолей)
	
	Отбор = Новый Структура();
	
	НомерПоля = 1;
	Пока НомерПоля <= Приоритет Цикл
		ИмяПоля = "Поле"+НомерПоля;
		Отбор.Вставить(ИмяПоля, ЭтотОбъект[ИмяПоля]);
		НомерПоля = НомерПоля + 1;
	КонецЦикла;
	
	Отбор.Вставить("Приоритет", Приоритет);
	Возврат Отбор;
	
КонецФункции

&НаСервере
Функция СоздатьНастройкуПравил()
	
	Настройка = Новый Структура();
	Настройка.Вставить("СтрокаПравилНастройки", СтрСоединить(Группировки.ВыгрузитьЗначения(), ","));
	УстановленныеКоды = ТаблицаНастроек(Группировки.Количество());
	Строка = УстановленныеКоды.Добавить();
	ЗаполнитьЗначенияСвойств(Строка, ЭтотОбъект);
	Строка.ОКВЭДНаименование = Элементы.КодОКВЭДРасширеннаяПодсказка.Заголовок;
	Настройка.Вставить("УстановленныеКоды", УстановленныеКоды);
		
	Возврат Настройка;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПоляРодительскихЭлементовРасшифровки(ЭлементРасшифровки, ПоляРасшифровкиРодителей)
	
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Для каждого ЗначениеПоля Из ЭлементРасшифровки.ПолучитьПоля() Цикл
			ПоляРасшифровкиРодителей.Вставить(СтрЗаменить(ЗначениеПоля.Поле, ".", ""), ЗначениеПоля.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Для каждого Родитель Из ЭлементРасшифровки.ПолучитьРодителей() Цикл
		ЗаполнитьПоляРодительскихЭлементовРасшифровки(Родитель, ПоляРасшифровкиРодителей);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодборОКВЭДСервер(Знач СтрокаПоиска)
	
	ПараметрыПодбора              = КлассификаторОКВЭД.НовыйОтбор();
	ПараметрыПодбора.СтрокаПоиска = СтрокаПоиска;
	ПараметрыПодбора.Режим        = "КодИНаименование";
	ПараметрыПодбора.Разрешенные  = Истина;
	
	Возврат КлассификаторОКВЭД.Подбор(ПараметрыПодбора);
	
КонецФункции

&НаКлиенте
Процедура ПодборИзОКВЭДЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура")
		Или РезультатВыбора.ВыбранныеЗначения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОКВЭД = РезультатВыбора.ВыбранныеЗначения[0].Код Тогда
		
		ОКВЭД = РезультатВыбора.ВыбранныеЗначения[0].Код;
		Элементы.КодОКВЭДРасширеннаяПодсказка.Заголовок = РезультатВыбора.ВыбранныеЗначения[0].Наименование;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НаименованиеОКВЭД()
	
	Если ЗначениеЗаполнено(ОКВЭД) Тогда
			
		ЗначениеОКВЭДПоКоду = КлассификаторОКВЭД.ЗначениеПоКоду(ОКВЭД, Ложь);
		Если ЗначениеОКВЭДПоКоду.Количество() > 0 Тогда
			
			Элементы.КодОКВЭДРасширеннаяПодсказка.Заголовок = ЗначениеОКВЭДПоКоду[0].Наименование;
			
		КонецЕсли;
		
	Иначе
		
		Элементы.КодОКВЭДРасширеннаяПодсказка.Заголовок = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикОповещенияВопрос(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти