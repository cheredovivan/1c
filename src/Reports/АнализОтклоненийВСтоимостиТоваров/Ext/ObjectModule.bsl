#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Отчет.АнализОтклоненийВСтоимостиТоваров.МодульОбъекта.ПриКомпоновкеРезультата");
	
	СтандартнаяОбработка = Ложь;
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	УстановитьПараметрыОтчета(НастройкиОтчета);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ДокументРезультат.ВысотаТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		НастроитьПараметрыОтчетаПоВариантуОтчета(Контекст.НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура настройки параметров отчета.
//
// Параметры:
//	НастройкиОтчета - Структура - настройки отчета
//	НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки компоновки данных
//	НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки 
//
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
	
	ПредопределенныйВариант = ПолучитьПредопределенныйВариант(НастройкиОтчета.ВариантСсылка);
	
	ПараметрДанныеСебестоимости = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеСебестоимости");
	ПараметрПоПредприятию = СхемаКомпоновкиДанных.Параметры.Найти("ПоПредприятию");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если ПредопределенныйВариант.КлючВарианта = "АнализОтклоненийВСтоимостиТоваровПоПредприятию" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		ПараметрПоПредприятию.Значение = Истина;
		
	Иначе
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		ПараметрПоПредприятию.Значение = Ложь;
		
	КонецЕсли;
	
	ПараметрДанныеСебестоимости.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
	 ИЛИ НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеСебестоимости = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеСебестоимости"));
	НастройкаДанныеОтчета = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеСебестоимости.ИдентификаторПользовательскойНастройки);
	Если Не НастройкаДанныеОтчета = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеОтчета.Значение) = Неопределено Тогда
		НастройкаДанныеОтчета.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПредопределенныйВариант(Знач Вариант)
	
	КлючиВариантов = Новый Массив;
	КлючиВариантов.Добавить("АнализОтклоненийВСтоимостиТоваровПоОрганизациям");
	КлючиВариантов.Добавить("АнализОтклоненийВСтоимостиТоваровПоПредприятию");
	
	// Если вариант не задан, то возьмем первый из списка по умолчанию
	Если Вариант = Неопределено Тогда
		Возврат Новый Структура("КлючВарианта", КлючиВариантов[0]);
	КонецЕсли;
	
	Пока КлючиВариантов.Найти(Вариант.КлючВарианта) = Неопределено
		И ЗначениеЗаполнено(Вариант.Родитель) Цикл
		Вариант = Вариант.Родитель;
	КонецЦикла;
	
	Возврат Вариант;
	
КонецФункции

// Установить параметры отчета.
// 
// Параметры:
//  НастройкиОтчета - Структура - настройки отчета.
Процедура УстановитьПараметрыОтчета(НастройкиОтчета)
	
	// Общий список для отбора в прочие операции.
	// Постепенно наполняется хоз. операциями по заданным категориям.
	ХозяйственныеОперацииПоКатегориям = Новый Массив();
	
	// Закупки.
	ХозяйственныеОперацииЗакупки = Новый Массив();
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.СторноПоступления);
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода);
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ВводОстатковОтклоненийВСтоимостиТоваров);
	ХозяйственныеОперацииЗакупки.Добавить(Перечисления.ХозяйственныеОперации.ОприходованиеТоваров);
	
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.СторноПоступления);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.КорректировкаПриобретенияПрошлогоПериода);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ВводОстатковОтклоненийВСтоимостиТоваров);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ОприходованиеТоваров);
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииЗакупки", ХозяйственныеОперацииЗакупки);
	
	// Доп. расходы.
	ХозяйственныеОперацииДопРасходы = Новый Массив();
	ХозяйственныеОперацииДопРасходы.Добавить(Перечисления.ХозяйственныеОперации.ДополнительныеРасходыБезПартии);
	ХозяйственныеОперацииДопРасходы.Добавить(Перечисления.ХозяйственныеОперации.ДополнительныеРасходыСПартией);
	ХозяйственныеОперацииДопРасходы.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	ХозяйственныеОперацииДопРасходы.Добавить(Перечисления.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимость);
	
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ДополнительныеРасходыБезПартии);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ДополнительныеРасходыСПартией);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭС);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.РаспределениеРасходовНаСебестоимость);
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииДопРасходы", ХозяйственныеОперацииДопРасходы);
	
	// Выпуск продукции.
	ХозяйственныеОперацииВыпуск = Новый Массив();
	ХозяйственныеОперацииВыпуск.Добавить(Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства);
	
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства);
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииВыпуск", ХозяйственныеОперацииВыпуск);
	
	// Перенос отклонений в стоимости товаров.
	ХозяйственныеОперацииПереносаОтклонений = Новый Массив();
	ХозяйственныеОперацииПереносаОтклонений.Добавить(Перечисления.ХозяйственныеОперации.ПереносОтклоненийВСтоимостиТоваров);
	
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.ПереносОтклоненийВСтоимостиТоваров);
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииПереносаОтклонений", ХозяйственныеОперацииПереносаОтклонений);
	
	// Продажи.
	ХозяйственныеОперацииПродажи = Новый Массив();
	ХозяйственныеОперацииПродажи.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	ХозяйственныеОперацииПродажи.Добавить(Перечисления.ХозяйственныеОперации.СторноРеализации);
	
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.РеализацияКлиенту);
	ХозяйственныеОперацииПоКатегориям.Добавить(Перечисления.ХозяйственныеОперации.СторноРеализации);
	
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииПродажи", ХозяйственныеОперацииПродажи);
	НастройкиОтчета.ПараметрыДанных.УстановитьЗначениеПараметра("ХозяйственныеОперацииПоКатегориям", ХозяйственныеОперацииПоКатегориям);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли