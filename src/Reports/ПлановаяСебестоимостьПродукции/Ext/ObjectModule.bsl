
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
	НаборДанных.Запрос = ТекстЗапроса_ПлановаяСебестоимостьПродукции();
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	ФормаПараметрыОтбор = ЭтаФорма.ФормаПараметры.Отбор;	// Структура
	
	Если ЭтаФорма.ФормаПараметры.Отбор.Свойство("Период")
		И Не ТипЗнч(ЭтаФорма.ФормаПараметры.Отбор.Период) = Тип("Дата") Тогда
		ФормаПараметрыОтбор.Удалить("Период");
	КонецЕсли;
	
	Если ЭтаФорма.ФормаПараметры.Отбор.Свойство("ПартияПродукции") Тогда
		ФормаПараметрыОтбор.Удалить("ПартияПродукции");
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		ЭтаФорма.Параметры.Отбор.Вставить("Калькуляция", Параметры.ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция ТекстЗапроса_ПлановаяСебестоимостьПродукции()
	
	ТекстыЗапросов = Новый Массив();
	ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
	|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
	|	МАКСИМУМ(МножителиПартийИПолуфабрикатов.Калькуляция.Дата) КАК Период
	|ПОМЕСТИТЬ ПериодыКалькуляций
	|ИЗ
	|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
	|ГДЕ
	|	МножителиПартийИПолуфабрикатов.Калькуляция.Дата <= КОНЕЦПЕРИОДА(&Период, ДЕНЬ)
	|	ИЛИ &Период = ДАТАВРЕМЯ(1, 1, 1)
	|{ГДЕ
	|	МножителиПартийИПолуфабрикатов.Калькуляция.*}
	|СГРУППИРОВАТЬ ПО
	|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
	|	МножителиПартийИПолуфабрикатов.Продукция,
	|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
	|	МножителиПартийИПолуфабрикатов.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПериодыКалькуляций.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	ПериодыКалькуляций.Продукция КАК Продукция,
	|	ПериодыКалькуляций.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПериодыКалькуляций.Назначение КАК Назначение,
	|	МАКСИМУМ(МножителиПартийИПолуфабрикатов.Калькуляция) КАК Калькуляция
	|ПОМЕСТИТЬ ПоследниеКалькуляции
	|ИЗ
	|	ПериодыКалькуляций КАК ПериодыКалькуляций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
	|		ПО ПериодыКалькуляций.Период = МножителиПартийИПолуфабрикатов.Калькуляция.Дата
	|		И ПериодыКалькуляций.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции
	|		И ПериодыКалькуляций.Продукция = МножителиПартийИПолуфабрикатов.Продукция
	|		И ПериодыКалькуляций.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
	|		И ПериодыКалькуляций.Назначение = МножителиПартийИПолуфабрикатов.Назначение
	|СГРУППИРОВАТЬ ПО
	|	ПериодыКалькуляций.ПартияСпецификацияПродукции,
	|	ПериодыКалькуляций.Продукция,
	|	ПериодыКалькуляций.ХарактеристикаПродукции,
	|	ПериодыКалькуляций.Назначение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПоследниеКалькуляции.Калькуляция КАК Калькуляция,
	|	ПоследниеКалькуляции.Продукция КАК Продукция,
	|	ПоследниеКалькуляции.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПоследниеКалькуляции.Назначение КАК Назначение,
	|	СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката) КАК ВсегоКоличество
	|ПОМЕСТИТЬ РассчитанноеКоличество
	|ИЗ
	|	ПоследниеКалькуляции КАК ПоследниеКалькуляции
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
	|		ПО ПоследниеКалькуляции.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
	|		И ПоследниеКалькуляции.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции
	|		И ПоследниеКалькуляции.Продукция = МножителиПартийИПолуфабрикатов.Продукция
	|		И ПоследниеКалькуляции.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
	|		И ПоследниеКалькуляции.Назначение = МножителиПартийИПолуфабрикатов.Назначение
	|		И МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|СГРУППИРОВАТЬ ПО
	|	ПоследниеКалькуляции.Калькуляция,
	|	ПоследниеКалькуляции.Продукция,
	|	ПоследниеКалькуляции.ХарактеристикаПродукции,
	|	ПоследниеКалькуляции.Назначение
	|";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	ТекстыЗапросов.Добавить(Отчеты.ПлановаяСебестоимостьПродукции.ТекстЗапроса_ДанныеПлановыхКалькуляций(Ложь));
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Данные.ЭтоПродукция КАК ЭтоПродукция,
	|	Данные.ОбъектКалькуляции КАК ОбъектКалькуляции,
	|	Данные.Калькуляция КАК Калькуляция,
	|	Данные.Продукция КАК Продукция,
	|	Данные.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Данные.Подразделение КАК Подразделение,
	|	Данные.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Характеристика КАК Характеристика,
	|	Данные.ТипЗатрат,
	|	СУММА(Данные.Количество) КАК Количество,
	|	СУММА(ВЫБОР
	|		КОГДА Данные.ЭтоПолуфабрикат
	|			ТОГДА 0
	|		ИНАЧЕ Данные.Стоимость
	|	КОНЕЦ) КАК Стоимость,
	|	СУММА(ВЫБОР
	|		КОГДА (Данные.ТипЗатрат = ""Материальные""
	|		ИЛИ Данные.ТипЗатрат = ""ВозвратныеОтходы"")
	|		И НЕ Данные.ЭтоПолуфабрикат
	|			ТОГДА Данные.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СтоимостьМатериальные,
	|	СУММА(ВЫБОР
	|		КОГДА Данные.ТипЗатрат = ""Трудозатраты""
	|		И НЕ Данные.ЭтоПолуфабрикат
	|			ТОГДА Данные.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СтоимостьТрудозатраты,
	|	СУММА(ВЫБОР
	|		КОГДА Данные.ТипЗатрат = ""ПрочиеПостоянные""
	|		И НЕ Данные.ЭтоПолуфабрикат
	|			ТОГДА Данные.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СтоимостьПрочиеПостоянные,
	|	СУММА(ВЫБОР
	|		КОГДА Данные.ТипЗатрат = ""ПрочиеПеременные""
	|		И НЕ Данные.ЭтоПолуфабрикат
	|			ТОГДА Данные.Стоимость
	|		ИНАЧЕ 0
	|	КОНЕЦ) КАК СтоимостьПрочиеПеременные,
	|	ВЫБОР
	|		КОГДА Данные.ЭтоПолуфабрикат
	|			ТОГДА МАКСИМУМ(ЕСТЬNULL(Цены.Цена, 0))
	|		КОГДА СУММА(Данные.Количество) = 0
	|			ТОГДА 0
	|		ИНАЧЕ СУММА(Данные.Стоимость) / СУММА(Данные.Количество)
	|	КОНЕЦ КАК Цена,
	|	СУММА(Данные.Забалансовая) КАК СтоимостьЗабалансовая,
	|	Данные.Продукция.ЕдиницаИзмерения КАК ПродукцияЕдиницаИзмерения,
	|	Данные.Номенклатура.ЕдиницаИзмерения КАК НоменклатураЕдиницаИзмерения,
	|	Данные.ПартияСпецификацияПродукции КАК ПартияПродукции,
	|	Данные.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
	|	Данные.Полуфабрикат КАК Полуфабрикат,
	|	Данные.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	Данные.Назначение КАК НазначениеПродукции,
	|	Данные.Полуфабрикат.ЕдиницаИзмерения КАК ПолуфабрикатЕдиницаИзмерения,
	|	Данные.Калькуляция.Организация КАК Организация,
	|	Данные.Порядок КАК Порядок,
	|	Данные.ПартияСпецификацияПродукции,
	|	0 КАК КоличествоВыпуска,
	|	Данные.КлючПартия
	|ИЗ
	|	Данные КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПолуфабрикатов КАК Цены
	|		ПО Данные.ОбъектКалькуляции = Цены.ОбъектКалькуляции
	|		И Данные.Калькуляция = Цены.Калькуляция
	|		И Данные.Продукция = Цены.Продукция
	|		И Данные.ХарактеристикаПродукции = Цены.ХарактеристикаПродукции
	|		И Данные.ПартияСпецификацияПродукции = Цены.ПартияПродукции
	|		И Данные.Номенклатура = Цены.Полуфабрикат
	|		И Данные.Характеристика = Цены.ХарактеристикаПолуфабриката
	|		И Данные.Назначение = Цены.НазначениеПродукции
	|СГРУППИРОВАТЬ ПО
	|	Данные.ЭтоПродукция,
	|	Данные.ОбъектКалькуляции,
	|	Данные.Характеристика,
	|	Данные.Продукция.ЕдиницаИзмерения,
	|	Данные.Калькуляция,
	|	Данные.Продукция,
	|	Данные.Номенклатура,
	|	Данные.Подразделение,
	|	Данные.Номенклатура.ЕдиницаИзмерения,
	|	Данные.ХарактеристикаПродукции,
	|	Данные.ТипЗатрат,
	|	Данные.СтатьяКалькуляции,
	|	Данные.КоличествоПолуфабриката,
	|	Данные.ПартияСпецификацияПродукции,
	|	Данные.ЭтоПолуфабрикат,
	|	Данные.Полуфабрикат,
	|	Данные.ХарактеристикаПолуфабриката,
	|	Данные.Назначение,
	|	Данные.Полуфабрикат.ЕдиницаИзмерения,
	|	Данные.Калькуляция.Организация,
	|	Данные.Порядок,
	|	Данные.КлючПартия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ФактическиеЗатраты.ЭтоПродукция КАК ЭтоПродукция,
	|	ФактическиеЗатраты.ОбъектКалькуляции КАК ОбъектКалькуляции,
	|	ФактическиеЗатраты.Калькуляция,
	|	ФактическиеЗатраты.Продукция,
	|	ФактическиеЗатраты.ХарактеристикаПродукции,
	|	ФактическиеЗатраты.Подразделение,
	|	ФактическиеЗатраты.СтатьяКалькуляции,
	|	ФактическиеЗатраты.Номенклатура,
	|	ФактическиеЗатраты.Характеристика,
	|	ФактическиеЗатраты.ТипЗатрат,
	|	ФактическиеЗатраты.Количество,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			ТОГДА ФактическиеЗатраты.Стоимость
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			ТОГДА 0
	|		ИНАЧЕ ОпределениеПолуфабрикатов.Стоимость * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			И ФактическиеЗатраты.ТипЗатрат = ""Материальные""
	|				ТОГДА ФактическиеЗатраты.Стоимость
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			ТОГДА 0
	|		КОГДА ФактическиеЗатраты.ТипЗатрат = ""Материальные""
	|			ТОГДА ОпределениеПолуфабрикатов.СтоимостьМатериальные * ФактическиеЗатраты.Количество /
	|				ОпределениеПолуфабрикатов.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			И ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
	|				ТОГДА ФактическиеЗатраты.Стоимость
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			И ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
	|				ТОГДА 0
	|		КОГДА ФактическиеЗатраты.ТипЗатрат = ""Трудозатраты""
	|			ТОГДА ОпределениеПолуфабрикатов.СтоимостьТрудозатраты * ФактическиеЗатраты.Количество /
	|				ОпределениеПолуфабрикатов.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
	|				ТОГДА ФактическиеЗатраты.Стоимость
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
	|				ТОГДА 0
	|		КОГДА ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПостоянные""
	|			ТОГДА ОпределениеПолуфабрикатов.СтоимостьПрочиеПостоянные * ФактическиеЗатраты.Количество /
	|				ОпределениеПолуфабрикатов.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
	|				ТОГДА ФактическиеЗатраты.Стоимость
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			И ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
	|				ТОГДА 0
	|		КОГДА ФактическиеЗатраты.ТипЗатрат = ""ПрочиеПеременные""
	|			ТОГДА ОпределениеПолуфабрикатов.СтоимостьПрочиеПеременные * ФактическиеЗатраты.Количество /
	|				ОпределениеПолуфабрикатов.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА ФактическиеЗатраты.Количество = 0
	|					ТОГДА 0
	|				ИНАЧЕ ФактическиеЗатраты.Стоимость / ФактическиеЗатраты.Количество
	|			КОНЕЦ
	|		ИНАЧЕ ОпределениеПолуфабрикатов.Стоимость * ФактическиеЗатраты.Количество / ОпределениеПолуфабрикатов.Количество /
	|			ФактическиеЗатраты.Количество
	|	КОНЕЦ КАК Цена,
	|	ВЫБОР
	|		КОГДА ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL
	|			ТОГДА ФактическиеЗатраты.Забалансовая
	|		КОГДА ОпределениеПолуфабрикатов.ФиксСтоимость
	|			ТОГДА 0
	|		ИНАЧЕ ОпределениеПолуфабрикатов.СтоимостьЗабалансовая * ФактическиеЗатраты.Количество /
	|			ОпределениеПолуфабрикатов.Количество
	|	КОНЕЦ КАК СтоимостьЗабалансовая,
	|	ФактическиеЗатраты.Продукция.ЕдиницаИзмерения,
	|	ФактическиеЗатраты.Номенклатура.ЕдиницаИзмерения,
	|	ФактическиеЗатраты.ПартияСпецификацияПродукции,
	|	НЕ ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры ЕСТЬ NULL,
	|	ФактическиеЗатраты.Полуфабрикат,
	|	ФактическиеЗатраты.ХарактеристикаПолуфабриката,
	|	ФактическиеЗатраты.Назначение,
	|	ФактическиеЗатраты.Полуфабрикат.ЕдиницаИзмерения,
	|	ФактическиеЗатраты.Калькуляция.Организация,
	|	ФактическиеЗатраты.Порядок,
	|	ФактическиеЗатраты.ПартияСпецификацияПродукции,
	|	0,
	|	ЕСТЬNULL(ОпределениеПолуфабрикатов.КлючПартия, ФактическиеЗатраты.КлючПартия)
	|ИЗ
	|	ФактическиеЗатраты КАК ФактическиеЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОпределениеПолуфабрикатов КАК ОпределениеПолуфабрикатов
	|		ПО ФактическиеЗатраты.ОбъектКалькуляции = ОпределениеПолуфабрикатов.ОбъектКалькуляции
	|		И ФактическиеЗатраты.АналитикаУчетаНоменклатуры = ОпределениеПолуфабрикатов.АналитикаУчетаНоменклатуры
	|		И ВЫБОР
	|			КОГДА ФактическиеЗатраты.ЭтоПродукция
	|				ТОГДА ФактическиеЗатраты.ПартияСпецификацияПродукции = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
	|			ИНАЧЕ ФактическиеЗатраты.ПартияПолуфабриката = ОпределениеПолуфабрикатов.ПартияСпецификацияПродукции
	|		КОНЕЦ
	|		И ОпределениеПолуфабрикатов.КлючПартия = ФактическиеЗатраты.КлючПартия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ЭтоПродукция,
	|	ДолиСтоимостиОтРассчитанногоКоличества.ОбъектКалькуляции КАК ОбъектКалькуляции,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Продукция,
	|	ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	NULL,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Продукция.ЕдиницаИзмерения,
	|	NULL,
	|	NULL,
	|	НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ПродукцияПолуфабрикат.Ссылка,
	|	ВЫБОР
	|		КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
	|		ИНАЧЕ МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
	|	КОНЕЦ,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Назначение,
	|	ПродукцияПолуфабрикат.ЕдиницаИзмерения,
	|	NULL,
	|	NULL,
	|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
	|	СУММА(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката * ДолиСтоимостиОтРассчитанногоКоличества.ДоляСтоимости),
	|	МножителиПартийИПолуфабрикатов.КлючПартия
	|ИЗ
	|	ДолиСтоимостиОтРассчитанногоКоличества КАК ДолиСтоимостиОтРассчитанногоКоличества
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
	|		ПО ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
	|		И ДолиСтоимостиОтРассчитанногоКоличества.Продукция = МножителиПартийИПолуфабрикатов.Продукция
	|		И
	|			ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
	|		И ДолиСтоимостиОтРассчитанногоКоличества.Назначение = МножителиПартийИПолуфабрикатов.Назначение
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ПродукцияПолуфабрикат
	|		ПО ПродукцияПолуфабрикат.Ссылка = ВЫБОР
	|			КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|				ТОГДА МножителиПартийИПолуфабрикатов.Продукция
	|			ИНАЧЕ МножителиПартийИПолуфабрикатов.Полуфабрикат
	|		КОНЕЦ
	|СГРУППИРОВАТЬ ПО
	|	МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка),
	|	ДолиСтоимостиОтРассчитанногоКоличества.ОбъектКалькуляции,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Калькуляция,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Продукция,
	|	ДолиСтоимостиОтРассчитанногоКоличества.ХарактеристикаПродукции,
	|	ПродукцияПолуфабрикат.Ссылка,
	|	ВЫБОР
	|		КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
	|		ИНАЧЕ МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
	|	КОНЕЦ,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Назначение,
	|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
	|	ПродукцияПолуфабрикат.ЕдиницаИзмерения,
	|	ДолиСтоимостиОтРассчитанногоКоличества.Продукция.ЕдиницаИзмерения,
	|	МножителиПартийИПолуфабрикатов.КлючПартия";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
	Результат = СтрСоединить(ТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	Возврат Результат;
КонецФункции
#КонецОбласти

#КонецЕсли