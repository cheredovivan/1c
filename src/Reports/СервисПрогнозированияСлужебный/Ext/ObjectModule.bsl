#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ВремяНачала = ОценкаПроизводительности.НачатьЗамерВремени();
	
	СтандартнаяОбработка = Ложь;
	
	ВидПлана = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ВидПлана");
	Если Не ЗначениеЗаполнено(ВидПлана.Значение) Тогда
		Возврат;
	КонецЕсли;
	
	ДетализацияПоКлиентам        = Ложь;
	ДетализацияПоСкладам         = Ложь;
	ДетализацияПоХарактеристикам = Ложь;
	ДетализацияИспользуется      = Ложь;
	ВариантВыводаДетализации     = "";
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	КлючВарианта    = НастройкиОтчета.ДополнительныеСвойства.КлючВарианта;
	
	ТребуетсяВыводЛегенды = (КлючВарианта = "ФактПрогнозРаздельно" Или КлючВарианта = "ФактПрогнозПоКатегориямРаздельно");
	
	СтруктураЗначений = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПлана.Значение, "ТипПлана,
		|ЗаполнятьСклад, ЗаполнятьСкладВТЧ,
		|ЗаполнятьПартнера, ЗаполнятьПартнераВТЧ, ЗаполнятьПоХарактеристикамНоменклатуры");
	
	Если СтруктураЗначений.ТипПлана = Перечисления.ТипыПланов.ПланПродаж Тогда
		Если СтруктураЗначений.ЗаполнятьПартнера Или СтруктураЗначений.ЗаполнятьПартнераВТЧ Тогда
			ДетализацияПоКлиентам = Истина;
		КонецЕсли;
		ДетализацияПоХарактеристикам = СтруктураЗначений.ЗаполнятьПоХарактеристикамНоменклатуры;
	КонецЕсли;
	
	Если СтруктураЗначений.ЗаполнятьСклад Или СтруктураЗначений.ЗаполнятьСкладВТЧ Тогда
		ДетализацияПоСкладам = Истина;
	КонецЕсли;
	
	Детализация = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Детализация");
	ЗначениеДетализации = Детализация.Значение;
	
	Если Детализация.Использование Тогда
		
		Если ЗначениеДетализации = "ПоСкладам" И ДетализацияПоСкладам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, "Склад");
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ГруппированиеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоКлиентам" И ДетализацияПоКлиентам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, "Партнер");
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ГруппированиеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоХарактеристикам" И ДетализацияПоХарактеристикам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ГруппировкаСтроки = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, ПолеСтроки);
			ДополнитьПоляГруппировкиПоВариантуДетализации(ГруппировкаСтроки, ЗначениеДетализации);
			
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ДополнениеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоСкладамКлиентам"
			И ДетализацияПоСкладам И ДетализацияПоКлиентам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ГруппировкаСтроки = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, ПолеСтроки);
			ДополнитьПоляГруппировкиПоВариантуДетализации(ГруппировкаСтроки, ЗначениеДетализации);
			
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ДополнениеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоСкладамХарактеристикам"
			И ДетализацияПоСкладам И ДетализацияПоХарактеристикам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ГруппировкаСтроки = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, ПолеСтроки);
			ДополнитьПоляГруппировкиПоВариантуДетализации(ГруппировкаСтроки, ЗначениеДетализации);
			
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ДополнениеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоКлиентамХарактеристикам"
			И ДетализацияПоКлиентам И ДетализацияПоХарактеристикам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ГруппировкаСтроки = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, ПолеСтроки);
			ДополнитьПоляГруппировкиПоВариантуДетализации(ГруппировкаСтроки, ЗначениеДетализации);
			
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ДополнениеСтроки";
			
		ИначеЕсли ЗначениеДетализации = "ПоСкладамКлиентамХарактеристикам"
			И ДетализацияПоСкладам И ДетализацияПоКлиентам И ДетализацияПоХарактеристикам Тогда
			
			СтрокиКомпоновки = НастройкиОтчета.Структура[0].Строки;
			СтрокиКомпоновки.Очистить();
			
			ПолеСтроки = ПолеСтрокиАнализируемыхДанных(КлючВарианта);
			ГруппировкаСтроки = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиОтчета, ПолеСтроки);
			ДополнитьПоляГруппировкиПоВариантуДетализации(ГруппировкаСтроки, ЗначениеДетализации);
			
			ДетализацияИспользуется = Истина;
			ВариантВыводаДетализации = "ДополнениеСтроки";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДетализацияИспользуется И ВариантВыводаДетализации = "ГруппированиеСтроки" Тогда
		ДобавитьПодчиненнуюГруппировку(СтрокиКомпоновки[0].Структура, ПолеСтроки);
	КонецЕсли;
	
	ТекстЗапросаТовары = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Элементы.НаборДанныхТовары.Запрос;
	ТекстЗапросаКатегории = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Элементы.НаборДанныхКатегории.Запрос;
	
	ПериодичностьОтчета = НастройкиОтчета.ПараметрыДанных.Элементы.Найти("ПериодичностьОтчета");
	Если ПериодичностьОтчета <> Неопределено Тогда
		Если ПериодичностьОтчета.Значение = Перечисления.Периодичность.Неделя Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "НЕДЕЛЯ)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "НЕДЕЛЯ)");
		ИначеЕсли ПериодичностьОтчета.Значение = Перечисления.Периодичность.Декада Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "ДЕКАДА)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "ДЕКАДА)");
		ИначеЕсли ПериодичностьОтчета.Значение = Перечисления.Периодичность.Месяц Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "МЕСЯЦ)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "МЕСЯЦ)");
		ИначеЕсли ПериодичностьОтчета.Значение = Перечисления.Периодичность.Квартал Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "КВАРТАЛ)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "КВАРТАЛ)");
		ИначеЕсли ПериодичностьОтчета.Значение = Перечисления.Периодичность.Полугодие Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "ПОЛУГОДИЕ)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "ПОЛУГОДИЕ)");
		ИначеЕсли ПериодичностьОтчета.Значение = Перечисления.Периодичность.Год Тогда
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "ДЕНЬ)", "ГОД)");
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "ДЕНЬ)", "ГОД)");
		КонецЕсли;
	КонецЕсли;
	
	ОграничиватьФактПродажПоПланамПрогноза = НастройкиОтчета.ПараметрыДанных.Элементы.Найти("ОграничиватьФактПродажПоПланамПрогноза");
	Если ОграничиватьФактПродажПоПланамПрогноза <> Неопределено
		И ОграничиватьФактПродажПоПланамПрогноза.Использование
		И ОграничиватьФактПродажПоПланамПрогноза.Значение Тогда
		
		Если ДетализацияПоСкладам Тогда
			Если ДетализацияПоХарактеристикам Тогда
				ТекстУсловияЗапроса = "(Номенклатура, Характеристика, Склад) В
				|					(ВЫБРАТЬ
				|						Набор.Номенклатура,
				|						Набор.Характеристика,
				|						Набор.Склад
				|					ИЗ
				|						ВТОтборПоПрогнозу КАК Набор)";
			Иначе
				ТекстУсловияЗапроса = "(Номенклатура, Склад) В
				|					(ВЫБРАТЬ
				|						Набор.Номенклатура,
				|						Набор.Склад
				|					ИЗ
				|						ВТОтборПоПрогнозу КАК Набор)";
			КонецЕсли;
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "&ОтборФактовПродажТоваровПоПланам", ТекстУсловияЗапроса);
			
			ТекстУсловияЗапроса = "(ТоварнаяКатегория, Склад) В
			|					(ВЫБРАТЬ
			|						Набор.ТоварнаяКатегория,
			|						Набор.Склад
			|					ИЗ
			|						ВТОтборПоПрогнозуПоКатегориям КАК Набор)";
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "&ОтборФактовПродажКатегорийПоПланам", ТекстУсловияЗапроса);
		Иначе
			Если ДетализацияПоХарактеристикам Тогда
				ТекстУсловияЗапроса = "(Номенклатура, Характеристика) В
				|					(ВЫБРАТЬ
				|						Набор.Номенклатура,
				|						Набор.Характеристика
				|					ИЗ
				|						ВТОтборПоПрогнозу КАК Набор)";
			Иначе
				ТекстУсловияЗапроса = "Номенклатура В
				|					(ВЫБРАТЬ
				|						Набор.Номенклатура
				|					ИЗ
				|						ВТОтборПоПрогнозу КАК Набор)";
			КонецЕсли;
			ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "&ОтборФактовПродажТоваровПоПланам", ТекстУсловияЗапроса);
			
			ТекстУсловияЗапроса = "ТоварнаяКатегория В
			|					(ВЫБРАТЬ
			|						Набор.ТоварнаяКатегория
			|					ИЗ
			|						ВТОтборПоПрогнозуПоКатегориям КАК Набор)";
			ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "&ОтборФактовПродажКатегорийПоПланам", ТекстУсловияЗапроса);
		КонецЕсли;
		
		ТекстУсловияЗапроса = "АналитикаУчетаНоменклатуры В
		|					(ВЫБРАТЬ
		|						Набор.АналитикаУчетаНоменклатуры
		|					ИЗ
		|						ВТОтборАналитикПоПрогнозу КАК Набор)";
		ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "&ОтборФактовПродажТоваровПоФактам", ТекстУсловияЗапроса);
		
		ТекстУсловияЗапроса = "АналитикаУчетаНоменклатуры В
		|					(ВЫБРАТЬ
		|						Набор.АналитикаУчетаНоменклатуры
		|					ИЗ
		|						ВТОтборАналитикПоПрогнозуПоКатегориям КАК Набор)";
		ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "&ОтборФактовПродажКатегорийПоФактам", ТекстУсловияЗапроса);
		
	Иначе
		ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "&ОтборФактовПродажТоваровПоПланам", "ИСТИНА");
		ТекстЗапросаТовары = СтрЗаменить(ТекстЗапросаТовары, "&ОтборФактовПродажТоваровПоФактам", "ИСТИНА");
		ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "&ОтборФактовПродажКатегорийПоПланам", "ИСТИНА");
		ТекстЗапросаКатегории = СтрЗаменить(ТекстЗапросаКатегории, "&ОтборФактовПродажКатегорийПоФактам", "ИСТИНА");
	КонецЕсли;

	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Элементы.НаборДанныхТовары.Запрос = ТекстЗапросаТовары;
	СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Элементы.НаборДанныхКатегории.Запрос = ТекстЗапросаКатегории;
	
	// Вывод легенды.
	Если ТребуетсяВыводЛегенды Тогда
		Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.СервисПрогнозированияСлужебный.Легенда");
		ОбластьЛегендаЗаголовок = Макет.ПолучитьОбласть("ЛегендаЗаголовок");
		ОбластьЛегенда = Макет.ПолучитьОбласть("Легенда");
		ДокументРезультат.НачатьАвтогруппировкуСтрок();
		ДокументРезультат.Вывести(ОбластьЛегендаЗаголовок, 1,, Ложь);
		ДокументРезультат.Вывести(ОбластьЛегенда, 2,, Ложь);
		ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	КлючеваяОперация = "Отчет.СервисПрогнозированияСлужебный.МодульОбъекта.ПриКомпоновкеРезультата";
	ОценкаПроизводительности.ЗакончитьЗамерВремени(КлючеваяОперация, ВремяНачала);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПодчиненнуюГруппировку(СтруктураГруппировки, ПолеПодчиненнойГруппировки)
	
	Группировка = СтруктураГруппировки.Добавить();
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(
		Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Поле = ПолеПодчиненнойГруппировки;
	
КонецПроцедуры

Процедура ДополнитьПоляГруппировкиПоВариантуДетализации(Группировка, ВариантДетализации)
	
	Если СтрНайти(ВариантДетализации, "Характеристикам") Тогда
		ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(
			Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных("Характеристика");
	КонецЕсли;
	
	Если СтрНайти(ВариантДетализации, "Складам") Тогда
		ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(
			Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных("Склад");
	КонецЕсли;
	
	Если СтрНайти(ВариантДетализации, "Клиентам") Тогда
		ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(
			Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Поле = Новый ПолеКомпоновкиДанных("Партнер");
	КонецЕсли;
	
КонецПроцедуры

Функция ПолеСтрокиАнализируемыхДанных(КлючВарианта)
	
	Если КлючВарианта = "Основной"
		Или КлючВарианта = "ФактПрогноз"
		Или КлючВарианта = "ФактПрогнозРаздельно"
		Или КлючВарианта = "СравнениеСПланом" Тогда
		
		ПолеСтроки = Новый ПолеКомпоновкиДанных("Номенклатура");
		
	КонецЕсли;
	
	Если КлючВарианта = "ПрогнозПоКатегориям"
		Или КлючВарианта = "ФактПрогнозПоКатегориям"
		Или КлючВарианта = "ФактПрогнозПоКатегориямРаздельно"
		Или КлючВарианта = "СравнениеСПланомПоКатегориям" Тогда
		
		ПолеСтроки = Новый ПолеКомпоновкиДанных("ТоварнаяКатегория");
		
	КонецЕсли;
	
	Возврат ПолеСтроки;
	
КонецФункции

#КонецОбласти

#КонецЕсли
