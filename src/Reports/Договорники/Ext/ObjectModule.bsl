#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	ВнешниеНаборыДанных = Новый Структура;
	ДобавитьЦепочкиИсправлений(ВнешниеНаборыДанных, Настройки);
	
	ЗарплатаКадрыОтчеты.ПриКомпоновкеРезультатаВТабличныйДокумент(
		ЭтотОбъект, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка, Ложь, ВнешниеНаборыДанных);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ИнициализироватьОтчет() Экспорт
	
	ЗарплатаКадрыОбщиеНаборыДанных.ЗаполнитьОбщиеИсточникиДанныхОтчета(ЭтотОбъект);
	
КонецПроцедуры

// Для общей формы "Форма отчета" подсистемы "Варианты отчетов".
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы <> КлючВарианта Тогда
		
		ИнициализироватьОтчет();
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		КлючСхемы = КлючВарианта;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЦепочкиИсправлений(ВнешниеНаборыДанных, Настройки)
	
	ЗначениеПараметраПериод = Настройки.ПараметрыДанных.Элементы.Найти("Период");
	
	ТипыСсылки = Новый Массив;
	ТипыСсылки.Добавить(Тип("ДокументСсылка.ДоговорАвторскогоЗаказа"));
	ТипыСсылки.Добавить(Тип("ДокументСсылка.ДоговорРаботыУслуги"));
	
	Результат = Новый ТаблицаЗначений();
	Результат.Колонки.Добавить("Договор", Новый ОписаниеТипов(ТипыСсылки));
	Результат.Колонки.Добавить("ДоговорЦепочки", Новый ОписаниеТипов(ТипыСсылки));
	
	ВнешниеНаборыДанных.Вставить("ЦепочкиИсправлений", Результат);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.ДокументОснование КАК Договор
		|ИЗ
		|	РегистрСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера КАК ПериодыДействияДоговоровГражданскоПравовогоХарактера
		|ГДЕ
		|	ПериодыДействияДоговоровГражданскоПравовогоХарактера.ДатаНачала <= &КонецПериода
		|	И ПериодыДействияДоговоровГражданскоПравовогоХарактера.ДатаОкончания >= &НачалоПериода
		|	И ПериодыДействияДоговоровГражданскоПравовогоХарактера.ДокументОснование.СпособОплаты = ЗНАЧЕНИЕ(Перечисление.СпособыОплатыПоДоговоруГПХ.ПоАктамВыполненныхРабот)");
	
	Запрос.УстановитьПараметр("НачалоПериода", ЗначениеПараметраПериод.Значение.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", ЗначениеПараметраПериод.Значение.ДатаОкончания);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
		
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДоговорыЦепочки = ИсправлениеДокументовЗарплатаКадры.ПолучитьДокументыЦепочкиИсправлений(Выборка.Договор);
		Для Каждого ДоговорЦепочки Из ДоговорыЦепочки Цикл
			НоваяСтрока = Результат.Добавить();
			НоваяСтрока.Договор = Выборка.Договор;
			НоваяСтрока.ДоговорЦепочки = ДоговорЦепочки;
		КонецЦикла;
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.Договор = Выборка.Договор;
		НоваяСтрока.ДоговорЦепочки = Выборка.Договор;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли