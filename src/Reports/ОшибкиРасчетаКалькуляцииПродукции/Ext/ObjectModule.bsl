
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = ЭтаФорма.Параметры;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		Параметры.Отбор.Вставить("Калькуляция", Параметры.ПараметрКоманды);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ИспользоватьРазрядыРасценокРаботСотрудников",
		ПолучитьФункциональнуюОпцию("ИспользоватьРазрядыРасценокРаботСотрудников"));
	
	//++ Локализация
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДополнительныйРазрезТарифнойСетки",
		Константы.ДополнительныйРазрезТарифнойСетки.Получить());
	//-- Локализация

	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1;
	НаборДанных.Запрос = ТекстЗапросаОтчетаОшибкиРасчетаКалькуляцииПродукции();
	
	Ценообразование.УстановитьВариантЦенообразованияВСКД(КомпоновщикНастроек);
	
КонецПроцедуры

Функция ТекстЗапросаОтчетаОшибкиРасчетаКалькуляцииПродукции()
	
	МассивТекстовЗапроса = Новый Массив();
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Калькуляция,
	|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|			ИНАЧЕ ПлановыеМатериальныеЗатраты.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) КАК ХарактеристикаЦО,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка) КАК СерияЦО,
	|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК УпаковкаЦО,
	|	ПлановыеМатериальныеЗатраты.Период КАК Период
	|ПОМЕСТИТЬ НоменклатураДляЦен
	|ИЗ
	|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = ПлановыеМатериальныеЗатраты.Номенклатура.ВидНоменклатуры)
	|ГДЕ
	|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
	|	И (ПлановыеМатериальныеЗатраты.Калькуляция = &Калькуляция)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеМатериальныеЗатраты.Калькуляция,
	|	ПлановыеМатериальныеЗатраты.Номенклатура,
	|	ПлановыеМатериальныеЗатраты.Характеристика,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА ВидыНоменклатуры.НастройкиКлючаЦенПоХарактеристике = ЗНАЧЕНИЕ(Перечисление.ВариантОтбораДляКлючаЦен.НеИспользовать)
	|				ТОГДА ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)
	|			ИНАЧЕ ПлановыеМатериальныеЗатраты.Характеристика.ХарактеристикаНоменклатурыДляЦенообразования
	|		КОНЕЦ, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)),
	|	ПлановыеМатериальныеЗатраты.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НоменклатураДляЦен.Калькуляция КАК Калькуляция,
	|	НоменклатураДляЦен.Номенклатура КАК Номенклатура,
	|	НоменклатураДляЦен.Характеристика КАК Характеристика,
	|	НоменклатураДляЦен.ХарактеристикаЦО КАК ХарактеристикаЦО,
	|	НоменклатураДляЦен.СерияЦО КАК СерияЦО,
	|	НоменклатураДляЦен.УпаковкаЦО КАК УпаковкаЦО,
	|	НоменклатураДляЦен.Период КАК Период,
	|	МАКСИМУМ(ЦеныНоменклатуры.Период) КАК ПериодЦены
	|ПОМЕСТИТЬ ДатыЦенВРазрезе
	|ИЗ
	|	НоменклатураДляЦен КАК НоменклатураДляЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО (ЦеныНоменклатуры.Номенклатура = НоменклатураДляЦен.Номенклатура)
	|			И (ЦеныНоменклатуры.Характеристика = НоменклатураДляЦен.Характеристика)
	|			И (ЦеныНоменклатуры.ВидЦены = НоменклатураДляЦен.Калькуляция.ВидЦены)
	|			И НоменклатураДляЦен.Период >= ЦеныНоменклатуры.Период
	|ГДЕ
	|	НЕ &ИспользуетсяЦенообразование25
	|	
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураДляЦен.Калькуляция,
	|	НоменклатураДляЦен.Период,
	|	НоменклатураДляЦен.Характеристика,
	|	НоменклатураДляЦен.ХарактеристикаЦО,
	|	НоменклатураДляЦен.СерияЦО,
	|	НоменклатураДляЦен.УпаковкаЦО,
	|	НоменклатураДляЦен.Номенклатура
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НоменклатураДляЦен.Калькуляция,
	|	НоменклатураДляЦен.Номенклатура,
	|	НоменклатураДляЦен.Характеристика,
	|	НоменклатураДляЦен.ХарактеристикаЦО КАК ХарактеристикаЦО,
	|	НоменклатураДляЦен.СерияЦО КАК СерияЦО,
	|	НоменклатураДляЦен.УпаковкаЦО КАК УпаковкаЦО,
	|	НоменклатураДляЦен.Период,
	|	МАКСИМУМ(ЦеныНоменклатуры25.Период)
	|ИЗ
	|	НоменклатураДляЦен КАК НоменклатураДляЦен
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры25
	|		ПО (ЦеныНоменклатуры25.Номенклатура = НоменклатураДляЦен.Номенклатура)
	|		И (ЦеныНоменклатуры25.ХарактеристикаЦО = НоменклатураДляЦен.ХарактеристикаЦО)
	|		И (ЦеныНоменклатуры25.СерияЦО = НоменклатураДляЦен.СерияЦО)
	|		И (ЦеныНоменклатуры25.УпаковкаЦО = НоменклатураДляЦен.УпаковкаЦО)
	|		И (ЦеныНоменклатуры25.ВидЦены = НоменклатураДляЦен.Калькуляция.ВидЦены)
	|		И НоменклатураДляЦен.Период >= ЦеныНоменклатуры25.Период
	|			
	|ГДЕ
	|	&ИспользуетсяЦенообразование25
	|
	|СГРУППИРОВАТЬ ПО
	|	НоменклатураДляЦен.Калькуляция,
	|	НоменклатураДляЦен.Период,
	|	НоменклатураДляЦен.ХарактеристикаЦО,
	|	НоменклатураДляЦен.СерияЦО,
	|	НоменклатураДляЦен.УпаковкаЦО,
	|	НоменклатураДляЦен.Характеристика,
	|	НоменклатураДляЦен.Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДатыЦенВРазрезе.Калькуляция КАК Калькуляция,
	|	ДатыЦенВРазрезе.Номенклатура КАК Номенклатура,
	|	ДатыЦенВРазрезе.Характеристика КАК Характеристика,
	|	ДатыЦенВРазрезе.Период КАК Период,
	|	ЕСТЬNULL(ЦеныНоменклатуры.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК Упаковка,
	|	ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) КАК Цена,
	|	ЦеныНоменклатуры.Валюта КАК Валюта
	|ПОМЕСТИТЬ ЦеныНоменклатуры
	|ИЗ
	|	ДатыЦенВРазрезе КАК ДатыЦенВРазрезе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ДатыЦенВРазрезе.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ДатыЦенВРазрезе.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И (ЦеныНоменклатуры.ВидЦены = ДатыЦенВРазрезе.Калькуляция.ВидЦены)
	|			И ДатыЦенВРазрезе.ПериодЦены = ЦеныНоменклатуры.Период
	|ГДЕ
	|	НЕ &ИспользуетсяЦенообразование25
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДатыЦенВРазрезе.Калькуляция,
	|	ДатыЦенВРазрезе.Номенклатура,
	|	ДатыЦенВРазрезе.Характеристика,
	|	ДатыЦенВРазрезе.Период,
	|	ЕСТЬNULL(ЦеныНоменклатуры25.Упаковка, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)),
	|	ЕСТЬNULL(ЦеныНоменклатуры25.Цена, 0),
	|	ЦеныНоменклатуры25.Валюта
	|ИЗ
	|	ДатыЦенВРазрезе КАК ДатыЦенВРазрезе
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры25
	|		ПО ДатыЦенВРазрезе.Номенклатура = ЦеныНоменклатуры25.Номенклатура
	|			И ДатыЦенВРазрезе.ХарактеристикаЦО = ЦеныНоменклатуры25.ХарактеристикаЦО
	|			И ДатыЦенВРазрезе.СерияЦО = ЦеныНоменклатуры25.СерияЦО
	|			И ДатыЦенВРазрезе.УпаковкаЦО = ЦеныНоменклатуры25.УпаковкаЦО
	|			И (ЦеныНоменклатуры25.ВидЦены = ДатыЦенВРазрезе.Калькуляция.ВидЦены)
	|			И ДатыЦенВРазрезе.ПериодЦены = ЦеныНоменклатуры25.Период
	|ГДЕ
	|	&ИспользуетсяЦенообразование25";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	МассивТекстовЗапроса.Добавить(РегистрыНакопления.ПлановыеТрудозатраты.ТекстЗапросаРасценкиТрудозатрат(Истина));
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеМатериальныеЗатраты.Период КАК Период,
	|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
	|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Калькуляция,
	|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
	|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
	|	""Материалы"" КАК ТипЗатрат
	|ИЗ
	|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ПО ПлановыеМатериальныеЗатраты.Калькуляция = ЦеныНоменклатуры.Калькуляция
	|			И ПлановыеМатериальныеЗатраты.Номенклатура = ЦеныНоменклатуры.Номенклатура
	|			И ПлановыеМатериальныеЗатраты.Характеристика = ЦеныНоменклатуры.Характеристика
	|			И ПлановыеМатериальныеЗатраты.Период = ЦеныНоменклатуры.Период
	|ГДЕ
	|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
	|	И ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) = 0
	|	И (ПлановыеМатериальныеЗатраты.Калькуляция = &Калькуляция)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПлановыеТрудозатраты.Период КАК Период,
	|	ПлановыеТрудозатраты.Подразделение КАК Подразделение,
	|	ПлановыеТрудозатраты.Калькуляция,
	|	ПлановыеТрудозатраты.ВидРабот,
	|	НЕОПРЕДЕЛЕНО,
	|	""Трудозатраты""
	|ИЗ
	|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Расценки КАК Расценки
	|		ПО ПлановыеТрудозатраты.Калькуляция = Расценки.Калькуляция
	|			И ПлановыеТрудозатраты.ВидРабот = Расценки.ВидРабот
	|			И ПлановыеТрудозатраты.Период = Расценки.Период
	|			И ПлановыеТрудозатраты.Подразделение = Расценки.Подразделение
	|ГДЕ
	|	ЕСТЬNULL(Расценки.Расценка, 0) = 0
	|	И (ПлановыеТрудозатраты.Калькуляция = &Калькуляция)";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	Возврат СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
КонецФункции

#КонецОбласти

#КонецЕсли