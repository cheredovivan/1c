#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Тип = Параметры.Тип;
	ИдентификаторСтроки = Параметры.ИдентификаторСтроки;
	
	Элементы.ГруппаСвНевПредДок.Видимость = ("СвНевПредДок" = Тип);
	Если Элементы.ГруппаСвНевПредДок.Видимость Тогда 
		Заголовок = "Сведения о запрошенных документах, невозможных к представлению";
		Элементы.КодПричНевПред.Заголовок = "Причина невозможности представления документа";
		Элементы.ДатаНач.Заголовок = "Дата начала периода документа";
		Элементы.ДатаОкон.Заголовок = "Дата окончания периода документа";
		Элементы.ДопОпис.Заголовок = "Дополнительное описание запрошенного документа";
		Элементы.ПричиныОбосНевПред.Заголовок = "Обоснование причины невозможности представления документа";
	КонецЕсли;
	
	Элементы.ГруппаСвНевПредИнф.Видимость = ("СвНевПредИнф" = Тип);
	Если Элементы.ГруппаСвНевПредИнф.Видимость Тогда 
		Заголовок = "Сведения о запрошенных сведениях (информации), невозможных к представлению";
		Элементы.КодПричНевПред.Заголовок = "Причина невозможности представления сведений(информации)";
		Элементы.ДатаНач.Заголовок = "Дата начала периода сведений(информации)";
		Элементы.ДатаОкон.Заголовок = "Дата окончания периода сведений(информации)";
		Элементы.ДопОпис.Заголовок = "Дополнительное описание запрошенного сведений(информации)";
		Элементы.ПричиныОбосНевПред.Заголовок = "Обоснование причины невозможности представления сведений(информации)";
	КонецЕсли;
	
	Если ТипЗнч(Параметры.СжатыеДанные) = Тип("ХранилищеЗначения") Тогда 
		ЗагрузкаСжатыхДанных(Параметры.СжатыеДанные.Получить());
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	Закрыть(РезультатЗарытия());
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	ТекстОписание = ?("СвНевПредДок" = Тип, "документа", "сведений (информации)");
	ОчиститьСообщения ();
	НаличиеОшибок = Ложь;
	
	Если Не ЗначениеЗаполнено(КодПричНевПред) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана причина невозможности представления документа", , "КодПричНевПред");
		НаличиеОшибок = Истина;
	КонецЕсли;
	Если СтрДлина(СокрЛП(НомПП)) <> 2 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан / неправильно указан номер из запроса", , "НомПП");
		НаличиеОшибок = Истина;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Причины.Количество()) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен список причин", , "Причины");
		НаличиеОшибок = Истина;
	КонецЕсли;
	
	Если "СвНевПредДок" = Тип Тогда 
		Если Не ЗначениеЗаполнено(ДатаДок) И Не ЗначениеЗаполнено(ДатаНач) И Не ЗначениеЗаполнено(ДатаОкон) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать дату или период документа", , "ДатаДок");
			НаличиеОшибок = Истина;
		ИначеЕсли Не ЗначениеЗаполнено(ДатаДок) Тогда 
			Если Не ЗначениеЗаполнено(ДатаНач) Тогда 
				ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать дату начала периода " + ТекстОписание, , "ДатаНач");
				НаличиеОшибок = Истина;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ДатаОкон) Тогда 
				ОбщегоНазначенияКлиент.СообщитьПользователю("Необходимо указать дату окончания периода " + ТекстОписание, , "ДатаОкон");
				НаличиеОшибок = Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ДатаНач) И Не ЗначениеЗаполнено(ДатаОкон) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не указана дата окончания периода " + ТекстОписание, , "ДатаОкон");
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаДок) И (Год(ДатаДок) <= 1990 Или Год(ДатаДок) >= 2100) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Неправильно указана дата " + ТекстОписание, , "ДатаДок");
		НаличиеОшибок = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаНач) И (Год(ДатаНач) <= 1990 Или Год(ДатаНач) >= 2100) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Неправильно указана дата начала периода " + ТекстОписание, , "ДатаНач");
		НаличиеОшибок = Истина;
	КонецЕсли;
	Если ЗначениеЗаполнено(ДатаОкон) И (Год(ДатаОкон) <= 1990 Или Год(ДатаОкон) >= 2100) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Неправильно указана дата окончания периода " + ТекстОписание, , "ДатаОкон");
		НаличиеОшибок = Истина;
	КонецЕсли;
	
	Для Каждого Стр Из Причины Цикл 
		Если Не ЗначениеЗаполнено(Стр.ОбосНевПред) Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Причины", Причины.Индекс(Стр) + 1, "ОбосНевПред");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнена причина", , ПутьКПолю);
			НаличиеОшибок = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Не НаличиеОшибок Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибок не обнаружено");
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗагрузитьПоказатель(РеквизитНаФорме, ЗначениеСвойства)
	ТекущийТип = Новый Массив;
	ТекущийТип.Добавить(ТипЗнч(РеквизитНаФорме));
	ОТ = Новый ОписаниеТипов(ТекущийТип);
	РеквизитНаФорме = ОТ.ПривестиЗначение(ЗначениеСвойства);
КонецФункции

&НаСервере
Процедура ЗагрузкаСжатыхДанных(ТекстJSON)
	РеквизитыФормы = Новый Массив;
	Для Каждого Элт Из ПолучитьРеквизиты("") Цикл 
		РеквизитыФормы.Добавить(Элт.Имя);
	КонецЦикла;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(ТекстJSON);
	
	Пока ЧтениеJSON.Прочитать() Цикл 
		Если ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда 
			ИмяСвойства = ЧтениеJSON.ТекущееЗначение;
			Если СтрНачинаетсяС(ИмяСвойства, УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПрефиксРеквизита()) Тогда 
				ЧтениеJSON.Прочитать();
				ЗначениеСвойства = ?(ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка, ЧтениеJSON.ТекущееЗначение, "");
				ИмяРеквизитаНаФорме = СтрЗаменить(ИмяСвойства, УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПрефиксРеквизита(), "");
				
				Если ЗначениеЗаполнено(ИмяСвойства) И ЗначениеЗаполнено(ЗначениеСвойства) И РеквизитыФормы.Найти(ИмяРеквизитаНаФорме) <> Неопределено Тогда 
					ЗагрузитьПоказатель(ЭтотОбъект[ИмяРеквизитаНаФорме], ЗначениеСвойства)
				КонецЕсли;
			ИначеЕсли СтрНачинаетсяС(ИмяСвойства, УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПрефиксТаблицы()) Тогда 
				СтрокаТаблицы = Неопределено;
				ИмяРеквизитаНаФорме = СтрЗаменить(ИмяСвойства, УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПрефиксТаблицы(), "");
				Если РеквизитыФормы.Найти(ИмяРеквизитаНаФорме) = Неопределено Тогда
					СтрокаТаблицы = Неопределено;
					Продолжить;
				КонецЕсли;
				
				ЧтениеJSON.Прочитать();
				Если ЧтениеJSON.ТипТекущегоЗначения <> ТипЗначенияJSON.НачалоМассива Тогда 
					Продолжить;
				КонецЕсли;
				
				Если РеквизитыФормы.Найти(ИмяРеквизитаНаФорме) <> Неопределено Тогда 
					ТаблицаФормы = ЭтотОбъект[ИмяРеквизитаНаФорме];
					КолонкиТаблицы = РеквизитФормыВЗначение(ИмяРеквизитаНаФорме).Колонки;
					
					Пока ЧтениеJSON.Прочитать() Цикл 
						Если ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.КонецМассива Тогда 
							Прервать;
						ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда 
							СтрокаТаблицы = ТаблицаФормы.Добавить();
						ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
							ИмяСвойства = ЧтениеJSON.ТекущееЗначение;
							ЧтениеJSON.Прочитать();
							ЗначениеСвойства = ?(ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка, ЧтениеJSON.ТекущееЗначение, "");
							Если КолонкиТаблицы.Найти(ИмяСвойства) <> Неопределено Тогда 
								ЗагрузитьПоказатель(СтрокаТаблицы[ИмяСвойства], ЗначениеСвойства);
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеJSON.Закрыть();
КонецПроцедуры

&НаСервере
Функция РезультатЗарытия()
	Ошибка = Не ЗначениеЗаполнено(Причины.Количество()) 
		Или Не ЗначениеЗаполнено(КодПричНевПред)
		Или Не СтрПодобнаПоРегулярномуВыражению(НомПП, "[0-9]{2}");
	
	Для Каждого Стр Из Причины Цикл 
		Ошибка = Ошибка Или Не ЗначениеЗаполнено(Стр.ОбосНевПред);
	КонецЦикла;
	
	Если "СвНевПредДок" = Тип И Не ЗначениеЗаполнено(ДатаДок) Тогда 
		Ошибка = Ошибка Или Не ЗначениеЗаполнено(ДатаНач) Или Не ЗначениеЗаполнено(ДатаОкон);
	ИначеЕсли "СвНевПредИнф" = Тип Тогда 
		Ошибка = Ошибка Или (ЗначениеЗаполнено(ДатаНач) И Не ЗначениеЗаполнено(ДатаОкон));
	КонецЕсли;
	
	ТекстовоеОписание = "Ответ по ";
	Если "СвНевПредДок" = Тип Тогда 
		ТекстовоеОписание = ТекстовоеОписание + "документу 1." + НомПП;
	ИначеЕсли "СвНевПредИнф" = Тип Тогда 
		ТекстовоеОписание = ТекстовоеОписание + "информации (сведениям) 2." + НомПП;
	КонецЕсли;
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "version", "5.01");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "КНД", "1150113");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "Форма", "ФормаОписаниеНевозможности501");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "schema", "NO_UVNPOKODOKNO_1_750_06_05_01_02");
	ЗаписатьТаблицуПоказателей(ЗаписьJSON, "Причины");
	
	ПоказателиДляЗаписи = Новый Структура;
	ПоказателиДляЗаписи.Вставить("ДатаДок", ДатаДок);
	ПоказателиДляЗаписи.Вставить("ДатаНач", ДатаНач);
	ПоказателиДляЗаписи.Вставить("ДатаОкон", ДатаОкон);
	ПоказателиДляЗаписи.Вставить("ДопОпис", ДопОпис);
	ПоказателиДляЗаписи.Вставить("КодПричНевПред", КодПричНевПред);
	ПоказателиДляЗаписи.Вставить("НомДок", НомДок);
	ПоказателиДляЗаписи.Вставить("НомПП", НомПП);
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьПакетПоказателей(ПоказателиДляЗаписи, ЗаписьJSON);
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	
	РезультатЗакрытия = Новый Структура;
	РезультатЗакрытия.Вставить("Тип", Тип);
	РезультатЗакрытия.Вставить("ИдентификаторСтроки", ИдентификаторСтроки);
	РезультатЗакрытия.Вставить("СжатыеДанные", Новый ХранилищеЗначения(ЗаписьJSON.Закрыть(), Новый СжатиеДанных(9)));
	РезультатЗакрытия.Вставить("ТекстовоеОписание", ТекстовоеОписание);
	РезультатЗакрытия.Вставить("Ошибка", Ошибка);
	Возврат РезультатЗакрытия;
КонецФункции

&НаСервере
Функция КонвертироватьПоказатель(Показатель)
	Если ТипЗнч(Показатель) = Тип("Число") Тогда 
		Возврат Формат(Показатель, "ЧГ=");
	ИначеЕсли ТипЗнч(Показатель) = Тип("Дата") Тогда 
		Возврат Формат(Показатель, "ДФ=yyyyMMdd");
	ИначеЕсли ТипЗнч(Показатель) = Тип("Булево") Тогда 
		Возврат Формат(Показатель, "БЛ=false; БИ=true");
	Иначе
		Возврат Строка(Показатель);
	КонецЕсли;
КонецФункции

&НаСервере
Процедура ЗаписатьТаблицуПоказателей(ЗаписьJSON, ИмяТаблицы) Экспорт 
	Таблица = РеквизитФормыВЗначение(ИмяТаблицы, Тип("ТаблицаЗначений"));
	
	ЗаписьJSON.ЗаписатьИмяСвойства(УведомлениеОСпецрежимахНалогообложенияПовтИсп.ПрефиксТаблицы() + ИмяТаблицы);
	ЗаписьJSON.ЗаписатьНачалоМассива();
	Для Каждого Стр Из Таблица Цикл 
		Если Не УведомлениеОСпецрежимахНалогообложенияСлужебный.СтрокаТаблицыЗначенийЗаполнена(Стр) Тогда 
			Продолжить;
		КонецЕсли;
		
		ЗаписьJSON.ЗаписатьНачалоОбъекта();
		Для Каждого Колонка Из Таблица.Колонки Цикл 
			ЗаписьJSON.ЗаписатьИмяСвойства(Колонка.Имя);
			ЗаписьJSON.ЗаписатьЗначение(КонвертироватьПоказатель(Стр[Колонка.Имя]));
		КонецЦикла;
		ЗаписьJSON.ЗаписатьКонецОбъекта();
	КонецЦикла;
	ЗаписьJSON.ЗаписатьКонецМассива();
КонецПроцедуры

#КонецОбласти
