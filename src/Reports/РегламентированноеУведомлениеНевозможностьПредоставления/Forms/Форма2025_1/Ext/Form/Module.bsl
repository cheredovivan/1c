#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	УведомлениеОСпецрежимахНалогообложения.НачальныеОперацииПриСозданииНаСервере(ЭтотОбъект);
	Если ЗначениеЗаполнено(ЭтотОбъект["СтруктураРеквизитовФормы"].ПоследнийЦиклОбмена) Тогда 
		ДоступностьРеквизитов(Ложь);
	КонецЕсли;
	
	Объект.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.НевозможностьПредоставленияИнформации;
	НачальнаяИнициализация();
	Если Не ОбщегоНазначения.СсылкаСуществует(Объект.Ссылка) Тогда 
		Объект.ДатаПодписи = ТекущаяДатаСеанса();
		_ДатаУведомления = Объект.ДатаПодписи;
		Если Параметры.Свойство("НалоговыйОрган") И ЗначениеЗаполнено(Параметры.НалоговыйОрган) Тогда 
			Объект.РегистрацияВИФНС = Параметры.НалоговыйОрган;
		Иначе
			Объект.РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Объект.Организация);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Данные) И ТипЗнч(Данные) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			ЗаполнитьДаннымиТребования(Данные);
		КонецЕсли;
		
		УстановитьДанныеОрганизации();
		УстановитьДанныеПоРегистрацииВИФНС();
	КонецЕсли;
	
	ЗагрузитьДанные();
	ЗаголовкиДекораций(ЭтотОбъект);
	РегламентированнаяОтчетность.ДобавитьКнопкуПрисоединенныеФайлы(ЭтотОбъект);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		ЗаблокироватьДанныеДляРедактирования(Объект.Ссылка, , УникальныйИдентификатор);
	КонецЕсли;
	
	Элементы.ПриложенныеДокументы.Видимость = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСФайлами");
	Элементы.ФормаРазрешитьВыгружатьСОшибками.Пометка = ЭтотОбъект["РазрешитьВыгружатьСОшибками"];
	УведомлениеОСпецрежимахНалогообложения.СпрятатьКнопкиВыгрузкиОтправкиУНеактуальныхФорм(ЭтотОбъект);
	Элементы.СоздатьНаОснованииТребования.Видимость = 
		ИнтерфейсыВзаимодействияБРО.ПодключенДокументооборотСКонтролирующимОрганом(Объект.Организация, Перечисления.ТипыКонтролирующихОрганов.ФНС);
	Если РегламентированнаяОтчетность.ДоступнаПодсистема85() Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 = ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьНазначениеДействияКомандФормыРеглОтчета(Команды);
	КонецЕсли;
	ИнициализироватьВыводСтатусовИВажныхСообщений_85();

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	РазблокироватьНаСервере();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", ПараметрыЗаписи, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "УведомлениеОСпецрежимахНалогообложения_НавигацияПоОшибкам" Тогда 
		Попытка
			Если ЗначениеЗаполнено(Параметр.ИмяСтраницы) Тогда 
				Элементы.ГруппаЗакладки.ТекущаяСтраница = Элементы[Параметр.ИмяСтраницы];
			КонецЕсли;
			ТекущийЭлемент = Элементы[Параметр.ИмяОбласти];
			
			Если Параметр.ИмяОбласти = "_СвНевозмПред" И ЗначениеЗаполнено(Параметр.УИДСтраницы) Тогда 
				НС = _СвНевозмПред.НайтиСтроки(Новый Структура("УИД", Параметр.УИДСтраницы));
				Если НС.Количество() > 0 Тогда 
					Элементы._СвНевозмПред.ТекущаяСтрока = НС[0].ПолучитьИдентификатор();
				КонецЕсли;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		Активизировать();
		Если ТипЗнч(Источник) = Тип("ФормаКлиентскогоПриложения") И Источник.Открыта() Тогда 
			Источник.Закрыть( );
		КонецЕсли;
	КонецЕсли;
	Если ИмяСобытия = "Завершение отправки" Или 
		ИмяСобытия = "Завершение отправки в контролирующий орган" Или
		ИмяСобытия = "Успешная отправка" Тогда
		ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере();	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru = 'Вы уверены, что уведомление уже сдано?';
													|en = 'Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияВИФНСПриИзменении(Элемент)
	Модифицированность = Истина;
	Объект.РегистрацияВИФНС = ПредопределенноеЗначение("Справочник.РегистрацииВНалоговомОргане.ПустаяСсылка");
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияВИФНСНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуВыбораРегистрацииВИФНС(ЭтотОбъект, "КодНО");
КонецПроцедуры

&НаКлиенте
Процедура ФИОИспНажатие(Элемент)
	ФИОНажатие("Исп", "Ввод данных исполнителя");
КонецПроцедуры

&НаКлиенте
Процедура ФИОПодпНажатие(Элемент)
	ФИОНажатие("Подп", "Ввод данных подписанта");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_СвНевозмПред

&НаКлиенте
Асинх Процедура ДокументыСканированныеДокументыОписаниеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	Если Элементы._СвНевозмПред.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(Элементы._СвНевозмПред.ТекущиеДанные.УИД) Тогда 
		Элементы._СвНевозмПред.ТекущиеДанные.УИД = Новый УникальныйИдентификатор;
	КонецЕсли;
	
	Если Модифицированность Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Если Ждать ВопросАсинх("Для добавления файла необходимо сохранить уведомление. Сохранить?", РежимДиалогаВопрос.ДаНет) 
			<> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		СохранитьДанные();
	КонецЕсли;
	
	ТД = Элементы._СвНевозмПред.ТекущиеДанные;
	ПараметрыОткрытия = Новый Структура("Уведомление, РедактируемыйУИД, ПриложенныеФайлы", Объект.Ссылка, ТД.УИД, ТД.ПриложенныеФайлы);
	
	ОО = Новый ОписаниеОповещения("ВыборСпискаДокументовЗавершение", ЭтотОбъект);
	ОткрытьФорму("Отчет." + СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".")[1] 
		+ ".Форма.ФормаПриложенныхСканированныхДокументов", ПараметрыОткрытия, ЭтотОбъект,,,,ОО,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура _СвНевозмПредПередУдалением(Элемент, Отказ)
	_СвНевозмПредПередУдалениемНаСервере(Элемент.ТекущаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура _СвНевозмПредТекстовоеОписаниеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Элементы._СвНевозмПред.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ТД = Элементы._СвНевозмПред.ТекущиеДанные;
	ПараметрыОткрытия = Новый Структура("Тип, ИдентификаторСтроки, СжатыеДанные", ТД.Тип, ТД.ПолучитьИдентификатор(), ТД.СжатыеДанные);
	ОО = Новый ОписаниеОповещения("_ДобавитьСвНевПредЗавершение", ЭтотОбъект);
	ОткрытьФорму("Отчет." + Объект.ИмяОтчета + ".Форма.ФормаОписаниеНевозможности501",
		ПараметрыОткрытия, ЭтотОбъект,,,,ОО,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура _СвНевозмПредВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ДокументыСканированныеДокументыОписание" Тогда 
		СтандартнаяОбработка = Ложь;
		ДокументыСканированныеДокументыОписаниеНачалоВыбора(Элемент, ВыбраннаяСтрока, СтандартнаяОбработка);
	ИначеЕсли Поле.Имя = "_СвНевозмПредТекстовоеОписание" Тогда
		_СвНевозмПредТекстовоеОписаниеНачалоВыбора(Элемент, ВыбраннаяСтрока, Ложь, СтандартнаяОбработка)
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьДанные();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения",,Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьXML(Команда)
	
	ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если ВыгружаемыеДанные <> Неопределено Тогда 
		РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Отправка в ФНС
&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(ЭтотОбъект, "ФНС");
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПроверитьВыгрузку(ЭтотОбъект, ПроверитьВыгрузкуНаСервере());
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНаОснованииТребования(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ВыбратьТребование(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	ПечатьБРОНаСервере();
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтотОбъект, "Открыть", Ложь, ЭтотОбъект["СтруктураРеквизитовУведомления"].СписокПечатаемыхЛистов);
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьВыгружатьСОшибками(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаРазрешитьВыгружатьСОшибками(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСвНевПредДок(Команда)
	ПараметрыОткрытия = Новый Структура("Тип, ИдентификаторСтроки, СжатыеДанные", "СвНевПредДок", Неопределено, Неопределено);
	ОО = Новый ОписаниеОповещения("_ДобавитьСвНевПредЗавершение", ЭтотОбъект);
	ОткрытьФорму("Отчет." + Объект.ИмяОтчета + ".Форма.ФормаОписаниеНевозможности501",
		ПараметрыОткрытия, ЭтотОбъект,,,,ОО,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСвНевПредИнф(Команда)
	ПараметрыОткрытия = Новый Структура("Тип, ИдентификаторСтроки, АрхивДанных", "СвНевПредИнф", Неопределено, Неопределено);
	ОО = Новый ОписаниеОповещения("_ДобавитьСвНевПредЗавершение", ЭтотОбъект);
	ОткрытьФорму("Отчет." + Объект.ИмяОтчета + ".Форма.ФормаОписаниеНевозможности501",
		ПараметрыОткрытия, ЭтотОбъект,,,,ОО,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКодаНОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда 
		Объект.РегистрацияВИФНС = Результат;
		УстановитьДанныеПоРегистрацииВИФНС();
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ФИОНажатие(ИмяПоля, Заголовок)
	ПараметрыВыборФИО = Новый Структура;
	ПараметрыВыборФИО.Вставить("Фамилия", ЭтотОбъект["_Фамилия" + ИмяПоля]);
	ПараметрыВыборФИО.Вставить("Имя", ЭтотОбъект["_Имя" + ИмяПоля]);
	ПараметрыВыборФИО.Вставить("Отчество", ЭтотОбъект["_Отчество" + ИмяПоля]);
	ПараметрыВыборФИО.Вставить("ИмяПоля", ИмяПоля);
	ПараметрыВыборФИО.Вставить("ИмяСтраницы", "СведенияОбОтправителе");
	ПараметрыВыборФИО.Вставить("Заголовок", Заголовок);
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораФИО", ПараметрыВыборФИО, ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ОткрытьФормуВыбораФИОЗавершение", ЭтотОбъект, Неопределено),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораФИОЗавершение(РезультатВыбора, ДопПарам) Экспорт
	Если ТипЗнч(РезультатВыбора) = Тип("Структура") Тогда 
		Постфикс = ?(РезультатВыбора.ИмяПоля = "Исп", "Исп", "Подп");
		ЭтотОбъект["_Фамилия" + Постфикс] = РезультатВыбора.Фамилия;
		ЭтотОбъект["_Имя" + Постфикс] = РезультатВыбора.Имя;
		ЭтотОбъект["_Отчество" + Постфикс] = РезультатВыбора.Отчество;
		ЗаголовкиДекораций(ЭтотОбъект);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаголовкиДекораций(Форма)
	Форма.Элементы.ФИОИсп.Заголовок = ?((ЗначениеЗаполнено(Форма._ФамилияИсп) И ЗначениеЗаполнено(Форма._ИмяИсп)),
		Форма._ФамилияИсп + " " + Форма._ИмяИсп + " " + Форма._ОтчествоИсп, "<Необходимо заполнить>");
	Форма.Элементы.ФИОПодп.Заголовок = ?((ЗначениеЗаполнено(Форма._ФамилияПодп) И ЗначениеЗаполнено(Форма._ИмяПодп)),
		Форма._ФамилияПодп + " " + Форма._ИмяПодп + " " + Форма._ОтчествоПодп, "<Необходимо заполнить>");
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные()
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
	СтруктураРеквизитов = СтруктураПараметров.СтруктураРеквизитов;
	Реквизиты = ПолучитьРеквизиты();
	Для Каждого Рекв Из Реквизиты Цикл 
		Если СтруктураРеквизитов.Свойство(Рекв.Имя) Тогда 
			Если ТипЗнч(СтруктураРеквизитов[Рекв.Имя]) = Тип("ТаблицаЗначений")
				Или ТипЗнч(СтруктураРеквизитов[Рекв.Имя]) = Тип("ДеревоЗначений") Тогда 
				ЗначениеВРеквизитФормы(СтруктураРеквизитов[Рекв.Имя], Рекв.Имя);
			Иначе
				ЭтотОбъект[Рекв.Имя] = СтруктураРеквизитов[Рекв.Имя];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураПараметров.Свойство("РазрешитьВыгружатьСОшибками", ЭтотОбъект["РазрешитьВыгружатьСОшибками"]);
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Дата = ТекущаяДатаСеанса() 
	КонецЕсли;
	
	Реквизиты = ПолучитьРеквизиты();
	СтруктураРеквизитов = Новый Структура();
	Для Каждого Рекв Из Реквизиты Цикл 
		Если СтрНачинаетсяС(Рекв.Имя, "_") Тогда 
			Если ТипЗнч(ЭтотОбъект[Рекв.Имя]) = Тип("ДанныеФормыКоллекция")
				Или ТипЗнч(ЭтотОбъект[Рекв.Имя]) = Тип("ДанныеФормыДерево") Тогда 
				СтруктураРеквизитов.Вставить(Рекв.Имя, РеквизитФормыВЗначение(Рекв.Имя));
			Иначе
				СтруктураРеквизитов.Вставить(Рекв.Имя, ЭтотОбъект[Рекв.Имя]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураПараметров = Новый Структура("СтруктураРеквизитов, РазрешитьВыгружатьСОшибками", 
		СтруктураРеквизитов, ЭтотОбъект["РазрешитьВыгружатьСОшибками"]);
	
	УведомлениеОСпецрежимахНалогообложения.СохранитьДанные(ЭтотОбъект, СтруктураПараметров);
КонецПроцедуры

&НаСервере
Процедура НачальнаяИнициализация()
	_ПрПодп = 1;
	Элементы.ГруппаЮЛ.Видимость = ЭтотОбъект["ЭтоЮЛ"];
	Элементы.ГруппаФЛ.Видимость = Не ЭтотОбъект["ЭтоЮЛ"];
	Элементы.ГруппаО.Заголовок = "Сведения " + ?(ЭтотОбъект["ЭтоЮЛ"], "об организации", "о физическом лице");
	
	Элементы._НомУведом.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ДатаЗапр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._НомЗапр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ИмяФайлЗапр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ИдЗапр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._НаимОргОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ИННЮЛОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._КППОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ИННФЛОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ФамилияОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ИмяОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ОтчествоОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ДолжнПодп.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._Тлф.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._Email.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._НаимДокПодп.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ПрПодп.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ОГРНОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
	Элементы._ОГРНИПОтпр.УстановитьДействие("ПриИзменении", "ПриИзмененииРеквизитов");
КонецПроцедуры

&НаСервере
Процедура ПриЗакрытииНаСервере()
	СохранитьДанные();
КонецПроцедуры

&НаСервере
Процедура РазблокироватьНаСервере()
	РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
КонецПроцедуры

&НаСервере
Функция СформироватьXMLНаСервере(УникальныйИдентификатор)
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт 
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВыгрузкуНаСервере()
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ПроверитьДокументСВыводомВТаблицу(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизитов(Элемент)
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДаннымиТребования(Требование) Экспорт
	ДанныеЗапроса = УведомлениеОСпецрежимахНалогообложения.ДанныеЗапросаДокументовИнформации(Требование);
	ПараметрыЗапроса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеЗапроса, "Параметры", Новый Структура);
	
	_НомЗапр = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗапроса, "НомЗапр");
	_ИмяФайлЗапр = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗапроса, "ИдФайл");
	_ИдЗапр = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗапроса, "ИдЗапр");
	_ДатаЗапр = РегламентированнаяОтчетностьЗагрузка.ДатаИзСтрокиЛюбогоФормата(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЗапроса, "ДатаЗапр"));
	
	Объект.Основание = Требование;
	Модифицированность = Истина;
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставителяПоФизЛицу(Физлицо)
	Если ЗначениеЗаполнено(Физлицо) Тогда 
		ДанныеПредставителя = РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОФизЛице(Физлицо, , Объект.ДатаПодписи);
		Объект.ПодписантФамилия = СокрЛП(ДанныеПредставителя.Фамилия);
		Объект.ПодписантИмя = СокрЛП(ДанныеПредставителя.Имя);
		Объект.ПодписантОтчество = СокрЛП(ДанныеПредставителя.Отчество);
		
		_ФамилияПодп = СокрЛП(ДанныеПредставителя.Фамилия);
		_ИмяПодп = СокрЛП(ДанныеПредставителя.Имя);
		_ОтчествоПодп = СокрЛП(ДанныеПредставителя.Отчество);
		
		_ФамилияОтпр = _ФамилияПодп;
		_ИмяОтпр = _ИмяПодп;
		_ОтчествоОтпр = _ОтчествоПодп;
	Иначе
		Объект.ПодписантФамилия = "";
		Объект.ПодписантИмя = "";
		Объект.ПодписантОтчество = "";
		
		_ФамилияПодп = "";
		_ИмяПодп = "";
		_ОтчествоПодп = "";
		_ИННФЛОтпр = "";
		_ФамилияОтпр = "";
		_ИмяОтпр = "";
		_ОтчествоОтпр = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставителяПоОрганизации()
	Документы.УведомлениеОСпецрежимахНалогообложения.УстановитьДанныеРуководителя(Объект);
	
	_ФамилияПодп = Объект.ПодписантФамилия;
	_ИмяПодп = Объект.ПодписантИмя;
	_ОтчествоПодп = Объект.ПодписантОтчество;
	Если ЭтотОбъект["ЭтоЮЛ"] Тогда 
		Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "ИННЮЛ,КППЮЛ,НаимЮЛПол");
		_НаимОргОтпр = Сведения.НаимЮЛПол;
		_ИННЮЛОтпр = Сведения.ИННЮЛ;
		_КППОтпр = Сведения.КППЮЛ;
	Иначе
		Сведения = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, Объект.ДатаПодписи, "ИННФЛ,ФамилияИП,ИмяИП,ОтчествоИП");
		_ИННФЛОтпр = Сведения.ИННФЛ;
		_ФамилияОтпр = Сведения.ФамилияИП;
		_ИмяОтпр = Сведения.ИмяИП;
		_ОтчествоОтпр = Сведения.ОтчествоИП;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьДанныеПоРегистрацииВИФНС()
	Реквизиты = РегистрацияВНОСервер.ДанныеРегистрации(Объект.РегистрацияВИФНС);
	_КодНО = Реквизиты.Код;
	Модифицированность = Истина;
	Если ЗначениеЗаполнено(Реквизиты.Представитель) Тогда
		УстановитьПредставителяПоФизЛицу(Реквизиты.Представитель);
		_ПрПодп = 2;
		_НаимДокПодп = Реквизиты.ДокументПредставителя;
	Иначе
		УстановитьПредставителяПоОрганизации();
		_ПрПодп = 1;
		_НаимДокПодп = "";
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере()
	УведомлениеОСпецрежимахНалогообложения.ПечатьУведомленияБРО(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ВыборСпискаДокументовЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура")
		И Ответ.Свойство("_ДокИнф") Тогда 
		
		Модифицированность = Истина;
		Элементы._СвНевозмПред.ТекущиеДанные.ДокументыПредставление = Ответ.ТекстовоеОписание;
		ДЗ = Элементы._СвНевозмПред.ТекущиеДанные.ДокИнф;
		ДЗ.ПолучитьЭлементы().Очистить();
		Для Каждого Стр1 Из Ответ._ДокИнф.ПолучитьЭлементы() Цикл 
			НовСтр = ДЗ.ПолучитьЭлементы().Добавить();
			ЗаполнитьЗначенияСвойств(НовСтр, Стр1);
			Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл 
				НовСтр2 = НовСтр.ПолучитьЭлементы().Добавить();
				ЗаполнитьЗначенияСвойств(НовСтр2, Стр2);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция СформироватьОписаниеСканКопий()
	ДЗ = Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы();
	ВсегоД = 0;
	ВсегоФ = 0;
	Для Каждого Стр1 Из ДЗ Цикл
		ВсегоД = ВсегоД + 1;
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл
			ВсегоФ = ВсегоФ + 1;
		КонецЦикла;
	КонецЦикла;
	
	Если ВсегоД = 0 Тогда 
		Возврат "Нет приложенных скан-копий";
	Иначе
		Возврат "Приложено " + ВсегоД + " документ/ов и " + ВсегоФ + " файл/ов";
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ОбработкаПослеДобавленияПрисоединенногоФайла(ИмяДокумент, УИДДокумент, УИДФайл, ПрисоединенныйФайл, ПрисоединенныйФайлИмя) Экспорт
	Модифицированность = Истина;
	ДЗ = Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы();
	НовСтр = Неопределено;
	Для Каждого Стр Из ДЗ Цикл 
		Если Стр.УИДДокумент = УИДДокумент Тогда 
			НовСтр = Стр;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НовСтр = Неопределено Тогда
		НовСтр = ДЗ.Добавить();
		НовСтр.УИДДокумент = УИДДокумент;
		НовСтр.Документ = ИмяДокумент;
	КонецЕсли;
	
	НовСтрФайл = НовСтр.ПолучитьЭлементы().Добавить();
	НовСтрФайл.УИДФайл = УИДФайл;
	НовСтрФайл.Документ = ПрисоединенныйФайлИмя;
	НовСтрФайл.ПрисоединенныйФайл = ПрисоединенныйФайл;
	НовСтрФайл.ИндексКартинки = 2;
	
	Элементы._СвНевозмПред.ТекущиеДанные.СканированныеДокументыОписание = СформироватьОписаниеСканКопий();
	СохранитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеУдаленияПрисоединенногоФайла(УИДДокумент, УИДФайл) Экспорт
	НовСтр = Неопределено;
	Для Каждого Стр Из Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы() Цикл 
		Если Стр.УИДДокумент = УИДДокумент Тогда 
			НовСтр = Стр;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НовСтр = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	Для Каждого Стр Из НовСтр.ПолучитьЭлементы() Цикл 
		Если Стр.УИДФайл = УИДФайл Тогда 
			НовСтр.ПолучитьЭлементы().Удалить(Стр);
			Элементы._СвНевозмПред.ТекущиеДанные.СканированныеДокументыОписание = СформироватьОписаниеСканКопий();
			Модифицированность = Истина;
			СохранитьДанные();
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаПослеУдаленияДокумента(УИДДокумент) Экспорт
	Для Каждого Стр Из Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы() Цикл 
		Если Стр.УИДДокумент = УИДДокумент Тогда 
			Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы().Удалить(Стр);
			Элементы._СвНевозмПред.ТекущиеДанные.СканированныеДокументыОписание = СформироватьОписаниеСканКопий();
			Модифицированность = Истина;
			СохранитьДанные();
			Возврат;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура _СвНевозмПредПередУдалениемНаСервере(ТекущаяСтрока)
	Для Каждого Стр1 Из _СвНевозмПред.НайтиПоИдентификатору(ТекущаяСтрока).ПриложенныеФайлы.ПолучитьЭлементы() Цикл 
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл 
			УведомлениеОСпецрежимахНалогообложения.УдалениеПрисоединенногоСлужебногоФайла(Стр2.ПрисоединенныйФайл, Ложь);
		КонецЦикла;
	КонецЦикла;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СинхронизацияДереваПриложенныхФайлов(ПриложенныеФайлы) Экспорт 
	Модифицированность = Истина;
	ДЗ = Элементы._СвНевозмПред.ТекущиеДанные.ПриложенныеФайлы.ПолучитьЭлементы();
	ДЗ.Очистить();
	Для Каждого Стр1 Из ПриложенныеФайлы.ПолучитьЭлементы() Цикл 
		НовСтр1 = ДЗ.Добавить();
		ЗаполнитьЗначенияСвойств(НовСтр1, Стр1);
		Для Каждого Стр2 Из Стр1.ПолучитьЭлементы() Цикл 
			ЗаполнитьЗначенияСвойств(НовСтр1.ПолучитьЭлементы().Добавить(), Стр2);
		КонецЦикла;
	КонецЦикла;
	Элементы._СвНевозмПред.ТекущиеДанные.СканированныеДокументыОписание = СформироватьОписаниеСканКопий();
	СохранитьДанные();
КонецПроцедуры

&НаСервере
Процедура РазрешитьРедактированиеРеквизитовОбъекта() Экспорт
	РегламентированнаяОтчетность.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
	ДоступностьРеквизитов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	РегламентированнаяОтчетностьКлиент.РазрешитьРедактированиеРеквизитовОтчета(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ДоступностьРеквизитов(Доступность)
	РеквизитыДляРедактирования = "РегистрацияВИФНС;ГруппаП;ГруппаО;_СвНевозмПред;ГруппаИсходныйЗапрос;ГруппаЭтоУведомление";
	
	Для Каждого Элт Из СтрРазделить(РеквизитыДляРедактирования, ";", Ложь) Цикл 
		Элементы[Элт].Доступность = Доступность;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьДанныеОрганизации()
	Если ЭтотОбъект["ЭтоЮЛ"] Тогда
		СтрокаСведений = "ИННЮЛ,НаимЮЛПол,КППЮЛ,ОГРН";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, 
			ТекущаяДатаСеанса(), СтрокаСведений);
		_ИННЮЛОтпр = СведенияОбОрганизации.ИННЮЛ;
		_КППОтпр = СведенияОбОрганизации.КППЮЛ;
		_НаимОргОтпр = СведенияОбОрганизации.НаимЮЛПол;
		_ОГРНОтпр = СведенияОбОрганизации.ОГРН;
	Иначе
		СтрокаСведений = "ИННФЛ,ФамилияИП,ИмяИП,ОтчествоИП,ОГРН";
		СведенияОбОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Объект.Организация, 
			ТекущаяДатаСеанса(), СтрокаСведений);
		_ИННФЛОтпр = СведенияОбОрганизации.ИННФЛ;
		_ФамилияОтпр = СведенияОбОрганизации.ФамилияИП;
		_ИмяОтпр = СведенияОбОрганизации.ИмяИП;
		_ОтчествоОтпр = СведенияОбОрганизации.ОтчествоИП;
		_ОГРНИПОтпр = СведенияОбОрганизации.ОГРН;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура _ДобавитьСвНевПредЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда 
		СтрокаДляОбработки = ?(Ответ.ИдентификаторСтроки = Неопределено, _СвНевозмПред.Добавить(), _СвНевозмПред.НайтиПоИдентификатору(Ответ.ИдентификаторСтроки));
		ЗаполнитьЗначенияСвойств(СтрокаДляОбработки, Ответ);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьВыводСтатусовИВажныхСообщений_85()

	Если РегламентированнаяОтчетность.ДоступнаПодсистема85() Тогда
		МодульЭДОСКонтролирующимиОрганами = ОбщегоНазначения.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганами");
		МодульЭДОСКонтролирующимиОрганами.УстановитьРазмещениеНаименованияЭтапаБРО_85(ЭтотОбъект);
		МодульЭДОСКонтролирующимиОрганами.ВывестиСтатусыИВыжныеСообщенияНаФорму_85(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере() Экспорт

	Если РегламентированнаяОтчетность.ДоступнаПодсистема85() Тогда
		МодульЭДОСКонтролирующимиОрганами = ОбщегоНазначения.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганами");
		МодульЭДОСКонтролирующимиОрганами.ВывестиСтатусыИВыжныеСообщенияНаФорму_85(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДетальноеОписаниеСтатусаПослеВыбора(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если Результат = "ОбновитьОтправку" Тогда
		
		ОбновитьОтправку(Неопределено);
		
	ИначеЕсли Результат = "ГиперссылкаПротокол" Тогда 	
		
		ГиперссылкаПротоколНажатие(Неопределено);
		
	ИначеЕсли Результат = "ЭтапыОтправки" Тогда 	
		
		ЭтапыОтправкиНажатие(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиСтатусыИВыжныеСообщенияНаФорму() Экспорт

	ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНажатияКнопкиВВажномСообщении(Команда)

	Если Команда.Имя = "ОтправитьНеотправленноеИзвещение" Тогда
		ОтправитьНеотправленноеИзвещение(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылкиВВажномСообщении(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Если НРег(НавигационнаяСсылкаФорматированнойСтроки) = "критическиеошибки" Тогда
		КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаЗакрытияВажногоСообщения(Команда)

	МодульКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер");
	МодульКлиентСервер.ЗакрытьВажноеСообщение(ЭтотОбъект, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)

	МодульОбщегоНазначенияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбщегоНазначенияКлиент");
	МодульОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект, ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти
