#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Т11");
	НастройкиВарианта.Описание = НСтр("ru = 'Унифицированная форма № Т-11';
										|en = 'Unified form No. T-11'");
	НастройкиВарианта.Включен = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "Т11а");
	НастройкиВарианта.Описание = НСтр("ru = 'Унифицированная форма № Т-11а';
										|en = 'Unified form No. T-11a'");
	НастройкиВарианта.Включен = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ЗарплатаКадрыОтчеты.ВывестиВКоллекциюПечатнуюФорму("Отчет.ПечатнаяФормаТ11",
		МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
КонецПроцедуры

Процедура Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено) Экспорт
	
	Если РезультатКомпоновки.ОтчетПустой Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатКомпоновки.КлючВарианта = "Т11" Тогда
		ВывестиНаПечатьТ11(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати, ПараметрыПечати);
	ИначеЕсли РезультатКомпоновки.КлючВарианта = "Т11а" Тогда
		ВывестиНаПечатьТ11а(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиНаПечатьТ11(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати, ПараметрыПечати = Неопределено)
	
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Премия_Т11";
	ДокументРезультат.ОриентацияСтраницы= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Для Каждого ДанныеНаПечать Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПерваяСтрокаПечатнойФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		Для Каждого ДанныеДетальныхЗаписей Из ДанныеНаПечать.Строки Цикл
			
			НачалоБланка = ДокументРезультат.ВысотаТаблицы + 1;
			Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			Если ДанныеНаПечать.Строки.Количество() = 1 Тогда
				НомерНаПечать = Строка(ДанныеДетальныхЗаписей.СсылкаНаОбъектНомерНаПечать);
			Иначе
				НомерНаПечать = Строка(ДанныеДетальныхЗаписей.СсылкаНаОбъектНомерНаПечать) + "/" + ДанныеДетальныхЗаписей.ДанныеПоощренияНомерСтроки;
			КонецЕсли;
			ДанныеПользовательскихПолей = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеДетальныхЗаписей);			ДанныеПользовательскихПолей.Вставить("СсылкаНаОбъектНомерНаПечать", НомерНаПечать);
			
			Если РезультатКомпоновки.ДанныеОтчета.Колонки.Найти("ФизическоеЛицо") = Неопределено
				Или РезультатКомпоновки.ДанныеОтчета.Колонки.Найти("Награда") = Неопределено Тогда
				ОснованиеНаПечать = "";
			Иначе
				ОснованиеНаПечать = ОснованиеНаПечать(ДанныеДетальныхЗаписей.СсылкаНаОбъект,
								ДанныеДетальныхЗаписей.РаботаОрганизация,
								ДанныеДетальныхЗаписей.ФизическоеЛицо,
								ДанныеДетальныхЗаписей.Награда);
			КонецЕсли;
								
								
			ПараметрыОснование = Новый Структура("ОснованиеТекст", ОснованиеНаПечать);
			
			ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
				ДокументРезультат,
				РезультатКомпоновки.МакетПечатнойФормы,
				"ПриказШапка",
				ДанныеНаПечать,
				ДанныеДетальныхЗаписей,
				ДанныеПользовательскихПолей);
				
			Награждение = Ложь;
			Если ПараметрыПечати = Неопределено ИЛИ Не ПараметрыПечати.Свойство("Награждение", Награждение)
				ИЛИ Не Награждение = Истина Тогда

				ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
					ДокументРезультат,
					РезультатКомпоновки.МакетПечатнойФормы,
					"ПриказРезультат",
					ДанныеНаПечать,
					ДанныеДетальныхЗаписей,
					ДанныеПользовательскихПолей);
					
			КонецЕсли;
				
			ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
				ДокументРезультат,
				РезультатКомпоновки.МакетПечатнойФормы,
				"Основание",
				ПараметрыОснование);
				
			ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
				ДокументРезультат,
				РезультатКомпоновки.МакетПечатнойФормы,
				"ПриказПодвал",
				ДанныеНаПечать,
				ДанныеДетальныхЗаписей,
				ДанныеПользовательскихПолей);
			
			Если ПараметрыПечати <> Неопределено Тогда
				ЭДОБЗК.ЗадатьДетальнуюОбластьПечати(ПараметрыПечати, ДокументРезультат,
					"ПФ_MXL_Отгул", НачалоБланка, ДанныеДетальныхЗаписей, ДанныеДетальныхЗаписей.СсылкаНаОбъект);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ОбъектыПечати <> Неопределено Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, ПерваяСтрокаПечатнойФормы, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиНаПечатьТ11а(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати)
	
	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Премия_Т11а";
	ДокументРезультат.ОриентацияСтраницы= ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Для Каждого ДанныеНаПечать Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПерваяСтрокаПечатнойФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		ДанныеПользовательскихПолейШапки = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеНаПечать);
		
		ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
			ДокументРезультат, РезультатКомпоновки.МакетПечатнойФормы, "Шапка",
			ДанныеНаПечать,
			ДанныеПользовательскихПолейШапки);
		
		ОбластьШапкиТаблицы = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("ШапкаПовторятьПриПечати");
		ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьШапкиТаблицы,
			ДанныеНаПечать,
			ДанныеПользовательскихПолейШапки);
		
		ОбластьПодвал = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Подвал");
		
		Если РезультатКомпоновки.ДанныеОтчета.Колонки.Найти("ФизическоеЛицо") = Неопределено
			Или РезультатКомпоновки.ДанныеОтчета.Колонки.Найти("Награда") = Неопределено Тогда
			ОснованиеНаПечать = "";
		Иначе
			ФизическиеЛица = ДанныеНаПечать.Строки.Выгрузитьколонку("ФизическоеЛицо");
			ОснованиеНаПечать = ОснованиеНаПечать(ДанныеНаПечать.СсылкаНаОбъект, ДанныеНаПечать.РаботаОрганизация, ФизическиеЛица, ДанныеНаПечать.Награда);
		КонецЕсли;
		
		ПараметрыОснование = Новый Структура("ОснованиеТекст", ОснованиеНаПечать);
		
		ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьПодвал,
			ДанныеНаПечать,
			ДанныеПользовательскихПолейШапки,
			ПараметрыОснование);
		
		Для Каждого ДанныеДетальныхЗаписей Из ДанныеНаПечать.Строки Цикл
			
			ДанныеПользовательскихПолей = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеДетальныхЗаписей);
			
			ОбластьСтроки = РезультатКомпоновки.МакетПечатнойФормы.ПолучитьОбласть("Строка");
			ЗарплатаКадрыОтчеты.ЗаполнитьПараметрыОбластиМакета(ОбластьСтроки,
				ДанныеНаПечать,
				ДанныеДетальныхЗаписей,
				ДанныеПользовательскихПолей);
			
			СписокУмещаемыхОбластей = Новый Массив;
			СписокУмещаемыхОбластей.Добавить(ОбластьСтроки);
			
			Если ДанныеНаПечать.Строки.Индекс(ДанныеДетальныхЗаписей) = ДанныеНаПечать.Строки.Количество()-1 Тогда
				СписокУмещаемыхОбластей.Добавить(ОбластьПодвал);
			КонецЕсли;
			
			Если НЕ ДокументРезультат.ПроверитьВывод(СписокУмещаемыхОбластей) Тогда
				
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				ДокументРезультат.Вывести(ОбластьШапкиТаблицы);
				
			КонецЕсли;
			
			ДокументРезультат.Вывести(ОбластьСтроки);
			
		КонецЦикла;
		
		ДокументРезультат.Вывести(ОбластьПодвал);
		
		Если ОбъектыПечати <> Неопределено Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, ПерваяСтрокаПечатнойФормы, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ВидыПоощренийНаПечать(Награда, ВидПремии, ВидПризаПодарка) Экспорт
	
	ВидыПоощрений = Новый Массив;

	Если Не Награда = Null Тогда
		КатегорияНаграды = Справочники.КатегорииНаград.ПустаяСсылка();
		Если ТипЗнч(Награда) = Тип("СправочникСсылка.Награды") Тогда
			КатегорияНаграды = Награда.КатегорияНаграды;
		КонецЕсли;
		Если КатегорияНаграды = Справочники.КатегорииНаград.ПустаяСсылка() Тогда
			Шаблон = НСтр("ru = '[Награда]';
							|en = '[Награда]'");
		Иначе
			Шаблон = НСтр("ru = '[КатегорияНаграды] ""[Награда]""';
							|en = '[КатегорияНаграды] ""[Награда]""'");
		КонецЕсли;
		ПараметрыШаблона = Новый Структура("КатегорияНаграды, Награда", 
							СклонениеПредставленийОбъектов.ПросклонятьПредставление(Строка(КатегорияНаграды), 5), Строка(Награда));
		ВидыПоощрений.Добавить(СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Шаблон, ПараметрыШаблона));
	КонецЕсли;

	Если Не ВидПремии = Null Тогда
		ВидыПоощрений.Добавить(СклонениеПредставленийОбъектов.ПросклонятьПредставление(Строка(ВидПремии), 5));
	КонецЕсли;
	
	Если Не ВидПризаПодарка = Null Тогда
		Если ВидПризаПодарка = Перечисления.ВидыПризовПодарков.Приз Тогда
			ВидыПоощрений.Добавить(СклонениеПредставленийОбъектов.ПросклонятьПредставление(НСтр("ru = 'Ценный приз';
																								|en = 'A valuable prize'"), 5));
		Иначе
			ВидыПоощрений.Добавить(СклонениеПредставленийОбъектов.ПросклонятьПредставление(НСтр("ru = 'Ценный подарок';
																								|en = 'A valuable gift'"), 5));
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтрСоединить(ВидыПоощрений, СтрШаблон(НСтр("ru = ',%1';
														|en = ',%1'"), Символы.ПС));
	
КонецФункции

Функция ВидПоощренияНаПечать(ВидПремии, ВидПризаПодарка) Экспорт
	
	Если ВидПремии = Null Тогда
		
		Если ВидПризаПодарка = Перечисления.ВидыПризовПодарков.Приз Тогда
			ВидПоощрения = НСтр("ru = 'Ценный приз';
								|en = 'A valuable prize'");
		Иначе
			ВидПоощрения = НСтр("ru = 'Ценный подарок';
								|en = 'A valuable gift'");
		КонецЕсли;
		
		Возврат ВидПоощрения;
		
	Иначе
		Возврат Строка(ВидПремии);
	КонецЕсли;
	
КонецФункции

Функция ОснованиеНаПечать(ДокументНематериальногоПоощрения, Знач Организация, Знач ФизическиеЛица, Знач Награда)
	
	ОснованиеПриказа = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПроцессыНаграждения") Тогда
		МодульПроцессыНаграждения = ОбщегоНазначения.ОбщийМодуль("ПроцессыНаграждения");
		ОснованиеПриказа = МодульПроцессыНаграждения.ОснованиеПриказаПечатнойФормыТ11(ДокументНематериальногоПоощрения, Организация, ФизическиеЛица, Награда);
	КонецЕсли;
	
	Возврат ОснованиеПриказа;
	
КонецФункции


#КонецОбласти

#КонецЕсли