#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПустойИдентификатор", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());

	ЗаказНаПроизводство = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ЗаказНаПроизводство").Значение;
	Назначения = ПолучитьНазначенияЗаказа(ЗаказНаПроизводство);
	ЕстьНазначения = Не Назначения.Количество() = 0;
	
	ТолькоИзлишки = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ТолькоИзлишки").Значение;
	СверхЗаказа = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "СверхЗаказа").Значение;
	Номенклатура = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "Номенклатура");
	
	ДоступныВсеСклады = ДоступныВсеСклады();
	
	Если Не ДоступныВсеСклады Тогда
		
		СхемаИзлишки = СхемаКомпоновкиДанных.ВложенныеСхемыКомпоновкиДанных.Секция1_ОбособленныеИзлишки; // ВложеннаяСхемаКомпоновкиДанных
		НаборИзлишки = СхемаИзлишки.Схема.НаборыДанных.ОбособленныеИзлишки;
		ПолеИзлишек = НаборИзлишки.Поля.Найти("Излишек"); // ПолеНабораДанныхСхемыКомпоновкиДанных
		ПолеИзлишек.Заголовок = НСтр("ru = 'Излишек (на доступных складах)';
									|en = 'Surplus (in available warehouses)'");
		
		СхемаИзлишкиДефицит = СхемаКомпоновкиДанных.ВложенныеСхемыКомпоновкиДанных.Секция2_ВРаботе; // ВложеннаяСхемаКомпоновкиДанных
		НаборИзлишкиДефицит = СхемаИзлишкиДефицит.Схема.НаборыДанных.ИзлишкиДефицит;
		
		ПолеИзлишекДефицит = НаборИзлишкиДефицит.Поля.Найти("Излишек"); // ПолеНабораДанныхСхемыКомпоновкиДанных
		ПолеИзлишекДефицит.Заголовок = НСтр("ru = 'Излишек (на доступных складах)';
											|en = 'Surplus (in available warehouses)'");
		
		ПолеИзлишекДефицит = НаборИзлишкиДефицит.Поля.Найти("Дефицит"); // ПолеНабораДанныхСхемыКомпоновкиДанных
		ПолеИзлишекДефицит.Заголовок = НСтр("ru = 'Дефицит (на доступных складах)';
											|en = 'Shortage (in available warehouses)'");
		
	КонецЕсли;
	
	Для Каждого Группировка Из КомпоновщикНастроек.Настройки.Структура Цикл
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Группировка.Настройки, "Номенклатура", Номенклатура.Значение, Номенклатура.Использование);
		Если Группировка.Имя = "ОбособленныеИзлишки" Тогда
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Группировка.Настройки, "Назначения", Назначения, Истина);
			КомпоновкаДанныхКлиентСервер.УстановитьПараметр(Группировка.Настройки, "ЕстьНазначения", ЕстьНазначения);
			Группировка.Использование = ЕстьНазначения;
		ИначеЕсли Группировка.Имя = "ВРаботе" Тогда
			Если ТолькоИзлишки Тогда
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Группировка.Настройки.Отбор, "Излишек", 0, ВидСравненияКомпоновкиДанных.Больше);
			Иначе
				КомпоновкаДанныхКлиентСервер.УдалитьОтбор(Группировка.Настройки.Отбор, "Излишек");
			КонецЕсли;
			Если СверхЗаказа И Группировка.Настройки.Структура.Количество() > 0 Тогда
				КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Группировка.Настройки.Структура[0].Отбор, "СверхЗаказа", 0, ВидСравненияКомпоновкиДанных.Больше);
			Иначе
				КомпоновкаДанныхКлиентСервер.УдалитьОтбор(Группировка.Настройки.Структура[0].Отбор, "СверхЗаказа");
			КонецЕсли;
		ИначеЕсли Группировка.Имя = "Произведено" Тогда
			Группировка.Использование = Не ТолькоИзлишки;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - см. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	Настройки.События.ПриСозданииНаСервере = Истина;
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   ЭтаФорма - ФормаКлиентскогоПриложения - Форма отчета.
//   Отказ - Булево - Передается из параметров обработчика "как есть".
//   СтандартнаяОбработка - Булево - Передается из параметров обработчика "как есть".
//
// См. также:
//   "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
Процедура ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка) Экспорт
	Параметры = ЭтаФорма.Параметры;
	Если Параметры.Свойство("ПараметрКоманды") И ТипЗнч(Параметры.ПараметрКоманды) = Тип(
		"ДокументСсылка.ЗаказНаПроизводство2_2") Тогда
		ЭтаФорма.ФормаПараметры.Отбор.Вставить("ЗаказНаПроизводство", Параметры.ПараметрКоманды);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНазначенияЗаказа(ЗаказНаПроизводство)
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Назначения.Ссылка КАК Назначение
	|ИЗ
	|	Справочник.Назначения КАК Назначения
	|ГДЕ
	|	Назначения.Ссылка В
	|		(ВЫБРАТЬ
	|			ЗаказНаПроизводство2_2.Назначение КАК Назначение
	|		ИЗ
	|			Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
	|		ГДЕ
	|			ЗаказНаПроизводство2_2.Ссылка = &ЗаказНаПроизводство
	|
	|		ОБЪЕДИНИТЬ
	|
	|		ВЫБРАТЬ
	|			ЗаказНаПроизводство2_2.НазначениеПродукция
	|		ИЗ
	|			Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
	|		ГДЕ
	|			ЗаказНаПроизводство2_2.Ссылка = &ЗаказНаПроизводство
	|			И НЕ ЗаказНаПроизводство2_2.НазначениеПродукция = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|
	|		ОБЪЕДИНИТЬ
	|
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ЗаказНаПроизводство2_2Продукция.Назначение
	|		ИЗ
	|			Документ.ЗаказНаПроизводство2_2.Продукция КАК ЗаказНаПроизводство2_2Продукция
	|		ГДЕ
	|			ЗаказНаПроизводство2_2Продукция.Ссылка = &ЗаказНаПроизводство
	|			И НЕ ЗаказНаПроизводство2_2Продукция.Назначение = ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
	|	И Назначения.Заказ <> НЕОПРЕДЕЛЕНО";

	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Назначение");
КонецФункции

Функция ДоступныВсеСклады()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(Склады.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ВсегоСкладов = Выборка.Количество;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(Склады.Ссылка) КАК Количество
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	НЕ Склады.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ДоступноСкладов = Выборка.Количество;
	
	Возврат ВсегоСкладов = ДоступноСкладов;
	
КонецФункции

#КонецОбласти

#КонецЕсли