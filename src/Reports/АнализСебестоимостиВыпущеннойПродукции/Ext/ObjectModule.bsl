#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//
// Параметры:
//	Контекст							- Произвольный								- параметры контекста, в котором используется отчет.
//	КлючСхемы							- Строка									- идентификатор текущей схемы компоновщика настроек.
//	КлючВарианта						- Строка									- имя предопределенного или уникальный идентификатор пользовательского
//																						варианта отчета.
//										- Неопределено 								- вызов для варианта расшифровки или без контекста.
//	НовыеНастройкиКД					- НастройкиКомпоновкиДанных					- настройки варианта отчета, которые будут загружены
//																						в компоновщик настроек после его инициализации.
//										- Неопределено 								- настройки варианта не надо загружать (уже загружены ранее).
//	НовыеПользовательскиеНастройкиКД	- ПользовательскиеНастройкиКомпоновкиДанных - пользовательские настройки, которые будут загружены в компоновщик
//																						настроек после его инициализации.
//										- Неопределено 								- пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Если НовыеПользовательскиеНастройкиКД = Неопределено Тогда
			ПользовательскиеНастройки = Контекст.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки; // ПользовательскиеНастройкиКомпоновкиДанных -
			НастроитьПараметрыОтчетаПоВариантуОтчета(Контекст.НастройкиОтчета, НовыеНастройкиКД, ПользовательскиеНастройки);
		Иначе
			НастроитьПараметрыОтчетаПоВариантуОтчета(Контекст.НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
		КонецЕсли;
		ОтчетыСервер.ПодключитьСхему(ЭтотОбъект, Контекст, СхемаКомпоновкиДанных, КлючСхемы);
		
		НастроитьПараметрыОтборыПоФункциональнымОпциям(НовыеНастройкиКД);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("Отчет.АнализСебестоимостиВыпущеннойПродукции.МодульОбъекта.ПриКомпоновкеРезультата");
	
	СтандартнаяОбработка = Ложь;
	
	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	
	ПараметрПартионныйУчетВерсии22 = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ПартионныйУчетВерсии22");
	Если ПараметрПартионныйУчетВерсии22 <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ПартионныйУчетВерсии22",
			РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22());
	КонецЕсли;
	
	ПараметрДатаПереходаНаПартионныйУчетВерсии22 = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек, "ДатаПереходаНаПартионныйУчетВерсии22");
	Если ПараметрДатаПереходаНаПартионныйУчетВерсии22 <> Неопределено Тогда
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "ДатаПереходаНаПартионныйУчетВерсии22",
			РасчетСебестоимостиПовтИсп.ДатаПереходаНаПартионныйУчетВерсии22());
	КонецЕсли;
	
	ТекстЗапросаМатериальные = РасчетСебестоимостиЛокализация.АнализСебестоимостиВыпущеннойПродукции_ТекстЗапросаМатериальные();
	Если ЗначениеЗаполнено(ТекстЗапросаМатериальные) Тогда
		
		НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Затраты; // НаборДанныхОбъединениеСхемыКомпоновкиДанных
		НаборДанных.Элементы.Материальные.Запрос = ТекстЗапросаМатериальные;
		
	КонецЕсли;
	
	ТекстЗапросаМатериальные_Версия22 = ТекстЗапросаМатериальные_Версия22();
	
	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Затраты; // НаборДанныхОбъединениеСхемыКомпоновкиДанных
	НаборДанных.Элементы.Материальные_Версия22.Запрос = ТекстЗапросаМатериальные_Версия22;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	УдалитьПоказателиИзОтборовРасшифровки(КомпоновщикНастроек, НастройкиОтчета);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ДокументРезультат.ВысотаТаблицы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Настраивает доступные значения у параметра компоновки данных "Данные по себестоимости" в зависимости от варианта.
// 
// Параметры:
//	Документ - ДокументСсылка.РаспределениеПрочихЗатрат - расшифровывающийся документ.
//	НовыеНастройкиКД - НастройкиКомпоновкиДанных - переопределяемые настройки отчета.
//	НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - настройки схемы компоновки данных.
Процедура НастроитьПараметрыОтчетаПоВариантуОтчета(НастройкиОтчета, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД)
	
	КлючиВариантов = КлючиПредопределенныхВариантов();
	ПредопределенныйВариант = ПолучитьПредопределенныйВариант(НастройкиОтчета.ВариантСсылка);
	КлючПредопределенногоВарианта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПредопределенныйВариант, "КлючВарианта");
	Если Не ЗначениеЗаполнено(ПредопределенныйВариант) 
		Или КлючиВариантов.Найти(КлючПредопределенногоВарианта) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрДанныеОтчета = СхемаКомпоновкиДанных.Параметры.Найти("ДанныеПоСебестоимости");
	ПараметрПоПредприятию = СхемаКомпоновкиДанных.Параметры.Найти("ПоПредприятию");
	
	СписокВыбора = Новый СписокЗначений;
	
	Если ПредопределенныйВариант.КлючВарианта = "АнализСебестоимостиВыпущеннойПродукцииПоПредприятию"
		Или ПредопределенныйВариант.КлючВарианта = "АнализСебестоимостиВыпущеннойПродукции22ПоПредприятию"
		Или ПредопределенныйВариант.КлючВарианта = "СравнениеСебестоимостиВыпущеннойПродукцииПоЗаказамПоПредприятию"
		Или ПредопределенныйВариант.КлючВарианта = "ВыпускПродукцииПоПлановойСтоимостиПоПредприятию" Тогда
		
		СписокВыбора.Добавить(1, НСтр("ru = 'В валюте упр. учета с НДС';
										|en = 'In management accounting currency, including VAT'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'В валюте упр. учета без НДС';
										|en = 'In management accounting currency, excluding VAT'"));
		ПараметрПоПредприятию.Значение = Истина;
		
	Иначе
		
		Если РасчетСебестоимостиПовтИсп.УправленческийУчетОрганизаций() Тогда
			СписокВыбора.Добавить(3, НСтр("ru = 'В валюте упр. учета';
											|en = 'In management accounting currency'"));
		КонецЕсли;
		СписокВыбора.Добавить(4, НСтр("ru = 'В валюте регл. учета';
										|en = 'In local accounting currency'"));
		ПараметрПоПредприятию.Значение = Ложь;
		
	КонецЕсли;
	
	ПараметрДанныеОтчета.УстановитьДоступныеЗначения(СписокВыбора);
	
	Если НовыеНастройкиКД = Неопределено
		Или НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеПараметраДанныеОтчета = НовыеНастройкиКД.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДанныеПоСебестоимости"));
	НастройкаДанныеОтчета = НовыеПользовательскиеНастройкиКД.Элементы.Найти(ЗначениеПараметраДанныеОтчета.ИдентификаторПользовательскойНастройки);
	
	Если Не НастройкаДанныеОтчета = Неопределено
		И СписокВыбора.НайтиПоЗначению(НастройкаДанныеОтчета.Значение) = Неопределено Тогда
		НастройкаДанныеОтчета.Значение = СписокВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьПредопределенныйВариант(Знач Вариант)
	
	КлючиВариантов = КлючиПредопределенныхВариантов();
	
	Пока КлючиВариантов.Найти(Вариант.КлючВарианта) = Неопределено
		И ЗначениеЗаполнено(Вариант.Родитель) Цикл
		Вариант = Вариант.Родитель;
	КонецЦикла;
	
	Возврат Вариант;
	
КонецФункции

Функция КлючиПредопределенныхВариантов()
	
	КлючиВариантов = Новый Массив;
	КлючиВариантов.Добавить("АнализСебестоимостиВыпущеннойПродукции");
	КлючиВариантов.Добавить("АнализСебестоимостиВыпущеннойПродукцииПоПредприятию");
	КлючиВариантов.Добавить("СравнениеСебестоимостиВыпущеннойПродукцииПоЗаказам");
	КлючиВариантов.Добавить("СравнениеСебестоимостиВыпущеннойПродукцииПоЗаказамПоПредприятию");
	КлючиВариантов.Добавить("ВыпускПродукцииПоПлановойСтоимостиПоОрганизациям");
	
	Возврат КлючиВариантов;
	
КонецФункции

Процедура НастроитьПараметрыОтборыПоФункциональнымОпциям(КомпоновщикНастроекФормы)
	
	ВедетсяУчетПостоянныхИВременныхРазниц = Ложь; 
	//++ НЕ УТ
	
	//++ Локализация
	ВедетсяУчетПостоянныхИВременныхРазниц = Истина;
	//-- Локализация
	
	//-- НЕ УТ
	Если НЕ ВедетсяУчетПостоянныхИВременныхРазниц Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить("ПостояннаяРазница");
		УдаляемыеПоля.Добавить("ВременнаяРазница");
		УдаляемыеПоля.Добавить("СтоимостьНУ");
		РасчетСебестоимостиПрикладныеАлгоритмы.ОграничитьИспользованиеПолейСКД(УдаляемыеПоля, СхемаКомпоновкиДанных,
			КомпоновщикНастроек, "", Истина);
		Для Каждого Поле Из УдаляемыеПоля Цикл 
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, Поле);
		КонецЦикла;
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля, Истина);
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("УчетПоНДД") Тогда
		УдаляемыеПоля = Новый Массив; // Массив из Строка
		УдаляемыеПоля.Добавить("НДД");
		Для Каждого Поле Из УдаляемыеПоля Цикл 
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(КомпоновщикНастроекФормы, Поле);
		КонецЦикла;
		КомпоновкаДанныхСервер.УдалитьВыбранноеПолеИзВсехНастроекОтчета(КомпоновщикНастроекФормы, УдаляемыеПоля, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьПоказателиИзОтборовРасшифровки(КомпоновщикНастроек, НастройкиОтчета)
	Если КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.Свойство("РежимРасшифровки")
		И КомпоновщикНастроек.ФиксированныеНастройки.ДополнительныеСвойства.РежимРасшифровки Тогда
		Показатели = Новый Массив; // Массив из Строка
		Показатели.Добавить("КоличествоЗатрат");
		Показатели.Добавить("СтоимостьЗатрат");
		Показатели.Добавить("СтоимостьЗатратНаЕдиницуПродукции");
		Показатели.Добавить("КоличествоЗатратНаЕдиницуПродукции");
		Показатели.Добавить("СтоимостьЗатратЗабалансовая");
		Показатели.Добавить("СтоимостьНУ");
		Показатели.Добавить("ВременнаяРазница");
		Показатели.Добавить("ПостояннаяРазница");
		Показатели.Добавить("СтоимостьЗатратНаЕдиницуПродукцииЗабаланс");
		Показатели.Добавить("НДД");
		Показатели.Добавить("ПлановаяСтоимость");
		Показатели.Добавить("ОтклонениеСтоимостиПродукции");
		Показатели.Добавить("КоличествоПродукции");
		
		Для Каждого Показатель Из Показатели Цикл 
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(НастройкиОтчета, Показатель);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

Функция ТекстЗапросаМатериальные_Версия22()
	
	Возврат "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Сегменты.Номенклатура КАК Номенклатура,
	|	Сегменты.Характеристика КАК Характеристика,
	|	ИСТИНА КАК ИспользуетсяОтборПоСегментуНоменклатуры
	|ПОМЕСТИТЬ ОтборПоСегментуНоменклатуры
	|ИЗ
	|	РегистрСведений.НоменклатураСегмента КАК Сегменты
	|{ГДЕ
	|	Сегменты.Сегмент.* КАК СегментНоменклатуры,
	|	Сегменты.Номенклатура.* КАК Номенклатура,
	|	Сегменты.Характеристика.* КАК Характеристика}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	ИспользуетсяОтборПоСегментуНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СебестоимостьТоваров.Организация КАК Организация,
	|	НАЧАЛОПЕРИОДА(СебестоимостьТоваров.Период, МЕСЯЦ) КАК ПериодДействия
	|ПОМЕСТИТЬ ПериодыДействияСреднескользящей
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК СебестоимостьТоваров
	|ГДЕ
	|	СебестоимостьТоваров.СпособОпределенияСебестоимости = ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимостиТоваров.ПоСреднескользящей)
	|	И (СебестоимостьТоваров.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|{ГДЕ
	|	СебестоимостьТоваров.Организация.*}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодДействия,
	|	Организация
	|;
	|
	//++ Устарело_Переработка24
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДД.Регистратор,
	|	ДД.АналитикаУчетаНоменклатуры,
	|	ДД.ВидЗапасов,
	|	ДД.КорАналитикаУчетаНоменклатуры,
	|	ДД.КорВидЗапасов,
	|	ДД.ИдентификаторСтроки
	|ПОМЕСТИТЬ РаботыДляДавальца
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаВыпуска
	|		ПО АналитикаВыпуска.Ссылка = ДД.КорАналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО ИСТИНА
	|ГДЕ
	|	(ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И ДД.Активность
	|	И ДД.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Партия)
	|	И ДД.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|	И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|	И ДД.КорАналитикаУчетаНоменклатуры <> ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	|	И &ПартионныйУчетВерсии22
	|	И ДД.Период >= &ДатаПереходаНаПартионныйУчетВерсии22
	|{ГДЕ
	|	(АналитикаВыпуска.Номенклатура, АналитикаВыпуска.Характеристика) В
	|		(ВЫБРАТЬ
	|			ОтборПоСегментуНоменклатуры.Номенклатура,
	|			ОтборПоСегментуНоменклатуры.Характеристика
	|		ИЗ
	|			ОтборПоСегментуНоменклатуры
	|		ГДЕ
	|			ОтборПоСегментуНоменклатуры.ИспользуетсяОтборПоСегментуНоменклатуры = &ИспользуетсяОтборПоСегментуНоменклатуры)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	КорАналитикаУчетаНоменклатуры,
	|	КорВидЗапасов
	|;
	//-- Устарело_Переработка24
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДД.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ Константы.ИспользоватьУправлениеПроизводством2_2
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА АналитикаВыпуска.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА АналитикаВыпуска.Подразделение
	|		КОГДА АналитикаВыпуска.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|			ТОГДА АналитикаВыпуска.Партнер
	|		ИНАЧЕ АналитикаВыпуска.СкладскаяТерритория.Подразделение
	|	КОНЕЦ КАК ПодразделениеПродукции,
	|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаПродукции,
	|	АналитикаВыпуска.Номенклатура КАК Продукция,
	|	АналитикаВыпуска.Характеристика КАК ХарактеристикаПродукции,
	|	АналитикаВыпуска.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПродукции,
	|	ВЫБОР
	|		КОГДА &ДетализироватьПоСериямВыпуска
	|			ТОГДА АналитикаВыпуска.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК СерияПродукции,
	|	ВЫБОР
	|		КОГДА Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|			ТОГДА Аналитика.СтатьяКалькуляции
	|		ИНАЧЕ ДД.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляции,
	|	""Партии"" КАК ВидЗатрат,
	|	Аналитика.Номенклатура КАК Затрата,
	|	Аналитика.Характеристика КАК ХарактеристикаЗатраты,
	|	Аналитика.Серия КАК СерияЗатраты,
	|	ВЫБОР
	|		КОГДА &ЕдиницыКоличества = 0
	|			ТОГДА Аналитика.Номенклатура.ЕдиницаИзмерения
	|		КОГДА &ЕдиницыКоличества = 1
	|			ТОГДА Аналитика.Номенклатура.ЕдиницаДляОтчетов
	|	КОНЕЦ КАК ЕдиницаИзмеренияЗатраты,
	|	Аналитика.МестоХранения КАК Подразделение,
	|	ВЫБОР
	|		КОГДА ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ТОГДА НЕОПРЕДЕЛЕНО
			//++ НЕ УТКА
	|		КОГДА ЭтапПроизводства2_2.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЭтапПроизводства2_2.Распоряжение
			//-- НЕ УТКА

			//++ Устарело_Производство21

			//++ НЕ УТКА
	|		КОГДА ДД.Партия ССЫЛКА Документ.ЗаказНаПроизводство ТОГДА ДД.Партия
			//-- НЕ УТКА

			//-- Устарело_Производство21
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА &ДетализироватьПоПартиямПроизводства
	|			ТОГДА ДД.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|	КОНЕЦ КАК ПартияПроизводства,
	|	ВЫБОР
	|		КОГДА ДД.АналитикаФинансовогоУчета ССЫЛКА Справочник.ГруппыАналитическогоУчетаНоменклатуры
	|			ТОГДА ДД.АналитикаФинансовогоУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаПродукции,
	|	ДД.КорВидЗапасов КАК ВидЗапасов,
	|	АналитикаВыпуска.Назначение КАК НазначениеПродукции,
	|	ВЫБОР
	|		КОГДА НЕ &ДетализироватьПоНазначениям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			//++ НЕ УТКА
	|		КОГДА НЕ Продукция.Назначение ЕСТЬ NULL
	|			ТОГДА Продукция.Назначение
	|		КОГДА НЕ ЭтапПобочныеИзделия.Назначение ЕСТЬ NULL
	|			ТОГДА ЭтапПобочныеИзделия.Назначение
			//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА АналитикаВыпуска.Назначение
	|	КОНЕЦ КАК НазначениеПолучателяПродукции,
	|	ВЫБОР
	|		КОГДА &ЕдиницыКоличества = 0
	|			ТОГДА ДД.Количество
	|		КОГДА &ЕдиницыКоличества = 1
	|			ТОГДА ВЫБОР
	|				КОГДА Аналитика.Номенклатура.КоэффициентЕдиницыДляОтчетов <> 0
	|					ТОГДА ДД.Количество / Аналитика.Номенклатура.КоэффициентЕдиницыДляОтчетов
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоЗатрат,
	|	ВЫБОР
	|		КОГДА ДокументРасчетаСебестоимости.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 1
	|					ТОГДА ДД.Количество * (Цены.Стоимость + Цены.СтоимостьДопРасходы + Цены.Трудозатраты +
	|						Цены.ПостатейныеПостоянныеСНДС + Цены.ПостатейныеПеременныеСНДС)
	|				КОГДА &ДанныеПоСебестоимости = 2
	|					ТОГДА ДД.Количество * (Цены.СтоимостьБезНДС + Цены.СтоимостьДопРасходыБезНДС + Цены.Трудозатраты +
	|						Цены.ПостатейныеПостоянныеБезНДС + Цены.ПостатейныеПеременныеБезНДС)
	|				КОГДА &ДанныеПоСебестоимости = 3
	|					ТОГДА ДД.Количество * (Цены.СтоимостьУпр + Цены.ДопРасходыУпр + Цены.ТрудозатратыУпр +
	|						Цены.ПостатейныеПостоянныеУпр + Цены.ПостатейныеПеременныеУпр)
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * (Цены.СтоимостьРегл + Цены.ДопРасходыРегл + Цены.ТрудозатратыРегл +
	|						Цены.ПостатейныеПостоянныеРегл + Цены.ПостатейныеПеременныеРегл)
	|				ИНАЧЕ ВЫБОР
	|					КОГДА &ПоПредприятию
	|						ТОГДА ДД.Количество * (Цены.СтоимостьБезНДС + Цены.СтоимостьДопРасходыБезНДС + Цены.Трудозатраты +
	|							Цены.ПостатейныеПостоянныеБезНДС + Цены.ПостатейныеПеременныеБезНДС)
	|					ИНАЧЕ ДД.Количество * (Цены.СтоимостьУпр + Цены.ДопРасходыУпр + Цены.ТрудозатратыУпр +
	|						Цены.ПостатейныеПостоянныеУпр + Цены.ПостатейныеПеременныеУпр)
	|				КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 1
	|				ТОГДА ДД.Стоимость + ДД.ДопРасходы + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеСНДС + ДД.ПостатейныеПеременныеСНДС
	|			КОГДА &ДанныеПоСебестоимости = 2
	|				ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеБезНДС +
	|					ДД.ПостатейныеПеременныеБезНДС
	|			КОГДА &ДанныеПоСебестоимости = 3
	|				ТОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр + ДД.ТрудозатратыУпр + ДД.ПостатейныеПостоянныеУпр +
	|					ДД.ПостатейныеПеременныеУпр
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл + ДД.ТрудозатратыРегл + ДД.ПостатейныеПостоянныеРегл +
	|					ДД.ПостатейныеПеременныеРегл
	|			ИНАЧЕ ВЫБОР
	|				КОГДА &ПоПредприятию
	|					ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеБезНДС +
	|						ДД.ПостатейныеПеременныеБезНДС
	|				ИНАЧЕ ДД.СтоимостьУпр + ДД.ДопРасходыУпр + ДД.ТрудозатратыУпр + ДД.ПостатейныеПостоянныеУпр +
	|					ДД.ПостатейныеПеременныеУпр
	|			КОНЕЦ
	|		КОНЕЦ
	|	КОНЕЦ КАК СтоимостьЗатрат,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.ВременнаяРазница
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.ВременнаяРазница
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.ПостояннаяРазница
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.ПостояннаяРазница
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.СтоимостьНДД
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьНДД
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК НДД,
	|	ВЫБОР
	|		КОГДА ДокументРасчетаСебестоимости.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.СтоимостьЗабалансоваяРегл
	|				ИНАЧЕ ДД.Количество * Цены.СтоимостьЗабалансовая
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьЗабалансоваяРегл
	|			ИНАЧЕ ДД.СтоимостьЗабалансовая
	|		КОНЕЦ
	|	КОНЕЦ КАК СтоимостьЗатратЗабалансовая,
	|	ВЫБОР
	|		КОГДА ПериодыДействияСреднескользящей.Организация ЕСТЬ NULL
	|			ТОГДА ДД.Регистратор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Регистратор
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаВыпуска
	|		ПО (АналитикаВыпуска.Ссылка = ДД.КорАналитикаУчетаНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК Цены
	|		ПО (Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры)
	|			И (Цены.Организация = ДД.Организация)
	|			И (Цены.Период = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
	|			И (Цены.ВидЗапасов = ДД.ВидЗапасов)
	|			И (ВЫБОР
	|				КОГДА Цены.РазделУчета = ДД.РазделУчета
	|					ТОГДА ИСТИНА
	|				КОГДА ДД.Количество < 0
	|						И (ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|								И Цены.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|							ИЛИ Цены.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство), ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)))
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК ДокументРасчетаСебестоимости
	|		ПО (НАЧАЛОПЕРИОДА(ДокументРасчетаСебестоимости.Ссылка.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
	|			И (ДокументРасчетаСебестоимости.Организация = ДД.Организация)
	|			И (ДокументРасчетаСебестоимости.Ссылка.Проведен)
	|			И (ДокументРасчетаСебестоимости.Ссылка.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка))
	|			И (НЕ ДокументРасчетаСебестоимости.Ссылка.ПредварительныйРасчет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыДействияСреднескользящей КАК ПериодыДействияСреднескользящей
	|		ПО (ПериодыДействияСреднескользящей.Организация = ДД.Организация)
	|			И (ПериодыДействияСреднескользящей.ПериодДействия = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
			//++ Устарело_Переработка24
	|		ЛЕВОЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
	|		ПО (РаботыДляДавальца.Регистратор = ДД.Регистратор)
	|			И (РаботыДляДавальца.ВидЗапасов = ДД.КорВидЗапасов)
	|			И (РаботыДляДавальца.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры)
	|			И (РаботыДляДавальца.ИдентификаторСтроки = ДД.ИдентификаторСтроки)
			//-- Устарело_Переработка24

			//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикиПартий
	|		ПО (АналитикиПартий.Ссылка = ДД.КорАналитикаУчетаПартий)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Продукция
	|		ПО ДД.Регистратор = Продукция.Ссылка
	|			И (АналитикиПартий.КодСтроки = Продукция.КодСтроки)
	|			И (НЕ Продукция.Отменено)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ЭтапПобочныеИзделия
	|		ПО ДД.Регистратор = ЭтапПобочныеИзделия.Ссылка
	|			И (АналитикиПартий.КодСтроки = ЭтапПобочныеИзделия.КодСтроки)
	|			И (НЕ ЭтапПобочныеИзделия.Отменено)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО ДД.Регистратор = ЭтапПроизводства2_2.Ссылка
			//-- НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО (Реестр.СторнируемыйДокумент = ДД.Регистратор)
	|			И (Реестр.СторноИсправление)
	|			И (Реестр.ДатаДокументаИБ <= &КонецПериода
	|				ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			И (Реестр.Проведен)
	|ГДЕ
	|	(ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|			ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДД.Активность
	|	И (ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (ТИП(Документ.ПроизводствоБезЗаказа), ТИП(Документ.РегистраторРасчетаСебестоимости))
	|			И Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.Сторно
	|			И Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//++ Устарело_Производство21
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ВыпускПродукции
		//-- Устарело_Производство21	

		//++ НЕ УТКА

		//++ Устарело_Переработка24
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетДавальцу
		//-- Устарело_Переработка24
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
	|			И Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетДавальцуМеждуОрганизациями
	|			И Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
		//-- НЕ УТКА
		
		//++ Устарело_Переработка24
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетПереработчика
	|			И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство)
		//-- Устарело_Переработка24
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетПереработчика2_5
	|			И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство))
	|	И &ПартионныйУчетВерсии22
	|	И ДД.Период >= &ДатаПереходаНаПартионныйУчетВерсии22
	//++ Устарело_Переработка24
	|	И РаботыДляДавальца.Регистратор ЕСТЬ NULL
	//-- Устарело_Переработка24
	
	// Исключаем документы, по которым введено исправление или сторно в текущем периоде.
	|	И Реестр.Ссылка ЕСТЬ NULL
	|	И НЕ ДД.Сторно
	|{ГДЕ
	|	((АналитикаВыпуска.Номенклатура, АналитикаВыпуска.Характеристика) В
	|			(ВЫБРАТЬ
	|				ОтборПоСегментуНоменклатуры.Номенклатура,
	|				ОтборПоСегментуНоменклатуры.Характеристика
	|			ИЗ
	|				ОтборПоСегментуНоменклатуры
	|			ГДЕ
	|				ОтборПоСегментуНоменклатуры.ИспользуетсяОтборПоСегментуНоменклатуры = &ИспользуетсяОтборПоСегментуНоменклатуры))}
	|
	//++ Устарело_Переработка24
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДД.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НЕ Константы.ИспользоватьУправлениеПроизводством2_2
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА АналитикаВыпуска.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Подразделение)
	|			ТОГДА АналитикаВыпуска.Подразделение
	|		КОГДА АналитикаВыпуска.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.Партнер)
	|			ТОГДА АналитикаВыпуска.Партнер
	|		ИНАЧЕ АналитикаВыпуска.СкладскаяТерритория.Подразделение
	|	КОНЕЦ КАК ПодразделениеПродукции,
	|	ДД.КорАналитикаУчетаНоменклатуры КАК АналитикаПродукции,
	|	АналитикаВыпуска.Номенклатура КАК Продукция,
	|	АналитикаВыпуска.Характеристика КАК ХарактеристикаПродукции,
	|	АналитикаВыпуска.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПродукции,
	|	ВЫБОР
	|		КОГДА &ДетализироватьПоСериямВыпуска
	|			ТОГДА АналитикаВыпуска.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК СерияПродукции,
	|	ВЫБОР
	|		КОГДА Аналитика.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
	|			ТОГДА Аналитика.СтатьяКалькуляции
	|		ИНАЧЕ ДД.СтатьяКалькуляции
	|	КОНЕЦ КАК СтатьяКалькуляции,
	|	""Партии"" КАК ВидЗатрат,
	|	Аналитика.Номенклатура КАК Затрата,
	|	Аналитика.Характеристика КАК ХарактеристикаЗатраты,
	|	Аналитика.Серия КАК СерияЗатраты,
	|	ВЫБОР
	|		КОГДА &ЕдиницыКоличества = 0
	|			ТОГДА Аналитика.Номенклатура.ЕдиницаИзмерения
	|		КОГДА &ЕдиницыКоличества = 1
	|			ТОГДА Аналитика.Номенклатура.ЕдиницаДляОтчетов
	|	КОНЕЦ КАК ЕдиницаИзмеренияЗатраты,
	|	Аналитика.МестоХранения КАК Подразделение,
	|	(ВЫБОР
	|		КОГДА ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|			ТОГДА НЕОПРЕДЕЛЕНО
		//++ НЕ УТКА
	|		КОГДА ЭтапПроизводства2_2.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА ЭтапПроизводства2_2.Распоряжение
		//-- НЕ УТКА

		//++ Устарело_Производство21

		//++ НЕ УТКА
	|		КОГДА ДД.Партия ССЫЛКА Документ.ЗаказНаПроизводство ТОГДА ДД.Партия
		//-- НЕ УТКА

		//-- Устарело_Производство21
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ) КАК ЗаказНаПроизводство,
	|	ВЫБОР
	|		КОГДА &ДетализироватьПоПартиямПроизводства
	|			ТОГДА ДД.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииПроизводства.ПустаяСсылка)
	|	КОНЕЦ КАК ПартияПроизводства,
	|	ВЫБОР
	|		КОГДА ДД.АналитикаФинансовогоУчета ССЫЛКА Справочник.ГруппыАналитическогоУчетаНоменклатуры
	|			ТОГДА ДД.АналитикаФинансовогоУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК ГруппаПродукции,
	|	ДД.КорВидЗапасов КАК ВидЗапасов,
	|	АналитикаВыпуска.Назначение КАК НазначениеПродукции,
	|	ВЫБОР
	|		КОГДА НЕ &ДетализироватьПоНазначениям
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			//++ НЕ УТКА
	|		КОГДА НЕ Продукция.Назначение ЕСТЬ NULL
	|			ТОГДА Продукция.Назначение
	|		КОГДА НЕ ЭтапПобочныеИзделия.Назначение ЕСТЬ NULL
	|			ТОГДА ЭтапПобочныеИзделия.Назначение
			//-- НЕ УТКА
	|		КОГДА ИСТИНА
	|			ТОГДА АналитикаВыпуска.Назначение
	|	КОНЕЦ КАК НазначениеПолучателяПродукции,
	|	ВЫБОР
	|		КОГДА &ЕдиницыКоличества = 0
	|			ТОГДА ДД.Количество
	|		КОГДА &ЕдиницыКоличества = 1
	|			ТОГДА ВЫБОР
	|				КОГДА Аналитика.Номенклатура.КоэффициентЕдиницыДляОтчетов <> 0
	|					ТОГДА ДД.Количество / Аналитика.Номенклатура.КоэффициентЕдиницыДляОтчетов
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК КоличествоЗатрат,
	|	ВЫБОР
	|		КОГДА ДокументРасчетаСебестоимости.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 1
	|					ТОГДА ДД.Количество * (Цены.Стоимость + Цены.СтоимостьДопРасходы + Цены.Трудозатраты +
	|						Цены.ПостатейныеПостоянныеСНДС + Цены.ПостатейныеПеременныеСНДС)
	|				КОГДА &ДанныеПоСебестоимости = 2
	|					ТОГДА ДД.Количество * (Цены.СтоимостьБезНДС + Цены.СтоимостьДопРасходыБезНДС + Цены.Трудозатраты +
	|						Цены.ПостатейныеПостоянныеБезНДС + Цены.ПостатейныеПеременныеБезНДС)
	|				КОГДА &ДанныеПоСебестоимости = 3
	|					ТОГДА ДД.Количество * (Цены.СтоимостьУпр + Цены.ДопРасходыУпр + Цены.ТрудозатратыУпр +
	|						Цены.ПостатейныеПостоянныеУпр + Цены.ПостатейныеПеременныеУпр)
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * (Цены.СтоимостьРегл + Цены.ДопРасходыРегл + Цены.ТрудозатратыРегл +
	|						Цены.ПостатейныеПостоянныеРегл + Цены.ПостатейныеПеременныеРегл)
	|				ИНАЧЕ ВЫБОР
	|					КОГДА &ПоПредприятию
	|						ТОГДА ДД.Количество * (Цены.СтоимостьБезНДС + Цены.СтоимостьДопРасходыБезНДС + Цены.Трудозатраты +
	|							Цены.ПостатейныеПостоянныеБезНДС + Цены.ПостатейныеПеременныеБезНДС)
	|					ИНАЧЕ ДД.Количество * (Цены.СтоимостьУпр + Цены.ДопРасходыУпр + Цены.ТрудозатратыУпр +
	|						Цены.ПостатейныеПостоянныеУпр + Цены.ПостатейныеПеременныеУпр)
	|				КОНЕЦ
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 1
	|				ТОГДА ДД.Стоимость + ДД.ДопРасходы + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеСНДС + ДД.ПостатейныеПеременныеСНДС
	|			КОГДА &ДанныеПоСебестоимости = 2
	|				ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеБезНДС +
	|					ДД.ПостатейныеПеременныеБезНДС
	|			КОГДА &ДанныеПоСебестоимости = 3
	|				ТОГДА ДД.СтоимостьУпр + ДД.ДопРасходыУпр + ДД.ТрудозатратыУпр + ДД.ПостатейныеПостоянныеУпр +
	|					ДД.ПостатейныеПеременныеУпр
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьРегл + ДД.ДопРасходыРегл + ДД.ТрудозатратыРегл + ДД.ПостатейныеПостоянныеРегл +
	|					ДД.ПостатейныеПеременныеРегл
	|			ИНАЧЕ ВЫБОР
	|				КОГДА &ПоПредприятию
	|					ТОГДА ДД.СтоимостьБезНДС + ДД.ДопРасходыБезНДС + ДД.Трудозатраты + ДД.ПостатейныеПостоянныеБезНДС +
	|						ДД.ПостатейныеПеременныеБезНДС
	|				ИНАЧЕ ДД.СтоимостьУпр + ДД.ДопРасходыУпр + ДД.ТрудозатратыУпр + ДД.ПостатейныеПостоянныеУпр +
	|					ДД.ПостатейныеПеременныеУпр
	|			КОНЕЦ
	|		КОНЕЦ
	|	КОНЕЦ КАК СтоимостьЗатрат,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.ВременнаяРазница
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.ВременнаяРазница
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК ВременнаяРазница,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.ПостояннаяРазница
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.ПостояннаяРазница
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК ПостояннаяРазница,
	|	ВЫБОР
	|		КОГДА ДокументРасчета.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.СтоимостьНДД
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьНДД
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	КОНЕЦ КАК НДД,
	|	ВЫБОР
	|		КОГДА ДокументРасчетаСебестоимости.Ссылка ЕСТЬ NULL
	|			ТОГДА ВЫБОР
	|				КОГДА &ДанныеПоСебестоимости = 4
	|					ТОГДА ДД.Количество * Цены.СтоимостьЗабалансоваяРегл
	|				ИНАЧЕ ДД.Количество * Цены.СтоимостьЗабалансовая
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА &ДанныеПоСебестоимости = 4
	|				ТОГДА ДД.СтоимостьЗабалансоваяРегл
	|			ИНАЧЕ ДД.СтоимостьЗабалансовая
	|		КОНЕЦ
	|	КОНЕЦ КАК СтоимостьЗатратЗабалансовая,
	|	ВЫБОР
	|		КОГДА ПериодыДействияСреднескользящей.Организация ЕСТЬ NULL
	|			ТОГДА ДД.Регистратор
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Регистратор
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК ДД
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РаботыДляДавальца КАК РаботыДляДавальца
	|		ПО (РаботыДляДавальца.Регистратор = ДД.Регистратор)
	|			И (РаботыДляДавальца.ВидЗапасов = ДД.КорВидЗапасов)
	|			И (РаботыДляДавальца.АналитикаУчетаНоменклатуры = ДД.КорАналитикаУчетаНоменклатуры)
	|			И (РаботыДляДавальца.ИдентификаторСтроки = ДД.ИдентификаторСтроки)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК Аналитика
	|		ПО (Аналитика.Ссылка = ДД.АналитикаУчетаНоменклатуры)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикаВыпуска
	|		ПО (АналитикаВыпуска.Ссылка = РаботыДляДавальца.КорАналитикаУчетаНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтоимостьТоваров КАК Цены
	|		ПО (Цены.АналитикаУчетаНоменклатуры = ДД.АналитикаУчетаНоменклатуры)
	|			И (Цены.Организация = ДД.Организация)
	|			И (Цены.Период = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
	|			И (Цены.ВидЗапасов = ДД.ВидЗапасов)
	|			И (ВЫБОР
	|				КОГДА Цены.РазделУчета = ДД.РазделУчета
	|					ТОГДА ИСТИНА
	|				КОГДА ДД.Количество < 0
	|						И (ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|								И Цены.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПустаяСсылка)
	|							ИЛИ Цены.РазделУчета В (ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.НезавершенноеПроизводство), 
	|													ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПринятыеНаОтветхранение)))
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РасчетСебестоимостиТоваров.Организации КАК ДокументРасчетаСебестоимости
	|		ПО (НАЧАЛОПЕРИОДА(ДокументРасчетаСебестоимости.Ссылка.Дата, МЕСЯЦ) = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
	|			И (ДокументРасчетаСебестоимости.Организация = ДД.Организация)
	|			И (ДокументРасчетаСебестоимости.Ссылка.Проведен)
	|			И (ДокументРасчетаСебестоимости.Ссылка.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.ПустаяСсылка))
	|			И (НЕ ДокументРасчетаСебестоимости.Ссылка.ПредварительныйРасчет)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыДействияСреднескользящей КАК ПериодыДействияСреднескользящей
	|		ПО (ПериодыДействияСреднескользящей.Организация = ДД.Организация)
	|			И (ПериодыДействияСреднескользящей.ПериодДействия = НАЧАЛОПЕРИОДА(ДД.Период, МЕСЯЦ))
			//++ НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПартий КАК АналитикиПартий
	|		ПО (АналитикиПартий.Ссылка = ДД.КорАналитикаУчетаПартий)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Продукция
	|		ПО ДД.Регистратор = Продукция.Ссылка
	|			И (АналитикиПартий.КодСтроки = Продукция.КодСтроки)
	|			И (НЕ Продукция.Отменено)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ЭтапПобочныеИзделия
	|		ПО ДД.Регистратор = ЭтапПобочныеИзделия.Ссылка
	|			И (АналитикиПартий.КодСтроки = ЭтапПобочныеИзделия.КодСтроки)
	|			И (НЕ ЭтапПобочныеИзделия.Отменено)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|		ПО ДД.Регистратор = ЭтапПроизводства2_2.Ссылка
			//-- НЕ УТКА
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК Реестр
	|		ПО (Реестр.СторнируемыйДокумент = ДД.Регистратор)
	|			И (Реестр.СторноИсправление)
	|			И (Реестр.ДатаДокументаИБ <= &КонецПериода
	|				ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|			И (Реестр.Проведен)
	|ГДЕ
	|	(ДД.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|			ИЛИ &КонецПериода = ДАТАВРЕМЯ(1, 1, 1))
	|	И ДД.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ДД.Активность
	|	И (ДД.Регистратор ССЫЛКА Документ.ОтчетПереработчика2_5
	|			И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетПереработчика
	|			И ДД.РазделУчета = ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ПроизводственныеЗатраты)
		//++ Устарело_Производство21
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ВыпускПродукции
		//-- Устарело_Производство21

		//++ НЕ УТКА
	|		ИЛИ ДД.Регистратор ССЫЛКА Документ.ОтчетДавальцу
		//-- НЕ УТКА
	|		ИЛИ ТИПЗНАЧЕНИЯ(ДД.Регистратор) В (
		//++ НЕ УТКА
	|											ТИП(Документ.ЭтапПроизводства2_2),
		//-- НЕ УТКА
	|											ТИП(Документ.ПроизводствоБезЗаказа),
	|											ТИП(Документ.РегистраторРасчетаСебестоимости))
	|			И ДД.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукции),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|										ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|		)
	// Исключаем документы, по которым введено исправление или сторно в текущем периоде.
	|	И Реестр.Ссылка ЕСТЬ NULL
	|	И НЕ ДД.Сторно
	|{ГДЕ
	|	((АналитикаВыпуска.Номенклатура, АналитикаВыпуска.Характеристика) В
	|			(ВЫБРАТЬ
	|				ОтборПоСегментуНоменклатуры.Номенклатура,
	|				ОтборПоСегментуНоменклатуры.Характеристика
	|			ИЗ
	|				ОтборПоСегментуНоменклатуры
	|			ГДЕ
	|				ОтборПоСегментуНоменклатуры.ИспользуетсяОтборПоСегментуНоменклатуры = &ИспользуетсяОтборПоСегментуНоменклатуры))}
	//-- Устарело_Переработка24
	|";
	
КонецФункции

// Рекурсивно удаляет отбор из настроек и пользовательских настроек отчета.
//
// Параметры:
//  НастройкиКомпоновкиДанных - КомпоновщикНастроекКомпоновкиДанных, НастройкиКомпоновкиДанных, ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных - Настройки.
//  ИмяЭлемента  - Строка - имя элемента, который будет удален.
Процедура УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(НастройкиКомпоновкиДанных, ИмяЭлемента) Экспорт
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных.Настройки;
		ПользовательскиеНастройки = НастройкиКомпоновкиДанных.ПользовательскиеНастройки;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных")
		ИЛИ ТипЗнч(НастройкиКомпоновкиДанных) = Тип("ГруппировкаКомпоновкиДанных")
		ИЛИ ТипЗнч(НастройкиКомпоновкиДанных) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Настройки = НастройкиКомпоновкиДанных;
		ПользовательскиеНастройки = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КоллекцияЭлементовСтруктурыНастроекКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиКомпоновкиДанных) = Тип("КоллекцияЭлементовСтруктурыТаблицыКомпоновкиДанных") Тогда
		Для Каждого СтрокаЭлементовСтруктуры Из НастройкиКомпоновкиДанных Цикл
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(СтрокаЭлементовСтруктуры, ИмяЭлемента);
		КонецЦикла;
		// В самой коллекции элементов нет настроек отбора.
		Возврат;
	ИначеЕсли ТипЗнч(НастройкиКомпоновкиДанных) = Тип("ТаблицаКомпоновкиДанных") Тогда
		Для Каждого СтрокаЭлементовСтруктуры Из НастройкиКомпоновкиДанных.Строки Цикл
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(СтрокаЭлементовСтруктуры, ИмяЭлемента);
		КонецЦикла;
		Для Каждого СтрокаЭлементовСтруктуры Из НастройкиКомпоновкиДанных.Колонки Цикл
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(СтрокаЭлементовСтруктуры, ИмяЭлемента);
		КонецЦикла;
		// В самой таблице нет настроек отбора.
		Возврат;
	Иначе
		Возврат;
	КонецЕсли;
	
	ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Настройки.Отбор, ИмяЭлемента); // Массив Из ЭлементОтбораКомпоновкиДанных
	
	Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
		ИдентификаторПользовательскойНастройки = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		Если ЭлементОтбора.Родитель <> Неопределено Тогда
			ЭлементОтбора.Родитель.Элементы.Удалить(ЭлементОтбора);
		Иначе
			Настройки.Отбор.Элементы.Удалить(ЭлементОтбора);
		КонецЕсли;
		
		ЭлементПользовательскихНастроек = ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если ЭлементПользовательскихНастроек <> Неопределено Тогда
			ПользовательскиеНастройки.Элементы.Удалить(ИдентификаторПользовательскойНастройки);
		Иначе
			Для Каждого ЭлементПользНастроек Из ПользовательскиеНастройки.Элементы Цикл
				Если ТипЗнч(ЭлементПользНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
					ЭлементыПользовательскихОтборов = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(ЭлементПользНастроек, ИмяЭлемента);
					Для Каждого ЭлементПользовательскогоОтбора Из ЭлементыПользовательскихОтборов Цикл
						ЭлементПользНастроек.Элементы.Удалить(ЭлементПользовательскогоОтбора);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиКомпоновкиДанных) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(НастройкиКомпоновкиДанных) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		Если НастройкиКомпоновкиДанных.Структура.Количество() <> 0 Тогда
			УдалитьРекурсивноЭлементОтбораИзВсехНастроекОтчета(НастройкиКомпоновкиДанных.Структура, ИмяЭлемента);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли