#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.НачисленныеСуммыАкцизов);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "НачисленныеСуммыАкцизов");
	ОписаниеВарианта.Описание = НСтр("ru = 'Информация о суммах акцизов, начисленных в выбранном периоде.';
									|en = 'Information on excise amounts accrued in the selected period.'");
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

//++ Локализация

// Формирует текст, выводимый в заголовке отчета.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//
// Возвращаемое значение:
//   Строка      - текст заголовка с учетом периода.
//
Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Начисленные суммы акцизов %1';
			|en = 'Accrued excise amounts %1'"),
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(
			ПараметрыОтчета.НачалоПериода,
			ПараметрыОтчета.КонецПериода,
			Истина));
			
КонецФункции
//++ НЕ УТ

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - см. БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета.
//
// Пустые параметры компоновки отчета.
// 
// Возвращаемое значение:
//  Структура - Пустые параметры компоновки отчета:
//   * ИдентификаторОтчета - Строка.
//   * РазмещениеДополнительныхПолей - Число.
//   * Группировка - см. Отчет.НачисленныеСуммыАкцизов.Группировка.
//   * ДополнительныеПоля - см. Отчет.НачисленныеСуммыАкцизов.ДополнительныеПоля.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	// Общая структура настроек.
	ПараметрыОтчета = БухгалтерскиеОтчеты.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.Вставить("ИдентификаторОтчета", "НачисленныеСуммыАкцизов");
	
	БухгалтерскиеОтчеты.ДобавитьПоказателиВПараметры(ПараметрыОтчета, ПолучитьНаборПоказателей());
	// Дополним параметрами, влияющими на формирование отчета.
	ОтчетОбъект = Отчеты.НачисленныеСуммыАкцизов.Создать();
	// 0 = В одной колонке, 1 = В отдельных колонках
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей", 0);
	ПараметрыОтчета.Вставить("Группировка", ОтчетОбъект.Группировка.ВыгрузитьКолонки());
	ПараметрыОтчета.Вставить("ДополнительныеПоля", ОтчетОбъект.ДополнительныеПоля.ВыгрузитьКолонки());
	
	Возврат ПараметрыОтчета;
	
КонецФункции

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//  Структура - флаги, задающие необходимость дополнительных действий:
//   * ИспользоватьПередКомпоновкойМакета - Булево.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	ПараметрыИсполнения = Новый Структура;
	ПараметрыИсполнения.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	ПараметрыИсполнения.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	
	Возврат ПараметрыИсполнения;
	
КонецФункции

// В процедуре можно доработать компоновщик перед выводом в отчет. Изменения сохранены не будут.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  Схема        - СхемаКомпоновкиДанных - описание получаемых данных.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//
Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	КомпоновщикНастроек.Настройки.Выбор.Элементы.Очистить();
	
	// Параметры
	Если ЗначениеЗаполнено(ПараметрыОтчета.НачалоПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	КонецЕсли;
	
	ПараметрПериод = Новый СтандартныйПериод;
	ПараметрПериод.ДатаНачала = ПараметрыОтчета.НачалоПериода;
	Если ЗначениеЗаполнено(ПараметрыОтчета.КонецПериода) Тогда
		ПараметрПериод.ДатаОкончания = КонецДня(ПараметрыОтчета.КонецПериода);
	КонецЕсли;
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(КомпоновщикНастроек, "Период", ПараметрПериод);
	
	// Группировка
	БухгалтерскиеОтчетыВызовСервера.ДобавитьГруппировки(ПараметрыОтчета, КомпоновщикНастроек);
	
	// Дополнительные данные
	БухгалтерскиеОтчетыВызовСервера.ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек);
	
	КоличествоПоказателей = БухгалтерскиеОтчетыВызовСервера.КоличествоПоказателей(ПараметрыОтчета);
	
	Если КоличествоПоказателей > 1 Тогда
		
		Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
			Если ПараметрыОтчета["Показатель" + ИмяПоказателя] Тогда
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек, ИмяПоказателя);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек);
	
КонецПроцедуры

// Задает набор показателей, которые позволяют настраивать отчет.
//
// Возвращаемое значение:
//   Массив из Строка - имена показателей.
//
Функция ПолучитьНаборПоказателей() Экспорт
	
	НаборПоказателей = Новый Массив;
	НаборПоказателей.Добавить("КоличествоАкциза");
	НаборПоказателей.Добавить("Количество");
	НаборПоказателей.Добавить("СуммаАкцизаРегл");
	НаборПоказателей.Добавить("СуммаАкцизаУпр");
	
	Возврат НаборПоказателей;
	
КонецФункции

// Выводит макет заголовка расшифровки.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПодготовитьПараметрыОтчета() в ФормаОтчета.
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - связь настроек компоновки данных и схемы компоновки.
//  Результат - ТабличныйДокумент - табличный документ для вывода данных отчета.
//
Процедура ПриВыводеЗаголовка(ПараметрыОтчета, КомпоновщикНастроек, Результат) Экспорт 
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		СписокОрганизаций = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПараметрыОтчета.Организация);
	Иначе
		СписокОрганизаций = Новый Массив;
	КонецЕсли;
	
	ЗначенияОтбораДанных = Новый Структура("СписокОрганизаций, Период");
	ЗначенияОтбораДанных.СписокОрганизаций = СписокОрганизаций;
	ЗначенияОтбораДанных.Период = ПараметрыОтчета.КонецПериода;
	
	Если Не ДанныеОтчетаАктуальны(ЗначенияОтбораДанных) Тогда
		
		ТекстСообщения = НСтр("ru = 'Данные в отчете могут быть неактуальны:
		|Имеются невыполненные задания неоперативного допроведения документов по акцизам.';
		|en = 'Data in the report might be outdated:
		|There are incomplete jobs of backdate additional posting of excise documents.'");
		
		Результат.Область("R1C1").Текст = ТекстСообщения;
		Результат.Область("R1C1").ЦветТекста = Метаданные.ЭлементыСтиля.ЦветТекстаПроблема.Значение;
		Результат.Область(1, 1, 1, ).ВысотаСтроки = 0;
		
	КонецЕсли;
	
	БухгалтерскиеОтчетыВызовСервера.ВывестиЗаголовокОтчета(ПараметрыОтчета, КомпоновщикНастроек, Результат);
	
КонецПроцедуры
//-- НЕ УТ

//-- Локализация
#КонецОбласти
//++ Локализация

//++ НЕ УТ
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбраннаяФорма = "ФормаОтчета";
	
КонецПроцедуры

#КонецОбласти
//-- НЕ УТ

// Не требуется в УТ.

//-- Локализация

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет информационное сообщение в шапку отчета.
//
// Параметры:
//	ДокументРезультат - ТабличныйДокумент - Результат отчета.
//	ЗначенияОтбораДанных - Структура - см. ПолучитьЗначенияОтбораДанных().
//
Процедура ДобавитьИнформационноеСообщениеВШапку(ДокументРезультат, ЗначенияОтбораДанных) Экспорт
	
	Если ДанныеОтчетаАктуальны(ЗначенияОтбораДанных) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Данные в отчете могут быть неактуальны:
	|Имеются невыполненные задания неоперативного допроведения документов по акцизам.';
	|en = 'Data in the report might be outdated:
	|There are incomplete jobs of backdate additional posting of excise documents.'");
	
	ДокументРезультат.Область("R1C1").Текст = ТекстСообщения;
	ДокументРезультат.Область("R1C1").ЦветТекста = Метаданные.ЭлементыСтиля.ЦветТекстаПроблема.Значение;
	ДокументРезультат.Область(1, 1, 1, ).ВысотаСтроки = 0;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеОтчетаАктуальны(ЗначенияОтбораДанных)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПРЕДСТАВЛЕНИЕ(ЗаданияКРасчету.Организация) КАК ОрганизацияПредставление
	|ИЗ
	|	РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК ЗаданияКРасчету
	|ГДЕ
	|	ЗаданияКРасчету.ВидЗадания = ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоАкцизам)
	|	И ВЫБОР КОГДА &БезОтбораПоОрганизации ТОГДА ИСТИНА ИНАЧЕ ЗаданияКРасчету.Организация В (&СписокОрганизаций) КОНЕЦ
	|	И ВЫБОР КОГДА &Период = ДАТАВРЕМЯ(1, 1, 1) ТОГДА ИСТИНА ИНАЧЕ ЗаданияКРасчету.ПериодЗадания <= &Период КОНЕЦ
	|СГРУППИРОВАТЬ ПО
	|	ПРЕДСТАВЛЕНИЕ(ЗаданияКРасчету.Организация)";
	
	Запрос.УстановитьПараметр("БезОтбораПоОрганизации", ЗначенияОтбораДанных.СписокОрганизаций.Количество() = 0);
	Запрос.УстановитьПараметр("СписокОрганизаций", ЗначенияОтбораДанных.СписокОрганизаций);
	Запрос.УстановитьПараметр("Период", ЗначенияОтбораДанных.Период);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли