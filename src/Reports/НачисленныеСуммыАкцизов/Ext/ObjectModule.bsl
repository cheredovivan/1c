#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	// Отключаем стандартную обработку, т.к. формировать отчет будем "вручную".
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	ЗначенияОтбораДанных = ПолучитьЗначенияОтбораДанных(НастройкиОтчета);
	Отчеты.НачисленныеСуммыАкцизов.ДобавитьИнформационноеСообщениеВШапку(ДокументРезультат, ЗначенияОтбораДанных);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры
//++ Локализация

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	//++ НЕ УТ
	Проверки = Новый Структура();
	БухгалтерскиеОтчетыВызовСервера.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, Проверки);
	//-- НЕ УТ
	Возврат; // В УТ пустой обработчик.
КонецПроцедуры
//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьЗначенияОтбораДанных(НастройкиОсновнойСхемы)

	ЭлементыОтбора = НастройкиОсновнойСхемы.Отбор.Элементы;
	
	ЗначенияОтбораДанных = Новый Структура("СписокОрганизаций, Период");
	
	ЗначенияОтбораДанных.СписокОрганизаций = ОтчетыУТСервер.ЗначениеОтбора("Организация", ЭлементыОтбора);
	Период = ОтчетыУТСервер.ЗначениеПараметра(НастройкиОсновнойСхемы, "КонецПериода");
	ЗначенияОтбораДанных.Период = ?(ЗначениеЗаполнено(Период), Период, '00010101');
	
	Возврат ЗначенияОтбораДанных;
	
КонецФункции

//++ Локализация

//++ НЕ УТ

// Вызывается из обработчика события "ПередЗагрузкойНастроекВКомпоновщик" при инициализации варианта отчета средствами БСП.
//
// Параметры:
//  Контекст - Структура - Контекст инициализации варианта очтета.
//  КлючСхемы - Строка - Ключ схемы компоновки данных.
//  КлючВарианта - Строка - Ключ варианта отчета.
//  НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки, с которыми будет выполнена инициализация варианта отчета.
//  НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - Настройки, с которыми будет выполнена инициализация варианта отчета.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД,
	НовыеПользовательскиеНастройкиКД) Экспорт
	
	БухгалтерскиеОтчетыВызовСервера.ПередЗагрузкойНастроекВКомпоновщик(
		ЭтотОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД);
	
КонецПроцедуры

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - См. ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
	СхемаКомпоновкиДанных = ПолучитьМакет("СхемаКомпоновкиДанных");
	
КонецПроцедуры
//-- НЕ УТ

// Не требуется в УТ.

//-- Локализация
#КонецОбласти

#КонецЕсли