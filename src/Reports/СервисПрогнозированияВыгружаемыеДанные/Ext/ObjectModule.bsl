#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	ИнфоОКоллекции = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
	ВыгружатьПартнера = ИнфоОКоллекции.ВложенноеОписание["КодКлиента"].Выгружать;
	ВыгружатьХарактеристику = ИнфоОКоллекции.ВложенноеОписание["КодХарактеристики"].Выгружать;
	
	ПолеХарактеристика = Новый ПолеКомпоновкиДанных("Характеристика");
	ПолеКодХарактеристики = Новый ПолеКомпоновкиДанных("КодХарактеристики");
	ПолеПартнер = Новый ПолеКомпоновкиДанных("Партнер");
	ПолеКодПартнера = Новый ПолеКомпоновкиДанных("КодПартнера");
	
	Если Не ВыгружатьПартнера
		Или Не ВыгружатьХарактеристику Тогда
		Для Каждого КолонкаОтчета Из НастройкиОтчета.Выбор.Элементы Цикл
			Если Не ВыгружатьХарактеристику
				И (КолонкаОтчета.Поле = ПолеХарактеристика Или КолонкаОтчета.Поле = ПолеКодХарактеристики) Тогда
					КолонкаОтчета.Использование = Ложь;
			КонецЕсли;
			Если Не ВыгружатьПартнера
				И (КолонкаОтчета.Поле = ПолеПартнер Или КолонкаОтчета.Поле = ПолеКодПартнера) Тогда
					КолонкаОтчета.Использование = Ложь;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	// Вывод легенды.
	ПредставлениеОтборов = СервисПрогнозирования.ПолучитьОтборыВыгрузкиСтрокой();
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Отчет.СервисПрогнозированияВыгружаемыеДанные.Легенда");
	ОбластьЗаголовокОтборы = Макет.ПолучитьОбласть("ЗаголовокОтборы");
	ОбластьЛегенда = Макет.ПолучитьОбласть("Легенда");
	ОбластьДетальнаяИнформация = Макет.ПолучитьОбласть("ДетальнаяИнформация");
	ОбластьДетальнаяИнформация.Параметры.ДетальнаяИнформацияПоОтборам = ПредставлениеОтборов;
	ДокументРезультат.НачатьАвтогруппировкуСтрок();
	ДокументРезультат.Вывести(ОбластьЗаголовокОтборы, 1,, Ложь);
	ДокументРезультат.Вывести(ОбластьЛегенда, 2,, Ложь);
	ДокументРезультат.Вывести(ОбластьДетальнаяИнформация, 2,, Ложь);
	ДокументРезультат.ЗакончитьАвтогруппировкуСтрок();
	
	// Получение таблицы для подстановки в результат вывода отчета.
	ВыборкаПоВыгружаемымТоварам = СервисПрогнозированияПереопределяемый.ПолучитьИнформациюОВыгружаемыхПродажах();
	ВнешниеНаборы = Новый Структура();
	ВнешниеНаборы.Вставить("ВтВыгружаемыеДанные", ВыборкаПоВыгружаемымТоварам);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
