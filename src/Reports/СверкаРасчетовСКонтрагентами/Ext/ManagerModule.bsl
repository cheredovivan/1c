
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует набор параметров, задающих флаги выполнения дополнительных действий над сущностями, обрабатываемыми
// в процессе формирования отчета.
//
// Возвращаемое значение:
//   Структура   - флаги, задающие необходимость дополнительных действий.
//
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	ПараметрыВыполненияОтчета = Новый Структура;
	ПараметрыВыполненияОтчета.Вставить("ИспользоватьПередКомпоновкойМакета",          Истина);
	ПараметрыВыполненияОтчета.Вставить("ИспользоватьПослеКомпоновкиМакета" ,          Ложь);
	ПараметрыВыполненияОтчета.Вставить("ИспользоватьПослеВыводаРезультата",           Истина);
	ПараметрыВыполненияОтчета.Вставить("ИспользоватьДанныеРасшифровки",               Истина);
	ПараметрыВыполненияОтчета.Вставить("ИспользоватьРасширенныеПараметрыРасшифровки", Ложь);
	
	Возврат ПараметрыВыполненияОтчета;
	
КонецФункции

// Возвращает структуру параметров, наличие которых требуется для успешного формирования отчета.
//
// Возвращаемое значение:
//   Структура - структура параметров для формирования отчета.
//
Функция ПустыеПараметрыКомпоновкиОтчета() Экспорт
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация",                       Справочники.Организации.ПустаяСсылка());
	ПараметрыОтчета.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	ПараметрыОтчета.Вставить("Контрагент",                        Справочники.Контрагенты.ПустаяСсылка());
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок" ,                Ложь);
	ПараметрыОтчета.Вставить("ВыводитьПодвал",                    Ложь);
	ПараметрыОтчета.Вставить("МакетОформления",                   Неопределено);
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных",             Неопределено);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета",               "");
	ПараметрыОтчета.Вставить("НастройкиКомпоновкиДанных",         Неопределено);
	ПараметрыОтчета.Вставить("ДанныеРасшифровки",                 Неопределено);
	ПараметрыОтчета.Вставить("ДополнительныеПоля",                Неопределено);
	ПараметрыОтчета.Вставить("РазмещениеДополнительныхПолей",     0);
	ПараметрыОтчета.Вставить("БыстрыйОтбор",                      "");
	ПараметрыОтчета.Вставить("РежимРасшифровки",                  Ложь);
	ПараметрыОтчета.Вставить("НачалоПериода",                     Дата(1,1,1));
	ПараметрыОтчета.Вставить("КонецПериода",                      Дата(1,1,1));
	ПараметрыОтчета.Вставить("ВыводитьОрганизацию",               Истина);
	ПараметрыОтчета.Вставить("ИдентификаторыРасхождений",         Неопределено);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Процедура ПередКомпоновкойМакета(ПараметрыОтчета, Схема, КомпоновщикНастроек) Экспорт
	
	// Устанавливаем параметры если источник данных в СКД явлется запросом
	УстановитьПараметр(КомпоновщикНастроек, "НачалоПериода", НачалоДня(ПараметрыОтчета.НачалоПериода));
	УстановитьПараметр(КомпоновщикНастроек, "КонецПериода", КонецДня(ПараметрыОтчета.КонецПериода));
	УстановитьПараметр(КомпоновщикНастроек, "Организация", ПараметрыОтчета.Организация);
	УстановитьПараметр(КомпоновщикНастроек, "Контрагент", ПараметрыОтчета.Контрагент);
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		
		Если ПараметрыОтчета.ВключатьОбособленныеПодразделения Тогда
			Отбор = КомпоновщикНастроек.Настройки.Отбор;
			ГруппаОтборОрганизация = Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаОтборОрганизация.Представление = "###ОтборПоОрганизацииСОП###";
			ГруппаОтборОрганизация.ТипГруппы     = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			ДобавитьОтбор(ГруппаОтборОрганизация, "Организация"                    , ПараметрыОтчета.Организация);
			ДобавитьОтбор(ГруппаОтборОрганизация, "Организация.ГоловнаяОрганизация", ПараметрыОтчета.Организация);
		Иначе
			НовыйОтбор = ДобавитьОтбор(КомпоновщикНастроек, "Организация", ПараметрыОтчета.Организация,,, Истина);
			НовыйОтбор.Представление = "###ОтборПоОрганизации###"; 
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Контрагент) Тогда
		
		НовыйОтбор = ДобавитьОтбор(КомпоновщикНастроек, "Контрагент", ПараметрыОтчета.Контрагент,,, Истина);
		НовыйОтбор.Представление = "###ОтборПоКонтрагенту###"; 
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.ИдентификаторыРасхождений) Тогда
		НовыйОтбор = ДобавитьОтбор(КомпоновщикНастроек, "ИдентификаторРасхождения", ПараметрыОтчета.ИдентификаторыРасхождений,ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		НовыйОтбор.Представление = "###ОтборПоИдентификаторРасхождения###";
	КонецЕсли;
	
КонецПроцедуры

Функция ДобавитьОтбор(ЭлементСтруктуры, Знач Поле, Значение = Неопределено, ВидСравнения = Неопределено, Использование = Истина, ВПользовательскиеНастройки = Ложь) Экспорт
	
	Если ТипЗнч(Поле) = Тип("Строка") Тогда
		Поле = Новый ПолеКомпоновкиДанных(Поле);
	КонецЕсли;
	
	Если ТипЗнч(ЭлементСтруктуры) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Настройки.Отбор;
		
		Если ВПользовательскиеНастройки Тогда
			Для Каждого ЭлементНастройки Из ЭлементСтруктуры.ПользовательскиеНастройки.Элементы Цикл	
				Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = ЭлементСтруктуры.Настройки.Отбор.ИдентификаторПользовательскойНастройки Тогда
					Отбор = ЭлементНастройки;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("НастройкиКомпоновкиДанных") Тогда
		Отбор = ЭлементСтруктуры.Отбор;
	Иначе
		Отбор = ЭлементСтруктуры;
	КонецЕсли;
		
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	ЗначениеОтбораКомпоновки = Значение;
	// Механизмы компоновки в качестве списка принимают СписокЗначений,
	// но не более очевидную коллекцию - Массив.
	ТипЗначенияОтбора = ТипЗнч(Значение);
	Если ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке
		Или ВидСравнения = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии
		Или ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке
		Или ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСпискеПоИерархии Тогда
		
		Если ТипЗначенияОтбора = Тип("Массив") Тогда
			ЗначениеОтбораКомпоновки = Новый СписокЗначений;
			ЗначениеОтбораКомпоновки.ЗагрузитьЗначения(Значение);
		ИначеЕсли ТипЗначенияОтбора = Тип("ФиксированныйМассив") Тогда
			ЗначениеОтбораКомпоновки = Новый СписокЗначений;
			ЗначениеОтбораКомпоновки.ЗагрузитьЗначения(Новый Массив(Значение));
		КонецЕсли;
		
	КонецЕсли;
	
	НовыйЭлемент = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйЭлемент.Использование  = Использование;
	НовыйЭлемент.ЛевоеЗначение  = Поле;
	НовыйЭлемент.ВидСравнения   = ВидСравнения;
	НовыйЭлемент.ПравоеЗначение = ЗначениеОтбораКомпоновки;
	
	Возврат НовыйЭлемент;
	
КонецФункции

Функция УстановитьПараметр(Настройки, Параметр, Значение, Использование = Истина) Экспорт
	
	ЗначениеПараметра = ПолучитьПараметр(Настройки, Параметр);
	
	Если ЗначениеПараметра <> Неопределено Тогда
		ЗначениеПараметра.Использование = Использование;
		ЗначениеПараметра.Значение      = Значение;
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

Функция ПолучитьПараметр(Настройки, Параметр) Экспорт
	
	ЗначениеПараметра = Неопределено;
	ПолеПараметр = ?(ТипЗнч(Параметр) = Тип("Строка"), Новый ПараметрКомпоновкиДанных(Параметр), Параметр);
	
	Если ТипЗнч(Настройки) = Тип("НастройкиКомпоновкиДанных") Тогда
		ЗначениеПараметра = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
	ИначеЕсли ТипЗнч(Настройки) = Тип("ПользовательскиеНастройкиКомпоновкиДанных") Тогда
		Для Каждого ЭлементНастройки Из Настройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ТипЗнч(Настройки) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		Для Каждого ЭлементНастройки Из Настройки.ПользовательскиеНастройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") И ЭлементНастройки.Параметр = ПолеПараметр Тогда
				ЗначениеПараметра = ЭлементНастройки;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеПараметра = Неопределено Тогда
			ЗначениеПараметра = Настройки.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(ПолеПараметр);
		КонецЕсли;
	ИначеЕсли ТипЗнч(Настройки) = Тип("КоллекцияЗначенийПараметровКомпоновкиДанных") Тогда
		ЗначениеПараметра = Настройки.Найти(ПолеПараметр);
	ИначеЕсли ТипЗнч(Настройки) = Тип("ОформлениеКомпоновкиДанных") Тогда
		ЗначениеПараметра = Настройки.НайтиЗначениеПараметра(ПолеПараметр);
	КонецЕсли;
	
	Возврат ЗначениеПараметра;
	
КонецФункции

// В процедуре можно изменить табличный документ после вывода в него данных.
//
// Параметры:
//  ПараметрыОтчета - Структура - см. ПустыеПараметрыКомпоновкиОтчета()
//  Результат    - ТабличныйДокумент - сформированный отчет.
//
Процедура ПослеВыводаРезультата(ПараметрыОтчета, Результат) Экспорт
	
	ИтогиСверкиПоТаблице = ИтогиСверкиПоТаблице(Результат);
	
	Если ИтогиСверкиПоТаблице.КоличествоДокументов <> 0
		И ИтогиСверкиПоТаблице.КоличествоДокументов = ИтогиСверкиПоТаблице.БезРасхождений Тогда
		
		// Отобразим информацию об успешной сверке
		Результат.Очистить();
		
		МакетУспешнаяСверка = Отчеты.СверкаРасчетовСКонтрагентами.ПолучитьМакет("УспешнаяСверка");
		
		Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда 
			ОбластьЗаголовок = МакетУспешнаяСверка.ПолучитьОбласть("Заголовок");
			СведенияОрганизации = СервисСверкиРасчетовПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Организация, ПараметрыОтчета.КонецПериода);
			ОбластьЗаголовок.Параметры.Организация = СведенияОрганизации.НаименованиеДляПечатныхФорм;
		Иначе
			ОбластьЗаголовок = МакетУспешнаяСверка.ПолучитьОбласть("ЗаголовокБезОтбораПоОрганизации");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтчета.Контрагент) Тогда
			СведенияКонтрагента = СервисСверкиРасчетовПереопределяемый.СведенияОЮрФизЛице(ПараметрыОтчета.Контрагент, ПараметрыОтчета.КонецПериода);
			ОбластьЗаголовок.Параметры.Контрагент = СведенияКонтрагента.НаименованиеДляПечатныхФорм;
		Иначе
			ОбластьЗаголовок.Параметры.Контрагент = НСтр("ru = 'контрагентами';
														|en = 'counterparties'");
		КонецЕсли;
		
		ОбластьЗаголовок.Параметры.Период = ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
		
		ОбластьЗаголовок.Параметры.ОписаниеОтбора = Строка(ПараметрыОтчета.НастройкиКомпоновкиданных.Отбор);
		
		Результат.Вывести(ОбластьЗаголовок);
		
		ОбластьОписаниеСверки = МакетУспешнаяСверка.ПолучитьОбласть("ОписаниеСверки");
		
		ПоследняяЦифра1   = Прав(Строка(ИтогиСверкиПоТаблице.КоличествоДокументов),1) = "1" 
			И ?(ИтогиСверкиПоТаблице.КоличествоДокументов > 10, 
			Прав(Строка(ИтогиСверкиПоТаблице.КоличествоДокументов),2) <> "11", Истина);
		ТекстСверено      = ?(ПоследняяЦифра1, НСтр("ru = 'Сверен ';
													|en = 'Reconciled '"),  НСтр("ru = 'Сверено ';
																			|en = 'Reconciled '")); 
		ТекстДокументов   = ПолучитьСклоненияСтрокиПоЧислу("документ" , ИтогиСверкиПоТаблице.КоличествоДокументов ,,,"ПД=Винительный");
		СвереноДокументов = ТекстСверено + ТекстДокументов[0];
		
		ОбластьОписаниеСверки.Параметры.СвереноДокументов = СвереноДокументов;
		Результат.Вывести(ОбластьОписаниеСверки);
		
		ОбластьПодробнееОСервисе = МакетУспешнаяСверка.ПолучитьОбласть("ПодробнееОСервисе");
		КонтрагентыБизнесСеть    = РегистрыСведений.КонтрагентыБизнесСеть.КонтрагентыEDIПоОрганизации();
		ОбластьПодробнееОСервисе.Параметры.КоличествоПодключенных = КонтрагентыБизнесСеть.Количество(); 
		ОбластьПодробнееОСервисе.Параметры.КонтрагентыБизнесСети  = "e1cib/list/РегистрСведений.КонтрагентыБизнесСеть";
		ОбластьПодробнееОСервисе.Параметры.СсылкаНаИТС            = "https://its.1c.ru/db/partnerits#content:4430:hdoc";
		
		Результат.Вывести(ОбластьПодробнееОСервисе);
		
	КонецЕсли;
	
	СервисСверкиРасчетовПереопределяемый.ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
	
КонецПроцедуры

Функция ПолучитьТекстЗаголовка(ПараметрыОтчета) Экспорт
	
	ТекстЗаголовок = ПараметрыОтчета.СхемаКомпоновкиДанных.ВариантыНастроек[ПараметрыОтчета.ИдентификаторМакета].Представление;
	
	Возврат ТекстЗаголовок + ПолучитьПредставлениеПериода(ПараметрыОтчета.НачалоПериода, ПараметрыОтчета.КонецПериода);
	
КонецФункции

// Возвращает структуру итогов сверки по таблице отчета. 
//
// Параметры:
//  Результат    - ТабличныйДокумент - сформированный отчет.
//
// Возвращаемое значение:
//   ИтогиСверки - структура см. тело функции.
//
Функция ИтогиСверкиПоТаблице(Результат) Экспорт
	
	ИтогиСверки = Новый Структура();
	ИтогиСверки.Вставить("НетИтогов",              Ложь);
	
	ИтогиСверки.Вставить("КоличествоДокументов",   0);
	ИтогиСверки.Вставить("НеОтраженоОрганизацией", 0);
	ИтогиСверки.Вставить("НеОтраженоКонтрагентом", 0);
	ИтогиСверки.Вставить("РазличаютсяРеквизиты",   0);
	ИтогиСверки.Вставить("БезРасхождений",         0);

	ОбластьСверено = Результат.НайтиТекст("<Сверено>");
	Если ОбластьСверено = Неопределено Тогда
		ИтогиСверки.НетИтогов = Истина;
		Возврат ИтогиСверки;
	КонецЕсли;
	
	Попытка
		ИтогиСверки.БезРасхождений = Число(Формат(Результат.Область(ОбластьСверено.Низ, 2).Текст, "ЧГ=0"));
	Исключение
		// Исключение не отрабатываем.
	КонецПопытки;
	
	Попытка
		ИтогиСверки.НеОтраженоОрганизацией = Число(Формат(Результат.Область(ОбластьСверено.Низ+1, 3).Текст, "ЧГ=0"));
		ИтогиСверки.НеОтраженоКонтрагентом = Число(Формат(Результат.Область(ОбластьСверено.Низ+1, 4).Текст, "ЧГ=0"));
		ИтогиСверки.РазличаютсяРеквизиты   = Число(Формат(Результат.Область(ОбластьСверено.Низ+1, 5).Текст, "ЧГ=0"));
	Исключение
		// Исключение не отрабатываем.
	КонецПопытки;
	
	ИтогиСверки.КоличествоДокументов = ИтогиСверки.БезРасхождений
		+ ИтогиСверки.НеОтраженоОрганизацией
		+ ИтогиСверки.НеОтраженоКонтрагентом
		+ ИтогиСверки.РазличаютсяРеквизиты;
	
	Возврат ИтогиСверки;
	
КонецФункции

Функция ПолучитьПредставлениеПериода(НачалоПериода = '00010101', КонецПериода = '00010101')
	
	ТекстПериод = "";
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда 
		Если КонецПериода >= НачалоПериода Тогда
			ТекстПериод = " " + НСтр("ru = 'за';
									|en = 'for'")+ " ";
			ТекстПериод = ТекстПериод
				+ ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");	
		Иначе
			ТекстПериод = "";
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(НачалоПериода) Тогда
		ТекстПериод = " " + НСтр("ru = 'за';
								|en = 'for'")+ " "
			+ ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(Дата(3999, 11, 11)), "ФП = Истина");
		ТекстПериод = СтрЗаменить(ТекстПериод, Сред(ТекстПериод, СтрНайти(ТекстПериод, " - ")), " - ...");
	КонецЕсли;
	
	Возврат ТекстПериод;
	
КонецФункции

#КонецОбласти

#КонецЕсли