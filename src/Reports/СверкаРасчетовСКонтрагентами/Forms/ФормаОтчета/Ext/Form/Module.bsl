
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СервисСверкиРасчетовПереопределяемый.ОтчетПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	СервисСверкиРасчетовПереопределяемый.ЗаполнитьСписокОрганизаций(Элементы.ПолеОрганизация, СоответствиеОрганизаций);
	
	РежимРасшифровки          = Параметры.РежимРасшифровки;
	ОтложенноеФормирование    = Параметры.ОтложенноеФормирование;
	ИдентификаторыРасхождений = Параметры.ИдентификаторыРасхождений;
	ДатаНачалаСверкиРасчетов = СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов();
	
	ОбновитьДатуПолученияДанных();
	
	Если РежимРасшифровки Тогда
		СтандартнаяОбработка = Ложь;
		Отчет.Организация = Справочники.Организации.ПустаяСсылка();
		Отчет.Контрагент  = Справочники.Контрагенты.ПустаяСсылка();
		Отчет.НачалоПериода = Дата(1,1,1);
		Отчет.КонецПериода  = Дата(1,1,1);
	КонецЕсли;
	
	УстановитьНачальнуюДатуПолученияРасхождений(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНачальнуюДатуПолученияРасхождений(Форма)
	
	Отчет = Форма.Отчет;
	// Получаем расхождения с даты начала отчета,
	// но не ранее даты начала сверки сервисом см. СервисСверкиРасчетов.ДатаНачалаСверкиРасчетов().
	Форма.НачальнаяДатаПолученияРасхождений = Отчет.НачалоПериода;
	Если Отчет.НачалоПериода < Форма.ДатаНачалаСверкиРасчетов Тогда
		Форма.НачальнаяДатаПолученияРасхождений = Форма.ДатаНачалаСверкиРасчетов;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПриОткрытии(ЭтотОбъект, Отказ);
	
	Если ОтложенноеФормирование Тогда
		Возврат;
	КонецЕсли;
		
	Если РежимРасшифровки Тогда
		ЭтоПовторноеФормирование = Истина;
		ЗапуститьФормированиеОтчета();
	Иначе
		ПодключитьОбработчикОжидания("Подключаемый_ОбновитьДанныеОтчета", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбсужденияПодключены" Тогда
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		Если ОповещенияСверкиВключены Тогда 
			ПоказатьБаннерВключитьОповещения();
		КонецЕсли;
	КонецЕсли

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьОтчет(Команда)
	
	Если ЭтоПовторноеФормирование Тогда
		ОбновитьДанныеОтчета(Истина);
	Иначе
		ЭтоПовторноеФормирование = Истина;
		ЗапуститьФормированиеОтчета();
	КонецЕсли;
	
	Элементы.Результат.АктивизироватьПоУмолчанию = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьНастройкиИСформироватьОтчет(Команда)
	
	// Сбрасываем признак повторного формирования при изменении настроек.
	// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
	ЭтоПовторноеФормирование = Ложь;
	
	СформироватьОтчет(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Отчет.НачалоПериода, Отчет.КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	СервисСверкиРасчетовКлиентПереопределяемый.ОткрытьФормуВыбораПериода(ПараметрыВыбора, Элементы.ВыбратьПериод, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	Элементы.ПрименитьНастройки.КнопкаПоУмолчанию = Истина;
	ОткрытьНастройки();
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКак(Команда)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтчетСохранитьКак(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьНастройки(Команда)
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	СкрытьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьОповещения(Команда)

	Если НЕ СервисСверкиРасчетовВызовСервера.ОбсужденияПодключены() Тогда
		МодульОбсужденияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбсужденияКлиент");
		МодульОбсужденияКлиент.ПоказатьПодключение();
	Иначе 
		ОповещенияСверкиВключены = СервисСверкиРасчетовВызовСервера.ВключитьОповещениеСверки();
		Если ОповещенияСверкиВключены Тогда
			ПоказатьБаннерВключитьОповещения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеВключатьОповещения(Команда)
	
	СкрытьБаннерВключитьОповещения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеОрганизацияПриИзменении(Элемент)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияПриИзменении(Элемент, ПолеОрганизация,
		Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОрганизацияПриИзменении(ЭтотОбъект, Элемент);
	
	// Сбрасываем признак повторного формирования при изменении настроек.
	// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
	ЭтоПовторноеФормирование = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияОткрытие(Элемент, СтандартнаяОбработка,
		ПолеОрганизация, СоответствиеОрганизаций);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ПолеОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, 
		СоответствиеОрганизаций, Отчет.Организация, Отчет.ВключатьОбособленныеПодразделения);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	// Сбрасываем признак повторного формирования при изменении настроек.
	// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
	ЭтоПовторноеФормирование = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	// Сбрасываем признак повторного формирования при изменении настроек.
	// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
	ЭтоПовторноеФормирование = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	ПредыдущаяНачальнаяДатаРасхождений = ЭтотОбъект.НачальнаяДатаПолученияРасхождений;
	УстановитьНачальнуюДатуПолученияРасхождений(ЭтотОбъект);
	
	Если ПредыдущаяНачальнаяДатаРасхождений > ЭтотОбъект.НачальнаяДатаПолученияРасхождений Тогда
		// Если поменяли дату начала на более ранний срок, то необходимо
		// получить расхождения из сервиса повторно.
		ЭтоПовторноеФормирование = Истина;
	Иначе
		// Сбрасываем признак повторного формирования при изменении настроек.
		// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
		ЭтоПовторноеФормирование = Ложь;
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура УбратьОтборНовыеРасхожденияНажатие(Элемент)
	
	ЭтоПовторноеФормирование = Истина;
	ИдентификаторыРасхождений.Очистить();
	Элементы.ГруппаОтборНовыеРасхождения.Видимость = Ложь;
	ЗапуститьФормированиеОтчета();
	
	Элементы.Результат.АктивизироватьПоУмолчанию = Ложь;
	
КонецПроцедуры

#Область НастройкиОтчета

&НаКлиенте
Процедура ГруппировкаСнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Ложь;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаУстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицы Из Отчет.Группировка Цикл
		СтрокаТаблицы.Использование = Истина;
	КонецЦикла;
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ГруппировкаПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);  
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппировкаПередНачаломИзменения(Элемент, Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ГруппировкаПередНачаломИзменения(ЭтаФорма, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПриИзменении(Элемент)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПриИзменении(ЭтотОбъект, Элемент, Ложь);
	
	ОбновитьТекстЗаголовка(ЭтотОбъект);
	Элементы.ГруппаБаннер.Видимость = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0;

КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыПередНачаломИзменения(Элемент, Отказ)

	СервисСверкиРасчетовКлиентПереопределяемый.ОтборыПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура РазмещениеДополнительныхПолейПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПриИзменении(Элемент)
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ДополнительныеПоляПередНачаломДобавления(ЭтотОбъект, Элемент, Отказ, Копирование, Родитель, Группа);
	
КонецПроцедуры

&НаКлиенте
Процедура ДополнительныеПоляПередНачаломИзменения(Элемент, Отказ)
	
	СервисСверкиРасчетовКлиентПереопределяемый.ДополнительныеПоляПередНачаломИзменения(ЭтотОбъект, Элемент, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыводитьЗаголовокПриИзменении(Элемент)

	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийПоляРезультата

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если ТипЗнч(Расшифровка) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		Если СтрНайти(Расшифровка, "http") = 1 Тогда
			ПерейтиПоНавигационнойСсылке(Расшифровка);
		ИначеЕсли СтрНайти(Расшифровка, "КонтрагентыБизнесСеть") Тогда
			ПерейтиПоНавигационнойСсылке(Расшифровка);
		КонецЕсли;
	Иначе
		СервисСверкиРасчетовКлиентПереопределяемый.ОбработкаРасшифровкиСтандартногоОтчета(ЭтотОбъект, Элемент, Расшифровка, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаДополнительнойРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	// Не будем обрабатывать нажатие на правую кнопку мыши.
	// Покажем стандартное контекстное меню ячейки табличного документа.
	Расшифровка = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПриИзменении(Элемент)
	Если Элементы.Результат.Редактирование Тогда
		Элементы.ГруппаБаннер.Видимость = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_СформироватьПриОткрытии()
	
	ЭтоПовторноеФормирование = Истина;
	ОбновитьДанныеОтчета(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеОтчета(СформироватьОтчетПослеОбновления = Ложь)
	
	Если ВыполняютсяСервисныеЗадания() Тогда
		// Обмен с сервисом уже запущен, просто формируем отчет если требуется
		Если СформироватьОтчетПослеОбновления Тогда
			ЗапуститьФормированиеОтчета();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("СформироватьОтчетПослеОбновления", СформироватьОтчетПослеОбновления);
	
	ОбменССервисом = ОбменятьсяДаннымиССервисом(УникальныйИдентификатор, НачальнаяДатаПолученияРасхождений);
	ИдентификаторЗаданияОбмена = ОбменССервисом.ИдентификаторЗадания;
	Если ОбменССервисом.Статус = "Выполнено" Тогда
		ПослеЗавершенияОбмена(ОбменССервисом, ДополнительныеПараметры);
		Возврат
	КонецЕсли;
	
	Элементы.Сформировать.Доступность            = Ложь;
	Элементы.ГруппаОбновление.Видимость          = Истина;
	Элементы.ДекорацияСервисНеОтвечает.Видимость = Ложь;
	
	ОповещениеОЗавершении   = Новый ОписаниеОповещения("ПослеЗавершенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыОжидания       = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтаФорма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ОбменССервисом, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполняютсяСервисныеЗадания()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Отбор = Новый Структура("Состояние", СостояниеФоновогоЗадания.Активно);
	
	МассивФоновыхЗаданиях = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	ИмяСервиса = СервисСверкиРасчетов.ИмяСервиса();
	Для каждого ФоновоеЗадание Из МассивФоновыхЗаданиях Цикл
		
		Если СтрНайти(ФоновоеЗадание.Наименование, ИмяСервиса) <> 0 Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбменятьсяДаннымиССервисом(УникальныйИдентификатор, НачальнаяДатаПолученияРасхождений)
	
	НаименованиеЗадания = СтрШаблон("%1. Обмен с сервисом", СервисСверкиРасчетов.ИмяСервиса());
	ИмяМетода           = "СервисСверкиРасчетов.ОбменятьсяССервисом";
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	ПараметрыПроцедуры = Новый Структура("ЭтоВызовИзОтчета, НачальнаяДатаПолученияРасхождений",
		Истина, НачальнаяДатаПолученияРасхождений);
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, ПараметрыПроцедуры);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияОбмена(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Элементы.Сформировать.Доступность   = Истина;
	Элементы.ГруппаОбновление.Видимость = Ложь;
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		Возврат;
	Иначе
		ИдентификаторЗаданияОбмена = Неопределено;
	КонецЕсли;
	
	Если Результат.Статус <> "Выполнено" Тогда 
		Возврат;
	КонецЕсли;
	
	ОбновитьДатуПолученияДанных();
	
	Оповестить("ФормированиеОтчетаПоРасхождениям");
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры)
		И ДополнительныеПараметры.СформироватьОтчетПослеОбновления Тогда
		ЗапуститьФормированиеОтчета();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДатуПолученияДанных()
	
	ДатаУспешногоПолученияДанных = ДатаПолученияДанныхСервисаСверкиРасчетов;
	ДатаПолученияДанныхСервисаСверкиРасчетов = Константы.ДатаПолученияДанныхСервисаСверкиРасчетов.Получить();
	Если ЗначениеЗаполнено(ДатаПолученияДанныхСервисаСверкиРасчетов) Тогда
		Элементы.НадписьДатаАктуальности.Заголовок = СтрШаблон(НСтр("ru = 'Данные отчета получены из сервиса %1';
																	|en = 'The report data is obtained from the %1 service'"), СервисСверкиРасчетов.ИмяСервиса());
		Элементы.ДатаПолученияДанныхСервисаСверкиРасчетов.Видимость = Истина;
		Элементы.ДекорацияСервисНеОтвечает.Видимость = ДатаУспешногоПолученияДанных = ДатаПолученияДанныхСервисаСверкиРасчетов;
	Иначе
		Элементы.НадписьДатаАктуальности.Заголовок = СтрШаблон(НСтр("ru = 'Данные отчета не были получены из сервиса %1';
																	|en = 'The report data was not obtained from the %1 service'"), СервисСверкиРасчетов.ИмяСервиса());
		Элементы.ДатаПолученияДанныхСервисаСверкиРасчетов.Видимость = Ложь;
		Элементы.ДекорацияСервисНеОтвечает.Видимость                = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПодготовленныеДанные(РезультатВыполнения, ДополнительныеПараметры) Экспорт

	ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПодготовленныеДанныеНаСервере(РезультатВыполнения)

	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатВыполнения, "Статус", "Отменено") = "Отменено" Тогда
		
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		// ОписаниеЗаданияФормированияОтчета не очищаем на случай, если отмена произошла перед началом нового формирования отчета,
		// и описание уже относится к новому запуску.
		Возврат;
		
	ИначеЕсли РезультатВыполнения.Статус = "Ошибка" Тогда
		// Это результат аварийного завершения фонового задания.
		ОбщегоНазначения.СообщитьПользователю(РезультатВыполнения.КраткоеПредставлениеОшибки);
		
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		
		ПозицияТочки = СтрНайти(ЭтотОбъект.ИмяФормы, ".", ,, 2);
		ПолноеИмяМетаданного = ?(ПозицияТочки = 0, ЭтотОбъект.ИмяФормы, Лев(ЭтотОбъект.ИмяФормы, ПозицияТочки - 1));
		ОтчетМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданного);
		ОтчетОрганизация = ?(ЭтотОбъект.Отчет.Свойство("Организация"), ЭтотОбъект.Отчет.Организация, Неопределено);
		
		ЗаписьЖурналаРегистрации("ОшибкаФормированияОтчета",
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетМетаданные,
			ОтчетОрганизация,
			РезультатВыполнения.ПодробноеПредставлениеОшибки,
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
		Возврат;
		
	КонецЕсли;
	
	ЭтотОбъект.ОписаниеЗаданияФормированияОтчета = Неопределено;
    ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(ЭтотОбъект.Элементы.Результат, "НеИспользовать");
	
	ДанныеОтчета = ПолучитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	УдалитьИзВременногоХранилища(РезультатВыполнения.АдресРезультата);
	Если ДанныеОтчета = Неопределено Или Не ДанныеОтчета.Свойство("Результат") Тогда
		// Некорректный результат формирования отчета.
		ЭтотОбъект.Результат = Новый ТабличныйДокумент;
		ЭтотОбъект.ДанныеРасшифровки = Неопределено;
		
		ПозицияТочки = СтрНайти(ЭтотОбъект.ИмяФормы, ".", ,, 2);
		ПолноеИмяМетаданного = ?(ПозицияТочки = 0, ЭтотОбъект.ИмяФормы, Лев(ЭтотОбъект.ИмяФормы, ПозицияТочки - 1));
		ОтчетМетаданные = Метаданные.НайтиПоПолномуИмени(ПолноеИмяМетаданного);
		ОтчетОрганизация = ?(ЭтотОбъект.Отчет.Свойство("Организация"), ЭтотОбъект.Отчет.Организация, Неопределено);
		
		ЗаписьЖурналаРегистрации("ОшибкаФормированияОтчета",
			УровеньЖурналаРегистрации.Ошибка,
			ОтчетМетаданные,
			ОтчетОрганизация,
			НСтр("ru = 'Некорректный результат формирования отчета: отсутствует Результат';
				|en = 'Incorrect report generation result: Result is missing'"),
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
			
		Возврат;
		
	КонецЕсли;
		
	ЭтотОбъект.Результат = ДанныеОтчета.Результат;
	ДанныеОтчета.Результат = Неопределено;
	ЭтотОбъект.ДанныеРасшифровки = ДанныеОтчета.ДанныеРасшифровки;
	ДанныеОтчета.ДанныеРасшифровки = Неопределено;
	
	ПоказатьБаннер();
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннерВключитьОповещения()

	НеПоказыватьПредложениеПодключитьОповещения = ХранилищеОбщихНастроек.Загрузить(ВРег("СервисСверкиРасчетовНеПоказыватьПредложениеПодключитьОповещения"));
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если ЗначениеЗаполнено(НеПоказыватьПредложениеПодключитьОповещения) Тогда
		Элементы.ГруппаВключениеОповещений.Видимость = ПолныеПраваИлиПривилегированныйРежим
		И Не СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены()
		И Не НеПоказыватьПредложениеПодключитьОповещения;
	Иначе
		Элементы.ГруппаВключениеОповещений.Видимость = ПолныеПраваИлиПривилегированныйРежим
			И Не СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкрытьБаннерВключитьОповещения()
	
	ХранилищеОбщихНастроек.Сохранить(ВРег("СервисСверкиРасчетовНеПоказыватьПредложениеПодключитьОповещения"),, Истина);
	Элементы.ГруппаВключениеОповещений.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьБаннер()
	
	Если ИдентификаторыРасхождений.Количество() > 0 Тогда
		// Если показываем только новые расхождения, то вместо баннера с итогами расхождений
		// показываем плашку отбора по новым расхождениям. 
		Элементы.ГруппаБаннер.Видимость                = Ложь;
		Элементы.ГруппаОтборНовыеРасхождения.Видимость = Истина;
		ОчиститьОбластьСИтогами();
		Возврат;
	Иначе
		Элементы.ГруппаОтборНовыеРасхождения.Видимость = Ложь;
	КонецЕсли;
	
	ИтогиСверкиПоТаблице = Отчеты.СверкаРасчетовСКонтрагентами.ИтогиСверкиПоТаблице(Результат);
	
	Если ИтогиСверкиПоТаблице.НетИтогов Тогда
		Элементы.ГруппаБаннер.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ПоказатьБаннерВключитьОповещения();
	
	Элементы.ГруппаБаннер.Видимость = Отчет.КомпоновщикНастроек.Настройки.Отбор.Элементы.Количество() = 0;
	
	КоличествоДокументов   = ИтогиСверкиПоТаблице.КоличествоДокументов;
	НеОтраженоОрганизацией = ИтогиСверкиПоТаблице.НеОтраженоОрганизацией;
	НеОтраженоКонтрагентом = ИтогиСверкиПоТаблице.НеОтраженоКонтрагентом;
	РазличаютсяРеквизиты   = ИтогиСверкиПоТаблице.РазличаютсяРеквизиты;
	БезРасхождений         = ИтогиСверкиПоТаблице.БезРасхождений;
	НетИтогов              = ИтогиСверкиПоТаблице.НетИтогов;
	
	ЗапятаяПослеНеОтраженоОрганизацией   = "";
	ЗапятаяПослеНеОтраженоКонтрагентом   = "";
	ТочкаПослеСРазличающимисяРеквизитами = ""; 
	
	НеОтраженоОрганизациейНиз = 0;
	НеОтраженоКонтрагентомНиз = 0;
	
	Если НЕ НетИтогов Тогда
		
		ОчиститьОбластьСИтогами();
		
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	
	Если КоличествоДокументов = 0 Тогда
		
		Элементы.ГруппаБаннер.ЦветФона = ЦветаСтиля.ЦветФонаОтрицательногоЗначения;
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Сверка не производилась';
																|en = 'No reconciliation was performed'")));
		
	Иначе
		
		Если НеОтраженоКонтрагентом > 0
			ИЛИ РазличаютсяРеквизиты > 0 Тогда
				
			ЗапятаяПослеНеОтраженоОрганизацией = ", ";
				
		КонецЕсли;
			
		Если РазличаютсяРеквизиты > 0 Тогда
				
			ЗапятаяПослеНеОтраженоКонтрагентом = ", ";
				
		КонецЕсли;
		
		Элементы.ГруппаБаннер.ЦветФона = Новый Цвет(215, 240, 199);
		
		Сверено = НСтр("ru = 'Сверено';
						|en = 'Reconciled'");
		Если Прав(Строка(КоличествоДокументов), 1) = "1"
			И Прав(Строка(КоличествоДокументов), 2) <> "11" Тогда
			Сверено = Лев(Сверено,6);
		КонецЕсли;
		
		СвереноДокументов = ПолучитьСклоненияСтрокиПоЧислу("документ", КоличествоДокументов,,,"ПД=Винительный");
		ДокументовБезРасхождений = ПолучитьСклоненияСтрокиПоЧислу("документ", БезРасхождений,,,"ПД=Винительный");
		
		
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = '%1 %2';
																		|en = '%1 %2'"),
			Сверено,
			СвереноДокументов[0])));
			
		// Для управления сворачиванием/разворачивание группирок отчета из баннера
		ОбластьНеОтраженыОрганизацией = Результат.НайтиТекст(Строка("Не отражено организацией"));
		ОбластьНеОтраженыКонтрагентом = Результат.НайтиТекст(Строка("Не отражено контрагентом"));
		ОбластьРазличаютсяРеквизиты = Результат.НайтиТекст(Строка("Различаются реквизиты"));
		
		Если ОбластьНеОтраженыОрганизацией <> Неопределено Тогда
			Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
				НеОтраженоОрганизациейНиз = ОбластьНеОтраженыКонтрагентом.Низ - 1;
			ИначеЕсли ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				НеОтраженоОрганизациейНиз = ОбластьРазличаютсяРеквизиты.Низ - 1;
			Иначе
				НеОтраженоОрганизациейНиз = Результат.ВысотаТаблицы;
			КонецЕсли;
		КонецЕсли;
		
		Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
			Если ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				НеОтраженоКонтрагентомНиз = ОбластьРазличаютсяРеквизиты.Низ - 1;
			Иначе
				НеОтраженоКонтрагентомНиз = Результат.ВысотаТаблицы;
			КонецЕсли;
		КонецЕсли;
	
		// Формулировка баннера
		Если ОбластьНеОтраженыОрганизацией <> Неопределено
			ИЛИ ОбластьНеОтраженыКонтрагентом <> Неопределено
			ИЛИ ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ': ';
																	|en = ': '")));
			Элементы.ГруппаБаннер.ЦветФона = ЦветаСтиля.ЦветФонаОтрицательногоЗначения;
			
			Если ОбластьНеОтраженыОрганизацией <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru = 'не отражено организацией';
						|en = 'not recorded by the company'"),,,,
					"Не отражено организацией"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = ' (%1)%2';
																				|en = ' (%1)%2'"),
					НеОтраженоОрганизацией,
					ЗапятаяПослеНеОтраженоОрганизацией)));
					
			КонецЕсли;
			
			Если ОбластьНеОтраженыКонтрагентом <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru = 'не отражено контрагентом';
						|en = 'not recorded by the counterparty'"),,,,
					"Не отражено контрагентом"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = ' (%1)%2';
																				|en = ' (%1)%2'"),
					НеОтраженоКонтрагентом,
					ЗапятаяПослеНеОтраженоКонтрагентом)));
					
			КонецЕсли;
			
			Если ОбластьРазличаютсяРеквизиты <> Неопределено Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
					НСтр("ru = 'с различающимися реквизитами';
						|en = 'with different attributes'"),,,,
					"Различаются реквизиты"));
					
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = ' (%1)';
																				|en = ' (%1)'"),
					РазличаютсяРеквизиты)));
					
			КонецЕсли;
				
			Если БезРасхождений > 0 Тогда
				
				МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон(НСтр("ru = ' и еще %1 без расхождений';
																				|en = ' and %1 more without discrepancies'"),
					ДокументовБезРасхождений[0])));
				
			КонецЕсли;
			
		Иначе
			
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = ': расхождений не обнаружено';
																	|en = ': no discrepancies are found'")));
			
		КонецЕсли;
		
	КонецЕсли;
	
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = '.';
															|en = '.'")));
	
	Элементы.Баннер.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	ОчиститьСообщения();
	Элементы.ГруппаБаннер.Видимость = Ложь;
	ОтказПроверкиЗаполнения = Ложь;
	
	РезультатВыполнения = СформироватьОтчетНаСервере();
	РезультатВыполнения.Свойство("ОтказПроверкиЗаполнения", ОтказПроверкиЗаполнения);
	
	Если ОтказПроверкиЗаполнения = Истина Тогда
		
		ПоказатьНастройки("");
		
	Иначе
		
		Если РезультатВыполнения.Статус = "Выполняется" Тогда 
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
		КонецЕсли;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗагрузитьПодготовленныеДанные", ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);

		СкрытьНастройки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьДанныеОтчета()
	
	ОбновитьДанныеОтчета();
	
КонецПроцедуры

&НаСервере
Функция СформироватьОтчетНаСервере() Экспорт
	
	Если Не КомпоновщикИнициализирован Тогда
		ИнициализироватьКомпоновщикаНастроек();
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда 
		Возврат Новый Структура("ЗаданиеВыполнено, ОтказПроверкиЗаполнения", Истина, Истина);
	КонецЕсли;
	
	Если ЭтотОбъект.ОписаниеЗаданияФормированияОтчета <> Неопределено Тогда
		
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ЭтотОбъект.ОписаниеЗаданияФормированияОтчета.ИдентификаторЗадания);
		ЭтотОбъект.ОписаниеЗаданияФормированияОтчета = Неопределено;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	Настройки = Отчет.КомпоновщикНастроек.Настройки;
	Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
	Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
	Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ВыводитьЗаголовок);
	Настройки.ДополнительныеСвойства.Вставить("ВыводитьПодвал"   , ВыводитьПодвал);
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчетаНаСервере();
	
	ОписаниеЗаданияФормированияОтчета = ЗапуститьПодготовкуОтчета(ЭтотОбъект, ПараметрыОтчета);
	
	Элементы.Сформировать.КнопкаПоУмолчанию = Истина;
	
	Возврат ОписаниеЗаданияФормированияОтчета;
	
КонецФункции

Функция ЗапуститьПодготовкуОтчета(Форма, ПараметрыОтчета) Экспорт
	
	ПараметрыЗадания = ДлительныеОперации.ПараметрыВыполненияФункции(Форма.УникальныйИдентификатор);
	
	НаименованиеЗадания = НСтр("ru = 'Выполнение отчета: %1';
								|en = 'Running report: %1'");
	ИмяОтчета = "СверкаРасчетовСКонтрагентами";;
	
	ПараметрыЗадания.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НаименованиеЗадания, ИмяОтчета);
	
	Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		ПараметрыЗадания.ЗапуститьНеВФоне = Истина;
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыЗадания, "СервисСверкиРасчетовВызовСервера.ПодготовитьОтчет", ПараметрыОтчета);
	
КонецФункции

&НаСервере
Функция ПодготовитьПараметрыОтчетаНаСервере()
	
	МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ЭтотОбъект.ИмяФормы);
	
	ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
	
	ПараметрыОтчета.Организация                       = Отчет.Организация;
	ПараметрыОтчета.ВключатьОбособленныеПодразделения = Отчет.ВключатьОбособленныеПодразделения;
	ПараметрыОтчета.Контрагент                        = Отчет.Контрагент;
	ПараметрыОтчета.НачалоПериода                     = Отчет.НачалоПериода;
	ПараметрыОтчета.КонецПериода                      = Отчет.КонецПериода;
	ПараметрыОтчета.РежимРасшифровки                  = Отчет.РежимРасшифровки;
	ПараметрыОтчета.СхемаКомпоновкиДанных             = ПолучитьИзВременногоХранилища(СхемаКомпоновкиДанных);
	ПараметрыОтчета.ИдентификаторОтчета               = "СверкаРасчетовСКонтрагентами";
	ПараметрыОтчета.ДанныеРасшифровки                 = ДанныеРасшифровки;
	ПараметрыОтчета.НастройкиКомпоновкиДанных         = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок"        , ВыводитьЗаголовок);
	ПараметрыОтчета.Вставить("ВыводитьПодвал"           , Ложь);
	ПараметрыОтчета.Вставить("ВыводитьОрганизацию"      , НЕ ЗначениеЗаполнено(Отчет.Организация));
	
	ПараметрыОтчета.Вставить("ИдентификаторМакета"      , "Основной"); // Ключ текущего варианта настроек отчета
	ПараметрыОтчета.Вставить("Группировка"              , Отчет.Группировка.Выгрузить());
	ПараметрыОтчета.Вставить("ДополнительныеПоля"       , Отчет.ДополнительныеПоля.Выгрузить());
	ПараметрыОтчета.Вставить("ИдентификаторыРасхождений", ИдентификаторыРасхождений);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикаНастроек()
	
	СервисСверкиРасчетовПереопределяемый.ИнициализацияКомпоновщикаНастроек(ЭтотОбъект, Истина, КлючТекущегоВарианта);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТекстЗаголовка(Форма)

	Отчет = Форма.Отчет;

	ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сверка расчетов с контрагентами%1';
			|en = 'AR/AP reconciliation with counterparties%1'"),
		ПолучитьПредставлениеПериода(Отчет.НачалоПериода, Отчет.КонецПериода));

	Если ЗначениеЗаполнено(Отчет.Организация) И Форма.ИспользуетсяНесколькоОрганизаций Тогда
		ЗаголовокОтчета = ЗаголовокОтчета + " Демо база";
	КонецЕсли;

	Форма.Заголовок = ЗаголовокОтчета;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(НачалоПериода = '00010101', КонецПериода = '00010101', ТолькоДаты  = Ложь, ВидПериода = 0)
	
	ТекстПериод = "";
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда 
		Если КонецПериода >= НачалоПериода Тогда
			ТекстПериод = ?(ТолькоДаты, "", " " + НСтр("ru = 'за';
														|en = 'for'")+ " ");
			ТекстПериод = ТекстПериод
				+ ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(КонецПериода), "ФП = Истина");	
		Иначе
			ТекстПериод = "";
		КонецЕсли;
	ИначеЕсли ЗначениеЗаполнено(НачалоПериода) Тогда
		ТекстПериод = ?(ТолькоДаты, "", " " + НСтр("ru = 'за';
													|en = 'for'")+ " ")
			+ ПредставлениеПериода(НачалоДня(НачалоПериода), КонецДня(Дата(3999, 11, 11)), "ФП = Истина");
		ТекстПериод = СтрЗаменить(ТекстПериод, Сред(ТекстПериод, СтрНайти(ТекстПериод, " - ")), " - ...");
	КонецЕсли;
	
	Возврат ТекстПериод;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЗапрещенныеПоля(Режим = "") Экспорт
	
	СписокПолей = Новый Массив;
	
	СписокПолей.Добавить("UserFields");
	СписокПолей.Добавить("DataParameters");
	СписокПолей.Добавить("SystemFields");
	СписокПолей.Добавить("Показатели");
	СписокПолей.Добавить("Период");
	СписокПолей.Добавить("НомерСтроки");
	
	СписокПолей.Добавить("КоличествоДокументов");
	СписокПолей.Добавить("ПризнакОтраженияНДС");
	СписокПолей.Добавить("СимволСноски");
	СписокПолей.Добавить("ДатаВходящегоДокумента");
	СписокПолей.Добавить("НомерВходящегоДокумента");
	
	СписокПолей.Добавить("ОшДокументРегистратор");
	СписокПолей.Добавить("ОшСтавкаНДС");
	СписокПолей.Добавить("ОшНоменклатура");
	СписокПолей.Добавить("ОшКоличествоДокументов");
	
	
	Для Каждого ДоступноеПоле Из Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если ДоступноеПоле.Ресурс Тогда
			СписокПолей.Добавить(Строка(ДоступноеПоле.Поле));
		КонецЕсли;
	КонецЦикла;
		
	Если Режим = "Отбор" Тогда
		СписокПолей.Добавить("РасшифровкаНачислениеРеализацияДокументРегистратор");
		СписокПолей.Добавить("ПризнакНаличияОшибок");
	ИначеЕсли Режим = "Порядок" Тогда
		СервисСверкиРасчетовКлиентПереопределяемый.ДобавитьПоляРесурсовВЗапрещенныеПоля(ЭтаФорма, СписокПолей);
		СписокПолей.Добавить("ПризнакНаличияОшибок");
	ИначеЕсли Режим = "Группировка" Тогда
		СписокПолей.Добавить("ЗаписьДополнительногоЛиста");
	ИначеЕсли Режим = "Выбор" Тогда
		СписокПолей.Добавить("ПризнакНаличияОшибок");
		СписокПолей.Добавить("ОтражениеНДСВУчете");
	КонецЕсли;
	
	Возврат Новый ФиксированныйМассив(СписокПолей);
	
КонецФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Отчет, РезультатВыбора, "НачалоПериода,КонецПериода");
	
	Если ОписаниеЗаданияФормированияОтчета = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
	ПредыдущаяНачальнаяДатаРасхождений = ЭтотОбъект.НачальнаяДатаПолученияРасхождений;
	УстановитьНачальнуюДатуПолученияРасхождений(ЭтотОбъект);
	
	Если ПредыдущаяНачальнаяДатаРасхождений > ЭтотОбъект.НачальнаяДатаПолученияРасхождений Тогда
		// Если поменяли дату начала на более ранний срок, то необходимо
		// получить расхождения из сервиса повторно.
		ЭтоПовторноеФормирование = Истина;
	Иначе
		// Сбрасываем признак повторного формирования при изменении настроек.
		// В таком случае не обновляем данные из сервиса, а формируем отчет по имеющимся в программе данным.
		ЭтоПовторноеФормирование = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.НастройкиОтчета;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьНастройки()
	
	Элементы.РазделыОтчета.ТекущаяСтраница = Элементы.Отчет;
	
КонецПроцедуры

&НаКлиенте
Процедура БаннерОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НаименованиеГруппировки = Строка(НавигационнаяСсылкаФорматированнойСтроки);
	НайденнаяОбласть = Результат.НайтиТекст(НаименованиеГруппировки);
	
	Если НайденнаяОбласть <> Неопределено Тогда
		
		Результат.ПоказатьУровеньГруппировокСтрок(0);
		Элементы.Результат.ТекущаяОбласть = НайденнаяОбласть;
		
		Если НаименованиеГруппировки = "Различаются реквизиты" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(Результат.ВысотаТаблицы, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Если НаименованиеГруппировки = "Не отражено организацией" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(НеОтраженоОрганизациейНиз, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Если НаименованиеГруппировки = "Не отражено контрагентом" Тогда
			Результат.Область("R" + Формат(НайденнаяОбласть.Низ, "ЧГ=0")+
			":R" + Формат(НеОтраженоКонтрагентомНиз, "ЧГ=0")).Видимость = Истина;
		КонецЕсли;
		
		Элементы.Результат.АктивизироватьПоУмолчанию = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы 
		И ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ОтменитьВыполнениеЗаданияПриЗакрытии();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьВыполнениеЗаданияПриЗакрытии()
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОбмена) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗаданияОбмена);
	КонецЕсли;
	
	ИдентификаторЗаданияОбмена = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьОбластьСИтогами()
	
	Результат.УдалитьОбласть(Результат.Область("R"+Формат(Результат.ВысотаТаблицы -1, "ЧГ=0")+":R"+
	Формат(Результат.ВысотаТаблицы, "ЧГ=0")),
	ТипСмещенияТабличногоДокумента.ПоВертикали);

КонецПроцедуры

#КонецОбласти