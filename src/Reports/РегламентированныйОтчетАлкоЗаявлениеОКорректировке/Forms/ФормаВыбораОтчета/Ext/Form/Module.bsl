
&НаСервере
Функция ТекстДинамическогоЗапроса()

	Текст = " 
			|	ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ЖурналОтчетовСтатусы.НаименованиеОтчета КАК НаименованиеОтчета,
			|	ЖурналОтчетовСтатусы.Комментарий КАК Комментарий,
			|	ЖурналОтчетовСтатусы.Организация КАК Организация,
			|	ЖурналОтчетовСтатусы.Статус КАК СтатусОтчета,
			|	ЖурналОтчетовСтатусы.ФинансовыйПериод КАК ПредставлениеПериода,
			|	ЖурналОтчетовСтатусы.ВариантОтчета КАК ВидОтчета,
			|	ЖурналОтчетовСтатусы.Ссылка КАК СсылкаОтчета
			|ИЗ
			|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка.ИсточникОтчета = &ИсточникОтчета
			|	" + ?(ТолькоПервичные, "И ЖурналОтчетовСтатусы.ВариантОтчета = ""П""", "") + "
			|	И ЖурналОтчетовСтатусы.ПометкаУдаления = ЛОЖЬ";
	 
	
	Если ЗначениеЗаполнено(Организация) Тогда	
		Текст = Текст + "
		 	|	И ЖурналОтчетовСтатусы.Организация = &Организация";	
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда	
		Текст = Текст + "
		 	|	И ЖурналОтчетовСтатусы.ДатаНачала = &ДатаНачала";	
	КонецЕсли;
	 
	Если ЗначениеЗаполнено(ДатаКонца) Тогда	
		Текст = Текст + "
		 	|	И ЖурналОтчетовСтатусы.ДатаОкончания = &ДатаОкончания";	
	КонецЕсли; 

	Возврат  Текст;
	
КонецФункции 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Организация 				= Параметры.Организация;
	ДатаНачала				  	= Параметры.мДатаНачалаПериодаОтчета;
	ДатаКонца					= Параметры.мДатаКонцаПериодаОтчета;
	
	// СписокАлкоОтчетов - структура с ключом Номеротчета и 3начением Имя отчета в метаданных
	ИсточникОтчета 			= Параметры.ИсточникОтчета;
	
	ТолькоПервичные = Истина;
	
	ИнициализацияДинамическогоСписка();
	
	РегламентированнаяОтчетностьАЛКО.ПодготовкаИнтерфейсаФормыАЛКО(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправляемаяФормаВладелец = Неопределено;
	РегламентированнаяОтчетностьАЛКОКлиент.ИнициализироватьUIПодчиненнойФормыОтчетаАЛКО(
												ЭтаФорма, УправляемаяФормаВладелец);
												
КонецПроцедуры

&НаСервере
Функция ПолучитьЗаявлениеОКорректировкеПоЖурналу(ДокументОтчета)

	Возврат  РегламентированнаяОтчетностьАЛКО.ПолучитьЗаявлениеОКорректировкеПоЖурналу(ДокументОтчета);

КонецФункции
 
&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если  Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	СсылкаОтчета = ТекущиеДанные.СсылкаОтчета;
	
	// Нужно проверить, не введено ли уже Заявление о корректировке по этому отчету.
	ЗаявлениеОКорректировке = ПолучитьЗаявлениеОКорректировкеПоЖурналу(СсылкаОтчета);
	
	Если ЗначениеЗаполнено(ЗаявлениеОКорректировке) Тогда
		
		ТекстПредупреждения = НСтр("ru='По данному отчету заведено Заявление о корректировке
									|" + Строка(ЗаявлениеОКорректировке) + "
									|
									|Выберите другой отчет.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		
		Возврат;
		
	КонецЕсли;
	
	Закрыть(СсылкаОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоПервичныеПриИзменении(Элемент)
	
	ИнициализацияДинамическогоСписка();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализацияДинамическогоСписка()

	ТекстЗапроса = ТекстДинамическогоЗапроса();
	
	ДинСписокОтчетов.ТекстЗапроса = ТекстЗапроса;
	
	ДинСписокОтчетов.Параметры.УстановитьЗначениеПараметра("ИсточникОтчета", ИсточникОтчета);
	
	Если ЗначениеЗаполнено(Организация) Тогда	
		ДинСписокОтчетов.Параметры.УстановитьЗначениеПараметра("Организация", Организация);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаНачала) Тогда	
		ДинСписокОтчетов.Параметры.УстановитьЗначениеПараметра("ДатаНачала", НачалоДня(ДатаНачала));	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаКонца) Тогда	
		ДинСписокОтчетов.Параметры.УстановитьЗначениеПараметра("ДатаОкончания", НачалоДня(ДатаКонца));	
	КонецЕсли;
	
	ДинСписокОтчетов.ОсновнаяТаблица = "";
	
	Элементы.Отчеты.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьUIФормы(НастройкаUI) Экспорт
	
	// Серверный вызов ради скорости исполнения, 
	// на клиенте намного медленнее из-за постоянного обновления формы.
	ИнициализироватьUIФормыНаСервере(НастройкаUI)

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьUIФормыНаСервере(НастройкаUI) Экспорт

	РегламентированнаяОтчетностьАЛКОКлиентСервер.ИнициализироватьUIФормы(ЭтаФорма, НастройкаUI);

КонецПроцедуры