#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.СтатусГрафика Тогда
		
		СтатусГрафика = СтатусРабочийГрафик();
		
	КонецЕсли;
	
	ЭтапПроизводства = Параметры.ПараметрКоманды;
	Если ЗначениеЗаполнено(Параметры.Этап) Тогда
		ЭтапПроизводства = Параметры.Этап;
		ЗаказНаПроизводство = Параметры.ЗаказНаПроизводство;
		КлючПартия = Параметры.КлючПартия;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтапПроизводства) Тогда
		
		СформироватьОтчетВФоне();
		
	КонецЕсли;
	
	Элементы.ЭтапПроизводства.Видимость = НЕ ЗначениеЗаполнено(ЭтапПроизводства);
	Элементы.ГруппаЭтап.Видимость = ЗначениеЗаполнено(ЭтапПроизводства);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		ПодключитьОбработчикОжидания("НачатьОжиданиеДлительнойОперации", 0.1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭтапПроизводстваНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(ЭтапПроизводства) И ТипЗнч(ЭтапПроизводства) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		
		СтруктураОтбора.Вставить(
			"Распоряжение",
			РаспоряжениеЭтапа(ЭтапПроизводства));
			
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Отбор", СтруктураОтбора);
	
	ОткрытьФорму(
		"Документ.ЭтапПроизводства2_2.ФормаВыбора",
		ПараметрыФормы,
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапПроизводстваНажатие(Элемент)
	ПоказатьЗначение(, ЭтапПроизводства);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДиаграммыФормыСрокиВыполнения

&НаКлиенте
Процедура ДиаграммаГантаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	Если ТипЗнч(Расшифровки) = Тип("Массив")
		И Расшифровки.Количество() > 1 Тогда
		
		ЗначениеРасшифровки  = Расшифровки[1];
		
		Если ТипЗнч(ЗначениеРасшифровки.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2")
			ИЛИ ТипЗнч(ЗначениеРасшифровки.Этап) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
			
			СтандартнаяОбработка = Ложь;
			
			КлючПартия = ЗначениеРасшифровки.КлючПартия;
			
			Если ТипЗнч(ЭтапПроизводства) = Тип("ДокументСсылка.ЭтапПроизводства2_2")
				И ТипЗнч(ЗначениеРасшифровки.Этап) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
				ЗаказНаПроизводство = РаспоряжениеЭтапа(ЭтапПроизводства);
			ИначеЕсли ТипЗнч(ЭтапПроизводства) = Тип("СправочникСсылка.ЭтапыПроизводства")
				И ТипЗнч(ЗначениеРасшифровки.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда 
				ЗаказНаПроизводство = Неопределено;
				КлючПартия = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
			КонецЕсли;
			
			ЭтапПроизводства = ЗначениеРасшифровки.Этап;
			ЗапуститьФормированиеОтчета();
		
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Расшифровки) = Тип("Структура")
		И Расшифровки.Свойство("Этап") Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(, Расшифровки.Этап);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ЗапуститьФормированиеОтчета();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФормированиеОтчета

&НаКлиенте
Процедура ЗапуститьФормированиеОтчета()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчетВФоне();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СформироватьОтчетВФонеЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетВФоне()
	
	СформироватьГиперссылкуЭтапа();
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Этап", ЭтапПроизводства);
	ПараметрыПроцедуры.Вставить("СтатусГрафика", СтатусГрафика);
	ПараметрыПроцедуры.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
	ПараметрыПроцедуры.Вставить("КлючПартия",          КлючПартия);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Формирование отчета ""График смежных этапов производства""';
			|en = 'Generate the ""Adjacent production stages"" report'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Отчеты.ДиаграммаСмежныхЭтаповПроизводства.СформироватьОтчет",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчетВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьДиаграмму(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатФормированияОтчетаВФоне(ЭтотОбъект, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьРезультатФормированияОтчетаВФоне(Форма, Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		НоваяДиаграмма = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(НоваяДиаграмма) = Тип("ДиаграммаГанта") Тогда
			Форма.ДиаграммаГанта = НоваяДиаграмма;
		КонецЕсли;
		
		Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
		
	Иначе
		
		ОчиститьИПоказатьДиаграмму(Форма);
		
		Если Результат.Статус = "Ошибка" Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьДиаграмму(Форма)
	
	Форма.ДиаграммаГанта.Очистить();
	Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиаграмма;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьДлительнуюОперацию(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

&НаСервереБезКонтекста
Функция РаспоряжениеЭтапа(ЭтапПроизводства)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтапПроизводства, "Распоряжение");

КонецФункции

&НаСервере
Процедура СформироватьГиперссылкуЭтапа()
	
	Если ТипЗнч(ЭтапПроизводства) = Тип("СправочникСсылка.ЭтапыПроизводства") Тогда
		Элементы.ЭтапПроизводстваСсылка.Заголовок = Документы.ЗаказНаПроизводство2_2.ПредставлениеЗаказа(ЗаказНаПроизводство) + ", "
			+ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтапПроизводства, "Наименование");
	Иначе
		Элементы.ЭтапПроизводстваСсылка.Заголовок = Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(ЭтапПроизводства);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
