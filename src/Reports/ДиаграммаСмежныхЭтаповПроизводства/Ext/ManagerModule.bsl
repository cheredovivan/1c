#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Формирует отчет.
//
// Параметры:
//  Параметры  - Структура - параметры формирования отчета:
//		* Этап - ДокументСсылка.ЭтапПроизводства2_2, СправочникСсылка.ЭтапыПроизводства - диагностируемый этап.
//		* СтатусГрафика - Число - статус диагностируемого графика.
//		* ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2
//		* КлючПартия - УникальныйИдентификатор
//  АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат формирования отчета.
//
Процедура СформироватьОтчет(Параметры, АдресХранилища) Экспорт
	
	ДиаграммаГанта = Новый ДиаграммаГанта;
	
	НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта);
	
	Границы = Новый Структура("Начало, Окончание", '39991231', '00010101');
	
	ВыбратьДанныеИЗаполнитьДиаграмму(ДиаграммаГанта, Параметры, Границы);
	
	НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы);
	
	ПоместитьВоВременноеХранилище(ДиаграммаГанта, АдресХранилища);
	
КонецПроцедуры

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений.
//  - Неопределено.
//
Функция ДобавитьКомандуСмежныеЭтапыПроизводства(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДиаграммаСмежныхЭтаповПроизводства)
		И УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор      = Метаданные.Отчеты.ДиаграммаСмежныхЭтаповПроизводства.ПолноеИмя();
		КомандаОтчет.Представление      = НСтр("ru = 'Смежные этапы';
												|en = 'Adjacent stages'");
		КомандаОтчет.ИмяФормы           = "Отчет.ДиаграммаСмежныхЭтаповПроизводства.Форма";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность           = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ДиаграммаСмежныхЭтаповПроизводства);
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыбратьДанныеИЗаполнитьДиаграмму(ДиаграммаГанта, Параметры, Границы)
	
	ДиаграммаГанта.Обновление = Ложь;
	
	Данные = СмежныеЭтапыИЗависимости(Параметры);
	
	Если Данные.Этапы.Количество() > 0 Тогда
		
		Серия = СерияПоУмолчанию(ДиаграммаГанта);
		
		ДанныеТекущегоЭтапа = Данные.Этапы.Найти(Параметры.Этап);
		ТекущаяСпецификация = ?(ЗначениеЗаполнено(ДанныеТекущегоЭтапа), ДанныеТекущегоЭтапа.Спецификация, Неопределено);
		
		Интервалы = Новый Соответствие;
		Для каждого Строка Из Данные.Этапы Цикл
			
			Границы.Начало = МИН(Границы.Начало, Строка.Начало);
			Границы.Окончание = МАКС(Границы.Окончание, Строка.Окончание);
			
			Если ТекущаяСпецификация <> Неопределено И Строка.Спецификация <> ТекущаяСпецификация Тогда
				Строка.ПредставлениеЭтапа = Строка.ПредставлениеЭтапа + " / " + Строка.СпецификацияПредставление;
			КонецЕсли;
			
			Точка = УстановитьТочку(ДиаграммаГанта, Параметры.Этап, Строка);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			Интервал = ДобавитьИнтервал(Значение, Строка);
			
			Интервалы.Вставить(Строка.Этап, Интервал);
			
		КонецЦикла;
		
		// Связи между этапами.
		Для каждого Строка Из Данные.Зависимости Цикл
			
			Интервал = Интервалы.Получить(Строка.Этап); // ИнтервалДиаграммыГанта - 
			СледующийИнтервал = Интервалы.Получить(Строка.СледующийЭтап);
			
			Если НЕ Интервал = Неопределено
				И НЕ СледующийИнтервал = Неопределено Тогда
				
				Связь = Интервал.Добавить(СледующийИнтервал);
				Связь.ТипСвязи = ТипСвязиДиаграммыГанта.КонецНачало;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	
КонецПроцедуры

// Возвращает смежные этапы и зависимости
//
// Параметры:
//  Параметры - Структура - Параметры:
// * Этап - СправочникСсылка.ЭтапыПроизводства, ДокументСсылка.ЭтапПроизводства2_2 - этап.
// * СтатусГрафика - Число - статус графика производства.
// * ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - 
// * КлючПартия - УникальныйИдентификатор -  
//
// Возвращаемое значение:
//  Структура - содержит:
//   * Этапы - ТаблицаЗначений - содержит:
//   * Зависимости - ТаблицаЗначений - 
//
Функция СмежныеЭтапыИЗависимости(Параметры)
	
	ПустойКлючСвязи = Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000");
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Этап", Параметры.Этап);
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", Параметры.ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", Параметры.СтатусГрафика);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", ПустойКлючСвязи);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик());
	
	КлючПартия = Неопределено;
	Если ЗначениеЗаполнено(Параметры.КлючПартия) Тогда
		КлючПартия = Параметры.КлючПартия;
	Иначе
		РеквизитыЭтапа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Параметры.Этап, "ПартияПроизводства, ДинамическаяСтруктура");
		КлючПартия = ?(РеквизитыЭтапа.ДинамическаяСтруктура,
			РеквизитыЭтапа.ПартияПроизводства.УникальныйИдентификатор(),
			ПустойКлючСвязи);
	КонецЕсли;
	Запрос.УстановитьПараметр("КлючПартия", КлючПартия);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&Этап       КАК Этап,
		|	&КлючПартия КАК КлючПартия
		|ПОМЕСТИТЬ ДанныеЭтапа";
	
	Запрос.Выполнить();
	
	УправлениеПроизводством.СоздатьВТСвязиЭтапов(МенеджерВременныхТаблиц, "ДанныеЭтапа", "Этап", "КлючПартия", Истина);
	
	ТекстыЗапросовПакета = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТСвязиЭтапов.Этап КАК Этап,
	|	ВТСвязиЭтапов.КлючПартия КАК КлючПартия
	|ПОМЕСТИТЬ ВТЭтапы
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТСвязиЭтапов.СледующийЭтап,
	|	ВТСвязиЭтапов.СледующийЭтапКлючПартия
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Если Параметры.СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.Этап КАК Этап,
		|	Т.Начало КАК Начало,
		|	Т.Окончание КАК Окончание,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Т.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА &ПредставлениеЭтапа
		|		ИНАЧЕ Т.Этап.Наименование
		|	КОНЕЦ КАК ПредставлениеЭтапа,
		|	ВЫБОР
		|		КОГДА Т.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Завершен,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Т.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА Т.Этап.РучноеРазмещениеВГрафике
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РучноеРазмещение,
		|	Т.КлючПартия КАК КлючПартия,
		|	Т.Спецификация КАК Спецификация,
		|	Т.Спецификация.Наименование КАК СпецификацияПредставление
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.Этап = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
		|ГДЕ
		|	(ТИПЗНАЧЕНИЯ(&Этап) = ТИП(Документ.ЭтапПроизводства2_2) ИЛИ Т.ЗаказНаПроизводство = &ЗаказНаПроизводство)
		|	И (Т.Этап, Т.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ВТЭтапы.Этап,
		|				ВТЭтапы.КлючПартия
		|			ИЗ
		|				ВТЭтапы)
		|	И Т.СтатусГрафика = &СтатусГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.Порядок)";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап,
		|	ГрафикЭтаповПроизводства2_2.Начало КАК Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание КАК Окончание,
		|	ГрафикЭтаповПроизводства2_2.Порядок КАК Порядок,
		|	ГрафикЭтаповПроизводства2_2.КлючПартия КАК КлючПартия,
		|	ГрафикЭтаповПроизводства2_2.Спецификация КАК Спецификация,
		|	ГрафикЭтаповПроизводства2_2.Спецификация.Наименование КАК СпецификацияПредставление
		|ПОМЕСТИТЬ ВТГрафикПоСтатусу
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	(ТИПЗНАЧЕНИЯ(&Этап) = ТИП(Документ.ЭтапПроизводства2_2) ИЛИ ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство)
		|	И (ГрафикЭтаповПроизводства2_2.Этап, ГрафикЭтаповПроизводства2_2.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ВТЭтапы.Этап,
		|				ВТЭтапы.КлючПартия
		|			ИЗ
		|				ВТЭтапы)
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Этап КАК Этап,
		|	Т.Начало КАК Начало,
		|	Т.Окончание КАК Окончание,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(Т.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА &ПредставлениеЭтапа
		|		ИНАЧЕ Т.Этап.Наименование
		|	КОНЕЦ КАК ПредставлениеЭтапа,
		|	Т.Завершен КАК Завершен,
		|	Т.РучноеРазмещение КАК РучноеРазмещение,
		|	Т.КлючПартия КАК КлючПартия,
		|	Т.Спецификация КАК Спецификация,
		|	Т.СпецификацияПредставление КАК СпецификацияПредставление
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТГрафикПоСтатусу.Этап КАК Этап,
		|		ВТГрафикПоСтатусу.Начало КАК Начало,
		|		ВТГрафикПоСтатусу.Окончание КАК Окончание,
		|		ВЫБОР
		|			КОГДА ВТГрафикПоСтатусу.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК Завершен,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ВТГрафикПоСтатусу.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|				ТОГДА ВТГрафикПоСтатусу.Этап.РучноеРазмещениеВГрафике
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК РучноеРазмещение,
		|		ВТГрафикПоСтатусу.Порядок КАК Порядок,
		|		ВТГрафикПоСтатусу.КлючПартия КАК КлючПартия,
		|		ВТГрафикПоСтатусу.Спецификация КАК Спецификация,
		|		ВТГрафикПоСтатусу.СпецификацияПредставление КАК СпецификацияПредставление
		|	ИЗ
		|		ВТГрафикПоСтатусу КАК ВТГрафикПоСтатусу
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГрафикЭтаповПроизводства2_2.Этап,
		|		ГрафикЭтаповПроизводства2_2.Начало,
		|		ГрафикЭтаповПроизводства2_2.Окончание,
		|		ВЫБОР
		|			КОГДА ГрафикЭтаповПроизводства2_2.Этап.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ВЫБОР
		|			КОГДА ТИПЗНАЧЕНИЯ(ГрафикЭтаповПроизводства2_2.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|				ТОГДА ГрафикЭтаповПроизводства2_2.Этап.РучноеРазмещениеВГрафике
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ,
		|		ГрафикЭтаповПроизводства2_2.Порядок КАК Порядок,
		|		ГрафикЭтаповПроизводства2_2.КлючПартия КАК КлючПартия,
		|		ГрафикЭтаповПроизводства2_2.Спецификация КАК Спецификация,
		|		ГрафикЭтаповПроизводства2_2.Спецификация.Наименование КАК СпецификацияПредставление
		|	ИЗ
		|		РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|	ГДЕ
		|		(ТИПЗНАЧЕНИЯ(&Этап) = ТИП(Документ.ЭтапПроизводства2_2) ИЛИ ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство)
		|		И (ГрафикЭтаповПроизводства2_2.Этап, ГрафикЭтаповПроизводства2_2.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ВТЭтапы.Этап,
		|				ВТЭтапы.КлючПартия
		|			ИЗ
		|				ВТЭтапы)
		|		И НЕ ГрафикЭтаповПроизводства2_2.Этап В
		|					(ВЫБРАТЬ
		|						ВТГрафикПоСтатусу.Этап
		|					ИЗ
		|						ВТГрафикПоСтатусу)
		|		И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик) КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
		|		ПО Т.Этап = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоЗапуска, -Т.Порядок)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа("Т.Этап"));
	
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТСвязиЭтапов.Этап КАК Этап,
	|	ВТСвязиЭтапов.СледующийЭтап
	|ИЗ
	|	ВТСвязиЭтапов КАК ВТСвязиЭтапов";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Разделитель =
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
	Запрос.Текст = ТекстЗапроса;
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	МаксИндекс = МассивРезультатов.ВГраница();
	
	Результат = Новый Структура;
	Результат.Вставить("Этапы", МассивРезультатов[МаксИндекс-1].Выгрузить());
	Результат.Вставить("Зависимости", МассивРезультатов[МаксИндекс].Выгрузить());
	
	Возврат Результат;
	
КонецФункции

Функция СерияПоУмолчанию(ДиаграммаГанта)
	
	Возврат ДиаграммаГанта.УстановитьСерию("ГрафикПроизводства");
	
КонецФункции

Функция УстановитьТочку(ДиаграммаГанта, Этап, Данные)
	
	Результат = ДиаграммаГанта.УстановитьТочку(Данные.Этап);
	Результат.Текст = Данные.ПредставлениеЭтапа;
	Результат.Расшифровка = Новый Структура("Этап, КлючПартия", Данные.Этап, Данные.КлючПартия);
	
	Если Данные.Этап = Этап Тогда
		Результат.Шрифт = Новый Шрифт(,, Истина);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьИнтервал(Значение, Данные)
	
	Результат = Значение.Добавить();
	Результат.Начало = Данные.Начало;
	Результат.Конец = Данные.Окончание;
	Результат.Текст = ТекстИнтервалаДиаграммы(
		Данные.Начало, Данные.Окончание);
	Результат.Расшифровка = Новый Структура("Этап, КлючПартия", Данные.Этап, Данные.КлючПартия);
	Результат.Цвет = ЦветИнтервала(Данные.Завершен, Данные.РучноеРазмещение);
	
	Возврат Результат;
	
КонецФункции

Процедура НастроитьОбщиеСвойстваДиаграммы(ДиаграммаГанта)
	
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	ДиаграммаГанта.Окантовка = Ложь;
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДиаграммаГанта.ВертикальнаяПрокрутка = Истина;
	ДиаграммаГанта.ОтображатьПустыеЗначения = Ложь;
	ДиаграммаГанта.ОтображатьЗаголовок = Ложь;
	ДиаграммаГанта.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОтображениеТекстаИнтервала = ОтображениеТекстаИнтервалаДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОбластьПостроения.Право = 1;
	
КонецПроцедуры

Процедура НастроитьШкалуВремениДиаграммы(ДиаграммаГанта, Границы)
	
	Если НЕ ЗначениеЗаполнено(Границы.Окончание) Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиШкалы = УправлениеПроизводствомКлиентСервер.НастройкиШкалыДиаграммыГантаВРежимеВсеДанные(
		Границы.Начало, Границы.Окончание, 9);
	
	ШкалаВремениЭлементы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	
	Для Индекс = 1 По ШкалаВремениЭлементы.Количество()-1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[Индекс]);
	КонецЦикла;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = НастройкиШкалы.Единица;
	ЭлементШкалы.Формат = НастройкиШкалы.Формат;
	
	Если ШкалаВремениЭлементы.Количество() = 2 Тогда
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[0]);
	КонецЕсли;
	
	ДиаграммаГанта.УстановитьПолныйИнтервал(
		НастройкиШкалы.НачалоПолногоИнтервала,
		НастройкиШкалы.ОкончаниеПолногоИнтервала);
	
	// Выделение выходных 
	
	ДиаграммаГанта.ИнтервалыФона.Очистить();
	
	Графики = Новый Массив();
	Графики.Добавить(Константы.ОсновнойКалендарьПредприятия.Получить());
	
	РасписанияРаботы = ПланированиеПроизводства.РасписаниеРаботыПоГрафику(Графики, НастройкиШкалы.НачалоПолногоИнтервала, НастройкиШкалы.ОкончаниеПолногоИнтервала);
	РасписанияРаботы.Свернуть("ДатаГрафика");
	РасписанияРаботы.Индексы.Добавить("ДатаГрафика");
	
	ПериодыНедоступности = Новый ТаблицаЗначений();
	ПериодыНедоступности.Колонки.Добавить("Начало",    Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ПериодыНедоступности.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	
	ТекущийПериод = НастройкиШкалы.НачалоПолногоИнтервала;
	Пока ТекущийПериод <= НастройкиШкалы.ОкончаниеПолногоИнтервала Цикл
		
		Если РасписанияРаботы.Найти(ТекущийПериод, "ДатаГрафика") = Неопределено Тогда
			НовыйПериод = ПериодыНедоступности.Добавить();
			НовыйПериод.Начало = ТекущийПериод;
			НовыйПериод.Окончание = КонецДня(НовыйПериод.Начало);
		КонецЕсли;
		
		ТекущийПериод = ТекущийПериод + 86400;
	КонецЦикла;
	
	Для каждого ДанныеНедоступности Из ПериодыНедоступности Цикл
		
		ДиаграммаГанта.ИнтервалыФона.Добавить(ДанныеНедоступности.Начало, ДанныеНедоступности.Окончание);
		
	КонецЦикла;
	
КонецПроцедуры

Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

Функция ТекстИнтервалаДиаграммы(Начало, Окончание)
	
	ФорматнаяСтрока = УправлениеПроизводством.ФорматнаяСтрокаДляДатыГрафикаПроизводства();
	ФорматНачало = Формат(Начало, ФорматнаяСтрока);
	ФорматОкончание = Формат(Окончание, ФорматнаяСтрока);
	
	Если ФорматНачало = ФорматОкончание Тогда
		Результат = ФорматНачало;
	Иначе
		Результат = ФорматНачало + " - " + ФорматОкончание;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ЦветИнтервала(ЭтапЗавершен, РучноеРазмещение)
	
	Если ЭтапЗавершен Тогда
		Результат = ЦветаСтиля.ЦветФонаЭтапЗавершен;
	Иначе
		Если НЕ РучноеРазмещение Тогда
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированАвтоматически;
		Иначе
			Результат = ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированВручную;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли