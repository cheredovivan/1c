#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ТипЗнч(Параметры.ДанныеДляРедактирования) = Тип("ХранилищеЗначения") Тогда 
		ЗагрузкаДанных(Параметры.ДанныеДляРедактирования);
	КонецЕсли;
	
	ОтчетностьМайнеров.СправочникиМайнеровВТаблицыНаФорме(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПрОтсВотчерПриИзменении(Элемент)
	СсылкаНаПул = "";
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаПулПриИзменении(Элемент)
	ПрОтсВотчер = ?(ЗначениеЗаполнено(СсылкаНаПул), Ложь, ПрОтсВотчер);
КонецПроцедуры

&НаКлиенте
Процедура СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОчиститьСообщения();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвДобытВалют

&НаКлиенте
Процедура СвДобытВалютВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "СвДобытВалютОписание" Тогда 
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СозданиеНового", Ложь);
		ПараметрыФормы.Вставить("ДанныеДляРедактирования", Элемент.ТекущиеДанные.Данные);
		ПараметрыФормы.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
		
		ОткрытьФорму(СтрЗаменить(ИмяФормы, "ФормаСвМайнинг", "ФормаСвДобытВалют"), ПараметрыФормы, ЭтотОбъект,,,,
			Новый ОписаниеОповещения("РедактированиеСвДобытВалютЗавершение", ЭтотОбъект, ПараметрыФормы), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СвДобытВалютПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	ДобавитьВалюты(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СвДобытВалютПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	УдалитьВалюты(Неопределено);
КонецПроцедуры

&НаКлиенте
Процедура СвДобытВалютОписаниеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвОборудПроизв

&НаКлиенте
Процедура СвОборудПроизвПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	УдалениеОборудования(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СвОборудПроизвАлгоритмНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ВыборИзСписка("Алгоритмы", "Алгоритм", Элемент, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвОборудСправоч

&НаКлиенте
Процедура СвОборудСправочПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	УдалениеОборудования(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СвОборудСправочКодОборудНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ВыборИзСписка("Оборудование", "КодОборуд", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СвОборудСправочПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Если Не Копирование Тогда 
		Отказ = Истина;
		Элементы.СвОборудСправоч.ТекущаяСтрока = СвОборудСправоч.Добавить().ПолучитьИдентификатор();
		ВыборИзСписка("Оборудование", "КодОборуд", Элементы.СвОборудСправочКодОборуд, Ложь);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьВалюты(Команда)
	Если СвДобытВалют.Количество() >= 500 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Допускается не более 500 строк со сведениями");
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("СозданиеНового", Истина);
		ПараметрыФормы.Вставить("ДанныеДляРедактирования", Неопределено);
		ПараметрыФормы.Вставить("ВыбраннаяСтрока", Неопределено);
		
		ОткрытьФорму(СтрЗаменить(ИмяФормы, "ФормаСвМайнинг", "ФормаСвДобытВалют"), ПараметрыФормы, ЭтотОбъект,,,,
			Новый ОписаниеОповещения("РедактированиеСвДобытВалютЗавершение", ЭтотОбъект, ПараметрыФормы), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьВалюты(Команда)
	Если Элементы.СвДобытВалют.ТекущиеДанные <> Неопределено И
		КодВозвратаДиалога.Да = Ждать ВопросАсинх("Удалить сведения?", РежимДиалогаВопрос.ДаНетОтмена) Тогда 
		
		СвДобытВалют.Удалить(Элементы.СвДобытВалют.ТекущиеДанные);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Проверить(Команда)
	ОчиститьСообщения();
	Если СвОборудСправоч.Количество() + СвОборудПроизв.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнены сведения об оборудовании", , "СвОборудСправоч");
		Возврат;
	КонецЕсли;
	Если СвДобытВалют.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнены сведения о валютах", , "СвДобытВалют");
		Возврат;
	КонецЕсли;
	Если Тарифы.Количество() = 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнены сведения о тарифах и объеме энергии", , "Тарифы");
		Возврат;
	КонецЕсли;
	Если Не ПрОтсВотчер И Не ЗначениеЗаполнено(СсылкаНаПул) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнена watcher-link ссылка", , "СсылкаНаПул");
		Возврат;
	КонецЕсли;
	
	Для Каждого Стр Из СвОборудСправоч Цикл 
		Если СтрДлина(СокрЛП(Стр.КодОборуд)) <> 5 Тогда
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудСправоч", СвОборудСправоч.Индекс(Стр) + 1, "КодВалют");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Код оборудования должен содержать 5 символов", , ПутьКПолю);
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.ЗаводНом) Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудСправоч", СвОборудСправоч.Индекс(Стр) + 1, "ЗаводНом");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан заводской номер", , ПутьКПолю);
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.КоличЧасЭкспл) Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудСправоч", СвОборудСправоч.Индекс(Стр) + 1, "КоличЧасЭкспл");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не указано количество часов", , ПутьКПолю);
			Возврат;
		ИначеЕсли Стр.КоличЧасЭкспл > 2208 Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудСправоч", СвОборудСправоч.Индекс(Стр) + 1, "КоличЧасЭкспл");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Количество часов эксплуатации за месяц не более 2208", , ПутьКПолю);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из СвОборудПроизв Цикл
		Если СтрДлина(СокрЛП(Стр.Алгоритм)) <> 4 Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудПроизв", СвОборудПроизв.Индекс(Стр) + 1, "Алгоритм");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Код алгоритма должен содержать 4 символа", , ПутьКПолю);
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.Марка) Или Не ЗначениеЗаполнено(Стр.Модель) Или Не ЗначениеЗаполнено(Стр.ВычислитМощн)
			 Или Не ЗначениеЗаполнено(Стр.КоличЭнерг) Или Не ЗначениеЗаполнено(Стр.ЗаводНом) Или Не ЗначениеЗаполнено(Стр.КоличЧасЭкспл) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнены обязательные реквизиты (подчеркнуты красным)", , "СвОборудПроизв");
			Элементы.Страницы.ТекущаяСтраница = Элементы.ОборудПроизв;
			Элементы.СвОборудПроизв.ТекущаяСтрока = Стр.ПолучитьИдентификатор();
			Возврат;
		ИначеЕсли Стр.КоличЧасЭкспл > 2208 Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("СвОборудПроизв", СвОборудПроизв.Индекс(Стр) + 1, "КоличЧасЭкспл");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Количество часов эксплуатации за месяц не более 2208", , ПутьКПолю);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	Для Каждого Стр Из Тарифы Цикл
		Если Не ЗначениеЗаполнено(Стр.Тариф) Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Тарифы", Тарифы.Индекс(Стр) + 1, "Тариф");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен тариф", , ПутьКПолю);
			Возврат;
		ИначеЕсли Не ЗначениеЗаполнено(Стр.ОбъемЭнерг) Тогда 
			ПутьКПолю = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Тарифы", Тарифы.Индекс(Стр) + 1, "ОбъемЭнерг");
			ОбщегоНазначенияКлиент.СообщитьПользователю("Не заполнен объем потребленной энергии", , ПутьКПолю);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибок не обнаружено");
КонецПроцедуры

&НаКлиенте
Процедура Ок(Команда)
	Если Не ЗаполненоКорректно() Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Заполнение некорректно, выполните проверку заполнения");
	Иначе
		Закрыть(ОписаниеРезультата());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьСвОборудСправоч(Команда)
	Если ЗначениеЗаполнено(СвОборудСправоч.Количество()) 
		И КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить сведения об оборудовании?", РежимДиалогаВопрос.ДаНетОтмена) Тогда
		СвОборудСправоч.Очистить();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОчиститьСвОборудПроизв(Команда)
	Если ЗначениеЗаполнено(СвОборудПроизв.Количество()) 
		И КодВозвратаДиалога.Да = Ждать ВопросАсинх("Очистить сведения об оборудовании?", РежимДиалогаВопрос.ДаНетОтмена) Тогда
		СвОборудПроизв.Очистить();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузкаДанных(ДанныеДляРедактирования)
	ОтчетностьМайнеров.JSONВРеквизитыФормы(ЭтотОбъект, ДанныеДляРедактирования.Получить());
	Для Каждого Стр Из СвДобытВалют Цикл 
		Стр.Данные = Новый ХранилищеЗначения(ПолучитьСтрокуИзДвоичныхДанных(Base64Значение(Стр.Данные)));
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура РедактированиеСвДобытВалютЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		СтрДляРедактирования = ?(ДополнительныеПараметры.СозданиеНового, 
			СвДобытВалют.Добавить(), СвДобытВалют.НайтиПоИдентификатору(ДополнительныеПараметры.ВыбраннаяСтрока));
		ЗаполнитьЗначенияСвойств(СтрДляРедактирования, Результат);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ЗаполненоКорректно()
	Если (СвОборудСправоч.Количество() + СвОборудПроизв.Количество() = 0)
		Или (Не ПрОтсВотчер И Не ЗначениеЗаполнено(СсылкаНаПул)) Или (СвДобытВалют.Количество() = 0) Или (Тарифы.Количество() = 0) Тогда 
		Возврат Ложь;
	КонецЕсли;
	
	Для Каждого Стр Из СвОборудСправоч Цикл 
		Если СтрДлина(СокрЛП(Стр.КодОборуд)) <> 5 Или Не ЗначениеЗаполнено(Стр.ЗаводНом) 
			Или Не ЗначениеЗаполнено(Стр.КоличЧасЭкспл) Или Стр.КоличЧасЭкспл > 2208 Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из СвОборудПроизв Цикл
		Если Не ЗначениеЗаполнено(Стр.Марка) Или Не ЗначениеЗаполнено(Стр.Модель) Или Не ЗначениеЗаполнено(Стр.ВычислитМощн)
			Или Не ЗначениеЗаполнено(Стр.КоличЭнерг) Или Не ЗначениеЗаполнено(Стр.ЗаводНом) Или Не ЗначениеЗаполнено(Стр.КоличЧасЭкспл) 
			Или СтрДлина(СокрЛП(Стр.Алгоритм)) <> 4 Или Стр.КоличЧасЭкспл > 2208 Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Стр Из Тарифы Цикл
		Если Не ЗначениеЗаполнено(Стр.Тариф) Или Не ЗначениеЗаполнено(Стр.ОбъемЭнерг) Тогда 
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
КонецФункции

&НаСервере
Функция ОписаниеРезультата()
	РезультатЗакрытия = Новый Структура("Описание, Данные", "", Неопределено);
	Для Каждого Стр Из СвДобытВалют Цикл 
		Стр.Данные = Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(Стр.Данные.Получить()));
	КонецЦикла;
	
	РезультатЗакрытия.Описание = "Информация о " + (СвОборудСправоч.Количество() + СвОборудПроизв.Количество()) + " единицах оброрудования";
	
	ОписаниеJSON = СвМайнингУведомлениеМайнераВJSON();
	РезультатЗакрытия.Данные = Новый ХранилищеЗначения(ОписаниеJSON, Новый СжатиеДанных(9));
	Возврат РезультатЗакрытия;
КонецФункции

&НаСервере
Функция СвМайнингУведомлениеМайнераВJSON()
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписьJSON.ЗаписатьНачалоОбъекта();
	
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "version", "4.02");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "Форма", "ФормаСвМайнинг");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьИмяЗначениеСвойства(ЗаписьJSON, "schema", "");
	УведомлениеОСпецрежимахНалогообложенияСлужебный.ЗаписатьПакетПоказателей(Новый Структура("ПрОтсВотчер, СсылкаНаПул", ПрОтсВотчер, СсылкаНаПул), ЗаписьJSON);
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "СвДобытВалют");
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "СвОборудПроизв");
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "СвОборудСправоч");
	ОтчетностьМайнеров.ЗаписатьТаблицуПоказателей(ЭтотОбъект, ЗаписьJSON, "Тарифы");
	
	ЗаписьJSON.ЗаписатьКонецОбъекта();
	Результат = ЗаписьJSON.Закрыть();
	Возврат Результат;
КонецФункции

&НаКлиенте
Асинх Процедура УдалениеОборудования(Элемент)
	Если КодВозвратаДиалога.Да = Ждать ВопросАсинх("Удалить оборудование?", РежимДиалогаВопрос.ДаНетОтмена) Тогда 
		Для Каждого Элт Из ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Элементы[Элемент.Имя].ВыделенныеСтроки) Цикл 
			ЭтотОбъект[Элемент.Имя].Удалить(ЭтотОбъект[Элемент.Имя].НайтиПоИдентификатору(Элт));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСписка(ИмяСписка, ИмяКолонки, Элемент, СтандартнаяОбработка) Экспорт 
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Выбор кода");
	ПараметрыФормы.Вставить("ТаблицаЗначений", ЭтотОбъект[ИмяСписка]);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", Элемент.Родитель.ТекущиеДанные[ИмяКолонки]));
	
	ДополнительныеПараметры = Новый Структура("Форма, Элемент, ИмяКолонки", ЭтотОбъект, Элемент, ИмяКолонки);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборИзСпискаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", 
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ВыборИзСпискаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ДополнительныеПараметры.Элемент.Родитель.ТекущиеДанные[ДополнительныеПараметры.ИмяКолонки] = РезультатВыбора.Код;
КонецПроцедуры

#КонецОбласти
