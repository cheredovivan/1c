#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
  
Функция ДанныеВыгрузкиИзЗаполненногоМакетаШаблона(Форма, ТекстВыгрузки, ПараметрыВыгрузки) Экспорт
	
	ИмяВременнойПапкиZIP = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПолучитьИмяВременногоФайла());
	СоздатьКаталог(ИмяВременнойПапкиZIP);
	
	ИмяВременнойПапкиXLSM = ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ПолучитьИмяВременногоФайла());
	СоздатьКаталог(ИмяВременнойПапкиXLSM);
	
	// Распаковка архива с шаблоном и создание распакованного XLSM-файла.
	ИмяФайлаАрхива = ИмяВременнойПапкиZIP + "archive.zip";
	
	АрхивыШаблона = Форма.ОбъектОтчета(Форма.ИмяФормы).ПолучитьМакет(ПараметрыВыгрузки.ИмяМакетаШаблона);
	АрхивыШаблона.Записать(ИмяФайлаАрхива);
	
	ЧтениеZip = Новый ЧтениеZipФайла(ИмяФайлаАрхива);
	
	ИмяФайлаВыгрузки = ИмяВременнойПапкиZIP + ПараметрыВыгрузки.ИдФайл + ".xlsm";
	
	ИмяАрхиваШаблона = Строка(ПараметрыВыгрузки.СрокПредМП) + ".zip";
	ИмяФайлаСценария = ПараметрыВыгрузки.ИмяФайлаСценария;
	
	ЭлементАрхиваШаблона = ЧтениеZip.Элементы.Найти(ИмяАрхиваШаблона);
	ЭлементФайлаСценария = ЧтениеZip.Элементы.Найти(ИмяФайлаСценария);
	
	Если (ЭлементАрхиваШаблона = Неопределено ИЛИ ЭлементАрхиваШаблона.Путь <> "")
	 ИЛИ (ЭлементФайлаСценария = Неопределено ИЛИ ЭлементФайлаСценария.Путь <> "") Тогда
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Оценочная карта угрозы банкротства';
										|en = 'Оценочная карта угрозы банкротства'"),
			УровеньЖурналаРегистрации.Ошибка,,, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение НСтр("ru = 'Архив с шаблонами оценочных карт не полный.';
								|en = 'Архив с шаблонами оценочных карт не полный.'");
	КонецЕсли;
	
	ИмяФайлаШаблона = ИмяВременнойПапкиZIP + ИмяАрхиваШаблона;
	
	ЧтениеZip.Извлечь(ЭлементАрхиваШаблона, ИмяВременнойПапкиZIP, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	ЧтениеZip.Извлечь(ЭлементФайлаСценария, ИмяВременнойПапкиZIP, РежимВосстановленияПутейФайловZIP.НеВосстанавливать);
	ЧтениеZip.Закрыть();
	
	ЧтениеZip = Новый ЧтениеZipФайла(ИмяФайлаШаблона); // распаковка содержимого архива шаблона
	ЧтениеZip.ИзвлечьВсе(ИмяВременнойПапкиXLSM, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	ЧтениеZip.Закрыть();
	
	ПереместитьФайл(ИмяВременнойПапкиZIP + ИмяФайлаСценария,
		ИмяВременнойПапкиXLSM + ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути("xl") + ИмяФайлаСценария);
	
	УдалитьФайлы(ИмяФайлаАрхива);
	УдалитьФайлы(ИмяФайлаШаблона);
	
	// Подготовка распакованных файлов шаблона к обработке.
	ТЗОбработки = Новый ТаблицаЗначений;
	ТЗОбработки.Колонки.Добавить("ИД",          Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("Каталог",     Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("ИмяФайла",    Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("ТипФайла",    Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("ТипФайлаXML", Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("ЭтоКнига",    Новый ОписаниеТипов("Булево"));
	ТЗОбработки.Колонки.Добавить("ЭтоЛист",     Новый ОписаниеТипов("Булево"));
	ТЗОбработки.Колонки.Добавить("ИмяЛиста",    Новый ОписаниеТипов("Строка"));
	ТЗОбработки.Колонки.Добавить("ИДЛиста",     Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(3,0)));
	ТЗОбработки.Колонки.Добавить("Статус",      Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(1,0)));
	ТЗОбработки.Колонки.Добавить("СвязьСтрока");
	ТЗОбработки.Колонки.Добавить("Документ");
	ТЗОбработки.Колонки.Добавить("Свойства");
	ТЗОбработки.Колонки.Добавить("КэшЭлем",     Новый ОписаниеТипов("Структура"));
	
	НайденныеФайлы = НайтиФайлы(ИмяВременнойПапкиXLSM, "*", Истина);
	Для Каждого Файл Из НайденныеФайлы Цикл
		Если НЕ Файл.Существует() ИЛИ Файл.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;
		
		Файл.УстановитьВремяИзменения('19800101000000'); // сразу установим минимальную дату изменения файла
		
		Если Файл.Расширение <> ".xml" И Файл.Расширение <> ".vml" И Файл.Расширение <> ".rels" Тогда
			Продолжить;
		КонецЕсли;
		
		НовСтрока = ТЗОбработки.Добавить();
		НовСтрока.Каталог  = Файл.Путь;
		НовСтрока.ИмяФайла = Файл.Имя;
		Если Файл.Расширение = ".rels" Тогда
			НовСтрока.ТипФайла = "Relationships";
		ИначеЕсли Файл.ИмяБезРасширения = "[Content_Types]" Тогда
			НовСтрока.ТипФайла = "Types";
		Иначе
			Продолжить;
		КонецЕсли;
		
		НовСтрока.Документ = РегламентированнаяОтчетностьПреобразованиеОФД.ДокументDOMИзФайлаXML(Файл.ПолноеИмя, НовСтрока.Свойства);
	КонецЦикла;
	
	ТЗОбработки.Сортировать("Каталог,ИмяФайла");
	
	ФайлРабочаяКнигаСтрока = Неопределено;
	СписокЛистовКорень     = Неопределено;
	СписокЛистовЭлементы   = Неопределено;
	
	ФайлыСвязейСтроки = ТЗОбработки.НайтиСтроки(Новый Структура("ТипФайла", "Relationships"));
	Для Каждого ФайлСвязейСтрока Из ФайлыСвязейСтроки Цикл
		
		СвязиИзДокумента = ФайлСвязейСтрока.Документ.ПолучитьЭлементыПоИмени("Relationship");
		Для Каждого СвязьИзДокумента Из СвязиИзДокумента Цикл
			
			ИД = СвязьИзДокумента.ПолучитьАтрибут("Id");
			ТипФайлаXML = СвязьИзДокумента.ПолучитьАтрибут("Type");
			ОтносительныйПутьКФайлу = СвязьИзДокумента.ПолучитьАтрибут("Target");
			
			Файл = Новый Файл(ФайлСвязейСтрока.Каталог + ".." + ПолучитьРазделительПути()
				+ СокрЛП(СтрЗаменить(ОтносительныйПутьКФайлу, "/", ПолучитьРазделительПути())));
			Если Файл.Существует() Тогда
				ФайлыПоСсылкеСтроки = ТЗОбработки.НайтиСтроки(Новый Структура("Каталог,ИмяФайла", Файл.Путь, Файл.Имя));
				Если ФайлыПоСсылкеСтроки.Количество() > 0 Тогда
					ФайлПоСсылкеСтрока = ФайлыПоСсылкеСтроки[0];
					
					ФайлПоСсылкеСтрока.ИД = ИД;
					ТипФайлаXMLСостав = СтрРазделить(ТипФайлаXML, "/");
					ФайлПоСсылкеСтрока.ТипФайла = ТипФайлаXMLСостав[ТипФайлаXMLСостав.ВГраница()];
					ФайлПоСсылкеСтрока.ТипФайлаXML = ТипФайлаXML;
					ФайлПоСсылкеСтрока.СвязьСтрока = ФайлСвязейСтрока;
					
					Если ФайлПоСсылкеСтрока.ТипФайла = "officeDocument" Тогда
						
						ФайлПоСсылкеСтрока.ЭтоКнига = Истина;
						ФайлПоСсылкеСтрока.Документ = РегламентированнаяОтчетностьПреобразованиеОФД.ДокументDOMИзФайлаXML(
							Файл.ПолноеИмя, ФайлПоСсылкеСтрока.Свойства);
						
						ФайлРабочаяКнигаСтрока = ФайлПоСсылкеСтрока; // файл этого типа единственный в шаблоне
						СписокЛистовКорень = ФайлРабочаяКнигаСтрока.Документ.ПолучитьЭлементыПоИмени("sheets")[0];
						СписокЛистовЭлементы = СписокЛистовКорень.ПолучитьЭлементыПоИмени("sheet");
						
						Продолжить;
						
					ИначеЕсли ФайлПоСсылкеСтрока.ТипФайла = "worksheet" Тогда
						
						ФайлПоСсылкеСтрока.ЭтоЛист = Истина;
						
						Если СписокЛистовЭлементы <> Неопределено И СписокЛистовЭлементы.Количество() > 0 Тогда
							Разыменователь = ФайлРабочаяКнигаСтрока.Документ.СоздатьРазыменовательПИ();
							РезультатXPath = ФайлРабочаяКнигаСтрока.Документ.ВычислитьВыражениеXPath("xmlns:sheet[@r:id='" + ИД + "']",
								СписокЛистовКорень, Разыменователь, ТипРезультатаDOMXPath.ПервыйУпорядоченныйУзел);
							Если ТипЗнч(РезультатXPath.ОдиночныйУзелЗначение) = Тип("ЭлементDOM") Тогда
								ФайлПоСсылкеСтрока.ИмяЛиста = СокрЛП(РезультатXPath.ОдиночныйУзелЗначение.ПолучитьАтрибут("name"));
								ФайлПоСсылкеСтрока.ИДЛиста = Число(" " + РезультатXPath.ОдиночныйУзелЗначение.ПолучитьАтрибут("sheetId"));
							КонецЕсли;
						КонецЕсли;
						
					КонецЕсли;
					
					ФайлПоСсылкеСтрока.Документ = РегламентированнаяОтчетностьПреобразованиеОФД.ДокументDOMИзФайлаXML(
						Файл.ПолноеИмя, ФайлПоСсылкеСтрока.Свойства);
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	// Подготовка дополнительных параметров выгрузки для оптимизации заполнения документов.
	ДопПараметрыШаблона = Новый Структура();
	ПараметрыВыгрузки.Вставить("ДопПараметрыШаблона", ДопПараметрыШаблона);
	
	ДопПараметрыШаблона.Вставить("ДокументРабочаяКнига", ФайлРабочаяКнигаСтрока.Документ);
	
	ФайлОбщиеСтроки = ТЗОбработки.Найти("sharedStrings", "ТипФайла");
	Если ФайлОбщиеСтроки <> Неопределено Тогда
		ФайлОбщиеСтроки.Статус = 1; // всегда будет модифицирован
		ДопПараметрыШаблона.Вставить("ДокументОбщиеСтроки", ФайлОбщиеСтроки.Документ);
		ДопПараметрыШаблона.Вставить("ИндексыОбщихСтрок", РегламентированнаяОтчетностьПреобразованиеОФД.ИндексыОбщихСтрок(
			ФайлОбщиеСтроки.Документ));
	КонецЕсли;
	
	// Подготовка соответствия ссылок именованным областям.
	ИменаИСсылкиРабочейКниги = Новый СписокЗначений;
	КорневыеУзлыИменаИСсылки = ФайлРабочаяКнигаСтрока.Документ.ПолучитьЭлементыПоИмени("definedNames");
	Если КорневыеУзлыИменаИСсылки.Количество() > 0 Тогда
		УзлыИменаИСсылки = КорневыеУзлыИменаИСсылки[0].ПолучитьЭлементыПоИмени("definedName");
		Для Каждого УзелИмяИСсылка Из УзлыИменаИСсылки Цикл
			ИменаИСсылкиРабочейКниги.Добавить(УзелИмяИСсылка.ПолучитьАтрибут("name"), УзелИмяИСсылка.ТекстовоеСодержимое);
		КонецЦикла;
	КонецЕсли;
	ДопПараметрыШаблона.Вставить("ИменаИСсылкиРабочейКниги", ИменаИСсылкиРабочейКниги);
	
	// Внесение изменений в документы DOM распакованного шаблона.
	ЗаписатьДанныеВДокументыШаблона(Форма, ТекстВыгрузки, ПараметрыВыгрузки, ТЗОбработки);
	ЗаписатьДополнительнуюИнформациюВДокументыШаблона(ПолноеИмяПользователя(), ТЗОбработки);
	
	// Запись модифицированных файлов шаблона после обработки.
	Для Каждого СтрокаТаблицы Из ТЗОбработки Цикл
		Если СтрокаТаблицы.Статус > 0 Тогда
			РегламентированнаяОтчетностьПреобразованиеОФД.ЗаписатьДокументDOMВФайлXML(
				СтрокаТаблицы.Документ,
				СтрокаТаблицы.Каталог + СтрокаТаблицы.ИмяФайла,
				СтрокаТаблицы.Свойства, СтрокаТаблицы.ТипФайла);
		КонецЕсли;
	КонецЦикла;
	
	// Удаление дополнительных параметров шаблона (данные используются только на сервере).
	ПараметрыВыгрузки.Удалить("ДопПараметрыШаблона");
	
	// Упаковка измененных файлов в файл шаблона.
	ЗаписьZip = Новый ЗаписьZipФайла(ИмяФайлаВыгрузки, , , МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Оптимальный);
	ЗаписьZip.Добавить(ИмяВременнойПапкиXLSM + "*.*",
		РежимСохраненияПутейZIP.СохранятьОтносительныеПути, РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	ЗаписьZip.Записать();
	
	АдресШаблонаВоВремХранилище = ПоместитьВоВременноеХранилище(
		Новый ДвоичныеДанные(ИмяФайлаВыгрузки), Новый УникальныйИдентификатор);
	
	// Удаляются временные папки со всеми созданными файлами.
	УдалитьФайлы(ИмяВременнойПапкиXLSM);
	УдалитьФайлы(ИмяВременнойПапкиZIP);
	
	ДанныеВыгрузкиШаблона = Новый Структура;
	ДанныеВыгрузкиШаблона.Вставить("ИмяФайлаВыгрузки", ПараметрыВыгрузки.ИдФайл + ".xlsm");
	ДанныеВыгрузкиШаблона.Вставить("АдресФайлаВыгрузки", АдресШаблонаВоВремХранилище);
	ДанныеВыгрузкиШаблона.Вставить("ТипФайлаВыгрузки", "ОтчетФНС_ФМ");
	
	Возврат ДанныеВыгрузкиШаблона;
	
КонецФункции

Процедура ЗаписатьДанныеВДокументыШаблона(Форма, ТекстВыгрузки, ПараметрыВыгрузки, ТЗОбработки)
	
	// Раздел 1.
	ДанныеРаздел1 = Форма.мДанныеОтчета.Раздел1;
	
	ПолнаяСсылка = Новый Структура("ИмяЛиста,Ссылка", "Оценочная карта");
	
	Для Каждого ИмяИСсылка Из ПараметрыВыгрузки.Раздел1_Ссылки Цикл
		
		ЗначениеПоказателя = ДанныеРаздел1[ИмяИСсылка.Значение];
		
		Если ИмяИСсылка.Пометка Тогда // требуется округлить и разделить
			ЗначениеПоказателя = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
				Форма, ЗначениеПоказателя) / 1000;
		КонецЕсли;
		
		Если ИмяИСсылка.Значение = "СтавкаРефинансирования" Тогда
			ЗначениеПоказателя = ЗначениеПоказателя / 100;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			ЗначениеПоказателя = ЧисловойЭквивалентДатыExcel(ЗначениеПоказателя);
		КонецЕсли;
		
		ПолнаяСсылка.Ссылка = ИмяИСсылка.Представление;
		
		РегламентированнаяОтчетностьПреобразованиеОФД.УстановитьЗначениеВЯчейкеЛиста
			(ПолнаяСсылка, ЗначениеПоказателя, ТЗОбработки, ПараметрыВыгрузки);
		
	КонецЦикла;
	
	// Раздел 2.
	ДанныеРаздел2 = Форма.мДанныеОтчета.Раздел2;
	
	ПолнаяСсылка = Новый Структура("ИмяЛиста,Ссылка", "Оценочная карта");
	
	Для Каждого ИмяИСсылка Из ПараметрыВыгрузки.Раздел2_Ссылки Цикл
		
		ЗначениеПоказателя = ДанныеРаздел2[ИмяИСсылка.Значение];
		
		Если ИмяИСсылка.Пометка Тогда // требуется округлить и разделить
			ЗначениеПоказателя = РегламентированнаяОтчетностьКлиентСервер.ОкруглитьЧислоПоФормату(
				Форма, ЗначениеПоказателя) / 1000;
		КонецЕсли;
		
		Если ТипЗнч(ЗначениеПоказателя) = Тип("Дата") Тогда
			ЗначениеПоказателя = ЧисловойЭквивалентДатыExcel(ЗначениеПоказателя);
		КонецЕсли;
		
		ПолнаяСсылка.Ссылка = ИмяИСсылка.Представление;
		
		РегламентированнаяОтчетностьПреобразованиеОФД.УстановитьЗначениеВЯчейкеЛиста
			(ПолнаяСсылка, ЗначениеПоказателя, ТЗОбработки, ПараметрыВыгрузки);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьДополнительнуюИнформациюВДокументыШаблона(ИмяРедактора, ТЗОбработки)
	
	ФайлОсновной = ТЗОбработки.Найти("core-properties", "ТипФайла");
	Если ФайлОсновной <> Неопределено Тогда
		ФайлОсновной.Статус = 1; // будет модифицирован
		
		УзлыИмяРедактора = ФайлОсновной.Документ.ПолучитьЭлементыПоИмени("cp:lastModifiedBy");
		Если УзлыИмяРедактора.Количество() > 0 Тогда
			УзлыИмяРедактора[0].ТекстовоеСодержимое = ИмяРедактора;
		КонецЕсли;
		УзлыВремяРедактирования = ФайлОсновной.Документ.ПолучитьЭлементыПоИмени("dcterms:modified");
		Если УзлыВремяРедактирования.Количество() > 0 Тогда
			УзлыВремяРедактирования[0].ТекстовоеСодержимое = XMLСтрока(ТекущаяУниверсальнаяДата()) + "Z";
		КонецЕсли;
	КонецЕсли;
	
	// Снимаем защиту у модифицированных листов.
	ФайлыЛистаСтроки = ТЗОбработки.НайтиСтроки(Новый Структура("ЭтоЛист,Статус", Истина, 1));
	Для Каждого ФайлЛистаСтрока Из ФайлыЛистаСтроки Цикл
		УзелЗащитыЛиста = Неопределено;
		Если ФайлЛистаСтрока.КэшЭлем.Свойство("sheetProtection", УзелЗащитыЛиста) Тогда
			УзелЗащитыЛиста.Док.РодительскийУзел.УдалитьДочерний(УзелЗащитыЛиста.Док);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ЧисловойЭквивалентДатыExcel(ЗначениеДаты)
	
	Возврат 1 + (НачалоДня(ЗначениеДаты) - '18991231') / (60 * 60 * 24);
	
КонецФункции

#КонецОбласти

#КонецЕсли