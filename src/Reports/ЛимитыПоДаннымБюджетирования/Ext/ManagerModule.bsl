#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета,
		"ИспользованияЛимитовПоДаннымБюджетирования");
	НастройкиВарианта.Описание =
		НСтр("ru = 'План-факт анализ лимитов по данным бюджетирования';
			|en = 'Variance analysis of budgeting limits'");
	
КонецПроцедуры

Функция ПредставлениеАналитикиОтчетаСоСдвигом(ПредставлениеАналитики, УровеньГруппировки) Экспорт
	Возврат СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1%2';
			|en = '%1%2'"),
		СдвигГруппировкиОтчета(УровеньГруппировки),
		ПредставлениеАналитики);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает строку пустых символов сдвига уровня группировки.
// Длина сдвига зависит от уровня группировки.
//
// Параметры:
// 	УровеньГруппировки - Число - Уровень группировки отчета.
//
// Возвращаемое значение:
// 	Строка - Строка пустых символов перед представлением значения группировки.
//
Функция СдвигГруппировкиОтчета(УровеньГруппировки)
	Возврат СтроковыеФункцииКлиентСервер.СформироватьСтрокуСимволов("  ", УровеньГруппировки);
КонецФункции

Процедура ВывестиАктуальностьОтраженияРезервовПоЛимитам(ДокументРезультат, ДопСвойства,
	ПроверятьАктивныеЗадания = Истина) Экспорт
	
	НужнаНадпись = ДопСвойства.Свойство("КоличествоДокументовКОтражениюЛимитовПоДаннымБюджетирования");
	
	Если ПроверятьАктивныеЗадания Тогда
		Ключ = СтрШаблон("УправляющийПоток_%1", "ЗаданияКОтражениюЛимитовПоДаннымБюджетирования");
		
		Отбор = Новый Структура();
		Отбор.Вставить("Ключ", Ключ);
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
		
		АктивныеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		
		Если АктивныеЗадания.Количество() = 0 Тогда
			НужнаНадпись = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НужнаНадпись Тогда
		ТаблицаПредупреждение = Новый ТабличныйДокумент;
		ОбластьПредупреждение = ТаблицаПредупреждение.Область(1,1,1,1);
		ЧастиТекста = Новый Массив;
		ЧастиТекста.Добавить(НСтр("ru = 'Отражения лимитов по данным бюджетирования ожидает %1 документов.';
									|en = '%1 documents await recording budgeting limits.'"));
		ЧастиТекста.Добавить(НСтр("ru = 'Запущено задание по отражению лимитов.';
									|en = 'The limit recording job is started.'"));
		ЧастиТекста.Добавить(НСтр("ru = 'После отражения Вам будет предложено переформировать отчет.';
									|en = 'You will be offered to rebuild the report after recording.'"));
		ТекстПредупреждения = СтрСоединить(ЧастиТекста, Символы.ПС);
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстПредупреждения,
			ДопСвойства.КоличествоДокументовКОтражениюЛимитовПоДаннымБюджетирования);
		ОбластьПредупреждение.Текст = ТекстПредупреждения;
		ОбластьПредупреждение.ЦветТекста = ЦветаСтиля.ЦветОтрицательногоЧисла;
		ДокументРезультат.ВставитьОбласть(ТаблицаПредупреждение.Область(1, , 1, ), ДокументРезультат.Область(1, , 1, ),
			ТипСмещенияТабличногоДокумента.ПоВертикали);
	Иначе
		ДопСвойства.Удалить("КоличествоДокументовКОтражениюЛимитовПоДаннымБюджетирования");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли