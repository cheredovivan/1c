//@strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// При компоновке результата.
// 
// Параметры:
//  ДокументРезультат - ТабличныйДокумент - Документ результат
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - Данные расшифровки
//  СтандартнаяОбработка - Булево - Стандартная обработка
//
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	ДокументУРБ = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ДокументУРБ").Значение;  // ДокументСсылка.УсловияРетроБонусовПоставщика 
	ВариантОтчета = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ВариантОтчета").Значение;  // Строка 
	ВыводитьАртикул = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "ВыводитьАртикул").Значение;  // Булево
	
	ПоДаннымДокумента = (ВариантОтчета = 2);
	
	СтруктураПараметров = ДополнительныеПараметрыПоУРБ(ДокументУРБ, ПоДаннымДокумента);
	
	СоставТоваров = СтруктураПараметров.СоставТоваров;
	СоставСегментыСвойствоНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
		ИЛИ СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры);
	ПроверитьСоставТоваров(СоставСегментыСвойствоНоменклатуры);
	
	СтруктураПараметров.ВыводитьАртикул = ВыводитьАртикул;
	
	ОформитьЗаголовки(НастройкиОтчета, СтруктураПараметров, ПоДаннымДокумента);
	
	НаборыДанных = ИнициализироватьВнешнийНаборДанных(СтруктураПараметров, ПоДаннымДокумента);
	
	КомментарийСегментыНеЗафиксированы = НСтр("ru = 'Данные о составе документа не зафиксированы, могут быть изменены по правилам формирования состава сегментов или свойств';
												|en = 'Document is not recorded, it can be changed according to the segment or property generation rules'");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
		НастройкиОтчета,
		"КомментарийСегментыНеЗафиксированы",
		КомментарийСегментыНеЗафиксированы);
	
	КомментарийСегментыЗафиксированы = НСтр("ru = 'Расчет и начисление ретро-бонуса осуществляется по данным зафиксированного состава документа';
											|en = 'Rebate is calculated and accrued according to the data of the recorded document'");
	КомпоновкаДанныхКлиентСервер.УстановитьПараметр(
		НастройкиОтчета,
		"КомментарийСегментыЗафиксированы",
		КомментарийСегментыЗафиксированы);
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, НаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	ОтчетПустой = ОтчетыСервер.ОтчетПустой(ЭтотОбъект, ПроцессорКомпоновки);
	КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("ОтчетПустой", ОтчетПустой);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения, Неопределено - Форма отчета или форма настроек отчета.
//       Неопределено когда вызов без контекста.
//   КлючВарианта - Строка, Неопределено - Имя предопределенного
//       или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено когда вызов без контекста.
//   Настройки - См. ОтчетыКлиентСервер.НастройкиОтчетаПоУмолчанию
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.ФормироватьСразу = Истина;
	Настройки.РазрешеноИзменятьВарианты = Ложь;
	Настройки.РазрешеноИзменятьСтруктуру = Ложь;
	
	Настройки.События.ПриСозданииНаСервере = Истина;
	Настройки.События.ПередЗагрузкойНастроекВКомпоновщик = Истина;
	
КонецПроцедуры

// Вызывается в обработчике одноименного события формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета:
//      * Отчет - ОтчетОбъект.РасчетРетроБонусовКлиентов
//      * Параметры - Структура:
//         ** ПараметрКоманды - ДокументСсылка.УсловияРетроБонусовПоставщика
//   Отказ - Булево - Признак отказа от создания формы.
//      См. описание одноименного параметра "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//   СтандартнаяОбработка - Булево - Признак выполнения стандартной (системной) обработки события.
//      См. описание одноименного параметра "ФормаКлиентскогоПриложения.ПриСозданииНаСервере" в синтакс-помощнике.
//
// См. также:
//   Процедура для вывода добавленных команд в форму: ОтчетыСервер.ВывестиКоманду().
//   Глобальный обработчик этого события: ОтчетыПереопределяемый.ПриСозданииНаСервере().
//
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Параметры = Форма.Параметры;
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки(); 
	
	ПоДаннымДокумента = Ложь;
	Если НастройкиОтчета.ДополнительныеСвойства.КлючВарианта = "СоставРетроБонусовПоставщиковПоДаннымДокументаКонтекст" Тогда
		
		ПоДаннымДокумента = Истина;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ПараметрКоманды") Тогда
		
		ПараметрКоманды = Параметры.ПараметрКоманды; // ДокументСсылка.УсловияРетроБонусовПоставщика
		ТипПараметраКоманды = ТипЗнч(ПараметрКоманды);
		
		Форма.НастройкиОтчета.РазрешеноВыбиратьВарианты = Ложь;
		
		Если ТипПараметраКоманды = Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
			
			СтруктураПараметров = ДополнительныеПараметрыПоУРБ(ПараметрКоманды, ПоДаннымДокумента);
			СоставТоваров = СтруктураПараметров.СоставТоваров;
			СоставСегментыСвойствоНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
				ИЛИ СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры);
			УРБСогласован = СтруктураПараметров.УРБСогласован;
			
			Если НЕ УРБСогласован
			   И НЕ ПоДаннымДокумента Тогда
				
				ТекстСообщения = НСтр("ru = 'Формирование отчета предусмотрено только для согласованных документов';
										|en = 'The report can be generated only for approved documents'");
				ВызватьИсключение ТекстСообщения;
				
			ИначеЕсли НЕ СоставСегментыСвойствоНоменклатуры Тогда
				
				ПроверитьСоставТоваров(СоставСегментыСвойствоНоменклатуры);
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли НЕ ЗначениеЗаполнено(Параметры.ПредставлениеВарианта) Тогда
		
		Отказ = Истина;
		СинонимДокумента = Метаданные.Документы.УсловияРетроБонусовПоставщика.Синоним;
		ШаблонОшибки = НСтр("ru = 'Отчет предназначен только для открытия из документа ""%1"".';
							|en = 'You can only open the report from the ""%1"" document.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, СинонимДокумента);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается перед загрузкой новых настроек. Используется для изменения схемы компоновки.
//   Например, если схема отчета зависит от ключа варианта или параметров отчета.
//   Чтобы изменения схемы вступили в силу следует вызывать метод ОтчетыСервер.ПодключитьСхему().
//
// Параметры:
//   Контекст - См. ОбщаяФорма.ФормаОтчета
//   КлючСхемы - Строка -
//       Идентификатор текущей схемы компоновщика настроек.
//       По умолчанию не заполнен (это означает что компоновщик инициализирован на основании основной схемы).
//       Используется для оптимизации, чтобы переинициализировать компоновщик как можно реже).
//       Может не использоваться, если переинициализация выполняется безусловно.
//   КлючВарианта - Строка, Неопределено -
//       Имя предопределенного или уникальный идентификатор пользовательского варианта отчета.
//       Неопределено, когда вызов для варианта расшифровки или без контекста.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда настройки варианта не надо загружать (уже загружены ранее).
//   НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных, Неопределено -
//       Пользовательские настройки, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда пользовательские настройки не надо загружать (уже загружены ранее).
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД) Экспорт
	
	Если КлючСхемы = КлючВарианта Тогда
		Возврат;
	КонецЕсли;
	
	Если НовыеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") Тогда
		
		Параметры = Контекст.Параметры;
		Если Параметры.Свойство("ПараметрКоманды") Тогда
			
			ПараметрКоманды = Параметры.ПараметрКоманды; // ДокументСсылка.УсловияРетроБонусовПоставщика
			ЗаполнитьПараметрыПоКонтексту(ПараметрКоманды, НовыеНастройкиКД);
			
		КонецЕсли;
		
	КонецЕсли;
	
	КлючСхемы = КлючВарианта;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоставТоваров(СоставСегментыСвойствоНоменклатуры)
	
	Если НЕ СоставСегментыСвойствоНоменклатуры Тогда
		
		ТекстСообщения = НСтр("ru = 'Формирование отчета предусмотрено только для документов с настройкой по сегментам или свойству номенклатуры';
								|en = 'The report can be generated only for documents with customization by item segments or property'");
		ВызватьИсключение ТекстСообщения;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОформитьЗаголовки(НастройкиОтчета, СтруктураПараметров, ПоДаннымДокумента)
	
	СегментыЗафиксированы = СтруктураПараметров.ФиксацияВыполнена;
	СоставТоваров = СтруктураПараметров.СоставТоваров;
	Товары = СтруктураПараметров.Товары;
	ВыводитьАртикул = СтруктураПараметров.ВыводитьАртикул;
	
	Если ПоДаннымДокумента Тогда
		ПредставлениеДокумента = СтруктураПараметров.ДокументУРБПредставление;
	Иначе
		ПредставлениеДокумента = СтруктураПараметров.ДокументУРБПервичныйПредставление;
	КонецЕсли;
		
	ШаблонЗаголовка = НСтр("ru = 'Состав документа %1';
							|en = '%1 document content'");
	ЗаголовокОтчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонЗаголовка, ПредставлениеДокумента);
	КомпоновкаДанныхКлиентСервер.УстановитьПараметрВывода(НастройкиОтчета, "Заголовок", ЗаголовокОтчета);
	
	Для Каждого ЭлементСтруктуры Из НастройкиОтчета.Структура Цикл
		
		Если ЭлементСтруктуры.Имя = "Товары" Тогда
			
			Если Товары = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
				
				ЗаголовокГруппировки = НСтр("ru = 'Выбранные товары';
											|en = 'Selected goods'");
				
			Иначе
				
				ЗаголовокГруппировки = НСтр("ru = 'Все, кроме выбранных товаров';
											|en = 'All except for selected goods'");
				
			КонецЕсли;
			КомпоновкаДанныхКлиентСервер.УстановитьПараметрВывода(ЭлементСтруктуры, "Заголовок", ЗаголовокГруппировки);
			
			ПолеКомпоновки = Новый ПолеКомпоновкиДанных("Артикул");
			
			Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				
				Если ТипЗнч(ПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")
				   И ПолеГруппировки.Поле = ПолеКомпоновки Тогда
					
					ПолеГруппировки.Использование = ВыводитьАртикул;
					Прервать;
					
				КонецЕсли;
				
			КонецЦикла;
			
			ПолеКомпоновки = Новый ПолеКомпоновкиДанных("Характеристика");
				
				Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				
					Если ТипЗнч(ПолеГруппировки) = Тип("ПолеГруппировкиКомпоновкиДанных")
					   И ПолеГруппировки.Поле = ПолеКомпоновки Тогда
						
						ПолеГруппировки.Использование =
							(СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры);
						Прервать;
						
					КонецЕсли;
					
				КонецЦикла;
				
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийСегментыНеЗафиксированы" Тогда
			
			ИспользоватьГруппировку = (НЕ СегментыЗафиксированы И НЕ ПоДаннымДокумента);
			ЭлементСтруктуры.Использование = ИспользоватьГруппировку; 
			
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийСегментыЗафиксированы" Тогда
			
			ИспользоватьГруппировку = (СегментыЗафиксированы И НЕ ПоДаннымДокумента);
			ЭлементСтруктуры.Использование = ИспользоватьГруппировку;
			
		ИначеЕсли ЭлементСтруктуры.Имя = "КомментарийПустой" Тогда
			
			ЭлементСтруктуры.Использование = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Заполняет параметры из переданного контекста.
// 
// Параметры:
//  ПараметрКоманды - ЛюбаяСсылка
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных, Неопределено -
//       Настройки варианта отчета, которые будут загружены в компоновщик настроек после его инициализации.
//       Неопределено, когда настройки варианта не надо загружать (уже загружены ранее).
//
Процедура ЗаполнитьПараметрыПоКонтексту(ПараметрКоманды, НовыеНастройкиКД)
	
	ТипПараметраКоманды = ТипЗнч(ПараметрКоманды);
	
	Если ТипПараметраКоманды = Тип("ДокументСсылка.УсловияРетроБонусовПоставщика") Тогда
		
		КомпоновкаДанныхКлиентСервер.УстановитьПараметр(НовыеНастройкиКД, "ДокументУРБ", ПараметрКоманды);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ИнициализироватьВнешнийНаборДанных(СтруктураПараметров, ПоДаннымДокумента)
	
	НаборДанныхТовары = Новый ТаблицаЗначений;
	НаборДанныхТовары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	НаборДанныхТовары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	НаборДанных = Новый Структура;
	НаборДанных.Вставить("НаборДанныхТовары", НаборДанныхТовары);
	
	ДокументУРБ = СтруктураПараметров.ДокументУРБ;
	ДокументУРБПервичный = СтруктураПараметров.ДокументУРБПервичный;
	СоставТоваров = СтруктураПараметров.СоставТоваров;
	СегментыЗафиксированы = СтруктураПараметров.ФиксацияВыполнена;
	
	СоставСегментыНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры);
	СоставСвойствоНоменклатуры = (СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры); 
	
	Если СоставСегментыНоменклатуры
	 ИЛИ СоставСвойствоНоменклатуры Тогда
	
		Если СегментыЗафиксированы
		   И НЕ ПоДаннымДокумента Тогда
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("ДокументУсловий", ДокументУРБПервичный);
			
			Запрос.Текст =
			"ВЫБРАТЬ
			|	РетроБонусыПоставщиковТовары.Номенклатура КАК Номенклатура,
			|	РетроБонусыПоставщиковТовары.Характеристика КАК Характеристика
			|ИЗ
			|	РегистрСведений.РетроБонусыПоставщиковТовары КАК РетроБонусыПоставщиковТовары
			|ГДЕ
			|	РетроБонусыПоставщиковТовары.ДокументУсловий = &ДокументУсловий"; 
			
			Результат = Запрос.Выполнить();
			НаборДанных.НаборДанныхТовары = Результат.Выгрузить();
			
		Иначе 
			
			Если ПоДаннымДокумента Тогда
				
				Если СоставСвойствоНоменклатуры Тогда
					
					НаборДанных.НаборДанныхТовары = ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента);
					
				Иначе
					
					НаборДанных.НаборДанныхТовары = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовТоваров(
						ДокументУРБ, 
						ПоДаннымДокумента);
					
				КонецЕсли;
				
			Иначе
				
				Если СоставСвойствоНоменклатуры Тогда
					
					НаборДанных.НаборДанныхТовары = ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента);
					
				Иначе
					
					НаборДанных.НаборДанныхТовары = РетроБонусыСервер.ЗаполнитьДанныеДляФиксацииСегментовТоваров(
						ДокументУРБПервичный);
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НаборДанных;
	
КонецФункции

Функция ТоварыПоСвойству(СтруктураПараметров, ПоДаннымДокумента)
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаТоваров.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	РезультатПроверки = РетроБонусыСервер.ДанныеПроверкиСвойстваНоменклатуры(СтруктураПараметров.СвойствоНоменклатуры);
	
	Если РезультатПроверки.РазрешеноИспользовать Тогда
		
		ТекстТоварыПоСвойству = РетроБонусыСервер.ТекстЗапросаТоварыПоСвойству(
			Ложь,
			СтруктураПараметров.СвойствоНоменклатуры,
			РезультатПроверки.Свойство,
			ПоДаннымДокумента);
		
		ТекстСоставТоваров =
		"ВЫБРАТЬ
		|	ТоварыПоСвойству.ДокументУсловий КАК ДокументУсловий,
		|	ТоварыПоСвойству.Характеристика КАК Характеристика,
		|	ТоварыПоСвойству.Номенклатура КАК Номенклатура
		|ИЗ
		|	ВТ_ТоварыПоСвойству КАК ТоварыПоСвойству
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
		|		ПО ТоварыПоСвойству.Номенклатура = СправочникНоменклатура.Ссылка
		|			И СправочникНоменклатура.ТипНоменклатуры В (&ТипыНоменклатуры)
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		ТекстРазделитель = ОбщегоНазначения.РазделительПакетаЗапросов();
		
		ФрагментыЗапроса = Новый Массив; // Массив из Строка
		ФрагментыЗапроса.Добавить(ТекстТоварыПоСвойству);
		ФрагментыЗапроса.Добавить(ТекстСоставТоваров);
		
		ТекстЗапроса = СтрСоединить(ФрагментыЗапроса, ТекстРазделитель);
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Если ПоДаннымДокумента Тогда
			Запрос.УстановитьПараметр("ИсходныйДокумент", СтруктураПараметров.ДокументУРБ);
		Иначе
			Запрос.УстановитьПараметр("ИсходныйДокумент", СтруктураПараметров.ДокументУРБПервичный);
		КонецЕсли;
		Запрос.УстановитьПараметр("СвойствоНоменклатуры", РезультатПроверки.Свойство);
		ТипыНоменклатуры = РетроБонусыСервер.ПоддерживаемыеТипыНоменклатуры();
		Запрос.УстановитьПараметр("ТипыНоменклатуры", ТипыНоменклатуры);
		
		ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
		
	Возврат ТаблицаТоваров;
	
КонецФункции

Функция ДополнительныеПараметрыПоУРБ(ДокументУРБ, ПоДаннымДокумента = Ложь)
	
	СтруктураПараметров = СтруктураПараметровОтчета();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументУсловий", ДокументУРБ);
	Запрос.УстановитьПараметр("ПоДаннымДокумента", ПоДаннымДокумента);
	
	Если ПоДаннымДокумента Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеУсловий.ОтборТоваров КАК Товары,
		|	ЕСТЬNULL(ВидыРетроБонусовПоставщиков.СоставТоваров,
		|		ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)) КАК СоставТоваров,
		|	ЛОЖЬ КАК ФиксацияВыполнена,
		|	ДанныеУсловий.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДокументовРетроБонусов.Согласован) КАК УРБСогласован,
		|	ВЫБОР
		|		КОГДА ДанныеУсловий.Исправление
		|			ТОГДА ДанныеУсловий.ИсправляемыйДокумент
		|		ИНАЧЕ ДанныеУсловий.Ссылка
		|	КОНЕЦ КАК ДокументУРБПервичный,
		|	ДанныеУсловий.Представление КАК ДокументУРБПредставление,
		|	ВЫБОР
		|		КОГДА ДанныеУсловий.Исправление
		|			ТОГДА ДанныеУсловий.ИсправляемыйДокумент.Представление
		|		ИНАЧЕ ДанныеУсловий.Представление
		|	КОНЕЦ КАК ДокументУРБПервичныйПредставление,
		|	ЕСТЬNULL(ВидыРетроБонусовПоставщиков.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры
		|ИЗ
		|	Документ.УсловияРетроБонусовПоставщика КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовПоставщиков КАК ВидыРетроБонусовПоставщиков
		|		ПО ДанныеУсловий.ВидРетроБонуса = ВидыРетроБонусовПоставщиков.Ссылка
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.Товары, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)) КАК Товары,
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.СоставТоваров, ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)) КАК СоставТоваров,
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ФиксацияВыполнена, ЛОЖЬ) КАК ФиксацияВыполнена,
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ДокументУсловий, ЛОЖЬ) <> ЛОЖЬ КАК УРБСогласован,
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ДокументУсловий, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)) КАК ДокументУРБПервичный,
		|	ДанныеУсловий.Представление КАК ДокументУРБПредставление,
		|	ВЫБОР
		|		КОГДА НЕ РетроБонусыПоставщиковУсловия.ДокументУсловий ЕСТЬ NULL
		|			ТОГДА РетроБонусыПоставщиковУсловия.ДокументУсловий.Представление
		|		ИНАЧЕ """"
		|	КОНЕЦ КАК ДокументУРБПервичныйПредставление,
		|	ЕСТЬNULL(ВидыРетроБонусовПоставщиков.СвойствоНоменклатуры, """") КАК СвойствоНоменклатуры
		|ИЗ
		|	Документ.УсловияРетроБонусовПоставщика КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыПоставщиковУсловия КАК РетроБонусыПоставщиковУсловия
		|		ПО ДанныеУсловий.ИсправляемыйДокумент = РетроБонусыПоставщиковУсловия.ДокументУсловий
		|		И ДанныеУсловий.Исправление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовПоставщиков КАК ВидыРетроБонусовПоставщиков
		|		ПО (РетроБонусыПоставщиковУсловия.Вид = ВидыРетроБонусовПоставщиков.Ссылка)
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий
		|	И НЕ РетроБонусыПоставщиковУсловия.ДокументУсловий ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.Товары, ЗНАЧЕНИЕ(Перечисление.СоставыСписковРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.СоставТоваров, ЗНАЧЕНИЕ(Перечисление.СоставыТоваровРетроБонусов.ПустаяСсылка)),
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ФиксацияВыполнена, ЛОЖЬ),
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ДокументУсловий, ЛОЖЬ) <> ЛОЖЬ,
		|	ЕСТЬNULL(РетроБонусыПоставщиковУсловия.ДокументУсловий, ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)),
		|	ДанныеУсловий.Представление,
		|	ВЫБОР
		|		КОГДА НЕ РетроБонусыПоставщиковУсловия.ДокументУсловий ЕСТЬ NULL
		|			ТОГДА РетроБонусыПоставщиковУсловия.ДокументУсловий.Представление
		|		ИНАЧЕ """"
		|	КОНЕЦ,
		|	ЕСТЬNULL(ВидыРетроБонусовПоставщиков.СвойствоНоменклатуры, """")
		|ИЗ
		|	Документ.УсловияРетроБонусовПоставщика КАК ДанныеУсловий
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыПоставщиковУсловия КАК РетроБонусыПоставщиковУсловия
		|		ПО ДанныеУсловий.Ссылка = РетроБонусыПоставщиковУсловия.ДокументУсловий
		|		И НЕ ДанныеУсловий.Исправление
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыРетроБонусовПоставщиков КАК ВидыРетроБонусовПоставщиков
		|		ПО (РетроБонусыПоставщиковУсловия.Вид = ВидыРетроБонусовПоставщиков.Ссылка)
		|ГДЕ
		|	ДанныеУсловий.Ссылка = &ДокументУсловий
		|	И НЕ РетроБонусыПоставщиковУсловия.ДокументУсловий ЕСТЬ NULL";
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураПараметров, Выборка);
		
	КонецЕсли;
	
	СтруктураПараметров.ДокументУРБ = ДокументУРБ;
	
	Возврат СтруктураПараметров;
	
КонецФункции

Функция СтруктураПараметровОтчета()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ДокументУРБ", Документы.УсловияРетроБонусовПоставщика.ПустаяСсылка());
	СтруктураПараметров.Вставить("ДокументУРБПервичный", Документы.УсловияРетроБонусовПоставщика.ПустаяСсылка());
	СтруктураПараметров.Вставить("ДокументУРБПредставление", "");
	СтруктураПараметров.Вставить("ДокументУРБПервичныйПредставление", "");
	СтруктураПараметров.Вставить("СоставТоваров", Перечисления.СоставыТоваровРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("Товары", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	СтруктураПараметров.Вставить("ФиксацияВыполнена", Ложь);
	СтруктураПараметров.Вставить("УРБСогласован", Ложь);
	СтруктураПараметров.Вставить("ВыводитьАртикул", Ложь);
	СтруктураПараметров.Вставить("СвойствоНоменклатуры", "");
	
	Возврат СтруктураПараметров;
	
КонецФункции

#КонецОбласти

#КонецЕсли