#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений.
//  - Неопределено.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.МониторингЗаказа)
		И УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор               = Метаданные.Отчеты.МониторингЗаказа.ПолноеИмя();
		КомандаОтчет.Представление               = НСтр("ru = 'Мониторинг заказа';
														|en = 'Monitor order'");
		КомандаОтчет.Менеджер                    = "Отчет.МониторингЗаказа";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность      = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.МониторингЗаказа);
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Формирует отчет.
//
// Параметры:
//  Параметры  - Структура - параметры формирования отчета:
//		* ДиаграммаГанта - ДиаграммаГанта - Диаграмма ганта.
//		* ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ на производство.
//		* СтатусГрафика - Число - статус диагностируемого графика.
//		* РежимОтображения - Число - режим отображения отчета (см. функцию РежимыОтображения).
//  АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат формирования отчета.
//
Процедура СформироватьОтчет(Параметры, АдресХранилища) Экспорт
	
	ДиаграммаГанта = Параметры.ДиаграммаГанта;
	ЗаказНаПроизводство = Параметры.ЗаказНаПроизводство;
	РежимОтображения = Параметры.РежимОтображения;
	СтатусГрафика = Параметры.СтатусГрафика;
	Границы = Новый Структура("Начало, Окончание", '39991231', '00010101');
	
	Если РежимОтображения = РежимПоПодразделениям()
		ИЛИ РежимОтображения = РежимПоВидамРабочихЦентров() Тогда
		
		СформироватьОтчетПоИсполнителям(ДиаграммаГанта, ЗаказНаПроизводство, РежимОтображения, СтатусГрафика, Границы);
		
	ИначеЕсли РежимОтображения = РежимПоПродукции() Тогда
		
		СформироватьОтчетПоПартиямЗапуска(ДиаграммаГанта, ЗаказНаПроизводство, СтатусГрафика, Границы);
		
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(ДиаграммаГанта, АдресХранилища);
	
КонецПроцедуры

// Режимы отображения.
// 
// Возвращаемое значение:
//  Структура - Режимы отображения:
// * ПоПродукции - Число - индекс режима "По продукции"
// * ПоПодразделениям - Число - индекс режима "По подразделениям"
// * ПоВидамРабочихЦентров - Число - индекс режима "По видам РЦ"
Функция РежимыОтображения() Экспорт
	
	РежимыРаботы = Новый Структура();
	РежимыРаботы.Вставить("ПоПродукции", РежимПоПродукции());
	РежимыРаботы.Вставить("ПоПодразделениям", РежимПоПодразделениям());
	РежимыРаботы.Вставить("ПоВидамРабочихЦентров", РежимПоВидамРабочихЦентров());
	Возврат РежимыРаботы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПоПодразделениямИВидамРабочихЦентров

Процедура СформироватьОтчетПоИсполнителям(ДиаграммаГанта, ЗаказНаПроизводство, РежимОтображения, СтатусГрафика, Границы)
	
	ДиаграммаГанта.Обновление = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	График = ГрафикПоИсполнителям(ЗаказНаПроизводство, РежимОтображения, СтатусГрафика, Границы);
	УстановитьПривилегированныйРежим(Ложь);
	
	Серия = ДиаграммаГанта.УстановитьСерию("ГрафикПроизводства");
	
	Исполнитель = Неопределено;
	Для Индекс = 0 По График.Количество()-1 Цикл
		
		Строка = График[Индекс];
		Если Исполнитель = Неопределено
			ИЛИ НЕ Исполнитель = Строка.Исполнитель Тогда
			
			Точка = УстановитьТочкуИсполнитель(ДиаграммаГанта, Строка);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			
		КонецЕсли;
		
		ДобавитьИнтервалИсполнитель(Значение, Строка);
		
	КонецЦикла;
	
	ДиаграммаГанта.Обновление = Ложь;
	
КонецПроцедуры

Функция ГрафикПоИсполнителям(ЗаказНаПроизводство, РежимОтображения, СтатусГрафика, Границы)
	
	Результат = ИнициализироватьГрафикПоИсполнителям();
	
	Запрос = ИнициализироватьЗапросПоИсполнителям(ЗаказНаПроизводство, РежимОтображения, СтатусГрафика);
	
	ВыборкаИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если ВыборкаИтоги.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(Границы, ВыборкаИтоги);
		
		ВыборкаИсполнитель = ВыборкаИтоги.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаИсполнитель.Следующий() Цикл
			
			ПредыдущаяСтрока = Неопределено;
			
			Выборка = ВыборкаИсполнитель.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				Если ПредыдущаяСтрока = Неопределено
					ИЛИ (ПредыдущаяСтрока.Окончание + 1) < Выборка.Начало Тогда
					
					НоваяСтрока = Результат.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
					
					ПредыдущаяСтрока = НоваяСтрока;
					
				Иначе
					
					ПредыдущаяСтрока.Окончание = Макс(ПредыдущаяСтрока.Окончание, Выборка.Окончание);
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИнициализироватьГрафикПоИсполнителям()
	
	ТипыИсполнителей = Новый Массив;
	ТипыИсполнителей.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ТипыИсполнителей.Добавить(Тип("СправочникСсылка.ВидыРабочихЦентров"));
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Исполнитель", Новый ОписаниеТипов(ТипыИсполнителей));
	Результат.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));
	Результат.Колонки.Добавить("Начало", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата"));
	
	Возврат Результат;
	
КонецФункции

Функция ИнициализироватьЗапросПоИсполнителям(ЗаказНаПроизводство, РежимОтображения, СтатусГрафика)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если РежимОтображения = РежимПоПодразделениям() Тогда
		ТекстЗапроса = ТекстЗапросаГрафикПоПодразделениям(СтатусГрафика);
	Иначе
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	МИНИМУМ(ДоступностьВидовРабочихЦентров.ДатаИнтервала) КАК МинДата,
		|	МАКСИМУМ(ДоступностьВидовРабочихЦентров.ДатаИнтервала) КАК МаксДата,
		|	ДоступностьВидовРабочихЦентров.Смена КАК Смена
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
		|ГДЕ
		|	ДоступностьВидовРабочихЦентров.Распоряжение = &ЗаказНаПроизводство
		|	И ДоступностьВидовРабочихЦентров.Смена <> ЗНАЧЕНИЕ(Справочник.Календари.ПустаяСсылка)
		|	И ДоступностьВидовРабочихЦентров.Занято <> 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступностьВидовРабочихЦентров.Смена");
		
		Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
		СменыИДаты = Запрос.Выполнить().Выгрузить();
		
		ПолныйИнтервалНачало = '00010101';
		ПолныйИнтервалОкончание = '00010101';
		ГрафикиСмен = Новый Массив;
		
		Если СменыИДаты.Количество() <> 0 Тогда
			ПолныйИнтервалНачало = СменыИДаты[0].МинДата;
			ПолныйИнтервалОкончание = СменыИДаты[0].МаксДата;
			Для Каждого Строка Из СменыИДаты Цикл
				ПолныйИнтервалНачало = Мин(ПолныйИнтервалНачало, Строка.МинДата);
				ПолныйИнтервалОкончание = Макс(ПолныйИнтервалНачало, Строка.МаксДата);
				ГрафикиСмен.Добавить(Строка.Смена);
			КонецЦикла;
		КонецЕсли;
		
		КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(
			МенеджерВременныхТаблиц, ГрафикиСмен, ПолныйИнтервалНачало - 86400 * 4, ПолныйИнтервалОкончание + 86400 * 4, Ложь);
		
		ТекстЗапроса = ТекстЗапросаГрафикПоВидамРЦ(СтатусГрафика);
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	
	Возврат Запрос;
	
КонецФункции

Функция ТекстЗапросаГрафикПоПодразделениям(СтатусГрафика)
	
	Если СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		
		Результат = 
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение КАК Исполнитель,
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение.Представление КАК Представление,
		|	ГрафикЭтаповПроизводства2_2.Начало КАК Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание КАК Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|	И НЕ ГрафикЭтаповПроизводства2_2.Этап В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Исполнитель,
		|	Начало,
		|	Окончание
		|ИТОГИ
		|	МИНИМУМ(Начало),
		|	МАКСИМУМ(Окончание)
		|ПО
		|	ОБЩИЕ,
		|	Исполнитель";
		
	Иначе
		
		Результат =
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение КАК Исполнитель,
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение.Представление КАК Представление,
		|	ГрафикЭтаповПроизводства2_2.Начало КАК Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание КАК Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|	И НЕ ГрафикЭтаповПроизводства2_2.Этап В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение,
		|	ГрафикЭтаповПроизводства2_2.Этап.Подразделение.Представление,
		|	ГрафикЭтаповПроизводства2_2.Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик
		|	И НЕ (ГрафикЭтаповПроизводства2_2.Этап, ГрафикЭтаповПроизводства2_2.КлючПартия) В
		|				(ВЫБРАТЬ
		|					ДД.Этап,
		|					ДД.КлючПартия
		|				ИЗ
		|					РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ДД
		|				ГДЕ
		|					ДД.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|					И ДД.СтатусГрафика = &СтатусГрафика)
		|	И НЕ ГрафикЭтаповПроизводства2_2.Этап В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка))
		|
		|УПОРЯДОЧИТЬ ПО
		|	Исполнитель,
		|	Начало,
		|	Окончание
		|ИТОГИ
		|	МИНИМУМ(Начало),
		|	МАКСИМУМ(Окончание)
		|ПО
		|	ОБЩИЕ,
		|	Исполнитель";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ТекстЗапросаГрафикПоВидамРЦ(СтатусГрафика)
	
	Результат = "
		|ВЫБРАТЬ
		|	Расписание.ГрафикРаботы КАК Смена,
		|	МИНИМУМ(ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
		|		ЧАС(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)))*60
		|		+ МИНУТА(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1))))) КАК Начало,
		|	МАКСИМУМ(
		|		ВЫБОР
		|			КОГДА Расписание.ВремяОкончания ЕСТЬ NULL 
		|				ИЛИ Расписание.ВремяОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|				ТОГДА ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, ДЕНЬ, 1)
		|			ИНАЧЕ ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
		|				ЧАС(Расписание.ВремяОкончания)*60 + МИНУТА(Расписание.ВремяОкончания))
		|		КОНЕЦ) КАК Окончание
		|ПОМЕСТИТЬ ВТИнтервалыСмен
		|ИЗ
		|	ВТРасписанияРаботы КАК Расписание
		|
		|СГРУППИРОВАТЬ ПО
		|	Расписание.ГрафикРаботы,
		|	Расписание.Период";
	
	Результат = Результат + ОбщегоНазначения.РазделительПакетаЗапросов();
	
	Если СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		
		Результат = Результат +
		"ВЫБРАТЬ
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра КАК Исполнитель,
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра.Представление КАК Представление,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала КАК Начало,
		|	ВЫБОР ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ЧАС)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, НЕДЕЛЯ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, МЕСЯЦ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
		|			ТОГДА ИнтервалыСмен.Окончание
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
		|	КОНЕЦ КАК Окончание
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИнтервалыСмен КАК ИнтервалыСмен
		|		ПО ИнтервалыСмен.Смена = ДоступностьВидовРабочихЦентров.Смена
		|		И ИнтервалыСмен.Начало = ДоступностьВидовРабочихЦентров.ДатаИнтервала
		|ГДЕ
		|	ДоступностьВидовРабочихЦентров.Распоряжение = &ЗаказНаПроизводство
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	ИнтервалыСмен.Окончание
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДоступностьВидовРабочихЦентров.Занято) <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Исполнитель,
		|	Начало,
		|	Окончание
		|ИТОГИ
		|	МИНИМУМ(Начало),
		|	МАКСИМУМ(Окончание)
		|ПО
		|	ОБЩИЕ,
		|	Исполнитель";
		
	Иначе
		
		Результат = Результат +
		"ВЫБРАТЬ
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра КАК Исполнитель,
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра.Представление КАК Представление,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала КАК Начало,
		|	ВЫБОР ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ЧАС)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, НЕДЕЛЯ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
		|			ТОГДА КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, МЕСЯЦ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
		|			ТОГДА ИнтервалыСмен.Окончание
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ДоступностьВидовРабочихЦентров.ДатаИнтервала, ДЕНЬ)
		|	КОНЕЦ КАК Окончание
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИнтервалыСмен КАК ИнтервалыСмен
		|		ПО ИнтервалыСмен.Смена = ДоступностьВидовРабочихЦентров.Смена
		|		И ИнтервалыСмен.Начало = ДоступностьВидовРабочихЦентров.ДатаИнтервала
		|ГДЕ
		|	ДоступностьВидовРабочихЦентров.Распоряжение = &ЗаказНаПроизводство
		|	И НЕ (ДоступностьВидовРабочихЦентров.Этап, ДоступностьВидовРабочихЦентров.КлючПартия) В
		|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|					ПланированиеЗагрузки.Этап,
		|					ПланированиеЗагрузки.КлючПартия
		|				ИЗ
		|					РегистрСведений.ПланированиеЗагрузкиВидовРабочихЦентров КАК ПланированиеЗагрузки
		|				ГДЕ
		|					ПланированиеЗагрузки.Распоряжение = &ЗаказНаПроизводство
		|					И ПланированиеЗагрузки.СтатусГрафика = &СтатусГрафика)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	ИнтервалыСмен.Окончание
		|
		|ИМЕЮЩИЕ
		|	СУММА(ДоступностьВидовРабочихЦентров.Занято) <> 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПланированиеЗагрузки.ВидРабочегоЦентра,
		|	ПланированиеЗагрузки.ВидРабочегоЦентра.Представление,
		|	ПланированиеЗагрузки.ДатаИнтервала,
		|	ВЫБОР ПланированиеЗагрузки.ВидРабочегоЦентра.Подразделение.ИнтервалПланирования
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
		|			ТОГДА КОНЕЦПЕРИОДА(ПланированиеЗагрузки.ДатаИнтервала, ЧАС)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|			ТОГДА КОНЕЦПЕРИОДА(ПланированиеЗагрузки.ДатаИнтервала, ДЕНЬ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
		|			ТОГДА КОНЕЦПЕРИОДА(ПланированиеЗагрузки.ДатаИнтервала, НЕДЕЛЯ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
		|			ТОГДА КОНЕЦПЕРИОДА(ПланированиеЗагрузки.ДатаИнтервала, МЕСЯЦ)
		|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
		|			ТОГДА ИнтервалыСмен.Окончание
		|		ИНАЧЕ КОНЕЦПЕРИОДА(ПланированиеЗагрузки.ДатаИнтервала, ДЕНЬ)
		|	КОНЕЦ
		|ИЗ
		|	РегистрСведений.ПланированиеЗагрузкиВидовРабочихЦентров КАК ПланированиеЗагрузки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТИнтервалыСмен КАК ИнтервалыСмен
		|		ПО ИнтервалыСмен.Смена = ПланированиеЗагрузки.Смена
		|		И ИнтервалыСмен.Начало = ПланированиеЗагрузки.ДатаИнтервала
		|ГДЕ
		|	ПланированиеЗагрузки.Распоряжение = &ЗаказНаПроизводство
		|	И ПланированиеЗагрузки.СтатусГрафика = &СтатусГрафика
		|
		|УПОРЯДОЧИТЬ ПО
		|	Исполнитель,
		|	Начало,
		|	Окончание
		|ИТОГИ
		|	МИНИМУМ(Начало),
		|	МАКСИМУМ(Окончание)
		|ПО
		|	ОБЩИЕ,
		|	Исполнитель";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьТочкуИсполнитель(ДиаграммаГанта, Данные)
	
	Результат = ДиаграммаГанта.УстановитьТочку(Данные.Исполнитель);
	Результат.Текст = Данные.Представление;
	Результат.Расшифровка = Данные.Исполнитель;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьИнтервалИсполнитель(Значение, Данные)
	
	Интервал = Значение.Добавить();
	Интервал.Начало = Данные.Начало;
	Интервал.Конец = Данные.Окончание + 1;
	Интервал.Цвет = WebЦвета.Зеленый;
	
КонецПроцедуры

#КонецОбласти

#Область ПоПартиямЗапуска

Процедура СформироватьОтчетПоПартиямЗапуска(ДиаграммаГанта, ЗаказНаПроизводство, СтатусГрафика, Границы)
	
	ДиаграммаГанта.Обновление = Ложь;
	
	Данные = ГрафикИЗависимостиПоПартиямЗапуска(ЗаказНаПроизводство, СтатусГрафика);
	ВыборкаИзделия = Данные.ВыборкаИзделия;
	
	Если ВыборкаИзделия.Количество() > 0 Тогда
		
		Серия = ДиаграммаГанта.УстановитьСерию("ГрафикПроизводства");
		
		Интервалы = Новый ТаблицаЗначений;
		Интервалы.Колонки.Добавить("ПартияПроизводства");
		Интервалы.Колонки.Добавить("Интервал");
		
		Интервалы.Индексы.Добавить("ПартияПроизводства");
		
		Пока ВыборкаИзделия.Следующий() Цикл
			
			Границы.Начало = МИН(Границы.Начало, ВыборкаИзделия.Начало);
			Границы.Окончание = МАКС(Границы.Окончание, ВыборкаИзделия.Окончание);
			
			Точка = УстановитьТочкуПартияЗапуска(ДиаграммаГанта, ВыборкаИзделия);
			Значение = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
			Интервал = ДобавитьИнтервалПартияЗапуска(Значение, ВыборкаИзделия);
			
			НоваяСтрока = Интервалы.Добавить();
			НоваяСтрока.ПартияПроизводства = ВыборкаИзделия.ПартияПроизводства;
			НоваяСтрока.Интервал = Интервал;
			
		КонецЦикла;
		
		ТипЗаказа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказНаПроизводство, "ТипПроизводственногоПроцесса");
		Если ТипЗаказа <> Перечисления.ТипыПроизводственныхПроцессов.Ремонт Тогда
			
			ДобавитьСвязиИнтервалов(Данные.Зависимости, Интервалы)
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДиаграммаГанта.Обновление = Ложь;
	
КонецПроцедуры

// Возвращает график и зависимости по партиям запуска
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ.
//  СтатусГрафика - Число - статус графика производства.
//
// Возвращаемое значение:
//  Структура - содержит:
//   * ВыборкаИзделия - ВыборкаИзРезультатаЗапроса - содержит:
//   * Зависимости - ТаблицаЗначений - 
//
Функция ГрафикИЗависимостиПоПартиямЗапуска(ЗаказНаПроизводство, СтатусГрафика)
	
	ТекстыЗапросовПакета = Новый Массив;
	
	Если СтатусГрафика = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик() Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап.ПартияПроизводства КАК ПартияПроизводства,
		|	МИНИМУМ(ГрафикЭтаповПроизводства2_2.Начало) КАК Начало,
		|	МАКСИМУМ(ГрафикЭтаповПроизводства2_2.Окончание) КАК Окончание
		|ПОМЕСТИТЬ ВТГрафикПроизводства
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|
		|СГРУППИРОВАТЬ ПО
		|	ГрафикЭтаповПроизводства2_2.Этап.ПартияПроизводства
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияПроизводства";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.Этап,
		|	ГрафикЭтаповПроизводства2_2.Начало,
		|	ГрафикЭтаповПроизводства2_2.Окончание
		|ПОМЕСТИТЬ ВТГрафикПоСтатусу
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Этап.ПартияПроизводства КАК ПартияПроизводства,
		|	МИНИМУМ(ВложенныйЗапрос.Начало) КАК Начало,
		|	МАКСИМУМ(ВложенныйЗапрос.Окончание) КАК Окончание
		|ПОМЕСТИТЬ ВТГрафикПроизводства
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВТГрафикПоСтатусу.Этап КАК Этап,
		|		ВТГрафикПоСтатусу.Начало КАК Начало,
		|		ВТГрафикПоСтатусу.Окончание КАК Окончание
		|	ИЗ
		|		ВТГрафикПоСтатусу КАК ВТГрафикПоСтатусу
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ГрафикЭтаповПроизводства2_2.Этап,
		|		ГрафикЭтаповПроизводства2_2.Начало,
		|		ГрафикЭтаповПроизводства2_2.Окончание
		|	ИЗ
		|		РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|	ГДЕ
		|		ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|		И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик
		|		И НЕ ГрафикЭтаповПроизводства2_2.Этап В
		|					(ВЫБРАТЬ
		|						ВТГРафикПоСтатусу.Этап
		|					ИЗ
		|						ВТГРафикПоСтатусу)) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Этап.ПартияПроизводства
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ПартияПроизводства";
		
	КонецЕсли;
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Изделия.Ссылка КАК Отправитель,
	|	Изделия.ЭтапПотребитель КАК Получатель
	|ПОМЕСТИТЬ ВТСвязиНоменклатуры
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Изделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Документ
	|		ПО Изделия.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Распоряжение = &ЗаказНаПроизводство
	|	И Документ.Проведен
	|	И НЕ Изделия.Отменено
	|	И Изделия.ЭтапПотребитель <> ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Изделия.Ссылка,
	|	Изделия.ЭтапПотребитель
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК Изделия
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Документ
	|		ПО Изделия.Ссылка = Документ.Ссылка
	|ГДЕ
	|	Документ.Распоряжение = &ЗаказНаПроизводство
	|	И Документ.Проведен
	|	И НЕ Изделия.Отменено
	|	И Изделия.ЭтапПотребитель <> ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	График.ПартияПроизводства          КАК ПартияПроизводства,
	|	График.Начало                      КАК Начало,
	|	График.Окончание                   КАК Окончание,
	|	Партия.ОсновноеИзделиеНоменклатура КАК Номенклатура,
	|	Партия.ОсновноеИзделиеНоменклатура.Представление КАК НоменклатураПредставление
	|ИЗ
	|	ВТГрафикПроизводства КАК График
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК Партия
	|		ПО График.ПартияПроизводства = Партия.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начало,
	|	Окончание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭтапОтправитель.ПартияПроизводства КАК ПартияПроизводства,
	|	ЭтапПолучатель.ПартияПроизводства КАК СледующийЭтап
	|ИЗ
	|	ВТСвязиНоменклатуры КАК ВТСвязиНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапОтправитель
	|		ПО ВТСвязиНоменклатуры.Отправитель = ЭтапОтправитель.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПолучатель
	|		ПО ВТСвязиНоменклатуры.Получатель = ЭтапПолучатель.Ссылка
	|ГДЕ
	|	ЭтапОтправитель.ПартияПроизводства <> ЭтапПолучатель.ПартияПроизводства";
	ТекстыЗапросовПакета.Добавить(ТекстЗапроса);
	
	Разделитель =
	"
	|;
	|/////////////////////////////////////////////////////////////
	|";
	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, Разделитель);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	МаксИндекс = МассивРезультатов.ВГраница();
	
	Результат = Новый Структура;
	
	Результат.Вставить(
		"ВыборкаИзделия",
		МассивРезультатов[МаксИндекс-1].Выбрать());
	
	Результат.Вставить(
		"Зависимости",
		МассивРезультатов[МаксИндекс].Выгрузить());
	
	Возврат Результат;
	
КонецФункции

Функция УстановитьТочкуПартияЗапуска(ДиаграммаГанта, ВыборкаИзделия)
	
	ЗначениеТочки = ВыборкаИзделия.ПартияПроизводства;
	Результат = ДиаграммаГанта.УстановитьТочку(ЗначениеТочки);
	
	Результат.Текст = ВыборкаИзделия.НоменклатураПредставление;
	Результат.Расшифровка = ВыборкаИзделия.ПартияПроизводства;
	
	Возврат Результат;
	
КонецФункции

Функция ДобавитьИнтервалПартияЗапуска(Значение, ВыборкаИзделия)
	
	Результат = Значение.Добавить();
	Результат.Начало = ВыборкаИзделия.Начало;
	Результат.Конец = ВыборкаИзделия.Окончание;
	Результат.Расшифровка = Новый Структура("ПартияПроизводства, Тип", ВыборкаИзделия.ПартияПроизводства, "Партия");
	Результат.Цвет = WebЦвета.Зеленый;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСвязиИнтервалов(Зависимости, Интервалы)
	
	Для каждого СтрокаЗависимость Из Зависимости Цикл
		
		НайденнаяСтрока = Интервалы.Найти(СтрокаЗависимость.ПартияПроизводства, "ПартияПроизводства");
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			
			Интервал = НайденнаяСтрока.Интервал; // ИнтервалДиаграммыГанта - 
			
			НайденнаяСтрока = Интервалы.Найти(СтрокаЗависимость.СледующийЭтап, "ПартияПроизводства");
			Если НЕ НайденнаяСтрока = Неопределено Тогда
				
				СледующийИнтервал = НайденнаяСтрока.Интервал;
				
				Связь = Интервал.Добавить(СледующийИнтервал);
				Связь.ТипСвязи = ТипСвязиДиаграммыГанта.КонецНачало;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция РежимПоПродукции()

	Возврат 0;
	
КонецФункции

Функция РежимПоПодразделениям()
	
	Возврат 1;
	
КонецФункции

Функция РежимПоВидамРабочихЦентров()
	
	Возврат 2;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли