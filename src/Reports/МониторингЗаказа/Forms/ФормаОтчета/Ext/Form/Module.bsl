
#Область ОписаниеПеременных

&НаКлиенте
Перем Этапы;

&НаКлиенте
Перем Партии;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

&НаКлиенте
Перем ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьПриОткрытии = Ложь;
	
	Если Параметры.Свойство("ПараметрКоманды", ЗаказНаПроизводство) Тогда
		
		СформироватьПриОткрытии = Истина;
		
	ИначеЕсли Параметры.Свойство("ЗаказНаПроизводство", ЗаказНаПроизводство) Тогда
		
		СформироватьПриОткрытии = Истина;
		
		Если Не ЗаказНаПроизводство.Пустая() И Параметры.СкрыватьЗаказ Тогда
			Элементы.ЗаказНаПроизводство.Видимость = Ложь; // панель навигации формы
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Параметры.Свойство("СтатусГрафика", СтатусГрафика) Тогда
		СтатусГрафика = СтатусРабочийГрафик();
	КонецЕсли;
	
	ДиаграммаГанта.АвтоОпределениеПолногоИнтервала = Ложь;
	ДиаграммаГанта.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	ДиаграммаГанта.Окантовка = Ложь;
	ДиаграммаГанта.ОтображатьЛегенду = Ложь;
	ДиаграммаГанта.ВертикальнаяПрокрутка = Истина;
	ДиаграммаГанта.ОтображатьПустыеЗначения = Ложь;
	ДиаграммаГанта.ОтображатьЗаголовок = Ложь;
	ДиаграммаГанта.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОтображениеТекстаИнтервала = ОтображениеТекстаИнтервалаДиаграммыГанта.НеОтображать;
	ДиаграммаГанта.ОбластьПостроения.Право = 1;
	
	РабочаяДата = ОбщегоНазначения.ТекущаяДатаПользователя();
	
	ПараметрыВывода = Новый Структура();
	ПараметрыВывода.Вставить("ЦветаФона", ЦветаФона());
	ПараметрыВывода.Вставить("СтатусГрафика", СтатусГрафика);
	ПараметрыВывода.Вставить("ПоказыватьЭтапы", Истина);
	ПараметрыВывода.Вставить("ПоказыватьСвязи", Истина);
	ПараметрыВывода.Вставить("РазворачиватьПервыйУровеньПослеЗагрузки", Ложь);
	ПараметрыВывода.Вставить("РабочаяДата", РабочаяДата);
	
	ЗагрузитьНастройкиФормы();
	
	Режимы = Отчеты.МониторингЗаказа.РежимыОтображения();
	ЗаполнитьСписокРежимовОтображения();
	
	ПрочитатьРеквизитыЗаказа();
	
	УстановитьЗаголовокФормы();
	
	НастроитьВидимостьДоступностьЭлементов();
	
	Если СформироватьПриОткрытии Тогда
		
		СформироватьОтчетНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СмещениеВремени = СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().СмещениеСтандартногоВремени;
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		НачатьОжиданиеДлительнойОперации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию();
	КонецЕсли;
	
	СохранитьНастройкиФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаказНаПроизводствоПриИзменении(Элемент)
	
	ЗаказНаПроизводствоПриИзмененииНаСервере();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаказНаПроизводствоПриИзмененииНаСервере()
	
	ПрочитатьРеквизитыЗаказа();
	
	УстановитьЗаголовокФормы();
	
	НастроитьВидимостьДоступностьЭлементов();
	
	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	СформироватьОтчетНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимОтображенияПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);

	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	ПодключитьОбработчикОжидания("СформироватьОтчет", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЭтапыПриИзменении(Элемент)
	
	ПараметрыВывода.ПоказыватьЭтапы = ПоказыватьЭтапы;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	ПодключитьОбработчикОжидания("СформироватьОтчет", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	ПодключитьОбработчикОжидания("СформироватьОтчет", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаТаблицаПередРазворачиванием(Элемент, Строка, Отказ)
	
	Если Не ЗначениеЗаполнено(Точки) Или Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Отчет.МониторингЗаказа.ФормаОтчета.РазвернутьТочку");
	
	СерияДиаграммы = СерияДиаграммы();
	
	ЗначениеДиаграммы = ДиаграммаГанта.НайтиЗначениеПоИдентификатору(Строка);
	
	ТочкаДиаграммы = ЗначениеДиаграммы.Точка;
	Точка = Точки.НайтиСтроки(Новый Структура("Значение", ТочкаДиаграммы.Значение))[0];

	Если Не Точка.Загружена Тогда
		ЗагрузитьТочку(Точка, ТочкаДиаграммы)
	КонецЕсли;
	
	ДиаграммаГанта.РазвернутьТочку(ТочкаДиаграммы, Ложь);

	РазвернутьИлиСвернутьТочку(ДиаграммаГанта, Точка, ТочкаДиаграммы, Истина, СерияДиаграммы);

КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаТаблицаПередСворачиванием(Элемент, Строка, Отказ)
	
	Если Не ЗначениеЗаполнено(Точки) Или Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Отчет.МониторингЗаказа.ФормаОтчета.СвернутьТочку");
	
	СерияДиаграммы = СерияДиаграммы();
	
	ЗначениеДиаграммы = ДиаграммаГанта.НайтиЗначениеПоИдентификатору(Строка);
	
	ТочкаДиаграммы = ЗначениеДиаграммы.Точка;
	Точка = Точки.НайтиСтроки(Новый Структура("Значение", ТочкаДиаграммы.Значение))[0];
	
	ДиаграммаГанта.СвернутьТочку(ТочкаДиаграммы, Ложь);

	РазвернутьИлиСвернутьТочку(ДиаграммаГанта, Точка, ТочкаДиаграммы, Ложь, СерияДиаграммы);

КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаГантаОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипЗнч(Расшифровки) = Тип("Массив")
		И Расшифровки.Количество() > 1 Тогда
		
		ЗначениеРасшифровки = Расшифровки[1];
		
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("Структура") И ЗначениеРасшифровки.Тип = "Партия" Тогда
			
			ПараметрыФормы = Новый Структура();
			
			Если ЗначениеРасшифровки.Свойство("ПартияПроизводства") Тогда
				ПараметрыФормы.Вставить("ПараметрКоманды", ЗначениеРасшифровки.ПартияПроизводства);
			Иначе
				ПараметрыФормы.Вставить("КлючПартия", ЗначениеРасшифровки.КлючПартия);
			КонецЕсли;
			
			ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафика);
			ПараметрыФормы.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
			
			ОткрытьФорму(
				"Отчет.ДиаграммаПроизводстваПартииЗапуска.ФормаОбъекта",
				ПараметрыФормы,
				ЭтотОбъект,
				Новый УникальныйИдентификатор);
				
		ИначеЕсли ТипЗнч(ЗначениеРасшифровки) = Тип("Структура") И ЗначениеРасшифровки.Тип = "Этап" Тогда
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафика);
			Если ТипЗнч(ЗначениеРасшифровки.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
				ПараметрыФормы.Вставить("ПараметрКоманды", ЗначениеРасшифровки.Этап);
			Иначе
				ПараметрыФормы.Вставить("Этап", ЗначениеРасшифровки.Этап);
				ПараметрыФормы.Вставить("КлючПартия", ЗначениеРасшифровки.КлючПартия);
				ПараметрыФормы.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
			КонецЕсли;
			
			ОткрытьФорму(
				"Отчет.ДиагностикаЭтапаПроизводства.ФормаОбъекта",
				ПараметрыФормы,
				ЭтотОбъект,
				Новый УникальныйИдентификатор);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	ПодключитьОбработчикОжидания("СформироватьОтчет_ПолныйРасчет", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсе(Команда)
	
	Готово = ЗагрузитьВсеНаСервере(100);
	
	Если Не Готово Тогда
		
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВопросРазвернутьВсеПродолжитьВыводЗавершение", ЭтаФорма),
			СтрШаблон(НСтр("ru = 'Выведено %1 элементов. Продолжить вывод?';
							|en = '%1 items are displayed. Continue?'"), 100),
			РежимДиалогаВопрос.ДаНет,,
			КодВозвратаДиалога.Нет);
			
		Возврат;
		
	КонецЕсли;
	
	РазвернутьВсеТочки();
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсе(Команда)
	
	СвернутьВсеТочки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьСвязи(Команда)
	
	ПоказыватьСвязи = Не ПоказыватьСвязи;
	ПараметрыВывода.ПоказыватьСвязи = ПоказыватьСвязи;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);

	ДиаграммаГанта.Очистить(); // 60010106
	Точки.Очистить();
	
	ПодключитьОбработчикОжидания("СформироватьОтчет", 0.1, Истина);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Режим_ПоПродукции_ДинамическаяСтруктура

#Область ДинамическийВывод

&НаСервереБезКонтекста
Процедура ЗагрузитьСледующийУровень(Отбор, Этапы, Партии, РабочаяДата, ПараметрыВывода)
	
	СтатусГрафика = ПараметрыВывода.СтатусГрафика;
	ПоказыватьЭтапы = ПараметрыВывода.ПоказыватьЭтапы;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ПоказыватьЭтапы", ПоказыватьЭтапы);
	Запрос.УстановитьПараметр("РабочаяДата", РабочаяДата);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.УстановитьПараметр("Отбор", Отбор);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	
	|	МАКСИМУМ(Изделия.Этап) КАК Этап,
	|	
	|	План.Номенклатура           КАК Номенклатура,
	|	План.Характеристика         КАК Характеристика,
	|	План.Склад                  КАК Склад,
	|	План.Назначение             КАК Назначение,
	|	План.ЗаказНаПроизводство    КАК ЗаказНаПроизводство,
	|	План.КлючНоменклатура       КАК КлючНоменклатура
	|
	|ПОМЕСТИТЬ ПеремещаемыеИзделия
	|ИЗ
	|
	|	РегистрСведений.СтруктураЗаказа КАК Изделия
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК План
	|	ПО Изделия.ЗаказНаПроизводство = План.ЗаказНаПроизводство
	|	 И Изделия.Номенклатура        = План.Номенклатура
	|	 И Изделия.Характеристика      = План.Характеристика
	|	 И Изделия.КлючНоменклатура    = План.КлючСвязиПеремещение
	|
	|ГДЕ
	|	Изделия.КлючПартия В (&Отбор)
	|	И Изделия.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление)
	|
	|СГРУППИРОВАТЬ ПО
	|	План.Номенклатура,
	|	План.Характеристика,
	|	План.Склад,
	|	План.Назначение,
	|	План.ЗаказНаПроизводство,
	|	План.КлючНоменклатура,
	|	План.КлючПартия
	|
	|ИМЕЮЩИЕ
	|	СУММА(План.Требуется)  > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Склад,
	|	Назначение,
	|	ЗаказНаПроизводство,
	|	КлючНоменклатура
	|;
	|
	|ВЫБРАТЬ
	|	Изделия.Номенклатура        КАК Номенклатура,
	|	Изделия.Характеристика      КАК Характеристика,
	|	Изделия.Склад               КАК Склад,
	|	Изделия.Назначение          КАК Назначение,
	|	Изделия.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Изделия.КлючНоменклатура    КАК КлючНоменклатура,
	|	Изделия.Этап                КАК Этап
	|ПОМЕСТИТЬ ВТИзделия
	|ИЗ
	|	РегистрСведений.СтруктураЗаказа КАК Изделия
	|ГДЕ
	|	Изделия.КлючПартия В (&Отбор)
	|	И Изделия.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Потребление)
	|
	|СГРУППИРОВАТЬ ПО
	|	Изделия.Номенклатура,
	|	Изделия.Характеристика,
	|	Изделия.Склад,
	|	Изделия.Назначение,
	|	Изделия.ЗаказНаПроизводство,
	|	Изделия.КлючНоменклатура,
	|	Изделия.Этап
	|
	|ИМЕЮЩИЕ
	|	СУММА(Изделия.Требуется) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаПроизводство, КлючНоменклатура
	|;
	|
	|ВЫБРАТЬ
	|	
	|	МАКСИМУМ(Изделия.Этап) КАК Этап,
	|	
	|	План.Номенклатура           КАК Номенклатура,
	|	План.Характеристика         КАК Характеристика,
	|	План.Склад                  КАК Склад,
	|	План.Назначение             КАК Назначение,
	|	План.ЗаказНаПроизводство    КАК ЗаказНаПроизводство,
	|	План.КлючНоменклатура       КАК КлючНоменклатура,
	|	План.КлючПартия             КАК КлючПартия, 
	|
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий) КАК Количество
	|
	|ПОМЕСТИТЬ План
	|ИЗ
	|
	|	ВТИзделия КАК Изделия
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК План
	|	ПО Изделия.Номенклатура        = План.Номенклатура
	|	 И Изделия.Характеристика      = План.Характеристика
	|	 И Изделия.Склад               = План.Склад
	|	 И Изделия.Назначение          = План.Назначение
	|	 И Изделия.ЗаказНаПроизводство = План.ЗаказНаПроизводство
	|	 И Изделия.КлючНоменклатура    = План.КлючНоменклатура
	|	 И План.ВидСтроки В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|
	|
	|СГРУППИРОВАТЬ ПО
	|	План.Номенклатура,
	|	План.Характеристика,
	|	План.Склад,
	|	План.Назначение,
	|	План.ЗаказНаПроизводство,
	|	План.КлючНоменклатура,
	|	План.КлючПартия
	|
	|ИМЕЮЩИЕ
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий)  > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	
	|	МАКСИМУМ(Изделия.Этап) КАК Этап,
	|	
	|	План.Номенклатура           КАК Номенклатура,
	|	План.Характеристика         КАК Характеристика,
	|	План.Склад                  КАК Склад,
	|	План.Назначение             КАК Назначение,
	|	План.ЗаказНаПроизводство    КАК ЗаказНаПроизводство,
	|	План.КлючНоменклатура       КАК КлючНоменклатура,
	|	План.КлючПартия             КАК КлючПартия, 
	|
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий) КАК Количество
	|
	|ИЗ
	|
	|	ПеремещаемыеИзделия КАК Изделия
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК План
	|	ПО Изделия.Номенклатура        = План.Номенклатура
	|	 И Изделия.Характеристика      = План.Характеристика
	|	 И Изделия.Склад               = План.Склад
	|	 И Изделия.Назначение          = План.Назначение
	|	 И Изделия.ЗаказНаПроизводство = План.ЗаказНаПроизводство
	|	 И Изделия.КлючНоменклатура    = План.КлючНоменклатура
	|	 И План.ВидСтроки В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|
	|СГРУППИРОВАТЬ ПО
	|	План.Номенклатура,
	|	План.Характеристика,
	|	План.Склад,
	|	План.Назначение,
	|	План.ЗаказНаПроизводство,
	|	План.КлючНоменклатура,
	|	План.КлючПартия
	|
	|ИМЕЮЩИЕ
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий)  > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КлючПартия
	|;
	|ВЫБРАТЬ
	|	График.Этап КАК Этап,
	|	График.КлючПартия КАК КлючПартия,
	|	ВЫБОР 
	|		КОГДА График.Этап ССЫЛКА Справочник.ЭтапыПроизводства ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Справочник.ЭтапыПроизводства).НомерЭтапа
	|		КОГДА График.Этап ССЫЛКА Документ.ЭтапПроизводства2_2 ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Документ.ЭтапПроизводства2_2).НомерЭтапа
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК НомерЭтапа,
	|	ВЫБОР 
	|		КОГДА График.Этап ССЫЛКА Справочник.ЭтапыПроизводства ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Справочник.ЭтапыПроизводства).НомерСледующегоЭтапа
	|		КОГДА График.Этап ССЫЛКА Документ.ЭтапПроизводства2_2 ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Документ.ЭтапПроизводства2_2).НомерСледующегоЭтапа
	|		ИНАЧЕ
	|			0
	|	КОНЕЦ КАК НомерСледующегоЭтапа,
	|	ВЫБОР 
	|		КОГДА График.Этап ССЫЛКА Документ.ЭтапПроизводства2_2 ТОГДА
	|			ИСТИНА
	|		ИНАЧЕ
	|			ЛОЖЬ
	|	КОНЕЦ КАК ЭтапСоздан,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(График.Этап) = ТИП(Справочник.ЭтапыПроизводства) ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Справочник.ЭтапыПроизводства).Наименование
	|		КОГДА ТИПЗНАЧЕНИЯ(График.Этап) = ТИП(Документ.ЭтапПроизводства2_2) ТОГДА
	|			ВЫРАЗИТЬ(График.Этап КАК Документ.ЭтапПроизводства2_2).Номер + "", "" + ВЫРАЗИТЬ(График.Этап КАК Документ.ЭтапПроизводства2_2).НаименованиеЭтапа
	|		ИНАЧЕ
	|			""-""
	|	КОНЕЦ КАК ЭтапПредставление,
	|
	|	График.Начало КАК ЛеваяГраница,
	|	График.Начало КАК Начало,
	|	График.Окончание КАК Окончание,
	|
	|	ВЫБОР
	|		КОГДА График.Этап ССЫЛКА Документ.ЭтапПроизводства2_2
	|		  И ВЫРАЗИТЬ(График.Этап КАК
	|			Документ.ЭтапПроизводства2_2).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|			ТОГДА 3
	|		КОГДА График.Этап ССЫЛКА Документ.ЭтапПроизводства2_2
	|		  И ВЫРАЗИТЬ(График.Этап КАК
	|			Документ.ЭтапПроизводства2_2).Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК Состояние
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
	|
	|ГДЕ
	|	График.КлючПартия В (&Отбор)
	|	И НЕ График.Этап В (НЕОПРЕДЕЛЕНО, ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка))
	|	И График.СтатусГрафика = &СтатусГрафика
	|	И &ПоказыватьЭтапы
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерЭтапа УБЫВ
	|;
	|
	|ВЫБРАТЬ
	|
	|	План.Этап КАК Этап,
	|
	|	План.Номенклатура         КАК Номенклатура,
	|	План.Характеристика       КАК Характеристика,
	|	План.Склад                КАК Склад,
	|	План.Назначение           КАК Назначение,
	|	План.ЗаказНаПроизводство  КАК ЗаказНаПроизводство,
	|	План.КлючНоменклатура     КАК КлючНоменклатура,
	|	План.КлючПартия           КАК КлючПартия,
	|
	|	ПРЕДСТАВЛЕНИЕ(План.Номенклатура)                  КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(План.Характеристика)                КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(План.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
	|
	|	График.Начало КАК ЛеваяГраница,
	|	График.Начало КАК Начало,
	|	График.Окончание КАК Окончание,
	|
	|	1 КАК Состояние,
	|	План.Количество КАК Количество,
	|
	|	ИСТИНА КАК ЕстьПодчиненныеСтроки
	|
	|ИЗ
	|	План КАК План
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
	|	ПО График.КлючПартия = План.КлючПартия
	|	 И График.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|	 И График.СтатусГрафика = &СтатусГрафика
	|
	|УПОРЯДОЧИТЬ ПО
	|	Окончание УБЫВ, Номенклатура, Характеристика, Количество УБЫВ
	|";
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	КоличествоПакетов = РезультатЗапроса.Количество();
	ЗагрузитьВМассивРезультатЗапроса(РезультатЗапроса[КоличествоПакетов-2], Этапы);
	ЗагрузитьВМассивРезультатЗапроса(РезультатЗапроса[КоличествоПакетов-1], Партии);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьТочку(Точка, ТочкаДиаграммы)
	
	КлючПартия = Новый УникальныйИдентификатор(Лев(Точка.Значение, 36));
	ЗагрузитьСледующийУровень(КлючПартия, Этапы, Партии, РабочаяДата, ПараметрыВывода);
	ВывестиТочки(ДиаграммаГанта, Этапы, Партии, ТочкаДиаграммы, Точки, ПараметрыВывода);
	Точка.Загружена = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТочкуНаСервере(Точка, ТочкаДиаграммы, Этапы, Партии)
	
	КлючПартия = Новый УникальныйИдентификатор(Лев(Точка.Значение, 36));
	ЗагрузитьСледующийУровень(КлючПартия, Этапы, Партии, РабочаяДата, ПараметрыВывода);
	ВывестиТочки(ДиаграммаГанта, Этапы, Партии, ТочкаДиаграммы, Точки, ПараметрыВывода);
	Точка.Загружена = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьПервыйУровень()
	
	Этапы = Новый Массив();
	Партии = Новый Массив();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ПустойЭтап", Справочники.ЭтапыПроизводства.ПустаяСсылка());
	Запрос.УстановитьПараметр("РабочаяДата", РабочаяДата);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Изделия.Номенклатура        КАК Номенклатура,
	|	Изделия.Характеристика      КАК Характеристика,
	|	Изделия.Склад               КАК Склад,
	|	Изделия.Назначение          КАК Назначение,
	|	Изделия.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	Изделия.КлючНоменклатура    КАК КлючНоменклатура
	|ПОМЕСТИТЬ ВТИзделия
	|ИЗ
	|	РегистрСведений.СтруктураЗаказа КАК Изделия
	|ГДЕ
	|	&УсловиеПолучения
	|
	|СГРУППИРОВАТЬ ПО
	|	Изделия.Номенклатура,
	|	Изделия.Характеристика,
	|	Изделия.Склад,
	|	Изделия.Назначение,
	|	Изделия.ЗаказНаПроизводство,
	|	Изделия.КлючНоменклатура
	|
	|ИМЕЮЩИЕ
	|	СУММА(Изделия.Требуется) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура, Характеристика, Склад, Назначение, ЗаказНаПроизводство, КлючНоменклатура
	|;
	|
	|ВЫБРАТЬ
	|	
	|	&ПустойЭтап КАК Этап,
	|	
	|	План.Номенклатура         КАК Номенклатура,
	|	План.Характеристика       КАК Характеристика,
	|	План.Склад                КАК Склад,
	|	План.Назначение           КАК Назначение,
	|	План.ЗаказНаПроизводство  КАК ЗаказНаПроизводство,
	|	План.КлючНоменклатура     КАК КлючНоменклатура,
	|	План.КлючПартия           КАК КлючПартия,
	|
	|	ПРЕДСТАВЛЕНИЕ(План.Номенклатура)                  КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕ(План.Характеристика)                КАК ХарактеристикаПредставление,
	|	ПРЕДСТАВЛЕНИЕ(План.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
	|
	|	МАКСИМУМ(График.Начало) КАК ЛеваяГраница,
	|	МАКСИМУМ(График.Начало) КАК Начало,
	|	МАКСИМУМ(График.Окончание) КАК Окончание,
	|
	|	1 КАК Состояние,
	|	ИСТИНА КАК ЕстьПодчиненныеСтроки,
	|
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий) КАК Количество
	|
	|ИЗ
	|
	|	ВТИзделия КАК Изделия
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК План
	|	ПО Изделия.Номенклатура        = План.Номенклатура
	|	 И Изделия.Характеристика      = План.Характеристика
	|	 И Изделия.Склад               = План.Склад
	|	 И Изделия.Назначение          = План.Назначение
	|	 И Изделия.ЗаказНаПроизводство = План.ЗаказНаПроизводство
	|	 И Изделия.КлючНоменклатура    = План.КлючНоменклатура
	|	 И План.ВидСтроки В (
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПродукции),
	|		ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.ПроизводствоПолуфабриката))
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
	|	ПО График.КлючПартия = План.КлючПартия
	|	 И График.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|	 И График.СтатусГрафика = &СтатусГрафика
	|
	|СГРУППИРОВАТЬ ПО
	|	План.Номенклатура,
	|	План.Характеристика,
	|	План.Склад,
	|	План.Назначение,
	|	План.ЗаказНаПроизводство,
	|	План.КлючНоменклатура,
	|	План.КлючПартия
	|
	|ИМЕЮЩИЕ
	|	СУММА(План.Запланировано + План.РаспределеноИзПартийОбособленно + План.РаспределеноИзПартий) > 0
	|
	|;";
	
	Если Номенклатура.Пустая() Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПолучения", "
			|Изделия.ЗаказНаПроизводство = &ЗаказНаПроизводство
			|И Изделия.ВидСтроки = ЗНАЧЕНИЕ(Перечисление.ВидыСтрокСтруктурыЗаказаНаПроизводство.Продукция)");
		Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
		
	Иначе
		
		ТекстОтбора = "";
		
		Отбор = Новый Структура();
		Отбор.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
		Отбор.Вставить("Номенклатура", Номенклатура);
		
		Для каждого КлючЗначение Из Отбор Цикл
			
			ТекстОтбора = 
				ТекстОтбора
				+ ?(ПустаяСтрока(ТекстОтбора), "", " И ")
				+ "Изделия."
				+ КлючЗначение.Ключ
				+ " В(&"
				+ КлючЗначение.Ключ
				+ ")";
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			
		КонецЦикла;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПолучения", ?(ПустаяСтрока(ТекстОтбора), "ЛОЖЬ", ТекстОтбора));
		
	КонецЕсли;
	
	ЗагрузитьВМассивРезультатЗапроса(Запрос.Выполнить(), Партии);
	ВывестиТочки(ДиаграммаГанта, Этапы, Партии, ДиаграммаГанта, Точки, ПараметрыВывода);
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьВсеНаСервере(Порция = -1)
	
	Этапы = Новый Массив();
	Партии = Новый Массив();
	
	НачальноеКоличествоТочек = Точки.Количество();
	
	НеЗагруженныеТочки = Точки.НайтиСтроки(Новый Структура("Загружена", Ложь));
	
	Граница = НеЗагруженныеТочки.ВГраница();
	
	Пока Граница >= 0 Цикл

		Точка = НеЗагруженныеТочки[0];
		
		ТочкаДиаграммы = ДиаграммаГанта.УстановитьТочку(Точка.Значение);
		
		ЗагрузитьТочкуНаСервере(Точка, ТочкаДиаграммы, Этапы, Партии);
		
		НеЗагруженныеТочки.Удалить(0);
		
		Граница = Граница - 1;
		
		Если Граница = -1 Тогда
			НеЗагруженныеТочки = Точки.НайтиСтроки(Новый Структура("Загружена", Ложь));
			Граница = НеЗагруженныеТочки.ВГраница();
		КонецЕсли;
		
		Если Порция > 0 И Точки.Количество() - НачальноеКоличествоТочек > Порция Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Граница = -1;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиТочки(ДиаграммаГанта, Этапы, Партии, Знач ТочкаРодитель, Точки, ПараметрыВывода)
	
	Серия = ДиаграммаГанта.УстановитьСерию("Занятость");
	ТочкиИЭтапы = Новый Соответствие(); 
	
	Если ТочкаРодитель.Точки.Количество() = 1 И ТочкаРодитель.Точки[0].Значение = 0 Тогда
		ТочкаРодитель.Точки.Удалить(ТочкаРодитель.Точки[0]);
	КонецЕсли;

	Для каждого Строка Из Этапы Цикл
		
		Точка = ТочкаРодитель.Точки.Добавить();
		
		ЗначениеРасшифровки = Новый Структура("Тип, Этап, КлючПартия", "Этап", Строка.Этап, Строка.КлючПартия);
		
		Точка.Значение    = "" + Строка.КлючПартия + "/" + Новый УникальныйИдентификатор();
		Точка.Текст       = Строка.ЭтапПредставление;
		Точка.Расшифровка = ЗначениеРасшифровки;
		Точка.Картинка = ?(Не Строка.ЭтапСоздан, БиблиотекаКартинок.ЭтапПроизводства, БиблиотекаКартинок.ЭтапПроизводстваПринятКВыполнению);
		
		ИнтервалДиаграммыГанта = ВывестиИнтервалы(ДиаграммаГанта, Строка, Точка, Серия, ПараметрыВывода);
		
		Если ПараметрыВывода.ПоказыватьСвязи
			И Строка.НомерСледующегоЭтапа <> 0 Тогда
			
			Для каждого Последователь Из Этапы Цикл
				
				Если Последователь.НомерЭтапа = Строка.НомерСледующегоЭтапа Тогда
					ВывестиСвязиИнтервалов(ДиаграммаГанта, ИнтервалДиаграммыГанта, ТочкиИЭтапы[Последователь.Этап], Серия);
				ИначеЕсли Последователь.НомерЭтапа < Строка.НомерСледующегоЭтапа Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ТочкиИЭтапы.Вставить(Строка.Этап, Точка);
		
		КешироватьТочку(Точки, Точка, Истина);
		
	КонецЦикла;
	
	Для Каждого Строка Из Партии Цикл
		
		Если ЗначениеЗаполнено(ТочкиИЭтапы) Тогда
			ТочкаРодитель = ТочкиИЭтапы.Получить(Строка.Этап);
		КонецЕсли;
		
		Точка = ТочкаРодитель.Точки.Добавить();
		
		ЗначениеРасшифровки = Новый Структура("Тип, КлючПартия", "Партия", Строка.КлючПартия);
		
		Представление = СтрШаблон("%1, %2 %3", НоменклатураКлиентСервер.ПредставлениеНоменклатуры(Строка.НоменклатураПредставление, Строка.ХарактеристикаПредставление), 
			Формат(Строка.Количество, "ЧН=; ЧГ="),
			Строка.ЕдиницаИзмеренияПредставление);
		
		Точка.Значение    = "" + Строка.КлючПартия + "/" + Новый УникальныйИдентификатор();
		Точка.Текст       = Представление;
		Точка.Расшифровка = ЗначениеРасшифровки;
		
		ИнтервалДиаграммыГанта = ВывестиИнтервалы(ДиаграммаГанта, Строка, Точка, Серия, ПараметрыВывода);
		
		Если ПараметрыВывода.ПоказыватьСвязи И ПараметрыВывода.ПоказыватьЭтапы Тогда
			ВывестиСвязиИнтервалов(ДиаграммаГанта, ИнтервалДиаграммыГанта, ТочкаРодитель, Серия);
		КонецЕсли;
		
		Если Строка.ЕстьПодчиненныеСтроки Тогда
			КешироватьТочку(Точки, Точка);
			ФиктивнаяТочка = Точка.Точки.Добавить();
			ФиктивнаяТочка.Значение = 0;
			ДиаграммаГанта.ПолучитьЗначение(ФиктивнаяТочка, Серия).Добавить();
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВывестиИнтервалы(ДиаграммаГанта, Строка, Точка, Серия, ПараметрыВывода)
	
	ЗначенияДиаграммыГанта = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
	
	// Вывод интервала по данным текущей строки
	ИнтервалДиаграммыГанта = ЗначенияДиаграммыГанта.Добавить();

	ЦветИнтервала = ?(Строка.Состояние = 3, ПараметрыВывода.ЦветаФона.ЭтапЗавершен, ПараметрыВывода.ЦветаФона.ЭтапНеЗавершенЗапланированАвтоматически);

	Если Строка.Начало <= Строка.Окончание Тогда
		ИнтервалДиаграммыГанта.Начало = Строка.Начало;
		ИнтервалДиаграммыГанта.Конец = Строка.Окончание;
	Иначе
		ИнтервалДиаграммыГанта.Начало = Строка.Начало - 86400;
		ИнтервалДиаграммыГанта.Конец = Строка.Начало;
	КонецЕсли;
	ИнтервалДиаграммыГанта.Расшифровка = Точка.Расшифровка;
	ИнтервалДиаграммыГанта.Цвет        = ЦветИнтервала;
	
	Возврат ИнтервалДиаграммыГанта;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиСвязиИнтервалов(ДиаграммаГанта, ИнтервалДиаграммыГанта, Точка, Серия)
	
	Если Не ТипЗнч(Точка) = Тип("ТочкаДиаграммыГанта") Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Интервал Из ДиаграммаГанта.ПолучитьЗначение(Точка, Серия) Цикл
		
		Элемент = ИнтервалДиаграммыГанта.Добавить(Интервал);
		
		Если Элемент.Начало.Конец > Элемент.Конец.Начало Тогда
			Элемент.Цвет = WebЦвета.Красный;
		Иначе
			Элемент.Цвет = Новый Цвет();
		КонецЕсли;
		
		Прервать;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КешироватьТочку(Точки, Точка, Загружена = Ложь)
	Кэш = Точки.Добавить();
	Кэш.Значение = Точка.Значение;
	Кэш.Загружена = Загружена;
	Кэш.Уровень = Точка.Уровень();
КонецПроцедуры

#КонецОбласти

#Область РазвернутьВсе

&НаКлиентеНаСервереБезКонтекста
Процедура РазвернутьИлиСвернутьТочку(ДиаграммаГанта, Точка, ТочкаДиаграммы, Развернуть, Серия)

	Интервалы = ДиаграммаГанта.ПолучитьЗначение(ТочкаДиаграммы, Серия);
	
	Если Интервалы.Количество() > 1 Тогда
		
		Интервал1 = Неопределено; // ИнтервалДиаграммыГанта
		Интервал2 = Неопределено; // ИнтервалДиаграммыГанта
		
		Для каждого Интервал Из Интервалы Цикл
			
			Интервал1 = ?(Интервал1 <> Неопределено И Интервал1.Начало < Интервал.Начало, Интервал1, Интервал);
			Интервал2 = ?(Интервал2 <> Неопределено И Интервал2.Начало > Интервал.Начало, Интервал2, Интервал);
			
		КонецЦикла;
		
		Интервал1.Конец = ?(Развернуть, '00010101', Интервал2.Начало - 1);
		
	КонецЕсли;
	
	Точка.Развернута = Развернуть;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросРазвернутьВсеПродолжитьВыводЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗагрузитьВсеНаСервере();
		
	КонецЕсли;
	
	РазвернутьВсеТочки();
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьВсеТочки()
	
	ЗагруженныеТочки = Точки.НайтиСтроки(Новый Структура("Загружена", Истина)); // развернуть все загруженные точки
	
	СерияДиаграммы = СерияДиаграммы();
	
	Для каждого Точка Из ЗагруженныеТочки Цикл
		
		ТочкаДиаграммы = ДиаграммаГанта.УстановитьТочку(Точка.Значение);
		
		ДиаграммаГанта.РазвернутьТочку(ТочкаДиаграммы, Ложь);
		
		РазвернутьИлиСвернутьТочку(ДиаграммаГанта, Точка, ТочкаДиаграммы, Истина, СерияДиаграммы);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсеТочки()
	
	СерияДиаграммы = СерияДиаграммы();
	
	Для Индекс = -Точки.Количество() + 1 По 0 Цикл
		
		Точка = Точки[-Индекс];
		
		ТочкаДиаграммы = ДиаграммаГанта.УстановитьТочку(Точка.Значение);
		
		ДиаграммаГанта.СвернутьТочку(ТочкаДиаграммы, Ложь);
		
		РазвернутьИлиСвернутьТочку(ДиаграммаГанта, Точка, ТочкаДиаграммы, Ложь, СерияДиаграммы);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Настройки

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючФормы(),
		КлючНастроекФормы());
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		Настройки.Свойство("РежимОтображения", РежимОтображения);
		Настройки.Свойство("ПоказыватьЭтапы", ПоказыватьЭтапы);
		Настройки.Свойство("ПоказыватьСвязи", ПоказыватьСвязи);
		
		ПараметрыВывода.ПоказыватьЭтапы = ПоказыватьЭтапы;
		ПараметрыВывода.ПоказыватьСвязи = ПоказыватьСвязи;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиФормы()
	
	Настройки = Новый Структура;
	
	Настройки.Вставить("РежимОтображения", РежимОтображения);
	Настройки.Вставить("ПоказыватьЭтапы", ПоказыватьЭтапы);
	Настройки.Вставить("ПоказыватьСвязи", ПоказыватьСвязи);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючФормы(),
		КлючНастроекФормы(),
		Настройки);
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Отчет.МониторингЗаказа.ФормаОтчета";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастроекФормы()
	
	Возврат "Основные";
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ЗаполнитьСписокРежимовОтображения()
	
	СписокРежимов = Элементы.РежимОтображения.СписокВыбора;
		
	СписокРежимов.Добавить(
		Режимы.ПоПродукции,
		НСтр("ru = 'По продукции';
			|en = 'By products'"));
	
	СписокРежимов.Добавить(
		Режимы.ПоПодразделениям,
		НСтр("ru = 'По подразделениям';
			|en = 'By business units'"));
	
	СписокРежимов.Добавить(
		Режимы.ПоВидамРабочихЦентров,
		НСтр("ru = 'По видам рабочих центров';
			|en = 'By work center types'"));
		
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовокФормы()
	
	Если СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		Шаблон = НСтр("ru = 'Мониторинг заказа %1';
						|en = '%1 order monitoring'");
		
	ИначеЕсли СтатусГрафика = СтатусПредварительныйГрафик() Тогда
		
		Шаблон = НСтр("ru = 'Мониторинг заказа (планирование) %1';
						|en = '%1 order monitoring (planning)'");
		
	ИначеЕсли СтатусГрафика = СтатусМодельГрафика() Тогда
		
		Шаблон = НСтр("ru = 'Мониторинг заказа (модель) %1';
						|en = '%1 order monitoring (model)'");
		
	КонецЕсли;
	
	Заголовок = СтрШаблон(Шаблон, ?(Не Элементы.ЗаказНаПроизводство.Видимость, "", // панель навигации формы
		КраткоеПредставлениеЗаказа));
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРеквизитыЗаказа()
	
	РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ЗаказНаПроизводство, "ДинамическаяСтруктура, Статус, Номер, Дата");
	
	ЗаполнитьЗначенияСвойств(ЭтаФорма, РеквизитыЗаказа);
	
	Если ЗаказНаПроизводство.Пустая() Тогда
		КраткоеПредставлениеЗаказа = "";
	Иначе
		КраткоеПредставлениеЗаказа = Документы.ЗаказНаПроизводство2_2.ПредставлениеЗаказа(ЗаказНаПроизводство, "", РеквизитыЗаказа);
	КонецЕсли;
	
	ТекущийСтатусЗаказа = РеквизитыЗаказа.Статус;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьДоступностьЭлементов()
	
	Элементы.ПредупреждениеЗаказЗакрыт.Видимость = ТекущийСтатусЗаказа = Перечисления.СтатусыЗаказовНаПроизводство2_2.Закрыт;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(ЭтаФорма)
	
	ДинамическаяСтруктура = ЭтаФорма.ДинамическаяСтруктура;
	
	Режимы = ЭтаФорма.Режимы;
	РежимОтображения = ЭтаФорма.РежимОтображения;
	
	Элементы = ЭтаФорма.Элементы;
	Элементы.Номенклатура.Видимость = ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции;
	Элементы.ПоказыватьЭтапы.Видимость = ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции;
	Элементы.ПоказыватьСвязи.Видимость = ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции;
	Элементы.РазвернутьВсе.Видимость = Ложь; // 60010106
	Элементы.СвернутьВсе.Видимость = Ложь;
	
	Если ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции Тогда
		Элементы.ДиаграммаГантаТаблицаТочка.ОтображатьВШапке = Истина;
	Иначе
		
		Элементы.ДиаграммаГантаТаблицаТочка.ОтображатьВШапке = Ложь;
	КонецЕсли;
	
	Элементы.ПоказыватьСвязи.Пометка = ЭтаФорма.ПоказыватьСвязи;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьШкалуВремениДиаграммы()

	// Полный интервал
	
	Период = РегистрыСведений.ГрафикЭтаповПроизводства2_2.ПолныйИнтервал(ЗаказНаПроизводство); 
	
	Если Не ЗначениеЗаполнено(Период.Начало) Или Период.Начало > Период.Окончание Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиШкалы = УправлениеПроизводствомКлиентСервер.НастройкиШкалыДиаграммыГантаВРежимеВсеДанные(
		Период.Начало, Период.Окончание, 9);
	
	ШкалаВремениЭлементы = ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы;
	
	Для Индекс = 1 По ШкалаВремениЭлементы.Количество()-1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[Индекс]);
	КонецЦикла;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = НастройкиШкалы.Единица;
	ЭлементШкалы.Формат = НастройкиШкалы.Формат;
	
	Если ШкалаВремениЭлементы.Количество() = 2 Тогда
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[0]);
	КонецЕсли;
	
	ДиаграммаГанта.УстановитьПолныйИнтервал(
		НастройкиШкалы.НачалоПолногоИнтервала,
		НастройкиШкалы.ОкончаниеПолногоИнтервала);

	// Выделение выходных 
	
	ДиаграммаГанта.ИнтервалыФона.Очистить();
	
	Графики = Новый Массив();
	Графики.Добавить(Константы.ОсновнойКалендарьПредприятия.Получить());
	
	РасписанияРаботы = ПланированиеПроизводства.РасписаниеРаботыПоГрафику(Графики, НастройкиШкалы.НачалоПолногоИнтервала, НастройкиШкалы.ОкончаниеПолногоИнтервала);
	РасписанияРаботы.Свернуть("ДатаГрафика");
	РасписанияРаботы.Индексы.Добавить("ДатаГрафика");
	
	ПериодыНедоступности = Новый ТаблицаЗначений();
	ПериодыНедоступности.Колонки.Добавить("Начало",    Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	ПериодыНедоступности.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата",,, Новый КвалификаторыДаты(ЧастиДаты.ДатаВремя)));
	
	ТекущийПериод = НастройкиШкалы.НачалоПолногоИнтервала;
	Пока ТекущийПериод <= НастройкиШкалы.ОкончаниеПолногоИнтервала Цикл
		
		Если РасписанияРаботы.Найти(ТекущийПериод, "ДатаГрафика") = Неопределено Тогда
			НовыйПериод = ПериодыНедоступности.Добавить();
			НовыйПериод.Начало = ТекущийПериод;
			НовыйПериод.Окончание = КонецДня(НовыйПериод.Начало);
		КонецЕсли;
		
		ТекущийПериод = ТекущийПериод + 86400;
	КонецЦикла;
	
	Для каждого ДанныеНедоступности Из ПериодыНедоступности Цикл
		
		ДиаграммаГанта.ИнтервалыФона.Добавить(ДанныеНедоступности.Начало, ДанныеНедоступности.Окончание);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

&НаСервере
Функция СтатусПредварительныйГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусПредварительныйГрафик();
	
КонецФункции

&НаСервере
Функция СтатусМодельГрафика()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика();
	
КонецФункции

&НаКлиенте
Функция СерияДиаграммы()
	Серия = ДиаграммаГанта.УстановитьСерию("Занятость");
	Возврат Серия;
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьВМассивРезультатЗапроса(Результат, Массив)
	
	Массив.Очистить();
	
	Если Не
		Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		
		СтруктураСтрокой = "";
		НужнаЗапятая = Ложь;
		Для Каждого Колонка Из Результат.Колонки Цикл
			Если НужнаЗапятая Тогда
				СтруктураСтрокой = СтруктураСтрокой + ",";
			КонецЕсли;
			СтруктураСтрокой = СтруктураСтрокой + Колонка.Имя;
			НужнаЗапятая = Истина;
		КонецЦикла;
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Новый Структура(СтруктураСтрокой);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			Массив.Добавить(НоваяСтрока);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьДлительнуюОперацию()
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);

КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации()
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация) Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатФормированияОтчетаОповещение", ЭтотОбъект);
	
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ОбработатьРезультатФормированияОтчета();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатФормированияОтчетаОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ОбработатьРезультатФормированияОтчета();
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчетНаСервере();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтчет_ПолныйРасчет()
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьОтчетНаСервере(Истина);
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтчетНаСервере(ПолныйРасчет = Ложь)
	
	ГрафикЗагружен = Ложь;
	ДатаАктуальности = '00010101';
	
	Если Не ДлительнаяОперация = Неопределено Тогда
		ОтменитьДлительнуюОперацию();
	КонецЕсли;
	
	Если ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции Тогда
		
		ДлительнаяОперация = Неопределено
		
	Иначе
		
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("ЗаказНаПроизводство", ЗаказНаПроизводство);
		ПараметрыПроцедуры.Вставить("СтатусГрафика", СтатусГрафика);
		ПараметрыПроцедуры.Вставить("РежимОтображения", РежимОтображения);
		ПараметрыПроцедуры.Вставить("ДиаграммаГанта", ДиаграммаГанта);
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование отчета ""Мониторинг заказа на производство""';
																|en = 'Generate the ""Production order monitoring"" report'");
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
			"Отчеты.МониторингЗаказа.СформироватьОтчет",
			ПараметрыПроцедуры,
			ПараметрыВыполнения);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДлительнаяОперация)
		И ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ОбработатьРезультатФормированияОтчета();
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	Иначе
		
		НачатьОжиданиеДлительнойОперации = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатФормированияОтчета()

	Если ЗначениеЗаполнено(ДлительнаяОперация) И ДлительнаяОперация.Статус = "Выполнено" Тогда
		
		ДиаграммаГанта = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		ДлительнаяОперация = Неопределено;
		
	Иначе
		
		Если ДинамическаяСтруктура И РежимОтображения = Режимы.ПоПродукции Тогда
			
			ГрафикЗагружен = Истина;
			ЗагрузитьПервыйУровень();
			
		КонецЕсли;
		
	КонецЕсли;
	
	НастроитьШкалуВремениДиаграммы();
	
	Элементы.ПредупреждениеТребуетсяРассчитатьГрафик.Видимость = РегистрыСведений.ГрафикЭтаповПроизводства2_2.ТребуетсяРассчитатьГрафик(ЗаказНаПроизводство);
	
КонецПроцедуры

&НаСервере
Функция ЦветаФона()
	
	ЦветаФона = Новый Структура();
	
	ЦветаФона.Вставить("ЭтапЗавершен",                            ЦветаСтиля.ЦветФонаЭтапЗавершен);
	ЦветаФона.Вставить("ЭтапНеЗавершенЗапланированАвтоматически", ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированАвтоматически);
	ЦветаФона.Вставить("ЭтапНеЗавершенЗапланированВручную",       ЦветаСтиля.ЦветФонаЭтапНеЗавершенЗапланированВручную);
	
	Возврат ЦветаФона;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация

Этапы = Новый Массив();
Партии = Новый Массив();

#КонецОбласти