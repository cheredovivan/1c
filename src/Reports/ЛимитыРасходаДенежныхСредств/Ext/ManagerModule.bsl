#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//	СтрокаТаблицыЗначений, Неопределено - сформированная команда для вывода в подменю.
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьЛимитыРасходаДенежныхСредств") Тогда
		
		Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ЛимитыРасходаДенежныхСредств)
			//++ НЕ УТ
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьЛимитыРасходаДенежныхСредствБюджетирования")
			//-- НЕ УТ
			Тогда
			
			КомандаОтчет = КомандыОтчетов.Добавить();
			
			КомандаОтчет.Менеджер = Метаданные.Отчеты.ЛимитыРасходаДенежныхСредств.ПолноеИмя();
			КомандаОтчет.Представление = НСтр("ru = 'Лимиты расхода ДС';
												|en = 'Cash flow limits'");
			
			КомандаОтчет.МножественныйВыбор = Ложь;
			КомандаОтчет.Важность = "Обычное";
			КомандаОтчет.КлючВарианта = "ЛимитыРасходаДенежныхСредствКонтекст";
			
			Возврат КомандаОтчет;
			
		КонецЕсли;
		
	КонецЕсли;
	
	//++ НЕ УТ
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ЛимитыРасходаДенежныхСредствПоДаннымБюджетирования)
		И ПолучитьФункциональнуюОпцию("ИспользоватьЛимитыРасходаДенежныхСредствБюджетирования") Тогда
		
		КонтрольЛимитов2_5 = ПолучитьФункциональнуюОпцию("КонтрольЛимитовПоДаннымБюджетирования2_5");
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		Если КонтрольЛимитов2_5 Тогда
			КомандаОтчет.Менеджер = Метаданные.Отчеты.ЛимитыПоДаннымБюджетирования.ПолноеИмя();
			КомандаОтчет.Представление = НСтр("ru = 'Лимиты по данным бюджетирования';
												|en = 'Budgeting limits'");
		Иначе
			КомандаОтчет.Менеджер = Метаданные.Отчеты.ЛимитыРасходаДенежныхСредствПоДаннымБюджетирования.ПолноеИмя();
			КомандаОтчет.Представление = НСтр("ru = 'Лимиты расхода ДС';
												|en = 'Cash flow limits'");
		КонецЕсли;
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		Если КонтрольЛимитов2_5 Тогда
			КомандаОтчет.КлючВарианта = "ИспользованияЛимитовПоДаннымБюджетированияКонтекст";
		Иначе
			КомандаОтчет.КлючВарианта = "ИспользованияЛимитовРасходаДенежныхСредствКонтекст";
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	//-- НЕ УТ

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ЛимитыРасходаДенежныхСредств);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'План-фактный анализ использования лимитов за период.';
									|en = 'View variance analysis of cash flow limits over the period.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ИспользованияЛимитовРасходаДенежныхСредств");
	ОписаниеВарианта.Описание = НСтр("ru = 'План-фактный анализ использования лимитов за период.
		|По каким статьям ДДС есть превышение или лимит израсходован не полностью?';
		|en = 'View variance analysis of cash flow limits over the period.
		|Check which cash flow items have an exceeded limit or not fully reached limit.'");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ЛимитыРасходаДенежныхСредствКонтекст");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли