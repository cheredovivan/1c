#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - из:
//    *Менеджер - Строка -
//    *Представление - Строка -
//    *МножественныйВыбор - Булево - 
//    *Важность - Строка -
//    *КлючВарианта - Строка -
Функция ДобавитьКомандуУкомплектованностиПоЗаказу(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.УкомплектованностьЗаказов) 
			И ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.УкомплектованностьЗаказов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Укомплектованность заказа';
											|en = 'Order completeness'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ПоЗаказу";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// Возвращаемое значение:
//   СтрокаТаблицыЗначений - из:
//    *Менеджер - Строка -
//    *Представление - Строка -
//    *МножественныйВыбор - Булево - 
//    *Важность - Строка -
//    *КлючВарианта - Строка -
Функция ДобавитьКомандуУкомплектованностиПоНоменклатуре(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.УкомплектованностьЗаказов) 
			И ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.УкомплектованностьЗаказов.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Укомплектованность заказов на производство';
											|en = 'Production order completeness'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		КомандаОтчет.КлючВарианта = "ПоНоменклатуре";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.УкомплектованностьЗаказов);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ПоВсемЗаказам");
	ОписаниеВарианта.Описание = НСтр("ru = 'Оценка укомплектованности заказов материалами и полуфабрикатами.
		|По каким заказам есть проблемы с обеспечением?
		|Какая часть материалов и полуфабрикатов есть в наличии, будет или не будет в срок?';
		|en = 'Estimate whether orders are equipped with materials and semi-finished products.
		|Check what orders have supply issues.
		|Verify if all materials and semi-finished products are available. If not, check if they will be supplied on time.'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ПоЗаказу");
	ОписаниеВарианта.Наименование = НСтр("ru = 'Укомплектованность заказа на производство';
										|en = 'Production order completeness'");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ укомплектованности заказа материалами и полуфабрикатами.
		|По каким материалам и полуфабрикатам есть проблемы с обеспечением?
		|Какие материалы и полуфабрикаты есть в наличии, будут или не будут в срок?';
		|en = 'Analyze whether orders are equipped with materials and semi-finished products.
		|Check what materials and semi-finished products have supply issues.
		|Verify what materials and semi-finished product are available. If not, check if they will be supplied on time.'");
	
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ПоНоменклатуре");
	ОписаниеВарианта.Наименование = НСтр("ru = 'Укомплектованность заказов на производство по номенклатуре';
										|en = 'Production order completeness by items'");
	ОписаниеВарианта.Описание = НСтр("ru = 'Анализ укомплектованности заказов по выбранной номенклатуре
		|По каким заказам есть проблемы с обеспечением?';
		|en = 'Analyze order completeness by the selected items
		|Check what orders have supply issues.'");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ПоИсточникамИДатамКонтекст");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ПоЭтапуКонтекст");
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
		
#КонецЕсли