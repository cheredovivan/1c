#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область БухгалтерскиеОтчеты

// Описывает свойства отчета (манифест), декларирующий поддержанные в нем возможности подсистемы,
// включая реализованный программный интерфейс.
//
// Возвращаемое значение:
// 	Структура - Описание:
// * ИспользоватьПослеВыводаРезультата - Булево -
// * ИспользоватьПриВыводеЗаголовка - Булево -
// * ИспользоватьВнешниеНаборыДанных - Булево -
// * ИспользоватьПослеКомпоновкиМакета - Булево -
// * ИспользоватьПередКомпоновкойМакета - Булево -
// * ИспользоватьДанныеРасшифровки - Булево -
Функция ПолучитьПараметрыИсполненияОтчета() Экспорт
	
	Манифест = Новый Структура;
	Манифест.Вставить("ИспользоватьДанныеРасшифровки",      Истина);
	Манифест.Вставить("ИспользоватьПередКомпоновкойМакета", Истина);
	Манифест.Вставить("ИспользоватьПослеКомпоновкиМакета",  Истина);
	Манифест.Вставить("ИспользоватьВнешниеНаборыДанных",    Ложь);
	Манифест.Вставить("ИспользоватьПриВыводеЗаголовка",     Истина);
	Манифест.Вставить("ИспользоватьПослеВыводаРезультата",  Истина);
	
	СправкиРасчеты.УстановитьОтчетНеИспользуетНаборыСуммовыхПоказателей(Манифест);

	Возврат Манифест;

КонецФункции

#КонецОбласти

#Область ОбработчикиБухгалтерскиеОтчеты

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПередКомпоновкойМакета.
//
// Параметры:
//  Контекст			 - Структура - контекст, в котором формируется отчет.
//                         См. СправкиРасчеты.КонтекстФормированияОтчета.
//  Схема				 - СхемаКомпоновкиДанных - схема отчета.
//  КомпоновщикНастроек	 - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//
Процедура ПередКомпоновкойМакета(Контекст, Схема, КомпоновщикНастроек) Экспорт
	
	РегистрыСведений.РасчетОтложенногоНалога.НастроитьПериодОтчета(
		КомпоновщикНастроек,
		Контекст.НачалоПериода,
		Контекст.КонецПериода,
		Контекст.Организация,
		Истина);
		
	ВключитьГруппировкуФилиал(КомпоновщикНастроек.Настройки.Структура, Контекст.Организация);
	
	СтавкиНалога = РегистрыСведений.ПримененныеСтавкиОтложенногоНалога.ПрочитатьСтавки(
		Контекст.КонецПериода,
		Контекст.Организация,
		?(ЗначениеЗаполнено(Контекст.ВариантНО), Контекст.ВариантНО, Неопределено),
		"%");
		
	Контекст.ВыполнениеОтчета.Вставить("СтавкиНалога", СтавкиНалога); // См. ПослеВыводаРезультата
	Контекст.ВыполнениеОтчета.Вставить("НачальныеДанныеОтчетнаяДата", 
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек,"НачальныеДанныеОтчетнаяДата").Значение);
	Контекст.ВыполнениеОтчета.Вставить("ОтчетнаяДата", 
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек,"ОтчетнаяДата").Значение);
	Контекст.ВыполнениеОтчета.Вставить("ПредыдущийРасчетОтчетнаяДата", 
		БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(КомпоновщикНастроек,"ПредыдущийРасчетОтчетнаяДата").Значение);
		
	ИспользуетсяРаздельныйУчет = РаздельныйУчетПоНалогуНаПрибыль.ИспользуетсяРаздельныйУчет(
		Контекст.Организация, Контекст.НачалоПериода);
		
	Контекст.ВыполнениеОтчета.Вставить("ВыводитьСтавкиНалога", НЕ (ИспользуетсяРаздельныйУчет И НЕ ЗначениеЗаполнено(Контекст.ВариантНО)));
		
	Если ТипЗнч(СтавкиНалога) = Тип("Структура") Тогда
		СтавкаТекущегоНалога   = СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ТекущийНалог] / 100;
		СтавкаПредыдущаяОценка = СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ПредыдущаяОценка] / 100;
	ИначеЕсли ТипЗнч(СтавкиНалога) = Тип("Число") Тогда
		СтавкаТекущегоНалога   = СтавкиНалога / 100;
		СтавкаПредыдущаяОценка = СтавкиНалога / 100;
	Иначе
		СтавкаТекущегоНалога   = 0;
		СтавкаПредыдущаяОценка = 0;
	КонецЕсли;
	
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"СтавкаТекущегоНалога",
		СтавкаТекущегоНалога);
		
	БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
		КомпоновщикНастроек,
		"СтавкаПредыдущаяОценка",
		СтавкаПредыдущаяОценка);
	
	НастроитьКолонкиОтчета(
		КомпоновщикНастроек,
		СтавкиНалога,
		Контекст.НачалоПериода,
		Контекст.КонецПериода,
		Контекст.Организация,
		Контекст.ВыполнениеОтчета.ВыводитьСтавкиНалога);
		
	НастроитьОтборПоОрганизации(КомпоновщикНастроек, Контекст.Организация);
	
	Если ЗначениеЗаполнено(Контекст.ВариантНО) Тогда
		БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек, "ВариантНО", Контекст.ВариантНО);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПослеКомпоновкиМакета.
//
// Параметры:
//  Контекст		 - Структура - контекст, в котором формируется отчет.
//                     См. СправкиРасчеты.КонтекстФормированияОтчета.
//  МакетКомпоновки	 - МакетКомпоновкиДанных - сформированный макет компоновки данных.
//
Процедура ПослеКомпоновкиМакета(Контекст, МакетКомпоновки) Экспорт
	
	СправкиРасчеты.НастроитьГрафыОтчета(МакетКомпоновки, ГрафыОтчета());
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПриВыводеЗаголовка.
//
// Параметры:
//  Контекст		 - Структура - контекст, в котором формируется отчет.
//                     См. СправкиРасчеты.КонтекстФормированияОтчета.
//  КомпоновщикНастроек	 - КомпоновщикНастроекКомпоновкиДанных - описание связи настроек и схемы отчета.
//  Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПриВыводеЗаголовка(Контекст, КомпоновщикНастроек, Результат) Экспорт
	
	СправкиРасчеты.ВывестиШапкуОтчета(Результат, Контекст);
	
КонецПроцедуры

// Обработчик события подсистемы БухгалтерскиеОтчеты.
// Вызов определяется параметром ИспользоватьПослеВыводаРезультата.
//
// Параметры:
//  Контекст  - Структура - контекст, в котором формируется отчет.
//              См. СправкиРасчеты.КонтекстФормированияОтчета.
//  Результат - ТабличныйДокумент - табличный документ с сформированным отчетом.
//
Процедура ПослеВыводаРезультата(Контекст, Результат) Экспорт
	
	Результат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	
	СправкиРасчеты.ОформитьРезультатОтчета(Результат, Контекст, Истина);
	
	ВывестиПорядокРасчета(
		Результат,
		Контекст.ВыполнениеОтчета,
		Контекст.НачалоПериода,
		Контекст.КонецПериода,
		Контекст.Организация);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТонкаяНастройка

Функция ИспользуютсяРазныеСтавки(СтавкиНалога)
	Возврат (ТипЗнч(СтавкиНалога) = Тип("Соответствие"));
КонецФункции

Процедура ВключитьГруппировкуФилиал(Группировки, Организация)
	
	ГруппировкаФилиалы = БухгалтерскиеОтчеты.НайтиГруппировкуТаблицы(Группировки, "Филиал");
	
	Если ГруппировкаФилиалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВсяОрганизация = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсяОрганизация(Организация);
	Если ВсяОрганизация.Количество() > 1 Тогда
		ГруппировкаФилиалы.Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Включен;
	Иначе
		ГруппировкаФилиалы.Состояние = СостояниеЭлементаНастройкиКомпоновкиДанных.Отключен;
	КонецЕсли;
	
КонецПроцедуры

Процедура НастроитьОтборПоОрганизации(КомпоновщикНастроек, Организация)
	
	// Отложенный налог рассчитывается по организации.
	// Поэтому для того, чтобы вывести данные по филиалу, следует
	// - отбор по полю Организация выполнять по головной организации
	// - дополнительно установить отбор по полю Филиал.
	
	ОписаниеОтбораПоОрганизации = Новый Структура;
	ОписаниеОтбораПоОрганизации.Вставить("Организация",                       Организация);
	ОписаниеОтбораПоОрганизации.Вставить("ВключатьОбособленныеПодразделения", Ложь);
	
	ДобавитьОтборФилиал(КомпоновщикНастроек.ФиксированныеНастройки.Отбор, ОписаниеОтбораПоОрганизации);
	
	БухгалтерскиеОтчетыВызовСервера.ДобавитьОтборПоОрганизации(ОписаниеОтбораПоОрганизации, КомпоновщикНастроек);
	
КонецПроцедуры

Процедура ДобавитьОтборФилиал(Отбор, ОписаниеОтбораПоОрганизации)
	
	Если Не ЗначениеЗаполнено(ОписаниеОтбораПоОрганизации.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ГоловнаяОрганизация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОписаниеОтбораПоОрганизации.Организация, "ГоловнаяОрганизация");
	
	Если Не ЗначениеЗаполнено(ГоловнаяОрганизация)
		Или ГоловнаяОрганизация = ОписаниеОтбораПоОрганизации.Организация Тогда
		Возврат;
	КонецЕсли;
	
	// Выводим данные только по филиалу
	ОтборФилиал = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборФилиал.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Филиал");
	ОтборФилиал.ВидСравнения     = ВидСравненияКомпоновкиДанных.Равно;
	ОтборФилиал.ПравоеЗначение   = ОписаниеОтбораПоОрганизации.Организация;
	ОтборФилиал.Использование    = Истина;
	ОтборФилиал.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	// Отложенный налог рассчитывается по головной организации
	ОписаниеОтбораПоОрганизации.Организация = ГоловнаяОрганизация;
	
КонецПроцедуры

Процедура НастроитьКолонкиОтчета(КомпоновщикНастроек, СтавкиНалога, НачалоПериода, КонецПериода, Организация, ВыводитьСтавкиНалога)
	
	ИспользованиеКолонок = Новый Соответствие; // См. СправкиРасчеты.ИспользованиеКолонокСуммовыхПоказателей
		
	ИспользуютсяРазныеСтавки = ИспользуютсяРазныеСтавки(СтавкиНалога);
	
	ИспользованиеКолонок.Вставить("НаНачалоПериода",        Истина);
	ИспользованиеКолонок.Вставить("НаКонецПериода",         Истина);
	ИспользованиеКолонок.Вставить("ЗаОтчетныйПериод",       ИспользуютсяРазныеСтавки);
	ИспользованиеКолонок.Вставить("ЗаМесяц",                ИспользуютсяРазныеСтавки И НачалоМесяца(НачалоПериода) <> НачалоМесяца(КонецПериода));
	ИспользованиеКолонок.Вставить("ЗаОтчетныйПериодКратко", НЕ ИспользуютсяРазныеСтавки);
	ИспользованиеКолонок.Вставить("ЗаМесяцКратко",          НЕ ИспользуютсяРазныеСтавки И НачалоМесяца(НачалоПериода) <> НачалоМесяца(КонецПериода));
	
	СправкиРасчеты.НастроитьИспользованиеКолонокСуммовыхПоказателей(
		КомпоновщикНастроек,
		ИспользованиеКолонок);
	
	ТекстыЗаголовковГруппКолонок = ЗаголовкиГруппКолонок(СтавкиНалога, НачалоПериода, КонецПериода, ВыводитьСтавкиНалога);
	
	БухгалтерскиеОтчеты.УстановитьЗаголовкиГруппКолонок(КомпоновщикНастроек, ТекстыЗаголовковГруппКолонок);
		
КонецПроцедуры

Функция ГрафыОтчета()
	
	Разделы = Новый Соответствие;
	
	// Заголовки строк
	Разделы.Вставить(
		СправкиРасчеты.СлужебноеИмяЗаголовкиСтрок(),
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НСтр("ru = '1';
															|en = '1'")));
	
	// "НаНачалоПериода"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '2';
								|en = '2'")); // СуммаОперацийНачало
	ГрафыРаздела.Добавить(НСтр("ru = '3';
								|en = '3'")); // НалогНачало
	
	Разделы.Вставить("НаНачалоПериода", ГрафыРаздела);
	
	// "НаКонецПериода"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '4';
								|en = '4'")); // СуммаОперацийКонец
	ГрафыРаздела.Добавить(НСтр("ru = '5';
								|en = '5'")); // НалогКонец
	
	Разделы.Вставить("НаКонецПериода", ГрафыРаздела);
		
	// "ЗаОтчетныйПериод"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '6';
								|en = '6'")); // ИзменениеСуммыОпераций
	ГрафыРаздела.Добавить(НСтр("ru = '7';
								|en = '7'")); // ИзменениеСуммыНалога
	ГрафыРаздела.Добавить(НСтр("ru = '8';
								|en = '8'")); // НалогНаПрибыльОтОперацийКоторыеНеВключаютсяВЧистуюПрибыль
	ГрафыРаздела.Добавить(НСтр("ru = '9';
								|en = '9'")); // ЭффектВключенияСуммыОперацииВНераспределеннуюПрибыль
	ГрафыРаздела.Добавить(НСтр("ru = '10';
								|en = '10'")); // ЭффектИзмененияСтавкиТекущегоНалога
	ГрафыРаздела.Добавить(НСтр("ru = '11';
								|en = '11'")); // ЭффектИзмененияСтавкиБудущегоНалога
	
	Разделы.Вставить("ЗаОтчетныйПериод", ГрафыРаздела);
	
	// "ЗаМесяц"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '12';
								|en = '12'")); // ИзменениеСуммыОперацийЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '13';
								|en = '13'"));  // ИзменениеСуммыНалогаЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '14';
								|en = '14'")); // НалогНаПрибыльОтОперацийКоторыеНеВключаютсяВЧистуюПрибыльЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '15';
								|en = '15'")); // ЭффектВключенияСуммыОперацииВНераспределеннуюПрибыльЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '16';
								|en = '16'")); // ЭффектИзмененияСтавкиТекущегоНалогаЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '17';
								|en = '17'")); // ЭффектИзмененияСтавкиБудущегоНалогаЗаМесяц
	
	Разделы.Вставить("ЗаМесяц", ГрафыРаздела);
	
	// "ЗаОтчетныйПериодКратко"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '6';
								|en = '6'")); // ИзменениеСуммыОпераций
	ГрафыРаздела.Добавить(НСтр("ru = '7';
								|en = '7'")); // ИзменениеСуммыНалога
	ГрафыРаздела.Добавить(НСтр("ru = '8';
								|en = '8'")); // НалогНаПрибыльОтОперацийКоторыеНеВключаютсяВЧистуюПрибыль
	ГрафыРаздела.Добавить(НСтр("ru = '9';
								|en = '9'")); // ЭффектВключенияСуммыОперацииВНераспределеннуюПрибыль
	
	Разделы.Вставить("ЗаОтчетныйПериодКратко", ГрафыРаздела);
	
	// "ЗаМесяцКратко"
	ГрафыРаздела = Новый Массив;
	ГрафыРаздела.Добавить(НСтр("ru = '10';
								|en = '10'")); // ИзменениеСуммыОперацийЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '11';
								|en = '11'")); // ИзменениеСуммыНалогаЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '12';
								|en = '12'")); // НалогНаПрибыльОтОперацийКоторыеНеВключаютсяВЧистуюПрибыльЗаМесяц
	ГрафыРаздела.Добавить(НСтр("ru = '13';
								|en = '13'")); // ЭффектВключенияСуммыОперацииВНераспределеннуюПрибыльЗаМесяц
	
	Разделы.Вставить("ЗаМесяцКратко", ГрафыРаздела);

	Возврат Разделы;
	
КонецФункции

Функция ЗаголовкиГруппКолонок(СтавкиНалога, НачалоПериода, КонецПериода, ВыводитьСтавкиНалога)
	
	Заголовки = Новый Структура;
	Заголовки.Вставить("НаНачалоПериода",        "");
	Заголовки.Вставить("НаКонецПериода",         "");
	Заголовки.Вставить("ЗаОтчетныйПериод",       "");
	Заголовки.Вставить("ЗаМесяц",                "");
	Заголовки.Вставить("ЗаОтчетныйПериодКратко", "");
	Заголовки.Вставить("ЗаМесяцКратко",          "");
	
	ИспользуютсяРазныеСтавки = ИспользуютсяРазныеСтавки(СтавкиНалога) И ВыводитьСтавкиНалога;
	
	Если ИспользуютсяРазныеСтавки Тогда
		ШаблонГраницаПериода = НСтр("ru = 'На [ОтчетнаяДата], по ставке [Ставка]%';
									|en = 'As of [ОтчетнаяДата], at rate of [Ставка]%'");
	Иначе
		ШаблонГраницаПериода = НСтр("ru = 'На [ОтчетнаяДата]';
									|en = 'As of [ОтчетнаяДата]'");
	КонецЕсли;
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("ОтчетнаяДата", Формат(НачалоПериода, "ДФ=MM.yyyy; ДЛФ=D;"));
	Если ИспользуютсяРазныеСтавки Тогда
		ПараметрыТекста.Вставить("Ставка", СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ПредыдущаяОценка]);
	КонецЕсли;
	
	Заголовки.НаНачалоПериода = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонГраницаПериода, ПараметрыТекста);
	
	ПараметрыТекста = Новый Структура;
	ПараметрыТекста.Вставить("ОтчетнаяДата", Формат(КонецДня(КонецПериода) + 1, "ДФ=MM.yyyy; ДЛФ=D;"));
	Если ИспользуютсяРазныеСтавки Тогда
		ПараметрыТекста.Вставить("Ставка", СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.БудущийНалог]);
	КонецЕсли;
	
	Заголовки.НаКонецПериода = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(ШаблонГраницаПериода, ПараметрыТекста);
	
	ПараметрыТекста = Новый Структура;
	Если ИспользуютсяРазныеСтавки Тогда
		ПараметрыТекста.Вставить("Ставка", СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ТекущийНалог]);
	КонецЕсли;
	
	Заголовки.ЗаОтчетныйПериод = НСтр("ru = 'За отчетный период';
										|en = 'For accounting period'");
	Заголовки.ЗаОтчетныйПериодКратко = Заголовки.ЗаОтчетныйПериод;
		
	Заголовки.ЗаМесяц = СтрШаблон(НСтр("ru = 'За %1';
										|en = 'For %1'"), Формат(КонецПериода, "ДФ=MM.yyyy;"));
	Заголовки.ЗаМесяцКратко = Заголовки.ЗаМесяц;
		
	Возврат Заголовки;
		
КонецФункции

Функция ПараметрыПорядкаРасчета()
	
	ПараметрыПорядкаРасчета = Новый Структура;
	ПараметрыПорядкаРасчета.Вставить("ОтчетнаяДата");
	ПараметрыПорядкаРасчета.Вставить("НачальныеДанныеОтчетнаяДата");
	ПараметрыПорядкаРасчета.Вставить("ПредыдущийРасчетОтчетнаяДата");
	ПараметрыПорядкаРасчета.Вставить("СтавкаНалогаПредыдущаяОценка");
	ПараметрыПорядкаРасчета.Вставить("СтавкаНалогаБудущийНалог");
	ПараметрыПорядкаРасчета.Вставить("СтавкаНалогаТекущийНалог");
	
	Возврат ПараметрыПорядкаРасчета;
	
КонецФункции

Процедура ВывестиПорядокРасчета(ТабличныйДокумент, ВыполнениеОтчета, НачалоПериода, КонецПериода, Организация)
		
	СтавкиНалога = ВыполнениеОтчета.СтавкиНалога;
	ВыводитьСтавкиНалога = ВыполнениеОтчета.ВыводитьСтавкиНалога;
	ИспользуютсяРазныеСтавки = ИспользуютсяРазныеСтавки(СтавкиНалога);
	
	ПараметрыПорядкаРасчета = ПараметрыПорядкаРасчета();
	
	ПараметрыПорядкаРасчета.ОтчетнаяДата = Формат(ВыполнениеОтчета.ОтчетнаяДата, "ДФ=MM.yyyy;");
	ПараметрыПорядкаРасчета.НачальныеДанныеОтчетнаяДата = Формат(ВыполнениеОтчета.НачальныеДанныеОтчетнаяДата, "ДФ=MM.yyyy;");
	ПараметрыПорядкаРасчета.ПредыдущийРасчетОтчетнаяДата = Формат(ВыполнениеОтчета.ПредыдущийРасчетОтчетнаяДата, "ДФ=MM.yyyy;");
		
	Если НЕ ВыводитьСтавкиНалога Тогда
		ПараметрыПорядкаРасчета.СтавкаНалогаПредыдущаяОценка = НСтр("ru = 'Ставка налога';
																	|en = 'Tax rate'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаБудущийНалог = НСтр("ru = 'Ставка налога';
																|en = 'Tax rate'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаТекущийНалог = НСтр("ru = 'Ставка налога';
																|en = 'Tax rate'");							
	ИначеЕсли ИспользуютсяРазныеСтавки Тогда
		ПараметрыПорядкаРасчета.СтавкаНалогаПредыдущаяОценка = Формат(СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ПредыдущаяОценка], "ЧН=0; ЧФ='Ч %'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаБудущийНалог = Формат(СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.БудущийНалог], "ЧН=0; ЧФ='Ч %'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаТекущийНалог = Формат(СтавкиНалога[Перечисления.ВидыСтавокОтложенногоНалога.ТекущийНалог], "ЧН=0; ЧФ='Ч %'");					
	Иначе
		ПараметрыПорядкаРасчета.СтавкаНалогаПредыдущаяОценка = Формат(СтавкиНалога, "ЧН=0; ЧФ='Ч %'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаБудущийНалог = Формат(СтавкиНалога, "ЧН=0; ЧФ='Ч %'");			
		ПараметрыПорядкаРасчета.СтавкаНалогаТекущийНалог = Формат(СтавкиНалога, "ЧН=0; ЧФ='Ч %'");			
	КонецЕсли;
	
	Шаблон = Отчеты.СправкаРасчетНалогаНаПрибыльОтОперацийРезультатКоторыхНеВключаетсяВПрибыль.ПолучитьМакет("ПорядокРасчета");
	
	// Заголовок
	ШаблонЗаголовок = Шаблон.ПолучитьОбласть("Заголовок");
	ТабличныйДокумент.Вывести(ШаблонЗаголовок);
	
	ШаблонРасчет = Шаблон.ПолучитьОбласть("Расчет");
	ШаблонРасчет.Параметры.Заполнить(ПараметрыПорядкаРасчета);
	ТабличныйДокумент.Вывести(ШаблонРасчет);
	
	Если ИспользуютсяРазныеСтавки Тогда
		ШаблонРазныеСтавкиНалога = Шаблон.ПолучитьОбласть("РазныеСтавкиНалога");
		ШаблонРазныеСтавкиНалога.Параметры.Заполнить(ПараметрыПорядкаРасчета);
		ТабличныйДокумент.Вывести(ШаблонРазныеСтавкиНалога);	
		
		ИмяШаблонаПредыдущийРасчет = "РазныеСтавкиНалогаПредыдущийРасчет";
			
	Иначе
		ШаблонОднаСтавкаНалога = Шаблон.ПолучитьОбласть("ОднаСтавкаНалога");
		ШаблонОднаСтавкаНалога.Параметры.Заполнить(ПараметрыПорядкаРасчета);
		ТабличныйДокумент.Вывести(ШаблонОднаСтавкаНалога);	

		ИмяШаблонаПредыдущийРасчет = "ОднаСтавкаНалогаПредыдущийРасчет";		
	КонецЕсли;
	
	Если НачалоМесяца(НачалоПериода) <> НачалоМесяца(КонецПериода) Тогда
		ШаблонПредыдущийРасчет = Шаблон.ПолучитьОбласть(ИмяШаблонаПредыдущийРасчет);
		ШаблонПредыдущийРасчет.Параметры.Заполнить(ПараметрыПорядкаРасчета);
		ТабличныйДокумент.Вывести(ШаблонПредыдущийРасчет);				
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли