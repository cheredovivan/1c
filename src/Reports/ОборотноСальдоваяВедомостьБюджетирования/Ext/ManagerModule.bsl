#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Для подсистемы "Варианты отчетов" при работе в модели сервиса.
// Возвращаемое значение:
//  Массив - Содержит массив структур со свойствами:
//     * ВариантОтчета - Структура - Вариант отчета.
//        Имя             - Строка - Имя варианта отчета.
//        Представление   - Строка - Представление варианта отчета.
//
Функция ВариантыНастроек() Экспорт
	
	Результат = Новый Массив;
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "ОборотноСальдоваяВедомость");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотно-сальдовая ведомость по показателям бюджетов';
											|en = 'Trial balance for budget balance items'"));
	Результат.Добавить(Вариант);
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "ОборотноСальдоваяВедомостьПоПоказателюБюджетов");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотно-сальдовая ведомость по показателю бюджетов';
											|en = 'Trial balance for the budget balance item'"));
	Результат.Добавить(Вариант);
	
	Вариант = Новый Структура;
	Вариант.Вставить("Имя",           "Расшифровка");
	Вариант.Вставить("Представление", НСтр("ru = 'Оборотно-сальдовая ведомость по показателям бюджетов';
											|en = 'Trial balance for budget balance items'"));
	Результат.Добавить(Вариант);
	
	Возврат Результат;
	
КонецФункции

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
// 
// Возвращаемое значение:
//     СтрокаТаблицыЗначений
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ОборотноСальдоваяВедомостьБюджетирования) 
			И ПолучитьФункциональнуюОпцию("ИспользоватьБюджетирование") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ОборотноСальдоваяВедомостьБюджетирования.ПолноеИмя();
		КомандаОтчет.Представление = НСтр("ru = 'Оборотно-сальдовая ведомость';
											|en = 'Trial balance'");
		
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Важное";
		КомандаОтчет.КлючВарианта = "ОборотноСальдоваяВедомостьПоПоказателюБюджетов";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;

	Возврат Неопределено;

КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   ВыводитьВариантыОтчетов - Булево.
//
Процедура НастроитьВариантыОтчета(Настройки, ВыводитьВариантыОтчетов) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ОборотноСальдоваяВедомостьБюджетирования);
	ОписаниеОтчета.ОпределитьНастройкиФормы = Истина;
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, ОписаниеОтчета, ВыводитьВариантыОтчетов);
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "");
	ОписаниеВарианта.Описание = НСтр("ru = 'Сравнение плановых и фактических сальдо и оборотов по показателям за период.';
									|en = 'Compare planned and actual balance and turnovers for the balance items over the period.'");
	ОписаниеВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, ОписаниеОтчета, "ОборотноСальдоваяВедомость");
	ОписаниеВарианта.Описание = НСтр("ru = 'Сравнение плановых и фактических сальдо и оборотов по показателям за период.';
									|en = 'Compare planned and actual balance and turnovers for the balance items over the period.'");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "ОборотноСальдоваяВедомостьПоПоказателюБюджетов");
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(Настройки, ОписаниеОтчета, "Расшифровка");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли