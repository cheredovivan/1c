// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//   НастройкиОтчета - см. ВариантыОтчетов.ОписаниеОтчета.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	ВыводитьВариантыОтчетов = Ложь;
	
	НастройкиОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ДействующиеРетроБонусыПоставщиков);
	НастройкиОтчета.ФункциональныеОпции.Добавить(Метаданные.ФункциональныеОпции.ИспользоватьРетроБонусыПоставщиков.Имя);
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(
		Настройки, НастройкиОтчета, "ДействующиеРетроБонусыПоставщиков");
	НастройкиВарианта.Описание = НСтр("ru = 'Какие условия ретро-бонусов поставщика действуют в указанном периоде?';
										|en = 'What vendor rebate conditions are valid during the specified period?'");
	НастройкиВарианта.Размещение.Вставить(Метаданные.Подсистемы.Закупки.Подсистемы.РетроБонусыПоставщиков);
	
	ВариантыОтчетов.УстановитьРежимВыводаВПанеляхОтчетов(Настройки, НастройкиОтчета, ВыводитьВариантыОтчетов);
	
	ВариантыОтчетовУТПереопределяемый.ОтключитьВариантОтчета(
		Настройки,
		НастройкиОтчета,
		"ДействующиеРетроБонусыПоставщиковКонтекст");
	
КонецПроцедуры

#КонецОбласти

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - см. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//   КлючВарианта - Строка - 
//   
//	Возвращаемое значение:
//		СтрокаТаблицыЗначений - описание созданной команды.
//
Функция ДобавитьКомандуОтчета(КомандыОтчетов, Параметры, КлючВарианта = "") Экспорт
	
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДействующиеРетроБонусыПоставщиков)
	   И ПолучитьФункциональнуюОпцию("ИспользоватьРетроБонусыПоставщиков") Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		Представление = НСтр("ru = 'Действующие ретро-бонусы поставщика';
							|en = 'Active vendor rebates'");
		КомандаОтчет.Менеджер = Метаданные.Отчеты.ДействующиеРетроБонусыПоставщиков.ПолноеИмя();
		КомандаОтчет.Представление = Представление;
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность = "Обычное";
		
		Если ЗначениеЗаполнено(КлючВарианта) Тогда
			
			КомандаОтчет.КлючВарианта = КлючВарианта;
			
		КонецЕсли;
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли