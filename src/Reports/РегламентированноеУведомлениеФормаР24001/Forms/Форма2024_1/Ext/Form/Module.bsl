#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Данные = Неопределено;
	ПараметрыЗаполнения = Неопределено;
	Параметры.Свойство("Данные", Данные);
	Параметры.Свойство("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	Объект.ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаР24001;
	УведомлениеОСпецрежимахНалогообложения.НачальныеОперацииПриСозданииНаСервере(ЭтотОбъект);
	УведомлениеОСпецрежимахНалогообложения.СформироватьСпискиВыбора(ЭтотОбъект, "СпискиВыбора2024_1");
	
	Если ТипЗнч(Данные) = Тип("Структура") Тогда
		СформироватьДеревоСтраниц();
		УведомлениеОСпецрежимахНалогообложения.СформироватьСтруктуруДанныхУведомленияНовогоОбразца(ЭтотОбъект);
		УведомлениеОСпецрежимахНалогообложения.ЗагрузитьДанныеПростогоУведомления(ЭтотОбъект, Данные, ПредставлениеУведомления)
	ИначеЕсли Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Организация = Параметры.Ключ.Организация;
		ЗагрузитьДанные(Параметры.Ключ);
	ИначеЕсли Параметры.Свойство("ЗначениеКопирования") И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.Организация = Параметры.ЗначениеКопирования.Организация;
		ЗагрузитьДанные(Параметры.ЗначениеКопирования);
	Иначе
		Параметры.Свойство("Организация", Объект.Организация);
		СформироватьДеревоСтраниц();
		
		ВходящийКонтейнер = Новый Структура("ИмяФормы, ДеревоСтраниц", ИмяФормы, РеквизитФормыВЗначение("ДеревоСтраниц"));
		РезультатКонтейнер = Новый Структура;
		УведомлениеОСпецрежимахНалогообложения.СформироватьКонтейнерДанныхУведомления(ВходящийКонтейнер, РезультатКонтейнер, Истина);
		Для Каждого КЗ Из РезультатКонтейнер Цикл
			ЭтотОбъект[КЗ.Ключ] = КЗ.Значение;
		КонецЦикла;
		
		РезультатКонтейнер.Очистить();
		
		ИнициализироватьМногострочныеЧасти(ВходящийКонтейнер, РезультатКонтейнер);
		
		Для Каждого КЗ Из РезультатКонтейнер Цикл
			ЗначениеВРеквизитФормы(КЗ.Значение, КЗ.Ключ);
		КонецЦикла;
		
		УведомлениеОСпецрежимахНалогообложения.ДополнитьСлужебнымиСтруктурамиАдреса(ЭтотОбъект["ДанныеУведомления"], ЭтотОбъект["ДанныеМногостраничныхРазделов"]);
		
	КонецЕсли;
	
	Если Параметры.СформироватьФормуОтчетаАвтоматически Тогда
		ЗаполнитьАвтоНаСервере(ПараметрыЗаполнения);
	КонецЕсли;
	
	Если Параметры.СформироватьПечатнуюФорму Тогда
		Модифицированность = Истина;
		СохранитьДанные();
		Отказ = Истина;
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			РазблокироватьДанныеДляРедактирования(Объект.Ссылка, УникальныйИдентификатор);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиентСервер.ПриИнициализацииФормыРегламентированногоОтчета(ЭтотОбъект, "РегистрацияЮЛФНС");
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		СохраненныеДанныеУведомления = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ДанныеУведомления").Получить();
		УведомлениеЗаполненоВПомощнике = СохраненныеДанныеУведомления.Свойство("ДанныеПомощникаЗаполнения") 
			И ТипЗнч(СохраненныеДанныеУведомления.ДанныеПомощникаЗаполнения) = Тип("Структура")
			И ЗначениеЗаполнено(СохраненныеДанныеУведомления.ДанныеПомощникаЗаполнения);
	Иначе
		УведомлениеЗаполненоВПомощнике = Ложь;
	КонецЕсли;
	
	ИнтернетПоддержкаПодключена = ОбщегоНазначения.РазделениеВключено();
	Если Не ИнтернетПоддержкаПодключена
		И Не Элементы.ПредставлениеУведомления.ТолькоПросмотр 
		И ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда 
		
		ДанныеИнтернетПоддержки = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		ИнтернетПоддержкаПодключена = ТипЗнч(ДанныеИнтернетПоддержки) = Тип("Структура")
			И ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИнтернетПоддержки, "Логин"))
			И ЗначениеЗаполнено(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИнтернетПоддержки, "Пароль"));
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	ИныеДокументы = РегистрационныеФормыКлиентСервер.ПредставлениеЗаполненногоПоляПриложенныхФайлов(Объект.Ссылка, "ИныеДокументы");
	СканЗаявление = РегистрационныеФормыКлиентСервер.ПредставлениеЗаполненногоПоляПриложенныхФайлов(Объект.Ссылка, "СканЗаявление");
	ПриИзмененииОпцийСдачи(ЭтотОбъект);
	ИнициализироватьВыводСтатусовИВажныхСообщений_85();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	РегламентированнаяОтчетностьКлиент.ПередЗакрытиемРегламентированногоОтчета(
		ЭтотОбъект, Отказ, СтандартнаяОбработка, ЗавершениеРаботы, ТекстПредупреждения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", , Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", Объект.Организация, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "УведомлениеОСпецрежимахНалогообложения_НавигацияПоОшибкам" Тогда
		УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаОповещенияНавигацииПоОшибкам(
			ЭтотОбъект, Параметр, Источник);
	КонецЕсли;
	Если ИмяСобытия = "Завершение отправки" Или 
		ИмяСобытия = "Завершение отправки в контролирующий орган" Или
		ИмяСобытия = "Успешная отправка" Тогда
		ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере();	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГиперссылкаНаименованиеЭтапаНажатие(Элемент)
	
	ПараметрыИзменения = Новый Структура;
	ПараметрыИзменения.Вставить("Форма", ЭтотОбъект);
	ПараметрыИзменения.Вставить("Организация", Объект.Организация);
	ПараметрыИзменения.Вставить("КонтролирующийОрган",
		ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РегистрацияЮЛФНС"));
	ПараметрыИзменения.Вставить("ТекстВопроса", НСтр("ru = 'Вы уверены, что уведомление уже сдано?';
													|en = 'Вы уверены, что уведомление уже сдано?'"));
	
	РегламентированнаяОтчетностьКлиент.ИзменитьСтатусОтправки(ПараметрыИзменения);
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаПротоколНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьПротоколИзПанелиОтправки(ЭтотОбъект, "РегистрацияЮЛФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыОтправкиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьСостояниеОтправкиИзПанелиОтправки(ЭтотОбъект, "РегистрацияЮЛФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент,
	НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьКритическиеОшибкиИзПанелиОтправки(ЭтотОбъект, "РегистрацияЮЛФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтроки(Элемент)
	Если УведомлениеОСпецрежимахНалогообложенияКлиент.НеобходимоФормированиеТабличногоДокумента(ЭтотОбъект, Элемент, ЭтотОбъект["УИДПереключение"]) Тогда
		ОтключитьОбработчикОжидания("ДеревоСтраницПриАктивизацииСтрокиЗавершение");
		ПодключитьОбработчикОжидания("ДеревоСтраницПриАктивизацииСтрокиЗавершение", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСтраницПриАктивизацииСтрокиЗавершение() Экспорт 
	ПредУИД = ЭтотОбъект["УИДПереключение"];
	Элемент = Элементы.ДеревоСтраниц;
	
	ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета);
	Если Элемент.ТекущиеДанные.Многострочность Тогда
		Для Каждого СтрокаМСЧ Из Элемент.ТекущиеДанные.МногострочныеЧасти Цикл
			ИмяМСЧ = СтрокаМСЧ.Значение;
			ВывестиМногострочнуюЧасть(ИмяМСЧ);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияВыбор(Элемент, Область, СтандартнаяОбработка)
	
	ИмяОбласти = Область.Имя;
	
	Если СтрЧислоВхождений(ИмяОбласти, "ДобавитьСтроку") > 0 Тогда
		ДобавитьСтроку(Неопределено);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	ИначеЕсли СтрЧислоВхождений(ИмяОбласти, "УдалитьСтроку") > 0 Тогда
		УдалитьСтроку(Неопределено);
		СтандартнаяОбработка = Ложь;
		Модифицированность = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект["РучнойВвод"] Тогда
		Возврат;
	КонецЕсли;
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПредставлениеУведомленияВыбор(ЭтотОбъект, Область, СтандартнаяОбработка);
	
	Если СтрНайти(ИмяОбласти, "В0101") = 1 Тогда
		ОбработкаАдреса(Область, СтандартнаяОбработка);
		Возврат;
	КонецЕсли;
	
	Если СтандартнаяОбработка И ЭтоОбластьОКСМ(Область) Тогда
		УведомлениеОСпецрежимахНалогообложенияКлиент.ВыборКодаСтраныИзСправочника(ЭтотОбъект, Область, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриИзмененииСодержимогоОбласти(Элемент, Область)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(ЭтотОбъект, Область);
	Если СтрНачинаетсяС(Область.Имя, "Д01010200_") Тогда 
		Постфикс = Число(СтрЗаменить(Область.Имя, "Д01010200_", ""));
		МногострочнаяЧастьД010102[Постфикс-1].Д01010200 = Область.Значение;
		Модифицированность = Истина;
	ИначеЕсли СтрНачинаетсяС(Область.Имя, "Д01020200_") Тогда 
		Постфикс = Число(СтрЗаменить(Область.Имя, "Д01020200_", ""));
		МногострочнаяЧастьД010202[Постфикс-1].Д01020200 = Область.Значение;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУведомленияПриАктивизации(Элемент)
	Область = ПредставлениеУведомления.ТекущаяОбласть;
	Элементы.ПредставлениеУведомленияКонтекстноеМенюОчиститьКодОКСМ.Доступность = ЭтоОбластьОКСМ(Область);
	НастроитьКонтекстноеМенюПредставленияУведомления(Область.Имя);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьXML(Команда)
	
	ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор);
	Если ВыгружаемыеДанные <> Неопределено Тогда
		РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтправку(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОбновитьОтправкуИзПанелиОтправки(ЭтотОбъект, "РегистрацияЮЛФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВКонтролирующийОрган(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНеотправленноеИзвещение(Команда)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьНеотправленноеИзвещениеИзПанелиОтправки(
		ЭтотОбъект, "РегистрацияЮЛФНС");
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьКодОКСМ(Команда)
	
	ПредставлениеУведомления.ТекущаяОбласть.Значение = "";
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПриИзмененииСодержимогоОбласти(
		ЭтотОбъект, ПредставлениеУведомления.ТекущаяОбласть);
	
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОчиститьУведомление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьБРО(Команда)
	ЕстьВыходЗаГраницы = Ложь;
	ПечатьБРОНаСервере(ЕстьВыходЗаГраницы);
	РегламентированнаяОтчетностьКлиент.ОткрытьФормуПредварительногоПросмотра(
		ЭтотОбъект, "Открыть", Ложь, ЭтотОбъект["СтруктураРеквизитовУведомления"].СписокПечатаемыхЛистов,
		Новый Структура("ЕстьВыходЗаГраницы", ЕстьВыходЗаГраницы));
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСДвухмернымШтрихкодомPDF417(Команда)
	Если ДоступнаПечатьPDF417(ИмяФормы) Тогда
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФормуУведомленияОСпецрежимахПоСсылке(ЭтотОбъект, Объект.Ссылка);
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Печать формы со штрихкодом PDF417 будет реализована в будущих версиях программы.';
									|en = 'Печать формы со штрихкодом PDF417 будет реализована в будущих версиях программы.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	РегламентированнаяОтчетностьКлиент.ПроверитьВИнтернете(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ПроверитьВыгрузку(ЭтотОбъект, ПроверитьВыгрузкуНаСервере());
КонецПроцедуры

&НаКлиенте
Процедура РучнойВвод(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаРучнойВвод(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаКлиенте();
	Оповестить("Запись_УведомлениеОСпецрежимахНалогообложения", , Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСтроку(Команда)
	
	Область = Элементы.ПредставлениеУведомления.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	
	Если СтрНайти(ИмяОбласти, "ДобавитьСтроку") = 1 Тогда
		ИмяОбластиДобавленияСтроки = ИмяОбласти;
		ИдМСЧ = СтрЗаменить(ИмяОбласти, "ДобавитьСтроку", "");
		ИдМСЧ = СтрЗаменить(ИдМСЧ, "Значок", "");
	Иначе
		ПолноеИмяМСЧ = ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти);
		ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
		ИмяОбластиДобавленияСтроки = "ДобавитьСтроку" + ИдМСЧ;
	КонецЕсли;
	
	ПроверкаМаксимумаСтрок = ПроверитьДостижениеМаксимальногоКоличестваСтрок(ИдМСЧ);
	
	Если ПроверкаМаксимумаСтрок.ДостигнутМаксимум Тогда
		ПоказатьПредупреждение( , ПроверкаМаксимумаСтрок.ТекстПредупреждения);
	Иначе
		ДобавитьСтрокуНаСервере(ИмяОбластиДобавленияСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьСтроку(Команда)
	Область = Элементы.ПредставлениеУведомления.ТекущаяОбласть;
	ИмяОбласти = Область.Имя;
	
	Если СтрНайти(ИмяОбласти, "УдалитьСтроку") = 1 Тогда
		ИмяОбластиУдаленияСтроки = ИмяОбласти;
	Иначе
		ПолноеИмяМСЧ = ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти);
		ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
		Постфикс = Сред(ИмяОбласти, СтрНайти(ИмяОбласти, "_"));
		ИмяОбластиУдаленияСтроки = "УдалитьСтроку" + ИдМСЧ + Постфикс;
	КонецЕсли;
	
	Если НЕ ПредставлениеУведомления.Области[ИмяОбластиУдаленияСтроки].Гиперссылка Тогда
		Возврат;
	КонецЕсли;
	
	Если Ждать ВопросАсинх("Удалить выбранную строку?", РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Да Тогда
		УдалитьСтрокуНаСервере(ИмяОбластиУдаленияСтроки);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеЭлементамиФормы

&НаКлиенте
Процедура НастроитьКонтекстноеМенюПредставленияУведомления(ИмяОбласти)
	
	ЭтоПоказательМСЧ = СтрНайти(ИмяОбласти, "В010107") = 1
	               ИЛИ СтрНайти(ИмяОбласти, "Д010102") = 1
	               ИЛИ СтрНайти(ИмяОбласти, "Д010202") = 1;
	
	ЭтоЕдинственнаяСтрока = Истина;
	Если ЭтоПоказательМСЧ Тогда
		ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
		ИмяПоказателяВторойСтроки = ИмяБезПостфикса + "_2";
		ЭтоЕдинственнаяСтрока = (ПредставлениеУведомления.Области.Найти(ИмяПоказателяВторойСтроки) = Неопределено);
	КонецЕсли;
	
	Элементы.ПредставлениеУведомленияКонтекстноеМенюДобавитьСтроку.Доступность = ЭтоПоказательМСЧ;
	Элементы.ПредставлениеУведомленияКонтекстноеМенюУдалитьСтроку.Доступность =
		ЭтоПоказательМСЧ И НЕ ЭтоЕдинственнаяСтрока;
	
КонецПроцедуры

#КонецОбласти

#Область Многострочность

&НаСервере
Процедура ДобавитьСтрокуНаСервере(ИмяОбласти)
	
	ИдМСЧ = СтрЗаменить(ИмяОбласти, "ДобавитьСтроку", "");
	ИдМСЧ = СтрЗаменить(ИдМСЧ, "Значок", "");
	ПолноеИмяМСЧ = "МногострочнаяЧасть" + ИдМСЧ;
	
	ЭтотОбъект[ПолноеИмяМСЧ].Добавить();
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	НомерНовойСтроки = ТаблицаМСЧ.Количество();
	
	ИсходныйМакетРаздела = МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ);
	ОбластьМСЧ = ИсходныйМакетРаздела.ПолучитьОбласть(ПолноеИмяМСЧ);
	Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
		ИмяПоказателя = КолонкаМСЧ.Имя;
		ОбластьПоказателя = ОбластьМСЧ.Область(ИмяПоказателя + "_1");
		ОбластьПоказателя.Имя = ИмяПоказателя + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерНовойСтроки);
	КонецЦикла;
	ОбластьУдаленияСтроки = ОбластьМСЧ.Область("УдалитьСтроку" + ИдМСЧ + "_1");
	ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерНовойСтроки);
	
	ПозицияВставки = ПредставлениеУведомления.Область(ИмяОбласти).Верх;
	
	ПредставлениеУведомления.ВставитьОбласть(
		ОбластьМСЧ.Область(1, , ОбластьМСЧ.ВысотаТаблицы),
		ПредставлениеУведомления.Область(ПозицияВставки, , ПозицияВставки, ),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	
	ИмяОбластиДляАктивации = ТаблицаМСЧ.Колонки[0].Имя + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерНовойСтроки);
	Элементы.ПредставлениеУведомления.ТекущаяОбласть = ПредставлениеУведомления.Область(ИмяОбластиДляАктивации);
	
	Если ЭтотОбъект["ДоступнаПодсистема85"] Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 = ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьТипОтображенияРедактируемойЯчейкиТабличногоДокумента(ПредставлениеУведомления);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УдалитьСтрокуНаСервере(ИмяОбласти)
	
	ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
	ИдМСЧ = СтрЗаменить(ИмяБезПостфикса, "УдалитьСтроку", "");
	ПолноеИмяМСЧ = "МногострочнаяЧасть" + ИдМСЧ;
	НомерСтроки = Число(Сред(ИмяОбласти, СтрНайти(ИмяОбласти, "_") + 1));
	
	МногострочнаяЧасть = ЭтотОбъект[ПолноеИмяМСЧ];
	
	Если МногострочнаяЧасть.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	МногострочнаяЧасть.Удалить(НомерСтроки - 1);
	
	// Обрабатываются только многострочные части с высотой 2 строки
	// и показателями, расположенными в одной строке.
	ВысотаБлокаМСЧ = 2;
	НомерСтрокиМакета = ПредставлениеУведомления.Область(ИмяОбласти).Верх;
	УдаляемаяОбласть = ПредставлениеУведомления.Область(НомерСтрокиМакета, , НомерСтрокиМакета + ВысотаБлокаМСЧ -1, );
	ПредставлениеУведомления.УдалитьОбласть(УдаляемаяОбласть, ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	
	Пока Истина Цикл
		ОбластьУдаленияСтроки = ПредставлениеУведомления.Области.Найти(
			"УдалитьСтроку" + ИдМСЧ + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерСтроки + 1));
		Если ОбластьУдаленияСтроки = Неопределено Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
			ИмяПоказателя = КолонкаМСЧ.Имя;
			ОбластьПоказателя = ПредставлениеУведомления.Область(ИмяПоказателя + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерСтроки + 1));
			ОбластьПоказателя.Имя = ИмяПоказателя + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерСтроки);
		КонецЦикла;
		ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(НомерСтроки);
		
		НомерСтроки = НомерСтроки + 1;
		
	КонецЦикла;
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиМногострочнуюЧасть(ПолноеИмяМСЧ)
	
	ИдМСЧ = ИдМСЧ(ПолноеИмяМСЧ);
	ТаблицаМСЧ = РеквизитФормыВЗначение(ПолноеИмяМСЧ);
	ИсходныйМакетРаздела = МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ);
	ПромежуточныйМакет = Новый ТабличныйДокумент;
	
	Инд = 1;
	Для Каждого Стр Из ЭтотОбъект[ПолноеИмяМСЧ] Цикл
		ОбластьМСЧ = ИсходныйМакетРаздела.ПолучитьОбласть(ПолноеИмяМСЧ);
		Для Каждого КолонкаМСЧ Из ТаблицаМСЧ.Колонки Цикл
			ИмяПоказателя = КолонкаМСЧ.Имя;
			ОбластьПоказателя = ОбластьМСЧ.Область(ИмяПоказателя + "_1");
			ОбластьПоказателя.Имя = ИмяПоказателя + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(Инд);
			ОбластьПоказателя.Значение = Стр[ИмяПоказателя];
		КонецЦикла;
		
		ОбластьУдаленияСтроки = ОбластьМСЧ.Область("УдалитьСтроку" + ИдМСЧ + "_1");
		ОбластьУдаленияСтроки.Имя = "УдалитьСтроку" + ИдМСЧ + "_" + РегламентированнаяОтчетностьКлиентСервер.СтрЧГ0(Инд);
		
		ПромежуточныйМакет.Вывести(ОбластьМСЧ);
		Инд = Инд + 1;
		
	КонецЦикла;
	
	ИсходнаяОбластьМСЧ = ПредставлениеУведомления.Область(ПолноеИмяМСЧ);
	ПозицияВставки = ИсходнаяОбластьМСЧ.Верх;
	
	ПредставлениеУведомления.УдалитьОбласть(ИсходнаяОбластьМСЧ, ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	ПредставлениеУведомления.ВставитьОбласть(
		ПромежуточныйМакет.Область(1, , ПромежуточныйМакет.ВысотаТаблицы),
		ПредставлениеУведомления.Область(ПозицияВставки, , ПозицияВставки, ),
		ТипСмещенияТабличногоДокумента.ПоВертикали);
	
	УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ);
	УведомлениеОСпецрежимахНалогообложения.ПозиционироватьсяНаЯчейке(ЭтотОбъект);
	Если ЭтотОбъект["ДоступнаПодсистема85"] Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 = ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьТипОтображенияРедактируемойЯчейкиТабличногоДокумента(ПредставлениеУведомления);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция МакетОтчетаСодержащийМСЧ(ПолноеИмяМСЧ)
	
	Если ПолноеИмяМСЧ = "МногострочнаяЧастьВ010107" Тогда
		Возврат УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, "Форма2024_1_Страница4");
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьД010102" Тогда
		Возврат УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, "Форма2024_1_Страница6");
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьД010202" Тогда
		Возврат УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, "Форма2024_1_Страница7");
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция МакетОтчетаСодержащийМСЧ():
			|вызов функции с непредусмотренным значением параметра';
			|en = 'Функция МакетОтчетаСодержащийМСЧ():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ИдМСЧ(ПолноеИмяМСЧ)
	
	Если ПолноеИмяМСЧ = "МногострочнаяЧастьВ010107" Тогда
		Возврат "В010107";
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьД010102" Тогда
		Возврат "Д010102";
		
	ИначеЕсли ПолноеИмяМСЧ = "МногострочнаяЧастьД010202" Тогда
		Возврат "Д010202";
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция ИдМСЧ():
			|вызов функции с непредусмотренным значением параметра';
			|en = 'Функция ИдМСЧ():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу(ИмяОбласти) Экспорт
	
	ИмяБезПостфикса = Лев(ИмяОбласти, СтрНайти(ИмяОбласти, "_") - 1);
	
	Если ИмяБезПостфикса = "В01010701" ИЛИ ИмяБезПостфикса = "В01010702" Тогда
		Возврат "МногострочнаяЧастьВ010107";
		
	ИначеЕсли ИмяБезПостфикса = "Д01010200" Тогда
		Возврат "МногострочнаяЧастьД010102";
		
	ИначеЕсли ИмяБезПостфикса = "Д01020200" Тогда
		Возврат "МногострочнаяЧастьД010202";
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу():
			|вызов функции с непредусмотренным значением параметра';
			|en = 'Функция ОпределитьПринадлежностьОбластиКМногострочномуРазделу():
			|вызов функции с непредусмотренным значением параметра'");
		
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьУдаленияПервойСтроки(ИдМСЧ)
	
	ТаблицаМСЧ = ЭтотОбъект["МногострочнаяЧасть" + ИдМСЧ];
	УдалениеВозможно = (ТаблицаМСЧ.Количество() >= 2);
	
	ОбластьУдаленияСтроки = ПредставлениеУведомления.Область("УдалитьСтроку" + ИдМСЧ + "_1");
	ОбластьУдаленияСтроки.Гиперссылка = УдалениеВозможно;
	ОбластьУдаленияСтроки.Текст = ?(УдалениеВозможно, "х", "");
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьДостижениеМаксимальногоКоличестваСтрок(ИдМСЧ)
	
	Результат = Новый Структура("ДостигнутМаксимум, ТекстПредупреждения", Ложь, "");
	
	Если ИдМСЧ = "В010107" Тогда
		ИмяПредельногоПоказателя = "В01010701_3";
		Если ПредставлениеУведомления.Области.Найти(ИмяПредельногоПоказателя) <> Неопределено Тогда
			Результат.ДостигнутМаксимум = Истина;
			Результат.ТекстПредупреждения = НСтр("ru = 'Допускается не более 3 строк с указанием здания/сооружения';
												|en = 'Допускается не более 3 строк с указанием здания/сооружения'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ВводАдресаФИАС

&НаКлиенте
Процедура ОбработкаАдреса(Область, СтандартнаяОбработка) Экспорт
	
	// Эмуляция механизма многострочных частей из налоговой отчетности.
	СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей = Новый Структура;
	ИменаГраф = Новый Массив;
	ИменаГраф.Добавить("В01010701");
	ИменаГраф.Добавить("В01010702");
	ИменаПодчиненныхГрупп = Новый Массив;
	СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей.Вставить("В010107",
		Новый Структура("ИменаГраф, ИсхКолСтрок, УровеньПодчинения, ИменаПодчиненныхГрупп",
			ИменаГраф, 1, 0, ИменаПодчиненныхГрупп));
	
	СтандартнаяОбработка = Ложь;
	
	СоответствиеПоказателейСтраницыРеквизитамАдреса = СоответствиеПоказателейСтраницыРеквизитамАдреса(ИмяФормы);
	
	ПараметрыОпределенияАдреса = Новый Структура;
	ПараметрыОпределенияАдреса.Вставить("СтруктураМногострочныхЧастей",
		СтруктураРеквизитовФормы_СтруктураМногострочныхЧастей);
	ПараметрыОпределенияАдреса.Вставить("СоответствиеПоказателейСтраницыРеквизитамАдреса",
		СоответствиеПоказателейСтраницыРеквизитамАдреса);
	
	ПоляАдресаВJSON = "";
	Для Каждого Элем Из СоответствиеПоказателейСтраницыРеквизитамАдреса Цикл
		Если Элем.Значение = "СтрокаАдресногоОбъекта" Тогда
			ПоляАдресаВJSON = ПредставлениеУведомления.Области[Элем.Ключ].Значение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Ввод адреса");
	ПараметрыФормы.Вставить("ЗначенияПолей", ПоляАдресаВJSON);
	ПараметрыФормы.Вставить("ВидКонтактнойИнформации", УведомлениеОСпецрежимахНалогообложенияКлиент.ВидКонтактнойИнформацииАдресСПроверкой());
	
	ДополнительныеПараметры = Новый Структура;
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение",
		ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент").ОткрытьФормуКонтактнойИнформации(
		ПараметрыФормы, , Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьАдресВМестеХранения(Результат);
	
	Элемент= Элементы.ДеревоСтраниц;
	
	ПоказатьТекущуюСтраницу(Элемент.ТекущиеДанные.ИмяМакета);
	Если Элемент.ТекущиеДанные.Многострочность Тогда
		Для Каждого СтрокаМСЧ Из Элемент.ТекущиеДанные.МногострочныеЧасти Цикл
			ИмяМСЧ = СтрокаМСЧ.Значение;
			ВывестиМногострочнуюЧасть(ИмяМСЧ);
		КонецЦикла;
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьАдресВМестеХранения(Результат)
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Адрес", Результат.Значение);
	ПараметрыЗаполнения.Вставить("СоответствиеПоказателейСтраницыРеквизитамАдреса",
		СоответствиеПоказателейСтраницыРеквизитамАдреса(ИмяФормы));
	ПараметрыЗаполнения.Вставить("ВерсияАдреса", 2);
	
	ВиртуальноеДеревоРазделов = Новый ДеревоЗначений;
	ВиртуальноеДеревоРазделов.Колонки.Добавить("Данные");
	ВиртуальноеДеревоРазделов.Колонки.Добавить("ДанныеМногострочныхЧастей");
	
	ВиртуальнаяВеткаРаздела = ВиртуальноеДеревоРазделов.Строки.Добавить();
	ВиртуальнаяВеткаРаздела.Данные = ЭтотОбъект["ДанныеУведомления"].ЛистВ;
	ВиртуальнаяВеткаРаздела.ДанныеМногострочныхЧастей = Новый Структура("В010107", МногострочнаяЧастьВ010107);
	
	ЗаполнитьАдресВФорматеФИАСНаСтраницеРеглОтчета(ВиртуальнаяВеткаРаздела, ПараметрыЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАдресВФорматеФИАСНаСтраницеРеглОтчета(СтраницаРаздела, ПараметрыЗаполнения)
	
	СоответствиеПоказателейСтраницыРеквизитамАдреса = ПараметрыЗаполнения.СоответствиеПоказателейСтраницыРеквизитамАдреса;
	
	ДополнительныеПараметры = Неопределено;
	Если ПараметрыЗаполнения.Свойство("ВерсияАдреса") Тогда
		ДополнительныеПараметры = Новый Структура("ВерсияАдреса", ПараметрыЗаполнения.ВерсияАдреса);
	КонецЕсли;
	
	СтруктураАдреса = УведомлениеОСпецрежимахНалогообложения.АдресВФорматеФИАС(
		ПараметрыЗаполнения.Адрес, ДополнительныеПараметры);
	
	ДанныеСтраницы = СтраницаРаздела.Данные;
	
	Для Каждого Элем Из СоответствиеПоказателейСтраницыРеквизитамАдреса Цикл
		Если ДанныеСтраницы.Свойство(Элем.Ключ) Тогда
			ДанныеСтраницы.Вставить(Элем.Ключ, СтруктураАдреса[Элем.Значение]);
			
		ИначеЕсли СтраницаРаздела.ДанныеМногострочныхЧастей.Свойство(Элем.Ключ)
			И ТипЗнч(Элем.Значение) = Тип("Массив") И Элем.Значение.Количество() >= 2
			И ТипЗнч(Элем.Значение[0]) = Тип("Строка") И ЗначениеЗаполнено(Элем.Значение[0])
			И СтруктураАдреса.Свойство(Элем.Значение[0]) И ТипЗнч(СтруктураАдреса[Элем.Значение[0]]) = Тип("Массив")
			И ТипЗнч(Элем.Значение[1]) = Тип("Структура") Тогда
			
			ДанныеМСЧ = СтраницаРаздела.ДанныеМногострочныхЧастей[Элем.Ключ];
			ДанныеМСЧ.Очистить();
			
			СоответствиеМногострочнойЧастиМножественномуРеквизитуАдреса = Элем.Значение[1];
			
			МножественныйРеквизитАдреса = СтруктураАдреса[Элем.Значение[0]];
			
			Для Каждого РеквизитАдреса Из МножественныйРеквизитАдреса Цикл
				СтрокаМногострочнойЧасти = ДанныеМСЧ.Добавить();
				Для Каждого ЭлемСоответствия Из СоответствиеМногострочнойЧастиМножественномуРеквизитуАдреса Цикл
					СтрокаМногострочнойЧасти[ЭлемСоответствия.Ключ] = РеквизитАдреса[ЭлемСоответствия.Значение];
				КонецЦикла;
			КонецЦикла;
			
			Если ДанныеМСЧ.Количество() = 0 Тогда
				СтрокаМногострочнойЧасти = ДанныеМСЧ.Добавить();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсОбращенияКМодулюМенеджераИОбъекта

&НаСервере
Процедура ИнициализироватьМногострочныеЧасти(КонтейнерВходящий, КонтейнерРезультат) Экспорт
	Отчеты[Объект.ИмяОтчета].ИнициализироватьМногострочныеЧасти(Объект.ИмяФормы, КонтейнерВходящий, КонтейнерРезультат);
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеПоказателейСтраницыРеквизитамАдреса(ПолноеИмяФормы)
	Возврат Отчеты[РегламентированнаяОтчетностьКлиентСервер.ИмяОтчета(ПолноеИмяФормы)].СоответствиеПоказателейСтраницыРеквизитамАдреса(
		РегламентированнаяОтчетностьКлиентСервер.ИмяФормыОтчета(ПолноеИмяФормы));
КонецФункции

&НаСервереБезКонтекста
Функция ДоступнаПечатьPDF417(ПолноеИмяФормы)
	Возврат Отчеты[РегламентированнаяОтчетностьКлиентСервер.ИмяОтчета(ПолноеИмяФормы)].ДоступнаПечатьPDF417(
		РегламентированнаяОтчетностьКлиентСервер.ИмяФормыОтчета(ПолноеИмяФормы));
КонецФункции

#КонецОбласти

&НаСервере
Процедура ОчисткаОтчета() Экспорт
	
	СформироватьДеревоСтраниц();
	
	ВходящийКонтейнер = Новый Структура("ИмяФормы, ДеревоСтраниц", ИмяФормы, РеквизитФормыВЗначение("ДеревоСтраниц"));
	РезультатКонтейнер = Новый Структура;
	УведомлениеОСпецрежимахНалогообложения.СформироватьКонтейнерДанныхУведомления(ВходящийКонтейнер, РезультатКонтейнер, Истина);
	Для Каждого КЗ Из РезультатКонтейнер Цикл
		ЭтотОбъект[КЗ.Ключ] = КЗ.Значение;
	КонецЦикла;
	
	РезультатКонтейнер.Очистить();
	ИнициализироватьМногострочныеЧасти(ВходящийКонтейнер, РезультатКонтейнер);
	Для Каждого КЗ Из РезультатКонтейнер Цикл
		ЗначениеВРеквизитФормы(КЗ.Значение, КЗ.Ключ);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьАвтоНаСервере(ПараметрыЗаполнения = Неопределено)
	ПараметрыОтчета = Новый Структура();
	ПараметрыОтчета.Вставить("Организация", Объект.Организация);
	ПараметрыОтчета.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыОтчета.Вставить("ПараметрыЗаполнения", ПараметрыЗаполнения);
	
	Контейнер = НовыйКонтейнерДляАвтозаполнения();
	РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьОтчет(Объект.ИмяОтчета, Объект.ИмяФормы, ПараметрыОтчета, Контейнер);
	ЗагрузитьПодготовленныеДанные(Контейнер);
КонецПроцедуры

&НаСервере
Функция НовыйКонтейнерДляАвтозаполнения()
	Контейнер = Новый Структура;
	Для Каждого КЗ Из ЭтотОбъект["ДанныеУведомления"] Цикл
		Контейнер.Вставить(КЗ.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(КЗ.Значение));
	КонецЦикла;
	
	Возврат Контейнер;
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные(Контейнер)
	Для Каждого КЗ Из Контейнер Цикл
		Если ЭтотОбъект["ДанныеУведомления"].Свойство(КЗ.Ключ) Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект["ДанныеУведомления"][КЗ.Ключ], КЗ.Значение);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СформироватьДеревоСтраниц() Экспорт
	Разложение = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ИмяФормы, ".");
	ДС = Отчеты[Разложение[1]].СформироватьДеревоСтраниц(Разложение[3]);
	ЗначениеВРеквизитФормы(ДС, "ДеревоСтраниц");
КонецПроцедуры

&НаСервере
Процедура ПоказатьТекущуюСтраницу(ИмяМакета)
	ПредставлениеУведомления.Очистить();
	ПредставлениеУведомления.Вывести(УведомлениеОСпецрежимахНалогообложения.ПолучитьМакетТабличногоДокумента(ЭтотОбъект, ИмяМакета));
	
	Если НЕ ОбщегоНазначения.РежимОтладки()
	   И ПредставлениеУведомления.Области.Найти("СкрытьПриОткрытии") <> Неопределено Тогда
		СкрываемаяОбласть = ПредставлениеУведомления.Область("СкрытьПриОткрытии");
		СкрываемаяОбласть.Видимость = Ложь;
	КонецЕсли;
	
	СтрДанных = ЭтотОбъект["ДанныеУведомления"][ЭтотОбъект["ТекущееИДНаименования"]];
	Для Каждого Обл Из ПредставлениеУведомления.Области Цикл
		Если Обл.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Прямоугольник
		   И Обл.СодержитЗначение Тогда
			СтрДанных.Свойство(Обл.Имя, Обл.Значение);
		КонецЕсли;
	КонецЦикла;
	УведомлениеОСпецрежимахНалогообложения.ПозиционироватьсяНаЯчейке(ЭтотОбъект);
	Если ЭтотОбъект["ДоступнаПодсистема85"] Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 = ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьТипОтображенияРедактируемойЯчейкиТабличногоДокумента(ПредставлениеУведомления);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СохранитьДанные() Экспорт
	
	Если ЗначениеЗаполнено(Объект.Ссылка) И Не Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = УведомлениеОСпецрежимахНалогообложения.СтруктураПараметровДляСохранения(ЭтотОбъект);
	СтруктураПараметров.Вставить("ДанныеПомощникаЗаполнения", Новый Структура);
	СтруктураПараметров.Вставить("МногострочныеЧасти", Новый Структура);
	СтруктураПараметров.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьВ010107", РеквизитФормыВЗначение("МногострочнаяЧастьВ010107"));
	СтруктураПараметров.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьД010102", РеквизитФормыВЗначение("МногострочнаяЧастьД010102"));
	СтруктураПараметров.МногострочныеЧасти.Вставить(
		"МногострочнаяЧастьД010202", РеквизитФормыВЗначение("МногострочнаяЧастьД010202"));
	
	СтруктураПолейОтправки = Новый Структура;
	Для Каждого КЗ Из РеквизитыКСохранению() Цикл 
		СтруктураПолейОтправки.Вставить(КЗ, ЭтотОбъект[КЗ]);
	КонецЦикла;
	СтруктураПараметров.Вставить("СтруктураПолейОтправки", СтруктураПолейОтправки);
	
	УведомлениеОСпецрежимахНалогообложения.СохранитьДанные(ЭтотОбъект, СтруктураПараметров);
	УведомлениеЗаполненоВПомощнике = Ложь;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанные(СсылкаНаДанные)
	СтруктураПараметров = УведомлениеОСпецрежимахНалогообложения.ЗагрузкаДанныхУведомления(ЭтотОбъект, СсылкаНаДанные);
	
	Для Каждого МСЧ Из СтруктураПараметров.МногострочныеЧасти Цикл
		ПолноеИмяМСЧ = МСЧ.Ключ;
		ТаблицаМСЧ = МСЧ.Значение;
		Если ТаблицаМСЧ.Количество() = 0 Тогда
			ТаблицаМСЧ.Добавить();
		КонецЕсли;
		ЭтотОбъект[ПолноеИмяМСЧ].Загрузить(ТаблицаМСЧ);
	КонецЦикла;
	
	ДобавитьНедостающиеПоказатели();
	Если СтруктураПараметров.Свойство("СтруктураПолейОтправки") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПараметров["СтруктураПолейОтправки"]);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДобавитьНедостающиеПоказатели()
	
	ЛистВ = ЭтотОбъект["ДанныеУведомления"].ЛистВ;
	
	Если НЕ ЛистВ.Свойство("ВД1010100") Тогда
		ЛистВ.Вставить("ВД1010100", "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоОбластьОКСМ(Область)
	
	Если Область.Имя = "А01040200" И ЭтотОбъект["ТекущееИДНаименования"] = "ЛистА" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ПриЗакрытииНаСервере()
	СохранитьДанные();
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотрЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СохранитьДанные();
		МассивПечати = Новый Массив;
		МассивПечати.Добавить(Объект.Ссылка);
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Документ.УведомлениеОСпецрежимахНалогообложения",
			"Уведомление", МассивПечати, Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СформироватьXMLНаСервере(УникальныйИдентификатор)
	
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ВыгрузитьДокумент(УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура СохранитьНаКлиенте(Автосохранение = Ложь,ВыполняемоеОповещение = Неопределено) Экспорт 
	Если Модифицированность И УведомлениеЗаполненоВПомощнике Тогда
		Оповестить("ЗакрытьПомощникВнесенияИзмененийЕГР", Объект.Ссылка);
	КонецЕсли;
	
	СохранитьДанные();
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПроверитьВыгрузкуНаСервере()
	СохранитьДанные();
	Документ = РеквизитФормыВЗначение("Объект");
	Возврат Документ.ПроверитьДокументСВыводомВТаблицу(УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура Подключаемый_ОткрытьПрисоединенныеФайлы(Команда)
	
	РегламентированнаяОтчетностьКлиент.СохранитьУведомлениеИОткрытьФормуПрисоединенныеФайлы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПечатьБРОНаСервере(ЕстьВыходЗаГраницы)
	ЕстьВыходЗаГраницы = Ложь;
	СохранитьДанные();
	ЭтотОбъект["СтруктураРеквизитовУведомления"] = Новый Структура("СписокПечатаемыхЛистов",
		Отчеты[Объект.ИмяОтчета].СформироватьСписокЛистов(Объект.Ссылка, ЕстьВыходЗаГраницы));
КонецПроцедуры

&НаКлиенте
Процедура УведомлениеЗаполненоВПомощникеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	Если НавигационнаяСсылкаФорматированнойСтроки = "ВнесениеИзмененийЕГР" Тогда
		СтандартнаяОбработка = Ложь;
		
		МодульРегистрацияОрганизацииКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("РегистрацияОрганизацииКлиентСервер");
		ПараметрыПомощника = МодульРегистрацияОрганизацииКлиентСервер.НовыеПараметрыПомощникаВнесенияИзменений();
		ПараметрыПомощника.Организация = Объект.Организация;
		ПараметрыПомощника.КонтекстныйВызов = Истина;
		
		МодульРегистрацияОрганизацииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РегистрацияОрганизацииКлиент");
		МодульРегистрацияОрганизацииКлиент.ОткрытьПомощникВнесенияИзменений(ПараметрыПомощника);
		
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьУведомлениеЗаполненоВПомощникеНажатие(Элемент)
	УведомлениеЗаполненоВПомощнике = Ложь;
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьВыгружатьСОшибками(Команда)
	УведомлениеОСпецрежимахНалогообложенияКлиент.ОбработкаРазрешитьВыгружатьСОшибками(ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьВыводСтатусовИВажныхСообщений_85()

	Если РегламентированнаяОтчетность.ДоступнаПодсистема85() Тогда
		МодульЭДОСКонтролирующимиОрганами = ОбщегоНазначения.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганами");
		МодульЭДОСКонтролирующимиОрганами.УстановитьРазмещениеНаименованияЭтапаБРО_85(ЭтотОбъект);
		МодульЭДОСКонтролирующимиОрганами.ВывестиСтатусыИВыжныеСообщенияНаФорму_85(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере() Экспорт

	Если РегламентированнаяОтчетность.ДоступнаПодсистема85() Тогда
		МодульЭДОСКонтролирующимиОрганами = ОбщегоНазначения.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганами");
		МодульЭДОСКонтролирующимиОрганами.ВывестиСтатусыИВыжныеСообщенияНаФорму_85(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДетальноеОписаниеСтатусаПослеВыбора(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	Если Результат = "ОбновитьОтправку" Тогда
		
		ОбновитьОтправку(Неопределено);
		
	ИначеЕсли Результат = "ГиперссылкаПротокол" Тогда 	
		
		ГиперссылкаПротоколНажатие(Неопределено);
		
	ИначеЕсли Результат = "ЭтапыОтправки" Тогда 	
		
		ЭтапыОтправкиНажатие(Неопределено);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВывестиСтатусыИВыжныеСообщенияНаФорму() Экспорт

	ВывестиСтатусыИВыжныеСообщенияНаФормуНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНажатияКнопкиВВажномСообщении(Команда)

	Если Команда.Имя = "ОтправитьНеотправленноеИзвещение" Тогда
		ОтправитьНеотправленноеИзвещение(Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаНавигационнойСсылкиВВажномСообщении(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	Если НРег(НавигационнаяСсылкаФорматированнойСтроки) = "критическиеошибки" Тогда
		КритическиеОшибкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаЗакрытияВажногоСообщения(Команда)

	МодульКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер");
	МодульКлиентСервер.ЗакрытьВажноеСообщение(ЭтотОбъект, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьДействиеИнформационногоСообщения(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)

	МодульОбщегоНазначенияКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбщегоНазначенияКлиент");
	МодульОбщегоНазначенияКлиент.ОбработатьДействиеИнформационногоСообщения(ЭтотОбъект, ЭлементИлиКоманда, НавигационнаяСсылка, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура РегистрирующийОрганКодНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Строки = ЭтотОбъект["СпискиВыбора"].НайтиСтроки(Новый Структура("ИДНаименования,ИмяПоля", "РегистрирующиеОрганы", "РегистрирующиеОрганы"));
	ЭтотОбъект["ЗагружаемыеКоды"].Очистить();
	Для Каждого Стр Из Строки Цикл 
		НовСтр = ЭтотОбъект["ЗагружаемыеКоды"].Добавить();
		НовСтр.Код = Стр.Код;
		НовСтр.Название = Стр.Наименование;
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Заголовок", "Выбор налогового органа");
	ПараметрыФормы.Вставить("ТаблицаЗначений", ЭтотОбъект["ЗагружаемыеКоды"]);
	ПараметрыФормы.Вставить("СтруктураДляПоиска", Новый Структура("Код", РегистрирующийОрганКод));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаСпискаЗавершение", ЭтотОбъект, Неопределено);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаВыбораЗначенияИзТаблицы", 
		ПараметрыФормы, ЭтотОбъект,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаСпискаЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	Если РезультатВыбора <> Неопределено Тогда
		РегистрирующийОрганКод = РезультатВыбора.Код;
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

#Область ПодтверждениеКонтактнойИнформации

&НаКлиенте
Процедура ВыслатьСМССКодом(Команда)
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Телефон", 0.1, Истина);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДополнительныеПараметрыКонтактнойИнформации(Форма, ИмяТекущегоЭлемента = "")
	Возврат РегистрационныеФормыКлиентСервер.ДополнительныеПараметрыКонтактнойИнформации(
		Форма.ЗаявительТелефонПредставление,
		Форма.ЗаявительEMailПредставление,
		"ЗаявительТелефонПредставление",
		"ЗаявительEMailПолучениеДокументов",
		ИмяТекущегоЭлемента);
КонецФункции

&НаКлиенте
Процедура Подключаемый_КодПодтверждения_Телефон()
	Если ТребуетсяИсправлениеНомераТелефонаЗаявителя(ЗаявительТелефонПредставление) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрационныеФормыКлиент.ПодтвердитьНомерТелефона(ЭтотОбъект,
		ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление"));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяИсправлениеНомераТелефонаЗаявителя(НомерТелефона)
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(НомерТелефона) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , НСтр("ru = 'Контактный телефон';
																						|en = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительТелефонПредставление", , Отказ);
	ИначеЕсли Не РегистрационныеФормыКлиентСервер.ТелефонСоответствуетТребованиям(НомерТелефона)
		Или Не ЗначениеЗаполнено(ЭлектроннаяПодписьВМоделиСервисаКлиентСервер.ПолучитьПредставлениеТелефона(НомерТелефона)) Тогда
		
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , "Корректность", НСтр("ru = 'Контактный телефон';
																									|en = 'Контактный телефон'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительТелефонПредставление", , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеОтправкиКодаПодтверждения_Телефон(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	РегистрационныеФормыКлиент.ПослеОтправкиКодаПодтвержденияТелефона(
		ЭтотОбъект, ДлительнаяОперация, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправка_Телефон()
	РегистрационныеФормыКлиент.ПовторнаяОтправкаКодаПодтвержденияТелефона(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ТекстВыслатьПовторноКодПодтвержденияНажатие(Элемент)
	СрокПовторнойОтправкиКодаДляТелефона = Неопределено;
	КодПодтвержденияТелефона = "";
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Телефон", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияТелефонаИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	КодПодтвержденияТелефона = СокрЛП(Текст);
	
	Если СтрДлина(Текст) = РегистрационныеФормыКлиентСервер.ДлинаКодаПодтверждения() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения_Телефон", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьКодПодтверждения_Телефон()
	РегистрационныеФормыКлиент.ПроверитьКодПодтвержденияТелефона(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительТелефонПредставление"));
КонецПроцедуры

&НаКлиенте
Процедура ВыслатьПисьмоНаПочту(Команда)
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Почта", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_КодПодтверждения_Почта()
	Если ТребуетсяИсправлениеАдресаЭлектроннойПочтыЗаявителя(ЗаявительEMailПредставление) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрационныеФормыКлиент.ПодтвердитьАдресЭлектроннойПочты(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительEMailПолучениеДокументов"));
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяИсправлениеАдресаЭлектроннойПочтыЗаявителя(АдресЭлектроннойПочты)
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(АдресЭлектроннойПочты) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , НСтр("ru = 'Email';
																						|en = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительEMailПредставление", , Отказ);
	ИначеЕсли Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(АдресЭлектроннойПочты) Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность", НСтр("ru = 'Email';
																									|en = 'Email'"));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ЗаявительEMailПредставление", , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПослеОтправкиКодаПодтверждения_Почта(ДлительнаяОперация, ДополнительныеПараметры = Неопределено) Экспорт
	РегистрационныеФормыКлиент.ПослеОтправкиКодаПодтвержденияПочты(
		ЭтотОбъект, ДлительнаяОперация, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПовторнаяОтправка_Почта()
	РегистрационныеФормыКлиент.ПовторнаяОтправкаКодаПодтвержденияПочты(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияПочтыИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	КодПодтвержденияПочты = СокрЛП(Текст);
	
	Если СтрДлина(Текст) = РегистрационныеФормыКлиентСервер.ДлинаКодаПодтверждения() Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения_Почта", 0.1, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьКодПодтверждения_Почта()
	РегистрационныеФормыКлиент.ПроверитьКодПодтвержденияПочты(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, "ЗаявительEMailПолучениеДокументов"));
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительEMailПолучениеДокументовПриИзменении(Элемент)
	ЗаявительEMailПриИзменении();
	
	РегистрационныеФормыКлиент.ЗаявительПочтаПриИзменении(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, Элемент.Имя));
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительEMailПриИзменении()
	ЗаявительEMailПредставление = СокрЛП(ЗаявительEMailПредставление);
	Если ПустаяСтрока(ЗаявительEMailПредставление) Тогда
		ЗаявительEMailЗначениеJSON = "";
		Возврат;
	КонецЕсли;
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	ЗаявительEMailЗначениеJSON = КонтактнаяИнформацияПоПредставлению(ЗаявительEMailПредставление, ПараметрыКонтактнойИнформации.Email);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПолученияКонтактнойИнформацииЗаявителя(Форма)
	Параметры = Новый Структура;
	Параметры.Вставить("Владелец", Форма.Объект.Организация);
	Параметры.Вставить("EMail", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.EmailОрганизации"));
	Параметры.Вставить("АдресПрописки", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));
	Параметры.Вставить("АдресДляИнформирования", Неопределено);
	Параметры.Вставить("Телефон", ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыКонтактнойИнформации.ТелефонОрганизации"));
	Возврат Параметры;
КонецФункции

&НаСервереБезКонтекста
Функция КонтактнаяИнформацияПоПредставлению(Знач Представление, Знач ВидКонтактнойИнформации)
	Результат = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(Представление, ВидКонтактнойИнформации);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ЗаявительТелефонПредставлениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ПараметрыКонтактнойИнформации.Телефон);
	ПараметрыОткрытия.Вставить("ЗначенияПолей", ЗаявительТелефонЗначениеJSON);
	ПараметрыОткрытия.Вставить("Представление", ЗаявительТелефонПредставление);
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПриИзменении(Элемент)
	РегистрационныеФормыКлиент.ЗаявительТелефонПриИзменении(
		ЭтотОбъект, ДополнительныеПараметрыКонтактнойИнформации(ЭтотОбъект, Элемент.Имя));
	ТелефонЗаявителяПриИзменении(ЗаявительТелефонПредставление);
КонецПроцедуры

&НаКлиенте
Процедура ТелефонЗаявителяПриИзменении(Текст)
	Если ПустаяСтрока(Текст) Тогда
		ЗаявительТелефонПредставление = "";
		ЗаявительТелефонЗначениеJSON = "";
		Возврат;
	КонецЕсли;
	
	ПараметрыКонтактнойИнформации = ПараметрыПолученияКонтактнойИнформацииЗаявителя(ЭтотОбъект);
	Телефон = РегистрационныеФормыКлиентСервер.ТелефонДляЗаявления(Текст);
	ЗаявительТелефонПредставление = Телефон;
	ЗаявительТелефонЗначениеJSON = КонтактнаяИнформацияПоПредставлению(Телефон, ПараметрыКонтактнойИнформации.Телефон);
КонецПроцедуры

&НаКлиенте
Процедура ТекстВыслатьПовторноКодПодтвержденияПочтаНажатие(Элемент)
	СрокПовторнойОтправкиКодаДляПочты = Неопределено;
	КодПодтвержденияПочты = "";
	ПодключитьОбработчикОжидания("Подключаемый_КодПодтверждения_Почта", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаявительТелефонПредставлениеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ЗаявительТелефонЗначениеJSON = ВыбранноеЗначение.Значение;
	ЗаявительТелефонПредставление = ВыбранноеЗначение.Представление;
КонецПроцедуры

&НаСервереБезКонтекста
Функция РеквизитыКСохранению()
	РеквизитыКСохранению = "ЗаявительEMailЗначениеJSON,ЗаявительEMailПредставление,ЗаявительТелефонЗначениеJSON,ЗаявительТелефонПредставление,"
		+ "НомерТелефонаПодтвержден,ЭлектроннаяПочтаПодтверждена,СпособПодачиДокументов,ФорматОтправкиДокументов,"
		+ "ПодписанныеДокументы,РегистрирующийОрганКод,ОшибкаПодписанияДокументов,НетСертификатаПодписи";
	Возврат СтрРазделить(РеквизитыКСохранению, ",", Ложь);
КонецФункции

&НаКлиенте
Процедура СпособПодачиДокументоЭлектронноОнлайнПриИзменении(Элемент)
	ПриИзмененииОпцийСдачи(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СпособПодачиДокументоЭлектронноПриИзменении(Элемент)
	ПриИзмененииОпцийСдачи(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИныеДокументы(Команда)
	РегистрационныеФормыКлиент.ОчиститьПрисоединенныеФайлы(ЭтотОбъект, "ИныеДокументы");
КонецПроцедуры

&НаКлиенте
Процедура ИныеДокументыНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РегистрационныеФормыКлиент.ОткрытьПрисоединенныеФайлы(ЭтотОбъект, Элемент.Имя, "Дерево");
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСканЗаявление(Команда)
	РегистрационныеФормыКлиент.ОчиститьПрисоединенныеФайлы(ЭтотОбъект, "СканЗаявление");
КонецПроцедуры

&НаКлиенте
Процедура СканЗаявлениеНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РегистрационныеФормыКлиент.ОткрытьПрисоединенныеФайлы(ЭтотОбъект, Элемент.Имя, "Список");
КонецПроцедуры

&НаКлиенте
Процедура ПодписанныеДокументыЗначениеНажатие(Элемент)
	ДанныеФайла = РегистрационныеФормыВызовСервера.ДанныеФайлаДляОткрытия(ПодписанныеДокументы, УникальныйИдентификатор);
	РаботаСФайламиКлиент.СохранитьФайлКак(ДанныеФайла);
КонецПроцедуры

&НаКлиенте
Процедура КартинкаОшибкаПодписиДокументовРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	РегистрационныеФормыКлиент.ОткрытьЖурналРегистрацииПриОшибкиПодписания();
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПодписатьДокументы(Команда)
	Если Модифицированность Тогда 
		Если Ждать ВопросАсинх("Перед формированием пакета необходимо сохранить заявление. Сохранить изменения?", РежимДиалогаВопрос.ДаНет)
			<> КодВозвратаДиалога.Да Тогда 
			Возврат;
		КонецЕсли;
		СохранитьДанные();
	КонецЕсли;
	Если Не ПроверкиПередОтправкой() Тогда 
		Возврат;
	КонецЕсли;
	
	Если РегистрационныеФормыКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(СпособПодачиДокументов, ФорматОтправкиДокументов) Тогда 
		ВыгрузкаВФоне();
	Иначе
		ПроверитьИПодписатьДокументы();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаВФоне()
	Доступность = Ложь;
	РезультатВыполнения = ВыгрузкаВФонеНаСервере();
	Если НЕ РезультатВыполнения.ЗаданиеВыполнено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыгрузкаВФонеЗавершение", ЭтотОбъект);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется формирование дополнительных файлов выгрузки';
												|en = 'Выполняется формирование дополнительных файлов выгрузки'");
		ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатВыполнения, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ВыгрузкаВФонеПроверкаРезультата();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВыгрузкаВФонеНаСервере()
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ЗапуститьВФоне = Истина;

	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне("РегистрационныеФормыВызовСервера.ДанныеВыгрузкиДляФормированияКонтейнера",
		Новый Структура("ИДОтчета, ИДРедакцииОтчета, Ссылка", Объект.ИмяОтчета, Объект.ИмяФормы, Объект.Ссылка),
		ПараметрыВыполненияВФоне);
	
	РезультатВыполнения.Вставить("ЗаданиеВыполнено", НРег(РезультатВыполнения.Статус) = "выполнено");
	ЭтотОбъект["АдресХранилища"] = РезультатВыполнения.АдресРезультата;
	
	Возврат РезультатВыполнения;
КонецФункции

&НаКлиенте
Процедура ВыгрузкаВФонеЗавершение(РезультатЗагрузки, ДополнительныеПараметры) Экспорт
	Доступность = Истина;
	Если РезультатЗагрузки = Неопределено ИЛИ РезультатЗагрузки.Статус <> "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось сформировать файлы выгрузки...';
														|en = 'Не удалось сформировать файлы выгрузки...'"));
	Иначе
		ВыгрузкаВФонеПроверкаРезультата();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаВФонеПроверкаРезультата()
	РезультатыВыгрузки = ПолучитьИзВременногоХранилища(ЭтотОбъект["АдресХранилища"]);
	Если РезультатыВыгрузки.КоличествоОшибок <> 0 Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("При проверке формата произошли ошибки. Воспользуйтесь проверкой выгрузки в меню формы");
		Возврат;
	КонецЕсли;
	
	Если Не РезультатыВыгрузки.Свойство("ФайлВыгрузкиДД") Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось выгрузить отчет.");
		Возврат;
	КонецЕсли;
	
	Если Не РезультатыВыгрузки.Свойство("ПДФФайлИзСервисаДД") Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось сформировать скан заявления в сервисе.");
		ОшибкаСервиса = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатыВыгрузки, "ОшибкаСервиса");
		Если ЗначениеЗаполнено(ОшибкаСервиса) Тогда 
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОшибкаСервиса);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ПроверитьИПодписатьДокументы();
КонецПроцедуры

&НаКлиенте
Функция СтраницаЗаполнена(Данные) Экспорт 
	Для Каждого КЗ Из Данные Цикл
		Если ТипЗнч(КЗ.Значение) = Тип("УникальныйИдентификатор") Тогда 
			Продолжить;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(КЗ.Значение) Тогда 
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
КонецФункции

&НаКлиенте
Функция ПроверкиПередОтправкой()
	Если СтрДлина(РегистрирующийОрганКод) <> 4 Или Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(РегистрирующийОрганКод) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Не указан / неправильно указан код налогового органа", , "РегистрирующийОрганКод");
		Возврат Ложь;
	КонецЕсли;
	
	ЛистЕ = ЭтотОбъект["ДанныеУведомления"].ЛистЕ;
	Если ЭлектроннаяПодписьВМоделиСервисаКлиентСервер.ПолучитьПредставлениеТелефона(ЗаявительТелефонПредставление)
		<> ЭлектроннаяПодписьВМоделиСервисаКлиентСервер.ПолучитьПредставлениеТелефона(ЛистЕ.Е01030100) Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Телефон заявителя на Листе Е должен совпадать с данными вкладки отправки");
		Возврат Ложь;
	КонецЕсли;
	Если НРег(СокрЛП(ЗаявительEMailПредставление)) <> НРег(СокрЛП(Строка(ЛистЕ.Е01020100))) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Электронная почта на Листе Е должна совпадать с данными вкладки отправки");
		Возврат Ложь;
	КонецЕсли;
	
	Если Не УведомлениеОСпецрежимахНалогообложенияКлиентСервер.СтраницаЗаполнена(ЭтотОбъект["ДанныеУведомления"].ЛистД1)
		И Не УведомлениеОСпецрежимахНалогообложенияКлиентСервер.СтраницаЗаполнена(ЭтотОбъект["ДанныеУведомления"].ЛистД2) Тогда 
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			"Необходимо указать сведения по видам деятельности, остальные изменения (ФИО, паспортные данные, адрес) ФНС получит автоматичеки");
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

&НаКлиенте
Функция ПараметрыДляПодписанияИзДанных()
	Результат = Новый Структура;
	ДанныеОрганизации = РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(
		Объект.Организация, ТекущаяДата(), "ИННФЛ");
	Результат.Вставить("ИНН", ДанныеОрганизации.ИННФЛ);
	Возврат Результат;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидыПрикрепленныхДокументов() Экспорт
	Результат = Новый Структура;
	Результат.Вставить("ИныеДокументы", "ИныеДокументы");
	Результат.Вставить("СканЗаявление", "СканЗаявление");
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ПроверитьИПодписатьДокументы()
	ПараметрыДляПодписанияИзДанных = ПараметрыДляПодписанияИзДанных();
	ПараметрыЗаявления = РегистрационныеФормыКлиент.НовыеПараметрыПодписанияЗаявления();
	
	ПараметрыЗаявления.EMailЗаявителя = ЗаявительEMailПредставление;
	ПараметрыЗаявления.Заявление = Объект.Ссылка;
	ПараметрыЗаявления.ИННЗаявителя = ПараметрыДляПодписанияИзДанных.ИНН;
	ПараметрыЗаявления.ЭтоЮрЛицо = Ложь;
	ПараметрыЗаявления.Организация = Объект.Организация;
	ПараметрыЗаявления.ВидДокумента = "ЗаявлениеПоФормеР24001";
	ПараметрыЗаявления.ВидыДокументов = ВидыПрикрепленныхДокументов();
	ПараметрыЗаявления.ОтображатьИндивидуальныйУстав = Ложь;
	ПараметрыЗаявления.Учредители = Ложь;
	ПараметрыЗаявления.НуженДокументПодтверждающийАдрес = Ложь;
	ПараметрыЗаявления.ЗаявлениеВXML = РегистрационныеФормыКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(
		СпособПодачиДокументов, ФорматОтправкиДокументов);
	
	ФайлыПрикрепленныеДокументы = РегистрационныеФормыВызовСервера.ПрикрепленныеФайлыЗаявления(
		ПараметрыЗаявления, УникальныйИдентификатор);
		
	ПараметрыФормирования = ИнтерфейсыВзаимодействияБРОКлиент.ПараметрыМетодаСформироватьПакетПоЕГРЮЛ();
	ПараметрыФормирования.КодНО = РегистрирующийОрганКод;
	ПараметрыФормирования.ЭлПочтаЗаявителя = ПараметрыЗаявления.EMailЗаявителя;
	ПараметрыФормирования.Заявление = ПараметрыЗаявления.Заявление;
	ПараметрыФормирования.ИННЗаявителя = ПараметрыЗаявления.ИННЗаявителя;
	
	Если СпособПодачиДокументов = РегистрационныеФормыКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронноОнлайн Тогда
		ПараметрыФормирования.ОтправитьПакет = Истина;
		ПараметрыФормирования.ПодписатьПакет = Ложь;
		ПараметрыФормирования.ВыводитьСообщения = Ложь;
	Иначе
		ПараметрыФормирования.ОтправитьПакет = Ложь;
		ПараметрыФормирования.ПодписатьПакет = Истина;
	КонецЕсли;
	
	Если ("1" = ЭтотОбъект["ДанныеУведомления"].ЛистЕ.Е01020200) Тогда
		ПараметрыФормирования.СпособПредоставленияОтвета = ПредопределенноеЗначение(
			"Перечисление.СпособыПолученияОтветаНаЭлГосРегистрацию.ВыдатьДокументыЛично");
	Иначе
		ПараметрыФормирования.СпособПредоставленияОтвета = ПредопределенноеЗначение(
			"Перечисление.СпособыПолученияОтветаНаЭлГосРегистрацию.ВыслатьДокументыПоПочте");
	КонецЕсли;
	
	ВидДокумента = "ЗаявлениеПоФормеР24001";
	Если РегистрационныеФормыКлиентСервер.ОтправлятьЗаявлениеВXMLФормате(СпособПодачиДокументов, ФорматОтправкиДокументов) Тогда 
		РезультатыВыгрузки = ПолучитьИзВременногоХранилища(ЭтотОбъект["АдресХранилища"]);
		ЗаявлениеВXML = Новый Массив;
		ЗаявлениеВXML.Добавить(ПоместитьВоВременноеХранилище(РезультатыВыгрузки.ФайлВыгрузкиДД, Новый УникальныйИдентификатор));
		РегистрационныеФормыКлиент.ДобавитьФайлВОписаниеФайлов(
			ВидДокумента, ЗаявлениеВXML, ПараметрыФормирования.ОписаниеФайлов, ПредопределенноеЗначение("Перечисление.ТипыСодержимогоФайлов.xml"));
			
		СканИзСервиса = Новый Массив;
		СканИзСервиса.Добавить(ПоместитьВоВременноеХранилище(РезультатыВыгрузки.ПДФФайлИзСервисаДД, Новый УникальныйИдентификатор));
		РегистрационныеФормыКлиент.ДобавитьФайлВОписаниеФайлов(
			ВидДокумента, СканИзСервиса, ПараметрыФормирования.ОписаниеФайлов, ПредопределенноеЗначение("Перечисление.ТипыСодержимогоФайлов.pdf"));
	Иначе
		Если ФайлыПрикрепленныеДокументы.СканЗаявление.Количество() > 0 Тогда
			РегистрационныеФормыКлиент.ДобавитьФайлВОписаниеФайлов(
				ВидДокумента, ФайлыПрикрепленныеДокументы.СканЗаявление, ПараметрыФормирования.ОписаниеФайлов);
		КонецЕсли;
	КонецЕсли;
	
	Если ФайлыПрикрепленныеДокументы.ИныеДокументы.Количество() > 0 Тогда
		ВидДокумента = "ИнойДокумент";
		Для Каждого ТекущийИнойДокумент Из ФайлыПрикрепленныеДокументы.ИныеДокументы Цикл
			РегистрационныеФормыКлиент.ДобавитьФайлВОписаниеФайлов("ИнойДокумент", ТекущийИнойДокумент.Значение, ПараметрыФормирования.ОписаниеФайлов);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыФормирования.ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПодписатьДокументыЗавершение", ЭтотОбъект, ПараметрыФормирования.ОписаниеФайлов);
	
	ИнтерфейсыВзаимодействияБРОКлиент.СформироватьПакетПоЕГРЮЛ(ПараметрыФормирования);
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДокументыЗавершение(РезультатПодписания, ДополнительныеПараметры) Экспорт
	ПодписатьДокументыНаСервере(РезультатПодписания, ДополнительныеПараметры);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыОтображенияЭлементовФормы(Форма)
	Результат = Новый Структура;
	Результат.Вставить("Заявление", Форма.Объект.Ссылка);
	Результат.Вставить("ЭтоЮрЛицо", Ложь);
	Результат.Вставить("ЕстьУведомлениеОПереходеНаУСН", Истина);
	Результат.Вставить("ЕстьПатент", Ложь);
	Возврат Результат
КонецФункции

&НаСервере
Процедура ПодписатьДокументыНаСервере(РезультатПодписания, ДополнительныеПараметры)
	РегистрационныеФормы.ОбработатьРезультатПодписанияДокументов(
		ЭтотОбъект, РезультатПодписания, ПараметрыОтображенияЭлементовФормы(ЭтотОбъект), ДополнительныеПараметры);
	
	Если ЗначениеЗаполнено(ПодписанныеДокументы)
		И СпособПодачиДокументов = РегистрационныеФормыКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронно Тогда
		
		Модифицированность = Истина;
		СохранитьДанные();
		ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправкиУведомления(
			Объект.Ссылка, ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусПодготовленоСтрокой());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодписанныеДокументы(Команда)
	ОчиститьПодписанныеДокументыИУстановитьКодРегистрирующегоОргана();
КонецПроцедуры

&НаСервере
Процедура ОчиститьПодписанныеДокументыИУстановитьКодРегистрирующегоОргана()
	РегистрирующийОрганКод = "";
	РегистрационныеФормыВызовСервера.УдалитьПрикрепленныеФайлы(
		Объект.Ссылка, РегистрационныеФормыКлиентСервер.ПредставлениеПодписанныеФайлы());
	ПодписанныеДокументы = Неопределено;
	Модифицированность = Истина;
	СохранитьДанные();
	ИнтерфейсыВзаимодействияБРО.СохранитьСтатусОтправкиУведомления(
		Объект.Ссылка, ИнтерфейсыВзаимодействияБРОКлиентСервер.СтатусВРаботеСтрокой());
	РегистрационныеФормыКлиентСервер.УправлениеФормойПриПодачеДокументовВЭлектронномВиде(ЭтотОбъект, ПараметрыОтображенияЭлементовФормы(ЭтотОбъект));
КонецПроцедуры

&НаКлиенте
Процедура ФорматОтправкиДокументовПриИзменении(Элемент)
	ПриИзмененииОпцийСдачи(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриИзмененииОпцийСдачи(Форма)
	РегистрационныеФормыКлиентСервер.УправлениеФормойПриПодачеДокументовВЭлектронномВиде(
		Форма, ПараметрыОтображенияЭлементовФормы(Форма));
	РегистрационныеФормыКлиентСервер.УправлениеСвойствамиГруппыПодтвержденияКонтактнойИнформации(
		Форма, ДополнительныеПараметрыКонтактнойИнформации(Форма));
КонецПроцедуры

&НаКлиенте
Процедура СтатусОтправкиНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	РегистрационныеФормыКлиент.СтатусОтправкиОбработкаНавигационнойСсылки(
		НавигационнаяСсылкаФорматированнойСтроки,
		Объект.Ссылка,
		РегистрационныеФормыКлиентСервер.ТипФайлаЛистЗаписиЕГР(Истина),
		УникальныйИдентификатор,
		СтандартнаяОбработка);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Форма.Элементы.ГруппаУведомлениеИзПомощника.Видимость = Форма.УведомлениеЗаполненоВПомощнике;
	Форма.Элементы.ГруппаПодписаниеИОтправка.Видимость = (Форма.ИнтернетПоддержкаПодключена И Не Форма.УведомлениеЗаполненоВПомощнике);
	Форма.Элементы.ГруппаПодписаниеИОтправка.ТолькоПросмотр = Форма.УведомлениеЗаполненоВПомощнике;
	
	ВозможностьОтправки = (Форма.ИнтернетПоддержкаПодключена И Не Форма.УведомлениеЗаполненоВПомощнике);
	Форма.Элементы.РегистрирующийОрганКод.Видимость = ВозможностьОтправки;
	Форма.Элементы.ГруппаСпособПодачиДокументов.Видимость = ВозможностьОтправки;
	Форма.Элементы.ГруппаФорматОтправкиДокументов.Видимость = ВозможностьОтправки;
	Форма.Элементы.ГруппаСпособПолученияДокументов.Видимость = ВозможностьОтправки;
	Форма.Элементы.ПрикрепленныеФайлы.Видимость = ВозможностьОтправки;
	Форма.Элементы.ЗаголовокФормированиеТранспортногоКонтейнера.Видимость = ВозможностьОтправки 
		И Форма.СпособПодачиДокументов = РегистрационныеФормыКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронно;
	Форма.Элементы.ГруппаФормированиеТранспортногоКонтейнера.Видимость = ВозможностьОтправки 
		И Форма.СпособПодачиДокументов = РегистрационныеФормыКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронно;
	Форма.Элементы.ГруппаОтправкаДокументовОнлайн.Видимость = ВозможностьОтправки 
		И Форма.СпособПодачиДокументов = РегистрационныеФормыКлиентСервер.СпособыПодачиДокументов().ИФНСэлектронноОнлайн;
КонецПроцедуры

#КонецОбласти
