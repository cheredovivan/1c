#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность.Интерфейс85") Тогда
		МодульРегламентированнаяОтчетностьИнтерфейс85 =
			ОбщегоНазначения.ОбщийМодуль("РегламентированнаяОтчетностьИнтерфейс85");
		МодульРегламентированнаяОтчетностьИнтерфейс85.УстановитьНазначениеДействияКоманды(Команды.ВыбратьОтчет);
	КонецЕсли;
	
	Список.Параметры.УстановитьЗначениеПараметра("Организация", Параметры.Организация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборОтчета(Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьОтчет(Команда)
	
	ОбработатьВыборОтчета(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборОтчета(СписокОтчетов)
	
	Если СписокОтчетов.ТекущиеДанные = Неопределено Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ПредставлениеОтчета = СписокОтчетов.ТекущиеДанные.ФинансовыйПериод;
	
	РезультатВыбора = Новый Структура;
	РезультатВыбора.Вставить("РегламентированныйОтчет", СписокОтчетов.ТекущиеДанные.РегламентированныйОтчет);
	РезультатВыбора.Вставить("ПредставлениеОтчета", ПредставлениеОтчета);
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

#КонецОбласти
