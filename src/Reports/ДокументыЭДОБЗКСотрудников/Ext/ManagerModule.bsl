#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВариантыОтчетов

// См. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.
//
Процедура НастроитьВариантыОтчета(Настройки, НастройкиОтчета) Экспорт
	
	НастройкиОтчета.ОпределитьНастройкиФормы = Истина;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыНаПодписании");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает сотрудников у которых есть не подписанные документы ЭДО';
										|en = 'The report shows employees who have unsigned EDI documents'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыБезПодписиОрганизации");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает документы ЭДО не подписанные со стороны организации';
										|en = 'The report shows EDI documents not signed by the company'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыНеПереданныеСотрудникам");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает документы ЭДО созданные, но так и не перданные сотрудникам';
										|en = 'The report shows created EDI documents that were not transferred to employees'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "ДокументыПолностьюПодписанные");
	НастройкиВарианта.Описание = НСтр("ru = 'Отчет показывает документы ЭДО работа с которыми завершена';
										|en = 'The report shows EDI documents for which work is completed'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
	НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, НастройкиОтчета, "КарточкаЭлектронногоДокумента");
	НастройкиВарианта.Описание = НСтр("ru = 'Формирует карточки документов ЭДО';
										|en = 'Generates EDI document cards'");
	НастройкиВарианта.ВидимостьПоУмолчанию = Ложь;
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВариантыОтчетов

#КонецОбласти

#КонецОбласти
	
#Область СлужебныйПрограммныйИнтерфейс

Процедура ДобавитьКомандыФормированияОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Отчеты.ДокументыЭДОБЗКСотрудников) Тогда
		Возврат;
	КонецЕсли;
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Документы на подписании';
										|en = 'Documents under signing'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыЭДОБЗКСотрудников";
	Команда.КлючВарианта       = "ДокументыНаПодписании";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыЭДОБЗКСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Без подписи организации';
										|en = 'Without company signature'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыЭДОБЗКСотрудников";
	Команда.КлючВарианта       = "ДокументыБезПодписиОрганизации";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыЭДОБЗКСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Не переданные сотрудникам';
										|en = 'Not transferred to employees'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыЭДОБЗКСотрудников";
	Команда.КлючВарианта       = "ДокументыНеПереданныеСотрудникам";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыЭДОБЗКСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
	Команда = КомандыОтчетов.Добавить();
	Команда.Представление      = НСтр("ru = 'Полностью подписанные';
										|en = 'Fully signed'");
	Команда.Важность           = "Обычное";
	Команда.Менеджер           = "Отчет.ДокументыЭДОБЗКСотрудников";
	Команда.КлючВарианта       = "ДокументыПолностьюПодписанные";
	Команда.СочетаниеКлавиш    = Новый СочетаниеКлавиш(Клавиша.A, Ложь, Истина, Истина);
	Команда.Подменю            = "ДокументыЭДОБЗКСотрудников";
	Команда.Картинка           = БиблиотекаКартинок.Отчет;
	Команда.Назначение         = "ДляСписка";
	
КонецПроцедуры

Процедура ДобавитьКомандуПечати(КомандыПечати, ИдентификаторыПФ = Неопределено, СторонняяПечатнаяФорма = Ложь) Экспорт
	
	// Трудовой договор микропредприятий
	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДокументыЭДОБЗКСотрудников) Тогда
		
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.Обработчик = "УправлениеПечатьюБЗККлиент.ВыполнитьКомандуПечати";
		КомандаПечати.МенеджерПечати = "Отчет.ДокументыЭДОБЗКСотрудников";
		КомандаПечати.Идентификатор = "ПФ_MXL_КарточкаЭлектронногоДокумента";
		КомандаПечати.Представление = НСтр("ru = 'Карточка';
											|en = 'Card'");
		
		Если Не Пользователи.ЭтоПолноправныйПользователь() И ИдентификаторыПФ <> Неопределено Тогда
			ЗарплатаКадры.ДобавитьИдентификаторКомандыДляПечатиВПакетномРежиме(ИдентификаторыПФ, КомандаПечати);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Отборы = Новый Массив;
	Отборы.Добавить(
		Новый Структура("ЛевоеЗначение,ВидСравнения,ПравоеЗначение",
			"СсылкаНаОбъект",
			ВидСравненияКомпоновкиДанных.ВСписке,
			МассивОбъектов));
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Отбор", Отборы);
	
	ЗарплатаКадрыОтчеты.ВывестиВКоллекциюПечатнуюФорму("Отчет.ДокументыЭДОБЗКСотрудников",
		МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода, ВнешниеНаборыДанных(), ДополнительныеПараметры);
		
КонецПроцедуры

Процедура Сформировать(ДокументРезультат, РезультатКомпоновки, ОбъектыПечати = Неопределено, ПараметрыПечати = Неопределено) Экспорт

	ДокументРезультат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_КарточкаЭлектронногоДокумента";
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ДокументРезультат.АвтоМасштаб = Истина;
	
	Для Каждого ДанныеНаПечать Из РезультатКомпоновки.ДанныеОтчета.Строки Цикл
		
		Если ДокументРезультат.ВысотаТаблицы > 0 Тогда
			ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПерваяСтрокаПечатнойФормы = ДокументРезультат.ВысотаТаблицы + 1;
		
		Для Каждого ДанныеДетальныхЗаписей Из ДанныеНаПечать.Строки Цикл
			
			ДанныеПользовательскихПолей = ЗарплатаКадрыОтчеты.ЗначенияЗаполненияПользовательскихПолей(РезультатКомпоновки.ИдентификаторыМакета, ДанныеДетальныхЗаписей);
			
			ОбластиШапки = "Шапка,Основание,Свойства";
			Если ТипЗнч(ДанныеДетальныхЗаписей.СсылкаНаОбъектОснованиеДокумента) = Тип("ДокументСсылка.ДокументЭДОБЗК") Тогда
				ОбластиШапки = "Шапка,Свойства";
			КонецЕсли;
			ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
				ДокументРезультат,
				РезультатКомпоновки.МакетПечатнойФормы,
				ОбластиШапки,
				ДанныеНаПечать,
				ДанныеДетальныхЗаписей,
				ДанныеПользовательскихПолей);
				
			Если ДанныеДетальныхЗаписей.Строки.Количество() > 0 Тогда
				
				ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
					ДокументРезультат,
					РезультатКомпоновки.МакетПечатнойФормы,
					"МероприятияШапка",
					ДанныеНаПечать,
					ДанныеДетальныхЗаписей,
					ДанныеПользовательскихПолей);
					
				Для Каждого СтрокаМероприятия Из ДанныеДетальныхЗаписей.Строки Цикл
					
					ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
						ДокументРезультат,
						РезультатКомпоновки.МакетПечатнойФормы,
						"МероприятияСтрока",
						ДанныеНаПечать,
						СтрокаМероприятия,
						ДанныеПользовательскихПолей);
						
				КонецЦикла;
				
			КонецЕсли;
			
			Запрос = Новый Запрос;
			Запрос.УстановитьПараметр("СсылкаНаОбъект", ДанныеНаПечать.СсылкаНаОбъект);
			Запрос.Текст =
				"ВЫБРАТЬ
				|	ДокументЭДОБЗКВнешниеПодписанты.НомерСтроки КАК НомерСтроки,
				|	ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо КАК ФизическоеЛицо,
				|	ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо.ФИО КАК КомуВыданСертификат,
				|	ИСТИНА КАК ПодписьСотрудника,
				|	ПодписиДокументовЭДОБЗК.Отпечаток КАК Отпечаток,
				|	ПодписиДокументовЭДОБЗК.ДатаПодписи КАК ДатаПодписи,
				|	ПодписиДокументовЭДОБЗК.РезультатСогласования КАК РезультатСогласования
				|ИЗ
				|	Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗКВнешниеПодписанты
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодписиДокументовЭДОБЗК КАК ПодписиДокументовЭДОБЗК
				|		ПО ДокументЭДОБЗКВнешниеПодписанты.Ссылка = ПодписиДокументовЭДОБЗК.Объект
				|			И ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо = ПодписиДокументовЭДОБЗК.ФизическоеЛицо
				|ГДЕ
				|	ДокументЭДОБЗКВнешниеПодписанты.Ссылка = &СсылкаНаОбъект";
			
			ПодписиОбъекта = Запрос.Выполнить().Выгрузить();
			ПодписиОбъекта.Колонки.Добавить("Статус", Новый ОписаниеТипов("Строка"));
			ПодписиОбъекта.Колонки.Добавить("ПодписьВерна", Новый ОписаниеТипов("Булево"));
			ПодписиОбъекта.Колонки.Добавить("ДатаПроверкиПодписи", Новый ОписаниеТипов("Дата"));
			ПодписиОбъекта.Колонки.Добавить("РезультатПроверкиПодписи", Новый ОписаниеТипов("Строка"));
			ПодписиОбъекта.Колонки.Добавить("Доверенность", Новый ОписаниеТипов("Строка"));
			
			УстановленныеПодписи = ЭлектроннаяПодписьЭДОБЗК.УстановленныеПодписи(ДанныеНаПечать.СсылкаНаОбъектЭлектронныйДокумент);
			Для Каждого УстановленнаяПодпись Из УстановленныеПодписи Цикл
				
				Доверенность = "";
				Если ЗначениеЗаполнено(УстановленнаяПодпись.РезультатПроверкиПодписиПоМЧД) Тогда
					Доверенности = Новый Массив;
					Для Каждого МЧДПодписи Из УстановленнаяПодпись.РезультатПроверкиПодписиПоМЧД Цикл
						Доверенности.Добавить(Строка(МЧДПодписи.МашиночитаемаяДоверенность));
					КонецЦикла;
					Если Доверенности.Количество() = 1 Тогда
						Доверенность = СтрШаблон(НСтр("ru = 'Доверенность: %1';
														|en = 'Authorization letter: %1'"), Доверенности[0]);
					Иначе
						Доверенность = СтрШаблон(НСтр("ru = 'Доверенности: %1';
														|en = 'Authorization letters: %1'"), СтрСоединить(Доверенности, ", "));
					КонецЕсли;
					
				КонецЕсли;
				
				СтрокиДокумента = ПодписиОбъекта.НайтиСтроки(Новый Структура("Отпечаток,ДатаПодписи", УстановленнаяПодпись.Отпечаток, УстановленнаяПодпись.ДатаПодписи));
				Если Не ЗначениеЗаполнено(СтрокиДокумента) Тогда
						СтрокаДокумента = ПодписиОбъекта.Добавить();
						СтрокаДокумента.ПодписьСотрудника = Ложь;
						СтрокаДокумента.ДатаПодписи = УстановленнаяПодпись.ДатаПодписи;
						СтрокаДокумента.РезультатСогласования = Перечисления.РезультатыСогласованияБЗК.Согласовано;
						СтрокаДокумента.Отпечаток = УстановленнаяПодпись.Отпечаток;
						СтрокаДокумента.КомуВыданСертификат = УстановленнаяПодпись.КомуВыданСертификат;
						СтрокаДокумента.ДатаПроверкиПодписи = УстановленнаяПодпись.ДатаПроверкиПодписи;
						СтрокаДокумента.ПодписьВерна = УстановленнаяПодпись.ПодписьВерна;
						СтрокаДокумента.Доверенность = Доверенность;
				Иначе
					Для Каждого СтрокаДокумента Из СтрокиДокумента Цикл
						СтрокаДокумента.КомуВыданСертификат = УстановленнаяПодпись.КомуВыданСертификат;
						СтрокаДокумента.ДатаПроверкиПодписи = УстановленнаяПодпись.ДатаПроверкиПодписи;
						СтрокаДокумента.ПодписьВерна = УстановленнаяПодпись.ПодписьВерна;
						СтрокаДокумента.Доверенность = Доверенность;
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			ПодписиОбъекта.Сортировать("ПодписьСотрудника,НомерСтроки,ДатаПодписи");
			Для Каждого СтрокаПодписи Из ПодписиОбъекта Цикл
				ЗаполнитьСтатусПроверкиПодписиОбъекта(СтрокаПодписи);
				СтрокиРезультата = Новый Массив;
				СтрокиРезультата.Добавить(СтрокаПодписи.Статус);                                                           
				Если ЗначениеЗаполнено(СтрокаПодписи.ДатаПроверкиПодписи) Тогда
					СтрокиРезультата.Добавить(СтрокаПодписи.ДатаПроверкиПодписи);
				КонецЕсли;
				Если ЗначениеЗаполнено(СтрокаПодписи.Доверенность) Тогда
					СтрокиРезультата.Добавить(СтрокаПодписи.Доверенность);
				КонецЕсли;
				СтрокаПодписи.РезультатПроверкиПодписи = СтрСоединить(СтрокиРезультата, ", ");
			КонецЦикла;
			
			ПодписиОтветсвенныхЛиц = ПодписиОбъекта.Скопировать(Новый Структура("ПодписьСотрудника", Ложь));
			Если ЗначениеЗаполнено(ПодписиОтветсвенныхЛиц) Тогда
				
				ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
					ДокументРезультат,
					РезультатКомпоновки.МакетПечатнойФормы,
					"ОтветственныеЛицаШапка",
					ДанныеНаПечать,
					ДанныеДетальныхЗаписей,
					ДанныеПользовательскихПолей);
					
				Для Каждого ПодписьОтветсвенногоЛица Из ПодписиОтветсвенныхЛиц Цикл
					
					ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
						ДокументРезультат,
						РезультатКомпоновки.МакетПечатнойФормы,
						"ОтветственныеЛицаСтрока",
						ДанныеНаПечать,
						ПодписьОтветсвенногоЛица,
						ДанныеПользовательскихПолей);
						
				КонецЦикла;
					
			КонецЕсли;
			
			ПодписиСотрудников = ПодписиОбъекта.Скопировать(Новый Структура("ПодписьСотрудника", Истина));
			Если ЗначениеЗаполнено(ПодписиСотрудников) Тогда
				
				ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
					ДокументРезультат,
					РезультатКомпоновки.МакетПечатнойФормы,
					"СотрудникиШапка",
					ДанныеНаПечать,
					ДанныеДетальныхЗаписей,
					ДанныеПользовательскихПолей);
					
				Для Каждого ПодписьСотрудника Из ПодписиСотрудников Цикл
					
					ЗарплатаКадрыОтчеты.ВывестиВДокументРезультатОбластиМакета(
						ДокументРезультат,
						РезультатКомпоновки.МакетПечатнойФормы,
						"СотрудникиСтрока",
						ДанныеНаПечать,
						ПодписьСотрудника,
						ДанныеПользовательскихПолей);
						
				КонецЦикла;
					
			КонецЕсли;
		
		КонецЦикла;
			
		Если ОбъектыПечати <> Неопределено Тогда
			УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ДокументРезультат, ПерваяСтрокаПечатнойФормы, ОбъектыПечати, ДанныеНаПечать.СсылкаНаОбъект);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСтатусПроверкиПодписиОбъекта(Объект) Экспорт
	
	Если Объект.ПодписьВерна Тогда
		Объект.Статус = НСтр("ru = 'Верна';
							|en = 'Correct'");
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаПроверкиПодписи) Тогда
		Объект.Статус = НСтр("ru = 'Неверна';
							|en = 'Incorrect'");
	ИначеЕсли ЗначениеЗаполнено(Объект.ДатаПодписи) Тогда
		Если ЗначениеЗаполнено(Объект.Отпечаток) Тогда
			Объект.Статус = НСтр("ru = 'Не проверялась';
								|en = 'Not checked'");
		Иначе
			Объект.Статус = НСтр("ru = 'Требуется собственноручная подпись';
								|en = 'Sign manually'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ВнешниеНаборыДанных() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КомментарииФайловДокументовЭДОБЗК.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(КомментарииФайловДокументовЭДОБЗК.Исполнитель) = ТИП(Справочник.ФизическиеЛица)
		|			ТОГДА КомментарииФайловДокументовЭДОБЗК.Исполнитель
		|		ИНАЧЕ КомментарииФайловДокументовЭДОБЗК.Исполнитель.ФизическоеЛицо
		|	КОНЕЦ КАК Исполнитель,
		|	КомментарииФайловДокументовЭДОБЗК.Комментарий КАК Комментарий,
		|	КомментарииФайловДокументовЭДОБЗК.ДатаКомментария КАК ДатаКомментария
		|ИЗ
		|	РегистрСведений.КомментарииФайловДокументовЭДОБЗК КАК КомментарииФайловДокументовЭДОБЗК";
	
	УстановитьПривилегированныйРежим(Истина);
	ВнешниеНаборы = Новый Структура("КомментарииЭлектронныхДокументов",  Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ВнешниеНаборы
	
КонецФункции

#КонецОбласти
	
#КонецЕсли