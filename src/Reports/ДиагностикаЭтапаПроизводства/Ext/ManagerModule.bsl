#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Формирует отчет.
//
// Параметры:
//  Параметры  - Структура - параметры формирования отчета. Содержит:
//		* Этап - ДокументСсылка.ЭтапПроизводства2_2 - диагностируемый этап.
//		* СтатусГрафика - Число - статус диагностируемого графика.
//		* ТабличныйДокумент - ТабличныйДокумент - табличный документ.
//  АдресХранилища - Строка - адрес хранилища, в которое будет помещен результат формирования отчета.
//
Процедура СформироватьОтчет(Параметры, АдресХранилища) Экспорт
	
	ТабличныйДокумент = Параметры.ТабличныйДокумент;
	ТабличныйДокумент.Очистить();
	РасшифровкиАльтернатив = Новый Массив;
	
	ДанныеЭтапа = ДанныеЭтапа(Параметры.Этап, Параметры.СтатусГрафика, Параметры.КлючПартия);
	
	МакетОформления = Отчеты.ДиагностикаЭтапаПроизводства.ПолучитьМакет("ДиагностикаЭтапа");
	
	Если ДанныеЭтапа <> Неопределено Тогда
		ВывестиШапку(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
		ВывестиСмежныеЭтапы(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
		ВывестиЛевуюГраницу(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
		ВывестиИсполнителей(ТабличныйДокумент, ДанныеЭтапа, МакетОформления, РасшифровкиАльтернатив);
		ВывестиМатериалы(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
		ОбвестиГраницыЭтапа(ТабличныйДокумент, ДанныеЭтапа);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ТабличныйДокумент", ТабличныйДокумент);
	Результат.Вставить("РасшифровкиАльтернатив", РасшифровкиАльтернатив);
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#Область КомандыПодменюОтчеты

// Добавляет команду отчета в список команд.
// 
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//
// Возвращаемое значение:
//  - СтрокаТаблицыЗначений.
//  - Неопределено.
//
Функция ДобавитьКомандуДиагностикаГрафикаЭтапаПроизводства(КомандыОтчетов) Экспорт

	Если ПравоДоступа("Просмотр", Метаданные.Отчеты.ДиагностикаЭтапаПроизводства)
		И УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
		
		КомандаОтчет = КомандыОтчетов.Добавить();
		
		КомандаОтчет.Идентификатор      = Метаданные.Отчеты.ДиагностикаЭтапаПроизводства.ПолноеИмя();
		КомандаОтчет.Представление      = НСтр("ru = 'Диагностика графика';
												|en = 'Schedule adjustment'");
		КомандаОтчет.ИмяФормы           = "Отчет.ДиагностикаЭтапаПроизводства.Форма";
		КомандаОтчет.МножественныйВыбор = Ложь;
		КомандаОтчет.Важность           = "Обычное";
		
		Возврат КомандаОтчет;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// Задает расширенные настройки отчета
//
// Параметры:
//   Настройки - см. ВариантыОтчетовПереопределяемый.НастроитьВариантыОтчетов.Настройки.
//
Процедура НастроитьВариантыОтчета(Настройки) Экспорт
	
	ОписаниеОтчета = ВариантыОтчетов.ОписаниеОтчета(Настройки, Метаданные.Отчеты.ДиагностикаЭтапаПроизводства);
	ВариантыОтчетовУТПереопределяемый.ОтключитьОтчет(ОписаниеОтчета);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СмежныеЭтапы

Процедура ВывестиСмежныеЭтапы(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	Этапы = СмежныеЭтапы(ДанныеЭтапа);
	Если Этапы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьЗаголовок = МакетОформления.ПолучитьОбласть("СмежныеЭтапыЗаголовок");
	Если ДанныеЭтапа.РазмещениеКНачалу Тогда
		ОбластьЗаголовок.Область("R1C1").Текст = НСтр("ru = 'Предшественники';
														|en = 'Predecessors stages'");
	Иначе
		ОбластьЗаголовок.Область("R1C1").Текст = НСтр("ru = 'Последователи';
														|en = 'Successors'");
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	Оформления = ОформленияИсполнители(МакетОформления);
	
	ОбластьЭтап = МакетОформления.ПолучитьОбласть("Этап");
	ОбластьЭтапДанные = МакетОформления.ПолучитьОбласть("ЭтапДанные");
	
	Для каждого Этап Из Этапы Цикл
		
		ОбластьЭтап.Параметры.ЭтапПроизводства = Этап.ЭтапПроизводства;
		ОбластьЭтап.Параметры.ПредставлениеЭтапа = Этап.ПредставлениеЭтапа;
		ТабличныйДокумент.Вывести(ОбластьЭтап);
		
		Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
			Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
			Начало = Интервал.Начало;
			
			Если ДанныеЭтапа.РазмещениеКНачалу Тогда
				
				Если Начало < Этап.ОкончаниеЭтапа Тогда
					УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалЗанят);
				Иначе
					УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалДоступен);
				КонецЕсли;
				
			Иначе
				
				Окончание = Интервал.Окончание - 1;
				Если Окончание >= Этап.НачалоЭтапа И ЗначениеЗаполнено(Этап.НачалоЭтапа) Тогда
					УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалЗанят);
				Иначе
					УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалДоступен);
				КонецЕсли;
				
			КонецЕсли;
			
			ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьЭтапДанные);
			
			Расшифровка = Новый Структура;
			Расшифровка.Вставить("ВидОбласти", "ОткрытьДиагностикуСмежногоЭтапа");
			Расшифровка.Вставить("ЭтапПроизводства", Этап.ЭтапПроизводства);
			Расшифровка.Вставить("КлючПартия", Этап.КлючПартия);
			
			ДобавленнаяОбласть.Расшифровка = Расшифровка;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция СмежныеЭтапы(ДанныеЭтапа)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ЭтапПроизводства", ДанныеЭтапа.Этап);
	Запрос.УстановитьПараметр("КлючПартия", ДанныеЭтапа.КлючПартия);
	Запрос.УстановитьПараметр("СтатусГрафика", ДанныеЭтапа.СтатусГрафика);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	&ЭтапПроизводства КАК ЭтапПроизводства,
		|	&КлючПартия       КАК КлючПартия
		|ПОМЕСТИТЬ ДанныеЭтапа";
	
	Запрос.Выполнить();
	
	УправлениеПроизводством.СоздатьВТСвязиЭтапов(МенеджерВременныхТаблиц, "ДанныеЭтапа", "ЭтапПроизводства", "КлючПартия", Истина, Истина);
	
	ТекстыЗапросовПакета = Новый Массив;
	
	Если ДанныеЭтапа.РазмещениеКНачалу Тогда
		ТекстыЗапросовПакета.Добавить(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Связи.Этап КАК ЭтапПроизводства,
			|	Связи.КлючПартия КАК КлючПартия
			|ПОМЕСТИТЬ ВТЭтапы
			|ИЗ
			|	ВТСвязиЭтапов КАК Связи
			|ГДЕ
			|	Связи.СледующийЭтап = &ЭтапПроизводства
			|	И Связи.СледующийЭтапКлючПартия В (&КлючПартия, &ПустойКлючСвязи)");
	Иначе
		ТекстыЗапросовПакета.Добавить(
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Связи.СледующийЭтап КАК ЭтапПроизводства,
			|	Связи.СледующийЭтапКлючПартия КАК КлючПартия
			|ПОМЕСТИТЬ ВТЭтапы
			|ИЗ
			|	ВТСвязиЭтапов КАК Связи
			|ГДЕ
			|	Связи.Этап = &ЭтапПроизводства
			|	И Связи.КлючПартия В (&КлючПартия, &ПустойКлючСвязи)");
	КонецЕсли;
	
	ТекстыЗапросовПакета.Добавить(
		РегистрыСведений.ГрафикЭтаповПроизводства2_2.ТекстЗапросаВТСрокиВыполненияЭтапов(
			"ВТЭтапы",
			"ЭтапПроизводства"));
	
	ТекстыЗапросовПакета.Добавить(
		"ВЫБРАТЬ
		|	Этапы.ЭтапПроизводства КАК ЭтапПроизводства,
		|	Этапы.КлючПартия КАК КлючПартия,
		|	&ПредставлениеЭтапа КАК ПредставлениеЭтапа,
		|	ЕСТЬNULL(Сроки.НачалоЭтапа, ДАТАВРЕМЯ(1, 1, 1)) КАК НачалоЭтапа,
		|	ЕСТЬNULL(Сроки.ОкончаниеЭтапа, ДАТАВРЕМЯ(1, 1, 1)) КАК ОкончаниеЭтапа
		|ИЗ
		|	ВТЭтапы КАК Этапы
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСрокиВыполненияЭтапов КАК Сроки
		|		ПО Этапы.ЭтапПроизводства = Сроки.ЭтапПроизводства
		|		 И Этапы.КлючПартия = Сроки.КлючПартия
		|
		|УПОРЯДОЧИТЬ ПО
		|	НачалоЭтапа,
		|	ОкончаниеЭтапа");
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапросовПакета, ";");
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		УправлениеПроизводством.ТекстЗапросаПредставлениеЭтапа("Этапы.ЭтапПроизводства"));
	
	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#Область Исполнители

Процедура ВывестиИсполнителей(ТабличныйДокумент, ДанныеЭтапа, МакетОформления, РасшифровкиАльтернатив)
	
	ВывестиЗаголовокИсполнители(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
	
	Если ЭтоЭтапУББВ(ДанныеЭтапа) Тогда
		ВывестиРасписаниеПодразделения(ТабличныйДокумент, ДанныеЭтапа, МакетОформления);
	Иначе
		Для каждого Исполнитель Из ДанныеЭтапа.Исполнители Цикл
			Если НЕ Исполнитель.Альтернативный Тогда
				ВывестиИсполнителя(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления, РасшифровкиАльтернатив);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиЗаголовокИсполнители(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	ОбластьЗаголовок = МакетОформления.ПолучитьОбласть("ДоступностьВидовРабочихЦентровЗаголовок");
	
	Если ЭтоЭтапУББВ(ДанныеЭтапа) Тогда
		ЗаголовокИсполнители = НСтр("ru = 'Доступность подразделения';
									|en = 'Shop floor capacity'");
	Иначе
		ЗаголовокИсполнители = НСтр("ru = 'Доступность видов рабочих центров';
									|en = 'Work center types capacity'");
	КонецЕсли;
	
	ОбластьЗаголовок.Параметры.ЗаголовокИсполнители = ЗаголовокИсполнители;
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
КонецПроцедуры

Процедура ВывестиРасписаниеПодразделения(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	// Вывод заголовка.
	ОбластьЗаголовок = МакетОформления.ПолучитьОбласть("Исполнитель");
	ОбластьЗаголовок.Параметры.Исполнитель = ДанныеЭтапа.Подразделение;
	ОбластьЗаголовок.Параметры.Требуется = ПолучитьВремяСтрокой(
		ДанныеЭтапа.ДлительностьЭтапа, ДанныеЭтапа.ЕдиницаИзмеренияДлительностиЭтапа);
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	// Вывод расписания.
	ОбластьИсполнительНетДоступности = МакетОформления.ПолучитьОбласть("ИсполнительДанные");
	ОбластьИсполнительЕстьДоступность = МакетОформления.ПолучитьОбласть("ИсполнительДанныеБезОграниченияДоступности");
	
	Оформления = ОформленияИсполнители(МакетОформления);
	
	Расписание = РасписаниеПодразделения(ДанныеЭтапа);
	
	Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
		Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
		Начало = Интервал.Начало;
		Окончание = Интервал.Окончание;
		
		ИнтервалЗанят = Ложь;
		
		Если Расписание.Найти(Начало, "ДатаГрафика") = Неопределено Тогда
			
			ОбластьДанные = ОбластьИсполнительНетДоступности;
			
			УстановитьОформлениеОбласти(
				ОбластьДанные.Области.ИсполнительДанные,
				Оформления.ИнтервалНедоступен);
			
		Иначе
			
			ОбластьДанные = ОбластьИсполнительЕстьДоступность;
			
			Если (ДанныеЭтапа.НачалоЭтапа >= Начало И ДанныеЭтапа.НачалоЭтапа < Окончание)
				ИЛИ (Начало >= ДанныеЭтапа.НачалоЭтапа И Начало < ДанныеЭтапа.ОкончаниеЭтапа) Тогда
				
				Оформление = Оформления.ИнтервалЗанятДиагностируемымЭтапом;
				ИнтервалЗанят = Истина;
				
			Иначе
				
				Оформление = Оформления.ИнтервалДоступен;
				
			КонецЕсли;
			
			УстановитьОформлениеОбласти(
				ОбластьДанные.Области.ИсполнительДанныеБезОграниченияДоступности,
				Оформление);
			
		КонецЕсли;
		
		ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьДанные);
		
		Если ИнтервалЗанят Тогда
			ЗаполнитьГраницыЭтапа(ДанныеЭтапа, ДобавленнаяОбласть);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция РасписаниеПодразделения(ДанныеЭтапа)
	
	Если ДанныеЭтапа.ПроизводствоНаСтороне Тогда
		
		Графики = Новый ТаблицаЗначений;
		Графики.Колонки.Добавить("ГрафикРаботы", Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла));
		Графики.Колонки.Добавить("ГрафикРаботыСсылка", Новый ОписаниеТипов("СправочникСсылка.Календари"));
		
		НоваяСтрока = Графики.Добавить();
		НоваяСтрока.ГрафикРаботы = 1;
		НоваяСтрока.ГрафикРаботыСсылка = Константы.ОсновнойКалендарьПредприятия.Получить();
	
	Иначе
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		ПроизводствоСервер.СоздатьВТГрафикиРаботыПодразделений(
			ДанныеЭтапа.Подразделение, МенеджерВременныхТаблиц, "ВТГрафикиРаботы");
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	&ГрафикРаботы                КАК ГрафикРаботы,
			|	ВТГрафикиРаботы.ГрафикРаботы КАК ГрафикРаботыСсылка
			|ИЗ
			|	ВТГрафикиРаботы КАК ВТГрафикиРаботы");
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ГрафикРаботы", 1);
		Графики = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	Результат = Обработки.ПланированиеГрафикаПроизводства2_2.РасписаниеУББВ(
		Графики, ДанныеЭтапа.ПолныйИнтервалНачало, ДанныеЭтапа.ПолныйИнтервалОкончание);
	
	Результат.Индексы.Добавить("ДатаГрафика");
	
	Возврат Результат;
	
КонецФункции

Функция РасписаниеББВ(ДанныеЭтапа, Исполнители)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИсполнителиЭтапов", Исполнители);
	Параметры.Вставить("РасписаниеББВ", Неопределено);
	
	Обработки.ПланированиеГрафикаПроизводства2_2.ПрочитатьРасписаниеББВ(
		Параметры,
		ДанныеЭтапа.ПолныйИнтервалНачало,
		ДанныеЭтапа.ПолныйИнтервалОкончание);
	
	Возврат Параметры.РасписаниеББВ;
	
КонецФункции

Функция ЗагрузкаВРЦБезОграничений(ДанныеЭтапа, Ссылки)
	
	Если ДанныеЭтапа.СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	СУММА(ДоступностьВидовРабочихЦентров.Занято) КАК ЗанятоТекущим
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРЦ
		|	ПО ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра = ВидыРЦ.Ссылка
		|
		|ГДЕ
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра В (&ВидыРабочихЦентров)
		|	И (ДатаИнтервала МЕЖДУ &Начало И &Окончание)
		|	И ДоступностьВидовРабочихЦентров.Распоряжение = &Распоряжение
		|	И ДоступностьВидовРабочихЦентров.Этап = &Этап
		|	И (ТИПЗНАЧЕНИЯ(ДоступностьВидовРабочихЦентров.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|	ИЛИ ДоступностьВидовРабочихЦентров.КлючПартия = &КлючПартия)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	ВидыРЦ.ПараллельнаяЗагрузка,
		|	ВидыРЦ.МаксимальнаяЗагрузка";
		
	Иначе
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ДатаИнтервала,
		|	СУММА(ВЫБОР
		|			КОГДА ПланированиеЗагрузкиВидовРабочихЦентров.Этап = &Этап
		|				ТОГДА ПланированиеЗагрузкиВидовРабочихЦентров.Занято
		|			ИНАЧЕ 0
		|		КОНЕЦ)
		|ИЗ
		|	РегистрСведений.ПланированиеЗагрузкиВидовРабочихЦентров КАК ПланированиеЗагрузкиВидовРабочихЦентров
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРЦ
		|	ПО ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра = ВидыРЦ.Ссылка
		|
		|ГДЕ
		|	ПланированиеЗагрузкиВидовРабочихЦентров.СтатусГрафика = &СтатусГрафика
		|	И ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра В(&ВидыРабочихЦентров)
		|	И ПланированиеЗагрузкиВидовРабочихЦентров.ДатаИнтервала МЕЖДУ &Начало И &Окончание
		|
		|СГРУППИРОВАТЬ ПО
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ДатаИнтервала,
		|	ВидыРЦ.ПараллельнаяЗагрузка,
		|	ВидыРЦ.МаксимальнаяЗагрузка";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("СтатусГрафика", ДанныеЭтапа.СтатусГрафика);
	Запрос.УстановитьПараметр("Распоряжение", ДанныеЭтапа.Распоряжение);
	Запрос.УстановитьПараметр("ВидыРабочихЦентров", Ссылки);
	Запрос.УстановитьПараметр("Начало", ДанныеЭтапа.ПолныйИнтервалНачало);
	Запрос.УстановитьПараметр("Окончание", ДанныеЭтапа.ПолныйИнтервалОкончание);
	Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Этап);
	Запрос.УстановитьПараметр("КлючПартия", ДанныеЭтапа.КлючПартия);
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("ВидРабочегоЦентра, ДатаИнтервала");
	
	Возврат Результат;
	
КонецФункции

Функция ДоступностьВРЦ(ДанныеЭтапа, Ссылки)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Распоряжение", ДанныеЭтапа.Распоряжение);
	Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Этап);
	Запрос.УстановитьПараметр("СтатусГрафика", ДанныеЭтапа.СтатусГрафика);
	Запрос.УстановитьПараметр("КлючПартия", ДанныеЭтапа.КлючПартия);
	
	МассивЭтапов = Новый Массив;
	СоздатьВТЗагрузкаРаспоряженияИЗаполнитьЭтапыИсключения(ДанныеЭтапа, Запрос, МассивЭтапов);
	
	Параметры = Новый Структура("Очередь, ПриоритетНомер, ПодразделениеНомер");
	ЗаполнитьЗначенияСвойств(Параметры, ДанныеЭтапа);
	Параметры.Вставить("ЗаказНаПроизводство", ДанныеЭтапа.Распоряжение);
	Параметры.Вставить("РазмещениеКНачалу", Ложь);
	Параметры.Вставить("ОтсутствиеПрочихЗаказов", Ложь);
	Параметры.Вставить("ОтменитьРучныеИзмененияГрафика", Ложь);
	Параметры.Вставить("КруглосуточнаяРаботаБезВыходных", Ложь);
	Параметры.Вставить("КлючПартия", ДанныеЭтапа.КлючПартия);
	
	Обработки.ПланированиеГрафикаПроизводства2_2.СоздатьВТДоступностьВидовРЦ(
		МенеджерВременныхТаблиц,
		"ВТДоступность",
		Ссылки,
		МассивЭтапов,
		ДанныеЭтапа.ПолныйИнтервалНачало, 
		ДанныеЭтапа.ПолныйИнтервалОкончание,
		Параметры);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТДоступность.Исполнитель КАК ВидРабочегоЦентра,
	|	ВТДоступность.ДатаИнтервала,
	|	ЕСТЬNULL(ВТЗагрузкаРаспоряжения.ЗанятоТекущим, 0) КАК ЗанятоТекущим,
	|	ВТДоступность.КоличествоРасход + ЕСТЬNULL(ВТЗагрузкаРаспоряжения.ЗанятоПрочими, 0) КАК ЗанятоПрочими,
	|	ВТДоступность.КоличествоПриход - (ЕСТЬNULL(ВТЗагрузкаРаспоряжения.ЗанятоТекущим, 0) + ВТДоступность.КоличествоРасход + ЕСТЬNULL(ВТЗагрузкаРаспоряжения.ЗанятоПрочими, 0)) КАК СвободноСРезервом,
	|	ВТДоступность.КоличествоПриход КАК ОбщаяДоступность,
	|	ВЫРАЗИТЬ(&Резерв КАК ЧИСЛО(15, 0)) КАК РезервОбщий,
	|	&ДоступноДляТекущегоСРезервом КАК ДоступноДляТекущегоСРезервом,
	|	ВЫБОР
	|		КОГДА НЕ ВТДоступность.РезервДоступности = 0
	|			ТОГДА ВЫБОР
	|					КОГДА &Резерв <= &ДоступноДляТекущегоСРезервом
	|						ТОГДА ВЫРАЗИТЬ(&Резерв КАК ЧИСЛО(15, 0))
	|					ИНАЧЕ &ДоступноДляТекущегоСРезервом
	|				КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РезервДоступноДляТекущего
	|ИЗ
	|	ВТДоступность КАК ВТДоступность
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗагрузкаРаспоряжения КАК ВТЗагрузкаРаспоряжения
	|		ПО ВТДоступность.Исполнитель = ВТЗагрузкаРаспоряжения.ВидРабочегоЦентра
	|			И ВТДоступность.ДатаИнтервала = ВТЗагрузкаРаспоряжения.ДатаИнтервала";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДоступноДляТекущегоСРезервом",
		"ВТДоступность.КоличествоПриход - (ВТДоступность.КоличествоРасход + ЕСТЬNULL(ВТЗагрузкаРаспоряжения.ЗанятоПрочими, 0))");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Резерв",
		"ВТДоступность.КоличествоПриход * (ВТДоступность.РезервДоступности / 100)");
	
	Запрос.Текст = ТекстЗапроса;
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Индексы.Добавить("ВидРабочегоЦентра, ДатаИнтервала");
	
	Возврат Результат;
	
КонецФункции

Процедура СоздатьВТЗагрузкаРаспоряженияИЗаполнитьЭтапыИсключения(ДанныеЭтапа, Запрос, ЭтапыИсключения)
	
	Если ДанныеЭтапа.СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	СУММА(ДоступностьВидовРабочихЦентров.Занято) КАК ЗанятоТекущим,
		|	СУММА(0) КАК ЗанятоПрочими
		|ПОМЕСТИТЬ ВТЗагрузкаРаспоряжения
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК ДоступностьВидовРабочихЦентров
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРЦ
		|	ПО ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра = ВидыРЦ.Ссылка
		|
		|ГДЕ
		|	ДоступностьВидовРабочихЦентров.Распоряжение = &Распоряжение
		|	И ДоступностьВидовРабочихЦентров.Этап = &Этап
		|	И (ТИПЗНАЧЕНИЯ(ДоступностьВидовРабочихЦентров.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|	ИЛИ ДоступностьВидовРабочихЦентров.КлючПартия = &КлючПартия)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДоступностьВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ДоступностьВидовРабочихЦентров.ДатаИнтервала,
		|	ВидыРЦ.ПараллельнаяЗагрузка,
		|	ВидыРЦ.МаксимальнаяЗагрузка";
		
		Запрос.Выполнить();
		
		ЭтапыИсключения.Добавить(ДанныеЭтапа.Этап);
		
	Иначе
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра,
		|	ПланированиеЗагрузкиВидовРабочихЦентров.ДатаИнтервала,
		|	ПланированиеЗагрузкиВидовРабочихЦентров.Этап,
		|	ПланированиеЗагрузкиВидовРабочихЦентров.Занято КАК Занято
		|ПОМЕСТИТЬ ВТРезультатыПланирования
		|ИЗ
		|	РегистрСведений.ПланированиеЗагрузкиВидовРабочихЦентров КАК ПланированиеЗагрузкиВидовРабочихЦентров
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРЦ
		|	ПО ПланированиеЗагрузкиВидовРабочихЦентров.ВидРабочегоЦентра = ВидыРЦ.Ссылка
		|
		|ГДЕ
		|	ПланированиеЗагрузкиВидовРабочихЦентров.СтатусГрафика = &СтатусГрафика
		|	И ПланированиеЗагрузкиВидовРабочихЦентров.Распоряжение = &Распоряжение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТРезультатыПланирования.ВидРабочегоЦентра,
		|	ВТРезультатыПланирования.ДатаИнтервала,
		|	СУММА(ВЫБОР
		|			КОГДА ВТРезультатыПланирования.Этап = &Этап
		|				ТОГДА ВТРезультатыПланирования.Занято
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ЗанятоТекущим,
		|	СУММА(ВЫБОР
		|			КОГДА НЕ ВТРезультатыПланирования.Этап = &Этап
		|				ТОГДА ВТРезультатыПланирования.Занято
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ЗанятоПрочими
		|ПОМЕСТИТЬ ВТЗагрузкаРаспоряжения
		|ИЗ
		|	ВТРезультатыПланирования КАК ВТРезультатыПланирования
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТРезультатыПланирования.ВидРабочегоЦентра,
		|	ВТРезультатыПланирования.ДатаИнтервала
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВТРезультатыПланирования.Этап
		|ИЗ
		|	ВТРезультатыПланирования КАК ВТРезультатыПланирования";
		
		ЭтапыИсключения = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Этап");
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиИсполнителя(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления, РасшифровкиАльтернатив)
	
	ВывестиЗаголовокИсполнитель(ТабличныйДокумент, Исполнитель, ДанныеЭтапа, МакетОформления, РасшифровкиАльтернатив);
	
	Если Исполнитель.УчитыватьДоступность Тогда
		ВывестиДоступностьИсполнительСОграничениями(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления);
	Иначе
		ВывестиДоступностьИсполнительБезОграничений(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления);
	КонецЕсли;
	
	АльтернативныеИсполнители = АльтернативныеИсполнители(Исполнитель, ДанныеЭтапа.Исполнители);
	Если АльтернативныеИсполнители <> Неопределено Тогда
		Для каждого АльтернативныйИсполнитель Из АльтернативныеИсполнители Цикл
			ВывестиИсполнителя(
				ТабличныйДокумент, ДанныеЭтапа, АльтернативныйИсполнитель, МакетОформления, РасшифровкиАльтернатив);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВывестиЗаголовокИсполнитель(ТабличныйДокумент, Исполнитель, ДанныеЭтапа, МакетОформления, РасшифровкиАльтернатив)
	
	АльтернативныеИсполнители = АльтернативныеИсполнители(Исполнитель, ДанныеЭтапа.Исполнители);
	
	ВыводитьАльтернативныхИсполнителей = ТипЗнч(ДанныеЭтапа.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2");
	
	Если Исполнитель.Альтернативный И НЕ ВыводитьАльтернативныхИсполнителей Тогда
		Возврат;
	КонецЕсли;
	
	ВыводитьИспользование = ВыводитьАльтернативныхИсполнителей
		И (Исполнитель.Альтернативный
		ИЛИ (НЕ АльтернативныеИсполнители = Неопределено
		И НЕ АльтернативныеИсполнители.Количество() = 0));
	
	Если ВыводитьИспользование Тогда
		
		Если НЕ Исполнитель.Альтернативный Тогда
			
			ОбластьИсполнитель = МакетОформления.ПолучитьОбласть("Исполнитель_Исполнитель");
			ОбластьИспользование = МакетОформления.ПолучитьОбласть("Исполнитель_Использование");
			ОбластьТребуется = МакетОформления.ПолучитьОбласть("Исполнитель_Требуется");
			
		Иначе
			
			ОбластьИсполнитель = МакетОформления.ПолучитьОбласть("ИсполнительАльтернативный_Исполнитель");
			ОбластьИспользование = МакетОформления.ПолучитьОбласть("ИсполнительАльтернативный_Использование");
			ОбластьТребуется = МакетОформления.ПолучитьОбласть("ИсполнительАльтернативный_Требуется");
		
		КонецЕсли;
		
	Иначе
		
		ОбластьИсполнитель = МакетОформления.ПолучитьОбласть("Исполнитель");
		ОбластьТребуется = ОбластьИсполнитель;
		
	КонецЕсли;
	
	Требуется = 0;
	ВидРабочегоЦентра = "";
	Если Исполнитель.ПараллельнаяЗагрузка Тогда
		Требуется = СтрШаблон("%1 %2", Исполнитель.Загрузка, Исполнитель.ЕдиницаИзмеренияЗагрузки);
		ВидРабочегоЦентра = СтрШаблон("%1, %%", Исполнитель.ВидРабочегоЦентра);
	Иначе
		ВидРабочегоЦентра = Исполнитель.ВидРабочегоЦентра;
		ВремяВСекундах = ПолучитьВремяВСекундах(Исполнитель.ВремяРаботы, Исполнитель.ЕдиницаИзмерения);
		Требуется = ПолучитьВремяСтрокой(ВремяВСекундах);
	КонецЕсли;
	
	ОбластьИсполнитель.Параметры.Исполнитель = ВидРабочегоЦентра;
	ОбластьИсполнитель.Области.Исполнитель.Расшифровка = Исполнитель.ВидРабочегоЦентра;
	
	ОбластьТребуется.Параметры.Требуется = Требуется;
	
	Если ВыводитьИспользование Тогда
		
		ТабличныйДокумент.Вывести(ОбластьИсполнитель);
		
		Если НЕ Исполнитель.Альтернативный Тогда
			ОбластьИспользование.Области.Исполнитель_Использование.Текст = НСтр("ru = 'показать альтернативы';
																				|en = 'show alternatives'");
			ОбластьИспользование.Области.Исполнитель_Использование.Гиперссылка = Истина;
			ОбластьИспользование.Области.Исполнитель_Использование.Шрифт = Новый Шрифт(,,,, Истина);
		Иначе
			ОбластьИспользование.Области.ИсполнительАльтернативный_Использование.Текст = НСтр("ru = 'использовать';
																								|en = 'use'");
			ОбластьИспользование.Области.ИсполнительАльтернативный_Использование.Гиперссылка = Истина;
			ОбластьИспользование.Области.ИсполнительАльтернативный_Использование.Шрифт = Новый Шрифт(,,,, Истина);
		КонецЕсли;
		
		ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьИспользование);
		
		Если НЕ Исполнитель.Альтернативный Тогда
			РасшифровкаИсполнитель = Новый Структура;
			РасшифровкаИсполнитель.Вставить("ВидОбласти", "УправлениеВидимостьюАльтернативныхВидовРЦ");
			РасшифровкаИсполнитель.Вставить("ВидРабочегоЦентра", Исполнитель.ВидРабочегоЦентра);
			РасшифровкаИсполнитель.Вставить("КлючСвязи", Исполнитель.КлючСвязи);
			РасшифровкаИсполнитель.Вставить("КоличествоАльтернатив", АльтернативныеИсполнители.Количество());
			РасшифровкаИсполнитель.Вставить("ВысотаТаблицы", ТабличныйДокумент.ВысотаТаблицы);
			РасшифровкаИсполнитель.Вставить("Лево", ДобавленнаяОбласть.Лево);
			РасшифровкаИсполнитель.Вставить("Верх", ДобавленнаяОбласть.Верх);
			ДобавленнаяОбласть.Расшифровка = РасшифровкаИсполнитель;
			
			РасшифровкиАльтернатив.Добавить(РасшифровкаИсполнитель);
		Иначе
			РасшифровкаАльтернативныйИсполнитель = Новый Структура;
			РасшифровкаАльтернативныйИсполнитель.Вставить("ВидОбласти", "ИспользоватьВидРЦ");
			РасшифровкаАльтернативныйИсполнитель.Вставить("ВидРабочегоЦентра", Исполнитель.ВидРабочегоЦентра);
			РасшифровкаАльтернативныйИсполнитель.Вставить("КлючСвязи", ?(ЗначениеЗаполнено(Исполнитель.МассивКлючей),
																							Исполнитель.МассивКлючей,
																							Исполнитель.КлючСвязи));
			ДобавленнаяОбласть.Расшифровка = РасшифровкаАльтернативныйИсполнитель;
		КонецЕсли;
		
		ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьТребуется);
		
	Иначе
		
		ТабличныйДокумент.Вывести(ОбластьИсполнитель);
		
	КонецЕсли;
	
КонецПроцедуры

Функция АльтернативныеИсполнители(Исполнитель, Исполнители)
	
	Результат = Исполнители.СкопироватьКолонки();
	
	Для Каждого Ключ Из Исполнитель.МассивКлючей Цикл
		Если НЕ Исполнитель.Альтернативный Тогда
			СтруктураПоиска = Новый Структура("КлючСвязи, Альтернативный", Ключ, Истина);
			РезультатПоиска = Исполнители.НайтиСтроки(СтруктураПоиска);
			Для Каждого ВРЦ Из РезультатПоиска Цикл
				Строка = Результат.Добавить();
				ЗаполнитьЗначенияСвойств(Строка, ВРЦ);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СвернутьИсполнителей(Результат, Истина);
	
КонецФункции

Процедура ВывестиДоступностьИсполнительСОграничениями(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления)
	
	ОбластьИсполнитель = МакетОформления.ПолучитьОбласть("ИсполнительДанные");
	Оформления = ОформленияИсполнители(МакетОформления);
	
	СтруктураПоиска = Новый Структура("ВидРабочегоЦентра, ДатаИнтервала", Исполнитель.ВидРабочегоЦентра);
	ПустыеЗначения = Новый Структура("ОбщаяДоступность, ДоступноДляТекущего, ЗанятоТекущим", "", "", "");
	
	Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
		Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
		ДатаИнтервала = Интервал.Начало;
		
		СтруктураПоиска.ДатаИнтервала = ДатаИнтервала;
		НайденныеСтроки = ДанныеЭтапа.ДоступностьВРЦ.НайтиСтроки(СтруктураПоиска);
		
		ИнтервалЗанят = Ложь;
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			ЗаполнитьЗначенияСвойств(ОбластьИсполнитель.Параметры, ПустыеЗначения);
			Оформление = Оформления.ИнтервалНедоступен;
			
		Иначе
			
			ЗначенияЗаполнения = НайденныеСтроки[0];
			
			Если Исполнитель.ПараллельнаяЗагрузка И ЗначенияЗаполнения.ОбщаяДоступность > 0 Тогда
				
				Если ЗначенияЗаполнения.РезервОбщий > 0 Тогда
					
					ДоступноБезРезерва = (ЗначенияЗаполнения.ОбщаяДоступность - ЗначенияЗаполнения.РезервОбщий)
										/ ЗначенияЗаполнения.ОбщаяДоступность * 100;
					Резерв = ЗначенияЗаполнения.РезервОбщий / ЗначенияЗаполнения.ОбщаяДоступность * 100;
					ОбщаяДоступность = Формат(ДоступноБезРезерва, "ЧДЦ=2;")
						+ " (+"
						+ Формат(Резерв, "ЧДЦ=2;")
						+ ")";
					
				Иначе
					ОбщаяДоступность = 100;
				КонецЕсли;
				
				Если ЗначенияЗаполнения.РезервДоступноДляТекущего > 0 Тогда
					
					ДоступноДляТекущегоБезРезерва = (ЗначенияЗаполнения.ДоступноДляТекущегоСРезервом
						- ЗначенияЗаполнения.РезервДоступноДляТекущего) / ЗначенияЗаполнения.ОбщаяДоступность * 100;
					РезервДляТекущего = ЗначенияЗаполнения.РезервДоступноДляТекущего
										/ ЗначенияЗаполнения.ОбщаяДоступность * 100;
					ДоступноДляТекущего = Формат(ДоступноДляТекущегоБезРезерва, "ЧДЦ=2;")
						+ " (+"
						+ Формат(РезервДляТекущего, "ЧДЦ=2;")
						+ ")";
						
				Иначе
					ДоступноДляТекущего = Формат(ЗначенияЗаполнения.ДоступноДляТекущегоСРезервом
												/ ЗначенияЗаполнения.ОбщаяДоступность * 100, "ЧДЦ=2;");
				КонецЕсли;
				
				ЗанятоТекущим = Формат(ЗначенияЗаполнения.ЗанятоТекущим
										/ ЗначенияЗаполнения.ОбщаяДоступность * 100, "ЧДЦ=2;");
			Иначе
				
				Если ЗначенияЗаполнения.РезервОбщий > 0 Тогда
					
					ДоступноБезРезерва = ЗначенияЗаполнения.ОбщаяДоступность - ЗначенияЗаполнения.РезервОбщий;
					ОбщаяДоступность = ПолучитьВремяСтрокой(ДоступноБезРезерва)
						+ " (+"
						+ ПолучитьВремяСтрокой(ЗначенияЗаполнения.РезервОбщий)
						+ ")";
					
				Иначе
					ОбщаяДоступность = ПолучитьВремяСтрокой(ЗначенияЗаполнения.ОбщаяДоступность);
				КонецЕсли;
				
				Если ЗначенияЗаполнения.РезервДоступноДляТекущего > 0 Тогда
					
					ДоступноДляТекущегоБезРезерва = ЗначенияЗаполнения.ДоступноДляТекущегоСРезервом
						- ЗначенияЗаполнения.РезервДоступноДляТекущего;
					ДоступноДляТекущего = ПолучитьВремяСтрокой(ДоступноДляТекущегоБезРезерва)
						+ " (+"
						+ ПолучитьВремяСтрокой(ЗначенияЗаполнения.РезервДоступноДляТекущего)
						+ ")";
						
				Иначе
					ДоступноДляТекущего = ПолучитьВремяСтрокой(ЗначенияЗаполнения.ДоступноДляТекущегоСРезервом);
				КонецЕсли;
				
				ЗанятоТекущим = ПолучитьВремяСтрокой(ЗначенияЗаполнения.ЗанятоТекущим);
				
			КонецЕсли;
			
			ОбластьИсполнитель.Параметры.ОбщаяДоступность = ОбщаяДоступность;
			ОбластьИсполнитель.Параметры.ДоступноДляТекущего = ДоступноДляТекущего;
			ОбластьИсполнитель.Параметры.ЗанятоТекущим = ЗанятоТекущим;
			
			Если ЗначенияЗаполнения.СвободноСРезервом < 0 Тогда
				Оформление = Оформления.ИнтервалПерегружен;
			ИначеЕсли ЗначенияЗаполнения.ЗанятоТекущим > 0 Тогда
				Оформление = Оформления.ИнтервалЗанятДиагностируемымЭтапом;
			ИначеЕсли ЗначенияЗаполнения.ЗанятоПрочими > 0 И ЗначенияЗаполнения.СвободноСРезервом = 0 Тогда
				Оформление = Оформления.ИнтервалЗанят;
			ИначеЕсли ЗначенияЗаполнения.ЗанятоПрочими > 0 Тогда
				Оформление = Оформления.ИнтервалЧастичноДоступен;
			ИначеЕсли ЗначенияЗаполнения.ОбщаяДоступность = 0 Тогда
				Оформление = Оформления.ИнтервалНедоступен;
			Иначе
				Оформление = Оформления.ИнтервалДоступен;
			КонецЕсли;
			
			Если ЗначенияЗаполнения.ЗанятоТекущим > 0 Тогда
				ИнтервалЗанят = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьОформлениеОбласти(ОбластьИсполнитель.Области.ИсполнительДанные, Оформление);
		
		ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьИсполнитель);
		
		Если ИнтервалЗанят Тогда
			ЗаполнитьГраницыЭтапа(ДанныеЭтапа, ДобавленнаяОбласть);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ВывестиДоступностьИсполнительБезОграничений(ТабличныйДокумент, ДанныеЭтапа, Исполнитель, МакетОформления)
	
	ОбластьИсполнительНетДоступности = МакетОформления.ПолучитьОбласть("ИсполнительДанные");
	ОбластьИсполнительЕстьДоступность = МакетОформления.ПолучитьОбласть("ИсполнительДанныеБезОграниченияДоступности");
	Оформления = ОформленияИсполнители(МакетОформления);
	
	Доступность = ДанныеЭтапа.РасписаниеББВ;
	СтруктураПоискаДоступность = Новый Структура;
	СтруктураПоискаДоступность.Вставить("ГрафикРаботы", Исполнитель.ГрафикРаботы);
	
	Загрузка = ДанныеЭтапа.ЗагрузкаВРЦБезОграничений;
	СтруктураПоискаЗагрузка = Новый Структура("ВидРабочегоЦентра, ДатаИнтервала");
	ЗаполнитьЗначенияСвойств(СтруктураПоискаЗагрузка, Исполнитель);
	
	ПустыеЗначения = Новый Структура("ЗанятоТекущим", "");
	
	Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
		Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
		ДатаИнтервала = Интервал.Начало;
		
		СтруктураПоискаДоступность.Вставить("ДатаИнтервала", ДатаИнтервала);
		НайденныеСтроки = Доступность.НайтиСтроки(СтруктураПоискаДоступность);
		
		ИнтервалЗанят = Ложь;
		
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			ОбластьИсполнитель = ОбластьИсполнительНетДоступности;
			УстановитьОформлениеОбласти(
				ОбластьИсполнитель.Области.ИсполнительДанные,
				Оформления.ИнтервалНедоступен);
			
		Иначе
			
			ОбластьИсполнитель = ОбластьИсполнительЕстьДоступность;
			
			СтруктураПоискаЗагрузка.ДатаИнтервала = ДатаИнтервала;
			НайденныеСтрокиЗагрузка = Загрузка.НайтиСтроки(СтруктураПоискаЗагрузка);
			
			Если НайденныеСтрокиЗагрузка.Количество() = 0 Тогда
				
				ЗаполнитьЗначенияСвойств(ОбластьИсполнитель.Параметры, ПустыеЗначения);
				Оформление = Оформления.ИнтервалДоступен;
				
			Иначе
				
				ЗначенияЗаполнения = НайденныеСтрокиЗагрузка[0];
				
				Если Исполнитель.ПараллельнаяЗагрузка Тогда
					ЗанятоТекущим = Формат(ЗначенияЗаполнения.ЗанятоТекущим
											/ ЗначенияЗаполнения.ОбщаяДоступность * 100, "ЧДЦ=2;");
				Иначе
					ЗанятоТекущим = ПолучитьВремяСтрокой(ЗначенияЗаполнения.ЗанятоТекущим);
				КонецЕсли;
				
				ОбластьИсполнитель.Параметры.ЗанятоТекущим = ЗанятоТекущим;
				
				Если ЗначенияЗаполнения.ЗанятоТекущим > 0 Тогда
					Оформление = Оформления.ИнтервалЗанятДиагностируемымЭтапом;
					ИнтервалЗанят = Истина;
				Иначе
					Оформление = Оформления.ИнтервалДоступен;
				КонецЕсли;
				
			КонецЕсли;
			
			УстановитьОформлениеОбласти(
				ОбластьИсполнитель.Области.ИсполнительДанныеБезОграниченияДоступности,
				Оформление);
			
		КонецЕсли;
		
		ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьИсполнитель);
		
		Если ИнтервалЗанят Тогда
			ЗаполнитьГраницыЭтапа(ДанныеЭтапа, ДобавленнаяОбласть);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ОформленияИсполнители(МакетОформления)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИнтервалЗанятДиагностируемымЭтапом",
		МакетОформления.ПолучитьОбласть("ИнтервалЗанятДиагностируемымЭтапом").Области.ИнтервалЗанятДиагностируемымЭтапом);
		
	Результат.Вставить("ИнтервалЗанят",
		МакетОформления.ПолучитьОбласть("ИнтервалЗанят").Области.ИнтервалЗанят);
	
	Результат.Вставить("ИнтервалНедоступен",
		МакетОформления.ПолучитьОбласть("ИнтервалНедоступен").Области.ИнтервалНедоступен);
	
	Результат.Вставить("ИнтервалДоступен",
		МакетОформления.ПолучитьОбласть("ИнтервалДоступен").Области.ИнтервалДоступен);
	
	Результат.Вставить("ИнтервалЧастичноДоступен",
		МакетОформления.ПолучитьОбласть("ИнтервалЧастичноДоступен").Области.ИнтервалЧастичноДоступен);
	
	Результат.Вставить("ИнтервалПерегружен",
		МакетОформления.ПолучитьОбласть("ИнтервалПерегружен").Области.ИнтервалПерегружен);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьГраницыЭтапа(ДанныеЭтапа, ДобавленнаяОбласть)
	
	Если ЗначениеЗаполнено(ДанныеЭтапа.ЛеваяГраницаЭтапа) Тогда
		
		ДанныеЭтапа.ЛеваяГраницаЭтапа = Мин(
			ДанныеЭтапа.ЛеваяГраницаЭтапа,
			ДобавленнаяОбласть.Лево);
			
	Иначе
		
		ДанныеЭтапа.ЛеваяГраницаЭтапа = ДобавленнаяОбласть.Лево;
		
	КонецЕсли;
	
	ДанныеЭтапа.ПраваяГраницаЭтапа = Макс(
		ДанныеЭтапа.ПраваяГраницаЭтапа,
		ДобавленнаяОбласть.Право);
	
КонецПроцедуры

#КонецОбласти

#Область Материалы

Процедура ВывестиМатериалы(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	Материалы = МатериалыЭтапа(ДанныеЭтапа);
	Если Материалы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьЗаголовок = МакетОформления.ПолучитьОбласть("МатериалыЗаголовок");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	Оформления = ОформленияИсполнители(МакетОформления);
	
	ОбластьМатериал       = МакетОформления.ПолучитьОбласть("Материал");
	ОбластьМатериалДанные = МакетОформления.ПолучитьОбласть("МатериалДанные");
	
	Для каждого Строка Из Материалы Цикл
		
		ОбластьМатериал.Параметры.Материал = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
			Строка.НоменклатураПредставление,
			Строка.ХарактеристикаПредставление,
			Строка.ЕдиницаИзмеренияПредставление);
		ОбластьМатериал.Параметры.РасшифровкаМатериал = Строка.Номенклатура;
		ОбластьМатериал.Параметры.Требуется = Строка.Количество;
		
		ТабличныйДокумент.Вывести(ОбластьМатериал);
		
		Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
			Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
			Начало = Интервал.Начало;
			
			Если Начало < Строка.ДатаДоступности Тогда
				УстановитьОформлениеОбласти(ОбластьМатериалДанные.Области.МатериалДанные, Оформления.ИнтервалЗанят);
			Иначе
				УстановитьОформлениеОбласти(ОбластьМатериалДанные.Области.МатериалДанные, Оформления.ИнтервалДоступен);
			КонецЕсли;
			
			ДобавленнаяОбласть = ТабличныйДокумент.Присоединить(ОбластьМатериалДанные);
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция МатериалыЭтапа(ДанныеЭтапа)

	УстановитьПривилегированныйРежим(Истина);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ДанныеЭтапа.ДинамическаяСтруктура Тогда
		
		УсловияПолучения = Новый Структура;
		УсловияПолучения.Вставить("ЗаказНаПроизводство", ДанныеЭтапа.Распоряжение);
		УсловияПолучения.Вставить("Этап", ДанныеЭтапа.Этап);
		УсловияПолучения.Вставить("КлючПартия", ДанныеЭтапа.КлючПартия);
		УправлениеПроизводством.СоздатьВТОграниченияГрафикаПроизводстваПоМатериаламДинамическаяСтруктура(
			МенеджерВременныхТаблиц, УсловияПолучения, Истина, Ложь, ДанныеЭтапа.СтатусГрафика);
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Материалы.Номенклатура                                КАК Номенклатура,
			|	Материалы.Номенклатура.Представление                  КАК НоменклатураПредставление,
			|	Материалы.Характеристика                              КАК Характеристика,
			|	Материалы.Характеристика.Представление                КАК ХарактеристикаПредставление,
			|	ВЫБОР &ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ЧАС)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ЧАС)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ДЕНЬ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, МЕСЯЦ)
			|	КОНЕЦ                                                 КАК ДатаДоступности,
			|	ДАТАВРЕМЯ(1,1,1)                                      КАК ДатаНачалаПеремещения,
			|	Материалы.Номенклатура.ЕдиницаИзмерения               КАК ЕдиницаИзмерения,
			|	Материалы.Номенклатура.ЕдиницаИзмерения.Представление КАК ЕдиницаИзмеренияПредставление,
			|	СУММА(Материалы.Требуется)                            КАК Количество
			|ИЗ
			|	ВТОграниченияГрафикаПроизводстваПоМатериалам КАК ДатыОбеспечения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтруктураЗаказа КАК Материалы
			|		ПО ДатыОбеспечения.КлючПартия = Материалы.КлючПартия
			|			И ДатыОбеспечения.Этап = Материалы.Этап
			|			И ДатыОбеспечения.Номенклатура = Материалы.Номенклатура
			|			И ДатыОбеспечения.Характеристика = Материалы.Характеристика
			|ГДЕ
			|	ДатыОбеспечения.ДатаДоступности > &ПолныйИнтервалНачало
			|
			|СГРУППИРОВАТЬ ПО
			|	Материалы.Номенклатура,
			|	Материалы.Характеристика,
			|	Материалы.Номенклатура.Представление,
			|	Материалы.Характеристика.Представление,
			|	ДатыОбеспечения.ДатаДоступности
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДатаДоступности УБЫВ,
			|	Номенклатура");
		Запрос.УстановитьПараметр("ПолныйИнтервалНачало", ДанныеЭтапа.ПолныйИнтервалНачало);
		Запрос.УстановитьПараметр("ИнтервалПланирования", ДанныеЭтапа.ИнтервалПланирования);
		
	Иначе
		
		УправлениеПроизводством.СоздатьВТОграниченияГрафикаПроизводстваПоМатериалам(
			МенеджерВременныхТаблиц, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеЭтапа.Этап), Истина, Ложь);
			
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	Материалы.Номенклатура                 КАК Номенклатура,
			|	Материалы.Номенклатура.Представление   КАК НоменклатураПредставление,
			|	Материалы.Характеристика               КАК Характеристика,
			|	Материалы.Характеристика.Представление КАК ХарактеристикаПредставление,
			|	ВЫБОР &ИнтервалПланирования
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Час)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ЧАС)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Смена)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ЧАС)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, ДЕНЬ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Неделя)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, НЕДЕЛЯ)
			|		КОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.Месяц)
			|			ТОГДА НАЧАЛОПЕРИОДА(ДатыОбеспечения.ДатаДоступности, МЕСЯЦ)
			|	КОНЕЦ                                  КАК ДатаДоступности,
			|	ДАТАВРЕМЯ(1,1,1)                       КАК ДатаНачалаПеремещения,
			|	&ТекстЗапросаЕдиницаИзмерения          КАК ЕдиницаИзмерения,
			|	&ТекстЗапросаПредставление             КАК ЕдиницаИзмеренияПредставление,
			|	СУММА(ВЫБОР
			|			КОГДА &ВыводитьБазовыеЕдиницыИзмерения
			|				ТОГДА Материалы.Количество
			|			ИНАЧЕ Материалы.КоличествоУпаковок
			|		КОНЕЦ)                                 КАК Количество
			|ИЗ
			|	ВТОграниченияГрафикаПроизводстваПоМатериалам КАК ДатыОбеспечения
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Материалы
			|		ПО ДатыОбеспечения.ЭтапПроизводства = Материалы.Ссылка
			|			И ДатыОбеспечения.Номенклатура = Материалы.Номенклатура
			|			И ДатыОбеспечения.Характеристика = Материалы.Характеристика
			|			И НЕ Материалы.Отменено
			|
			|СГРУППИРОВАТЬ ПО
			|	Материалы.Номенклатура,
			|	Материалы.Характеристика,
			|	Материалы.Номенклатура.Представление,
			|	Материалы.Характеристика.Представление,
			|	ДатыОбеспечения.ДатаДоступности,
			|	&ТекстЗапросаЕдиницаИзмерения,
			|	&ТекстЗапросаПредставление
			|
			|УПОРЯДОЧИТЬ ПО
			|	ДатаДоступности УБЫВ,
			|	Номенклатура");
		
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ТекстЗапросаЕдиницаИзмерения",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
				"Ссылка",
				"Материалы.Упаковка",
				"Материалы.Номенклатура"));
			
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ТекстЗапросаПредставление",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаЗначениеРеквизитаЕдиницыИзмерения(
				"Наименование",
				"Материалы.Упаковка",
				"Материалы.Номенклатура"));
			
		Запрос.УстановитьПараметр("ВыводитьБазовыеЕдиницыИзмерения", Константы.ВыводитьБазовыеЕдиницыИзмерения.Получить());
		Запрос.УстановитьПараметр("ИнтервалПланирования", ДанныеЭтапа.ИнтервалПланирования);
	
	КонецЕсли;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат ?(РезультатЗапроса.Пустой(), Неопределено, РезультатЗапроса.Выгрузить());
	
КонецФункции

#КонецОбласти

#Область ПрочиеОграничения

Процедура ВывестиЛевуюГраницу(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	Если Не ЗначениеЗаполнено(ДанныеЭтапа.ПланироватьНеРанее) Тогда
		Возврат;
	КонецЕсли;
	
	ОбластьЗаголовок = МакетОформления.ПолучитьОбласть("ПланироватьНеРанее");
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	ФорматДатыГрафика = УправлениеПроизводством.ФорматнаяСтрокаДляДатыГрафикаПроизводства();
	ТекстПланироватьНеРанее = Формат(ДанныеЭтапа.ПланироватьНеРанее, ФорматДатыГрафика);
	
	ОбластьЭтап = МакетОформления.ПолучитьОбласть("Этап");
	ОбластьЭтап.Параметры.ЭтапПроизводства   = ДанныеЭтапа.Этап;
	ОбластьЭтап.Параметры.ПредставлениеЭтапа = ТекстПланироватьНеРанее;
	ТабличныйДокумент.Вывести(ОбластьЭтап);
	
	Оформления = ОформленияИсполнители(МакетОформления);
	ОбластьЭтапДанные = МакетОформления.ПолучитьОбласть("ЭтапДанные");
	
	Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
		Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
		Начало = Интервал.Начало;
		
		Если Начало < ДанныеЭтапа.ПланироватьНеРанее Тогда
			УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалЗанят);
		Иначе
			УстановитьОформлениеОбласти(ОбластьЭтапДанные.Области.ЭтапДанные, Оформления.ИнтервалДоступен);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ДанныеЭтапа(Этап, СтатусГрафика, КлючПартия)
	
	// Реквизиты этапа
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ГрафикПроизводства.ЗаказНаПроизводство КАК Распоряжение,
		|	ГрафикПроизводства.Этап КАК Этап,
		|	ГрафикПроизводства.Спецификация КАК Спецификация,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ГрафикПроизводства.Этап.ДинамическаяСтруктура
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ДинамическаяСтруктура,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА &ПредставлениеЭтапа
		|		ИНАЧЕ ГрафикПроизводства.Этап.Наименование
		|	КОНЕЦ КАК ПредставлениеЭтапа,
		|	ГрафикПроизводства.Этап.Подразделение КАК Подразделение,
		|	ВЫБОР
		|		КОГДА НЕ ГрафикПроизводства.Этап.ПланироватьРаботуВидовРабочихЦентров
		|			ИЛИ ГрафикПроизводства.Этап.ПроизводствоНаСтороне
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТочностьГрафикаПроизводства.День)
		|		ИНАЧЕ ГрафикПроизводства.Этап.Подразделение.ИнтервалПланирования
		|	КОНЕЦ КАК ИнтервалПланирования,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|				И ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап.ПланироватьНеРанее) = ТИП(ДАТА)
		|			ТОГДА ГрафикПроизводства.Этап.ПланироватьНеРанее
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ПланироватьНеРанее,
		|	ГрафикПроизводства.Начало КАК НачалоЭтапа,
		|	ГрафикПроизводства.Окончание КАК ОкончаниеЭтапа,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ГрафикПроизводства.Этап.НаименованиеЭтапа
		|		ИНАЧЕ ГрафикПроизводства.Этап.Наименование
		|	КОНЕЦ КАК НаименованиеЭтапа,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|			ТОГДА ГрафикПроизводства.Этап.Номер
		|		ИНАЧЕ ГрафикПроизводства.Этап.НомерЭтапа
		|	КОНЕЦ КАК НомерЭтапа,
		|	ГрафикПроизводства.Этап.ПланироватьРаботуВидовРабочихЦентров КАК ПланироватьРаботуВидовРабочихЦентров,
		|	ГрафикПроизводства.Этап.ПроизводствоНаСтороне КАК ПроизводствоНаСтороне,
		|	ГрафикПроизводства.ЗаказНаПроизводство.Очередь КАК Очередь,
		|	ГрафикПроизводства.ЗаказНаПроизводство.Приоритет.РеквизитДопУпорядочивания КАК ПриоритетНомер,
		|	ГрафикПроизводства.ЗаказНаПроизводство.Подразделение.РеквизитДопУпорядочивания КАК ПодразделениеНомер,
		|	ГрафикПроизводства.Этап.ДлительностьЭтапа КАК ДлительностьЭтапа,
		|	ВЫБОР
		|		КОГДА ГрафикПроизводства.Этап.ЕдиницаИзмеренияДлительностиЭтапа = ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.ПустаяСсылка)
		|			ТОГДА &ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ
		|		ИНАЧЕ ГрафикПроизводства.Этап.ЕдиницаИзмеренияДлительностиЭтапа
		|	КОНЕЦ КАК ЕдиницаИзмеренияДлительностиЭтапа,
		|	ВЫБОР
		|		КОГДА НЕ ГрафикПроизводства.РазмещениеВыпуска = ЗНАЧЕНИЕ(Перечисление.СпособыПривязкиОперацийПроизводства.КОкончанию)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК РазмещениеКНачалу,
		|	ГрафикПроизводства.КоличествоПартий КАК КоличествоПартий,
		|	&СтатусГрафика КАК СтатусГрафика,
		|	ГрафикПроизводства.КлючПартия КАК КлючПартия
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикПроизводства
		|ГДЕ
		|	ГрафикПроизводства.Этап = &Этап
		|	И ГрафикПроизводства.СтатусГрафика = &СтатусГрафика
		|	И (ТИПЗНАЧЕНИЯ(ГрафикПроизводства.Этап) = ТИП(Документ.ЭтапПроизводства2_2)
		|	ИЛИ ГрафикПроизводства.КлючПартия = &КлючПартия)";
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа("ГрафикПроизводства.Этап"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	Запрос.УстановитьПараметр("КлючПартия", КлючПартия);
	Запрос.УстановитьПараметр("ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ",
		Справочники.ЭтапыПроизводства.ОсновнаяЕдиницаВремениДлительностиЭтапаУББВ());
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если РезультатЗапроса.Количество() <> 1 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗначенияЗаполнения = РезультатЗапроса[0];
	
	ДанныеЭтапа = Новый Структура;
	Для каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ДанныеЭтапа.Вставить(Колонка.Имя, ЗначенияЗаполнения[Колонка.Имя]);
	КонецЦикла;
	
	ДанныеЭтапа.Вставить("ЛеваяГраницаЭтапа", 0);
	ДанныеЭтапа.Вставить("ПраваяГраницаЭтапа", 0);
	
	// Полный интервал
	ПолныйИнтервалНачало = '00010101';
	ПолныйИнтервалОкончание = '00010101';
	
	Если ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
		ПолныйИнтервалНачало = НачалоДня(ДанныеЭтапа.НачалоЭтапа) - 86400;
		ПолныйИнтервалОкончание = КонецДня(ДанныеЭтапа.ОкончаниеЭтапа) + 86400;
	ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
		ПолныйИнтервалНачало = НачалоДня(ДанныеЭтапа.НачалоЭтапа) - (86400 * 4);
		ПолныйИнтервалОкончание = КонецДня(ДанныеЭтапа.ОкончаниеЭтапа) + (86400 * 4);
	ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
		ПолныйИнтервалНачало = НачалоНедели(ДанныеЭтапа.НачалоЭтапа) - (86400 * 7);
		ПолныйИнтервалОкончание = КонецНедели(ДанныеЭтапа.ОкончаниеЭтапа) + (86400 * 7);
	ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
		ПолныйИнтервалНачало = НачалоНедели(ДанныеЭтапа.НачалоЭтапа) - (86400 * 7);
		ПолныйИнтервалОкончание = КонецНедели(ДанныеЭтапа.ОкончаниеЭтапа) + (86400 * 7);
	ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		ПолныйИнтервалНачало = НачалоМесяца(НачалоМесяца(ДанныеЭтапа.НачалоЭтапа) - 1);
		ПолныйИнтервалОкончание = КонецМесяца(КонецМесяца(ДанныеЭтапа.ОкончаниеЭтапа) + 1);
	КонецЕсли;
	
	ДанныеЭтапа.Вставить("ПолныйИнтервалНачало", ПолныйИнтервалНачало);
	ДанныеЭтапа.Вставить("ПолныйИнтервалОкончание", ПолныйИнтервалОкончание);
	
	ДанныеЭтапа.Вставить("Исполнители", Неопределено);
	ДанныеЭтапа.Вставить("РасписаниеББВ", Неопределено);
	ДанныеЭтапа.Вставить("ЗагрузкаВРЦБезОграничений", Неопределено);
	ДанныеЭтапа.Вставить("ДоступностьВРЦ", Неопределено);
	
	ГрафикиСмен = Новый Массив;
	
	Если НЕ ЭтоЭтапУББВ(ДанныеЭтапа) Тогда
		// Исполнители
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВидыРЦ.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	ВидыРЦ.ВремяРаботы КАК ВремяРаботы,
			|	ВидыРЦ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВидыРЦ.Загрузка КАК Загрузка,
			|	ВидыРЦ.ВидРабочегоЦентра.ЕдиницаИзмеренияЗагрузки КАК ЕдиницаИзмеренияЗагрузки,
			|	ВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
			|	ВидыРЦ.КлючСвязи КАК КлючСвязи,
			|	НЕ ВидыРЦ.Использовать КАК Альтернативный,
			|	ВидыРЦ.НомерСтроки КАК НомерСтроки,
			|	ВидыРЦ.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
			|	ВидыРЦ.Ссылка.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
			|	0 КАК ГрафикРаботы,
			|	ЛОЖЬ КАК ЕстьРасчетПоФормуле
			|ИЗ
			|	Документ.ЭтапПроизводства2_2.ВидыРабочихЦентров КАК ВидыРЦ
			|ГДЕ
			|	ВидыРЦ.Ссылка = &Этап
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	АльтернативныеВидыРЦ.ВремяРаботы КАК ВремяРаботы,
			|	АльтернативныеВидыРЦ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	АльтернативныеВидыРЦ.Загрузка КАК Загрузка,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.ЕдиницаИзмеренияЗагрузки КАК ЕдиницаИзмеренияЗагрузки,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
			|	АльтернативныеВидыРЦ.КлючСвязиВидыРабочихЦентров КАК КлючСвязи,
			|	НЕ АльтернативныеВидыРЦ.Использовать КАК Альтернативный,
			|	ЕСТЬNULL(ВидыРЦ.НомерСтроки, 0) КАК НомерСтроки,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
			|	АльтернативныеВидыРЦ.Ссылка.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
			|	0 КАК ГрафикРаботы,
			|	ЛОЖЬ КАК ЕстьРасчетПоФормуле
			|ИЗ
			|	Документ.ЭтапПроизводства2_2.АльтернативныеВидыРабочихЦентров КАК АльтернативныеВидыРЦ
			|	
			|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВидыРабочихЦентров КАК ВидыРЦ
			|	ПО АльтернативныеВидыРЦ.Ссылка = ВидыРЦ.Ссылка
			|		И АльтернативныеВидыРЦ.КлючСвязиВидыРабочихЦентров = ВидыРЦ.КлючСвязи
			|ГДЕ
			|	АльтернативныеВидыРЦ.Ссылка = &Этап
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ВидыРЦ.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
			|			
			|			0
			|		
			|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
			|				И Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
			|			
			|			ВЫБОР
			|				КОГДА ЦЕЛ(&ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
			|					= &ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
			|					ТОГДА &ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * ВидыРЦ.ВремяРаботы
			|				ИНАЧЕ (ЦЕЛ(&ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * ВидыРЦ.ВремяРаботы
			|			КОНЕЦ
			|			
			|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
			|				И НЕ Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
			|			
			|			ВЫБОР
			|				КОГДА Цел(ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
			|						= ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
			|					ТОГДА ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * ВидыРЦ.ВремяРаботы
			|				ИНАЧЕ (Цел(ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * ВидыРЦ.ВремяРаботы
			|			КОНЕЦ
			|			
			|		ИНАЧЕ &КоличествоПартий * ВидыРЦ.ВремяРаботы
			|	КОНЕЦ КАК ВремяРаботы,
			|	ВидыРЦ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА ВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
			|			&КоличествоПартий * ВидыРЦ.Загрузка
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Загрузка,
			|	ВидыРЦ.ВидРабочегоЦентра.ЕдиницаИзмеренияЗагрузки КАК ЕдиницаИзмеренияЗагрузки,
			|	ВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
			|	ВидыРЦ.КлючСвязи КАК КлючСвязи,
			|	ЛОЖЬ КАК Альтернативный,
			|	ВидыРЦ.НомерСтроки КАК НомерСтроки,
			|	ВидыРЦ.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
			|	ВидыРЦ.Ссылка.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
			|	0 КАК ГрафикРаботы,
			|	ДЛИНАСТРОКИ(СОКРЛ(ВидыРЦ.АлгоритмРасчетаКоличества)) <> 0 КАК ЕстьРасчетПоФормуле
			|ИЗ
			|	Справочник.ЭтапыПроизводства.ВидыРабочихЦентров КАК ВидыРЦ
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
			|	ПО Этапы.Ссылка = ВидыРЦ.Ссылка
			|		И Этапы.Ссылка = &Этап
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК Спецификации
			|	ПО Этапы.Владелец = Спецификации.Ссылка
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
			|	ВЫБОР
			|		КОГДА АльтернативныеВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
			|			
			|			0
			|		
			|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
			|				И Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
			|			
			|			ВЫБОР
			|				КОГДА ЦЕЛ(&ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
			|					= &ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
			|					ТОГДА &ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * АльтернативныеВидыРЦ.ВремяРаботы
			|				ИНАЧЕ (ЦЕЛ(&ТекстЗапросаКоличествоИзделий / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * АльтернативныеВидыРЦ.ВремяРаботы
			|			КОНЕЦ
			|		
			|		КОГДА Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий <> 0
			|				И НЕ Этапы.Владелец.ВыпускПроизвольнымиПорциями ТОГДА
			|			
			|			ВЫБОР
			|				КОГДА Цел(ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий)
			|						= ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий
			|					ТОГДА ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий * АльтернативныеВидыРЦ.ВремяРаботы
			|				ИНАЧЕ (Цел(ЕСТЬNULL(&КоличествоПартий, 0) / Этапы.ОдновременноПроизводимоеКоличествоЕдиницПартийИзделий) + 1) * АльтернативныеВидыРЦ.ВремяРаботы
			|			КОНЕЦ
			|			
			|		ИНАЧЕ &КоличествоПартий * АльтернативныеВидыРЦ.ВремяРаботы
			|	КОНЕЦ КАК ВремяРаботы,
			|	АльтернативныеВидыРЦ.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
			|	ВЫБОР
			|		КОГДА АльтернативныеВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка ТОГДА
			|			&КоличествоПартий * АльтернативныеВидыРЦ.Загрузка
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Загрузка,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.ЕдиницаИзмеренияЗагрузки КАК ЕдиницаИзмеренияЗагрузки,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.ПараллельнаяЗагрузка КАК ПараллельнаяЗагрузка,
			|	АльтернативныеВидыРЦ.КлючСвязиВидыРабочихЦентров КАК КлючСвязи,
			|	ИСТИНА КАК Альтернативный,
			|	АльтернативныеВидыРЦ.НомерСтроки КАК НомерСтроки,
			|	АльтернативныеВидыРЦ.ВидРабочегоЦентра.УчитыватьДоступностьПоГрафикуРаботы КАК УчитыватьДоступность,
			|	АльтернативныеВидыРЦ.Ссылка.Подразделение.ИнтервалПланирования КАК ИнтервалПланирования,
			|	0 КАК ГрафикРаботы,
			|	ДЛИНАСТРОКИ(СОКРЛ(АльтернативныеВидыРЦ.АлгоритмРасчетаКоличества)) <> 0 КАК ЕстьРасчетПоФормуле
			|ИЗ
			|	Справочник.ЭтапыПроизводства.АльтернативныеВидыРабочихЦентров КАК АльтернативныеВидыРЦ
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК Этапы
			|	ПО Этапы.Ссылка = АльтернативныеВидыРЦ.Ссылка
			|		И Этапы.Ссылка = &Этап
			|
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК Спецификации
			|	ПО Этапы.Владелец = Спецификации.Ссылка
			|ГДЕ
			|	АльтернативныеВидыРЦ.Ссылка = &Этап
			|
			|УПОРЯДОЧИТЬ ПО
			|	НомерСтроки";
		
		ТекстЗапросаКоличествоИзделий =
			"&КоличествоПартий * Спецификации.ОсновноеИзделиеКоличествоУпаковок * ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)";
		ТекстЗапросаКоличествоИзделий = СтрЗаменить(ТекстЗапросаКоличествоИзделий,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"Спецификации.ОсновноеИзделиеУпаковка",
				"Спецификации.ОсновноеИзделиеНоменклатура"));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаКоличествоИзделий", ТекстЗапросаКоличествоИзделий);
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("КоличествоПартий", ДанныеЭтапа.КоличествоПартий);
		
		Исполнители = Запрос.Выполнить().Выгрузить();
		
		СтруктураПоиска = Новый Структура("ЕстьРасчетПоФормуле", Истина);
		СтрокиКРасчетуФормул = Исполнители.НайтиСтроки(СтруктураПоиска);
		Если СтрокиКРасчетуФормул.Количество() > 0 Тогда
			ТекстЗапроса = 
				"ВЫБРАТЬ
				|	СпецификацииИзделий.Номенклатура                    КАК Номенклатура,
				|	СпецификацииИзделий.Характеристика                  КАК Характеристика,
				|	СпецификацииИзделий.Спецификация                    КАК Спецификация,
				|	СпецификацииИзделий.КоличествоУпаковокНаЕдиницуПартииЗапуска
				|		* &КоличествоПартий
				|		* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) КАК Количество
				|ИЗ
				|	РегистрСведений.СпецификацииИзделий КАК СпецификацииИзделий
				|ГДЕ
				|	СпецификацииИзделий.Спецификация = &Спецификация
				|	И СпецификацииИзделий.Порядок = 1";
				
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
				"&ТекстЗапросаКоэффициентУпаковки",
				Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
					"СпецификацииИзделий.Упаковка",
					"СпецификацииИзделий.Номенклатура"));
			
			Запрос.Текст = ТекстЗапроса;
			Запрос.УстановитьПараметр("Спецификация", ДанныеЭтапа.Спецификация);
			Запрос.УстановитьПараметр("КоличествоПартий", ДанныеЭтапа.КоличествоПартий);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуре();
				ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Выборка);
				ДанныеСпецификации = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоНоменклатуре(
					ДанныеПоНоменклатуре,
					Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных("ВидыРабочихЦентров, АльтернативныеВидыРабочихЦентров"),
					Новый Структура("Этап", Этап));
				
				Для каждого Строка Из СтрокиКРасчетуФормул Цикл
					НайденнаяСтрока = ?(Строка.Альтернативный,
						ДанныеСпецификации.АльтернативныеВидыРабочихЦентров.Найти(Строка.ВидРабочегоЦентра, "ВидРабочегоЦентра"),
						ДанныеСпецификации.ВидыРабочихЦентров.Найти(Строка.ВидРабочегоЦентра, "ВидРабочегоЦентра"));
					Если НайденнаяСтрока <> Неопределено Тогда
						Строка.ВремяРаботы = НайденнаяСтрока.ВремяРаботы;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Исполнители = СвернутьИсполнителей(Исполнители, Ложь);
		ДанныеЭтапа.Вставить("Исполнители", Исполнители);
		
		// Расписание и доступность
		Ссылки = Новый Массив;
		СтруктураПоиска = Новый Структура("УчитыватьДоступность", Ложь);
		Для каждого Строка Из Исполнители.НайтиСтроки(СтруктураПоиска) Цикл
			Ссылки.Добавить(Строка.ВидРабочегоЦентра);
		КонецЦикла;
		Если Ссылки.Количество() > 0 Тогда
			ДанныеЭтапа.Вставить("РасписаниеББВ", РасписаниеББВ(ДанныеЭтапа, Исполнители));
			ДанныеЭтапа.Вставить("ЗагрузкаВРЦБезОграничений", ЗагрузкаВРЦБезОграничений(ДанныеЭтапа, Ссылки));
			
			Если ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
				Графики = ПроизводствоСервер.ГрафикиРаботыВидовРЦ(Ссылки);
				Для каждого Строка Из Графики Цикл
					ГрафикиСмен.Добавить(Строка.ГрафикРаботы);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Ссылки.Очистить();
		СтруктураПоиска.УчитыватьДоступность = Истина;
		Для каждого Строка Из Исполнители.НайтиСтроки(СтруктураПоиска) Цикл
			Ссылки.Добавить(Строка.ВидРабочегоЦентра);
		КонецЦикла;
		Если Ссылки.Количество() > 0 Тогда
			ДанныеЭтапа.Вставить("ДоступностьВРЦ", ДоступностьВРЦ(ДанныеЭтапа, Ссылки));
			
			Если ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
				Графики = ПроизводствоСервер.ГрафикиРаботыРЦ(Ссылки, Ложь);
				Для каждого Строка Из Графики Цикл
					ГрафикиСмен.Добавить(Строка.ГрафикРаботы);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Интервалы
	Интервалы = Неопределено;
	
	Если ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		КалендарныеГрафики.СоздатьВТРасписанияРаботыНаПериод(
			МенеджерВременныхТаблиц, ГрафикиСмен, ПолныйИнтервалНачало, ПолныйИнтервалОкончание, Ложь);
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(Расписание.ГрафикРаботы КАК Справочник.Календари).Наименование КАК Смена,
			|	МИНИМУМ(ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
			|		ЧАС(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1)))*60
			|		+ МИНУТА(ЕСТЬNULL(Расписание.ВремяНачала, ДАТАВРЕМЯ(1, 1, 1))))) КАК Начало,
			|	МАКСИМУМ(
			|		ВЫБОР
			|			КОГДА Расписание.ВремяОкончания ЕСТЬ NULL 
			|				ИЛИ Расписание.ВремяОкончания = ДАТАВРЕМЯ(1, 1, 1)
			|				ТОГДА ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, ДЕНЬ, 1)
			|			ИНАЧЕ ДОБАВИТЬКДАТЕ(Расписание.ДатаГрафика, МИНУТА,
			|				ЧАС(Расписание.ВремяОкончания)*60 + МИНУТА(Расписание.ВремяОкончания))
			|		КОНЕЦ) КАК Окончание
			|ИЗ
			|	ВТРасписанияРаботы КАК Расписание
			|ГДЕ
			|	Расписание.Период >= &Начало
			|
			|СГРУППИРОВАТЬ ПО
			|	Расписание.ГрафикРаботы,
			|	Расписание.Период,
			|	ВЫРАЗИТЬ(Расписание.ГрафикРаботы КАК Справочник.Календари).Наименование
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	"""" КАК Смена,
			|	КалендарныеГрафики.ДатаГрафика КАК Начало,
			|	ДОБАВИТЬКДАТЕ(КалендарныеГрафики.ДатаГрафика, ДЕНЬ, 1) КАК Окончание
			|ИЗ
			|	РегистрСведений.КалендарныеГрафики КАК КалендарныеГрафики
			|ГДЕ
			|	КалендарныеГрафики.Календарь = &ОсновнойКалендарь
			|	И КалендарныеГрафики.ДатаГрафика МЕЖДУ &Начало И &Окончание
			|	И НЕ ИСТИНА В (
			|		ВЫБРАТЬ ПЕРВЫЕ 1
			|			ИСТИНА
			|		ИЗ
			|			ВТРасписанияРаботы КАК Т
			|		ГДЕ
			|			Т.Период = КалендарныеГрафики.ДатаГрафика)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Начало");
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Начало", ПолныйИнтервалНачало);
		Запрос.УстановитьПараметр("Окончание", ПолныйИнтервалОкончание);
		Запрос.УстановитьПараметр("ОсновнойКалендарь", Константы.ОсновнойКалендарьПредприятия.Получить());
		
		Интервалы = Запрос.Выполнить().Выгрузить();
	Иначе
		Интервалы = Новый ТаблицаЗначений;
		Интервалы.Колонки.Добавить("Начало", Новый ОписаниеТипов("Дата"));
		Интервалы.Колонки.Добавить("Окончание", Новый ОписаниеТипов("Дата"));
		Интервалы.Колонки.Добавить("Смена", Новый ОписаниеТипов("Строка"));
		
		ТекущийИнтервал = ПолныйИнтервалНачало;
		Пока ТекущийИнтервал < ПолныйИнтервалОкончание Цикл
			НачалоИнтервала = ТекущийИнтервал;
			Если ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
				ТекущийИнтервал = КонецЧаса(ТекущийИнтервал) + 1;
			ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
				ТекущийИнтервал = КонецДня(ТекущийИнтервал) + 1;
			ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
				ТекущийИнтервал = КонецНедели(ТекущийИнтервал) + 1;
			ИначеЕсли ДанныеЭтапа.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
				ТекущийИнтервал = КонецМесяца(ТекущийИнтервал) + 1;
			КонецЕсли;
			
			НоваяСтрока = Интервалы.Добавить();
			НоваяСтрока.Начало = НачалоИнтервала;
			НоваяСтрока.Окончание = ТекущийИнтервал;
		КонецЦикла;
	КонецЕсли;
	
	ДанныеЭтапа.Вставить("Интервалы", Интервалы);
	
	Возврат ДанныеЭтапа;
	
КонецФункции

Процедура ВывестиШапку(ТабличныйДокумент, ДанныеЭтапа, МакетОформления)
	
	ИнтервалПланирования = ДанныеЭтапа.ИнтервалПланирования;
	Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
		ОбластьИнтервал = МакетОформления.ПолучитьОбласть("ИнтервалЧас");
		ОбластьИнтервалЗаголовок = МакетОформления.ПолучитьОбласть("ИнтервалЧасЗаголовок");
		
		СтруктураПараметры = Новый Структура("ЧасНачалоДня");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
		ОбластьИнтервал = МакетОформления.ПолучитьОбласть("ИнтервалДень");
		ОбластьИнтервалЗаголовок = МакетОформления.ПолучитьОбласть("ИнтервалДеньЗаголовок");
		
		СтруктураПараметры = Новый Структура("ДеньНачалоНедели, День1, День2,
			|День3, День4, День5, День6, День7");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
		ОбластьИнтервал = МакетОформления.ПолучитьОбласть("ИнтервалНеделя");
		ОбластьИнтервалЗаголовок = МакетОформления.ПолучитьОбласть("ИнтервалНеделяЗаголовок");
	
		СтруктураПараметры = Новый Структура("ДеньНачалоНедели");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		ОбластьИнтервал = МакетОформления.ПолучитьОбласть("ИнтервалМесяц");
		ОбластьИнтервалЗаголовок = МакетОформления.ПолучитьОбласть("ИнтервалМесяцЗаголовок");
		
		СтруктураПараметры = Новый Структура("ДеньНачалоМесяца");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
		ОбластьИнтервал = МакетОформления.ПолучитьОбласть("ИнтервалСмена");
		ОбластьИнтервалЗаголовок = МакетОформления.ПолучитьОбласть("ИнтервалСменаЗаголовок");
		
		СтруктураПараметры = Новый Структура("ДеньНачалоСмены");
	КонецЕсли;
	
	ОбластьИнтервалЗаголовок.Параметры.ЭтапПроизводства = ДанныеЭтапа.Этап;
	ОбластьИнтервалЗаголовок.Параметры.ПредставлениеЭтапа = ДанныеЭтапа.ПредставлениеЭтапа;
	ТабличныйДокумент.Вывести(ОбластьИнтервалЗаголовок);
	
	Для Индекс = 0 По ДанныеЭтапа.Интервалы.Количество()-1 Цикл
		Интервал = ДанныеЭтапа.Интервалы[Индекс]; // СтрокаТаблицыЗначений
		ТекущийИнтервал = Интервал.Начало;
		
		Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
			Если НачалоЧаса(ТекущийИнтервал) = НачалоДня(ТекущийИнтервал) Тогда
				СтруктураПараметры.ЧасНачалоДня = ТекущийИнтервал;
			КонецЕсли;
			Если КонецЧаса(ТекущийИнтервал) = КонецДня(ТекущийИнтервал) Тогда
				ОбластьИнтервал.Параметры.Заполнить(СтруктураПараметры);
				ТабличныйДокумент.Присоединить(ОбластьИнтервал);
			КонецЕсли;
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
			Если НачалоДня(ТекущийИнтервал) = НачалоНедели(ТекущийИнтервал) Тогда
				СтруктураПараметры.ДеньНачалоНедели = ТекущийИнтервал;
				ДеньНедели = 0;
			КонецЕсли;
			
			ЭтоТекущийДень = ТекущийИнтервал = НачалоДня(ТекущаяДатаСеанса());
			
			ДеньНедели = ДеньНедели + 1;
			Если ДеньНедели = 1 Тогда
				СтруктураПараметры.День1 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День1.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День1.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 2 Тогда
				СтруктураПараметры.День2 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День2.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День2.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 3 Тогда
				СтруктураПараметры.День3 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День3.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День3.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 4 Тогда
				СтруктураПараметры.День4 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День4.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День4.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 5 Тогда
				СтруктураПараметры.День5 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День5.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День5.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 6 Тогда
				СтруктураПараметры.День6 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День6.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День6.Шрифт,,, ЭтоТекущийДень);
			ИначеЕсли ДеньНедели = 7 Тогда
				СтруктураПараметры.День7 = ТекущийИнтервал;
				ОбластьИнтервал.Области.День7.Шрифт = Новый Шрифт(
					ОбластьИнтервал.Области.День7.Шрифт,,, ЭтоТекущийДень);
			КонецЕсли;
			
			Если КонецДня(ТекущийИнтервал) = КонецНедели(ТекущийИнтервал) Тогда
				ОбластьИнтервал.Параметры.Заполнить(СтруктураПараметры);
				ТабличныйДокумент.Присоединить(ОбластьИнтервал);
			КонецЕсли;
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
			СтруктураПараметры.ДеньНачалоНедели = ТекущийИнтервал;
			ОбластьИнтервал.Параметры.Заполнить(СтруктураПараметры);
		
			ТабличныйДокумент.Присоединить(ОбластьИнтервал);
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
			СтруктураПараметры.ДеньНачалоМесяца = ТекущийИнтервал;
			ОбластьИнтервал.Параметры.Заполнить(СтруктураПараметры);
		
			ТабличныйДокумент.Присоединить(ОбластьИнтервал);
		ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
			СтруктураПараметры.ДеньНачалоСмены = Формат(Интервал.Начало, "ДФ='dd MMM HH:mm'")
				+ Символы.ПС
				+ Интервал.Смена;
			ОбластьИнтервал.Параметры.Заполнить(СтруктураПараметры);
			
			ТабличныйДокумент.Присоединить(ОбластьИнтервал);
		КонецЕсли;
	КонецЦикла;
	
	ТабличныйДокумент.ФиксацияСверху = ОбластьИнтервал.ВысотаТаблицы;
	ТабличныйДокумент.ФиксацияСлева  = ОбластьИнтервалЗаголовок.ШиринаТаблицы;
	
КонецПроцедуры

Процедура УстановитьОформлениеОбласти(Приемник, Источник)
	
	Приемник.ЦветФона   = Источник.ЦветФона;
	Приемник.ЦветТекста = Источник.ЦветТекста;
	Приемник.ЦветУзора  = Источник.ЦветУзора;
	Приемник.Узор       = Источник.Узор;
	
КонецПроцедуры

Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

Функция ЭтоЭтапУББВ(ДанныеЭтапа)
	
	Возврат НЕ ДанныеЭтапа.ПланироватьРаботуВидовРабочихЦентров ИЛИ ДанныеЭтапа.ПроизводствоНаСтороне;
	
КонецФункции

Функция ПолучитьВремяВСекундах(ВремяРаботы, ЕдиницаИзмерения)
	
	Возврат ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВСекундах(
		ВремяРаботы, ЕдиницаИзмерения);
	
КонецФункции

Функция ПолучитьВремяСтрокой(ВремяРаботы, ЕдиницаИзмерения = Неопределено)
	
	Возврат ПланированиеПроизводстваКлиентСервер.ПолучитьВремяСтрокой(
		ВремяРаботы, ЕдиницаИзмерения);
	
КонецФункции

Процедура ОбвестиГраницыЭтапа(ТабличныйДокумент, ДанныеЭтапа)
	
	Если НЕ ЗначениеЗаполнено(ДанныеЭтапа.ЛеваяГраницаЭтапа)
		ИЛИ НЕ ЗначениеЗаполнено(ДанныеЭтапа.ПраваяГраницаЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,2);
	ОбластьТекущийЭтап = ТабличныйДокумент.Область(
		4, 
		ДанныеЭтапа.ЛеваяГраницаЭтапа, 
		ТабличныйДокумент.ВысотаТаблицы, 
		ДанныеЭтапа.ПраваяГраницаЭтапа);
													
	ОбластьТекущийЭтап.Обвести(Линия, Линия, Линия, Линия);
	ОбластьТекущийЭтап.ЦветРамки = WebЦвета.СветлоЗеленый;
	
КонецПроцедуры

Функция СвернутьИсполнителей(Исполнители, СворачиватьАльтернативные = Ложь)
	
	Если Исполнители.Колонки.Найти("МассивКлючей") = Неопределено Тогда
		Исполнители.Колонки.Добавить("МассивКлючей", Новый ОписаниеТипов("Массив"));
	КонецЕсли;
	
	Если Исполнители.Количество() < 2 Тогда
		Возврат Исполнители;
	КонецЕсли;
	
	Соответствие = Новый Соответствие();
	Для индекс = -Исполнители.Количество() + 1 По 0 Цикл
		
		Элемент = Исполнители[-индекс];
		
		Если НЕ ЗначениеЗаполнено(Элемент.МассивКлючей) Тогда
			Элемент.МассивКлючей.Добавить(Элемент.КлючСвязи);
		КонецЕсли;
		
		Если НЕ СворачиватьАльтернативные И Элемент.Альтернативный = Истина Тогда
			Продолжить;
		КонецЕсли;
		
		Если Соответствие.Количество() = 0 Тогда
			Соответствие.Вставить(Элемент.ВидРабочегоЦентра, Элемент);
			Продолжить;
		КонецЕсли;
		
		Дубль = Соответствие.Получить(Элемент.ВидРабочегоЦентра);
		
		Если НЕ ЗначениеЗаполнено(Дубль) Тогда
			Соответствие.Вставить(Элемент.ВидРабочегоЦентра, Элемент);
			Продолжить;
		КонецЕсли;
		
		Дубль.ВремяРаботы = Дубль.ВремяРаботы + Элемент.ВремяРаботы;
		Дубль.МассивКлючей.Добавить(Элемент.КлючСвязи);
		
		Исполнители.Удалить(Элемент);
	КонецЦикла;
	
	Возврат Исполнители;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
