#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("ПредметСделки", ПредметСделки) Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Уведомление") Тогда
		ВерсияУведомления_2012 = Параметры.Уведомление.ВерсияУведомления <= КонтролируемыеСделкиКлиентСервер.ВерсияУведомления_2012();
		Элементы.ГруппаОКВЭД.Видимость = ВерсияУведомления_2012;
		Элементы.ГруппаОКВЭД2.Видимость = Не ВерсияУведомления_2012;
		Элементы.ГруппаОКП.Видимость = ВерсияУведомления_2012;
		Элементы.ГруппаОКПД2.Видимость = Не ВерсияУведомления_2012;
	КонецЕсли;
	
	ОбновитьДанныеФормыПоПредметуСделки(ЭтаФорма);
	
	НаименованиеОКВЭД2 = НаименованиеОКВЭД2(КодОКВЭД2);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредметСделкиПриИзменении(Элемент)
	ОбновитьДанныеФормыПоПредметуСделки(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	Если ЭтаФорма.Модифицированность Тогда
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("КодОКВЭД",  КодОКВЭД);
		ЗначенияЗаполнения.Вставить("КодОКВЭД2", КодОКВЭД2);
		ЗначенияЗаполнения.Вставить("КодТНВЭД",  КодТНВЭД);
		ЗначенияЗаполнения.Вставить("КодОКП",    КодОКП);
		ЗначенияЗаполнения.Вставить("КодОКПД2",  КодОКПД2);
		ЗначенияЗаполнения.Вставить("ОблагаетсяНДПИПоПроцентнойСтавке", ОблагаетсяНДПИПоПроцентнойСтавке);
		ОбработатьИзмененияВПредметеСделки(ПредметСделки, ЗначенияЗаполнения);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура КодОКВЭД2ПриИзменении(Элемент)
	
	НаименованиеОКВЭД2 = НаименованиеОКВЭД2(КодОКВЭД2);
	
КонецПроцедуры

&НаКлиенте
Процедура КодОКВЭД2АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеПодбора = ПодборОКВЭДСервер(Текст);
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Для Каждого ОКВЭД Из ДанныеПодбора Цикл
			ДанныеВыбора.Добавить(ОКВЭД.Код, ОКВЭД.Представление);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодОКВЭД2НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработкаЗакрытия = Новый ОписаниеОповещения(
		"ПодборИзОКВЭДЗавершение",
		ЭтотОбъект);
	
	ПараметрыФормы = КлассификаторОКВЭДКлиент.НовыйПараметрыВыбора();
	ПараметрыФормы.ОписаниеОповещенияОЗакрытии = ОбработкаЗакрытия;
	ПараметрыФормы.Разрешенные = Истина;
	ПараметрыФормы.ТекущийКод = КодОКВЭД2;
	
	КлассификаторОКВЭДКлиент.Выбрать(ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьДанныеФормыПоПредметуСделки(Форма)
	
	СтруктураДанных = ВернутьДанныеПредмета(Форма.ПредметСделки);
	ЗаполнитьЗначенияСвойств(Форма, СтруктураДанных);
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Форма.Элементы, "ГруппаТНВЭД, ГруппаОКП, ГруппаОКПД2, ОблагаетсяНДПИПоПроцентнойСтавке", "Доступность", СтруктураДанных.ЭтоТовар);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВернутьДанныеПредмета(ПредметСделки)
	
	РеквизитыДляЗаполнения = "КодОКВЭД, КодОКВЭД2, КодТНВЭД, КодОКП, КодОКПД2, ОблагаетсяНДПИПоПроцентнойСтавке, ТипНоменклатуры";
	СтруктураВозврата = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПредметСделки, РеквизитыДляЗаполнения);
	СтруктураВозврата.Вставить("ЭтоТовар", Не СтруктураВозврата.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа
								И Не СтруктураВозврата.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга);
		
	Возврат СтруктураВозврата;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОбработатьИзмененияВПредметеСделки(СсылкаНаОбъект, ЗначенияЗаполнения)
		
	ИзменяемыйОбъект = СсылкаНаОбъект.ПолучитьОбъект();
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ЗаполнитьЗначенияСвойств(ИзменяемыйОбъект, ЗначенияЗаполнения);
	Исключение
		Отказ = Истина;
		Возврат;
	КонецПопытки;
	
	Попытка
		ИзменяемыйОбъект.Записать();
	Исключение
		Отказ = Истина;
	КонецПопытки;
	
	РазблокироватьДанныеДляРедактирования(СсылкаНаОбъект);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПодборОКВЭДСервер(Знач СтрокаПоиска)
	
	ПараметрыПодбора              = КлассификаторОКВЭД.НовыйОтбор();
	ПараметрыПодбора.СтрокаПоиска = СтрокаПоиска;
	ПараметрыПодбора.Режим        = "КодИНаименование";
	ПараметрыПодбора.Разрешенные  = Истина;
	
	Возврат КлассификаторОКВЭД.Подбор(ПараметрыПодбора);
	
КонецФункции

&НаКлиенте
Процедура ПодборИзОКВЭДЗавершение(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура")
		Или РезультатВыбора.ВыбранныеЗначения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Не КодОКВЭД2 = РезультатВыбора.ВыбранныеЗначения[0].Код Тогда
		
		КодОКВЭД2 = РезультатВыбора.ВыбранныеЗначения[0].Код;
		НаименованиеОКВЭД2 = РезультатВыбора.ВыбранныеЗначения[0].Наименование;
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаименованиеОКВЭД2(КодОКВЭД2)
	
	НаименованиеОКВЭД2 = "";
	
	Если ЗначениеЗаполнено(КодОКВЭД2) Тогда
		
		ЗначениеОКВЭД2ПоКоду = КлассификаторОКВЭД.ЗначениеПоКоду(КодОКВЭД2, Ложь);
		Если ЗначениеОКВЭД2ПоКоду.Количество() > 0 Тогда
			
			НаименованиеОКВЭД2 = ЗначениеОКВЭД2ПоКоду[0].Наименование;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НаименованиеОКВЭД2;
	
КонецФункции

#КонецОбласти