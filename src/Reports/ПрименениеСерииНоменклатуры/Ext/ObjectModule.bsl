#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Настройки общей формы отчета подсистемы "Варианты отчетов".
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - Форма отчета.
//   КлючВарианта - Строка - Имя предопределенного варианта отчета или уникальный идентификатор пользовательского.
//   Настройки - Структура - см. возвращаемое значение ОтчетыКлиентСервер.ПолучитьНастройкиОтчетаПоУмолчанию().
//
Процедура ОпределитьНастройкиФормы(Форма, КлючВарианта, Настройки) Экспорт
	
	Настройки.События.ПередЗагрузкойВариантаНаСервере = Истина;
	
КонецПроцедуры

// Вызывается в одноименном обработчике формы отчета после выполнения кода формы.
//
// Параметры:
//   Форма - ФормаКлиентскогоПриложения - форма отчета или настроек отчета.
//   НовыеНастройкиКД - НастройкиКомпоновкиДанных - настройки для загрузки в компоновщик настроек.
//
Процедура ПередЗагрузкойВариантаНаСервере(Форма, НовыеНастройкиКД) Экспорт
	
	КомпоновщикНастроекФормы = Форма.Отчет.КомпоновщикНастроек; // КомпоновщикНастроекКомпоновкиДанных - 
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
		ЗначениеПоиска = Новый ПараметрКомпоновкиДанных("Номенклатура");
		Для каждого ЭлементПараметр Из КомпоновщикНастроекФормы.Настройки.ПараметрыДанных.Элементы Цикл
			Если ЭлементПараметр.Параметр = ЗначениеПоиска Тогда
				ЭлементПараметр.ПредставлениеПользовательскойНастройки = НСтр("ru = 'Комплектующая';
																				|en = 'Component'");
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Для каждого ЭлементПараметр Из НовыеНастройкиКД.ПараметрыДанных.Элементы Цикл
			Если ЭлементПараметр.Параметр = ЗначениеПоиска Тогда
				ЭлементПараметр.ПредставлениеПользовательскойНастройки = НСтр("ru = 'Комплектующая';
																				|en = 'Component'");
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	НовыеНастройкиКД = КомпоновщикНастроекФормы.Настройки;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ИспользоватьПроизводство = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство");
	
	НастройкиОсновнойСхемы = КомпоновщикНастроек.ПолучитьНастройки();
	
	ОтборНоменклатура = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Номенклатура").Значение;
	ОтборСерия = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОсновнойСхемы, "Серия").Значение;
	
	Если НЕ ЗначениеЗаполнено(ОтборНоменклатура) Тогда
		Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Полуфабрикат или материал"" не заполнено.';
									|en = '""Semi-finished product or material"" is not filled in.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Поле ""Комплектующая"" не заполнено.';
									|en = '""Component"" is not filled in.'");
		КонецЕсли; 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ОтборСерия) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Серия"" не заполнено.';
								|en = '""Batch"" is not filled in.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ); 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	НастройкиОтчета = КомпоновщикНастроек.ПолучитьНастройки();
	
	// Подготовка данных
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Номенклатура", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Номенклатура").Значение);
	ПараметрыОтчета.Вставить("Характеристика", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Характеристика").Значение);
	ПараметрыОтчета.Вставить("Серия", КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиОтчета, "Серия").Значение);
	
	СтруктураСерииДерево = ПрименениеСерииНоменклатуры(ПараметрыОтчета);
	
	Для каждого СтрокаДерева Из СтруктураСерииДерево.Строки Цикл
		
		СтрокаДерева.ИдентификаторРодителя = "";
		СтрокаДерева.ИдентификаторЭлемента = Новый УникальныйИдентификатор;
		ЗаполнитьСлужебныеПоля(СтрокаДерева.Строки, СтрокаДерева.ИдентификаторЭлемента);
	
	КонецЦикла;
	
	ВнешниеНаборы = Новый Структура;
	ВнешниеНаборы.Вставить("ПрименениеСерии", СтруктураСерииДерево);
	
	// Оформление отчета
	ИспользоватьПроизводство = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство");
	ПараметрыОбласти = Новый Структура;
	Если ИспользоватьПроизводство Тогда
		ПараметрыОбласти.Вставить("ТекстПродукция", НСтр("ru = 'Продукция или полуфабрикат';
														|en = 'Products or semi-finished product'", ОбщегоНазначения.КодОсновногоЯзыка()));
		ПараметрыОбласти.Вставить("ТекстПроизведено", НСтр("ru = 'Произведено';
															|en = 'Produced'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Иначе
		ПараметрыОбласти.Вставить("ТекстПродукция", НСтр("ru = 'Комплект';
														|en = 'Kit'", ОбщегоНазначения.КодОсновногоЯзыка()));
		ПараметрыОбласти.Вставить("ТекстПроизведено", НСтр("ru = 'Собрано';
															|en = 'Assembled'", ОбщегоНазначения.КодОсновногоЯзыка()));
	КонецЕсли;
	
	ПоляНабораДанных = СхемаКомпоновкиДанных.НаборыДанных.ПрименениеСерии.Поля; // ПоляНабораДанныхСхемыКомпоновкиДанных
	ПолеНоменклатура = ПоляНабораДанных.Найти("Номенклатура");
	ПолеПроизведено = ПоляНабораДанных.Найти("Произведено");
	
	Если ПолеНоменклатура <> Неопределено Тогда
		ПолеНоменклатура.Заголовок = ПараметрыОбласти.ТекстПродукция;
	КонецЕсли;
	
	Если ПолеПроизведено <> Неопределено Тогда
		ПолеПроизведено.Заголовок = ПараметрыОбласти.ТекстПроизведено;
	КонецЕсли;
	
	// Формирование отчета
	ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифровки, Истина);

	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПрименениеСерииНоменклатуры(Параметры)
	
	ПрименениеСерииНоменклатурыДерево = Новый ДеревоЗначений;
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("Произведено", Новый ОписаниеТипов("Число"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("ЕдиницаИзмерения", Новый ОписаниеТипов("Строка"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("НоменклатураПредставление", Новый ОписаниеТипов("Строка"));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("СерияПредставление", Новый ОписаниеТипов("Строка"));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Новый ОписаниеТипов("УникальныйИдентификатор"));
	МассивТипов.Добавить(Новый ОписаниеТипов("Строка"));
	
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("ИдентификаторЭлемента", Новый ОписаниеТипов(МассивТипов));
	ПрименениеСерииНоменклатурыДерево.Колонки.Добавить("ИдентификаторРодителя", Новый ОписаниеТипов(МассивТипов));
	
	ПрименениеСерииКоллекция = ПрименениеСерииНоменклатурыДерево.Строки;
	
	СписокНоменклатуры = Новый ТаблицаЗначений; // см. ПроизводствоСервер.СлужебнаяСтруктураТаблицыЗначений
	СписокНоменклатуры.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	СписокНоменклатуры.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	СписокНоменклатуры.Колонки.Добавить("Строки");
	
	НоваяСтрока = СписокНоменклатуры.Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
	НоваяСтрока.Строки = ПрименениеСерииКоллекция;
	
	ОтборПоХарактеристике = ЗначениеЗаполнено(Параметры.Характеристика);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока СписокНоменклатуры.Количество() <> 0 Цикл
		
		НовыйСписокНоменклатуры = СписокНоменклатуры.СкопироватьКолонки();
		
		ПроизведенныеСерии = ПроизведенныеСерии(СписокНоменклатуры, ОтборПоХарактеристике);
		
		Если НЕ ОтборПоХарактеристике Тогда
			
			ОтборПоХарактеристике = Истина;
			
			СписокНоменклатуры.Очистить();
			
			Колонки = "НоменклатураСписка, ХарактеристикаСписка, СерияСписка";
			ПроизведенныеСерииКопия = ПроизведенныеСерии.Скопировать(, Колонки);
			ПроизведенныеСерииКопия.Свернуть(Колонки);
			
			Для каждого Строка Из ПроизведенныеСерииКопия Цикл
				
				НоваяСтрока = СписокНоменклатуры.Добавить();
				НоваяСтрока.Номенклатура = Строка.НоменклатураСписка;
				НоваяСтрока.Характеристика = Строка.ХарактеристикаСписка;
				НоваяСтрока.Серия = Строка.СерияСписка;
				НоваяСтрока.Строки = ПрименениеСерииКоллекция;
				
			КонецЦикла;
			
		КонецЕсли;
		
		Для каждого СтрокаНоменклатура Из СписокНоменклатуры Цикл
			
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("НоменклатураСписка", СтрокаНоменклатура.Номенклатура);
			СтруктураПоиска.Вставить("ХарактеристикаСписка", СтрокаНоменклатура.Характеристика);
			СтруктураПоиска.Вставить("СерияСписка", СтрокаНоменклатура.Серия);
			
	  		СписокСтрок = ПроизведенныеСерии.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаСерияИзделия Из СписокСтрок Цикл
				
				НоваяСерия = СтрокаНоменклатура.Строки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСерия, СтрокаСерияИзделия);
				НоваяСерия.НоменклатураПредставление = НоменклатураКлиентСервер.ПредставлениеНоменклатуры(
					СтрокаСерияИзделия.НоменклатураПредставление,
					СтрокаСерияИзделия.ХарактеристикаПредставление);
				
				Если ЗначениеЗаполнено(НоваяСерия.Серия) Тогда
					
					// Контроль зацикливания
					ЕстьЗацикливание = Ложь;
					
					Родитель = НоваяСерия.Родитель;
					Пока Родитель <> Неопределено Цикл
						Если Родитель.Номенклатура = НоваяСерия.Номенклатура
								И Родитель.Характеристика = НоваяСерия.Характеристика
								И Родитель.Серия = НоваяСерия.Серия Тогда
							ЕстьЗацикливание = Истина;
							Прервать;
						КонецЕслИ;
						Родитель = Родитель.Родитель;
					КонецЦикла;
					
					Если Не ЕстьЗацикливание Тогда
						НоваяНоменклатура = НовыйСписокНоменклатуры.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяНоменклатура, НоваяСерия);
						НоваяНоменклатура.Строки = НоваяСерия.Строки;
					КонецЕсли;
					
				КонецЕсли; 
				
			КонецЦикла; 
			
		КонецЦикла;
		
		СписокНоменклатуры = НовыйСписокНоменклатуры.Скопировать();
		
	КонецЦикла; 
	
	Возврат ПрименениеСерииНоменклатурыДерево;

КонецФункции

Функция ПроизведенныеСерии(СписокНоменклатуры, ОтборПоХарактеристике)
	
	ТекстыЗапроса = Новый Массив;
	
	#Область СписокНоменклатуры
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
	|	ВЫРАЗИТЬ(СписокНоменклатуры.Серия КАК Справочник.СерииНоменклатуры) КАК Серия
	|ПОМЕСТИТЬ СписокНоменклатуры
	|ИЗ
	|	&СписокНоменклатуры КАК СписокНоменклатуры
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия");
	
	#КонецОбласти
	
	//++ Локализация
	
	//++ НЕ УТ
	#Область РасходСерийПриПроизводстве21
	
	//++ НЕ УТКА
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства) КАК Документ,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Распоряжение КАК Распоряжение,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтроки КАК КодСтроки,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).КодСтрокиЭтапыГрафик КАК КодСтрокиЭтапыГрафик,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).Этап КАК Этап,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НоменклатураПолуфабриката КАК НоменклатураПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.МаршрутныйЛистПроизводства).НомерПартии КАК НомерПартии,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ РасходСерииПоГрафикуПроизводства
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПотреблениеМатериаловПриПроизводстве)
	|				И Документ ССЫЛКА Документ.МаршрутныйЛистПроизводства) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтроки,
	|	НоменклатураПолуфабриката,
	|	ХарактеристикаПолуфабриката
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ДвижениеСерии.Документ КАК Документ,
	|	ДокументРаспределение.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	ДокументРаспределение.КодСтрокиПродукция КАК КодСтрокиПродукция,
	|	СУММА(ДокументРаспределение.Количество) КАК Количество
	|ПОМЕСТИТЬ РаспределениеЗатратНаЭтапыГрафика
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)
	|				И Документ ССЫЛКА Документ.РаспределениеПроизводственныхЗатрат) КАК ДвижениеСерии
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ЭтапыГрафикаПроизводства КАК ДокументРаспределение
	|	ПО ДокументРаспределение.Ссылка = ДвижениеСерии.Документ
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвижениеСерии.Номенклатура,
	|	ДвижениеСерии.Характеристика,
	|	ДвижениеСерии.Серия,
	|	ДвижениеСерии.Документ,
	|	ДокументРаспределение.ЗаказНаПроизводство,
	|	ДокументРаспределение.КодСтрокиПродукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказНаПроизводство,
	|	КодСтрокиПродукция");
	//-- НЕ УТКА
	
	//++ Устарело_Производство21
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ДвижениеСерии.Документ КАК Документ
	|ПОМЕСТИТЬ СписаниеЗатратНаВыпускБезРаспоряжений
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.СписаниеМатериаловНаЗатраты)
	|				И Документ ССЫЛКА Документ.СписаниеЗатратНаВыпуск) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ");
	//-- Устарело_Производство21
	
	#КонецОбласти
	//-- НЕ УТ
	
	//++ НЕ УТКА
	#Область ДокументыРасходаСерийПриПроизводстве21
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РасходСерии.Номенклатура КАК Номенклатура,
	|	РасходСерии.Характеристика КАК Характеристика,
	|	РасходСерии.Серия КАК Серия,
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки КАК КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап КАК Этап
	|ПОМЕСТИТЬ МаршрутныеЛисты
	|ИЗ
	|	РасходСерииПоГрафикуПроизводства КАК РасходСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = РасходСерии.Распоряжение)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = РасходСерии.КодСтроки)
	|			И (МаршрутныйЛистПроизводства.НомерПартии = РасходСерии.НомерПартии)
	|			И (МаршрутныйЛистПроизводства.НоменклатураПолуфабриката = РасходСерии.НоменклатураПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.ХарактеристикаПолуфабриката = РасходСерии.ХарактеристикаПолуфабриката)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходСерии.Номенклатура КАК Номенклатура,
	|	РасходСерии.Характеристика КАК Характеристика,
	|	РасходСерии.Серия КАК Серия,
	|	МаршрутныйЛистПроизводства.Ссылка КАК Ссылка,
	|	МаршрутныйЛистПроизводства.Распоряжение КАК Распоряжение,
	|	МаршрутныйЛистПроизводства.КодСтроки КАК КодСтроки,
	|	МаршрутныйЛистПроизводства.Этап КАК Этап
	|ИЗ
	|	РаспределениеЗатратНаЭтапыГрафика КАК РасходСерии
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|		ПО (МаршрутныйЛистПроизводства.Распоряжение = РасходСерии.ЗаказНаПроизводство)
	|			И (МаршрутныйЛистПроизводства.КодСтроки = РасходСерии.КодСтрокиПродукция)
	|			И (МаршрутныйЛистПроизводства.Проведен)
	|			И (МаршрутныйЛистПроизводства.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхЛистовПроизводства.Отменен))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МаршрутныеЛисты.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(МаршрутныеЛисты.Номенклатура КАК Справочник.Номенклатура).ВидНоменклатуры КАК ВидНоменклатуры,
	|	МаршрутныеЛисты.Характеристика КАК Характеристика,
	|	МаршрутныеЛисты.Серия КАК Серия,
	|	МаршрутныеЛисты.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ МаршрутныеЛистыСписка
	|ИЗ
	|	МаршрутныеЛисты КАК МаршрутныеЛисты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика");
	
	#КонецОбласти
	//-- НЕ УТКА
	
    //-- Локализация
	
	//++ НЕ УТ
	#Область РасходСерийПриПроизводстве22
	
	// Производство без заказа
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ РасходСерийПриПроизводствеБезЗаказа
	|ИЗ
	|	(
	// Производство без заказа
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.МатериалыИРаботы КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Распределение материалов на производство без заказа.
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура        КАК Номенклатура,
	|		ТаблицаТовары.Характеристика      КАК Характеристика,
	|		ТаблицаТовары.Серия               КАК Серия,
	|		ТаблицаПартии.ПартияПроизводства  КАК ПартияПроизводства
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ТаблицаПартии
	|		ПО ТаблицаПартии.Ссылка = ТаблицаТовары.Ссылка
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаПартии.ПартияПроизводства.Документ) = ТИП(Документ.ПроизводствоБезЗаказа)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Распределение материалов на производство без заказа, серии указываются в отдельной табличной части.
	// Из-за отсутствия соответствия серий партиям производства - все серии участвуют во всех партиях производства.
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура        КАК Номенклатура,
	|		ТаблицаТовары.Характеристика      КАК Характеристика,
	|		ТаблицаТовары.Серия               КАК Серия,
	|		ТаблицаПартии.ПартияПроизводства  КАК ПартияПроизводства
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат.Серии КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ТаблицаПартии
	|		ПО ТаблицаПартии.Ссылка = ТаблицаТовары.Ссылка
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаПартии.ПартияПроизводства.Документ) = ТИП(Документ.ПроизводствоБезЗаказа)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	|	) КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|");
	
	// Производство на стороне
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	ТаблицаТовары.Характеристика КАК Характеристика,
	|	ТаблицаТовары.Серия КАК Серия,
	|	ТаблицаТовары.ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ РасходСерийПриПроизводствеНаСтороне
	|ИЗ
	|	(
	// Отчет переработчика
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.МатериалыИРаботы КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	//++ НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Отчет переработчика по этапам
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура     КАК Номенклатура,
	|		ТаблицаТовары.Характеристика   КАК Характеристика,
	|		ТаблицаТовары.Серия            КАК Серия,
	|		ДанныеЭтапа.ПартияПроизводства КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.МатериалыИРаботы КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДанныеЭтапа
	|		ПО ТаблицаТовары.ЭтапПроизводства = ДанныеЭтапа.Ссылка
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	//-- НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Распределение материалов на производство на стороне.
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура        КАК Номенклатура,
	|		ТаблицаТовары.Характеристика      КАК Характеристика,
	|		ТаблицаТовары.Серия               КАК Серия,
	|		ТаблицаПартии.ПартияПроизводства  КАК ПартияПроизводства
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ТаблицаПартии
	|		ПО ТаблицаПартии.Ссылка = ТаблицаТовары.Ссылка
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаПартии.ПартияПроизводства.Документ) = ТИП(Документ.ОтчетПереработчика2_5)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// Распределение материалов на производство на стороне, серии указываются в отдельной табличной части.
	// Из-за отсутствия соответствия серий партиям производства - все серии участвуют во всех партиях производства.
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура        КАК Номенклатура,
	|		ТаблицаТовары.Характеристика      КАК Характеристика,
	|		ТаблицаТовары.Серия               КАК Серия,
	|		ТаблицаПартии.ПартияПроизводства  КАК ПартияПроизводства
	|	ИЗ
	|		Документ.РаспределениеПроизводственныхЗатрат.Серии КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ТаблицаПартии
	|		ПО ТаблицаПартии.Ссылка = ТаблицаТовары.Ссылка
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ТИПЗНАЧЕНИЯ(ТаблицаПартии.ПартияПроизводства.Документ) = ТИП(Документ.ОтчетПереработчика2_5)
	|		И &ОтборПоСпискуНоменклатурыТаблица
	|	) КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|");
	
	//++ НЕ УТКА
	
	// Производство по заказам
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ЕСТЬNULL(СпрПартииПроизводства.Ссылка, ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.ЭтапПроизводства2_2).ПартияПроизводства) КАК ПартияПроизводства
	|ПОМЕСТИТЬ РасходСерийВЭтапахПроизводства
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И (СкладскаяОперация = ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПотреблениеМатериаловПриПроизводстве)
	|						И ТИПЗНАЧЕНИЯ(Документ) = ТИП(Документ.ЭтапПроизводства2_2)
	|					ИЛИ Документ В
	|						(ВЫБРАТЬ
	|							РаспределениеЗатрат.Ссылка КАК Ссылка
	|						ИЗ
	|							Документ.РаспределениеПроизводственныхЗатрат КАК РаспределениеЗатрат
	|						ГДЕ
	|							ИСТИНА В
	|								(ВЫБРАТЬ
	|									ИСТИНА
	|								ИЗ
	|									Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ПартииПроизводства
	|										ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|										ПО
	|											ПартииПроизводства.ПартияПроизводства = СпрПартииПроизводства.Ссылка
	|								ГДЕ
	|									ПартииПроизводства.Ссылка = РаспределениеЗатрат.Ссылка
	|									И ТИПЗНАЧЕНИЯ(СпрПартииПроизводства.Документ) = ТИП(Документ.ЗаказНаПроизводство2_2)
	|									И СпрПартииПроизводства.Документ <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка))))) КАК ДвижениеСерии
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспределениеПроизводственныхЗатрат.ПартииПроизводства КАК ПартииПроизводства
	|		ПО ДвижениеСерии.Документ = ПартииПроизводства.Ссылка
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО (ПартииПроизводства.ПартияПроизводства = СпрПартииПроизводства.Ссылка)
	|			И (ТИПЗНАЧЕНИЯ(СпрПартииПроизводства.Документ) = ТИП(Документ.ЗаказНаПроизводство2_2))
	|			И (СпрПартииПроизводства.Документ <> ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство2_2.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|");
	//-- НЕ УТКА
	#КонецОбласти
	
	#Область ПриходСерийПриПроизводстве22
	
	// Производство без заказа
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаТовары.Номенклатура       КАК Номенклатура,
	|	ТаблицаТовары.Характеристика     КАК Характеристика,
	|	ТаблицаТовары.Серия              КАК Серия,
	|	СУММА(ТаблицаТовары.Количество)  КАК Количество,
	|	ТаблицаТовары.ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ ПоступлениеСерийПриПроизводствеБезЗаказа
	|ИЗ
	|	(
	// производство продукции в кладовую
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделия КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|		И НЕ ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеБезЗаказа КАК ПартииРасхода)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// производство продукции в кладовую
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ВыходныеИзделияСерии КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|		И НЕ ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеБезЗаказа КАК ПартииРасхода)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// производство возвратных отходов в кладовую
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделия КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|		И НЕ ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		И НЕ ТаблицаТовары.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеБезЗаказа КАК ПартииРасхода)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	// производство возвратных отходов в кладовую
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ПроизводствоБезЗаказа.ПобочныеИзделияСерии КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТИПЗНАЧЕНИЯ(ТаблицаТовары.Получатель) = ТИП(Справочник.Склады)
	|		И НЕ ТаблицаТовары.Получатель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеБезЗаказа КАК ПартииРасхода)
	|	) КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|");
	
	//++ НЕ УТКА
	
	// Производство по заказам
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка КАК Ссылка,
	|	ЭтапПроизводства2_2.ПартияПроизводства
	|ПОМЕСТИТЬ ЭтапыПроизводства
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|ГДЕ
	|	ЭтапПроизводства2_2.Проведен
	|	И ЭтапПроизводства2_2.ПартияПроизводства В
	|			(ВЫБРАТЬ
	|				РасходСерий.ПартияПроизводства
	|			ИЗ
	|				РасходСерийВЭтапахПроизводства КАК РасходСерий)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПроизводства.ПартияПроизводства КАК ПартияПроизводства,
	|	ТаблицаВыпуск.Ссылка КАК Ссылка,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ТаблицаВыпуск.Назначение КАК Назначение,
	|	ТаблицаВыпуск.Получатель КАК Получатель,
	|	ТаблицаВыпуск.Произведено КАК Произведено,
	|	ТаблицаВыпуск.ДатаПроизводства КАК ДатаПроизводства,
	|	ТаблицаВыпуск.Подразделение КАК Подразделение,
	|	ТаблицаВыпуск.Серия КАК Серия,
	|	СУММА(ТаблицаВыпуск.Количество) КАК Количество
	|ПОМЕСТИТЬ ЭтапыВыходныеИзделия
	|ИЗ
	|	ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаВыпуск
	|		ПО ЭтапыПроизводства.Ссылка = ТаблицаВыпуск.Ссылка
	|ГДЕ
	|	ТаблицаВыпуск.Произведено
	|	И НЕ ТаблицаВыпуск.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаВыпуск.Ссылка,
	|	ТаблицаВыпуск.Произведено,
	|	ТаблицаВыпуск.Серия,
	|	ТаблицаВыпуск.Получатель,
	|	ТаблицаВыпуск.Характеристика,
	|	ТаблицаВыпуск.Назначение,
	|	ТаблицаВыпуск.ДатаПроизводства,
	|	ЭтапыПроизводства.ПартияПроизводства,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Подразделение
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Получатель,
	|	Произведено,
	|	ДатаПроизводства,
	|	Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЭтапыПроизводства.ПартияПроизводства КАК ПартияПроизводства,
	|	ТаблицаВыпуск.Ссылка КАК Ссылка,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ТаблицаВыпуск.Назначение КАК Назначение,
	|	ТаблицаВыпуск.Получатель КАК Получатель,
	|	ТаблицаВыпуск.Произведено КАК Произведено,
	|	ТаблицаВыпуск.ДатаПроизводства КАК ДатаПроизводства,
	|	ТаблицаВыпуск.Подразделение КАК Подразделение,
	|	ТаблицаВыпуск.ВладелецИзделия КАК ВладелецИзделия,
	|	ТаблицаВыпуск.Серия КАК Серия,
	|	СУММА(ТаблицаВыпуск.Количество) КАК Количество
	|ПОМЕСТИТЬ ЭтапыПобочныеИзделия
	|ИЗ
	|	ЭтапыПроизводства КАК ЭтапыПроизводства
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаВыпуск
	|		ПО ЭтапыПроизводства.Ссылка = ТаблицаВыпуск.Ссылка
	|ГДЕ
	|	ТаблицаВыпуск.Произведено = ИСТИНА
	|	И ТаблицаВыпуск.ЭтапПотребитель = ЗНАЧЕНИЕ(Документ.ЭтапПроизводства2_2.ПустаяСсылка)
	|	И НЕ ТаблицаВыпуск.Отменено
	|
	|СГРУППИРОВАТЬ ПО
	|	ЭтапыПроизводства.ПартияПроизводства,
	|	ТаблицаВыпуск.Получатель,
	|	ТаблицаВыпуск.Серия,
	|	ТаблицаВыпуск.Назначение,
	|	ТаблицаВыпуск.Произведено,
	|	ТаблицаВыпуск.Ссылка,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Характеристика,
	|	ТаблицаВыпуск.ДатаПроизводства,
	|	ТаблицаВыпуск.Подразделение,
	|	ТаблицаВыпуск.ВладелецИзделия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Номенклатура,
	|	Характеристика,
	|	Назначение,
	|	Получатель,
	|	Произведено,
	|	ДатаПроизводства,
	|	Подразделение,
	|	ВладелецИзделия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаВыпуск.ПартияПроизводства КАК ПартияПроизводства,
	|	ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|	ТаблицаВыпуск.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ТаблицаСерии.Серия, ТаблицаВыпуск.Серия) КАК Серия,
	|	ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество) КАК Количество
	|ПОМЕСТИТЬ ПриходСерийВЭтапахПроизводства
	|ИЗ
	|	ЭтапыВыходныеИзделия КАК ТаблицаВыпуск
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделияСерии КАК ТаблицаСерии
	|		ПО ТаблицаВыпуск.Ссылка = ТаблицаСерии.Ссылка
	|			И ТаблицаВыпуск.Номенклатура = ТаблицаСерии.Номенклатура
	|			И ТаблицаВыпуск.Характеристика = ТаблицаСерии.Характеристика
	|			И ТаблицаВыпуск.Назначение = ТаблицаСерии.Назначение
	|			И ТаблицаВыпуск.Получатель = ТаблицаСерии.Получатель
	|			И ТаблицаВыпуск.Произведено = ТаблицаСерии.Произведено
	|			И ТаблицаВыпуск.ДатаПроизводства = ТаблицаСерии.ДатаПроизводства
	|			И ТаблицаВыпуск.Подразделение = ТаблицаСерии.Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаВыпуск.ПартияПроизводства,
	|	ТаблицаВыпуск.Номенклатура,
	|	ТаблицаВыпуск.Характеристика,
	|	ЕСТЬNULL(ТаблицаСерии.Серия, ТаблицаВыпуск.Серия),
	|	ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество)
	|ИЗ
	|	ЭтапыПобочныеИзделия КАК ТаблицаВыпуск
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ПобочныеИзделияСерии КАК ТаблицаСерии
	|		ПО ТаблицаВыпуск.Ссылка = ТаблицаСерии.Ссылка
	|			И ТаблицаВыпуск.Номенклатура = ТаблицаСерии.Номенклатура
	|			И ТаблицаВыпуск.Характеристика = ТаблицаСерии.Характеристика
	|			И ТаблицаВыпуск.Назначение = ТаблицаСерии.Назначение
	|			И ТаблицаВыпуск.Получатель = ТаблицаСерии.Получатель
	|			И ТаблицаВыпуск.Произведено = ТаблицаСерии.Произведено
	|			И ТаблицаВыпуск.ДатаПроизводства = ТаблицаСерии.ДатаПроизводства
	|			И ТаблицаВыпуск.Подразделение = ТаблицаСерии.Подразделение
	|			И ТаблицаВыпуск.ВладелецИзделия = ТаблицаСерии.ВладелецИзделия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства");
	//-- НЕ УТКА
	#КонецОбласти
	
	#Область ПоступлениеСерийПриПроизводствеНаСтороне
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаТовары.Номенклатура       КАК Номенклатура,
	|	ТаблицаТовары.Характеристика     КАК Характеристика,
	|	ТаблицаТовары.Серия              КАК Серия,
	|	СУММА(ТаблицаТовары.Количество)  КАК Количество,
	|	ТаблицаТовары.ПартияПроизводства КАК ПартияПроизводства
	|ПОМЕСТИТЬ ПоступлениеСерийПриПроизводствеНаСтороне
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.ВыходныеИзделия КАК ТаблицаТовары
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеНаСтороне КАК ПартииРасхода)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура   КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Серия          КАК Серия,
	|		ТаблицаТовары.Количество     КАК Количество,
	|		СпрПартииПроизводства.Ссылка КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.ПобочныеИзделия КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПартииПроизводства КАК СпрПартииПроизводства
	|		ПО СпрПартииПроизводства.Документ = ТаблицаТовары.Ссылка
	|		И СпрПартииПроизводства.Код = ТаблицаТовары.НомерГруппыЗатрат
	|		И НЕ СпрПартииПроизводства.ПометкаУдаления
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И СпрПартииПроизводства.Ссылка В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеНаСтороне КАК ПартииРасхода)
	//++ НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура     КАК Номенклатура,
	|		ТаблицаТовары.Характеристика   КАК Характеристика,
	|		ТаблицаТовары.Серия            КАК Серия,
	|		ТаблицаТовары.Количество       КАК Количество,
	|		ДанныеЭтапа.ПартияПроизводства КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.ВыходныеИзделия КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДанныеЭтапа
	|		ПО ТаблицаТовары.ЭтапПроизводства = ДанныеЭтапа.Ссылка
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ДанныеЭтапа.ПартияПроизводства В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеНаСтороне КАК ПартииРасхода)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура     КАК Номенклатура,
	|		ТаблицаТовары.Характеристика   КАК Характеристика,
	|		ТаблицаТовары.Серия            КАК Серия,
	|		ТаблицаТовары.Количество       КАК Количество,
	|		ДанныеЭтапа.ПартияПроизводства КАК ПартияПроизводства
	|	ИЗ
	|		Документ.ОтчетПереработчика2_5.ПобочныеИзделия КАК ТаблицаТовары
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДанныеЭтапа
	|		ПО ТаблицаТовары.ЭтапПроизводства = ДанныеЭтапа.Ссылка
	|	
	|	ГДЕ
	|		ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		И ДанныеЭтапа.ПартияПроизводства В(
	|			ВЫБРАТЬ
	|				ПартииРасхода.ПартияПроизводства
	|			ИЗ
	|				РасходСерийПриПроизводствеНаСтороне КАК ПартииРасхода)
	//-- НЕ УТКА
	|	) КАК ТаблицаТовары
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Характеристика,
	|	ТаблицаТовары.Серия,
	|	ТаблицаТовары.ПартияПроизводства
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияПроизводства
	|");
	
	#КонецОбласти
	//-- НЕ УТ
	
	#Область РасходСерииПриСборке
	
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДвижениеСерии.Номенклатура КАК Номенклатура,
	|	ДвижениеСерии.Характеристика КАК Характеристика,
	|	ДвижениеСерии.Серия КАК Серия,
	|	ВЫРАЗИТЬ(ДвижениеСерии.Документ КАК Документ.СборкаТоваров) КАК Документ,
	|	ДвижениеСерии.КоличествоОборот КАК Количество
	|ПОМЕСТИТЬ РасходСерииПриСборке
	|ИЗ
	|	РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			&ОтборПоСпискуНоменклатуры
	|				И СкладскаяОперация В (ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектующихДляСборки),
	|										ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ОтгрузкаКомплектовДляРазборки))) КАК ДвижениеСерии
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Документ");
	
	#КонецОбласти
	
	#Область ПриходСерий
	ТекстыЗапроса.Добавить(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенныйЗапрос.НоменклатураСписка КАК НоменклатураСписка,
	|	ВложенныйЗапрос.ХарактеристикаСписка КАК ХарактеристикаСписка, 
	|	ВложенныйЗапрос.СерияСписка КАК СерияСписка,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.Представление КАК НоменклатураПредставление,
	|	ВложенныйЗапрос.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Характеристика.Представление КАК ХарактеристикаПредставление,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	ВложенныйЗапрос.Серия.Представление КАК СерияПредставление,
	|	СУММА(ВложенныйЗапрос.Произведено) КАК Произведено
	|ИЗ
	|(
	|	ВЫБРАТЬ
	|		РасходСерииПриСборке.Номенклатура КАК НоменклатураСписка,
	|		РасходСерииПриСборке.Характеристика КАК ХарактеристикаСписка,
	|		РасходСерииПриСборке.Серия КАК СерияСписка,
	|		ПоступлениеСерииПриСборке.Номенклатура КАК Номенклатура,
	|		ПоступлениеСерииПриСборке.Характеристика КАК Характеристика,
	|		ПоступлениеСерииПриСборке.Серия КАК Серия,
	|		ПоступлениеСерииПриСборке.КоличествоОборот КАК Произведено
	|	ИЗ 	
	|		РегистрНакопления.ДвиженияСерийТоваров.Обороты(
	|			,
	|			,
	|			,
	|			Документ В
	|					(ВЫБРАТЬ
	|						РасходСерииПриСборке.Документ
	|					ИЗ
	|						РасходСерииПриСборке КАК РасходСерииПриСборке)
	|				И СкладскаяОперация В (ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаСобранныхКомплектов),
	|										ЗНАЧЕНИЕ(Перечисление.СкладскиеОперации.ПриемкаКомплектующихПослеРазборки))) КАК ПоступлениеСерииПриСборке
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасходСерииПриСборке КАК РасходСерииПриСборке
	|		ПО РасходСерииПриСборке.Документ = ПоступлениеСерииПриСборке.Документ
	//++ НЕ УТКА

	//++ Локализация
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МаршрутныеЛистыСписка.Номенклатура КАК НоменклатураСписка,
	|		МаршрутныеЛистыСписка.Характеристика КАК ХарактеристикаСписка,
	|		МаршрутныеЛистыСписка.Серия КАК СерияСписка,
	|		ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|		ТаблицаВыпуск.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия 
	|		КОНЕЦ КАК Серия,
	|		СУММА(ЕСТЬNULL(ТаблицаСерии.КоличествоФакт, ТаблицаВыпуск.КоличествоФакт)) КАК Произведено
	|	ИЗ 
	|		МаршрутныеЛистыСписка КАК МаршрутныеЛистыСписка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделия КАК ТаблицаВыпуск
	|			ПО ТаблицаВыпуск.Ссылка = МаршрутныеЛистыСписка.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.МаршрутныйЛистПроизводства.ВыходныеИзделияСерии КАК ТаблицаСерии
	|			ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|				И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|				И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|				И ТаблицаСерии.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	ГДЕ
	|		ТаблицаВыпуск.КоличествоФакт <> 0
	|
	|	СГРУППИРОВАТЬ ПО
	|		МаршрутныеЛистыСписка.Номенклатура,
	|		МаршрутныеЛистыСписка.Характеристика,
	|		МаршрутныеЛистыСписка.Серия,
	|		ТаблицаВыпуск.Ссылка,
	|		ТаблицаВыпуск.Номенклатура,
	|		ТаблицаВыпуск.Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия
	|		КОНЕЦ
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		МаршрутныеЛистыСписка.Номенклатура КАК НоменклатураСписка,
	|		МаршрутныеЛистыСписка.Характеристика КАК ХарактеристикаСписка,
	|		МаршрутныеЛистыСписка.Серия КАК СерияСписка,
	|		ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|		ТаблицаВыпуск.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия 
	|		КОНЕЦ КАК Серия,
	|		СУММА(ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество)) КАК Произведено
	|	ИЗ 
	|		МаршрутныеЛистыСписка КАК МаршрутныеЛистыСписка
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаВыпуск
	|			ПО ТаблицаВыпуск.Распоряжение = МаршрутныеЛистыСписка.Ссылка
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Серии КАК ТаблицаСерии
	|			ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|				И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|				И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|				И ТаблицаСерии.Склад = ТаблицаВыпуск.Склад
	|				И ТаблицаСерии.Подразделение = ТаблицаВыпуск.Подразделение
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийСклад
	|			ПО ПолитикиУчетаСерийСклад.Ссылка = МаршрутныеЛистыСписка.ВидНоменклатуры
	|				И ПолитикиУчетаСерийСклад.Склад = ТаблицаВыпуск.Склад
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерийПодразделение
	|			ПО ПолитикиУчетаСерийПодразделение.Ссылка = МаршрутныеЛистыСписка.ВидНоменклатуры
	|				И ПолитикиУчетаСерийПодразделение.Склад = ТаблицаВыпуск.Подразделение
	|
	|	ГДЕ
	|		ТаблицаВыпуск.Ссылка.Проведен
	|		И НЕ ЕСТЬNULL(ПолитикиУчетаСерийСклад.ПолитикаУчетаСерий.УказыватьПриПроизводствеПродукции,
	|				ЕСТЬNULL(ПолитикиУчетаСерийПодразделение.ПолитикаУчетаСерий.УказыватьПриПроизводствеПродукции, ЛОЖЬ))
	|		И ЕСТЬNULL(ПолитикиУчетаСерийСклад.ПолитикаУчетаСерий.УказыватьПриПриемкеПродукцииИзПроизводства,
	|				ЕСТЬNULL(ПолитикиУчетаСерийПодразделение.ПолитикаУчетаСерий.УказыватьПриПриемкеПродукцииИзПроизводства, ЛОЖЬ))
	|
	|	СГРУППИРОВАТЬ ПО
	|		МаршрутныеЛистыСписка.Номенклатура,
	|		МаршрутныеЛистыСписка.Характеристика,
	|		МаршрутныеЛистыСписка.Серия,
	|		ТаблицаВыпуск.Номенклатура,
	|		ТаблицаВыпуск.Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия
	|		КОНЕЦ
	//-- Локализация

	//-- НЕ УТКА

	//++ НЕ УТ

	//++ Устарело_Производство21
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Номенклатура КАК НоменклатураСписка,
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Характеристика КАК ХарактеристикаСписка,
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Серия КАК СерияСписка,
	|		ТаблицаВыпуск.Номенклатура КАК Номенклатура,
	|		ТаблицаВыпуск.Характеристика КАК Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия 
	|		КОНЕЦ КАК Серия,
	|		СУММА(ЕСТЬNULL(ТаблицаСерии.Количество, ТаблицаВыпуск.Количество)) КАК Произведено
	|	ИЗ 
	|		СписаниеЗатратНаВыпускБезРаспоряжений КАК СписаниеЗатратНаВыпускБезРаспоряжений
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СписаниеЗатратНаВыпуск.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
	|			ПО ТаблицаВыходныеИзделия.Ссылка = СписаниеЗатратНаВыпускБезРаспоряжений.Документ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Товары КАК ТаблицаВыпуск
	|			ПО ТаблицаВыпуск.Ссылка = ТаблицаВыходныеИзделия.Распоряжение
	|				И ТаблицаВыпуск.Номенклатура = ТаблицаВыходныеИзделия.Номенклатура
	|				И ТаблицаВыпуск.Характеристика = ТаблицаВыходныеИзделия.Характеристика
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВыпускПродукции.Серии КАК ТаблицаСерии
	|			ПО ТаблицаСерии.Ссылка = ТаблицаВыпуск.Ссылка
	|				И ТаблицаСерии.Номенклатура = ТаблицаВыпуск.Номенклатура
	|				И ТаблицаСерии.Характеристика = ТаблицаВыпуск.Характеристика
	|				И ТаблицаСерии.Склад = ТаблицаВыпуск.Склад
	|				И ТаблицаСерии.Подразделение = ТаблицаВыпуск.Подразделение
	|
	|	СГРУППИРОВАТЬ ПО
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Номенклатура,
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Характеристика,
	|		СписаниеЗатратНаВыпускБезРаспоряжений.Серия,
	|		ТаблицаВыпуск.Номенклатура,
	|		ТаблицаВыпуск.Характеристика,
	|		ВЫБОР
	|			КОГДА ТаблицаВыпуск.Серия <> ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|				ТОГДА ТаблицаВыпуск.Серия
	|			ИНАЧЕ ТаблицаСерии.Серия
	|		КОНЕЦ
	//-- Устарело_Производство21
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// Производство без заказа
	|	ВЫБРАТЬ
	|		РасходСерийПриПроизводствеБезЗаказа.Номенклатура   КАК НоменклатураСписка,
	|		РасходСерийПриПроизводствеБезЗаказа.Характеристика КАК ХарактеристикаСписка,
	|		РасходСерийПриПроизводствеБезЗаказа.Серия          КАК СерияСписка,
	|		ПоступлениеСерий.Номенклатура                      КАК Номенклатура,
	|		ПоступлениеСерий.Характеристика                    КАК Характеристика,
	|		ПоступлениеСерий.Серия                             КАК Серия,
	|		ПоступлениеСерий.Количество                        КАК Произведено
	|	ИЗ
	|		ПоступлениеСерийПриПроизводствеБезЗаказа КАК ПоступлениеСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасходСерийПриПроизводствеБезЗаказа КАК РасходСерийПриПроизводствеБезЗаказа
	|		ПО РасходСерийПриПроизводствеБезЗаказа.ПартияПроизводства = ПоступлениеСерий.ПартияПроизводства
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	// Производство на стороне
	|	ВЫБРАТЬ
	|		РасходСерийПриПроизводствеНаСтороне.Номенклатура   КАК НоменклатураСписка,
	|		РасходСерийПриПроизводствеНаСтороне.Характеристика КАК ХарактеристикаСписка,
	|		РасходСерийПриПроизводствеНаСтороне.Серия          КАК СерияСписка,
	|		ПоступлениеСерий.Номенклатура                      КАК Номенклатура,
	|		ПоступлениеСерий.Характеристика                    КАК Характеристика,
	|		ПоступлениеСерий.Серия                             КАК Серия,
	|		ПоступлениеСерий.Количество                        КАК Произведено
	|	ИЗ 
	|		ПоступлениеСерийПриПроизводствеНаСтороне КАК ПоступлениеСерий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РасходСерийПриПроизводствеНаСтороне КАК РасходСерийПриПроизводствеНаСтороне
	|		ПО РасходСерийПриПроизводствеНаСтороне.ПартияПроизводства = ПоступлениеСерий.ПартияПроизводства
	//-- НЕ УТ
	
	//++ НЕ УТКА
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		РасходСерий.Номенклатура КАК НоменклатураСписка,
	|		РасходСерий.Характеристика КАК ХарактеристикаСписка,
	|		РасходСерий.Серия КАК СерияСписка,
	|		ПриходСерий.Номенклатура КАК Номенклатура,
	|		ПриходСерий.Характеристика КАК Характеристика,
	|		ПриходСерий.Серия КАК Серия,
	|		ПриходСерий.Количество КАК Произведено
	|	ИЗ 
	|		ПриходСерийВЭтапахПроизводства КАК ПриходСерий
	|			ЛЕВОЕ СОЕДИНЕНИЕ РасходСерийВЭтапахПроизводства КАК РасходСерий
	|			ПО ПриходСерий.ПартияПроизводства = РасходСерий.ПартияПроизводства
	|
	//-- НЕ УТКА
	|) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ(ВложенныйЗапрос.НоменклатураСписка = ВложенныйЗапрос.Номенклатура
	|		И ВложенныйЗапрос.ХарактеристикаСписка = ВложенныйЗапрос.Характеристика
	|		И ВложенныйЗапрос.СерияСписка = ВложенныйЗапрос.Серия)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.НоменклатураСписка,
	|	ВложенныйЗапрос.ХарактеристикаСписка,
	|	ВложенныйЗапрос.СерияСписка,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Серия
	|
	|УПОРЯДОЧИТЬ ПО
	|	НоменклатураПредставление,
	|	ХарактеристикаПредставление,
	|	СерияПредставление");
	#КонецОбласти
	
	Разделитель = 
	"
	|;
	|
	|/////////////////////////////////////////////////////////////
	|";
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, Разделитель);
	
	Если ОтборПоХарактеристике Тогда
		
		ОтборПоСпискуНоменклатуры = 
		"(Номенклатура, Характеристика, Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Характеристика,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
		ОтборПоСпискуНоменклатурыТаблица = 
		"(ТаблицаТовары.Номенклатура, ТаблицаТовары.Характеристика, ТаблицаТовары.Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Характеристика,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
	Иначе
		
		ОтборПоСпискуНоменклатуры = 
		"(Номенклатура, Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
		ОтборПоСпискуНоменклатурыТаблица = 
		"(ТаблицаТовары.Номенклатура, ТаблицаТовары.Серия) В
		|					(ВЫБРАТЬ
		|						СписокНоменклатуры.Номенклатура,
		|						СписокНоменклатуры.Серия
		|					ИЗ
		|						СписокНоменклатуры)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоСпискуНоменклатурыТаблица", ОтборПоСпискуНоменклатурыТаблица);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоСпискуНоменклатуры", ОтборПоСпискуНоменклатуры);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("СписокНоменклатуры", СписокНоменклатуры);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Результат.Индексы.Добавить("НоменклатураСписка,СерияСписка");
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСлужебныеПоля(СтрокиДерева, Идентификатор)

	Для каждого СтрокаДерева Из СтрокиДерева Цикл
		
		СтрокаДерева.ИдентификаторЭлемента = Новый УникальныйИдентификатор;
		СтрокаДерева.ИдентификаторРодителя = Идентификатор;
		ЗаполнитьСлужебныеПоля(СтрокаДерева.Строки, СтрокаДерева.ИдентификаторЭлемента);
	
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
