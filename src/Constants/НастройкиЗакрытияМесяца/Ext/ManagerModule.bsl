
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Константы.НастройкиЗакрытияМесяца.ОбработатьДанныеУстановитьКонстантыОтгрузкаБезППС";
	Обработчик.Версия = "11.5.25.55";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1d233800-6888-4148-8c8f-487614db0751");
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий =
		НСтр("ru = 'Заполняет настройки закрытия месяца с типом справочник ""Операции закрытия месяца""';
			|en = 'Fills in the month-end closing settings with the ""Month-end closing operations"" catalog type'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.ОперацииЗакрытияМесяца.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.НастройкиЗакрытияМесяца.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Константы.НастройкиЗакрытияМесяца.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

// Заполняет параметры с типом СправочникСсылка.ОперацииЗакрытияМесяца
//
Процедура ОбработатьДанныеУстановитьКонстантыОтгрузкаБезППС() Экспорт
	
	МетаданныеОбъекта = СоздатьМенеджерЗначения().Метаданные();
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("Константа.НастройкиЗакрытияМесяца");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОперацииЗакрытияМесяца.Ссылка,
	|	ОперацииЗакрытияМесяца.ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.ОперацииЗакрытияМесяца КАК ОперацииЗакрытияМесяца";
	
	ВыборкаОперацииЗакрытияМесяца = Запрос.Выполнить().Выбрать();
	
	СоответствиеОпераций = Новый Соответствие();
	
	Пока ВыборкаОперацииЗакрытияМесяца.Следующий() Цикл
		СоответствиеОпераций.Вставить(
			ВыборкаОперацииЗакрытияМесяца.ИмяПредопределенныхДанных,
			ВыборкаОперацииЗакрытияМесяца.Ссылка);
	КонецЦикла;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		УстановитьПривилегированныйРежим(Истина);
		СохраненныеНастройки = Константы.НастройкиЗакрытияМесяца.Получить().Получить(); // получим содержимое хранилища значения
		УстановитьПривилегированныйРежим(Ложь);
		
		ОбновленныеНастройки = Новый Соответствие;
		
		Если Не СохраненныеНастройки = Неопределено Тогда
			Если ТипЗнч(СохраненныеНастройки) = Тип("Соответствие") Тогда
				Для Каждого КлючИЗначение Из СохраненныеНастройки Цикл
					Операция = КлючИЗначение.Ключ;
					Если ТипЗнч(Операция) = Тип("ПеречислениеСсылка.УдалитьОперацииЗакрытияМесяца") Тогда
						ИмяОперации = XMLСтрока(Операция);
						ОперацияСправочник = СоответствиеОпераций.Получить(ИмяОперации);
						Если ОперацияСправочник = Неопределено Тогда
							СообщитьОНеудачнойОбработкеПараметра(ИмяОперации, МетаданныеОбъекта);
						Иначе
							ОбновленныеНастройки.Вставить(ОперацияСправочник, КлючИЗначение.Значение);
						КонецЕсли;
					ИначеЕсли ТипЗнч(Операция) = Тип("СправочникСсылка.ОперацииЗакрытияМесяца") Тогда
						ОбновленныеНастройки.Вставить(Операция, КлючИЗначение.Значение);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Истина);
			Константы.НастройкиЗакрытияМесяца.Установить(Новый ХранилищеЗначения(СохраненныеНастройки, Новый СжатиеДанных(9)));
			УстановитьПривилегированныйРежим(Ложь);
			
			ОбновитьПовторноИспользуемыеЗначения();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%';
								|en = 'Cannot process object %Объект%. Reason: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", МетаданныеОбъекта.ПолноеИмя());
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			МетаданныеОбъекта,
			МетаданныеОбъекта.ПолноеИмя(),
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОНеудачнойОбработкеПараметра(Параметр, МетаданныеОбъекта)
	
	ТекстСообщения = НСтр("ru = 'Не удалось обработать настройки параметра закрытия месяца: %Параметр% по причине: 
							|Не найдено соответствие в справочнике ""Операции закрытие месяца""
							|значнеию перечисления ""(не используется) Операции закрытие месяца""';
							|en = 'Cannot process the settings of the %Параметр% month-end closing parameter. Reason: 
							|No match is found in the ""Month-end closing operations"" catalog
							| for the ""(not used) Month-end closing operations"" enumeration value'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", МетаданныеОбъекта.ПолноеИмя());
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Параметр%", Параметр);
	ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
		УровеньЖурналаРегистрации.Предупреждение,
		МетаданныеОбъекта,
		МетаданныеОбъекта.ПолноеИмя(),
		ТекстСообщения);
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли
