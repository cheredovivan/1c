
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет наличие в информационной базе документов отгрузки без перехода права собственности версии 2.0.
//
// Возвращаемое значение:
// 	Булево - признак наличия документов.
//
Функция ЕстьДокументыОтгрузкиБезПереходаПраваСобственности2_0() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК ТаблицаРеестраДокументов
	|ГДЕ
	|	ТаблицаРеестраДокументов.Проведен
	|	И ТаблицаРеестраДокументов.ХозяйственнаяОперация В
	|	(ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.НачислениеНДСпоОтгрузкеТоваровВПути),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОтгрузкаБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияЧерезКомиссионераБезПереходаПраваСобственности))";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности.ОбработатьДанныеУстановитьКонстантыОтгрузкаБезППС";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("4f181f7b-7677-4d57-86b1-d754282af189");
	Обработчик.ЗапускатьТолькоВГлавномУзле = Истина;
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий =
		НСтр("ru = 'Заполняет признак использования отгрузки без перехода права собственности';
			|en = 'Fills the flag that indicates use of shipment without transfer of title to goods'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

// Устанавливает константы с признаками использования отгрузки без перехода права собственности
// 	по схемам 2.0 и 2.5.
//
Процедура ОбработатьДанныеУстановитьКонстантыОтгрузкаБезППС() Экспорт
	
	Блокировка = Новый БлокировкаДанных;
	Блокировка.Добавить("Константа.ИспользоватьОтгрузкуБезПереходаПраваСобственности");
	Блокировка.Добавить("Константа.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0");
	Блокировка.Добавить("Константа.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5");
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка.Заблокировать();
		
		ЗначениеКонстанты = ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности");
		Если ЗначениеКонстанты Тогда
			
			ЕстьДокументыОтгрузкиБезПереходаПраваСобственности2_0 =
				ЕстьДокументыОтгрузкиБезПереходаПраваСобственности2_0();
			
			Если ЕстьДокументыОтгрузкиБезПереходаПраваСобственности2_0
				И Не ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0") Тогда
				Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_0.Установить(Истина);
			КонецЕсли;
			
			Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами")
				И Не ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5") Тогда
				Константы.ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5.Установить(Истина);
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Неопределено);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
