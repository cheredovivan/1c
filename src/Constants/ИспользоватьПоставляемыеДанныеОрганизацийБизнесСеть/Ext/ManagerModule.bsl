// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает значение константы в Истина
Процедура Включить() Экспорт
	УстановитьЗначение(Истина)
КонецПроцедуры

// Получает значение константы
// 
// Возвращаемое значение:
//  Булево - Значение константы
Функция Значение() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Возврат Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает новое значение.
// 
// Параметры:
//  Значение - Булево - Новое значение константы
Процедура УстановитьЗначение(Знач Значение)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Значение", Значение);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	Константа.ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть КАК ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть
		|ГДЕ
		|	ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть.Значение = &Значение";
	
	Если Не Запрос.Выполнить().Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(
		Метаданные.Константы.ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть.ПолноеИмя());
	ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных.Заблокировать();
		Установить(Значение);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
