
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ОписаниеИсточника

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	ПараметрыОтражения.ПутьКДаннымОрганизация = "АналитикаУчетаПоПартнерам.Организация";
	ПараметрыОтражения.ПутьКДаннымРегистратор = "ДокументРегистратор";
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ВыделениеДолгосрочныхАктивовОбязательств = Истина;
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "ОбъектРасчетов.ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "ОбъектРасчетов.НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "ОбъектРасчетов.Подразделение";
	ПараметрыОтражения.ПутьКДаннымДатаПогашения = "ДатаПлановогоПогашения";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Валюта";
	ПараметрыОтражения.РесурсыУпр.Добавить("ПредоплатаУпр");
	ПараметрыОтражения.РесурсыУпр.Добавить("ДолгУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("ПредоплатаРегл");
	ПараметрыОтражения.РесурсыРегл.Добавить("ДолгРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("Предоплата");
	ПараметрыОтражения.РесурсыВал.Добавить("Долг");
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Контрагенты;
	ОпределитьПоказатели(ПараметрыОтражения);
	
	//-- НЕ УТ
	
	ПараметрыОтражения.УсловиеДебет = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.Долг <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ДолгУпр <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ДолгРегл <> 0
		|			ТОГДА ПсевдонимИсточникаДанных.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		КОГДА ПсевдонимИсточникаДанных.Предоплата <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ПредоплатаУпр <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ПредоплатаРегл <> 0
		|			ТОГДА ПсевдонимИсточникаДанных.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	
	ПараметрыОтражения.УсловиеКредит = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.Долг <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ДолгУпр <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ДолгРегл <> 0
		|			ТОГДА ПсевдонимИсточникаДанных.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		КОГДА ПсевдонимИсточникаДанных.Предоплата <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ПредоплатаУпр <> 0
		|		  ИЛИ ПсевдонимИсточникаДанных.ПредоплатаРегл <> 0
		|			ТОГДА ПсевдонимИсточникаДанных.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

//++ НЕ УТКА

// Определяет источники уточнения счета, доступные в регистре и их свойства.
//
// Параметры:
//  СвойстваИсточника - Строка - "ИмяПоля" - имя атрибута регистра накопления, из которого планируется получать источник уточнения счета.
//
// Возвращаемое значение:
//  Соответствие - Ключ - название источника уточнения счета. 
//                 Значение - структура свойств источника уточнения счета.
//
Функция ИсточникиУточненияСчета(СвойстваИсточника) Экспорт
	
	ИсточникиУточненияСчета = Новый Соответствие;
	
	ИсточникиУточненияСчета.Вставить(Перечисления.ТипыИсточниковУточненияСчета.ГруппаФинансовогоУчетаРасчетов,
		Новый Структура(СвойстваИсточника, "ГФУРасчетов"));
		
	Возврат ИсточникиУточненияСчета;
	
КонецФункции

// Определяет источники подразделений регистра и их свойства.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиПодразделений() Экспорт

	ИсточникиПодразделений = Новый Соответствие;
	
	ИсточникиПодразделений.Вставить(Перечисления.ИсточникиПодразделенийАналитическихРегистров.Заказ, "Подразделение");

	Возврат ИсточникиПодразделений;

КонецФункции

// Определяет источники направлений регистра и их свойства.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиНаправлений() Экспорт

	Результат = Новый Соответствие;
	
	ИсточникиНаправлений = Перечисления.ИсточникиНаправленийДеятельностиАналитическихРегистров;
	Результат.Вставить(ИсточникиНаправлений.НаправлениеДеятельностиКонтрагента, "НаправлениеДеятельностиКонтрагента");

	Возврат Результат;

КонецФункции

// Определяет источники заполнения субконто.
//
// Возвращаемое значение:
//  Массив - массив атрибутов регистра.
//
Функция ИсточникиСубконто() Экспорт

	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить("Партнер");
	МассивСубконто.Добавить("Контрагент");
	МассивСубконто.Добавить("Заказ");
	МассивСубконто.Добавить("Договор");

	Возврат Новый Структура("СубконтоДт, СубконтоКт", МассивСубконто, МассивСубконто);

КонецФункции

// Определяет показатели в валюте регистра.
//
// Параметры:
//  СвойстваПоказателей - Строка - "ИсточникВалюты" - источник валюты для показателя регистра.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиВВалюте(СвойстваПоказателей) Экспорт

	ПоказателиВВалюте = Новый Соответствие;
	
	ПоказателиВВалюте.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаВВалютеВзаиморасчетов, Новый Структура(СвойстваПоказателей, "Валюта"));
	
	Возврат ПоказателиВВалюте;

КонецФункции

// Определяет показатели в валюте регистра.
//
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиКоличества() Экспорт

	ПоказателиКоличества = Новый Соответствие;
	Возврат ПоказателиКоличества;

КонецФункции

//-- НЕ УТКА

//++ НЕ УТ

// Определяет показатели регистра.
//
// Параметры:
//  Свойства - Структура - содержащая ключи СвойстваПоказателей, СвойстваРесурсов
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция Показатели(Свойства) Экспорт

	Показатели = Новый Соответствие;
	
	СвойстваПоказателей = Свойства.СвойстваПоказателей;
	СвойстваРесурсов = Свойства.СвойстваРесурсов;
	
	МассивРесурсов = Новый Массив;
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "ПредоплатаУпр", "ВалютаУпр"));
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "ПредоплатаРегл", "ВалютаРегл"));
    МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "Предоплата", "Валюта"));
	Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.Сумма, Новый Структура(СвойстваПоказателей, МассивРесурсов));
	
	Возврат Показатели;
	
КонецФункции

//-- НЕ УТ

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов по данным временных таблиц.
// 
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Данные временных таблиц будут использованы при формировании новых заданий.
//  ИмяТаблицы - Строка - Имя таблицы в МВТ
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументовПоДаннымВТ(МенеджерВременныхТаблиц, ИмяТаблицы, ВидыЗаданий) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СправочникАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	ДанныеКОбработке.Период КАК ПериодЗадания,
	|	ДанныеКОбработке.ДокументРегистратор КАК Документ
	|ИЗ
	|	ИмяТаблицы КАК ДанныеКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК СправочникАналитикаУчетаПоПартнерам
	|		ПО ДанныеКОбработке.АналитикаУчетаПоПартнерам = СправочникАналитикаУчетаПоПартнерам.Ссылка
	|ГДЕ
	|	НЕ ДанныеКОбработке.ДокументРегистратор ССЫЛКА Документ.РасчетКурсовыхРазниц";
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ИмяТаблицы", ИмяТаблицы);

	ДанныеКОбработке = Запрос.Выполнить().Выгрузить();

	Для Каждого Запись Из ДанныеКОбработке Цикл

		Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

			НоваяСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
			НоваяСтрока.ВидЗадания = ТекущийВидЗадания;
			
		КонецЦикла;

	КонецЦикла;

	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТ

// Определяет показатели регистра
//
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияДвиженийВФинансовомУчете
//
Процедура ОпределитьПоказатели(ПараметрыОтражения)
	
	// Сумма предоплаты
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ПредоплатаУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("ПредоплатаРегл");
	ПараметрыОтраженияПоказателя.РесурсыВал.Добавить("Предоплата");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаПредоплаты, ПараметрыОтраженияПоказателя);
	
	// Сумма постоплаты
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ДолгУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("ДолгРегл");
	ПараметрыОтраженияПоказателя.РесурсыВал.Добавить("Долг");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаПостоплаты, ПараметрыОтраженияПоказателя);
	
КонецПроцедуры

//-- НЕ УТ

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.82";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("489f7d26-6bd5-7943-a55c-3d06d321674a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = СтрШаблон(НСтр("ru = 'Очищает реквизит ""%1"" и исправляет реквизит ""%2"" для движений документов ""%3"".
	                                              |Исправляет реквизит ""%4"" для движений ввода начальных остатков авансов.';
	                                              |en = 'Clears the ""%1"" attribute and corrects the ""%2"" attribute for  ""%3"" document register records.
	                                              |Corrects the ""%4"" attribute for opening prepayment balance entry transfer records.'"),
		Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Реквизиты.ВалютаДокумента.Представление(),
		Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Реквизиты.ПорядокЗачета.Представление(),
		Метаданные.Документы.РасчетКурсовыхРазниц.Представление(),
		Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Реквизиты.ПорядокОперации.Представление());
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.БлокируемыеОбъекты = "";
	
КонецПроцедуры

// Процедура регистрации данных для обработчика обновления ОбработатьДанныеДляПереходаНаВерсию.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра  = "РегистрНакопления.РасчетыСПоставщикамиПоСрокам";

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	#Область ТекстЗапроса
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|ГДЕ
	|	Расчеты.ДокументРегистратор ССЫЛКА Документ.РасчетКурсовыхРазниц
	|	И (Расчеты.ПорядокОперации <> Расчеты.ПорядокЗачета
	|		ИЛИ Расчеты.ВалютаДокумента <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
	|ГДЕ
	|	(Расчеты.ДокументРегистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|		ИЛИ Расчеты.ДокументРегистратор ССЫЛКА Документ.ВводОстатков)
	|	И Расчеты.Период <> Расчеты.ДатаВозникновения
	|	И Расчеты.ТипЗаписиВзаиморасчетов В (ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты),
	|	                                     ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга))
	|	И Расчеты.ДатаВозникновения >= ДАТАВРЕМЯ(2010,1,1)
	|	И (СТРОКА(ГОД(Расчеты.Период) - 2000) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 3, 2)
	|		ИЛИ ВЫБОР КОГДА МЕСЯЦ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 2)
	|				ИНАЧЕ СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 6, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 1) <> ""0""
	|			КОНЕЦ
	|		ИЛИ ВЫБОР КОГДА ДЕНЬ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 2)
	|				ИНАЧЕ СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 8, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 1) <> ""0""
	|			КОНЕЦ)
	|";
	#КонецОбласти
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы,
		ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрНакопления.РасчетыСПоставщикамиПоСрокам";

	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОбновляемыхДанных из ОбновляемыеДанные Цикл
		
		ОбновляемыйРегистратор = СтрокаОбновляемыхДанных.Регистратор;
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			#Область Блокировки
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщикамиПоСрокам.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ОбновляемыйРегистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			#КонецОбласти
			
			ПричинаИсключения = "ПлохиеДанные";
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ОбновляемыйРегистратор);
			НаборЗаписей.Прочитать();

			#Область ИсправлениеДвиженийПереоценки
			Для Каждого Запись Из НаборЗаписей Цикл
				Если ТипЗнч(Запись.ДокументРегистратор) = Тип("ДокументСсылка.РасчетКурсовыхРазниц")
					И (Запись.ПорядокОперации <> Запись.ПорядокЗачета
						ИЛИ Запись.ВалютаДокумента <> Справочники.Валюты.ПустаяСсылка()) Тогда
					Запись.ПорядокЗачета = Запись.ПорядокОперации;
					Запись.ВалютаДокумента = Справочники.Валюты.ПустаяСсылка();
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			#Область ИсправлениеПорядковОперацииВводаОстатковАвансов
			Для Каждого Запись из НаборЗаписей Цикл
				Если (ТипЗнч(Запись.ДокументРегистратор) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов")
						Или ТипЗнч(Запись.ДокументРегистратор) = Тип("ДокументСсылка.ВводОстатков"))
					И Запись.Период <> Запись.ДатаВозникновения
					И (Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУвеличениеПредоплаты
						Или Запись.ТипЗаписиВзаиморасчетов = Перечисления.ТипыЗаписейВзаиморасчетов.ОплатаУменьшениеДолга)
					И Запись.ДатаВозникновения >= Дата(2010, 1, 1)
					И Лев(Запись.ПорядокОперации, 8) <> Формат(Запись.Период, "ДФ=yyyyMMdd") Тогда
					
					ДатаСтрокой = Формат(Запись.Период, "ДФ=yyyyMMdd");
					Запись.ПорядокОперации = ДатаСтрокой + Сред(Запись.ПорядокОперации, 9);
					
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ОбновляемыйРегистратор);
				Если ЗначениеЗаполнено(ОбновляемыйРегистратор) Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
					ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
						ОбновляемыйРегистратор,
						РегистрыНакопления.РасчетыСКлиентами.ТекстСообщенияОбОшибке(ИнформацияОбОшибке(), ОбновляемыйРегистратор));
				КонецЕсли;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли