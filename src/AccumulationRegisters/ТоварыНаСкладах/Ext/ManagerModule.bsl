#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ТоварыНаСкладах.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.19";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("e0ca9ea7-01a0-4ba0-888a-ba5aac55c94f");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ТоварыНаСкладах.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	Комментарий = НСтр("ru = 'Обновляет движения по регистру накопления ""Товары на складах"" для документов ""Акт о расхождениях после приемки"".';
						|en = 'Updates register records in the ""Goods in warehouses"" accumulation register for the ""Receipt discrepancy report"" documents.'");
	
	//++ НЕ УТ
	ТекстКомментария = Новый Массив;
	ТекстКомментария.Добавить(НСтр("ru = 'Обновляет движения по регистру накопления ""Товары на складах"" для документов:';
									|en = 'Updates register records in the ""Goods in warehouses"" accumulation register for the following documents:'"));
	ТекстКомментария.Добавить(НСтр("ru = '- ""Акт о расхождениях после приемки"";';
									|en = '- Receipt discrepancy report'"));
	ТекстКомментария.Добавить(НСтр("ru = '- ""Движение продукции и материалов"".';
									|en = '- Movement of products and materials'"));
	
	Комментарий = СтрСоединить(ТекстКомментария, Символы.ПС);
	//-- НЕ УТ
	
	Обработчик.Комментарий = Комментарий;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.АктОРасхожденияхПослеПриемки.ПолноеИмя());
	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.Документы.ДвижениеПродукцииИМатериалов.ПолноеИмя());
	//-- НЕ УТ
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ТоварыНаСкладах.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыНакопления.ТоварыНаСкладах.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	СписокДокументов	= ДокументыКОбновлению();
	ПолноеИмяРегистра	= Метаданные.РегистрыНакопления.ТоварыНаСкладах.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(СписокДокументов, ",");
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ИмяРегистра = "ТоварыНаСкладах";
	
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.ПолучитьТекстыЗапроса = Истина;
	
	Регистраторы = Новый Массив;
	РегистраторыАктыПослеПриемки = Новый Массив;
	
	Для Каждого ПолноеИмяДокумента Из СписокДокументов Цикл
		ИмяДокумента = СтрРазделить(ПолноеИмяДокумента, ".")[1];
		ТекстыЗапроса = Документы[ИмяДокумента].ДанныеДокументаДляПроведения(Неопределено, ИмяРегистра, ДопПараметры);
		
		ОбщиеВременныеТаблицы = Неопределено;
		Если ПолноеИмяДокумента = "Документ.АктОРасхожденияхПослеПриемки" Тогда
			ОбщиеВременныеТаблицы = Новый Массив();
			Документы.АктОРасхожденияхПослеПриемки.ТекстЗапросаДанныеДокумента(ОбщиеВременныеТаблицы);
			Для Каждого Элемент Из ТекстыЗапроса Цикл
				// Особенность формирования текстов запросов проведения в механике склада.
				// Далее текст не воспринимается механикой перепроведения.
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Товары.Склад)", "Товары.Склад");
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Товары.Назначение)", "Товары.Назначение");
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Товары.Номенклатура)", "Товары.Номенклатура");
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Серии.Склад)", "Серии.Склад");
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Серии.Назначение)", "Серии.Назначение");
				Элемент.Значение = СтрЗаменить(Элемент.Значение, "(Серии.Номенклатура)", "Серии.Номенклатура");
			КонецЦикла;
		КонецЕсли;
		
		РегистраторыПоТипуДокумента = ПроведениеДокументов.РегистраторыДляПерепроведения(
			ТекстыЗапроса,
			ПолноеИмяРегистра,
			ПолноеИмяДокумента,
			ОбщиеВременныеТаблицы);
		
		Если ПолноеИмяДокумента = "Документ.АктОРасхожденияхПослеПриемки" Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(РегистраторыАктыПослеПриемки, РегистраторыПоТипуДокумента);
		Иначе
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Регистраторы, РегистраторыПоТипуДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаДокумента.Ссылка КАК Регистратор
	|ИЗ
	|	Документ.АктОРасхожденияхПослеПриемки КАК ТаблицаДокумента
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеПриемки.Товары КАК ТаблицаТовары
	|		ПО ТаблицаДокумента.Ссылка = ТаблицаТовары.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Проведен
	|	И ТаблицаДокумента.Ссылка В (&Регистраторы)
	|	И ТаблицаТовары.Действие = ЗНАЧЕНИЕ(Перечисление.ВариантыДействийПоРасхождениямВАктеПослеПриемки.ВернутьПерепоставленноеБезОформления)
	|	И ТаблицаДокумента.ТипОснованияАктаОРасхождении = ЗНАЧЕНИЕ(Перечисление.ТипыОснованияАктаОРасхождении.ПриобретениеТоваровУслуг)
	|	И ТаблицаТовары.Склад.ИспользоватьОрдернуюСхемуПриПоступлении
	|	И ТаблицаТовары.Склад.ДатаНачалаОрдернойСхемыПриПоступлении <= ТаблицаДокумента.Дата
	|	И (НЕ ТаблицаТовары.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		ИЛИ (ТаблицаТовары.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|			И ТаблицаТовары.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке > ТаблицаДокумента.Дата))";
	
	Запрос.УстановитьПараметр("Регистраторы", РегистраторыАктыПослеПриемки);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		Регистраторы,
		Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"));
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

// Обработчик обновления.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	СписокДокументов	= ДокументыКОбновлению();
	ПолноеИмяРегистра	= Метаданные.РегистрыНакопления.ТоварыНаСкладах.ПолноеИмя();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазыУТ.ДополнительныеПараметрыПерезаписиДвиженийИзОчереди();
	ДополнительныеПараметры.ОбновляемыеДанные = Параметры.ОбновляемыеДанные;
	
	ОбработкаЗавершена = ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(СписокДокументов,
																						ПолноеИмяРегистра,
																						Параметры.Очередь,
																						ДополнительныеПараметры);
	
	Параметры.ОбработкаЗавершена = ОбработкаЗавершена;
	
КонецПроцедуры

Функция ДокументыКОбновлению()
	
	СписокДокументов = Новый Массив;
	СписокДокументов.Добавить("Документ.АктОРасхожденияхПослеПриемки");
	//++ НЕ УТ
	СписокДокументов.Добавить("Документ.ДвижениеПродукцииИМатериалов");
	//-- НЕ УТ
	
	Возврат СписокДокументов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
