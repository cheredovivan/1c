
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.Организация КАК Организация,
	|	Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.Амортизация КАК Амортизация,
	|	Таблица.АмортизацияРегл КАК АмортизацияРегл,
	|	Таблица.АмортизацияНУ КАК АмортизацияНУ,
	|	Таблица.АмортизацияПР КАК АмортизацияПР,
	|	Таблица.АмортизацияВР КАК АмортизацияВР,
	|	Таблица.АмортизацияЦФ КАК АмортизацияЦФ,
	|	Таблица.АмортизацияНУЦФ КАК АмортизацияНУЦФ,
	|	Таблица.АмортизацияПРЦФ КАК АмортизацияПРЦФ,
	|	Таблица.АмортизацияВРЦФ КАК АмортизацияВРЦФ,
	|	Таблица.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|	Таблица.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл
	|ПОМЕСТИТЬ АмортизацияНМАПередЗаписью
	|ИЗ
	|	РегистрНакопления.АмортизацияНМА КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|		И (Таблица.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
	|		ИЛИ Таблица.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4)";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ) КАК Период,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.НематериальныйАктив КАК НематериальныйАктив,
	|	ИСТИНА КАК ОтражатьВРеглУчете,
	|	ИСТИНА КАК ОтражатьВУпрУчете,
	|	&Регистратор КАК Документ
	|ПОМЕСТИТЬ АмортизацияНМАИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период КАК Период,
	|		Таблица.ВидДвижения КАК ВидДвижения,
	|		Таблица.Организация КАК Организация,
	|		Таблица.НематериальныйАктив КАК НематериальныйАктив,
	|		Таблица.Подразделение КАК Подразделение,
	|		Таблица.ГруппаФинансовогоУчета КАК ГруппаФинансовогоУчета,
	|		Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		Таблица.Амортизация КАК Амортизация,
	|		Таблица.АмортизацияРегл КАК АмортизацияРегл,
	|		Таблица.АмортизацияНУ КАК АмортизацияНУ,
	|		Таблица.АмортизацияПР КАК АмортизацияПР,
	|		Таблица.АмортизацияВР КАК АмортизацияВР,
	|		Таблица.АмортизацияЦФ КАК АмортизацияЦФ,
	|		Таблица.АмортизацияНУЦФ КАК АмортизацияНУЦФ,
	|		Таблица.АмортизацияПРЦФ КАК АмортизацияПРЦФ,
	|		Таблица.АмортизацияВРЦФ КАК АмортизацияВРЦФ,
	|		Таблица.РезервПереоценкиАмортизации КАК РезервПереоценкиАмортизации,
	|		Таблица.РезервПереоценкиАмортизацииРегл КАК РезервПереоценкиАмортизацииРегл
	|	ИЗ
	|		РегистрНакопления.АмортизацияНМА КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И (Таблица.Регистратор ССЫЛКА Документ.АмортизацияНМА2_4
	|		ИЛИ Таблица.Регистратор ССЫЛКА Документ.ВводОстатковВнеоборотныхАктивов2_4)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период,
	|		Таблица.ВидДвижения,
	|		Таблица.Организация,
	|		Таблица.НематериальныйАктив,
	|		Таблица.Подразделение,
	|		Таблица.ГруппаФинансовогоУчета,
	|		Таблица.НаправлениеДеятельности,
	|		-Таблица.Амортизация,
	|		-Таблица.АмортизацияРегл,
	|		-Таблица.АмортизацияНУ,
	|		-Таблица.АмортизацияПР,
	|		-Таблица.АмортизацияВР,
	|		-Таблица.АмортизацияЦФ,
	|		-Таблица.АмортизацияНУЦФ,
	|		-Таблица.АмортизацияПРЦФ,
	|		-Таблица.АмортизацияВРЦФ,
	|		-Таблица.РезервПереоценкиАмортизации,
	|		-Таблица.РезервПереоценкиАмортизацииРегл
	|	ИЗ
	|		АмортизацияНМАПередЗаписью КАК Таблица) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.ВидДвижения,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.НематериальныйАктив,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.ГруппаФинансовогоУчета,
	|	ТаблицаИзменений.НаправлениеДеятельности
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.Амортизация) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияРегл) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияНУ) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияПР) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияВР) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияНУЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияПРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.АмортизацияВРЦФ) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.РезервПереоценкиАмортизации) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.РезервПереоценкиАмортизацииРегл) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ АмортизацияНМАПередЗаписью";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"АмортизацияНМАИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Организация КАК Организация,
		|	НачальныеЗаписи.Подразделение КАК Подразделение,
		|	НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НачальныеЗаписи.НематериальныйАктив КАК НематериальныйАктив,
		|	НачальныеЗаписи.Амортизация КАК Амортизация
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.АмортизацияНМА КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Организация КАК Организация,
		|		НачальныеЗаписи.Подразделение КАК Подразделение,
		|		НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		НачальныеЗаписи.НематериальныйАктив КАК НематериальныйАктив,
		|		НачальныеЗаписи.Амортизация КАК Амортизация
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.Период КАК Период,
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Организация КАК Организация,
		|		Записи.Подразделение КАК Подразделение,
		|		Записи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Записи.НематериальныйАктив КАК НематериальныйАктив,
		|		-Записи.Амортизация КАК Амортизация
		|	ИЗ
		|		РегистрНакопления.АмортизацияНМА КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.Период,
		|	Таблица.Активность,
		|	Таблица.ВидДвижения,
		|	Таблица.Организация,
		|	Таблица.Подразделение,
		|	Таблица.НаправлениеДеятельности,
		|	Таблица.НематериальныйАктив
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Амортизация) <> 0
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект, Неопределено,
			ДанныеКРегистрацииЗаданий);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли