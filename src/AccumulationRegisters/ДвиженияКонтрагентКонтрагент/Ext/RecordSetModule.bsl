#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;

	Если НЕ ДополнительныеСвойства.Свойство("СвойстваДокумента") Тогда
		ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
КонецПроцедуры

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если ДополнительныеСвойства.Свойство("НеРегистрироватьЗаданияНеоперативнойОчереди") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	НачальныеЗаписи.Организация КАК Организация,
		|	НачальныеЗаписи.Подразделение КАК Подразделение,
		|	НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НачальныеЗаписи.Партнер КАК Партнер,
		|	НачальныеЗаписи.Контрагент КАК Контрагент,
		|	НачальныеЗаписи.Договор КАК Договор,
		|	НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|	НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	НачальныеЗаписи.КорПартнер КАК КорПартнер,
		|	НачальныеЗаписи.КорКонтрагент КАК КорКонтрагент,
		|	НачальныеЗаписи.КорДоговор КАК КорДоговор,
		|	НачальныеЗаписи.КорОбъектРасчетов КАК КорОбъектРасчетов,
		|	НачальныеЗаписи.ТипСуммы КАК ТипСуммы,
		|	НачальныеЗаписи.КорТипСуммы КАК КорТипСуммы,
		|	НачальныеЗаписи.Сумма КАК Сумма,
		|	НачальныеЗаписи.СуммаБезНДС КАК СуммаБезНДС,
		|	НачальныеЗаписи.СуммаРегл КАК СуммаРегл,
		|	НачальныеЗаписи.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
		|	НачальныеЗаписи.Валюта КАК Валюта,
		|	НачальныеЗаписи.СуммаВВалюте КАК СуммаВВалюте,
		|	НачальныеЗаписи.СуммаБезНДСВВалюте КАК СуммаБезНДСВВалюте,
		|	НачальныеЗаписи.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	НачальныеЗаписи.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|	НачальныеЗаписи.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
		|	НачальныеЗаписи.КорВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|	НачальныеЗаписи.КорСуммаВВалютеВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|	НачальныеЗаписи.КорСуммаБезНДСВВалютеВзаиморасчетов КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
		|	НачальныеЗаписи.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
		|	НачальныеЗаписи.КорИсточникГФУРасчетов КАК КорИсточникГФУРасчетов,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.ДвиженияКонтрагентКонтрагент КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если ДополнительныеСвойства.Свойство("НеРегистрироватьЗаданияНеоперативнойОчереди") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Регистратор,
		|	Таблица.Период КАК Период,
		|	Таблица.Организация КАК Организация
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		НачальныеЗаписи.Организация КАК Организация,
		|		НачальныеЗаписи.Подразделение КАК Подразделение,
		|		НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		НачальныеЗаписи.Партнер КАК Партнер,
		|		НачальныеЗаписи.Контрагент КАК Контрагент,
		|		НачальныеЗаписи.Договор КАК Договор,
		|		НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		НачальныеЗаписи.КорПартнер КАК КорПартнер,
		|		НачальныеЗаписи.КорКонтрагент КАК КорКонтрагент,
		|		НачальныеЗаписи.КорДоговор КАК КорДоговор,
		|		НачальныеЗаписи.КорОбъектРасчетов КАК КорОбъектРасчетов,
		|		НачальныеЗаписи.ТипСуммы КАК ТипСуммы,
		|		НачальныеЗаписи.КорТипСуммы КАК КорТипСуммы,
		|		НачальныеЗаписи.Сумма КАК Сумма,
		|		НачальныеЗаписи.СуммаБезНДС КАК СуммаБезНДС,
		|		НачальныеЗаписи.СуммаРегл КАК СуммаРегл,
		|		НачальныеЗаписи.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
		|		НачальныеЗаписи.Валюта КАК Валюта,
		|		НачальныеЗаписи.СуммаВВалюте КАК СуммаВВалюте,
		|		НачальныеЗаписи.СуммаБезНДСВВалюте КАК СуммаБезНДСВВалюте,
		|		НачальныеЗаписи.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		НачальныеЗаписи.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|		НачальныеЗаписи.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
		|		НачальныеЗаписи.КорВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|		НачальныеЗаписи.КорСуммаВВалютеВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|		НачальныеЗаписи.КорСуммаБезНДСВВалютеВзаиморасчетов КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
		|		НачальныеЗаписи.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
		|		НачальныеЗаписи.КорИсточникГФУРасчетов КАК КорИсточникГФУРасчетов,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Активность КАК Активность,
		|		Записи.Период КАК Период,
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		Записи.Организация КАК Организация,
		|		Записи.Подразделение КАК Подразделение,
		|		Записи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Записи.Партнер КАК Партнер,
		|		Записи.Контрагент КАК Контрагент,
		|		Записи.Договор КАК Договор,
		|		Записи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		Записи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		Записи.КорПартнер КАК КорПартнер,
		|		Записи.КорКонтрагент КАК КорКонтрагент,
		|		Записи.КорДоговор КАК КорДоговор,
		|		Записи.КорОбъектРасчетов КАК КорОбъектРасчетов,
		|		Записи.ТипСуммы КАК ТипСуммы,
		|		Записи.КорТипСуммы КАК КорТипСуммы,
		|		-Записи.Сумма КАК Сумма,
		|		-Записи.СуммаБезНДС КАК СуммаБезНДС,
		|		-Записи.СуммаРегл КАК СуммаРегл,
		|		-Записи.СуммаРеглБезНДС КАК СуммаРеглБезНДС,
		|		Записи.Валюта КАК Валюта,
		|		Записи.СуммаВВалюте КАК СуммаВВалюте,
		|		Записи.СуммаБезНДСВВалюте КАК СуммаБезНДСВВалюте,
		|		Записи.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		Записи.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
		|		Записи.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаБезНДСВВалютеВзаиморасчетов,
		|		Записи.КорВалютаВзаиморасчетов КАК КорВалютаВзаиморасчетов,
		|		Записи.КорСуммаВВалютеВзаиморасчетов КАК КорСуммаВВалютеВзаиморасчетов,
		|		Записи.КорСуммаБезНДСВВалютеВзаиморасчетов КАК КорСуммаБезНДСВВалютеВзаиморасчетов,
		|		Записи.ИсточникГФУРасчетов КАК ИсточникГФУРасчетов,
		|		Записи.КорИсточникГФУРасчетов КАК КорИсточникГФУРасчетов,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.ДвиженияКонтрагентКонтрагент КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Активность,
		|	Таблица.Период,
		|	Таблица.Регистратор,
		|	Таблица.ХозяйственнаяОперация,
		|	Таблица.Организация,
		|	Таблица.Подразделение,
		|	Таблица.НаправлениеДеятельности,
		|	Таблица.Партнер,
		|	Таблица.Контрагент,
		|	Таблица.Договор,
		|	Таблица.ОбъектРасчетов,
		|	Таблица.КорНаправлениеДеятельности,
		|	Таблица.КорПартнер,
		|	Таблица.КорКонтрагент,
		|	Таблица.КорДоговор,
		|	Таблица.КорОбъектРасчетов,
		|	Таблица.ТипСуммы,
		|	Таблица.КорТипСуммы,
		|	Таблица.Валюта,
		|	Таблица.СуммаВВалюте,
		|	Таблица.СуммаБезНДСВВалюте,
		|	Таблица.ВалютаВзаиморасчетов,
		|	Таблица.СуммаВВалютеВзаиморасчетов,
		|	Таблица.СуммаБезНДСВВалютеВзаиморасчетов,
		|	Таблица.КорВалютаВзаиморасчетов,
		|	Таблица.КорСуммаВВалютеВзаиморасчетов,
		|	Таблица.КорСуммаБезНДСВВалютеВзаиморасчетов,
		|	Таблица.ИсточникГФУРасчетов,
		|	Таблица.КорИсточникГФУРасчетов,
		|	Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Сумма) <> 0 ИЛИ СУММА(Таблица.СуммаБезНДС) <> 0 
		|	ИЛИ СУММА(Таблица.СуммаРегл) <> 0 ИЛИ СУММА(Таблица.СуммаРеглБезНДС) <> 0
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект, Неопределено,
			ДанныеКРегистрацииЗаданий);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
