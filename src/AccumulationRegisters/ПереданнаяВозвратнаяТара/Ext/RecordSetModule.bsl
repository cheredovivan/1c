#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ДокументПередачи КАК ДокументПередачи,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Партнер КАК Партнер,
	|	Таблица.ВидЗапасов КАК ВидЗапасов,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.ПредусмотренЗалог КАК ПредусмотренЗалог,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА 
	|			Таблица.Количество
	|		ИНАЧЕ
	|			-Таблица.Количество
	|	КОНЕЦ КАК КоличествоПередЗаписью,
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА Таблица.Сумма
	|		ИНАЧЕ -Таблица.Сумма
	|	КОНЕЦ КАК СуммаПередЗаписью
	|ПОМЕСТИТЬ ДвиженияПереданнаяВозвратнаяТараПередЗаписью
	|ИЗ
	|	РегистрНакопления.ПереданнаяВозвратнаяТара КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Период                      КАК Период,
	|	ТаблицаИзменений.ДокументПередачи            КАК ДокументПередачи,
	|	ТаблицаИзменений.Номенклатура                КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика              КАК Характеристика,
	|	ТаблицаИзменений.Партнер                     КАК Партнер,
	|	ТаблицаИзменений.ВидЗапасов                  КАК ВидЗапасов,
	|	ТаблицаИзменений.НомерГТД                    КАК НомерГТД,
	|	ТаблицаИзменений.ПредусмотренЗалог           КАК ПредусмотренЗалог,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)  КАК КоличествоИзменение,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)       КАК СуммаИзменение
	|ПОМЕСТИТЬ ДвиженияПереданнаяВозвратнаяТараИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Период                 КАК Период,
	|		Таблица.ДокументПередачи       КАК ДокументПередачи,
	|		Таблица.Номенклатура           КАК Номенклатура,
	|		Таблица.Характеристика         КАК Характеристика,
	|		Таблица.Партнер                КАК Партнер,
	|		Таблица.ВидЗапасов             КАК ВидЗапасов,
	|		Таблица.НомерГТД               КАК НомерГТД,
	|		Таблица.ПредусмотренЗалог      КАК ПредусмотренЗалог,
	|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		Таблица.СуммаПередЗаписью      КАК СуммаИзменение
	|	ИЗ
	|		ДвиженияПереданнаяВозвратнаяТараПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Период              КАК Период,
	|		Таблица.ДокументПередачи    КАК ДокументПередачи,
	|		Таблица.Номенклатура        КАК Номенклатура,
	|		Таблица.Характеристика      КАК Характеристика,
	|		Таблица.Партнер             КАК Партнер,
	|		Таблица.ВидЗапасов          КАК ВидЗапасов,
	|		Таблица.НомерГТД            КАК НомерГТД,
	|		Таблица.ПредусмотренЗалог   КАК ПредусмотренЗалог,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Количество
	|			ИНАЧЕ
	|				Таблица.Количество
	|		КОНЕЦ                      КАК КоличествоИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) ТОГДА
	|				-Таблица.Сумма
	|			ИНАЧЕ
	|				Таблица.Сумма
	|		КОНЕЦ                      КАК СуммаИзменение
	|	ИЗ
	|		РегистрНакопления.ПереданнаяВозвратнаяТара КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.ДокументПередачи,
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Партнер,
	|	ТаблицаИзменений.ВидЗапасов,
	|	ТаблицаИзменений.НомерГТД,
	|	ТаблицаИзменений.ПредусмотренЗалог
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаИзменение) > 0
	|;
	|УНИЧТОЖИТЬ ДвиженияПереданнаяВозвратнаяТараПередЗаписью
	|";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
	Выборка = РезультатЗапроса.Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияПереданнаяВозвратнаяТараИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.ДокументПередачи КАК ДокументПередачи,
		|	НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|	НачальныеЗаписи.ПредусмотренЗалог КАК ПредусмотренЗалог,
		|	НачальныеЗаписи.Сумма КАК Сумма,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.ПереданнаяВозвратнаяТара КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.ДокументПередачи КАК ДокументПередачи
		|ПОМЕСТИТЬ ОчередьДанныеКДопроведениюДокументовУпрБаланс
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.ДокументПередачи КАК ДокументПередачи,
		|		НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|		НачальныеЗаписи.ПредусмотренЗалог КАК ПредусмотренЗалог,
		|		НачальныеЗаписи.Сумма КАК Сумма,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.Период КАК Период,
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.ДокументПередачи КАК ДокументПередачи,
		|		Записи.ВидЗапасов КАК ВидЗапасов,
		|		Записи.ПредусмотренЗалог КАК ПредусмотренЗалог,
		|		-Записи.Сумма КАК Сумма,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.ПереданнаяВозвратнаяТара КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.Период,
		|	Таблица.Активность,
		|	Таблица.ВидДвижения,
		|	Таблица.ДокументПередачи,
		|	Таблица.ВидЗапасов,
		|	Таблица.ПредусмотренЗалог,
		|	Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Сумма) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ДокументПередачи
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		Запрос.Выполнить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект,
			Запрос.МенеджерВременныхТаблиц);
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,
			"ОчередьДанныеКДопроведениюДокументовУпрБаланс");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
