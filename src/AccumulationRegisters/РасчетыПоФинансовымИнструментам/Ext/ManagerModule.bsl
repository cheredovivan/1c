#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	// Проведение
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
		//++ НЕ УТ
		
		//++ Локализация
		Параметры.ПодчиненныеРегистры.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментамНУ);
		//-- Локализация
		
		//-- НЕ УТ
	КонецЕсли;
	
	// Контроль
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Параметры.КонтрольныеРегистрыЗаданий.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
		Параметры.КонтрольныеРегистрыИзменений.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
	КонецЕсли;
	
	// Контроль даты запрета
	Если Свойства.РежимЗаписи <> РежимЗаписиДокумента.Запись Тогда
		Параметры.КонтрольныеРегистрыДатаЗапрета.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам);
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ДвиженияДокумента = МетаданныеДокумента.Движения;

	ТекстыЗапросов = Новый Соответствие();
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам;
	Если ДвиженияДокумента.Содержит(МетаданныеРегистра) Тогда
		ТекстыЗапросов.Вставить(МетаданныеРегистра.ПолноеИмя(),
			ПроведениеДокументов.ТекстСторнирующегоЗапроса(
				МетаданныеРегистра, МетаданныеДокумента));
	КонецЕсли;
			
	Возврат ТекстыЗапросов;
	
КонецФункции

// Процедура формирования движений по регистру.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РасчетыПоФинансовымИнструментам");
	//++ НЕ УТ
	
	//++ Локализация
	ПроведениеДокументов.ОтразитьДвижения(ТаблицыДляДвижений, Движения, "РасчетыПоФинансовымИнструментамНУ");
	//-- Локализация
	
	//-- НЕ УТ
	
КонецПроцедуры

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийРасчетыПоФинансовымИнструментам", 
		РегистрыНакопления.РасчетыПоФинансовымИнструментам.ТекстЗапросаКонтрольДатыЗапрета(Запрос));
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийРасчетыПоФинансовымИнструментам"; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	КлючиАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	&ИмяРегистра КАК ИмяРегистра,
	|	&Раздел КАК РазделДатыЗапрета
	|ИЗ
	|	&ИмяТаблицыИзменений КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|	ПО
	|		Таблица.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	Таблица.Сумма <> 0
	|	ИЛИ Таблица.СуммаУпр <> 0
	|	ИЛИ Таблица.СуммаРегл <> 0
	|";
	
	ИмяПараметраИмяРегистра = "ИмяРегистра" + ИмяРегистра;
	ИмяПараметраРаздел = "Раздел" + ИмяРегистра;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ИмяРегистра", "&" + ИмяПараметраИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&Раздел", "&" + ИмяПараметраРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ИмяТаблицыИзменений", ИмяТаблицыИзменений);
	
	Запрос.УстановитьПараметр(ИмяПараметраИмяРегистра, ИмяРегистра);
	Запрос.УстановитьПараметр(ИмяПараметраРаздел, "ФинансовыйКонтур");
	
	СтруктураТекстовЗапросов = ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	
	Возврат СтруктураТекстовЗапросов

КонецФункции

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРасчетыПоФинансовымИнструментамИзменение") Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаОстатков.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаОстатков.Договор                           КАК Договор,
		|	ТаблицаОстатков.ТипСуммы                          КАК ТипСуммы,
		|	ТаблицаОстатков.РасчетныйДокумент                 КАК РасчетныйДокумент,
		|	ТаблицаОстатков.Валюта                            КАК Валюта,
		|	ТаблицаОстатков.ОплачиваетсяОстаток               КАК Оплачивается,
		|	0                                                 КАК Сумма
		|
		|ИЗ
		|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(,
		|		(АналитикаУчетаПоПартнерам, Договор, ТипСуммы, РасчетныйДокумент, Валюта) В
		|		(ВЫБРАТЬ
		|			Таблица.АналитикаУчетаПоПартнерам,
		|			Таблица.Договор,
		|			Таблица.ТипСуммы,
		|			Таблица.РасчетныйДокумент,
		|			Таблица.Валюта
		|		ИЗ
		|			ДвиженияРасчетыПоФинансовымИнструментамИзменение КАК Таблица
		|		)
		|		) КАК ТаблицаОстатков
		|
		|ГДЕ
		|	ТаблицаОстатков.ОплачиваетсяОстаток > 0
		|
		//++ НЕ УТ
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОстатков.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаОстатков.Договор                           КАК Договор,
		|	ТаблицаОстатков.ТипСуммы                          КАК ТипСуммы,
		|	ТаблицаОстатков.РасчетныйДокумент                 КАК РасчетныйДокумент,
		|	ТаблицаОстатков.Валюта                            КАК Валюта,
		|	0                                                 КАК Оплачивается,
		|	ТаблицаОстатков.СуммаОстаток                      КАК Сумма
		|
		|ИЗ
		|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(,
		|		(АналитикаУчетаПоПартнерам, Договор, ТипСуммы, РасчетныйДокумент, Валюта) В
		|		(ВЫБРАТЬ
		|			Таблица.АналитикаУчетаПоПартнерам,
		|			Таблица.Договор,
		|			Таблица.ТипСуммы,
		|			Таблица.РасчетныйДокумент,
		|			Таблица.Валюта
		|		ИЗ
		|			ДвиженияРасчетыПоФинансовымИнструментамИзменение КАК Таблица
		|		ГДЕ
		|			Таблица.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАренде)
		|			И Таблица.РасчетныйДокумент = НЕОПРЕДЕЛЕНО
		|			И ВЫРАЗИТЬ(Таблица.Договор КАК Справочник.ДоговорыАренды).ВестиУчетАвансовПоТекущимАренднымПлатежамВРазрезеПлатежныхДокументов
		|		)
		|		) КАК ТаблицаОстатков
		|
		|ГДЕ
		|	ТаблицаОстатков.СуммаОстаток > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаОстатков.АналитикаУчетаПоПартнерам         КАК АналитикаУчетаПоПартнерам,
		|	ТаблицаОстатков.Договор                           КАК Договор,
		|	ТаблицаОстатков.ТипСуммы                          КАК ТипСуммы,
		|	ТаблицаОстатков.РасчетныйДокумент                 КАК РасчетныйДокумент,
		|	ТаблицаОстатков.Валюта                            КАК Валюта,
		|	0                                                 КАК Оплачивается,
		|	ТаблицаОстатков.СуммаОстаток                      КАК Сумма
		|
		|ИЗ
		|	РегистрНакопления.РасчетыПоФинансовымИнструментам.Остатки(,
		|		(АналитикаУчетаПоПартнерам, Договор, ТипСуммы, РасчетныйДокумент, Валюта) В
		|		(ВЫБРАТЬ
		|			Таблица.АналитикаУчетаПоПартнерам,
		|			Таблица.Договор,
		|			Таблица.ТипСуммы,
		|			Таблица.РасчетныйДокумент,
		|			Таблица.Валюта
		|		ИЗ
		|			ДвиженияРасчетыПоФинансовымИнструментамИзменение КАК Таблица
		|		ГДЕ
		|			(Таблица.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.УслугаПоАрендеАванс)
		|			ИЛИ Таблица.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыПлатежейПоАренде.ОбеспечительныйПлатеж))
		|			И Таблица.РасчетныйДокумент <> НЕОПРЕДЕЛЕНО
		|		)
		|		) КАК ТаблицаОстатков
		|
		|ГДЕ
		|	ТаблицаОстатков.СуммаОстаток < 0
		//-- НЕ УТ
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса, "ОшибкиРасчетыПоФинансовымИнструментам");
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
	Если ПроведениеДокументов.ЕстьЗаписиВТаблице(Документ, "ДвиженияРасчетыПоФинансовымИнструментамИзменение") Тогда
		
		Для каждого СтрокаОшибки Из РезультатыКонтроля.ОшибкиРасчетыПоФинансовымИнструментам Цикл
			Если СтрокаОшибки.Оплачивается <> 0 Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'По договору %1 оплачивается больше, чем утверждено в заявке, на %2 %3';
												|en = 'Under contract %1 payment is greater than confirmed in the request by %2 %3'"),
					СтрокаОшибки.Договор, СтрокаОшибки.Оплачивается, СтрокаОшибки.Валюта);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
			ИначеЕсли СтрокаОшибки.Сумма > 0
				И СтрокаОшибки.РасчетныйДокумент = Неопределено Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'По договору %1 оплачивается больше, чем указано в документах поступления услуг аренды, на %2 %3';
												|en = 'Under the %1 contract, payments exceed the amount specified in the rental service documents by %2 %3'"),
					СтрокаОшибки.Договор, СтрокаОшибки.Сумма, СтрокаОшибки.Валюта);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
			ИначеЕсли СтрокаОшибки.Сумма < 0
				И СтрокаОшибки.РасчетныйДокумент <> Неопределено Тогда 
				ТекстСообщения = СтрШаблон(НСтр("ru = 'По документу %1 превышена сумма зачета аванса на %2 %3';
												|en = 'Under the %1 document, the prepayment offset amount is exceeded by %2 %3'"),
					СтрокаОшибки.РасчетныйДокумент, -СтрокаОшибки.Сумма, СтрокаОшибки.Валюта);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Документ,,, Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	ПараметрыОтражения.ПутьКДаннымОрганизация = "АналитикаУчетаПоПартнерам.Организация";
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ВыделениеДолгосрочныхАктивовОбязательств = Истина;
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "Договор.ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "Договор.НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "Договор.Подразделение";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Валюта";
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("Сумма");
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Контрагенты;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.Договор.ПутьКДанным = "Договор";
	ПараметрыОтражения.СтруктураАналитики.ОбъектРасчетов.Вставить("Значение", Неопределено);
	ПараметрыОтражения.ПутьКДаннымДопНастройкаХозОперации = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты)
		|			ТОГДА ВЫБОР
		|				КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствПоЗаймамВыданным)
		|					ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеПроцентовПоЗаймамВыданным)
		|				КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОплатаПоКредитам)
		|					ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОплатаПроцентовПоКредитам)
		|				ИНАЧЕ ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации
		|			КОНЕЦ
		|		КОГДА ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации = ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеНДСВЧастиУслугиПоАренде)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ОтражениеУслугПоАрендеВРасходах)
		|		ИНАЧЕ ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	
	//-- НЕ УТ
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();

	Если ВидыЗаданий.Найти(Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) <> Неопределено Тогда

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;

		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникАналитикаУчетаПоПартнерам.Организация КАК Организация,
		|	ДанныеКОбработке.ПериодЗадания КАК ПериодЗадания,
		|	ДанныеКОбработке.Документ КАК Документ,
		|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
		|ИЗ
		|	ОчередьДанныеКДопроведениюДокументовУпрБаланс КАК ДанныеКОбработке
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК СправочникАналитикаУчетаПоПартнерам
		|		ПО ДанныеКОбработке.АналитикаУчетаПоПартнерам = СправочникАналитикаУчетаПоПартнерам.Ссылка";

		ТаблицаДанных = Запрос.Выполнить().Выгрузить();

		Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда

				Возврат ТаблицаДанных;

			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыПоФинансовымИнструментам.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.71";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("51119c20-5762-5c1b-bdf7-3f0a1da27bec");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыПоФинансовымИнструментам.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Обновление регистра ""Расчеты по финансовым инструментам"":';
								|en = 'Update the ""Financial instruments AR/AP"" register:'"));
	СписокОписаний.Добавить(НСтр("ru = '- Поле ""Аналитика учета по партнерам"" очищается от направления деятельности';
								|en = '- The ""Accounting dimension by partners"" field is cleared of the line of business'"));
	
	Обработчик.Комментарий = СтрСоединить(СписокОписаний, Символы.ПС);
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.КлючиАналитикиУчетаПоПартнерам.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрНакопления.РасчетыПоФинансовымИнструментам";
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистр.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК Регистр
	|ГДЕ
	|	Регистр.АналитикаУчетаПоПартнерам.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), ПараметрыВыборки.ПолныеИменаРегистров);
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	ПолноеИмяОбъекта = "РегистрНакопления.РасчетыПоФинансовымИнструментам";
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Ключи.Ссылка КАК НоваяАналитикаУчетаПоПартнерам,
		|	Расчеты.АналитикаУчетаПоПартнерам.Организация КАК Организация,
		|	Расчеты.АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|	Расчеты.АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|	Расчеты.АналитикаУчетаПоПартнерам.Договор КАК Договор,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
		|ИЗ
		|	РегистрНакопления.РасчетыПоФинансовымИнструментам КАК Расчеты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
		|			ПО Ключи.Организация = Расчеты.АналитикаУчетаПоПартнерам.Организация
		|			И Ключи.Партнер = Расчеты.АналитикаУчетаПоПартнерам.Партнер
		|			И Ключи.Контрагент = Расчеты.АналитикаУчетаПоПартнерам.Контрагент
		|			И Ключи.Договор = Расчеты.АналитикаУчетаПоПартнерам.Договор
		|			И Ключи.НаправлениеДеятельности = ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
		|ГДЕ
		|	Расчеты.Регистратор = &Ссылка
		|";
		
	// Ключ старая аналитика, значение - новая
	Соответствие = Новый Соответствие();
	
	Для Каждого СтрокаДанных Из ОбновляемыеДанные Цикл
		
		ПричинаИсключения = "";
		Рекомендация = "";
		
		НачатьТранзакцию();
		Попытка
			
			ПричинаИсключения = "Блокировка";
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоФинансовымИнструментам.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаДанных.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			ПричинаИсключения = "ПлохиеДанные"; 
			Рекомендация = НСтр("ru = 'Перепроведите документ вручную.';
								|en = 'Repost the document manually.'");
			
			НаборЗаписей = РегистрыНакопления.РасчетыПоФинансовымИнструментам.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(СтрокаДанных.Регистратор);
			НаборЗаписей.Прочитать();
			
			Запрос.УстановитьПараметр("Ссылка", СтрокаДанных.Регистратор);
			ДанныеАналитики = Запрос.Выполнить().Выгрузить();
			
			//АналитикаУчетаПоПартнерам
			Для Каждого Запись Из НаборЗаписей Цикл
				Если Соответствие.Получить(Запись.АналитикаУчетаПоПартнерам) = Неопределено Тогда
					ДанныеКлючаАналитики = ДанныеАналитики.НайтиСтроки(Новый Структура("АналитикаУчетаПоПартнерам",Запись.АналитикаУчетаПоПартнерам));
					Если ДанныеКлючаАналитики.Количество() > 0 Тогда
						Если ЗначениеЗаполнено(ДанныеКлючаАналитики[0].НоваяАналитикаУчетаПоПартнерам) Тогда
							НовыйКлюч = ДанныеКлючаАналитики[0].НоваяАналитикаУчетаПоПартнерам;
						Иначе
							НовыйКлюч = РегистрыСведений.АналитикаУчетаПоПартнерам.СоздатьКлючАналитики(ДанныеКлючаАналитики[0])
						КонецЕсли;
						Соответствие.Вставить(Запись.АналитикаУчетаПоПартнерам,НовыйКлюч);
						Запись.АналитикаУчетаПоПартнерам = НовыйКлюч;
					КонецЕсли;
				Иначе
					Запись.АналитикаУчетаПоПартнерам = Соответствие[Запись.АналитикаУчетаПоПартнерам];
				КонецЕсли;
			КонецЦикла;
			
			ПричинаИсключения = "Запись";
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), СтрокаДанных.Регистратор);
			
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					СтрокаДанных.Регистратор, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = "Запись" Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(НСтр("ru = 'Не удалось обработать набор записей регистра накопления ""Расчеты по финансовым инструментам"".';
													|en = 'Cannot process the ""Financial instruments AR/AP"" accumulation register record set.'"), Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано > 0 Тогда
		ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
