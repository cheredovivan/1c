#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
//++ НЕ УТ

//++ НЕ УТКА

// Процедуры и функции используемые механизмом отражения в МФУ.

// Определяет источники уточнения счета, доступные в регистре и их свойства.
//
// Параметры:
//  СвойстваИсточника - Строка - "ИмяПоля" - имя атрибута регистра накопления, из которого планируется получать источник уточнения счета.
//
// Возвращаемое значение:
//  Соответствие - Ключ - название источника уточнения счета. 
//                 Значение - структура свойств источника уточнения счета.
//
Функция ИсточникиУточненияСчета(СвойстваИсточника) Экспорт
	
	ИсточникиУточненияСчета = Новый Соответствие;
	
	// Источники уточнения не доступны.
	
	Возврат ИсточникиУточненияСчета;
	
КонецФункции

// Определяет источники подразделений регистра и их свойства.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиПодразделений() Экспорт

	ИсточникиПодразделений = Новый Соответствие;
	
	ИсточникиПодразделений.Вставить(Перечисления.ИсточникиПодразделенийАналитическихРегистров.ДокументОплаты, "ДокументОплатыПодразделение");
	ИсточникиПодразделений.Вставить(Перечисления.ИсточникиПодразделенийАналитическихРегистров.ДоговорКонтрагента, "ДоговорКонтрагентаПодразделение");
	
    Возврат ИсточникиПодразделений;
	
КонецФункции

// Определяет источники направлений регистра и их свойства.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя источника. 
//                 Значение - структура свойств источника. 
//
Функция ИсточникиНаправлений() Экспорт

	Результат = Новый Соответствие;
	
	ИсточникиНаправлений = Перечисления.ИсточникиНаправленийДеятельностиАналитическихРегистров;
	Результат.Вставить(ИсточникиНаправлений.НаправлениеДеятельности, "НаправлениеДеятельности");

	Возврат Результат;

КонецФункции

// Определяет источники заполнения субконто.
//
// Возвращаемое значение:
//  Массив - массив атрибутов регистра.
//
Функция ИсточникиСубконто() Экспорт

	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить("Покупатель");
	МассивСубконто.Добавить("СтавкаНДС");

	Возврат Новый Структура("СубконтоДт, СубконтоКт", МассивСубконто, МассивСубконто);
	
КонецФункции

// Определяет показатели в валюте регистра.
//
// Параметры:
//  СвойстваПоказателей - Строка - "ИсточникВалюты" - источник валюты для показателя регистра.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиВВалюте(СвойстваПоказателей) Экспорт

	ПоказателиВВалюте = Новый Соответствие;
	
	// Показатели в валюте не предусмотрены.
	
	Возврат ПоказателиВВалюте;

КонецФункции

// Определяет показатели в валюте регистра.
//
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция ПоказателиКоличества() Экспорт

	ПоказателиКоличества = Новый Соответствие;
	Возврат ПоказателиКоличества;

КонецФункции

//-- НЕ УТКА

// Определяет показатели регистра.
//
// Параметры:
//  Свойства - Структура - содержащая ключи СвойстваПоказателей, СвойстваРесурсов
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция Показатели(Свойства) Экспорт

	Показатели = Новый Соответствие;
	
	СвойстваПоказателей = Свойства.СвойстваПоказателей;
	СвойстваРесурсов = Свойства.СвойстваРесурсов;
	
	МассивРесурсов = Новый Массив;
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "НДСУпр", "ВалютаУпр"));
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "НДС", "ВалютаРегл"));
	
	Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаНДС, Новый Структура(СвойстваПоказателей, МассивРесурсов));
	
	Возврат Показатели;
	
КонецФункции
//-- НЕ УТ

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

//++ НЕ УТ

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.НДСЗаписиКнигиПродаж.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("de97bc49-9d7a-47a9-a0ec-a9afbf8c581d");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.НДСЗаписиКнигиПродаж.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий = НСтр("ru = 'Заполняет реквизит Код операции НДС 0% из регистра сведений Сведения о таможенных декларациях на экспорт';
									|en = 'Fills the ""0% VAT transaction code"" attribute from the ""Information of customs declarations for export"" information register'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.СведенияТаможенныхДекларацийЭкспорт.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.НДСЗаписиКнигиПродаж.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра	= "РегистрНакопления.НДСЗаписиКнигиПродаж";
	ПараметрыВыборки = Параметры.ПараметрыВыборки; // см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки КАК Ссылка
	|ИЗ
	|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|		ПО СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки = НДСЗаписиКнигиПродаж.Регистратор
	|ГДЕ
	|	СведенияТаможенныхДекларацийЭкспорт.КодОперации <> """"
	|	И НДСЗаписиКнигиПродаж.КодОперации0 = """"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СчетФактураНаНеподтвержденнуюРеализацию0.Ссылка КАК Ссылка
	|ИЗ
	|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураНаНеподтвержденнуюРеализацию0 КАК СчетФактураНаНеподтвержденнуюРеализацию0
	|		ПО СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки = СчетФактураНаНеподтвержденнуюРеализацию0.ДокументОснование
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.НДСЗаписиКнигиПродаж КАК НДСЗаписиКнигиПродаж
	|		ПО СчетФактураНаНеподтвержденнуюРеализацию0.Ссылка = НДСЗаписиКнигиПродаж.Регистратор
	|ГДЕ
	|	СведенияТаможенныхДекларацийЭкспорт.КодОперации <> """"
	|	И НДСЗаписиКнигиПродаж.КодОперации0 = """"";
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(Параметры, Регистраторы, ПолноеИмяРегистра);
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыНакопления.НДСЗаписиКнигиПродаж;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Не удалось заполнить код операции НДС 0% в регистре накопления ""НДС Записи книги продаж"".';
								|en = 'Cannot fill the 0% VAT transaction code in the ""VAT sales ledger entries"" accumulation register.'"));
	
	Для Каждого СтрокаТаблицы Из ОбновляемыеДанные Цикл
		
		ПроблемныйРегистратор = СтрокаТаблицы.Регистратор;
		ПричинаИсключения = "";
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ПроблемныйРегистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
				
			ПричинаИсключения = "ПлохиеДанные";
			Рекомендация = НСтр("ru = 'Подтвердите ставку 0% в документе ""Подтверждение ставки 0%"".';
								|en = 'Confirm the 0% rate in the ""Confirm 0% rate"" document.'");
			
			НаборЗаписей = РегистрыНакопления.НДСЗаписиКнигиПродаж.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ПроблемныйРегистратор);
			НаборЗаписей.Прочитать();
						
			Для Каждого Запись Из НаборЗаписей Цикл
				Если Запись.Событие = Перечисления.СобытияПоНДСПродажи.ПодтвержденаСтавка0 Тогда
					ЗапросРеквизита = Новый Запрос(
						"ВЫБРАТЬ ПЕРВЫЕ 1
						|	СведенияТаможенныхДекларацийЭкспорт.КодОперации КАК КодОперации
						|ИЗ
						|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
						|ГДЕ
						|	СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки = &ДокументОтгрузки");
					ЗапросРеквизита.УстановитьПараметр("ДокументОтгрузки", ПроблемныйРегистратор);
					Запись.КодОперации0 = ЗапросРеквизита.Выполнить().Выгрузить()[0].КодОперации;
				ИначеЕсли Запись.Событие = Перечисления.СобытияПоНДСПродажи.НеПодтвержденаСтавка0 Тогда
					ЗапросРеквизита = Новый Запрос(
						"ВЫБРАТЬ ПЕРВЫЕ 1
						|	СведенияТаможенныхДекларацийЭкспорт.КодОперации КАК КодОперации
						|ИЗ
						|	РегистрСведений.СведенияТаможенныхДекларацийЭкспорт КАК СведенияТаможенныхДекларацийЭкспорт
						|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СчетФактураНаНеподтвержденнуюРеализацию0 КАК СчетФактураНаНеподтвержденнуюРеализацию0
						|		ПО СведенияТаможенныхДекларацийЭкспорт.ДокументОтгрузки = СчетФактураНаНеподтвержденнуюРеализацию0.ДокументОснование
						|ГДЕ
						|	СчетФактураНаНеподтвержденнуюРеализацию0.Ссылка = &СчетФактураНаНеподтвержденнуюРеализацию0");
					ЗапросРеквизита.УстановитьПараметр("СчетФактураНаНеподтвержденнуюРеализацию0", ПроблемныйРегистратор);
					Запись.КодОперации0 = ЗапросРеквизита.Выполнить().Выгрузить()[0].КодОперации;
				КонецЕсли;
			КонецЦикла;
			
			ПричинаИсключения = "Запись";
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ПроблемныйРегистратор);
			
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				
				ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(ПроблемныйРегистратор, Рекомендация, Параметры);
				
			ИначеЕсли ПричинаИсключения = "Запись" Тогда
				
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);

КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
