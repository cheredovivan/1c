#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросовДляПолученияТаблицыИзменений = 
		ЗакрытиеМесяцаСервер.ТекстыЗапросовДляПолученияТаблицыИзмененийРегистра(Метаданные(), Отбор);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиНачальныхДанных;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("ТекстВыборкиТаблицыИзменений",
		ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиТаблицыИзменений);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ДополнительныеСвойства.ТекстВыборкиТаблицыИзменений;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|	НачальныеЗаписи.Валюта КАК Валюта,
		|	НачальныеЗаписи.РасчетныйДокумент КАК РасчетныйДокумент,
		|	НачальныеЗаписи.ТипСуммы КАК ТипСуммы,
		|	НачальныеЗаписи.Сумма КАК Сумма,
		|	НачальныеЗаписи.СуммаУпр КАК СуммаУпр,
		|	НачальныеЗаписи.СуммаРегл КАК СуммаРегл,
		|	НачальныеЗаписи.ПостояннаяРазница КАК ПостояннаяРазница,
		|	НачальныеЗаписи.ВременнаяРазница КАК ВременнаяРазница,
		|	НачальныеЗаписи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	НачальныеЗаписи.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	НачальныеЗаписи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	НачальныеЗаписи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.РезервыПоСомнительнымДолгам КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	СУММА(Таблица.СуммаУпр) КАК СуммаУпр
		|ПОМЕСТИТЬ ОчередьДанныеКДопроведениюДокументов
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		НачальныеЗаписи.Валюта КАК Валюта,
		|		НачальныеЗаписи.РасчетныйДокумент КАК РасчетныйДокумент,
		|		НачальныеЗаписи.ТипСуммы КАК ТипСуммы,
		|		НачальныеЗаписи.Сумма КАК Сумма,
		|		НачальныеЗаписи.СуммаУпр КАК СуммаУпр,
		|		НачальныеЗаписи.СуммаРегл КАК СуммаРегл,
		|		НачальныеЗаписи.ПостояннаяРазница КАК ПостояннаяРазница,
		|		НачальныеЗаписи.ВременнаяРазница КАК ВременнаяРазница,
		|		НачальныеЗаписи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		НачальныеЗаписи.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		НачальныеЗаписи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|		НачальныеЗаписи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Период КАК Период,
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		Записи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		Записи.Валюта КАК Валюта,
		|		Записи.РасчетныйДокумент КАК РасчетныйДокумент,
		|		Записи.ТипСуммы КАК ТипСуммы,
		|		-Записи.Сумма КАК Сумма,
		|		-Записи.СуммаУпр КАК СуммаУпр,
		|		-Записи.СуммаРегл КАК СуммаРегл,
		|		-Записи.ПостояннаяРазница КАК ПостояннаяРазница,
		|		-Записи.ВременнаяРазница КАК ВременнаяРазница,
		|		Записи.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|		Записи.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		Записи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|		Записи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.РезервыПоСомнительнымДолгам КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|		Таблица.Активность,
		|		Таблица.ВидДвижения,
		|		Таблица.Период,
		|		Таблица.Регистратор,
		|		Таблица.АналитикаУчетаПоПартнерам,
		|		Таблица.ОбъектРасчетов,
		|		Таблица.Валюта,
		|		Таблица.РасчетныйДокумент,
		|		Таблица.ТипСуммы,
		|		Таблица.ХозяйственнаяОперация,
		|		Таблица.ИдентификаторСтроки,
		|		Таблица.НастройкаХозяйственнойОперации,
		|		Таблица.ИдентификаторФинЗаписи,
		|		Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Сумма) <> 0 ИЛИ СУММА(Таблица.СуммаУпр) <> 0 ИЛИ СУММА(Таблица.СуммаРегл) <> 0
		|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0 ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		Запрос.Выполнить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект,
			Запрос.МенеджерВременныхТаблиц);
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,
			"ОчередьДанныеКДопроведениюДокументов");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
