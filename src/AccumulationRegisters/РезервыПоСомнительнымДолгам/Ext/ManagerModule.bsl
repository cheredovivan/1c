#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.РезервыПоСомнительнымДолгам.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийРезервыПоСомнительнымДолгам"; 
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	КлючиАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	&ИмяРегистра КАК ИмяРегистра,
	|	&Раздел КАК РазделДатыЗапрета
	|ИЗ
	|	&ИмяТаблицыИзменений КАК Таблица
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаПоПартнерам КАК КлючиАналитикаУчетаПоПартнерам
	|	ПО
	|		Таблица.АналитикаУчетаПоПартнерам = КлючиАналитикаУчетаПоПартнерам.КлючАналитики
	|";
	
	ИмяПараметраИмяРегистра = "ИмяРегистра" + ИмяРегистра;
	ИмяПараметраРаздел = "Раздел" + ИмяРегистра;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ИмяРегистра", "&" + ИмяПараметраИмяРегистра);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&Раздел", "&" + ИмяПараметраРаздел);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"&ИмяТаблицыИзменений", ИмяТаблицыИзменений);
	
	Запрос.УстановитьПараметр(ИмяПараметраИмяРегистра, ИмяРегистра);
	Запрос.УстановитьПараметр(ИмяПараметраРаздел, "ФинансовыйКонтур");
	
	СтруктураТекстовЗапросов = ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	
	Возврат СтруктураТекстовЗапросов

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	ПараметрыОтражения.ПутьКДаннымОрганизация = "АналитикаУчетаПоПартнерам.Организация";
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(ПсевдонимИсточникаДанных.ОбъектРасчетов) = ТИП(Справочник.ОбъектыРасчетов)
		|		И ПсевдонимИсточникаДанных.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ПсевдонимИсточникаДанных.ОбъектРасчетов КАК Справочник.ОбъектыРасчетов).ГруппаФинансовогоУчета
		|		КОГДА ТИПЗНАЧЕНИЯ(ПсевдонимИсточникаДанных.ОбъектРасчетов) = ТИП(Справочник.ДоговорыАренды)
		|		И ПсевдонимИсточникаДанных.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ДоговорыАренды.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ПсевдонимИсточникаДанных.ОбъектРасчетов КАК Справочник.ДоговорыАренды).ГруппаФинансовогоУчета
		|		КОГДА ТИПЗНАЧЕНИЯ(ПсевдонимИсточникаДанных.ОбъектРасчетов) = ТИП(Справочник.ДоговорыКредитовИДепозитов)
		|		И ПсевдонимИсточникаДанных.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ДоговорыКредитовИДепозитов.ПустаяСсылка)
		|			ТОГДА ВЫРАЗИТЬ(ПсевдонимИсточникаДанных.ОбъектРасчетов КАК Справочник.ДоговорыКредитовИДепозитов).ГруппаФинансовогоУчета
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "ОбъектРасчетов.НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Валюта";
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("Сумма");
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Контрагенты;
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();

	Если Не МенеджерВременныхТаблиц = Неопределено 
		И МенеджерВременныхТаблиц.Таблицы.Найти("ОчередьДанныеКДопроведениюДокументов") <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеКОбработке.ПериодЗадания КАК ПериодЗадания,
		|	ДанныеКОбработке.Документ КАК Документ,
		|	ДанныеКОбработке.СуммаУпр КАК СуммаУпр,
		|	СправочникАналитикаУчетаПоПартнерам.Организация КАК Организация
		|ИЗ
		|	ОчередьДанныеКДопроведениюДокументов КАК ДанныеКОбработке
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК СправочникАналитикаУчетаПоПартнерам
		|		ПО ДанныеКОбработке.АналитикаУчетаПоПартнерам = СправочникАналитикаУчетаПоПартнерам.Ссылка";

		ДанныеКОбработке = Запрос.Выполнить().Выгрузить();

		Для Каждого Запись Из ДанныеКОбработке Цикл

			Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

				Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда

					Если Запись.СуммаУпр <> 0 Тогда
						НоваяСтрока = ТаблицаДанных.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
						НоваяСтрока.ВидЗадания = ТекущийВидЗадания;
					КонецЕсли;

				КонецЕсли;

				Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияКонтрагентКонтрагент Тогда
					Если ТипЗнч(Запись.Документ) = Тип("ДокументСсылка.КорректировкаЗадолженности") Тогда
						НоваяСтрока = ТаблицаДанных.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
						НоваяСтрока.ВидЗадания = ТекущийВидЗадания;
					КонецЕсли;
				КонецЕсли;

			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;

	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
