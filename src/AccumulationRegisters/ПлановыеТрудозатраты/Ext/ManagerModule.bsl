#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция возвращает текст запроса формирования временной таблицы Расценки, с актуальными расценками по трудозатратам,
//		по каждой дате сформированных трудозатрат.
// Параметры:
//	ЗапросПоОднойПлановойКалькуляции - Булево - признак определяющий - текст запроса нужен для одной плановой калькуляции или для множества.
//
// Возвращаемое значение:
//	Строка - сформированный текст запроса по актуальным расценкам трудозатрат.
//
Функция ТекстЗапросаРасценкиТрудозатрат(ЗапросПоОднойПлановойКалькуляции) Экспорт

	МассивТекстовЗапроса = Новый Массив();
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ПлановыеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ПлановыеТрудозатраты.ВидРабот.Должность КАК Должность,
		|	ПлановыеТрудозатраты.ВидРабот.РасценкаПоТарифнойСетке КАК РасценкаПоТарифнойСетке,
		|	ПлановыеТрудозатраты.ВидРабот.КвалификационныйРазряд КАК КвалификационныйРазряд,
		|	ПлановыеТрудозатраты.ВидРабот.Трудоемкость КАК Трудоемкость,
		|	ПлановыеТрудозатраты.ВидРабот.КратностьТрудоемкости КАК КратностьТрудоемкости,
		|	ПлановыеТрудозатраты.ВидРабот.РасценкаЗаЧасРаботы КАК РасценкаЗаЧасРаботы,
		|	ПлановыеТрудозатраты.Подразделение КАК Подразделение,
		|	ПлановыеТрудозатраты.Калькуляция КАК Калькуляция,
		|	ПлановыеТрудозатраты.Калькуляция.Организация КАК Организация,
		|	ПлановыеТрудозатраты.Период КАК Период
		|ПОМЕСТИТЬ ВидыРабот
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты";
		
	Если ЗапросПоОднойПлановойКалькуляции Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ ПлановыеТрудозатраты.Калькуляция = &Калькуляция";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ ПлановыеТрудозатраты.Калькуляция В (&МассивПлановыхКалькуляций)";
	КонецЕсли;
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	//++ Локализация
	НормированиеИРасценкаРаботПоТарифнымСеткам = ПолучитьФункциональнуюОпцию("НормированиеИРасценкаРаботПоТарифнымСеткам");
	ИспользоватьТарифныеСеткиПриРасчетеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты");
	ТекстЗапроса_РасценкиПоТарифнымСеткам = "";
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ТекстЗапроса = "ВЫБРАТЬ
		|	ВидыРабот.Калькуляция КАК Калькуляция,
		|	ВидыРабот.Подразделение КАК Подразделение,
		|	ВидыРабот.Организация КАК Организация,
		|	ВидыРабот.ВидРабот КАК ВидРабот,
		|	ВидыРабот.КвалификационныйРазряд КАК КвалификационныйРазряд,
		|	ВидыРабот.Период КАК Период,
		|	ЗначенияТарифов.РазрядКатегория КАК РазрядКатегория,
		|	ЗначенияТарифов.ТарифнаяСетка КАК ТарифнаяСетка,
		|	МАКСИМУМ(ЗначенияТарифов.Период) КАК ПериодРасценки,
		|	МАКСИМУМ(ВидыРабот.Трудоемкость) КАК Трудоемкость,
		|	МАКСИМУМ(ВидыРабот.КратностьТрудоемкости) КАК КратностьТрудоемкости
		|ПОМЕСТИТЬ ДатыРасценокТарифныхСеток
		|ИЗ
		|	ВидыРабот КАК ВидыРабот
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
		|			ПО ВидыРабот.Должность = Должности.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|			ПО Должности.Ссылка = ШтатноеРасписание.Должность
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МестоПозицииШтатногоРасписанияВСтруктуреПредприятия КАК ПодразделениеШР
		|			ПО ШтатноеРасписание.Ссылка = ПодразделениеШР.Позиция
		|				И ВидыРабот.Подразделение = ПодразделениеШР.Подразделение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияТарифов КАК ЗначенияТарифов
		|			ПО ЗначенияТарифов.ТарифнаяСетка = ШтатноеРасписание.ТарифнаяСетка
		|				И (ЗначенияТарифов.РазрядКатегория = ВидыРабот.КвалификационныйРазряд)
		|				И ВидыРабот.Период >= ЗначенияТарифов.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРабот.Калькуляция,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.КвалификационныйРазряд,
		|	ЗначенияТарифов.РазрядКатегория,
		|	ЗначенияТарифов.ТарифнаяСетка,
		|	ВидыРабот.Период,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ТарифнаяСетка,
		|	КвалификационныйРазряд,
		|	ПериодРасценки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДатыРасценок.Калькуляция КАК Калькуляция,
		|	ДатыРасценок.Подразделение КАК Подразделение,
		|	ДатыРасценок.Организация КАК Организация,
		|	ДатыРасценок.ВидРабот КАК ВидРабот,
		|	ДатыРасценок.Период КАК Период,
		|	МИНИМУМ(ЗначенияТарифов.Тариф
		|		* ДатыРасценок.Трудоемкость / ДатыРасценок.КратностьТрудоемкости) КАК Расценка
		|
		|ПОМЕСТИТЬ РасценкиПоТарифнымСеткам
		|ИЗ
		|	ДатыРасценокТарифныхСеток КАК ДатыРасценок
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияТарифов КАК ЗначенияТарифов
		|		ПО ЗначенияТарифов.ТарифнаяСетка = ДатыРасценок.ТарифнаяСетка
		|			И (ЗначенияТарифов.РазрядКатегория = ДатыРасценок.КвалификационныйРазряд)
		|			И ЗначенияТарифов.Период = ДатыРасценок.ПериодРасценки
		|
		|СГРУППИРОВАТЬ ПО
		|	ДатыРасценок.Калькуляция,
		|	ДатыРасценок.ВидРабот,
		|	ДатыРасценок.Период,
		|	ДатыРасценок.Подразделение,
		|	ДатыРасценок.Организация
		|		
		|ИМЕЮЩИЕ
		|	МИНИМУМ(ЗначенияТарифов.Тариф) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Калькуляция,
		|	Организация,
		|	Подразделение,
		|	ВидРабот,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДатыРасценокТарифныхСеток
		|";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		ТекстЗапроса_РасценкиПоТарифнымСеткам = "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДД.Калькуляция КАК Калькуляция,
			|	ДД.Подразделение КАК Подразделение,
			|	ДД.Организация КАК Организация,
			|	ДД.ВидРабот КАК ВидРабот,
			|	ДД.Период КАК Период,
			|	ДД.Расценка КАК Расценка
			|
			|ИЗ
			|	РасценкиПоТарифнымСеткам КАК ДД
			|";
	КонецЕсли;
	
	ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам = "";
	Если НормированиеИРасценкаРаботПоТарифнымСеткам Тогда
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ВидыРабот.Период КАК ПериодВидаРабот,
		|	ВидыРабот.Организация КАК Организация,
		|	ВидыРабот.Подразделение КАК Подразделение,
		|	ВидыРабот.Должность КАК Должность,
		|	ВидыРабот.КвалификационныйРазряд КАК Разряд,
		|	МАКСИМУМ(ТарифныеСтавки.Период) КАК ПериодРасценки
		|ПОМЕСТИТЬ НРРТС_ДатыРасценок
		|ИЗ
		|	ВидыРабот КАК ВидыРабот
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО ВидыРабот.Должность = ТарифныеСтавки.Должность
		|			И (ВидыРабот.КвалификационныйРазряд = ТарифныеСтавки.Разряд)
		|			И ВидыРабот.Период >= ТарифныеСтавки.Период
		|
		|			И (
		|				(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.БезДетализации)
		|					И ТарифныеСтавки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|					И ТарифныеСтавки.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|				)
		|				ИЛИ
		|					(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация)
		|						И ТарифныеСтавки.Организация = ВидыРабот.Организация)
		|				ИЛИ
		|					(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|						И ТарифныеСтавки.Подразделение = ВидыРабот.Подразделение)
		|			)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРабот.Период,
		|	ВидыРабот.Организация,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.Должность,
		|	ВидыРабот.КвалификационныйРазряд
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Подразделение,
		|	Должность,
		|	Разряд,
		|	ПериодВидаРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыРабот.Период КАК ПериодВидаРабот,
		|	ВидыРабот.Должность КАК Должность,
		|	ВидыРабот.КвалификационныйРазряд КАК Разряд,
		|	МАКСИМУМ(ТарифныеСтавки.Период) КАК ПериодРасценки
		|ПОМЕСТИТЬ НРРТС_ДатыОбщихРасценок
		|ИЗ
		|	ВидыРабот КАК ВидыРабот
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|		ПО ВидыРабот.Должность = ТарифныеСтавки.Должность
		|			И (ВидыРабот.КвалификационныйРазряд = ТарифныеСтавки.Разряд)
		|			И ВидыРабот.Период >= ТарифныеСтавки.Период
		|			
		|			И (
		|				(&ДополнительныйРазрезТарифнойСетки В (ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация),
		|														ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|													)
		|				)
		|				И ТарифныеСтавки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|				И ТарифныеСтавки.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|			)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРабот.Период,
		|	ВидыРабот.Должность,
		|	ВидыРабот.КвалификационныйРазряд
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Должность,
		|	Разряд,
		|	ПериодВидаРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВидыРабот.Калькуляция КАК Калькуляция,
		|	ВидыРабот.Подразделение КАК Подразделение,
		|	ВидыРабот.Организация КАК Организация,
		|	ВидыРабот.ВидРабот КАК ВидРабот,
		|	ВидыРабот.Период КАК Период,
		|	МИНИМУМ(
		|		ЕСТЬNULL(ЕСТЬNULL(ТарифныеСтавки.Тариф, ТарифныеСтавкиОбщие.Тариф), 0)
		|		* ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости
		|	) КАК Расценка
		|ПОМЕСТИТЬ НРРТС_Расценки
		|ИЗ
		|	ВидыРабот КАК ВидыРабот
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ НРРТС_ДатыРасценок КАК ДатыРасценок
		|		ПО ДатыРасценок.Организация = ВидыРабот.Организация
		|			И ДатыРасценок.Подразделение = ВидыРабот.Подразделение
		|			И ДатыРасценок.Должность = ВидыРабот.Должность
		|			И (ДатыРасценок.Разряд = ВидыРабот.КвалификационныйРазряд)
		|			И ДатыРасценок.ПериодВидаРабот = ВидыРабот.Период
		|			
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавки
		|			ПО ТарифныеСтавки.Период = ДатыРасценок.ПериодРасценки
		|				И ВидыРабот.Должность = ТарифныеСтавки.Должность
		|				И (ТарифныеСтавки.Разряд = ВидыРабот.КвалификационныйРазряд)
		|				И ТарифныеСтавки.Тариф <> 0
		|
		|				И (
		|					(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.БезДетализации)
		|						И ТарифныеСтавки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|						И ТарифныеСтавки.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|					)
		|					ИЛИ
		|						(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация)
		|							И ТарифныеСтавки.Организация = ВидыРабот.Организация)
		|					ИЛИ
		|						(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|							И ТарифныеСтавки.Подразделение = ВидыРабот.Подразделение)
		|				)
		|	
		|		ЛЕВОЕ СОЕДИНЕНИЕ НРРТС_ДатыОбщихРасценок КАК ДатыОбщихРасценок
		|		ПО ТарифныеСтавки.Тариф ЕСТЬ NULL
		|			И ДатыОбщихРасценок.Должность = ВидыРабот.Должность
		|			И (ДатыОбщихРасценок.Разряд = ВидыРабот.КвалификационныйРазряд)
		|			И ДатыОбщихРасценок.ПериодВидаРабот = ВидыРабот.Период
		|
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки КАК ТарифныеСтавкиОбщие
		|			ПО ТарифныеСтавкиОбщие.Период = ДатыОбщихРасценок.ПериодРасценки
		|				И ВидыРабот.Должность = ТарифныеСтавкиОбщие.Должность
		|				И (ТарифныеСтавкиОбщие.Разряд = ВидыРабот.КвалификационныйРазряд)
		|				И ТарифныеСтавкиОбщие.Тариф <> 0
		|				
		|				И (
		|					(&ДополнительныйРазрезТарифнойСетки В (ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация),
		|															ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|														)
		|					)
		|					И ТарифныеСтавкиОбщие.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|					И ТарифныеСтавкиОбщие.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|				)
		|ГДЕ
		|	ВидыРабот.РасценкаПоТарифнойСетке
		|	И &ДопУсловие
		|
		|СГРУППИРОВАТЬ ПО
		|	ВидыРабот.Калькуляция,
		|	ВидыРабот.Подразделение,
		|	ВидыРабот.Организация,
		|	ВидыРабот.ВидРабот,
		|	ВидыРабот.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НРРТС_ДатыРасценок
		|";
		ДопУсловие = "";
		Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
			ДопУсловие = "
			|	И НЕ ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
			|					ИЗ РасценкиПоТарифнымСеткам КАК РПТС
			|					ГДЕ РПТС.Калькуляция = ВидыРабот.Калькуляция
			|						И РПТС.Организация = ВидыРабот.Организация
			|						И РПТС.Подразделение = ВидыРабот.Подразделение
			|						И РПТС.ВидРабот = ВидыРабот.ВидРабот
			|						И РПТС.Период <= ВидыРабот.Период
			|				)";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловие", ДопУсловие);
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам = "
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НРРТС_Расценки.Калькуляция КАК Калькуляция,
		|	НРРТС_Расценки.Подразделение КАК Подразделение,
		|	НРРТС_Расценки.Организация КАК Организация,
		|	НРРТС_Расценки.ВидРабот КАК ВидРабот,
		|	НРРТС_Расценки.Период КАК Период,
		|	НРРТС_Расценки.Расценка КАК Расценка
		|ИЗ НРРТС_Расценки КАК НРРТС_Расценки
		|ГДЕ
		|	НРРТС_Расценки.Расценка <> 0
		|";
		
	КонецЕсли;
	//-- Локализация

	ТекстЗапроса = "ВЫБРАТЬ
				|	ВидыРабот.Калькуляция КАК Калькуляция,
				|	ВидыРабот.Подразделение КАК Подразделение,
				|	ВидыРабот.Организация КАК Организация,
				|	ВидыРабот.ВидРабот КАК ВидРабот,
				|	РасценкиРаботСотрудников.КвалификационныйРазряд КАК КвалификационныйРазряд,
				|	ВидыРабот.Период КАК Период,
				|	МАКСИМУМ(ВидыРабот.Трудоемкость) КАК Трудоемкость,
				|	МАКСИМУМ(ВидыРабот.КратностьТрудоемкости) КАК КратностьТрудоемкости,
				|	МАКСИМУМ(РасценкиРаботСотрудников.Период) КАК ПериодРасценки,
				|	МАКСИМУМ(ВидыРабот.РасценкаЗаЧасРаботы) КАК РасценкаЗаЧасРаботы
				|ПОМЕСТИТЬ ДатыРасценок
				|ИЗ
				|	ВидыРабот КАК ВидыРабот
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
				|		ПО РасценкиРаботСотрудников.ВидРабот = ВидыРабот.ВидРабот
				|			И НЕ ВидыРабот.РасценкаПоТарифнойСетке
				|			И РасценкиРаботСотрудников.Период <= ВидыРабот.Период
				|			И (НЕ &ИспользоватьРазрядыРасценокРаботСотрудников
				|				ИЛИ РасценкиРаботСотрудников.КвалификационныйРазряд = ВидыРабот.КвалификационныйРазряд)
				|
				|СГРУППИРОВАТЬ ПО
				|	ВидыРабот.Калькуляция,
				|	ВидыРабот.ВидРабот,
				|	РасценкиРаботСотрудников.КвалификационныйРазряд,
				|	ВидыРабот.Период,
				|	ВидыРабот.Подразделение,
				|	ВидыРабот.Организация
				|"; //@Query-part
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);

	ТекстЗапроса = "ВЫБРАТЬ
				|	ДатыРасценок.Калькуляция КАК Калькуляция,
				|	ДатыРасценок.Подразделение КАК Подразделение,
				|	ДатыРасценок.Организация КАК Организация,
				|	ДатыРасценок.ВидРабот КАК ВидРабот,
				|	ДатыРасценок.Период КАК Период,
				|	МИНИМУМ(РасценкиРаботСотрудников.Расценка
				|		*
				|		ВЫБОР КОГДА ДатыРасценок.РасценкаЗаЧасРаботы
				|			ТОГДА ДатыРасценок.Трудоемкость / ДатыРасценок.КратностьТрудоемкости
				|			ИНАЧЕ 1
				|		КОНЕЦ) КАК Расценка
				|ПОМЕСТИТЬ РасценкиПредварительная
				|ИЗ
				|	ДатыРасценок КАК ДатыРасценок
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РасценкиРаботСотрудников КАК РасценкиРаботСотрудников
				|		ПО ДатыРасценок.ВидРабот = РасценкиРаботСотрудников.ВидРабот
				|			И (НЕ &ИспользоватьРазрядыРасценокРаботСотрудников
				|				ИЛИ ДатыРасценок.КвалификационныйРазряд = РасценкиРаботСотрудников.КвалификационныйРазряд)
				|			И ДатыРасценок.ПериодРасценки = РасценкиРаботСотрудников.Период
				|
				|ГДЕ &ДопУсловие
				|
				|СГРУППИРОВАТЬ ПО
				|	ДатыРасценок.Калькуляция,
				|	ДатыРасценок.ВидРабот,
				|	ДатыРасценок.Период,
				|	ДатыРасценок.Подразделение,
				|	ДатыРасценок.Организация
				|
				|ИМЕЮЩИЕ
				|	МИНИМУМ(РасценкиРаботСотрудников.Расценка) <> 0
				|"; //@Query-part
	
	ДопУсловие = "ИСТИНА"; //@Query-part
	
	//++ Локализация
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ДопУсловие = "
			|	НЕ ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
			|					ИЗ РасценкиПоТарифнымСеткам КАК РПТС
			|					ГДЕ РПТС.Калькуляция = ДатыРасценок.Калькуляция
			|						И РПТС.Организация = ДатыРасценок.Организация
			|						И РПТС.Подразделение = ДатыРасценок.Подразделение
			|						И РПТС.ВидРабот = ДатыРасценок.ВидРабот
			|						И РПТС.Период <= ДатыРасценок.Период
			|				)";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам;
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ТекстЗапроса = ТекстЗапроса + ТекстЗапроса_РасценкиПоТарифнымСеткам;
	КонецЕсли;
	//-- Локализация
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДопУсловие", ДопУсловие); //@Query-part
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДД.Калькуляция КАК Калькуляция,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.Период КАК Период,
		|	ДД.Подразделение КАК Подразделение,
		|	ДД.Организация КАК Организация,
		|	МИНИМУМ(ДД.Расценка) КАК Расценка
		|ПОМЕСТИТЬ Расценки
		|ИЗ РасценкиПредварительная КАК ДД
		|СГРУППИРОВАТЬ ПО
		|	ДД.Калькуляция,
		|	ДД.ВидРабот,
		|	ДД.Период,
		|	ДД.Подразделение,
		|	ДД.Организация
		|ИНДЕКСИРОВАТЬ ПО
		|	Подразделение,
		|	ВидРабот,
		|	Период"; 

	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ДатыРасценок"); //@Query-part
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ РасценкиПредварительная"); //@Query-part
	
	//++ Локализация
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ РасценкиПоТарифнымСеткам");
	КонецЕсли;
	//-- Локализация
	
	Возврат СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
КонецФункции

// Функция возвращает текст запроса формирования временной таблицы ВТРасценкиТрудозатратСрезПоследних,
//		с актуальными расценками по трудозатратам, формируется через срез последних, на одну необходимую дату.
//
// Возвращаемое значение:
//	Строка - сформированный текст запроса по актуальным расценкам трудозатрат.
//
Функция ТекстЗапросаВТРасценкиТрудозатратСрезПоследних() Экспорт
	
	МассивТекстовЗапроса = Новый Массив();
	
	ТекстЗапросаВТОтборы = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВидыРабот.Ссылка КАК ВидРабот,
		|	ВидыРабот.Должность КАК Должность,
		|	ВидыРабот.РасценкаПоТарифнойСетке КАК РасценкаПоТарифнойСетке,
		|	ВидыРабот.КвалификационныйРазряд КАК КвалификационныйРазряд,
		|	ВидыРабот.Трудоемкость КАК Трудоемкость,
		|	ВидыРабот.КратностьТрудоемкости КАК КратностьТрудоемкости,
		|	ВидыРабот.РасценкаЗаЧасРаботы КАК РасценкаЗаЧасРаботы,
		|	ДД.Этап КАК Этап,
		|	ВЫБОР КОГДА ЕСТЬNULL(Организации.Ссылка, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|		ТОГДА &Организация
		|		ИНАЧЕ Организации.Ссылка
		|	КОНЕЦ КАК Организация,
		|	ДД.ПодразделениеЭтапа КАК Подразделение
		//++ Локализация
		|	,
		|	ШтатноеРасписание.ТарифнаяСетка КАК ТарифнаяСетка,
		|	ПодразделениеШР.Подразделение ЕСТЬ НЕ NULL КАК УстановленоПодразделениеШтатногоРасписания
		//-- Локализация
		|
		|ПОМЕСТИТЬ ВТОтборы
		|	ИЗ ВТСпецификацияТрудозатраты КАК ДД
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРабот
		|			ПО ДД.ВидРабот = ВидыРабот.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
		|			ПО ДД.Этап = ЭтапыПроизводства.Ссылка
		|		
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|			ПО ЭтапыПроизводства.Партнер = Организации.Ссылка
		|
		//++ Локализация
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
		|			ПО ВидыРабот.Должность = Должности.Ссылка
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|			ПО ВидыРабот.РасценкаПоТарифнойСетке
		|				И Должности.Ссылка = ШтатноеРасписание.Должность
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестоПозицииШтатногоРасписанияВСтруктуреПредприятия КАК ПодразделениеШР
		|			ПО ШтатноеРасписание.Ссылка = ПодразделениеШР.Позиция
		|				И ДД.ПодразделениеЭтапа = ПодразделениеШР.Подразделение
		//-- Локализация
		|
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ
		|(
		|	(
		//++ Локализация
		|		ТарифнаяСетка,
		|		РасценкаПоТарифнойСетке,
		|		УстановленоПодразделениеШтатногоРасписания,
		//-- Локализация
		|		КвалификационныйРазряд,
		|		Организация,
		|		Подразделение
		|	)
		|	,
		|	(ВидРабот)
		|)
		|"; //@Query-part
		
	ДопУсловие = "ИСТИНА"; //@Query-part
	
	//++ Локализация
	ИспользоватьТарифныеСеткиПриРасчетеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты");
	НормированиеИРасценкаРаботПоТарифнымСеткам = ПолучитьФункциональнуюОпцию("НормированиеИРасценкаРаботПоТарифнымСеткам");
	//-- Локализация
	
	ТекстЗапросаВТОтборы = СтрЗаменить(ТекстЗапросаВТОтборы, "&ДопУсловие", ДопУсловие); //@Query-part
	МассивТекстовЗапроса.Добавить(ТекстЗапросаВТОтборы);
	
	//++ Локализация
	
	ТекстЗапроса_РасценкиПоТарифнымСеткам = "";
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ТекстЗапроса = "
			|ВЫБРАТЬ
			|	ВТОтборы.Этап КАК Этап,
			|	ВТОтборы.Организация КАК Организация,
			|	ВТОтборы.ВидРабот КАК ВидРабот,
			|	ВТОтборы.Подразделение КАК Подразделение,
			|	ЗначенияТарифов.Тариф * ВТОтборы.Трудоемкость / ВТОтборы.КратностьТрудоемкости КАК Расценка
			|ПОМЕСТИТЬ РасценкиПоТарифнымСеткам
			|ИЗ РегистрСведений.ЗначенияТарифов.СрезПоследних(&Период,
			|		(ТарифнаяСетка, РазрядКатегория) В
			|			(ВЫБРАТЬ
			|				ВТОтборы.ТарифнаяСетка,
			|				ВТОтборы.КвалификационныйРазряд 
			|			ИЗ ВТОтборы КАК ВТОтборы
			|			ГДЕ ВТОтборы.РасценкаПоТарифнойСетке
			|				И ВТОтборы.УстановленоПодразделениеШтатногоРасписания
			|			)
			|
			|	) КАК ЗначенияТарифов
			|	
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборы КАК ВТОтборы
			|	ПО ЗначенияТарифов.ТарифнаяСетка = ВТОтборы.ТарифнаяСетка
			|		И (ЗначенияТарифов.РазрядКатегория = ВТОтборы.КвалификационныйРазряд)
			|		И (ВТОтборы.РасценкаПоТарифнойСетке)
			|		И (ВТОтборы.УстановленоПодразделениеШтатногоРасписания)
			|
			|ГДЕ
			|	ЗначенияТарифов.Тариф * ВТОтборы.Трудоемкость / ВТОтборы.КратностьТрудоемкости <> 0
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВидРабот,
			|	Подразделение";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса_РасценкиПоТарифнымСеткам = "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	РПТС.Этап КАК Этап,
			|	РПТС.Организация КАК Организация,
			|	РПТС.ВидРабот КАК ВидРабот,
			|	РПТС.Подразделение КАК Подразделение,
			|	РПТС.Расценка КАК Расценка
			|
			|ИЗ РасценкиПоТарифнымСеткам КАК РПТС
			|";
	КонецЕсли;

	ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам = "";
	Если НормированиеИРасценкаРаботПоТарифнымСеткам Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТарифныеСтавки.Период КАК Период,
		|	ТарифныеСтавки.Должность КАК Должность,
		|	ТарифныеСтавки.Разряд КАК Разряд,
		|	ТарифныеСтавки.Тариф КАК Тариф,
		|	ТарифныеСтавки.Организация КАК Организация,
		|	ТарифныеСтавки.Подразделение КАК Подразделение
		|
		|ПОМЕСТИТЬ ТарифныеСтавкиПредварительная
		|ИЗ РегистрСведений.ТарифныеСтавки.СрезПоследних(&Период,
		|		(Должность, Разряд) В
		|			(ВЫБРАТЬ Т.Должность, Т.КвалификационныйРазряд ИЗ ВТОтборы КАК Т ГДЕ Т.РасценкаПоТарифнойСетке)
		|	) КАК ТарифныеСтавки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Должность,
		|	Разряд,
		|	Организация,
		|	Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТОтборы.Этап КАК Этап,
		|	ВТОтборы.Организация КАК Организация,
		|	ВТОтборы.Подразделение КАК Подразделение,
		|	ВТОтборы.ВидРабот КАК ВидРабот,
		|	(ДД.Тариф * ВТОтборы.Трудоемкость / ВТОтборы.КратностьТрудоемкости) КАК Расценка,
		|	(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация)
		|		И ВТОтборы.Организация = ДД.Организация)
		|	ИЛИ (&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|			И ВТОтборы.Подразделение = ДД.Подразделение) КАК ЕстьЗначенияПодходящиеПодДопРазрез
		|ПОМЕСТИТЬ НРРТС_ВТРасценкиТрудозатратПредварительная
		|ИЗ
		|	ТарифныеСтавкиПредварительная КАК ДД
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборы КАК ВТОтборы
		|	ПО ВТОтборы.РасценкаПоТарифнойСетке
		|		И ДД.Должность = ВТОтборы.Должность
		|		И (ДД.Разряд = ВТОтборы.КвалификационныйРазряд)
		|		И (
		|			(&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.БезДетализации)
		|				И ДД.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|				И ДД.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|				)
		|			
		|			ИЛИ (&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация)
		|					И ДД.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|					И (ВТОтборы.Организация = ДД.Организация
		|						ИЛИ ДД.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|				)
		|			ИЛИ (&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение)
		|					И ДД.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|					И (ВТОтборы.Подразделение = ДД.Подразделение
		|						ИЛИ ДД.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|				)
		|		)
		|
		|ГДЕ &ДопУсловие
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабот,
		|	Подразделение,
		|	ЕстьЗначенияПодходящиеПодДопРазрез
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТарифныеСтавкиПредварительная
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РТП.Этап КАК Этап,
		|	РТП.Организация КАК Организация,
		|	РТП.ВидРабот КАК ВидРабот,
		|	РТП.Подразделение КАК Подразделение,
		|	МИНИМУМ(РТП.Расценка) КАК Расценка
		|ПОМЕСТИТЬ НРРТС_ВТРасценкиТрудозатратСрезПоследних
		|ИЗ
		|	НРРТС_ВТРасценкиТрудозатратПредварительная КАК РТП
		|ГДЕ
		|	(
		|		&ДополнительныйРазрезТарифнойСетки = ЗНАЧЕНИЕ(Перечисление.ДополнительныеРазрезыТарифнойСетки.БезДетализации)
		|		ИЛИ РТП.ЕстьЗначенияПодходящиеПодДопРазрез
		|		ИЛИ НЕ ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
		|						ИЗ НРРТС_ВТРасценкиТрудозатратПредварительная КАК ДД
		|						ГДЕ ДД.ВидРабот = РТП.ВидРабот
		|							И ДД.Подразделение = РТП.Подразделение
		|							И ДД.ЕстьЗначенияПодходящиеПодДопРазрез
		|						)
		|	)
		|
		|СГРУППИРОВАТЬ ПО
		|	РТП.ВидРабот,
		|	РТП.Подразделение,
		|	РТП.Этап,
		|	РТП.Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НРРТС_ВТРасценкиТрудозатратПредварительная
		|";
		ДопУсловие = "ИСТИНА";
		Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
			ДопУсловие = "
			|	НЕ ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА
			|					ИЗ РасценкиПоТарифнымСеткам КАК РПТС
			|					ГДЕ РПТС.ВидРабот = ВТОтборы.ВидРабот
			|						И РПТС.Подразделение = ВТОтборы.Подразделение
			|				)
			|";
		КонецЕсли;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДопУсловие", ДопУсловие);
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
		ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам = "
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	НРРТС.Этап КАК Этап,
			|	НРРТС.Организация КАК Организация,
			|	НРРТС.ВидРабот КАК ВидРабот,
			|	НРРТС.Подразделение КАК Подразделение,
			|	НРРТС.Расценка КАК Расценка
			|
			|ИЗ НРРТС_ВТРасценкиТрудозатратСрезПоследних КАК НРРТС
			|";
	КонецЕсли;
	//-- Локализация
	
	ТекстЗапроса = "
		|
		|
		|ВЫБРАТЬ
		|	ВТОтборы.Этап КАК Этап,
		|	ВТОтборы.Организация КАК Организация,
		|	РасценкиРаботСотрудниковСрезПоследних.ВидРабот КАК ВидРабот,
		|	ВТОтборы.Подразделение КАК Подразделение,
		|	РасценкиРаботСотрудниковСрезПоследних.Расценка
		|		* ВЫБОР КОГДА ВТОтборы.РасценкаЗаЧасРаботы
		|			ТОГДА ВТОтборы.Трудоемкость / ВТОтборы.КратностьТрудоемкости
		|			ИНАЧЕ 1	КОНЕЦ
		|	КАК Расценка
		|ПОМЕСТИТЬ ВТРасценкиТрудозатратПредварительная
		|ИЗ
		|	РегистрСведений.РасценкиРаботСотрудников.СрезПоследних(
		|		&Период,
		|		ВЫБОР КОГДА &ИспользоватьРазрядыРасценокРаботСотрудников
		|			ТОГДА (ВидРабот, КвалификационныйРазряд) В (ВЫБРАТЬ Т.ВидРабот, Т.КвалификационныйРазряд
		|														ИЗ ВТОтборы КАК Т
		|														ГДЕ НЕ Т.РасценкаПоТарифнойСетке)
		|			ИНАЧЕ ВидРабот В (ВЫБРАТЬ Т.ВидРабот
		|								ИЗ ВТОтборы КАК Т
		|								ГДЕ НЕ Т.РасценкаПоТарифнойСетке)
		|		КОНЕЦ
		|		И &ДопУсловие
		|
		|	) КАК РасценкиРаботСотрудниковСрезПоследних
		|
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОтборы КАК ВТОтборы
		|	ПО РасценкиРаботСотрудниковСрезПоследних.ВидРабот = ВТОтборы.ВидРабот
		|";
		
	ДопУсловие = "";
	//++ Локализация
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ДопУсловие = "
			|	И НЕ ВидРабот В (ВЫБРАТЬ РПТС.ВидРабот
			|						ИЗ РасценкиПоТарифнымСеткам КАК РПТС
			|						ГДЕ Подразделение = РПТС.Подразделение
			|					)
			|";
	КонецЕсли;
	//-- Локализация
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ДопУсловие", ДопУсловие); //@Query-part
	
	//++ Локализация
	ТекстЗапроса = ТекстЗапроса + ТекстЗапроса_НормированиеИРасценкаРаботПоТарифнымСеткам;
	Если ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		ТекстЗапроса = ТекстЗапроса + ТекстЗапроса_РасценкиПоТарифнымСеткам;
	КонецЕсли;
	//-- Локализация
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ДД.Этап КАК Этап,
		|	ДД.Организация КАК Организация,
		|	ДД.ВидРабот КАК ВидРабот,
		|	ДД.Подразделение КАК Подразделение,
		|	МИНИМУМ(ДД.Расценка) КАК Расценка
		|ПОМЕСТИТЬ ВТРасценкиТрудозатратСрезПоследних
		|ИЗ ВТРасценкиТрудозатратПредварительная КАК ДД
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.ВидРабот,
		|	ДД.Подразделение,
		|	ДД.Этап,
		|	ДД.Организация
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРабот,
		|	Подразделение
		|";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ВТОтборы"); //@Query-part
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ВТРасценкиТрудозатратПредварительная"); //@Query-part
		
	Возврат СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ПлановыеТрудозатраты.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.12";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1be4bc37-f30b-4610-9ef9-8085cdf5bbe4");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ПлановыеТрудозатраты.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Конвертация типа колонки ""Ключ партия"" регистра накопления ""Плановые трудозатраты"".';
									|en = 'Conversion of the ""Key lot"" column type of the ""Planned labor costs"" accumulation register.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ПлановыеТрудозатраты.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ПлановыеТрудозатраты.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыНакопления.ПлановыеТрудозатраты.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ПлановыеТрудозатраты.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ПлановыеТрудозатраты";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДД.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.ПлановыеТрудозатраты КАК ДД
	|
	|ГДЕ
	// Конвертировать значения колонки имеет смысл только для тех документов, у которых заполнено значение КлючПартия хотя бы в одной строке.
	|	ДД.УдалитьКлючПартия ЕСТЬ НЕ NULL
	|	И ДД.УдалитьКлючПартия <> &ПустойУИД";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПустойУИД", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры,
		Регистраторы,
		ПолноеИмяРегистра);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.ПлановыеТрудозатраты";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрНакопления.ПлановыеТрудозатраты.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", Выборка.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыНакопления.ПлановыеТрудозатраты.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.КлючПартия = Строка(Запись.УдалитьКлючПартия);
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Не удалось записать данные в регистр накопления ""Плановые трудозатраты"" по плановой калькуляции ""%1"", по причине: %2';
						|en = 'Cannot write data to the ""Planned labor costs"" accumulation register based on the ""%1"" product standard cost. Reason: %2'"),
					Выборка.Регистратор,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыНакопления.ПлановыеТрудозатраты,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли