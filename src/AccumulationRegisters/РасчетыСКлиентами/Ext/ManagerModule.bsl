#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|	ПО АналитикаУчетаПоПартнерам.КлючАналитики = ЭтотСписок.АналитикаУчетаПоПартнерам
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиПартнеры
	|	ПО ВнешниеПользователиПартнеры.ОбъектАвторизации = АналитикаУчетаПоПартнерам.Партнер
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ВнешниеПользователиПартнеры.Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// При определении доступных видов оповещений пользователей.
// 
// Параметры:
//  ОписаниеВидовОповещений - см. ОповещенияПользователейОСобытиях.ОписаниеВидовОповещенийПользователей
Процедура ПриОпределенииДоступныхВидовОповещенийПользователей(ОписаниеВидовОповещений) Экспорт
	
	ГруппаВидаОповещенийВзаиморасчеты = ОповещенияПользователейОСобытиях.ГруппаВидаОповещений("ВзаиморасчетыСКлиентами");
	
	НовыйВидОповещений = ОповещенияПользователейОСобытиях.ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений);
	НовыйВидОповещений.ГруппаВидаОповещений = ГруппаВидаОповещенийВзаиморасчеты;
	НовыйВидОповещений.Идентификатор = "427cb1f4-48be-44a1-9384-874a73f96380";
	НовыйВидОповещений.Наименование = НСтр("ru = 'Поступление оплаты от клиента';
											|en = 'Customer payment'", ОбщегоНазначения.КодОсновногоЯзыка());
	НовыйВидОповещений.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное;
	НовыйВидОповещений.ИмяШаблонаСКД = Метаданные.РегистрыНакопления.РасчетыСКлиентами.Макеты.Оповещения_ПоступлениеОплаты.ПолноеИмя();
	НовыйВидОповещений.ШаблонТекстаОповещения = "ru = 'Поступила оплата на сумму [СуммаПлатежа] [Валюта] [ДатаПлатежа]. [ДокументОплаты]';
												|en = 'Payment in the amount of [СуммаПлатежа] [Валюта] is received on [ДатаПлатежа]. [ДокументОплаты]'"; // @NStr-1
	ОповещенияПользователейОСобытиях.ДобавитьОписаниеИсточникаДанных(НовыйВидОповещений,
		Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
		
	НовыйВидОповещений = ОповещенияПользователейОСобытиях.ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений);
	НовыйВидОповещений.ГруппаВидаОповещений = ГруппаВидаОповещенийВзаиморасчеты;
	НовыйВидОповещений.Идентификатор = "f1ebc4d4-2e23-49f3-8fdf-677a69c3cdf6";
	НовыйВидОповещений.Наименование = НСтр("ru = 'Превышение лимита задолженности по договору';
											|en = 'Debt limit is exceeded under the contract'", ОбщегоНазначения.КодОсновногоЯзыка());
	НовыйВидОповещений.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Неоперативное;
	НовыйВидОповещений.ИмяШаблонаСКД = Метаданные.РегистрыНакопления.РасчетыСКлиентами.Макеты.Оповещения_ПревышениеЛимитаЗадолженности.ПолноеИмя();
	НовыйВидОповещений.ШаблонТекстаОповещения = "ru = 'Превышен лимит задолженности в [ДопустимаяСуммаЗадолженности] [Валюта] по договору на [СуммаПревышения] [Валюта]';
												|en = 'The debt limit of [ДопустимаяСуммаЗадолженности] [Валюта] is exceeded for [СуммаПревышения] [Валюта] under the contract'"; // @NStr-1
	НовыйВидОповещений.ТекстСгруппированногоОповещения = "ru = 'Превышен лимит задолженности по договорам.';
														|en = 'The debt limit under contracts is exceeded.'"; // @NStr-1
	
КонецПроцедуры

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов и распределению взаиморасчетов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Если Не МенеджерВременныхТаблиц = Неопределено Тогда

		Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда
				
				ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
				
				Если ПланыОбмена.ГлавныйУзел() = Неопределено
					И Не МенеджерВременныхТаблиц = Неопределено 
					И МенеджерВременныхТаблиц.Таблицы.Найти("ОчередьДанныеКДопроведениюДокументовУпрБаланс") <> Неопределено Тогда
					ТаблицаДанных = МенеджерВременныхТаблиц.Таблицы["ОчередьДанныеКДопроведениюДокументовУпрБаланс"].ПолучитьДанные().Выгрузить();
				КонецЕсли;
				
				Возврат ТаблицаДанных;
				
			КонецЕсли;

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам Тогда
				ТаблицаДанных = РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.ТаблицаФормированияЗаданий();
				Если МенеджерВременныхТаблиц.Таблицы.Найти("ЗаданияКРаспределениюВзаиморасчетовКлиенты") <> Неопределено Тогда
					ИсходныеДанные = МенеджерВременныхТаблиц.Таблицы["ЗаданияКРаспределениюВзаиморасчетовКлиенты"].ПолучитьДанные().Выгрузить();

					Для Каждого Запись Из ИсходныеДанные Цикл
						НоваяСтрока = ТаблицаДанных.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
						НоваяСтрока.ДатаЗаписи = ТекущаяДатаСеанса();
					КонецЦикла;

				КонецЕсли;
				
				Возврат ТаблицаДанных;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;

	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует текст запроса получения данных для регистрации заданий в регистре очереди "Задания к распределению взаиморасчетов".
// Задания в регистре очереди будут формироваться по данным таблицы изменений движений, а также по движениям документов данного регистра расчетов, 
// расположенных в цепочке после изменяемых по тем же гранулам распределения взаиморасчетов (АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта).
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаДанныеКРегистрацииЗаданийРаспределенияВзаиморасчетов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВЫБОР КОГДА РасчетыСКлиентамиИзменения.Сумма = 0
	|								И (РасчетыСКлиентамиИзменения.КОплате <> 0 ИЛИ РасчетыСКлиентамиИзменения.КОтгрузке <> 0)
	|								И ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) <> ТИП(Документ.ВводОстатков)
	|								И ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|							ТОГДА РасчетыСКлиентамиИзменения.ДатаРегистратора
	|							ИНАЧЕ РасчетыСКлиентамиИзменения.Период
	|						КОНЕЦ, ДЕНЬ)) КАК Период,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) В (&ТипыРегистраторовБезПроверкиИзменения)
	|				ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетыСКлиентамиИзменения.Период, ДЕНЬ)
	|		КОНЕЦ) КАК ПериодКонтроляИзменений,
	|	РасчетыСКлиентамиИзменения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиИзменения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиИзменения.ВалютаРасчетов КАК Валюта
	|ПОМЕСТИТЬ МинимальныеПериодыИзмененийКлиенты
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК РасчетыСКлиентамиИзменения
	|ГДЕ
	|	(РасчетыСКлиентамиИзменения.Сумма <> 0
	|	ИЛИ РасчетыСКлиентамиИзменения.КОплате <> 0
	|	ИЛИ РасчетыСКлиентамиИзменения.КОтгрузке <> 0)
	|	И &НовыеРасчеты
	|	И &СоздаватьЗаданияКРаспределениюВзаиморасчетов
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентамиИзменения.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиИзменения.ОбъектРасчетов,
	|	РасчетыСКлиентамиИзменения.ВалютаРасчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ЗаданияКРаспределению.ПериодЗадания) КАК ПериодЗадания,
	|	МИНИМУМ(ЗаданияКРаспределению.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений,
	|	ЗаданияКРаспределению.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределению.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗаданияКРаспределению.Валюта КАК Валюта,
	|	ЗаданияКРаспределению.Документ КАК Документ,
	|	ЗаданияКРаспределению.Организация КАК Организация
	|ПОМЕСТИТЬ ЗаданияКРаспределениюВзаиморасчетовКлиенты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСКлиентами.Сумма = 0
	|				И (РасчетыСКлиентами.КОплате <> 0 ИЛИ РасчетыСКлиентами.КОтгрузке <> 0)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) <> ТИП(Документ.ВводОстатков)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|			ТОГДА РасчетыСКлиентами.ДатаРегистратора
	|			ИНАЧЕ РасчетыСКлиентами.Период
	|		КОНЕЦ КАК ПериодЗадания,
	|		МинимальныеПериодыИзмененийКлиенты.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСКлиентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|		РасчетыСКлиентами.Валюта КАК Валюта,
	|		РасчетыСКлиентами.Регистратор КАК Документ,
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МинимальныеПериодыИзмененийКлиенты КАК МинимальныеПериодыИзмененийКлиенты
	|			ПО РасчетыСКлиентами.АналитикаУчетаПоПартнерам = МинимальныеПериодыИзмененийКлиенты.АналитикаУчетаПоПартнерам
	|			И РасчетыСКлиентами.ОбъектРасчетов = МинимальныеПериодыИзмененийКлиенты.ОбъектРасчетов
	|			И РасчетыСКлиентами.Валюта = МинимальныеПериодыИзмененийКлиенты.Валюта
	|			И РасчетыСКлиентами.Период >= МинимальныеПериодыИзмененийКлиенты.Период
	|	ГДЕ
	|		РасчетыСКлиентами.Активность
	|		И &НовыеРасчеты
	|		И &СоздаватьЗаданияКРаспределениюВзаиморасчетов
	|		И (РасчетыСКлиентами.Сумма <> 0
	|		ИЛИ РасчетыСКлиентами.КОплате <> 0
	|		ИЛИ РасчетыСКлиентами.КОтгрузке <> 0)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСКлиентамиИзменения.Сумма = 0
	|				И (РасчетыСКлиентамиИзменения.КОплате <> 0 ИЛИ РасчетыСКлиентамиИзменения.КОтгрузке <> 0)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) <> ТИП(Документ.ВводОстатков)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|			ТОГДА РасчетыСКлиентамиИзменения.ДатаРегистратора
	|			ИНАЧЕ РасчетыСКлиентамиИзменения.Период
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(РасчетыСКлиентамиИзменения.Документ) В (&ТипыРегистраторовБезПроверкиИзменения)
	|				ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетыСКлиентамиИзменения.Период, ДЕНЬ)
	|		КОНЕЦ,
	|		РасчетыСКлиентамиИзменения.АналитикаУчетаПоПартнерам,
	|		РасчетыСКлиентамиИзменения.ОбъектРасчетов,
	|		РасчетыСКлиентамиИзменения.ВалютаРасчетов,
	|		РасчетыСКлиентамиИзменения.Документ,
	|		РасчетыСКлиентамиИзменения.Организация
	|	ИЗ
	|		РасчетыСКлиентамиИзменения КАК РасчетыСКлиентамиИзменения
	|	ГДЕ
	|		(РасчетыСКлиентамиИзменения.Сумма <> 0
	|		ИЛИ РасчетыСКлиентамиИзменения.КОплате <> 0
	|		ИЛИ РасчетыСКлиентамиИзменения.КОтгрузке <> 0)
	|		И &НовыеРасчеты
	|		И &СоздаватьЗаданияКРаспределениюВзаиморасчетов) КАК ЗаданияКРаспределению
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКРаспределению.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределению.ОбъектРасчетов,
	|	ЗаданияКРаспределению.Валюта,
	|	ЗаданияКРаспределению.Документ,
	|	ЗаданияКРаспределению.Организация";

	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область КонтрольИтогов

// Есть изменения итогов набора.
// 
// Параметры:
//  Набор - РегистрНакопленияНаборЗаписей.РасчетыСКлиентами,
//          РегистрНакопленияНаборЗаписей.РасчетыСПоставщиками,
//          РегистрНакопленияНаборЗаписей.РасчетыСПоставщикамиПоСрокам,
//          РегистрНакопленияНаборЗаписей.РасчетыСКлиентамиПоСрокам.
// 
// Возвращаемое значение:
//  Булево - Есть изменения итогов набора
Функция ЕстьИзмененияИтоговНабора(Набор) Экспорт
	
	Если НЕ Набор.Модифицированность() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЭтоРасчетыСКлиентами = 
		ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСКлиентами")
		ИЛИ ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСКлиентамиПоСрокам");
		
	Если ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСКлиентами")
		ИЛИ ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСПоставщиками") Тогда
		ТекстЗапроса = ТекстЗапросаРасчеты(ЭтоРасчетыСКлиентами);
		
	ИначеЕсли ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСКлиентамиПоСрокам")
		ИЛИ ТипЗнч(Набор) = Тип("РегистрНакопленияНаборЗаписей.РасчетыСПоставщикамиПоСрокам") Тогда
		ТекстЗапроса = ТекстЗапросаРасчетыПоСрокам(ЭтоРасчетыСКлиентами);
		
	Иначе
		ТекстОшибки = НСтр("ru = 'Не удалось определить таблицу для сравнения итогов набора записей.';
							|en = 'Cannot identify the table for comparing record set totals.'");
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.ОшибкаКонфигурации);
	КонецЕсли;
	ОбновляемыйРегистратор = Набор.Отбор.Регистратор.Значение;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Регистратор", ОбновляемыйРегистратор);
	Запрос.УстановитьПараметр("НовыеДвижения", Набор.Выгрузить());
	РезультатСравнения = Запрос.Выполнить().Выгрузить();
	
	ЕстьИзменения = РезультатСравнения.Количество() > 0;
	Если ЕстьИзменения Тогда
		РезультатСравнения.Свернуть("Регистратор");
		Для Каждого Запись Из РезультатСравнения Цикл
			СтрокиТекста = Новый Массив;
			СтрокиТекста.Добавить(НСтр("ru = 'После обновления изменились итоги ресурсов в движениях документа. Обновление документа отменено.';
										|en = 'After the update, the resource totals in the document register records were changed. The document update is cancelled.'"));
			СтрокиТекста.Добавить(ТекстПерепроведитеДокументВРучную());
			ТекстРекомендации = СтрСоединить(СтрокиТекста, Символы.ПС);
			ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
				Запись.Регистратор,
				ТекстРекомендации);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ЕстьИзменения;
	
КонецФункции

Функция ТекстЗапросаРасчеты(ЭтоРасчетыСКлиентами)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Расчеты.ВидДвижения КАК ВидДвижения,
	|	Расчеты.Регистратор КАК Регистратор,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.Сумма КАК Сумма
	|ПОМЕСТИТЬ втНовыеДвижения
	|ИЗ
	|	&НовыеДвижения КАК Расчеты
	|;
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.Регистратор КАК Регистратор,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		-ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -Расчеты.Сумма
	|			ИНАЧЕ Расчеты.Сумма
	|		КОНЕЦ КАК Сумма
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор = &Регистратор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НовыеДвижения.Регистратор КАК Регистратор,
	|		НовыеДвижения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		НовыеДвижения.ОбъектРасчетов КАК ОбъектРасчетов,
	|		НовыеДвижения.Валюта КАК Валюта,
	|		ВЫБОР КОГДА НовыеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -НовыеДвижения.Сумма
	|			ИНАЧЕ НовыеДвижения.Сумма
	|		КОНЕЦ КАК Сумма
	|	ИЗ
	|		втНовыеДвижения КАК НовыеДвижения
	|	) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Сумма) <> 0
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"РегистрНакопления.РасчетыСКлиентами",
			"РегистрНакопления.РасчетыСПоставщиками"
		);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыПоСрокам(ЭтоРасчетыСКлиентами)

	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Расчеты.ВидДвижения КАК ВидДвижения,
	|	Расчеты.ДокументРегистратор КАК ДокументРегистратор,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Расчеты.Валюта КАК Валюта,
	|	Расчеты.Предоплата КАК Предоплата,
	|	Расчеты.Долг КАК Долг
	|ПОМЕСТИТЬ втНовыеДвижения
	|ИЗ
	|	&НовыеДвижения КАК Расчеты
	|;
	|
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта КАК Валюта,
	|	СУММА(ВложенныйЗапрос.Предоплата) КАК Предоплата,
	|	СУММА(ВложенныйЗапрос.Долг) КАК Долг
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.ДокументРегистратор КАК Регистратор,
	|		Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
	|		Расчеты.Валюта КАК Валюта,
	|		-ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -Расчеты.Предоплата
	|			ИНАЧЕ Расчеты.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		-ВЫБОР КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -Расчеты.Долг
	|			ИНАЧЕ Расчеты.Долг
	|		КОНЕЦ КАК Долг
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор = &Регистратор
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		НовыеДвижения.ДокументРегистратор КАК Регистратор,
	|		НовыеДвижения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		НовыеДвижения.ОбъектРасчетов КАК ОбъектРасчетов,
	|		НовыеДвижения.Валюта КАК Валюта,
	|		ВЫБОР КОГДА НовыеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -НовыеДвижения.Предоплата
	|			ИНАЧЕ НовыеДвижения.Предоплата
	|		КОНЕЦ КАК Предоплата,
	|		ВЫБОР КОГДА НовыеДвижения.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			ТОГДА -НовыеДвижения.Долг
	|			ИНАЧЕ НовыеДвижения.Долг
	|		КОНЕЦ КАК Долг
	|	ИЗ
	|		втНовыеДвижения КАК НовыеДвижения
	|	) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Регистратор,
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВложенныйЗапрос.Предоплата) <> 0
	|	ИЛИ СУММА(ВложенныйЗапрос.Долг) <> 0
	|";
	
	Если НЕ ЭтоРасчетыСКлиентами Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"РегистрНакопления.РасчетыСКлиентамиПоСрокам",
			"РегистрНакопления.РасчетыСПоставщикамиПоСрокам"
		);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращаемое значение:
//  Строка - Текст рекомендации для документа в случае не удачного обновления регистра
//
Функция ТекстПерепроведитеДокументВРучную() Экспорт
	
	Возврат НСтр("ru = 'Перепроведите документ вручную.';
				|en = 'Repost the document manually.'");
	
КонецФункции

// Параметры:
//  ИнформацияОбОшибке - ИнформацияОбОшибке
//  ОбновляемыйРегистратор - ДокументСсылка - Обновляемый регистратор
// 
// Возвращаемое значение:
//  Строка - Текст сообщения об ошибке
Функция ТекстСообщенияОбОшибке(ИнформацияОбОшибке, ОбновляемыйРегистратор) Экспорт
	
	ТекстСообщения = НСтр("ru = 'Не удалось обновить документ: %Объект% по причине: %Причина%';
							|en = 'Cannot update the document: %Объект%. Reason: %Причина%'");
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", ОбновляемыйРегистратор);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке));
	
	Возврат ТекстСообщения;
	
Конецфункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.82";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("489f7d26-6bd5-7943-a55c-3d06d445974a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСКлиентами.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Критичный;
	Обработчик.Комментарий = СтрШаблон(НСтр("ru = 'Исправляет реквизит ""%1"" для движений переносов расчетов.
												|Исправляет реквизит ""%1"" для движений ввода начальных остатков авансов.
												|Исправляет реквизит ""%2"" для движений платежей без даты погашения.';
												|en = 'Corrects the ""%1"" attribute for calculation transfer records.
												|Corrects the ""%1"" attribute for opening prepayment balance entry transfer records.
												| Corrects the ""%2"" attribute without a repayment date.'"), 
		Метаданные.РегистрыНакопления.РасчетыСКлиентами.Реквизиты.ПорядокОперации.Представление(),
		Метаданные.РегистрыНакопления.РасчетыСКлиентами.Реквизиты.ПорядокЗачетаПоДатеПлатежа.Представление());
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.БлокируемыеОбъекты = "";
	
КонецПроцедуры

// Процедура регистрации данных для обработчика обновления ОбработатьДанныеДляПереходаНаВерсию.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра  = "РегистрНакопления.РасчетыСКлиентами";

	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	#Область ТекстЗапроса
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК РасчетыПриемник
	|			ПО Расчеты.Период = РасчетыПриемник.Период
	|				И Расчеты.Регистратор = РасчетыПриемник.Регистратор
	|				И Расчеты.АналитикаУчетаПоПартнерамПриемник = РасчетыПриемник.АналитикаУчетаПоПартнерам
	|				И Расчеты.ОбъектРасчетовПриемник = РасчетыПриемник.ОбъектРасчетов
	|				И Расчеты.ВалютаПриемник = РасчетыПриемник.Валюта
	|				И Расчеты.ПорядокОперации = РасчетыПриемник.ПорядокОперации
	|ГДЕ
	|	Расчеты.СуммаПриемник > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	Расчеты.ДатаПлатежа = ДАТАВРЕМЯ(1,1,1)
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Расчеты.Сумма > 0
	|	И ВЫРАЗИТЬ(ЛЕВ(Расчеты.ПорядокЗачетаПоДатеПлатежа, 4) КАК Строка(4))<> ""3000""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	(Расчеты.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|		ИЛИ Расчеты.Регистратор ССЫЛКА Документ.ВводОстатков)
	|	И Расчеты.Период <> Расчеты.ДатаРегистратора
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Расчеты.ДатаРегистратора >= ДАТАВРЕМЯ(2010,1,1)
	|	И (СТРОКА(ГОД(Расчеты.Период) - 2000) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 3, 2)
	|		ИЛИ ВЫБОР КОГДА МЕСЯЦ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 2)
	|				ИНАЧЕ СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 6, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 1) <> ""0""
	|			КОНЕЦ
	|		ИЛИ ВЫБОР КОГДА ДЕНЬ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 2)
	|				ИНАЧЕ СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 8, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 1) <> ""0""
	|			КОНЕЦ)
	|";
	#КонецОбласти
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы,
		ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрНакопления.РасчетыСКлиентами";

	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОбновляемыхДанных из ОбновляемыеДанные Цикл
		
		ОбновляемыйРегистратор = СтрокаОбновляемыхДанных.Регистратор;
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			#Область Блокировки
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСКлиентами.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ОбновляемыйРегистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			#КонецОбласти
			
			ПричинаИсключения = "ПлохиеДанные";
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ОбновляемыйРегистратор);
			НаборЗаписей.Прочитать();
			
			#Область ИсправлениеПорядковОперацииПереносаРасчетов
			ТаблицаЗаписей = НаборЗаписей.Выгрузить();
			ЕстьИзменения = Ложь;
			Для Каждого ЗаписьИсточник Из ТаблицаЗаписей Цикл
				Если НЕ ЗначениеЗаполнено(ЗаписьИсточник.ПорядокОперации) Тогда
					Продолжить;
				КонецЕсли;
				Если ЗаписьИсточник.СуммаПриемник > 0 Тогда
					ЗаписиПриемники = ТаблицаЗаписей.НайтиСтроки(Новый Структура("АналитикаУчетаПоПартнерам,ОбъектРасчетов,Валюта",ЗаписьИсточник.АналитикаУчетаПоПартнерамПриемник,ЗаписьИсточник.ОбъектРасчетовПриемник,ЗаписьИсточник.ВалютаПриемник));
					ИсправилиПриемники = Ложь;
					Для Каждого ЗаписьПриемник Из ЗаписиПриемники Цикл
						Если ЗаписьПриемник.ПорядокОперации = ЗаписьИсточник.ПорядокОперации Тогда
							ЗаписьПриемник.ПорядокОперации =ЗаписьИсточник.ПорядокОперации + "3";
							ИсправилиПриемники = Истина;
						КонецЕсли
					КонецЦикла;
					Если ИсправилиПриемники Тогда
						ЗаписьИсточник.Порядокоперации = ЗаписьИсточник.ПорядокОперации + "2";
						ЕстьИзменения = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если ЕстьИзменения Тогда
				НаборЗаписей.Загрузить(ТаблицаЗаписей);
			КонецЕсли;
			#КонецОбласти
			
			#Область ИсправлениеПорядковОперацииПлатежейБезДатыПогашения
			Для Каждого Запись из НаборЗаписей Цикл
				Если Запись.ВидДвижения = ВидДвиженияНакопления.Расход
					И Запись.Сумма > 0 
					И Запись.ДатаПлатежа = Дата(1,1,1)
					И Лев(Запись.ПорядокЗачетаПоДатеПлатежа,4) <> "3000" Тогда
					
					Запись.ПорядокЗачетаПоДатеПлатежа = "30000101" + Прав(Запись.ПорядокЗачетаПоДатеПлатежа,СтрДлина(Запись.ПорядокЗачетаПоДатеПлатежа)-8);
					
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			#Область ИсправлениеПорядковОперацииВводаОстатковАвансов
			Для Каждого Запись из НаборЗаписей Цикл
				Если (ТипЗнч(ОбновляемыйРегистратор) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов")
						Или ТипЗнч(ОбновляемыйРегистратор) = Тип("ДокументСсылка.ВводОстатков"))
					И Запись.ДатаРегистратора <> Запись.Период
					И Запись.ВидДвижения = ВидДвиженияНакопления.Расход
					И Запись.ДатаРегистратора >= Дата(2010, 1, 1)
					И Лев(Запись.ПорядокОперации, 8) <> Формат(Запись.Период, "ДФ=yyyyMMdd") Тогда
					
					ДатаСтрокой = Формат(Запись.Период, "ДФ=yyyyMMdd");
					Запись.ПорядокОперации = ДатаСтрокой + Сред(Запись.ПорядокОперации, 9);
					
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			ИзменилисьИтоги = Ложь;
			Если НаборЗаписей.Количество() > 0 Тогда
				ИзменилисьИтоги = ЕстьИзмененияИтоговНабора(НаборЗаписей);
			КонецЕсли;
			
			Если НаборЗаписей.Модифицированность() И НЕ ИзменилисьИтоги Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ОбновляемыйРегистратор);
				Если ЗначениеЗаполнено(ОбновляемыйРегистратор) Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
					ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
						ОбновляемыйРегистратор,
						ТекстСообщенияОбОшибке(ИнформацияОбОшибке(), ОбновляемыйРегистратор)
						+ Символы.ПС + ТекстПерепроведитеДокументВРучную());
				КонецЕсли;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
