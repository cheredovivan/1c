#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений = 
		ДополнительныеСвойства.Свойство("СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений")
		И ДополнительныеСвойства.СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений;
	
	Если СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений Тогда
		СвойстваДокумента = Новый Структура();
		СвойстваДокумента.Вставить("ЭтоНовый", Ложь);
		СвойстваДокумента.Вставить("РежимЗаписи", РежимЗаписиДокумента.ОтменаПроведения);
		
		ДатаРегистратора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.Регистратор.Значение, "Дата");
		
		ДополнительныеСвойства.Вставить("СвойстваДокумента",       СвойстваДокумента);
		ДополнительныеСвойства.Вставить("ДатаРегистратора",        ДатаРегистратора);
		ДополнительныеСвойства.Вставить("РассчитыватьИзменения",   Истина);
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Новый МенеджерВременныхТаблиц);
		ДополнительныеСвойства.Вставить("ТаблицыКонтроля",         Новый Структура);
	КонецЕсли;
	
	Если Не СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений 
		И Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	#Область ТекстЗапроса
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Таблица.Регистратор  КАК Регистратор,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОбъектыРасчетов.Объект КАК Объект,
	|	Таблица.Валюта       КАК Валюта,
	|	Таблица.ДатаПлатежа  КАК ДатаПлатежа,
	|	ВЫБОР КОГДА НЕ Таблица.ИсключатьПриКонтроле ТОГДА
	|			ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|					-Таблица.КОплате
	|				ИНАЧЕ Таблица.КОплате
	|			КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ                КАК КОплатеПередЗаписью,
	|	ВЫБОР КОГДА НЕ Таблица.ИсключатьПриКонтроле ТОГДА
	|			ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|					Таблица.Оплачивается
	|				ИНАЧЕ - Таблица.Оплачивается
	|			КОНЕЦ
	|	КОНЕЦ                КАК ОплачиваетсяПередЗаписью,
	|	0                    КАК КОплатеПередЗаписьюКонтрольСрока,
	|	0                    КАК ОплачиваетсяПередЗаписьюКонтрольСрока,
	|	0                    КАК СуммаПередЗаписью,
	|	0                    КАК ОтгружаетсяПередЗаписью,
	|	0                    КАК ДопустимаяСуммаЗадолженностиПередЗаписью,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|
	|ПОМЕСТИТЬ РасчетыСКлиентамиПередЗаписью
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|			ПО ОбъектыРасчетов.Ссылка = Таблица.ОбъектРасчетов
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|		И (ОбъектыРасчетов.Объект ССЫЛКА Документ.ЗаказКлиента
	|			ИЛИ ОбъектыРасчетов.Объект ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ИЛИ ОбъектыРасчетов.Объект ССЫЛКА Документ.РеализацияТоваровУслуг
	//++ Устарело_Переработка24

	//++ НЕ УТКА
	|			ИЛИ ОбъектыРасчетов.Объект ССЫЛКА Документ.ЗаказДавальца
	//-- НЕ УТКА

	//-- Устарело_Переработка24
	|			ИЛИ ОбъектыРасчетов.Объект ССЫЛКА Документ.АктВыполненныхРабот)
	|	И НЕ &ЭтоНовый
	|	И НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Регистратор                  КАК Регистратор,
	|	Таблица.ОбъектРасчетов               КАК ОбъектРасчетов,
	|	Таблица.ОбъектРасчетов.Объект        КАК Объект,
	|	Таблица.Валюта                       КАК Валюта,
	|	Таблица.ДатаПлатежа                  КАК ДатаПлатежа,
	|	0                                    КАК КОплатеПередЗаписью,
	|	0                                    КАК ОплачиваетсяПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			-Таблица.КОплате
	|		ИНАЧЕ Таблица.КОплате
	|	КОНЕЦ                                КАК КОплатеПередЗаписьюКонтрольСрока,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			Таблица.Оплачивается
	|		ИНАЧЕ -Таблица.Оплачивается
	|	КОНЕЦ                                КАК ОплачиваетсяПередЗаписьюКонтрольСрока,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			ВЫБОР КОГДА Таблица.Сумма < 0 ТОГДА
	|					0
	|				ИНАЧЕ -Таблица.Сумма
	|			КОНЕЦ
	|		ИНАЧЕ Таблица.Сумма
	|	КОНЕЦ                                КАК СуммаПередЗаписью,
	|	ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|			0
	|		ИНАЧЕ Таблица.Отгружается
	|	КОНЕЦ                                КАК ОтгружаетсяПередЗаписью,
	|	Таблица.ДопустимаяСуммаЗадолженности КАК ДопустимаяСуммаЗадолженностиПередЗаписью,
	|	Таблица.АналитикаУчетаПоПартнерам    КАК АналитикаУчетаПоПартнерам
	|
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	|	И Таблица.ОбъектРасчетов.Объект <> НЕОПРЕДЕЛЕНО
	|	И НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|;
	|/////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расчеты.ВидДвижения               КАК ВидДвижения,
	|	Расчеты.Регистратор               КАК Регистратор,
	|	Расчеты.Период                    КАК Период,
	|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Расчеты.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	Расчеты.Валюта                    КАК Валюта,
	|
	|	Расчеты.Сумма        КАК Сумма,
	|	Расчеты.Оплачивается КАК Оплачивается,
	|	Расчеты.Отгружается  КАК Отгружается,
	|	Расчеты.КОплате      КАК КОплате,
	|	Расчеты.КОтгрузке    КАК КОтгрузке,
	|
	|	Расчеты.ХозяйственнаяОперация          КАК ХозяйственнаяОперация,
	|	Расчеты.СуммаРегл                      КАК СуммаРегл,
	|	Расчеты.СуммаУпр                       КАК СуммаУпр,
	|	Расчеты.СтатьяДвиженияДенежныхСредств  КАК СтатьяДвиженияДенежныхСредств,
	|	Расчеты.ПорядокОперации                КАК ПорядокОперации,
	|	Расчеты.ПорядокЗачетаПоДатеПлатежа     КАК ПорядокЗачетаПоДатеПлатежа,
	|	Расчеты.РасчетныйДокумент              КАК РасчетныйДокумент,
	|	Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|	Расчеты.ОбъектРасчетовПриемник         КАК ОбъектРасчетовПриемник,
	|	Расчеты.ВалютаПриемник                 КАК ВалютаПриемник,
	|	Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
	|	Расчеты.ВалютаДокумента                КАК ВалютаДокумента,
	|	Расчеты.ВариантОплаты                  КАК ВариантОплаты,
	|	Расчеты.ДатаПлатежа                    КАК ДатаПлатежа,
	|	Расчеты.ДатаРегистратора               КАК ДатаРегистратора,
	|	Расчеты.ПродажаПоЗаказу                КАК ПродажаПоЗаказу,
	|	Расчеты.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	Расчеты.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|	Расчеты.СвязанныйДокумент              КАК СвязанныйДокумент,
	|	Расчеты.Сторно                         КАК Сторно
	|ПОМЕСТИТЬ РасчетыСКлиентамиИсходныеДвижения
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|ГДЕ
	|	Расчеты.Регистратор = &Регистратор
	|";
	#КонецОбласти
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",                 ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.УстановитьПараметр("ОбменДанными",             ОбменДанными.Загрузка);
	Запрос.УстановитьПараметр("РассчитыватьИзменения",    ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства)
		И ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ВводОстатков")
		И ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОбъектовОплаты = СФормироватьТаблицуОбъектовОплаты();
	РегистрыСведений.ГрафикПлатежей.УстановитьБлокировкиДанныхДляРасчетаГрафика(
		ТаблицаОбъектовОплаты, "РегистрНакопления.РасчетыСКлиентами", "ОбъектРасчетов", "ОбъектРасчетов");
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	
	СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений = 
		ДополнительныеСвойства.Свойство("СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений")
		И ДополнительныеСвойства.СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) 
		И Не СинхронизацияДанныхЧерезУниверсальныйФорматУдалениеДвижений Тогда
		Возврат;
	КонецЕсли;
	
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	
	#Область ОперативноеПроведениеПоВзаиморасчетам
	
	ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено;
	
	// Проверим, что запись данных вызывается из механизма проведения документов.
	Если ДополнительныеСвойства.Свойство("СвойстваДвиженийМеханизмов")
		И ДополнительныеСвойства.СвойстваДвиженийМеханизмов.Свойство("Взаиморасчеты") Тогда
		
		Если ДополнительныеСвойства.СвойстваДвиженийМеханизмов.Взаиморасчеты.Свойство("ВыполнятьРаспределениеВзаиморасчетовВТранзакции") Тогда
			ВыполнятьРаспределениеВзаиморасчетовВТранзакции = ДополнительныеСвойства.СвойстваДвиженийМеханизмов.Взаиморасчеты.ВыполнятьРаспределениеВзаиморасчетовВТранзакции;
		КонецЕсли;
		
	Иначе
		// Загрузка данных или запись движений вызывается не из механизма проведения документов.
		ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Ложь;
	КонецЕсли;
	
	Если ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено Тогда
		
		Если (Не НоваяАрхитектураВзаиморасчетов Или ОбщегоНазначения.ИнформационнаяБазаФайловая()
			Или ОбщегоНазначения.РежимОтладки()) Тогда

			ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Истина;

		Иначе
			
			ВключенаНастройкаФоновогоРаспределенияВзаиморасчетов = Константы.РаспределятьВзаиморасчетыФоновымЗаданием.Получить();
			
			Если ВключенаНастройкаФоновогоРаспределенияВзаиморасчетов Тогда
				ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Ложь;
			Иначе
				Если ДополнительныеСвойства.СвойстваДокумента.Свойство("Дата") Тогда
					ДатаДокумента = ДополнительныеСвойства.СвойстваДокумента.Дата;
				ИначеЕсли ДополнительныеСвойства.Свойство("ДатаРегистратора") Тогда
					ДатаДокумента = ДополнительныеСвойства.ДатаРегистратора;
				Иначе
					ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Отбор.Регистратор.Значение, "Дата");
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;
	
	КонецЕсли;
	
	#КонецОбласти
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	#Область ТекстЗапроса
	МассивТекстовЗапроса = Новый Массив;
	ВременныеТаблицыКУничтожению = Новый Массив;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаИзменений.ОбъектРасчетов          КАК ОбъектРасчетов,
	|	ТаблицаИзменений.Валюта                  КАК Валюта,
	|	СУММА(ТаблицаИзменений.КОплатеИзменение) КАК КОплатеИзменение
	|	
	|ПОМЕСТИТЬ ДвиженияРасчетыСКлиентамиИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.ОбъектРасчетов             КАК ОбъектРасчетов,
	|		Таблица.Объект                     КАК Объект,
	|		Таблица.Валюта                     КАК Валюта,
	|		Таблица.КОплатеПередЗаписью        КАК КОплатеИзменение,
	|		Таблица.ОплачиваетсяПередЗаписью   КАК ОплачиваетсяИзменение
	|	ИЗ
	|		РасчетыСКлиентамиПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
	|		ОбъектыРасчетов.Объект КАК Объект,
	|		Таблица.Валюта         КАК Валюта,
	|		ВЫБОР КОГДА НЕ Таблица.ИсключатьПриКонтроле ТОГДА
	|				ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|						Таблица.КОплате
	|					ИНАЧЕ -Таблица.КОплате
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КОплатеИзменение,
	|		ВЫБОР КОГДА НЕ Таблица.ИсключатьПриКонтроле ТОГДА
	|				ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|						-Таблица.Оплачивается
	|					ИНАЧЕ Таблица.Оплачивается
	|				КОНЕЦ
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ОплачиваетсяИзменение
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|				ПО ОбъектыРасчетов.Ссылка = Таблица.ОбъектРасчетов
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|
	|) КАК ТаблицаИзменений
	|ГДЕ
	|	НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.ОбъектРасчетов,
	|	ТаблицаИзменений.Объект,
	|	ТаблицаИзменений.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КОплатеИзменение) + СУММА(ТаблицаИзменений.ОплачиваетсяИзменение) < 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Регистратор                                  КАК Регистратор,
	|	ТаблицаИзменений.ОбъектРасчетов                               КАК ОбъектРасчетов,
	|	ТаблицаИзменений.Валюта                                       КАК Валюта,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)                        КАК СуммаИзменение,
	|	СУММА(ТаблицаИзменений.ОтгружаетсяИзменение)                  КАК ОтгружаетсяИзменение,
	|	СУММА(ТаблицаИзменений.ДопустимаяСуммаЗадолженностиИзменение) КАК ДопустимаяСуммаЗадолженностиИзменение,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам                    КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ДвиженияРасчетыСКлиентамиИзменениеСуммыДолга
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Регистратор                              КАК Регистратор,
	|		Таблица.ОбъектРасчетов                           КАК ОбъектРасчетов,
	|		Таблица.Валюта                                   КАК Валюта,
	|		Таблица.СуммаПередЗаписью                        КАК СуммаИзменение,
	|		Таблица.ОтгружаетсяПередЗаписью                  КАК ОтгружаетсяИзменение,
	|		Таблица.ДопустимаяСуммаЗадолженностиПередЗаписью КАК ДопустимаяСуммаЗадолженностиИзменение,
	|		Таблица.АналитикаУчетаПоПартнерам                КАК АналитикаУчетаПоПартнерам
	|	ИЗ
	|		РасчетыСКлиентамиПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Регистратор                   КАК Регистратор,
	|		Таблица.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		Таблица.Валюта                        КАК Валюта,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|				ВЫБОР КОГДА Таблица.Сумма < 0 ТОГДА
	|						0
	|					ИНАЧЕ Таблица.Сумма
	|				КОНЕЦ
	|			ИНАЧЕ -Таблица.Сумма
	|		КОНЕЦ                                 КАК СуммаИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|				0
	|			ИНАЧЕ -Таблица.Отгружается
	|		КОНЕЦ                                 КАК ОтгружаетсяИзменение,
	|		-Таблица.ДопустимаяСуммаЗадолженности КАК ДопустимаяСуммаЗадолженностиИзменение,
	|		Таблица.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И Таблица.ОбъектРасчетов.Объект <> НЕОПРЕДЕЛЕНО
	|
	|) КАК ТаблицаИзменений
	|
	|ГДЕ
	|	&ПроведениеДокумента
	|	И НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|	И НЕ ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Регистратор) В (
	|		ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),
	|		ТИП(Документ.ПриходныйКассовыйОрдер),
	|		ТИП(Документ.ОперацияПоПлатежнойКарте),
	|		ТИП(Документ.ВзаимозачетЗадолженности),
	|		ТИП(Документ.КорректировкаЗадолженности)
	|		)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.ОбъектРасчетов,
	|	ТаблицаИзменений.Валюта,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) + СУММА(ТаблицаИзменений.ОтгружаетсяИзменение) < 0
	|	ИЛИ СУММА(ТаблицаИзменений.ДопустимаяСуммаЗадолженностиИзменение) > 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Регистратор                                  КАК Регистратор,
	|	ТаблицаИзменений.ОбъектРасчетов                               КАК ОбъектРасчетов,
	|	ТаблицаИзменений.Валюта                                       КАК Валюта,
	|	СУММА(ТаблицаИзменений.СуммаИзменение)                        КАК СуммаИзменение,
	|	СУММА(ТаблицаИзменений.ОтгружаетсяИзменение)                  КАК ОтгружаетсяИзменение,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам                    КАК АналитикаУчетаПоПартнерам
	|ПОМЕСТИТЬ ДвиженияРасчетыСКлиентамиИзменениеКонтрольСрока
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Регистратор                              КАК Регистратор,
	|		Таблица.ОбъектРасчетов                           КАК ОбъектРасчетов,
	|		Таблица.Валюта                                   КАК Валюта,
	|		Таблица.СуммаПередЗаписью                        КАК СуммаИзменение,
	|		Таблица.ОтгружаетсяПередЗаписью                  КАК ОтгружаетсяИзменение,
	|		Таблица.АналитикаУчетаПоПартнерам                КАК АналитикаУчетаПоПартнерам
	|	ИЗ
	|		РасчетыСКлиентамиПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Регистратор                   КАК Регистратор,
	|		Таблица.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		Таблица.Валюта                        КАК Валюта,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|				ВЫБОР КОГДА Таблица.Сумма < 0 ТОГДА
	|						0
	|					ИНАЧЕ Таблица.Сумма
	|				КОНЕЦ
	|			ИНАЧЕ -Таблица.Сумма
	|		КОНЕЦ                                 КАК СуммаИзменение,
	|		ВЫБОР КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) ТОГДА
	|				0
	|			ИНАЧЕ -Таблица.Отгружается
	|		КОНЕЦ                                 КАК ОтгружаетсяИзменение,
	|		Таблица.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	|		И Таблица.ОбъектРасчетов.Объект <> НЕОПРЕДЕЛЕНО
	|
	|) КАК ТаблицаИзменений
	|
	|ГДЕ
	|	&ПроведениеДокумента
	|	И НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|	И НЕ ТИПЗНАЧЕНИЯ(ТаблицаИзменений.Регистратор) В (
	|		ТИП(Документ.ПоступлениеБезналичныхДенежныхСредств),
	|		ТИП(Документ.ПриходныйКассовыйОрдер),
	|		ТИП(Документ.ОперацияПоПлатежнойКарте),
	|		ТИП(Документ.ВзаимозачетЗадолженности),
	|		ТИП(Документ.КорректировкаЗадолженности)
	|		)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Регистратор,
	|	ТаблицаИзменений.ОбъектРасчетов,
	|	ТаблицаИзменений.Валюта,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.СуммаИзменение) < 0
	|	ИЛИ СУММА(ТаблицаИзменений.ОтгружаетсяИзменение) < 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ)        КАК Месяц,
	|	Таблица.Период                              КАК Период,
	|	Таблица.ПорядокОперации                     КАК ПорядокОперации,
	|	Таблица.ПорядокЗачетаПоДатеПлатежа          КАК ПорядокЗачетаПоДатеПлатежа,
	|	Таблица.АналитикаУчетаПоПартнерам           КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов                      КАК ОбъектРасчетов,
	|	Таблица.Валюта                              КАК ВалютаРасчетов,
	|	Таблица.Регистратор                         КАК Документ,
	|	СУММА(Таблица.Сумма)                        КАК Сумма,
	|	СУММА(Таблица.Оплачивается)                 КАК Оплачивается,
	|	СУММА(Таблица.КОплате)                      КАК КОплате,
	|	СУММА(Таблица.КОтгрузке)                    КАК КОтгрузке,
	|	СУММА(Таблица.СуммаРегл)                    КАК СуммаРегл,
	|	СУММА(Таблица.СуммаУпр)                     КАК СуммаУпр,
	|	ЕСТЬNULL(Ключи.Организация,Неопределено)    КАК Организация,
	|	Таблица.АналитикаУчетаПоПартнерамПриемник   КАК АналитикаУчетаПоПартнерамПриемник,
	|	Таблица.ОбъектРасчетовПриемник              КАК ОбъектРасчетовПриемник,
	|	Таблица.ВалютаПриемник                      КАК ВалютаПриемник,
	|	Таблица.ПоДаннымОбъектаРасчетовИсточника    КАК ПоДаннымОбъектаРасчетовИсточника,
	|	ВЫБОР КОГДА Таблица.Сторно
	|			ТОГДА Таблица.СвязанныйДокумент
	|		ИНАЧЕ Таблица.Заказ
	|	КОНЕЦ                                       КАК Заказ,
	|	Таблица.НастройкаХозяйственнойОперации      КАК НастройкаХозяйственнойОперации,
	|	Таблица.РасчетныйДокумент                   КАК РасчетныйДокумент,
	|	Таблица.ДатаПлатежа                         КАК ДатаПлатежа,
	|	Таблица.ДатаРегистратора                    КАК ДатаРегистратора,
	|	Таблица.ИдентификаторФинЗаписи              КАК ИдентификаторФинЗаписи,
	|	Таблица.Сторно                              КАК Сторно,
	|	&СоздаватьЗаданияКЗакрытиюСледующегоМесяца  КАК СоздаватьЗаданияКЗакрытиюСледующегоМесяца,
	|	&РассчитыватьИзменения                      КАК ВызыватьИсключение,
	|	ЛОЖЬ                                        КАК НачальноеЗаполнение
	|ПОМЕСТИТЬ РасчетыСКлиентамиИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Расчеты.Регистратор                   КАК Регистратор,
	|		Расчеты.Период                        КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		Расчеты.Валюта                        КАК Валюта,
	|		Расчеты.Сумма                         КАК Сумма,
	|		Расчеты.Оплачивается                  КАК Оплачивается,
	|		Расчеты.Отгружается                   КАК Отгружается,
	|		Расчеты.КОплате                       КАК КОплате,
	|		Расчеты.КОтгрузке                     КАК КОтгрузке,
	|		Расчеты.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		Расчеты.СуммаРегл                     КАК СуммаРегл,
	|		Расчеты.СуммаУпр                      КАК СуммаУпр,
	|		Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
	|				И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма > 0
	|			ТОГДА 0
	|			КОГДА ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
	|				И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма < 0
	|			ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ИндексДвиженияВзаимозачета,
	|		Расчеты.ПорядокОперации                КАК ПорядокОперации,
	|		Расчеты.ПорядокЗачетаПоДатеПлатежа     КАК ПорядокЗачетаПоДатеПлатежа,
	|		Расчеты.РасчетныйДокумент              КАК РасчетныйДокумент,
	|		Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|		Расчеты.ОбъектРасчетовПриемник         КАК ОбъектРасчетовПриемник,
	|		Расчеты.ВалютаПриемник                 КАК ВалютаПриемник,
	|		Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
	|		Расчеты.ВалютаДокумента                КАК ВалютаДокумента,
	|		Расчеты.ВариантОплаты                  КАК ВариантОплаты,
	|		Расчеты.ДатаПлатежа                    КАК ДатаПлатежа,
	|		Расчеты.ДатаРегистратора               КАК ДатаРегистратора,
	|		Расчеты.ПродажаПоЗаказу                КАК Заказ,
	|		Расчеты.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|		Расчеты.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|		Расчеты.СвязанныйДокумент              КАК СвязанныйДокумент,
	|		Расчеты.Сторно                         КАК Сторно
	|	ИЗ РасчетыСКлиентамиИсходныеДвижения КАК Расчеты
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|		
	|	ВЫБРАТЬ
	|		Расчеты.Регистратор                   КАК Регистратор,
	|		Расчеты.Период                        КАК Период,
	|		Расчеты.АналитикаУчетаПоПартнерам     КАК АналитикаУчетаПоПартнерам,
	|		Расчеты.ОбъектРасчетов                КАК ОбъектРасчетов,
	|		Расчеты.Валюта                        КАК Валюта,
	|		-Расчеты.Сумма                        КАК Сумма,
	|		-Расчеты.Оплачивается                 КАК Оплачивается,
	|		-Расчеты.Отгружается                  КАК Отгружается,
	|		-Расчеты.КОплате                      КАК КОплате,
	|		-Расчеты.КОтгрузке                    КАК КОтгрузке,
	|		Расчеты.ХозяйственнаяОперация         КАК ХозяйственнаяОперация,
	|		-Расчеты.СуммаРегл                    КАК СуммаРегл,
	|		-Расчеты.СуммаУпр                     КАК СуммаУпр,
	|		Расчеты.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
	|		ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
	|				И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма > 0
	|			ТОГДА 0
	|			КОГДА ТИПЗНАЧЕНИЯ(Расчеты.Регистратор) = ТИП(Документ.ВзаимозачетЗадолженности)
	|				И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Расчеты.Сумма < 0
	|			ТОГДА -1
	|			ИНАЧЕ 1
	|		КОНЕЦ КАК ИндексДвиженияВзаимозачета,
	|		Расчеты.ПорядокОперации                КАК ПорядокОперации,
	|		Расчеты.ПорядокЗачетаПоДатеПлатежа     КАК ПорядокЗачетаПоДатеПлатежа,
	|		Расчеты.РасчетныйДокумент              КАК РасчетныйДокумент,
	|		Расчеты.АналитикаУчетаПоПартнерамПриемник КАК АналитикаУчетаПоПартнерамПриемник,
	|		Расчеты.ОбъектРасчетовПриемник         КАК ОбъектРасчетовПриемник,
	|		Расчеты.ВалютаПриемник                 КАК ВалютаПриемник,
	|		Расчеты.ПоДаннымОбъектаРасчетовИсточника КАК ПоДаннымОбъектаРасчетовИсточника,
	|		Расчеты.ВалютаДокумента                КАК ВалютаДокумента,
	|		Расчеты.ВариантОплаты                  КАК ВариантОплаты,
	|		Расчеты.ДатаПлатежа                    КАК ДатаПлатежа,
	|		Расчеты.ДатаРегистратора               КАК ДатаРегистратора,
	|		Расчеты.ПродажаПоЗаказу                КАК Заказ,
	|		Расчеты.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|		Расчеты.ИдентификаторФинЗаписи         КАК ИдентификаторФинЗаписи,
	|		Расчеты.СвязанныйДокумент              КАК СвязанныйДокумент,
	|		Расчеты.Сторно                         КАК Сторно
	|	ИЗ РегистрНакопления.РасчетыСКлиентами КАК Расчеты
	|	ГДЕ Расчеты.Регистратор = &Регистратор
	|) КАК Таблица
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК Ключи
	|	ПО Ключи.Ссылка = Таблица.АналитикаУчетаПоПартнерам
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов,
	|	Таблица.Валюта,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.СтатьяДвиженияДенежныхСредств,
	|	Таблица.ИндексДвиженияВзаимозачета,
	|	Таблица.РасчетныйДокумент,
	|	Таблица.ВалютаДокумента,
	|	Таблица.ВариантОплаты,
	|	Таблица.ДатаПлатежа,
	|	Таблица.ДатаРегистратора,
	|	Ключи.Организация,
	|	Таблица.АналитикаУчетаПоПартнерамПриемник,
	|	Таблица.ОбъектРасчетовПриемник,
	|	Таблица.ВалютаПриемник,
	|	Таблица.ПоДаннымОбъектаРасчетовИсточника,
	|	Таблица.ПорядокОперации,
	|	Таблица.ПорядокЗачетаПоДатеПлатежа,
	|	ВЫБОР КОГДА Таблица.Сторно
	|			ТОГДА Таблица.СвязанныйДокумент
	|		ИНАЧЕ Таблица.Заказ
	|	КОНЕЦ,
	|	Таблица.НастройкаХозяйственнойОперации,
	|	Таблица.ИдентификаторФинЗаписи,
	|	Таблица.Сторно
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Сумма) <> 0
	|	ИЛИ СУММА(Таблица.СуммаРегл) <> 0
	|	ИЛИ СУММА(Таблица.СуммаУпр) <> 0
	|	ИЛИ СУММА(Таблица.КОплате) <> 0
	|	ИЛИ СУММА(Таблица.КОтгрузке) <> 0
	|	ИЛИ СУММА(Таблица.Оплачивается) <> 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаИзменений.Документ                  КАК Регистратор,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаИзменений.ОбъектРасчетов            КАК ОбъектРасчетов,
	|	ТаблицаИзменений.Период                    КАК Период,
	|	ТаблицаИзменений.Сумма                     КАК Сумма
	|	
	|ПОМЕСТИТЬ РасчетыСКлиентамиИзмененияВводОстатков
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК ТаблицаИзменений
	|ГДЕ
	|	&РассчитыватьИзменения
	|	И ТаблицаИзменений.Сумма < 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ОбъектРасчетов,
	|	Таблица.Валюта,
	|	Таблица.ЗаявкаНаРасходованиеДенежныхСредств
	|ПОМЕСТИТЬ ДвиженияРасчетыСКлиентамиИзменениеОплачивается
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ОбменДанными
	|	И &РассчитыватьИзменения
	|	И Таблица.Оплачивается <> 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаИзменений.Месяц КАК Месяц,
	|	ТаблицаИзменений.Период КАК Период,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаИзменений.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаИзменений.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ТаблицаИзменений.Документ КАК Документ,
	|	ТаблицаИзменений.Организация КАК Организация,
	|	ТаблицаИзменений.Заказ КАК Заказ,
	|	ТаблицаИзменений.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
	|	ТаблицаИзменений.РасчетныйДокумент КАК РасчетныйДокумент,
	|	ТаблицаИзменений.ДатаПлатежа КАК ДатаПлатежа,
	|	ТаблицаИзменений.ДатаРегистратора КАК ДатаРегистратора,
	|	ТаблицаИзменений.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
	|	ТаблицаИзменений.Сторно КАК Сторно,
	|	ТаблицаИзменений.СоздаватьЗаданияКЗакрытиюСледующегоМесяца КАК СоздаватьЗаданияКЗакрытиюСледующегоМесяца,
	|	СУММА(ТаблицаИзменений.Сумма) КАК Сумма,
	|	СУММА(ТаблицаИзменений.СуммаРегл) КАК СуммаРегл,
	|	СУММА(ТаблицаИзменений.СуммаУпр) КАК СуммаУпр
	|ПОМЕСТИТЬ ТаблицаИзмененийРасчетыСКлиентами
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК ТаблицаИзменений
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Месяц,
	|	ТаблицаИзменений.Период,
	|	ТаблицаИзменений.АналитикаУчетаПоПартнерам,
	|	ТаблицаИзменений.ОбъектРасчетов,
	|	ТаблицаИзменений.ВалютаРасчетов,
	|	ТаблицаИзменений.Документ,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.Заказ,
	|	ТаблицаИзменений.НастройкаХозяйственнойОперации,
	|	ТаблицаИзменений.РасчетныйДокумент,
	|	ТаблицаИзменений.ДатаПлатежа,
	|	ТаблицаИзменений.ДатаРегистратора,
	|	ТаблицаИзменений.ИдентификаторФинЗаписи,
	|	ТаблицаИзменений.Сторно,
	|	ТаблицаИзменений.СоздаватьЗаданияКЗакрытиюСледующегоМесяца
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.Сумма) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаРегл) <> 0
	|	ИЛИ СУММА(ТаблицаИзменений.СуммаУпр) <> 0";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РасчетыСКлиентамиИзменения.Период КАК ПериодЗадания,
	|	СправочникАналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентамиИзменения.Документ КАК Документ,
	|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
	|ПОМЕСТИТЬ ОчередьДанныеКДопроведениюДокументовУпрБаланс
	|ИЗ
	|	РасчетыСКлиентамиИзменения КАК РасчетыСКлиентамиИзменения
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК СправочникАналитикаУчетаПоПартнерам
	|			ПО РасчетыСКлиентамиИзменения.АналитикаУчетаПоПартнерам = СправочникАналитикаУчетаПоПартнерам.Ссылка
	|ГДЕ 
	|	РасчетыСКлиентамиИзменения.СуммаУпр <> 0
	|		И &НовыеРасчеты
	|		И &ФормироватьУправленческийБаланс
	|		И &ЭтоГлавныйУзел";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	ВременныеТаблицыКУничтожению.Добавить("ОчередьДанныеКДопроведениюДокументовУпрБаланс");
	
	#Область ТекстЗапросаОперативноеРаспределениеВзаиморасчетов
	
	Если ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено 
		И Не ВключенаНастройкаФоновогоРаспределенияВзаиморасчетов Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	МИНИМУМ(ДвиженияДокумента.ЭтоЗаказ)
		|	ИЛИ МАКСИМУМ(ДвиженияДокумента.ЭтоУвеличениеЗадолженности) КАК НеобходимоСмотретьДолги,
		|	МИНИМУМ(ДвиженияДокумента.ЭтоЗаказ)
		|	ИЛИ МАКСИМУМ(ДвиженияДокумента.ЭтоУменьшениеЗадолженности) КАК НеобходимоСмотретьПлатежи
		|ПОМЕСТИТЬ УсловияПроверок
		|ИЗ
		|	(ВЫБРАТЬ
		|		Расчеты.Сумма = 0 КАК ЭтоЗаказ,
		|
		|		(Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Расчеты.Сумма > 0)
		|		ИЛИ 
		|		(Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И Расчеты.Сумма < 0) КАК ЭтоУвеличениеЗадолженности,
		|
		|		(Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			И Расчеты.Сумма > 0)
		|		ИЛИ 
		|		(Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			И Расчеты.Сумма < 0) КАК ЭтоУменьшениеЗадолженности
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|	ГДЕ
		|		Расчеты.Регистратор = &Регистратор
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ИСТИНА КАК ЭтоЗаказ,
		|		ЛОЖЬ КАК ЭтоУвеличениеЗадолженности,
		|		ЛОЖЬ КАК ЭтоУменьшениеЗадолженности) КАК ДвиженияДокумента";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	СУММА(ТаблицаОстатки.КоличествоПредоплат) >= &КоличествоЗаписейОстатков КАК МногоПредоплат,
		|	СУММА(ТаблицаОстатки.КоличествоДолгов) >= &КоличествоЗаписейОстатков КАК МногоДолгов
		|ПОМЕСТИТЬ ЧислоЗаписейОстатки
		|ИЗ
		|	(ВЫБРАТЬ
		|		РасчетыПоСрокамОстатки.РасчетныйДокумент КАК РасчетныйДокумент,
		|		РасчетыПоСрокамОстатки.ДатаПлановогоПогашения КАК ДатаПлановогоПогашения,
		|		РасчетыПоСрокамОстатки.ДатаВозникновения КАК ДатаВозникновения,
		|		ВЫБОР
		|			КОГДА РасчетыПоСрокамОстатки.ПредоплатаОстаток <> 0
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоПредоплат,
		|		ВЫБОР
		|			КОГДА РасчетыПоСрокамОстатки.ДолгОстаток <> 0
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоДолгов
		|	ИЗ
		|		РегистрНакопления.РасчетыСКлиентамиПоСрокам.Остатки(&ПериодОстатков, (АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта) В
		|			(ВЫБРАТЬ
		|				Расчеты.АналитикаУчетаПоПартнерам,
		|				Расчеты.ОбъектРасчетов,
		|				Расчеты.ВалютаРасчетов
		|			ИЗ
		|				РасчетыСКлиентамиИзменения КАК Расчеты)
		|		И ИСТИНА В
		|			(ВЫБРАТЬ
		|				УсловияПроверок.НеобходимоСмотретьПлатежи
		|			ИЗ
		|				УсловияПроверок КАК УсловияПроверок)) КАК РасчетыПоСрокамОстатки) КАК ТаблицаОстатки";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|		КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Расчеты.Сумма > 0
		|		ИЛИ Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И Расчеты.Сумма < 0
		|			ТОГДА Расчеты.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ) >= &КоличествоДокументовВпереди КАК МногоДолгов,
		|
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВЫБОР
		|		КОГДА Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|		И Расчеты.Сумма > 0
		|		ИЛИ Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		И Расчеты.Сумма < 0
		|			ТОГДА Расчеты.Регистратор
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ) >= &КоличествоДокументовВпереди КАК МногоПлатежей
		|
		|ПОМЕСТИТЬ ДокументыВпереди
		|ИЗ
		|	РасчетыСКлиентамиИзменения КАК ТаблицаИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|		ПО ТаблицаИзменений.АналитикаУчетаПоПартнерам = Расчеты.АналитикаУчетаПоПартнерам
		|		И ТаблицаИзменений.ОбъектРасчетов = Расчеты.ОбъектРасчетов
		|		И ТаблицаИзменений.ВалютаРасчетов = Расчеты.Валюта
		|		И (Расчеты.Период >= НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, ДЕНЬ))
		|		И (Расчеты.Активность)
		|ГДЕ
		|	НЕ Расчеты.Сумма = 0";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	МАКСИМУМ(УсловияРаспределения.ФоновоеРаспределениеВзаиморасчетов) КАК ФоновоеРаспределениеВзаиморасчетов
		|ПОМЕСТИТЬ РежимРаспределенияВзаиморасчетов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ВЫБОР
		|			КОГДА УсловияПроверок.НеобходимоСмотретьПлатежи
		|			И (ЕСТЬNULL(ДокументыВпереди.МногоДолгов, ЛОЖЬ)
		|			ИЛИ ЕСТЬNULL(ЧислоЗаписейОстатки.МногоДолгов, ЛОЖЬ)
		|			ИЛИ ЕСТЬNULL(ДокументыВпереди.МногоПлатежей, ЛОЖЬ)
		|			И НЕ ЕСТЬNULL(ЧислоЗаписейОстатки.МногоДолгов, ЛОЖЬ))
		|				ТОГДА ИСТИНА
		|			КОГДА УсловияПроверок.НеобходимоСмотретьДолги
		|			И ЕСТЬNULL(ДокументыВпереди.МногоДолгов, ЛОЖЬ)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ КАК ФоновоеРаспределениеВзаиморасчетов
		|	ИЗ
		|		УсловияПроверок КАК УсловияПроверок
		|			ЛЕВОЕ СОЕДИНЕНИЕ ЧислоЗаписейОстатки КАК ЧислоЗаписейОстатки
		|			ПО ИСТИНА
		|			ЛЕВОЕ СОЕДИНЕНИЕ ДокументыВпереди КАК ДокументыВпереди
		|			ПО ИСТИНА
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА
		|	ИЗ
		|		РасчетыСКлиентамиИзменения КАК ТаблицаИзменений
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК Задания
		|			ПО ТаблицаИзменений.АналитикаУчетаПоПартнерам = Задания.АналитикаУчетаПоПартнерам
		|			И ТаблицаИзменений.ОбъектРасчетов = Задания.ОбъектРасчетов
		|			И ТаблицаИзменений.ВалютаРасчетов = Задания.Валюта) КАК УсловияРаспределения";
		
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ УсловияПроверок");
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ЧислоЗаписейОстатки");
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ ДокументыВпереди");
		
		ВременныеТаблицыКУничтожению.Добавить("РежимРаспределенияВзаиморасчетов");
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ТекстЗапросаЗаданияРаспределениеВзаиморасчетов
	
	Если ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено
		Или Не ВыполнятьРаспределениеВзаиморасчетовВТранзакции Тогда
		
		ТекстЗапроса = РегистрыНакопления.РасчетыСКлиентами.ТекстЗапросаДанныеКРегистрацииЗаданийРаспределенияВзаиморасчетов();
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ МинимальныеПериодыИзмененийКлиенты");
		
		ВременныеТаблицыКУничтожению.Добавить("ЗаданияКРаспределениюВзаиморасчетовКлиенты");
		
	КонецЕсли;

	#КонецОбласти
	
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ РасчетыСКлиентамиПередЗаписью");
	МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ РасчетыСКлиентамиИсходныеДвижения");
	
	#КонецОбласти
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.УстановитьПараметр("Регистратор",              Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ПроведениеДокумента",      ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.Проведение);
	Запрос.УстановитьПараметр("ОбменДанными",             ОбменДанными.Загрузка);
	Запрос.УстановитьПараметр("РассчитыватьИзменения",    ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства)
		И ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ВводОстатков")
		И ТипЗнч(Отбор.Регистратор.Значение) <> Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
	Запрос.УстановитьПараметр("СоздаватьЗаданияКЗакрытиюСледующегоМесяца", ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
	Запрос.УстановитьПараметр("НовыеРасчеты", НоваяАрхитектураВзаиморасчетов);
	Запрос.УстановитьПараметр("ТипыРегистраторовБезПроверкиИзменения",  ОперативныеВзаиморасчетыСервер.СписокТиповРегистраторовПланов());
	Запрос.УстановитьПараметр("ФормироватьУправленческийБаланс", ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс"));
	Запрос.УстановитьПараметр("ЭтоГлавныйУзел", ПланыОбмена.ГлавныйУзел() = Неопределено);
	
	Если ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено
		И Не ВключенаНастройкаФоновогоРаспределенияВзаиморасчетов Тогда
		
		ТекстЗамены = "
		|ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			РежимРаспределения.ФоновоеРаспределениеВзаиморасчетов
		|		ИЗ
		|			РежимРаспределенияВзаиморасчетов КАК РежимРаспределения)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СоздаватьЗаданияКРаспределениюВзаиморасчетов", ТекстЗамены);
		Запрос.УстановитьПараметр("ПериодОстатков", ДатаДокумента);
		
		УсловияРаспределенияВзаиморасчетовВТранзакции = ОперативныеВзаиморасчетыСервер.УсловияДляРаспределенияВзаиморасчетовВТранзакцииДокумента();
		Запрос.УстановитьПараметр("КоличествоЗаписейОстатков",
			УсловияРаспределенияВзаиморасчетовВТранзакции.КоличествоЗаписейОстатков);
		Запрос.УстановитьПараметр("КоличествоДокументовВпереди",
			УсловияРаспределенияВзаиморасчетовВТранзакции.КоличествоДокументовВпереди);
		
	Иначе
		Запрос.УстановитьПараметр("СоздаватьЗаданияКРаспределениюВзаиморасчетов", Не ВыполнятьРаспределениеВзаиморасчетовВТранзакции);
	КонецЕсли;

	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРасчетыСКлиентамиИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	Выборка = Результат[1].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРасчетыСКлиентамиИзменениеСуммыДолга", Выборка.Следующий() И Выборка.Количество > 0);
	Выборка = Результат[2].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРасчетыСКлиентамиИзменениеКонтрольСрока", Выборка.Следующий() И Выборка.Количество > 0);
	Выборка = Результат[3].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"РасчетыСКлиентамиИзменения", Выборка.Следующий() И Выборка.Количество > 0);
	Выборка = Результат[4].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"РасчетыСКлиентамиИзмененияВводОстатков", Выборка.Следующий() И Выборка.Количество > 0);
	Выборка = Результат[5].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияРасчетыСКлиентамиИзменениеОплачивается", Выборка.Следующий() И Выборка.Количество > 0);
	
	// Таблица контроля для формирования заданий к закрытию месяца
	Выборка = Результат[6].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ТаблицаИзмененийРасчетыСКлиентами", Выборка.Следующий() И Выборка.Количество > 0);
		
	Если ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Неопределено Тогда

		ТаблицаУсловий = Запрос.МенеджерВременныхТаблиц.Таблицы["РежимРаспределенияВзаиморасчетов"].ПолучитьДанные().Выгрузить();
		
		ВыполнятьРаспределениеВзаиморасчетовВТранзакции = Не ТаблицаУсловий[0].ФоновоеРаспределениеВзаиморасчетов;
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("СвойстваДвиженийМеханизмов")
		И ДополнительныеСвойства.СвойстваДвиженийМеханизмов.Свойство("Взаиморасчеты") Тогда
		
		ДополнительныеСвойства.СвойстваДвиженийМеханизмов.Взаиморасчеты.Вставить(
			"ВыполнятьРаспределениеВзаиморасчетовВТранзакции", ВыполнятьРаспределениеВзаиморасчетовВТранзакции);
			
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("НеСоздаватьЗаданияКРаспределениюВзаиморасчетов",
		ВыполнятьРаспределениеВзаиморасчетовВТранзакции);

	Если ОбменДанными.Загрузка Тогда
		
		Если НоваяАрхитектураВзаиморасчетов Тогда
			
			РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьЗаданияПриОбменеДанными(ЭтотОбъект,
				Запрос.МенеджерВременныхТаблиц);
				
			Если ПланыОбмена.ГлавныйУзел() = Неопределено 
				И ДополнительныеСвойства.Свойство("ОбменРИБСоздаватьЗаданияНеоперативнойОчереди") Тогда
				
				ВидыЗаданий = Новый Массив;
				ВидыЗаданий.Добавить(Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу);
				РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.СоздатьЗаданияПриОбменеДанными(ЭтотОбъект,
					ВидыЗаданий, Запрос.МенеджерВременныхТаблиц);
					
			КонецЕсли;
			
		ИначеЕсли Константы.НачатПереходНаНовуюАрхитектуруВзаиморасчетов.Получить() Тогда
			
			// Задания к распределению расчетов формируются здесь для распределения расчетов при переходе
			ПараметрыИзменений = ОперативныеВзаиморасчетыСервер.ПараметрыРаспределенияРасчетов();
			ПараметрыИзменений.Регистратор = Отбор.Регистратор.Значение;
			ПараметрыИзменений.Загрузка = Истина;
			ПараметрыИзменений.ТаблицаИзменений = ОперативныеВзаиморасчетыСервер.ТаблицаИзмененийДляПересчета(
			Запрос.МенеджерВременныхТаблиц, Отбор.Регистратор.Значение);
			ПараметрыИзменений.РаспределениеСУчетомПриемников = Истина;
			ОперативныеВзаиморасчетыСервер.ЗарегистрироватьИзмененияКОтложенномуРаспределению(ПараметрыИзменений);

		КонецЕсли;
		
		РегистрыСведений.ВспомогательнаяИнформацияВзаиморасчетов.ЗаполнитьВспомогательнуюИнформацию(
				Запрос.МенеджерВременныхТаблиц, Истина);
	Иначе
		
		Если НоваяАрхитектураВзаиморасчетов Тогда

			НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект,
				Запрос.МенеджерВременныхТаблиц);

		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц, ВременныеТаблицыКУничтожению);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует таблицу объектов расчетов, которые были раньше в движениях и которые сейчас будут записаны.
//
Функция СформироватьТаблицуОбъектовОплаты()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ТаблицаНовыхОбъектовРасчетов", Выгрузить(, "ОбъектРасчетов"));
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов
	|ПОМЕСТИТЬ ОбъектыРасчетов
	|ИЗ &ТаблицаНовыхОбъектовРасчетов КАК Таблица
	|;
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЕСТЬNULL(Таблица.ОбъектРасчетов.Объект, &Регистратор) КАК ОбъектОплаты,
	|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И Таблица.ОбъектРасчетов <> ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбъектыРасчетов.ОбъектРасчетов.Объект КАК ОбъектОплаты,
	|	ОбъектыРасчетов.ОбъектРасчетов КАК ОбъектРасчетов
	|ИЗ ОбъектыРасчетов
	|ГДЕ
	|	ОбъектыРасчетов.ОбъектРасчетов.Объект <> Неопределено
	|";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗагрузитьСОбработкой(ТаблицаРасчетов) Экспорт
	
	ВзаиморасчетыСервер.ДобавитьЗаполнитьПорядокОперации(ТаблицаРасчетов, ТипЗнч(Отбор.Регистратор.Значение));
	Загрузить(ТаблицаРасчетов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
