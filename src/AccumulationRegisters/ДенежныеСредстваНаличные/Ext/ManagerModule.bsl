#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийДенежныеСредстваНаличные"; 
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	Возврат СтруктураТекстовЗапросов

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Касса)
	|	И ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "Касса.ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымМестоУчета = "Касса";
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "Касса.НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "Касса.Подразделение";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Касса.ВалютаДенежныхСредств";
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("Сумма");
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.ДенежныеСредства;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.ДенежныеСредства.ПутьКДанным = "Касса";
	ПараметрыОтражения.СтруктураАналитики.ТипДенежныхСредств.Вставить("Значение", Перечисления.ТипыДенежныхСредств.Наличные);
	ПараметрыОтражения.СтруктураАналитики.СтатьяКалькуляции.Вставить("Значение", Справочники.СтатьиКалькуляции.ПустаяСсылка());
	ПараметрыОтражения.СтруктураАналитики.ПодразделениеПлатежа.ПутьКДанным = "Подразделение";
	//++ Локализация
	ПараметрыОтражения.СтруктураАналитики.ТипПлатежаФЗ275.Вставить("Значение", Справочники.ТипыПлатежейФЗ275.ПустаяСсылка());
	ПараметрыОтражения.СтруктураАналитики.СтатьяЦелевыхСредств.Вставить("Значение", Неопределено);
	//-- Локализация
	
	ПараметрыОтражения.ПутьКДаннымДопНастройкаХозОперации = СтрЗаменить(
		"ВЫБОР
		|		КОГДА ПсевдонимИсточникаДанных.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствОтКонвертацииВалюты)
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ПоступлениеДенежныхСредствОтКонвертацииВалюты)
		|		ИНАЧЕ ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации
		|	КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	
	//-- НЕ УТ
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	Если Не ДанныеКРегистрацииЗаданий = Неопределено Тогда
		Возврат ДанныеКРегистрацииЗаданий;
	КонецЕсли;
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// Заполняет сведения об обработчиках обновления.
// 
// Параметры:
//  Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.ДенежныеСредстваНаличные.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.23.19";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("6d6669d9-63e2-41f3-baf6-4d846d7010a7");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.ДенежныеСредстваНаличные.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий =
		НСтр("ru = 'Заполнение реквизита ""Подразделение"" в регистре накопления ""Денежные средства (наличные)"".';
			|en = 'Fill the ""Business unit"" attribute in the ""Cash"" accumulation register.'");
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки; // см. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки
	ПолныеИменаОбъектов = Новый Массив;
	ПолныеИменаОбъектов.Добавить(Метаданные.Документы.ПриходныйКассовыйОрдер.ПолноеИмя());
	ПолныеИменаОбъектов.Добавить(Метаданные.Документы.РасходныйКассовыйОрдер.ПолноеИмя());
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(ПолныеИменаОбъектов, ",");
	ПараметрыВыборки.ПолныеИменаРегистров = Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПараметрыВыборки.ПолныеИменаРегистров;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|	И ДвиженияРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика))
		|	И ДвиженияРегистра.ИдентификаторФинЗаписи <> """"
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ГДЕ
		|				ДвиженияРегистра.Регистратор = ТаблицаРасшифровкаПлатежа.Ссылка
		|				И ДвиженияРегистра.ИдентификаторФинЗаписи = ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки
		|				И ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|	И ДвиженияРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным))
		|	И ДвиженияРегистра.ИдентификаторФинЗаписи <> """"
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ГДЕ
		|				ДвиженияРегистра.Регистратор = ТаблицаРасшифровкаПлатежа.Ссылка
		|				И ДвиженияРегистра.ИдентификаторФинЗаписи = ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки
		|				И ЕСТЬNULL(ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.Подразделение, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|	И ДвиженияРегистра.ХозяйственнаяОперация В (ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка), ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствОтКонвертацииВалюты))
		|	И ВЫРАЗИТЬ(ДвиженияРегистра.Регистратор КАК Документ.ПриходныйКассовыйОрдер).Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|	И ДвиженияРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств)
		|	И ДвиженияРегистра.ИдентификаторФинЗаписи <> """"
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
		|			ГДЕ
		|				ДвиженияРегистра.Регистратор = ТаблицаРасшифровкаПлатежа.Ссылка
		|				И ДвиженияРегистра.ИдентификаторФинЗаписи = ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки
		|				И ТаблицаРасшифровкаПлатежа.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|	И ДвиженияРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы)
		|	И ДвиженияРегистра.Касса.Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДвиженияРегистра.Регистратор
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваНаличные КАК ДвиженияРегистра
		|ГДЕ
		|	ДвиженияРегистра.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ДвиженияРегистра.Регистратор ССЫЛКА Документ.РасходныйКассовыйОрдер
		|	И ВЫРАЗИТЬ(ДвиженияРегистра.Регистратор КАК Документ.РасходныйКассовыйОрдер).Подразделение <> ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор"), ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры обработчика:
//   * ВерсияПодсистемыНаНачалоОбновления - Строка - версия подсистемы.
//   * ИмяОбработчика - Строка - имя обработчика.
//   * ОбновляемыеДанные - Структура.
//   * ОбработкаЗавершена - Булево, Неопределено - признак завершения обработки.
//   * Очередь - Число - очередь.
//   * ПрогрессВыполнения - Структура:
//     ** ВсегоОбъектов - Число - всего обработано объектов.
//     ** ОбработаноОбъектов - Число - обработано объектов.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.ПолноеИмя();
	ИмяРегистра = Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.Имя;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоДвижения = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
		
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Не удалось заполнить реквизит ""Подразделение"" в регистре накопления ""Денежные средства (наличные)"".';
								|en = 'Cannot fill the ""Business unit"" attribute in the ""Cash"" accumulation register.'"));
		
	ТекстЗапросаПриходныйКассовыйОрдер = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
	|	Документ.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|	И Документ.ХозяйственнаяОперация В (
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПогашениеЗаймаСотрудником),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыККМ),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствОтКонвертацииВалюты),
	|	ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
	|	Документ.Касса.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойКассы)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасшифровкаПлатежа.ОбъектРасчетов.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	ПО Документ.Ссылка = ТаблицаРасшифровкаПлатежа.Ссылка
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|	И Документ.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасшифровкаПлатежа.ДоговорКредитаДепозита.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	ПО Документ.Ссылка = ТаблицаРасшифровкаПлатежа.Ссылка
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|	И Документ.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	ТаблицаРасшифровкаПлатежа.ИдентификаторСтроки КАК ИдентификаторФинЗаписи,
	|	ТаблицаРасшифровкаПлатежа.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.ПриходныйКассовыйОрдер КАК Документ
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйКассовыйОрдер.РасшифровкаПлатежа КАК ТаблицаРасшифровкаПлатежа
	|	ПО Документ.Ссылка = ТаблицаРасшифровкаПлатежа.Ссылка
	|ГДЕ
	|	Документ.Ссылка = &Ссылка
	|	И Документ.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств)";
	
	ТекстЗапросаРасходныйКассовыйОрдер = 
	"ВЫБРАТЬ
	|	Документ.Ссылка КАК Ссылка,
	|	Документ.Исправление КАК Исправление,
	|	Документ.СторнируемыйДокумент КАК СторнируемыйДокумент,
	|	НЕОПРЕДЕЛЕНО КАК ИдентификаторФинЗаписи,
	|	Документ.Подразделение КАК Подразделение
	|ИЗ
	|	Документ.РасходныйКассовыйОрдер КАК Документ
	|ГДЕ
	|	Документ.Ссылка = &Ссылка";
	
	Для Каждого СтрокаТаблицы Из ОбновляемыеДанные Цикл
		
		ПричинаИсключения = 0;
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = 1; // Блокировка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра + ".НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицы.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки = Блокировка.Добавить(СтрокаТаблицы.Регистратор.Метаданные().ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", СтрокаТаблицы.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
				
			ПричинаИсключения = 2; // ПлохиеДанные
			Рекомендация = НСтр("ru = 'Перепроведите документ вручную.';
								|en = 'Repost the document manually.'");
			
			НаборЗаписей = РегистрыНакопления.ДенежныеСредстваНаличные.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(СтрокаТаблицы.Регистратор);
			НаборЗаписей.Прочитать();
			ТаблицаЗаписей = НаборЗаписей.Выгрузить();
			ТаблицаЗаписей.Индексы.Добавить("ИдентификаторФинЗаписи");
			ТаблицаЗаписейИзменена = Ложь;
			
			ТипРегистратора = ТипЗнч(СтрокаТаблицы.Регистратор);
			Если ТипРегистратора = Тип("ДокументСсылка.ПриходныйКассовыйОрдер") Тогда
				ТекстЗапроса = ТекстЗапросаПриходныйКассовыйОрдер;
			ИначеЕсли ТипРегистратора = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
				ТекстЗапроса = ТекстЗапросаРасходныйКассовыйОрдер;
			КонецЕсли;
			
			// Заполним подразделение в движениях Сторно = Ложь
			Запрос = Новый Запрос(ТекстЗапроса);
			Запрос.УстановитьПараметр("Ссылка", СтрокаТаблицы.Регистратор);
			
			//@skip-warning
			Выборка = Запрос.Выполнить().Выбрать();
			ЭтоИсправление = Ложь;
			СторнируемыйДокумент = Неопределено;
			Пока Выборка.Следующий() Цикл
				Если Не ЭтоИсправление И Выборка.Исправление Тогда
					ЭтоИсправление = Истина;
					СторнируемыйДокумент = Выборка.СторнируемыйДокумент;
				КонецЕсли;
				ЗаполнитьПодразделениеВТаблицеЗаписей(ТаблицаЗаписей, ТаблицаЗаписейИзменена, Выборка);
			КонецЦикла;
			
			Если ЭтоИсправление Тогда
				// Заполним подразделение в движениях Сторно = Истина
				Запрос.УстановитьПараметр("Ссылка", СторнируемыйДокумент);
				//@skip-warning
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					ЗаполнитьПодразделениеВТаблицеЗаписей(ТаблицаЗаписей, ТаблицаЗаписейИзменена, Выборка, Истина);
				КонецЦикла;
			КонецЕсли;
			
			ПричинаИсключения = 3; // Запись
			
			Если ТаблицаЗаписейИзменена Тогда
				НаборЗаписей.Загрузить(ТаблицаЗаписей);
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), СтрокаТаблицы.Регистратор);
			
			Если ПричинаИсключения = 2 Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					СтрокаТаблицы.Регистратор, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = 3 Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
	
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры, ДополнительныеПараметры);
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ЗаполнитьПодразделениеВТаблицеЗаписей(ТаблицаЗаписей, ТаблицаЗаписейИзменена, Выборка, Сторно = Ложь)
	
	СтруктураПоиска = Новый Структура("Сторно", Сторно);
	Если Выборка.ИдентификаторФинЗаписи <> Неопределено Тогда
		СтруктураПоиска.Вставить("ИдентификаторФинЗаписи", Выборка.ИдентификаторФинЗаписи);
	КонецЕсли;
	
	СтрокиТаблицыЗаписей = ТаблицаЗаписей.НайтиСтроки(СтруктураПоиска);
	Для каждого СтрокаТаблицы Из СтрокиТаблицыЗаписей Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Подразделение) И ЗначениеЗаполнено(Выборка.Подразделение) Тогда
			СтрокаТаблицы.Подразделение = Выборка.Подразделение;
			ТаблицаЗаписейИзменена = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
