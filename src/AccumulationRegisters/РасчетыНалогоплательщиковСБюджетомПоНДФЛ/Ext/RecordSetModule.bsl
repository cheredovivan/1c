#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() = 0 Или РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыБЗК.ЗаполнитьРегистраторНабораЗаписей(ЭтотОбъект, Замещение);
	
	УточнятьКрайнийСрокУплаты = Ложь;
	ДатаЗакона263ФЗ = УчетНДФЛ.ДатаЗакона263ФЗ();
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		Если СтрокаНабора.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			Если Не ЗначениеЗаполнено(СтрокаНабора.ДокументОснование) Тогда
				СтрокаНабора.ДокументОснование = СтрокаНабора.Регистратор;
			КонецЕсли;
		ИначеЕсли СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ЗачетАвансовыхПлатежей Или СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ПереданоНаВзысканиеВНалоговыйОрган Тогда	
			Если Не ЗначениеЗаполнено(СтрокаНабора.ДокументОснование) И ТипЗнч(СтрокаНабора.Регистратор) <> Тип("ДокументСсылка.ОперацияНалоговогоУчетаПоНДФЛ") Тогда
				СтрокаНабора.ДокументОснование = СтрокаНабора.Регистратор;
			КонецЕсли;
		ИначеЕсли СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.ВозвращеноНалоговымАгентом И СтрокаНабора.Период < ДатаЗакона263ФЗ Тогда	
			СтрокаНабора.КрайнийСрокУплаты = '00010101'
		ИначеЕсли СтрокаНабора.ВариантУдержания = Перечисления.ВариантыУдержанияНДФЛ.Удержано  Тогда	
			Если Не ЗначениеЗаполнено(СтрокаНабора.КрайнийСрокУплаты) Тогда
				УточнятьКрайнийСрокУплаты = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если УточнятьКрайнийСрокУплаты Тогда
		ГоловныеОрганизации = КоллекцииБЗК.УникальныеЗначенияКолонки(ЭтотОбъект, "ГоловнаяОрганизация");
		Если ГоловныеОрганизации.Количество() = 1 Тогда
			УчетНДФЛ.ПроставитьКрайнийСрокУплаты(ЭтотОбъект, ГоловныеОрганизации[0]);
		Иначе
			ВсеЗаписи = Выгрузить();
			Для Каждого ГоловнаяОрганизация Из ГоловныеОрганизации Цикл
				Найденные = ВсеЗаписи.НайтиСтроки(Новый Структура("ГоловнаяОрганизация", ГоловнаяОрганизация));
				УчетНДФЛ.ПроставитьКрайнийСрокУплаты(Найденные, ГоловнаяОрганизация);
			КонецЦикла;
			Загрузить(ВсеЗаписи);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли