#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;

	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|	НачальныеЗаписи.Валюта КАК Валюта,
		|	НачальныеЗаписи.РасчетныйДокумент КАК РасчетныйДокумент,
		|	НачальныеЗаписи.СуммаДисконтирования КАК СуммаДисконтирования,
		|	НачальныеЗаписи.СуммаДисконтированияРегл КАК СуммаДисконтированияРегл,
		|	НачальныеЗаписи.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
		|	НачальныеЗаписи.СтавкаДисконтирования КАК СтавкаДисконтирования,
		|	НачальныеЗаписи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|	НачальныеЗаписи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.ПроцентныеРасходыДисконтирования КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	СУММА(Таблица.СуммаДисконтированияУпр) КАК СуммаДисконтированияУпр
		|ПОМЕСТИТЬ ОчередьДанныеКДопроведениюДокументов
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		НачальныеЗаписи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		НачальныеЗаписи.Валюта КАК Валюта,
		|		НачальныеЗаписи.РасчетныйДокумент КАК РасчетныйДокумент,
		|		НачальныеЗаписи.СуммаДисконтирования КАК СуммаДисконтирования,
		|		НачальныеЗаписи.СуммаДисконтированияРегл КАК СуммаДисконтированияРегл,
		|		НачальныеЗаписи.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
		|		НачальныеЗаписи.СтавкаДисконтирования КАК СтавкаДисконтирования,
		|		НачальныеЗаписи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|		НачальныеЗаписи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Период КАК Период,
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|		Записи.ОбъектРасчетов КАК ОбъектРасчетов,
		|		Записи.Валюта КАК Валюта,
		|		Записи.РасчетныйДокумент КАК РасчетныйДокумент,
		|		-Записи.СуммаДисконтирования КАК СуммаДисконтирования,
		|		-Записи.СуммаДисконтированияРегл КАК СуммаДисконтированияРегл,
		|		-Записи.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр,
		|		Записи.СтавкаДисконтирования КАК СтавкаДисконтирования,
		|		Записи.ИдентификаторФинЗаписи КАК ИдентификаторФинЗаписи,
		|		Записи.НастройкаХозяйственнойОперации КАК НастройкаХозяйственнойОперации,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.ПроцентныеРасходыДисконтирования КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|		Таблица.Активность,
		|		Таблица.ВидДвижения,
		|		Таблица.Период,
		|		Таблица.Регистратор,
		|		Таблица.АналитикаУчетаПоПартнерам ,
		|		Таблица.ОбъектРасчетов,
		|		Таблица.Валюта,
		|		Таблица.РасчетныйДокумент,
		|		Таблица.СтавкаДисконтирования,
		|		Таблица.ИдентификаторФинЗаписи,
		|		Таблица.НастройкаХозяйственнойОперации,
		|		Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаДисконтирования) <> 0 ИЛИ СУММА(Таблица.СуммаДисконтированияРегл) <> 0
		|	ИЛИ СУММА(Таблица.СуммаДисконтированияУпр) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	АналитикаУчетаПоПартнерам
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		Запрос.Выполнить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект,
			Запрос.МенеджерВременныхТаблиц);
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,
			"ОчередьДанныеКДопроведениюДокументов");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли