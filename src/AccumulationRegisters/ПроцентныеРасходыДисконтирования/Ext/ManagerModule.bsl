
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	ПараметрыОтражения.ПутьКДаннымОрганизация = "АналитикаУчетаПоПартнерам.Организация";
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "ОбъектРасчетов.ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "ОбъектРасчетов.НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "ОбъектРасчетов.Подразделение";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Валюта";
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаДисконтированияУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаДисконтированияРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("СуммаДисконтирования");
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Контрагенты;
	
	//-- НЕ УТ
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

// Для отбора по типу в запросах
// 
// Возвращаемое значение:
//  Массив - типы документов, для которых применимо дисконтирование
//
Функция ПолучитьТипыДокументовУчаствующихВДисконтировании() Экспорт 
	
	ТипыДокументов = Новый Массив;
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.КорректировкаПриобретения"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.ПервичныйДокумент"));
	ТипыДокументов.Добавить(Тип("ДокументСсылка.Сторно"));
	
	Возврат ТипыДокументов;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Если Не МенеджерВременныхТаблиц = Неопределено 
		И МенеджерВременныхТаблиц.Таблицы.Найти("ОчередьДанныеКДопроведениюДокументов") <> Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СправочникАналитикаУчетаПоПартнерам.Организация КАК Организация,
		|	ИзмененныеДанные.ПериодЗадания КАК ПериодЗадания,
		|	ИзмененныеДанные.Документ КАК Документ,
		|	ИзмененныеДанные.СуммаДисконтированияУпр КАК СуммаДисконтированияУпр
		|ИЗ
		|	ОчередьДанныеКДопроведениюДокументов КАК ИзмененныеДанные
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаПоПартнерам КАК СправочникАналитикаУчетаПоПартнерам
		|		ПО ИзмененныеДанные.АналитикаУчетаПоПартнерам = СправочникАналитикаУчетаПоПартнерам.Ссылка";

		ДанныеКОбработке = Запрос.Выполнить().Выгрузить();

		Для Каждого Запись Из ДанныеКОбработке Цикл

			Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

				Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда
					
					Если Запись.СуммаДисконтированияУпр <> 0 Тогда
						НоваяСтрока = ТаблицаДанных.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
						НоваяСтрока.ВидЗадания = ТекущийВидЗадания;
					КонецЕсли;

				КонецЕсли;

				Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияКонтрагентКонтрагент Тогда

					Если ТипЗнч(Запись.Документ) = Тип("ДокументСсылка.РасчетПроцентныхРасходовДисконтирования") Тогда
						Продолжить;
					КонецЕсли;

					НоваяСтрока = ТаблицаДанных.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
					НоваяСтрока.ВидЗадания = ТекущийВидЗадания;

				КонецЕсли;

			КонецЦикла;

		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
