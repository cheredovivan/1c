//@strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Параметры.Организация)
	 ИЛИ НЕ ЗначениеЗаполнено(Параметры.Контрагент) Тогда
		
		Отказ = Истина;
		Возврат;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
	Организация = Параметры.Организация;
	Контрагент = Параметры.Контрагент;
	ДатаАктуальности = Параметры.ДатаАктуальности;
	Если ЗначениеЗаполнено(Параметры.Валюта) Тогда
		
		ВалютаДокумента = Параметры.Валюта;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.АдресТаблицыПодобранныхСтрок) Тогда
		
		АдресВХранилище = Параметры.АдресТаблицыПодобранныхСтрок;
		ПодобранныеСтроки = ПолучитьИзВременногоХранилища(АдресВХранилище); // ТаблицаЗначений
		УдалитьИзВременногоХранилища(АдресВХранилище);
		ОстаткиРетроБонусов.Загрузить(ПодобранныеСтроки);
		
	КонецЕсли;
	
	ДанныеОстатковВТаблицуЗначений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	Для Каждого НомерСтроки Из Элементы.ОстаткиРетроБонусов.ВыделенныеСтроки Цикл // Число
		
		СтрокаОстатков = ОстаткиРетроБонусов.НайтиПоИдентификатору(НомерСтроки);
		
		Если НЕ СтрокаОстатков.ДокументВыбран Тогда
			
			СтрокаОстатков.ДокументВыбран = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтметки(Команда)
	
	Для Каждого НомерСтроки Из Элементы.ОстаткиРетроБонусов.ВыделенныеСтроки Цикл // Число
		
		СтрокаОстатков = ОстаткиРетроБонусов.НайтиПоИдентификатору(НомерСтроки);
		
		Если СтрокаОстатков.ДокументВыбран Тогда
			
			СтрокаОстатков.ДокументВыбран = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаОстатковРБВыполнитьВыборЗначения(Команда)
	
	ОчиститьСообщения();
	
	Отбор = Новый Структура;
	Отбор.Вставить("ДокументВыбран", Истина);
	НайденныеСтроки = ОстаткиРетроБонусов.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		
		АдресВХранилище = АдресРезультатаВХранилище();
		Закрыть(АдресВХранилище);
		
	Иначе
		
		ВыполнитьВыборЗначенияСВопросом();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОстаткиРетроБонусов.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОстаткиРетроБонусов.ДокументВыбран");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ВыполнитьВыборЗначенияСВопросом()
	
	ТекстВопроса = НСтр("ru = 'Не выбрано ни одной строки. Продолжить?';
						|en = 'No line is selected. Continue?'");
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		АдресВХранилище = АдресРезультатаВХранилище();
		Закрыть(АдресВХранилище);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДанныеОстатковВТаблицуЗначений()
	
	Граница =  Новый Граница(КонецДня(ДатаАктуальности), ВидГраницы.Включая);
	КоличествоСтрок = ОстаткиРетроБонусов.Количество();
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОстаткиВыбранные.Валюта КАК Валюта,
	|	ОстаткиВыбранные.НомерСтроки КАК НомерСтроки,
	|	ОстаткиВыбранные.ДокументВыбран КАК ДокументВыбран,
	|	ОстаткиВыбранные.ДокументУсловий КАК ДокументУсловий,
	|	ОстаткиВыбранные.Договор КАК Договор,
	|	ОстаткиВыбранные.НачалоПериода КАК НачалоПериода,
	|	ОстаткиВыбранные.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ОстаткиВыбранные.Партнер КАК Партнер
	|ПОМЕСТИТЬ ВТ_ОстаткиВыбранные
	|ИЗ
	|	&ОстаткиРетроБонусов КАК ОстаткиВыбранные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РетроБонусыПоставщиковОбороты.Партнер КАК Партнер,
	|	РетроБонусыПоставщиковОбороты.Договор КАК Договор,
	|	РетроБонусыПоставщиковОбороты.ДокументУсловий КАК ДокументУсловий,
	|	РетроБонусыПоставщиковОбороты.НачалоПериода КАК НачалоПериода,
	|	РетроБонусыПоставщиковОбороты.ОкончаниеПериода КАК ОкончаниеПериода,
	|	РетроБонусыПоставщиковОбороты.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РетроБонусыПоставщиковОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
	|				ТОГДА РетроБонусыПоставщиковОбороты.НачисленоОборот
	|			ИНАЧЕ -РетроБонусыПоставщиковОбороты.КСписаниюОборот - РетроБонусыПоставщиковОбороты.СписаноОборот - РетроБонусыПоставщиковОбороты.АктированоОборот
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ОстаткиНаКонц
	|ИЗ
	|	РегистрНакопления.РетроБонусыПоставщиков.Обороты(
	|			,
	|			,
	|			,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент
	|				И ДокументУсловий <> ЗНАЧЕНИЕ(Документ.УсловияРетроБонусовПоставщика.ПустаяСсылка)
	|				И &УсловиеВалюта) КАК РетроБонусыПоставщиковОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	РетроБонусыПоставщиковОбороты.Партнер,
	|	РетроБонусыПоставщиковОбороты.Договор,
	|	РетроБонусыПоставщиковОбороты.ДокументУсловий,
	|	РетроБонусыПоставщиковОбороты.НачалоПериода,
	|	РетроБонусыПоставщиковОбороты.ОкончаниеПериода,
	|	РетроБонусыПоставщиковОбороты.Валюта,
	|	РетроБонусыПоставщиковОбороты.Организация,
	|	РетроБонусыПоставщиковОбороты.Контрагент
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РетроБонусыПоставщиковОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
	|				ТОГДА РетроБонусыПоставщиковОбороты.НачисленоОборот
	|			ИНАЧЕ -РетроБонусыПоставщиковОбороты.КСписаниюОборот - РетроБонусыПоставщиковОбороты.СписаноОборот - РетроБонусыПоставщиковОбороты.АктированоОборот
	|		КОНЕЦ) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	Договор,
	|	ДокументУсловий,
	|	НачалоПериода,
	|	ОкончаниеПериода,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РетроБонусыПоставщиковОбороты.Партнер КАК Партнер,
	|	РетроБонусыПоставщиковОбороты.Договор КАК Договор,
	|	РетроБонусыПоставщиковОбороты.ДокументУсловий КАК ДокументУсловий,
	|	РетроБонусыПоставщиковОбороты.НачалоПериода КАК НачалоПериода,
	|	РетроБонусыПоставщиковОбороты.ОкончаниеПериода КАК ОкончаниеПериода,
	|	РетроБонусыПоставщиковОбороты.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РетроБонусыПоставщиковОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
	|				ТОГДА РетроБонусыПоставщиковОбороты.НачисленоОборот
	|			ИНАЧЕ -РетроБонусыПоставщиковОбороты.КСписаниюОборот - РетроБонусыПоставщиковОбороты.СписаноОборот - РетроБонусыПоставщиковОбороты.АктированоОборот
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ОстаткиДату
	|ИЗ
	|	РегистрНакопления.РетроБонусыПоставщиков.Обороты(
	|			,
	|			&ГраницаАктуальности,
	|			,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент
	|				И (ДокументУсловий, Договор, Партнер, НачалоПериода, ОкончаниеПериода, Валюта) В
	|					(ВЫБРАТЬ
	|						ВТ_ОстаткиНаКонц.ДокументУсловий,
	|						ВТ_ОстаткиНаКонц.Договор,
	|						ВТ_ОстаткиНаКонц.Партнер,
	|						ВТ_ОстаткиНаКонц.НачалоПериода,
	|						ВТ_ОстаткиНаКонц.ОкончаниеПериода,
	|						ВТ_ОстаткиНаКонц.Валюта
	|					ИЗ
	|						ВТ_ОстаткиНаКонц КАК ВТ_ОстаткиНаКонц)) КАК РетроБонусыПоставщиковОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	РетроБонусыПоставщиковОбороты.Партнер,
	|	РетроБонусыПоставщиковОбороты.Договор,
	|	РетроБонусыПоставщиковОбороты.НачалоПериода,
	|	РетроБонусыПоставщиковОбороты.ОкончаниеПериода,
	|	РетроБонусыПоставщиковОбороты.ДокументУсловий,
	|	РетроБонусыПоставщиковОбороты.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РетроБонусыПоставщиковОбороты.ВидДвиженияРегистра = ЗНАЧЕНИЕ(Перечисление.ВидыДвиженияНакопления.Приход)
	|				ТОГДА РетроБонусыПоставщиковОбороты.НачисленоОборот
	|			ИНАЧЕ -РетроБонусыПоставщиковОбороты.КСписаниюОборот - РетроБонусыПоставщиковОбороты.СписаноОборот - РетроБонусыПоставщиковОбороты.АктированоОборот
	|		КОНЕЦ) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	Договор,
	|	НачалоПериода,
	|	ОкончаниеПериода,
	|	ДокументУсловий,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОстаткиДату.Партнер КАК Партнер,
	|	ВТ_ОстаткиДату.Договор КАК Договор,
	|	ВТ_ОстаткиДату.ДокументУсловий КАК ДокументУсловий,
	|	ВТ_ОстаткиДату.НачалоПериода КАК НачалоПериода,
	|	ВТ_ОстаткиДату.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_ОстаткиДату.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА ВТ_ОстаткиДату.Сумма > ВТ_ОстаткиНаКонц.Сумма
	|			ТОГДА ВТ_ОстаткиНаКонц.Сумма
	|		ИНАЧЕ ВТ_ОстаткиДату.Сумма
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ ВТ_Остатки
	|ИЗ
	|	ВТ_ОстаткиДату КАК ВТ_ОстаткиДату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОстаткиНаКонц КАК ВТ_ОстаткиНаКонц
	|		ПО ВТ_ОстаткиДату.Партнер = ВТ_ОстаткиНаКонц.Партнер
	|			И ВТ_ОстаткиДату.Договор = ВТ_ОстаткиНаКонц.Договор
	|			И ВТ_ОстаткиДату.НачалоПериода = ВТ_ОстаткиНаКонц.НачалоПериода
	|			И ВТ_ОстаткиДату.ОкончаниеПериода = ВТ_ОстаткиНаКонц.ОкончаниеПериода
	|			И ВТ_ОстаткиДату.ДокументУсловий = ВТ_ОстаткиНаКонц.ДокументУсловий
	|			И ВТ_ОстаткиДату.Валюта = ВТ_ОстаткиНаКонц.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ВТ_ОстаткиВыбранные.ДокументВыбран, ЛОЖЬ) КАК ДокументВыбран,
	|	ЕСТЬNULL(ВТ_ОстаткиВыбранные.НомерСтроки, &КоличествоСтрок) КАК НомерСтроки,
	|	ЕСТЬNULL(РетроБонусыУсловия.Вид, ЗНАЧЕНИЕ(Справочник.ВидыРетроБонусовКлиентов.ПустаяСсылка)) КАК ВидРетроБонуса,
	|	ЕСТЬNULL(РетроБонусыУсловия.ТипБонуса, ЗНАЧЕНИЕ(Перечисление.ТипыРетроБонусовПоставщиков.ПустаяСсылка)) КАК ТипБонуса,
	|	ЕСТЬNULL(РетроБонусыУсловия.Описание, """") КАК Описание,
	|	ОстаткиБонусов.Партнер КАК Партнер,
	|	ОстаткиБонусов.Договор КАК Договор,
	|	ОстаткиБонусов.НачалоПериода КАК НачалоПериода,
	|	ОстаткиБонусов.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ОстаткиБонусов.ДокументУсловий КАК ДокументУсловий,
	|	ОстаткиБонусов.Валюта КАК Валюта,
	|	ОстаткиБонусов.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТ_Подготовка
	|ИЗ
	|	ВТ_Остатки КАК ОстаткиБонусов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОстаткиВыбранные КАК ВТ_ОстаткиВыбранные
	|		ПО ОстаткиБонусов.Партнер = ВТ_ОстаткиВыбранные.Партнер
	|			И ОстаткиБонусов.Договор = ВТ_ОстаткиВыбранные.Договор
	|			И ОстаткиБонусов.Валюта = ВТ_ОстаткиВыбранные.Валюта
	|			И ОстаткиБонусов.ДокументУсловий = ВТ_ОстаткиВыбранные.ДокументУсловий
	|			И ОстаткиБонусов.НачалоПериода = ВТ_ОстаткиВыбранные.НачалоПериода
	|			И ОстаткиБонусов.ОкончаниеПериода = ВТ_ОстаткиВыбранные.ОкончаниеПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыПоставщиковУсловия КАК РетроБонусыУсловия
	|		ПО ОстаткиБонусов.ДокументУсловий = РетроБонусыУсловия.ДокументУсловий
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗавершенныеУсловияРетроБонусовПоставщиков КАК ЗавершенныеУсловияРетроБонусовПоставщиков
	|		ПО ОстаткиБонусов.ДокументУсловий = ЗавершенныеУсловияРетроБонусовПоставщиков.ДокументУсловий
	|ГДЕ
	|	НЕ ЕСТЬNULL(ЗавершенныеУсловияРетроБонусовПоставщиков.Завершено, ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВТ_Подготовка.ДокументВыбран) КАК ДокументВыбран,
	|	МИНИМУМ(ВТ_Подготовка.НомерСтроки) КАК НомерСтроки,
	|	ВТ_Подготовка.ВидРетроБонуса КАК ВидРетроБонуса,
	|	ВТ_Подготовка.ТипБонуса КАК ТипБонуса,
	|	ВТ_Подготовка.Описание КАК Описание,
	|	ВТ_Подготовка.Партнер КАК Партнер,
	|	ВТ_Подготовка.Договор КАК Договор,
	|	ВТ_Подготовка.НачалоПериода КАК НачалоПериода,
	|	ВТ_Подготовка.ОкончаниеПериода КАК ОкончаниеПериода,
	|	ВТ_Подготовка.ДокументУсловий КАК ДокументУсловий,
	|	ВТ_Подготовка.Валюта КАК Валюта,
	|	ВТ_Подготовка.Сумма КАК Сумма
	|ИЗ
	|	ВТ_Подготовка КАК ВТ_Подготовка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Подготовка.ВидРетроБонуса,
	|	ВТ_Подготовка.ТипБонуса,
	|	ВТ_Подготовка.Описание,
	|	ВТ_Подготовка.Партнер,
	|	ВТ_Подготовка.Договор,
	|	ВТ_Подготовка.НачалоПериода,
	|	ВТ_Подготовка.ОкончаниеПериода,
	|	ВТ_Подготовка.ДокументУсловий,
	|	ВТ_Подготовка.Валюта,
	|	ВТ_Подготовка.Сумма
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументВыбран УБЫВ,
	|	НомерСтроки";
	
	Если ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ТекстУсловияВалюта = "Валюта = &Валюта";
	Иначе
		ТекстУсловияВалюта = "ИСТИНА";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеВалюта", ТекстУсловияВалюта);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОстаткиРетроБонусов", ОстаткиРетроБонусов.Выгрузить());
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("ГраницаАктуальности", Граница);
	Запрос.УстановитьПараметр("Валюта", ВалютаДокумента);
	Запрос.УстановитьПараметр("КоличествоСтрок", КоличествоСтрок);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		ДанныеОбОстатках = РезультатЗапроса.Выгрузить();
		ОстаткиРетроБонусов.Загрузить(ДанныеОбОстатках);
		
	Иначе
		ОстаткиРетроБонусов.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция АдресРезультатаВХранилище()
	
	Отбор = Новый Структура;
	Отбор.Вставить("ДокументВыбран", Истина);
	
	Возврат ПоместитьВоВременноеХранилище(ОстаткиРетроБонусов.Выгрузить(Отбор));
	
КонецФункции

#КонецОбласти	