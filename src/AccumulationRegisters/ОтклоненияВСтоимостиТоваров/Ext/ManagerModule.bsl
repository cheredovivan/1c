#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	см. ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	
	ИмяРегистра = Метаданные.РегистрыНакопления.ОтклоненияВСтоимостиТоваров.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийОтклоненияВСтоимостиТоваров"; 
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	
	Возврат СтруктураТекстовЗапросов;

КонецФункции

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	Если Не ДанныеКРегистрацииЗаданий = Неопределено Тогда
		Возврат ДанныеКРегистрацииЗаданий;
	КонецЕсли;
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Заполнение ограничений доступа.
// 
// Параметры:
// 	Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

//++ НЕ УТ

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымНаправлениеДеятельности = "НаправлениеДеятельности";
	ПараметрыОтражения.ПутьКДаннымПодразделение = "Подразделение";
		
	//++ НЕ УТКА
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "Номенклатура.ГруппаФинансовогоУчета";
	ПараметрыОтражения.ПутьКДаннымМестоУчета = "МестоХранения";
	//-- НЕ УТКА
	
	ПараметрыОтражения.РесурсыУпр.Добавить("ОтклоненияВСтоимостиУпр");
	ПараметрыОтражения.РесурсыУпр.Добавить("ДопРасходыУпр");
	ПараметрыОтражения.РесурсыУпр.Добавить("НДСУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("ОтклоненияВСтоимостиРегл");
	ПараметрыОтражения.РесурсыРегл.Добавить("ДопРасходыРегл");
	ПараметрыОтражения.РесурсыРегл.Добавить("НДСРегл");
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Номенклатура;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(
		ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.Номенклатура.ПутьКДанным = "Номенклатура";
	ПараметрыОтражения.СтруктураАналитики.Склад.ПутьКДанным = "МестоХранения";
	ОпределитьПоказатели(ПараметрыОтражения);
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

// Определяет показатели регистра
//
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияДвиженийВФинансовомУчете
//
Процедура ОпределитьПоказатели(ПараметрыОтражения)
	
	// Доп расходы
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ОтклоненияВСтоимостиУпр");
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ДопРасходыУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("ОтклоненияВСтоимостиРегл");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("ДопРасходыРегл");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаДопРасходов, ПараметрыОтраженияПоказателя);
	
	// Доп расходы без НДС
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ОтклоненияВСтоимостиБезНДС");
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ДопРасходыБезНДС");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("0");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаДопРасходовБезНДС, ПараметрыОтраженияПоказателя);
	
	// Сумма НДС доп расходов
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ОтклоненияВСтоимостиСНДС");
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("ДопРасходыСНДС");
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("-ОтклоненияВСтоимостиБезНДС");
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("-ДопРасходыБезНДС");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("0");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаНДСДопРасходов, ПараметрыОтраженияПоказателя);
	
	// Сумма НДС
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("НДСУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("НДСРегл");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаНДС, ПараметрыОтраженияПоказателя);
	
КонецПроцедуры


//-- НЕ УТ

#КонецОбласти

#КонецОбласти

#КонецЕсли
