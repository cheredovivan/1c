#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросовДляПолученияТаблицыИзменений = 
		ЗакрытиеМесяцаСервер.ТекстыЗапросовДляПолученияТаблицыИзмененийРегистра(Метаданные(), Отбор);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиНачальныхДанных;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	ДополнительныеСвойства.Вставить("ТекстВыборкиТаблицыИзменений", ТекстыЗапросовДляПолученияТаблицыИзменений.ТекстВыборкиТаблицыИзменений);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ДополнительныеСвойства.ТекстВыборкиТаблицыИзменений;
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
	
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(ТаблицаИзменений.Регистратор) КАК Регистратор,
		|	МИНИМУМ(НАЧАЛОПЕРИОДА(ТаблицаИзменений.Период, МЕСЯЦ)) КАК НачалоМесяца,
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.Номенклатура,
		|	ТаблицаИзменений.Характеристика,
		|	ТаблицаИзменений.ВидЗапасов,
		|	ТаблицаИзменений.НаправлениеДеятельности,
		|	ТаблицаИзменений.Подразделение,
		|	ТаблицаИзменений.МестоХранения,
		|	ТаблицаИзменений.ПериодОтклонения,
		|	СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиСНДС) КАК ОтклоненияВСтоимостиСНДСИзменение,
		|	СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиБезНДС) КАК ОтклоненияВСтоимостиБезНДСИзменение,
		|	СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиРегл) КАК ОтклоненияВСтоимостиРеглИзменение,
		|	СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиУпр) КАК ОтклоненияВСтоимостиУпрИзменение,
		|	СУММА(ТаблицаИзменений.ДопРасходыСНДС) КАК ДопРасходыСНДСИзменение,
		|	СУММА(ТаблицаИзменений.ДопРасходыБезНДС) КАК ДопРасходыБезНДСИзменение,
		|	СУММА(ТаблицаИзменений.ДопРасходыРегл) КАК ДопРасходыРеглИзменение,
		|	СУММА(ТаблицаИзменений.ДопРасходыУпр) КАК ДопРасходыУпрИзменение,
		|	СУММА(ТаблицаИзменений.НДСРегл) КАК НДСРеглИзменение,
		|	СУММА(ТаблицаИзменений.НДСУпр) КАК НДСУпрИзменение,
		|	СУММА(ТаблицаИзменений.ПостояннаяРазница) КАК ПостояннаяРазницаИзменение,
		|	СУММА(ТаблицаИзменений.ВременнаяРазница) КАК ВременнаяРазницаИзменение,
		|	СУММА(ТаблицаИзменений.НДД) КАК НДДИзменение
		|
		|ПОМЕСТИТЬ ДвиженияОтклоненияВСтоимостиТоваровИзменение
		|
		|ИЗ
		|	ТаблицаИзмененийОтклоненияВСтоимостиТоваров КАК ТаблицаИзменений
		|
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Организация,
		|	ТаблицаИзменений.Номенклатура,
		|	ТаблицаИзменений.Характеристика,
		|	ТаблицаИзменений.ВидЗапасов,
		|	ТаблицаИзменений.НаправлениеДеятельности,
		|	ТаблицаИзменений.Подразделение,
		|	ТаблицаИзменений.МестоХранения,
		|	ТаблицаИзменений.ПериодОтклонения
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиСНДС) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиБезНДС) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиРегл) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ОтклоненияВСтоимостиУпр) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ДопРасходыСНДС) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ДопРасходыБезНДС) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ДопРасходыРегл) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ДопРасходыУпр) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.НДСРегл) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.НДСУпр) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ПостояннаяРазница) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.ВременнаяРазница) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.НДД) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
	
		Выборка = Запрос.Выполнить().Выбрать();
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
			"ДвиженияОтклоненияВСтоимостиТоваровИзменение", Выборка.Следующий() И Выборка.Количество > 0);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Организация КАК Организация,
		|	НачальныеЗаписи.Подразделение КАК Подразделение,
		|	НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|	НачальныеЗаписи.КорВидЗапасов КАК КорВидЗапасов,
		|	НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	НачальныеЗаписи.ОтклоненияВСтоимостиСНДС КАК ОтклоненияВСтоимостиСНДС,
		|	НачальныеЗаписи.ДопРасходыСНДС КАК ДопРасходыСНДС,
		|	НачальныеЗаписи.РасчетСебестоимости КАК РасчетСебестоимости,
		|	НачальныеЗаписи.РасчетПартий КАК РасчетПартий,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.ОтклоненияВСтоимостиТоваров КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Организация КАК Организация,
		|		НачальныеЗаписи.Подразделение КАК Подразделение,
		|		НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|		НачальныеЗаписи.КорВидЗапасов КАК КорВидЗапасов,
		|		НачальныеЗаписи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		НачальныеЗаписи.ОтклоненияВСтоимостиСНДС КАК ОтклоненияВСтоимостиСНДС,
		|		НачальныеЗаписи.ДопРасходыСНДС КАК ДопРасходыСНДС,
		|		НачальныеЗаписи.РасчетСебестоимости КАК РасчетСебестоимости,
		|		НачальныеЗаписи.РасчетПартий КАК РасчетПартий,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.Период КАК Период,
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Организация КАК Организация,
		|		Записи.Подразделение КАК Подразделение,
		|		Записи.ВидЗапасов КАК ВидЗапасов,
		|		Записи.КорВидЗапасов КАК КорВидЗапасов,
		|		Записи.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|		Записи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		-Записи.ОтклоненияВСтоимостиСНДС КАК ОтклоненияВСтоимостиСНДС,
		|		-Записи.ДопРасходыСНДС КАК ДопРасходыСНДС,
		|		Записи.РасчетСебестоимости КАК РасчетСебестоимости,
		|		Записи.РасчетПартий КАК РасчетПартий,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.ОтклоненияВСтоимостиТоваров КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.Период,
		|	Таблица.Активность,
		|	Таблица.ВидДвижения,
		|	Таблица.Организация,
		|	Таблица.Подразделение,
		|	Таблица.ВидЗапасов,
		|	Таблица.КорВидЗапасов,
		|	Таблица.НаправлениеДеятельности,
		|	Таблица.КорНаправлениеДеятельности,
		|	Таблица.РасчетСебестоимости,
		|	Таблица.РасчетПартий,
		|	Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.ОтклоненияВСтоимостиСНДС) <> 0 ИЛИ СУММА(Таблица.ДопРасходыСНДС) <> 0
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект, Неопределено,
			ДанныеКРегистрацииЗаданий);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
