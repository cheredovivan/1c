#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает занятость видов рабочих центров этапом
//
// Параметры:
//  Ссылка	 - ДокументСсылка.ЭтапПроизводства2_2	 - этап производства.
// 
// Возвращаемое значение:
//   - ТаблицаЗначений - занято этапом.
//
Функция ЗанятоЭтапом(Ссылка) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Таблица.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Таблица.ДатаИнтервала     КАК ДатаИнтервала,
		|	Таблица.Занято            КАК Количество
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Таблица
		|ГДЕ
		|	Таблица.Регистратор = &Ссылка");
		
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Записывает в регистр данные о загрузке оборудования графиком производства.
//
// Параметры:
//  Регистраторы		 - Массив - документы, движения которых необходимо записать.
//  ГрафикПроизводства	 - ТаблицаЗначений - данные о загрузке для записи.
//
Процедура ЗаписатьЗанятоГрафикомПроизводства(Регистраторы, ГрафикПроизводства) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГрафикПроизводства.Индексы.Добавить("Этап");
	СтруктураПоиска = Новый Структура("Этап");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЭтапПроизводства2_2.Ссылка       КАК Регистратор,
	|	ЭтапПроизводства2_2.Распоряжение КАК Распоряжение,
	|	ЭтапПроизводства2_2.Дата         КАК Период,
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Т
	|		ГДЕ
	|			Т.Регистратор = ЭтапПроизводства2_2.Ссылка) КАК ЕстьДвижения
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
	|ГДЕ
	|	ЭтапПроизводства2_2.Ссылка В(&Регистраторы)");
	Запрос.УстановитьПараметр("Регистраторы", Регистраторы);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураПоиска.Этап = Выборка.Регистратор;
		НайденныеСтроки = ГрафикПроизводства.НайтиСтроки(СтруктураПоиска);
		
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			
			Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Для каждого Строка Из НайденныеСтроки Цикл
				
				Запись = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Строка);
				ЗаполнитьЗначенияСвойств(Запись, Выборка, "Распоряжение, Период");
				
			КонецЦикла;
			Набор.Записать();
			
		ИначеЕсли Выборка.ЕстьДвижения Тогда
			
			Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
			Набор.Отбор.Регистратор.Установить(Выборка.Регистратор);
			Набор.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
	КолонкиИнтервалов = "ВидРабочегоЦентра, ДатаИнтервала";
	Интервалы = ГрафикПроизводства.Скопировать(, КолонкиИнтервалов);
	Интервалы.Свернуть(КолонкиИнтервалов);
	ПроверитьЗагрузкаНеПревышаетДоступностьГрафика(Интервалы);
	
КонецПроцедуры

// Выполняет проверку того, что загрузка оборудования не превышает доступное время.
// При выявлении превышения в базу записываются задания к пересчету графика производства.
//
// Параметры:
//  Интервалы - ТаблицаЗначений - интервалы времени, которые необходимо проверить. 
//
Процедура ПроверитьЗагрузкаНеПревышаетДоступностьГрафика(Интервалы) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Интервалы.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	Интервалы.ДатаИнтервала     КАК ДатаИнтервала
	|ПОМЕСТИТЬ ВТВсеИнтервалы
	|ИЗ
	|	&Интервалы КАК Интервалы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабочегоЦентра
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТВсеИнтервалы.ВидРабочегоЦентра               КАК ВидРабочегоЦентра,
	|	ВТВсеИнтервалы.ДатаИнтервала                   КАК ДатаИнтервала,
	|	ВидыРабочихЦентров.ВводитьДоступностьДляВидаРЦ КАК ВводитьДоступностьДляВидаРЦ
	|ПОМЕСТИТЬ ВТИнтервалы
	|ИЗ
	|	ВТВсеИнтервалы КАК ВТВсеИнтервалы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|	ПО ВТВсеИнтервалы.ВидРабочегоЦентра = ВидыРабочихЦентров.Ссылка
	|ГДЕ
	|	ВидыРабочихЦентров.УчитыватьДоступностьПоГрафикуРаботы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабочегоЦентра,
	|	ДатаИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ВидРабочегоЦентра  КАК ВидРабочегоЦентра,
	|	ВложенныйЗапрос.ДатаИнтервала      КАК ДатаИнтервала,
	|	ВложенныйЗапрос.Заказ              КАК Заказ,
	|	ВложенныйЗапрос.Этап               КАК Этап,
	|	СУММА(ВложенныйЗапрос.Доступность) КАК Доступность,
	|	СУММА(ВложенныйЗапрос.Загрузка)    КАК Загрузка
	|ПОМЕСТИТЬ ВТДоступность
	|ИЗ
	|	(ВЫБРАТЬ
	|		Доступность.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|		Доступность.ДатаИнтервала     КАК ДатаИнтервала,
	|		ВЫБОР
	|			КОГДА Доступность.ЭтоДвижениеВводаДоступности
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ Доступность.Распоряжение
	|		КОНЕЦ                         КАК Заказ,
	|		ВЫБОР
	|			КОГДА Доступность.ЭтоДвижениеВводаДоступности
	|				ИЛИ Доступность.КлючПартия <> &ПустойКлючСвязи
	|					И Доступность.Этап ССЫЛКА Справочник.ЭтапыПроизводства
	|				ТОГДА НЕОПРЕДЕЛЕНО
	|			ИНАЧЕ Доступность.Этап
	|		КОНЕЦ                         КАК Этап,
	|		ВЫБОР
	|			КОГДА ВТИнтервалы.ВводитьДоступностьДляВидаРЦ
	|				ТОГДА Доступность.ДоступностьПоВидуРЦ
	|			ИНАЧЕ Доступность.ДоступностьПоРЦ
	|		КОНЕЦ                         КАК Доступность,
	|		Доступность.Занято            КАК Загрузка
	|	ИЗ
	|		РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Доступность
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИнтервалы КАК ВТИнтервалы
	|		ПО Доступность.ВидРабочегоЦентра = ВТИнтервалы.ВидРабочегоЦентра
	|			И Доступность.ДатаИнтервала = ВТИнтервалы.ДатаИнтервала
	//++ Устарело_Производство21
	|	ГДЕ
	|		НЕ Доступность.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Доступность.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|		Доступность.ДатаИнтервала     КАК ДатаИнтервала,
	|		Доступность.Регистратор       КАК Заказ,
	|		НЕОПРЕДЕЛЕНО                  КАК Этап,
	|		0                             КАК Доступность,
	|		Доступность.Занято            КАК Загрузка
	|	ИЗ
	|		РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Доступность
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИнтервалы КАК ВТИнтервалы
	|			ПО Доступность.ВидРабочегоЦентра = ВТИнтервалы.ВидРабочегоЦентра
	|				И Доступность.ДатаИнтервала = ВТИнтервалы.ДатаИнтервала
	|	ГДЕ
	|		Доступность.Регистратор ССЫЛКА Документ.ЗаказНаПроизводство
	|		И ВЫРАЗИТЬ(Доступность.Регистратор КАК Документ.ЗаказНаПроизводство).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовНаПроизводство.Закрыт)
	//-- Устарело_Производство21
	|	) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ВидРабочегоЦентра,
	|	ВложенныйЗапрос.ДатаИнтервала,
	|	ВложенныйЗапрос.Заказ,
	|	ВложенныйЗапрос.Этап
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабочегоЦентра,
	|	ДатаИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДоступность.ВидРабочегоЦентра  КАК ВидРабочегоЦентра,
	|	ВТДоступность.ДатаИнтервала      КАК ДатаИнтервала,
	|	СУММА(ВТДоступность.Доступность) КАК ДоступностьИнтервал
	|ПОМЕСТИТЬ ВТПревышениеДоступности
	|ИЗ
	|	ВТДоступность КАК ВТДоступность
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТДоступность.ВидРабочегоЦентра,
	|	ВТДоступность.ДатаИнтервала
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВТДоступность.Загрузка) > СУММА(ВТДоступность.Доступность)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРабочегоЦентра,
	|	ДатаИнтервала
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТДоступность.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
	|	ВТДоступность.ДатаИнтервала     КАК ДатаИнтервала,
	|	ВТДоступность.Заказ             КАК Заказ,
	|	ВТДоступность.Этап              КАК Этап,
	|	ВТДоступность.Загрузка          КАК Загрузка,
	|	ВЫБОР
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство2_2).ДинамическаяСтруктура
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ                           КАК ДинамическаяСтруктура,
	|	ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			И ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство2_2).ДинамическаяСтруктура
	|			И ВТДоступность.Этап = НЕОПРЕДЕЛЕНО
	|		ИЛИ ВТДоступность.Этап ССЫЛКА Документ.ЭтапПроизводства2_2
	|			И НЕ ВЫРАЗИТЬ(ВТДоступность.Этап КАК Документ.ЭтапПроизводства2_2).РучноеРазмещениеВГрафике
	|			И ВЫРАЗИТЬ(ВТДоступность.Этап КАК Документ.ЭтапПроизводства2_2).Статус В (
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Формируется),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Сформирован),
	|				ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.КВыполнению)) КАК ДобавлятьВЗадания,
	|	ВТПревышениеДоступности.ДоступностьИнтервал КАК ДоступностьИнтервал,
	|	ВЫБОР
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство2_2).Приоритет.РеквизитДопУпорядочивания
	//++ Устарело_Производство21
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство).Приоритет.РеквизитДопУпорядочивания
	//-- Устарело_Производство21
	|	КОНЕЦ КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство2_2).Подразделение.РеквизитДопУпорядочивания
	//++ Устарело_Производство21
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство).Подразделение.РеквизитДопУпорядочивания
	//-- Устарело_Производство21
	|	КОНЕЦ КАК ПодразделениеПриоритет,
	|	ВЫБОР
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			ТОГДА 2
	//++ Устарело_Производство21
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА 1
	//-- Устарело_Производство21
	|	КОНЕЦ КАК ПриоритетЗаказа,
	|	ВЫБОР
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство2_2
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство2_2).Очередь
	//++ Устарело_Производство21
	|		КОГДА ВТДоступность.Заказ ССЫЛКА Документ.ЗаказНаПроизводство
	|			ТОГДА ВЫРАЗИТЬ(ВТДоступность.Заказ КАК Документ.ЗаказНаПроизводство).Очередь
	//-- Устарело_Производство21
	|	КОНЕЦ КАК Очередь
	|ИЗ
	|	ВТДоступность КАК ВТДоступность
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПревышениеДоступности КАК ВТПревышениеДоступности
	|	ПО ВТДоступность.ВидРабочегоЦентра = ВТПревышениеДоступности.ВидРабочегоЦентра
	|		И ВТДоступность.ДатаИнтервала = ВТПревышениеДоступности.ДатаИнтервала
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафикЭтаповПроизводства
	|	ПО ВТДоступность.Этап = НормативныйГрафикЭтаповПроизводства.ЭтапПроизводства
	|ГДЕ
	|	ВТДоступность.Загрузка > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ПодразделениеПриоритет,
	|	ПриоритетЗаказа,
	|	Очередь,
	|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.ДлительностьДоВыпуска, 0) УБЫВ,
	|	ЕСТЬNULL(НормативныйГрафикЭтаповПроизводства.Ресурсоемкость, 0) УБЫВ
	|ИТОГИ
	|	МАКСИМУМ(ДоступностьИнтервал)
	|ПО
	|	ВидРабочегоЦентра,
	|	ДатаИнтервала");
	
	Запрос.УстановитьПараметр("Интервалы", Интервалы);
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	ЗаданияКДобавлению = Новый ТаблицаЗначений();
	ЗаданияКДобавлению.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2"));
	ЗаданияКДобавлению.Колонки.Добавить("ЭтапПроизводства", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	
	Этапы = Новый Массив;
	
	ВыборкаВидРабочегоЦентра = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВидРабочегоЦентра.Следующий() Цикл
		
		ВыборкаДатаИнтервала = ВыборкаВидРабочегоЦентра.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаДатаИнтервала.Следующий() Цикл
			
			Доступность = ВыборкаДатаИнтервала.ДоступностьИнтервал;
			Выборка = ВыборкаДатаИнтервала.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.Загрузка > Доступность Тогда
					Доступность = 0;
					
					Если Выборка.ДобавлятьВЗадания
						И Выборка.ДинамическаяСтруктура Тогда
						НоваяСтрока = ЗаданияКДобавлению.Добавить();
						НоваяСтрока.ЗаказНаПроизводство = Выборка.Заказ;
						НоваяСтрока.ЭтапПроизводства = Выборка.Этап;
					ИначеЕсли Выборка.ДобавлятьВЗадания
						И Не Выборка.ДинамическаяСтруктура Тогда
						Этапы.Добавить(Выборка.Этап);
					КонецЕсли;
				Иначе
					Доступность = Доступность - Выборка.Загрузка;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЗаданияКДобавлению.Количество() > 0 Тогда
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.ДобавитьЗадания(ЗаданияКДобавлению, Истина);
	КонецЕсли;
	
	Если Этапы.Количество() > 0 Тогда
		РегистрыСведений.ЗаданияКРасчетуГрафикаПроизводства.ДобавитьЗадания(Этапы);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура СвернутьЗаписиПоРесурсуЗанято() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АВТОНОМЕРЗАПИСИ()       КАК Очередь,
		|	Доступность.Регистратор КАК Регистратор
		|ПОМЕСТИТЬ ВТРегистраторы
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Доступность
		|ГДЕ
		|	Доступность.Регистратор ССЫЛКА Документ.ЭтапПроизводства2_2
		|	ИЛИ Доступность.Регистратор ССЫЛКА Документ.РегистраторГрафикаПроизводства
		|
		|СГРУППИРОВАТЬ ПО
		|	Доступность.Регистратор,
		|	Доступность.ВидРабочегоЦентра,
		|	Доступность.ДатаИнтервала,
		|	Доступность.ВариантНаладки,
		|	Доступность.Распоряжение,
		|	Доступность.Подразделение,
		|	Доступность.Смена,
		|	Доступность.КлючПартия,
		|	Доступность.Этап
		|
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(Доступность.НомерСтроки) > 1
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Очередь";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Если Выборка.Количество = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаРегистраторы =
		"ВЫБРАТЬ
		|	Регистраторы.Регистратор КАК Регистратор
		|ИЗ
		|	ВТРегистраторы КАК Регистраторы
		|ГДЕ
		|	Регистраторы.Очередь >= &НачалоОчереди 
		|	И Регистраторы.Очередь < &КонецОчереди";
	
	ТекстЗапросаДвижения = 
		"ВЫБРАТЬ
		|	Доступность.Регистратор       КАК Регистратор,
		|	Доступность.Период            КАК Период,
		|	Доступность.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Доступность.ДатаИнтервала     КАК ДатаИнтервала,
		|	Доступность.ВариантНаладки    КАК ВариантНаладки,
		|	Доступность.Распоряжение      КАК Распоряжение,
		|	Доступность.Подразделение     КАК Подразделение,
		|	Доступность.Смена             КАК Смена,
		|	Доступность.КлючПартия        КАК КлючПартия,
		|	Доступность.Этап              КАК Этап,
		|	СУММА(Доступность.Занято)     КАК Занято
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Доступность
		|ГДЕ
		|	Доступность.Регистратор В (&Регистраторы)
		|
		|СГРУППИРОВАТЬ ПО
		|	Доступность.ВариантНаладки,
		|	Доступность.ВидРабочегоЦентра,
		|	Доступность.ДатаИнтервала,
		|	Доступность.КлючПартия,
		|	Доступность.Период,
		|	Доступность.Подразделение,
		|	Доступность.Распоряжение,
		|	Доступность.Регистратор,
		|	Доступность.Смена,
		|	Доступность.Этап
		|
		|ИМЕЮЩИЕ
		|	СУММА(Доступность.Занято) <> 0";
	
	НачалоОчереди = 1;
	СтруктураПоиска = Новый Структура("Регистратор");
	
	Пока Истина Цикл
		Запрос.Текст = ТекстЗапросаРегистраторы;
		Запрос.УстановитьПараметр("НачалоОчереди", НачалоОчереди);
		Запрос.УстановитьПараметр("КонецОчереди", НачалоОчереди + 100);
		НачалоОчереди = НачалоОчереди + 100;
		
		Регистраторы = Запрос.Выполнить().Выгрузить();
		Если Регистраторы.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоступностьВидовРабочихЦентров.НаборЗаписей");
			ЭлементБлокировки.ИсточникДанных = Регистраторы;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Регистратор", "Регистратор");
			Блокировка.Заблокировать();
			
			Запрос.Текст = ТекстЗапросаДвижения;
			Запрос.УстановитьПараметр("Регистраторы", Регистраторы.ВыгрузитьКолонку("Регистратор"));
			ВсеДвижения = Запрос.Выполнить().Выгрузить();
			ВсеДвижения.Индексы.Добавить("Регистратор");
			
			Для каждого Строка Из Регистраторы Цикл
				Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
				Набор.Отбор.Регистратор.Установить(Строка.Регистратор);
				
				СтруктураПоиска.Регистратор = Строка.Регистратор;
				Движения = ВсеДвижения.Скопировать(СтруктураПоиска);
				Если Движения.Количество() > 0 Тогда
					Набор.Загрузить(Движения);
				КонецЕсли;
				
				Набор.Записать();
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Доступность видов рабочих центров.Сворачивание записей';
					|en = 'Capacity of work center types.Collapse records'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Процедура ОчиститьИсториюЗагрузкиВидовРабочихЦентров() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ГраницаОчистки = НачалоМесяца(ТекущаяДатаСеанса() - 7*24*60*60);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегистраторГрафикаПроизводства.Ссылка КАК Ссылка,
		|	&ИмяДокументаРегистраторГрафика       КАК ИмяДокумента
		|ИЗ
		|	Документ.РегистраторГрафикаПроизводства КАК РегистраторГрафикаПроизводства
		|ГДЕ
		|	РегистраторГрафикаПроизводства.Дата < &ГраницаОчистки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 500
		|	ДоступностьВидаРабочихЦентров.Ссылка КАК Ссылка,
		|	&ИмяДокументаДоступностьВидаРЦ       КАК ИмяДокумента
		|ИЗ
		|	Документ.ДоступностьВидаРабочихЦентров КАК ДоступностьВидаРабочихЦентров
		|ГДЕ
		|	ДоступностьВидаРабочихЦентров.Дата < &ГраницаОчистки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 500
		|	ДоступностьРабочихЦентров.Ссылка КАК Ссылка,
		|	&ИмяДокументаДоступностьРЦ       КАК ИмяДокумента
		|ИЗ
		|	Документ.ДоступностьРабочихЦентров КАК ДоступностьРабочихЦентров
		|ГДЕ
		|	ДоступностьРабочихЦентров.Дата < &ГраницаОчистки");
	
	Запрос.УстановитьПараметр("ГраницаОчистки", ГраницаОчистки);
	Запрос.УстановитьПараметр("ИмяДокументаРегистраторГрафика", "Документ.РегистраторГрафикаПроизводства");
	Запрос.УстановитьПараметр("ИмяДокументаДоступностьВидаРЦ", "Документ.ДоступностьВидаРабочихЦентров");
	Запрос.УстановитьПараметр("ИмяДокументаДоступностьРЦ", "Документ.ДоступностьРабочихЦентров");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			Блокировка.Добавить(Выборка.ИмяДокумента).УстановитьЗначение("Ссылка", Выборка.Ссылка);
			Блокировка.Заблокировать();
			
			ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
			
			Если Не ДокументОбъект = Неопределено Тогда
				
				Для каждого Движения Из ДокументОбъект.Движения Цикл
					Движения.Записывать = Истина;
				КонецЦикла;
				
				ДокументОбъект.Движения.Записать();
				ДокументОбъект.Удалить();
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ЗаписьЖурналаРегистрации(
				НСтр("ru = 'Доступность видов рабочих центров.Очистка истории загрузки';
					|en = 'Capacity of work center types.Clear load history'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		КонецПопытки;
		
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
