#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК Т1 
	|	ПО Т.АналитикаУчетаПоПартнерам = Т1.КлючАналитики
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т1.Организация)
	|	И ЗначениеРазрешено(Т1.Партнер)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// При определении доступных видов оповещений пользователей.
// 
// Параметры:
//  ОписаниеВидовОповещений - см. ОповещенияПользователейОСобытиях.ОписаниеВидовОповещенийПользователей
Процедура ПриОпределенииДоступныхВидовОповещенийПользователей(ОписаниеВидовОповещений) Экспорт
	
	ГруппаВидаОповещенийВзаиморасчеты = ОповещенияПользователейОСобытиях.ГруппаВидаОповещений("ВзаиморасчетыСПоставщиками");
	
	НовыйВидОповещений = ОповещенияПользователейОСобытиях.ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений);
	НовыйВидОповещений.ГруппаВидаОповещений = ГруппаВидаОповещенийВзаиморасчеты;
	НовыйВидОповещений.Идентификатор = "bd8bbddb-fdc1-4629-b533-f3c6ceeca4f8";
	НовыйВидОповещений.Наименование = НСтр("ru = 'Оформление оплаты поставщику';
											|en = 'Creation of payment to vendor'", ОбщегоНазначения.КодОсновногоЯзыка());
	НовыйВидОповещений.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное;
	НовыйВидОповещений.ИмяШаблонаСКД = Метаданные.РегистрыНакопления.РасчетыСПоставщиками.Макеты.Оповещения_ПоступлениеОплаты.ПолноеИмя();
	НовыйВидОповещений.ШаблонТекстаОповещения = "ru = 'Оформлена оплата поставщику на сумму [СуммаПлатежа] [Валюта] [ДатаПлатежа]. [ДокументОплаты]';
												|en = 'Payment to vendor is registered in the amount of [СуммаПлатежа] [Валюта] on [ДатаПлатежа]. [ДокументОплаты]'"; // @NStr-1
	ОповещенияПользователейОСобытиях.ДобавитьОписаниеИсточникаДанных(НовыйВидОповещений,
		Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	
КонецПроцедуры

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов и распределению взаиморасчетов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Если Не МенеджерВременныхТаблиц = Неопределено Тогда

		Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда
				
				ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
				
				Если ПланыОбмена.ГлавныйУзел() = Неопределено
					И Не МенеджерВременныхТаблиц = Неопределено 
					И МенеджерВременныхТаблиц.Таблицы.Найти("ОчередьДанныеКДопроведениюДокументовУпрБаланс") <> Неопределено Тогда
					ТаблицаДанных = МенеджерВременныхТаблиц.Таблицы["ОчередьДанныеКДопроведениюДокументовУпрБаланс"].ПолучитьДанные().Выгрузить();
				КонецЕсли;
				
				Возврат ТаблицаДанных;
				
			КонецЕсли;

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам Тогда
				ТаблицаДанных = РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.ТаблицаФормированияЗаданий();
				Если МенеджерВременныхТаблиц.Таблицы.Найти("ЗаданияКРаспределениюВзаиморасчетовПоставщики") <> Неопределено Тогда
					ИсходныеДанные = МенеджерВременныхТаблиц.Таблицы["ЗаданияКРаспределениюВзаиморасчетовПоставщики"].ПолучитьДанные().Выгрузить();

					Для Каждого Запись Из ИсходныеДанные Цикл
						НоваяСтрока = ТаблицаДанных.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Запись);
						НоваяСтрока.ДатаЗаписи = ТекущаяДатаСеанса();
					КонецЦикла;
				КонецЕсли;
				
				Возврат ТаблицаДанных;
			КонецЕсли;
		КонецЦикла;

	КонецЕсли;

	Возврат ТаблицаДанных;
	
КонецФункции

// Формирует текст запроса получения данных для регистрации заданий в регистре очереди "Задания к распределению взаиморасчетов".
// Задания в регистре очереди будут формироваться по данным таблицы изменений движений, а также по движениям документов данного регистра расчетов, 
// расположенных в цепочке после изменяемых по тем же гранулам распределения взаиморасчетов (АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта).
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаДанныеКРегистрацииЗаданийРаспределенияВзаиморасчетов() Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ВЫБОР КОГДА РасчетыСПоставщикамиИзменения.Сумма = 0
	|								И (РасчетыСПоставщикамиИзменения.КОплате <> 0 ИЛИ РасчетыСПоставщикамиИзменения.КПоступлению <> 0)
	|								И ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) <> ТИП(Документ.ВводОстатков)
	|								И ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|							ТОГДА РасчетыСПоставщикамиИзменения.ДатаРегистратора
	|							ИНАЧЕ РасчетыСПоставщикамиИзменения.Период
	|						КОНЕЦ, ДЕНЬ)) КАК Период,
	|	МИНИМУМ(ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) В (&ТипыРегистраторовБезПроверкиИзменения)
	|				ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиИзменения.Период, ДЕНЬ)
	|		КОНЕЦ) КАК ПериодКонтроляИзменений,
	|	РасчетыСПоставщикамиИзменения.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиИзменения.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиИзменения.ВалютаРасчетов КАК Валюта
	|ПОМЕСТИТЬ МинимальныеПериодыИзмененийПоставщики
	|ИЗ
	|	РасчетыСПоставщикамиИзменения КАК РасчетыСПоставщикамиИзменения
	|ГДЕ
	|	(РасчетыСПоставщикамиИзменения.Сумма <> 0
	|	ИЛИ РасчетыСПоставщикамиИзменения.КОплате <> 0
	|	ИЛИ РасчетыСПоставщикамиИзменения.КПоступлению <> 0)
	|	И &НовыеРасчеты
	|	И &СоздаватьЗаданияКРаспределениюВзаиморасчетов
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщикамиИзменения.АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиИзменения.ОбъектРасчетов,
	|	РасчетыСПоставщикамиИзменения.ВалютаРасчетов
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта;
	|ВЫБРАТЬ
	|	МИНИМУМ(ЗаданияКРаспределению.ПериодЗадания) КАК ПериодЗадания,
	|	МИНИМУМ(ЗаданияКРаспределению.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений,
	|	ЗаданияКРаспределению.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределению.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗаданияКРаспределению.Валюта КАК Валюта,
	|	ЗаданияКРаспределению.Документ КАК Документ,
	|	ЗаданияКРаспределению.Организация КАК Организация
	|ПОМЕСТИТЬ ЗаданияКРаспределениюВзаиморасчетовПоставщики
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСПоставщиками.Сумма = 0
	|				И (РасчетыСПоставщиками.КОплате <> 0 ИЛИ РасчетыСПоставщиками.КПоступлению <> 0)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) <> ТИП(Документ.ВводОстатков)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|			ТОГДА РасчетыСПоставщиками.ДатаРегистратора
	|			ИНАЧЕ РасчетыСПоставщиками.Период
	|		КОНЕЦ КАК ПериодЗадания,
	|		МинимальныеПериодыИзмененийПоставщики.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений,
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|		РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
	|		РасчетыСПоставщиками.Валюта КАК Валюта,
	|		РасчетыСПоставщиками.Регистратор КАК Документ,
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МинимальныеПериодыИзмененийПоставщики КАК МинимальныеПериодыИзмененийПоставщики
	|			ПО РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = МинимальныеПериодыИзмененийПоставщики.АналитикаУчетаПоПартнерам
	|			И РасчетыСПоставщиками.ОбъектРасчетов = МинимальныеПериодыИзмененийПоставщики.ОбъектРасчетов
	|			И РасчетыСПоставщиками.Валюта = МинимальныеПериодыИзмененийПоставщики.Валюта
	|			И РасчетыСПоставщиками.Период >= МинимальныеПериодыИзмененийПоставщики.Период
	|	ГДЕ
	|		РасчетыСПоставщиками.Активность
	|		И (РасчетыСПоставщиками.Сумма <> 0
	|		ИЛИ РасчетыСПоставщиками.КОплате <> 0
	|		ИЛИ РасчетыСПоставщиками.КПоступлению <> 0)
	|		И &НовыеРасчеты
	|		И &СоздаватьЗаданияКРаспределениюВзаиморасчетов
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ВЫБОР КОГДА РасчетыСПоставщикамиИзменения.Сумма = 0
	|				И (РасчетыСПоставщикамиИзменения.КОплате <> 0 ИЛИ РасчетыСПоставщикамиИзменения.КПоступлению <> 0)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) <> ТИП(Документ.ВводОстатков)
	|				И ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) <> ТИП(Документ.ВводОстатковВзаиморасчетов)
	|			ТОГДА РасчетыСПоставщикамиИзменения.ДатаРегистратора
	|			ИНАЧЕ РасчетыСПоставщикамиИзменения.Период
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ТИПЗНАЧЕНИЯ(РасчетыСПоставщикамиИзменения.Документ) В (&ТипыРегистраторовБезПроверкиИзменения)
	|				ТОГДА ДАТАВРЕМЯ(3999, 1, 1)
	|			ИНАЧЕ НАЧАЛОПЕРИОДА(РасчетыСПоставщикамиИзменения.Период, ДЕНЬ)
	|		КОНЕЦ,
	|		РасчетыСПоставщикамиИзменения.АналитикаУчетаПоПартнерам,
	|		РасчетыСПоставщикамиИзменения.ОбъектРасчетов,
	|		РасчетыСПоставщикамиИзменения.ВалютаРасчетов,
	|		РасчетыСПоставщикамиИзменения.Документ,
	|		РасчетыСПоставщикамиИзменения.Организация
	|	ИЗ
	|		РасчетыСПоставщикамиИзменения КАК РасчетыСПоставщикамиИзменения
	|	ГДЕ
	|		(РасчетыСПоставщикамиИзменения.Сумма <> 0
	|		ИЛИ РасчетыСПоставщикамиИзменения.КОплате <> 0
	|		ИЛИ РасчетыСПоставщикамиИзменения.КПоступлению <> 0)
	|		И &НовыеРасчеты
	|		И &СоздаватьЗаданияКРаспределениюВзаиморасчетов) КАК ЗаданияКРаспределению
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКРаспределению.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределению.ОбъектРасчетов,
	|	ЗаданияКРаспределению.Валюта,
	|	ЗаданияКРаспределению.Документ,
	|	ЗаданияКРаспределению.Организация";
		
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.82";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a2f265d9-44a0-4a59-a251-a9dcd21023bc");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РасчетыСПоставщиками.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Критичный;
	Обработчик.Комментарий = СтрШаблон(НСтр("ru = 'Исправляет реквизит ""%1"" для движений переносов расчетов.
	|Исправляет реквизит ""%1"" для движений ввода начальных остатков авансов.
	|Исправляет дату регистратора ""Бронирование"".
	|Исправляет реквизит ""%2"" для движений платежей без даты погашения.';
	|en = 'Corrects the ""%1"" attribute for calculation transfer records.
	|Corrects the ""%1"" attribute for opening prepayment balance entry transfer records.
	| Corrects the ""Booking"" register date.
	|Corrects the ""%2"" attribute for payment records without a repayment date.'"),
		Метаданные.РегистрыНакопления.РасчетыСПоставщиками.Реквизиты.ПорядокОперации.Представление(),
	Метаданные.РегистрыНакопления.РасчетыСПоставщиками.Реквизиты.ПорядокЗачетаПоДатеПлатежа.Представление());
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.БлокируемыеОбъекты = "";

КонецПроцедуры

// Процедура регистрации данных для обработчика обновления ОбработатьДанныеДляПереходаНаВерсию.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РасчетыСПоставщиками";
	
	МассивТипов = Новый Массив;
	Для Каждого ТипРегистратора Из Метаданные.РегистрыНакопления.РасчетыСПоставщиками.СтандартныеРеквизиты.Регистратор.Тип.Типы() Цикл
		МассивТипов.Добавить(Метаданные.НайтиПоТипу(ТипРегистратора).ПолноеИмя());
	КонецЦикла;
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = СтрСоединить(МассивТипов, ",");
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	#Область ТекстЗапроса
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК РасчетыПриемник
	|			ПО Расчеты.Период = РасчетыПриемник.Период
	|				И Расчеты.Регистратор = РасчетыПриемник.Регистратор
	|				И Расчеты.АналитикаУчетаПоПартнерамПриемник = РасчетыПриемник.АналитикаУчетаПоПартнерам
	|				И Расчеты.ОбъектРасчетовПриемник = РасчетыПриемник.ОбъектРасчетов
	|				И Расчеты.ВалютаПриемник = РасчетыПриемник.Валюта
	|				И Расчеты.ПорядокОперации = РасчетыПриемник.ПорядокОперации
	|ГДЕ
	|	Расчеты.СуммаПриемник > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|// Исправить дату регистратора документов ""Бронирование""
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.Бронирование КАК Бронирование
	|		ПО Расчеты.Регистратор = Бронирование.Ссылка
	|ГДЕ
	|	НЕ Расчеты.Период = Расчеты.ДатаРегистратора
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|ГДЕ
	|	Расчеты.ДатаПлатежа = ДАТАВРЕМЯ(1,1,1)
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Расчеты.Сумма > 0
	|	И ВЫРАЗИТЬ(ЛЕВ(Расчеты.ПорядокЗачетаПоДатеПлатежа, 4) КАК Строка(4))<> ""3000""
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
	|ГДЕ
	|	(Расчеты.Регистратор ССЫЛКА Документ.ВводОстатковВзаиморасчетов
	|		ИЛИ Расчеты.Регистратор ССЫЛКА Документ.ВводОстатков)
	|	И Расчеты.Период <> Расчеты.ДатаРегистратора
	|	И Расчеты.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И Расчеты.ДатаРегистратора >= ДАТАВРЕМЯ(2010,1,1)
	|	И (СТРОКА(ГОД(Расчеты.Период) - 2000) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 3, 2)
	|		ИЛИ ВЫБОР КОГДА МЕСЯЦ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 2)
	|				ИНАЧЕ СТРОКА(МЕСЯЦ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 6, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 5, 1) <> ""0""
	|			КОНЕЦ
	|		ИЛИ ВЫБОР КОГДА ДЕНЬ(Расчеты.Период) > 9
	|				ТОГДА СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 2)
	|				ИНАЧЕ СТРОКА(ДЕНЬ(Расчеты.Период)) <> ПОДСТРОКА(Расчеты.ПорядокОперации, 8, 1)
	|					ИЛИ ПОДСТРОКА(Расчеты.ПорядокОперации, 7, 1) <> ""0""
	|			КОНЕЦ)
	|";
	#КонецОбласти
	
	Регистраторы = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Регистратор");
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры, 
		Регистраторы,
		ПолноеИмяРегистра);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрНакопления.РасчетыСПоставщиками";

	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаОбновляемыхДанных из ОбновляемыеДанные Цикл
		
		ОбновляемыйРегистратор = СтрокаОбновляемыхДанных.Регистратор;
				
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			#Область Блокировки
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ОбновляемыйРегистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			#КонецОбласти
			
			ПричинаИсключения = "ПлохиеДанные";
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ОбновляемыйРегистратор);
			НаборЗаписей.Прочитать();
			
			#Область ИсправлениеПорядковОперацииПереносаРасчетов
			ТаблицаЗаписей = НаборЗаписей.Выгрузить();
			ЕстьИзменения = Ложь;
			Для Каждого ЗаписьИсточник Из ТаблицаЗаписей Цикл
				Если НЕ ЗначениеЗаполнено(ЗаписьИсточник.ПорядокОперации) Тогда
					Продолжить;
				КонецЕсли;
				Если ЗаписьИсточник.СуммаПриемник > 0 Тогда
					ЗаписиПриемники = ТаблицаЗаписей.НайтиСтроки(Новый Структура("АналитикаУчетаПоПартнерам,ОбъектРасчетов,Валюта",ЗаписьИсточник.АналитикаУчетаПоПартнерамПриемник,ЗаписьИсточник.ОбъектРасчетовПриемник,ЗаписьИсточник.ВалютаПриемник));
					ИсправилиПриемники = Ложь;
					Для Каждого ЗаписьПриемник Из ЗаписиПриемники Цикл
						Если ЗаписьПриемник.ПорядокОперации = ЗаписьИсточник.ПорядокОперации Тогда
							ЗаписьПриемник.ПорядокОперации =ЗаписьИсточник.ПорядокОперации + "3";
							ИсправилиПриемники = Истина;
						КонецЕсли
					КонецЦикла;
					Если ИсправилиПриемники Тогда
						ЗаписьИсточник.Порядокоперации = ЗаписьИсточник.ПорядокОперации + "2";
						ЕстьИзменения = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если ЕстьИзменения Тогда
				НаборЗаписей.Загрузить(ТаблицаЗаписей);
			КонецЕсли;
			#КонецОбласти
			
			#Область ИсправлениеДатыРегистратора
			Если ТипЗнч(ОбновляемыйРегистратор) = Тип("ДокументСсылка.Бронирование") Тогда
				Для Каждого Запись Из НаборЗаписей Цикл
					Если НЕ Запись.ДатаРегистратора = Запись.Период Тогда
						Запись.ДатаРегистратора = Запись.Период;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			#КонецОбласти
			
			#Область ИсправлениеПорядковОперацииПлатежейБезДатыПогашения
			Для Каждого Запись из НаборЗаписей Цикл
				Если Запись.ВидДвижения = ВидДвиженияНакопления.Приход
					И Запись.Сумма > 0 
					И Запись.ДатаПлатежа = Дата(1,1,1)
					И Лев(Запись.ПорядокЗачетаПоДатеПлатежа,4) <> "3000" Тогда
					Запись.ПорядокЗачетаПоДатеПлатежа = "30000101" + Прав(Запись.ПорядокЗачетаПоДатеПлатежа,СтрДлина(Запись.ПорядокЗачетаПоДатеПлатежа)-8);
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			#Область ИсправлениеПорядковОперацииВводаОстатковАвансов
			Для Каждого Запись из НаборЗаписей Цикл
				Если (ТипЗнч(ОбновляемыйРегистратор) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов")
						Или ТипЗнч(ОбновляемыйРегистратор) = Тип("ДокументСсылка.ВводОстатков"))
					И Запись.ДатаРегистратора <> Запись.Период
					И Запись.ВидДвижения = ВидДвиженияНакопления.Приход
					И Запись.ДатаРегистратора >= Дата(2010, 1, 1)
					И Лев(Запись.ПорядокОперации, 8) <> Формат(Запись.Период, "ДФ=yyyyMMdd") Тогда
					
					ДатаСтрокой = Формат(Запись.Период, "ДФ=yyyyMMdd");
					Запись.ПорядокОперации = ДатаСтрокой + Сред(Запись.ПорядокОперации, 9);
					
				КонецЕсли;
			КонецЦикла;
			#КонецОбласти
			
			ИзменилисьИтоги = Ложь;
			Если НаборЗаписей.Количество() > 0 Тогда
				ИзменилисьИтоги = РегистрыНакопления.РасчетыСКлиентами.ЕстьИзмененияИтоговНабора(НаборЗаписей);
			КонецЕсли;
			
			Если НаборЗаписей.Модифицированность() И НЕ ИзменилисьИтоги Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ОбновляемыйРегистратор);
				Если ЗначениеЗаполнено(ОбновляемыйРегистратор) Тогда
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
					ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
						ОбновляемыйРегистратор,
						РегистрыНакопления.РасчетыСКлиентами.ТекстСообщенияОбОшибке(ИнформацияОбОшибке(), ОбновляемыйРегистратор)
						+ Символы.ПС + РегистрыНакопления.РасчетыСКлиентами.ТекстПерепроведитеДокументВРучную());
				КонецЕсли;
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли