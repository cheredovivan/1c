#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаАкцизаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаАкцизаРегл");
	
	ПараметрыОтражения.УсловиеДебет = "ЛОЖЬ";
	ПараметрыОтражения.УсловиеКредит = "ИСТИНА";
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.Номенклатура;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(
		ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.Номенклатура.ПутьКДанным = "Номенклатура";
	ПараметрыОтражения.СтруктураАналитики.Склад.Вставить("Значение", Неопределено);
	ПараметрыОтражения.СтруктураАналитики.ВидЗапасов.Вставить("Значение", Неопределено);
	ОпределитьПоказатели(ПараметрыОтражения);
	
	//-- НЕ УТ
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

//++ НЕ УТ

// Определяет показатели регистра.
//
//
// Параметры:
//  Свойства - Структура - содержащая ключи СвойстваПоказателей, СвойстваРесурсов.
//
// Возвращаемое значение:
//  Соответствие - Ключ - имя показателя.
//                 Значение - структура свойств показателя.
//
Функция Показатели(Свойства) Экспорт

	Показатели = Новый Соответствие;
	
	СвойстваПоказателей = Свойства.СвойстваПоказателей;
	СвойстваРесурсов = Свойства.СвойстваРесурсов;
	
	МассивРесурсов = Новый Массив;
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "СуммаАкцизаУпр", "ВалютаУпр"));
	МассивРесурсов.Добавить(Новый Структура(СвойстваРесурсов, "СуммаАкцизаРегл", "ВалютаРегл"));
	Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаДопРасходов, Новый Структура(СвойстваПоказателей, МассивРесурсов));

	Возврат Показатели;
	
КонецФункции

//-- НЕ УТ

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	//++ НЕ УТ
	
	//++ Локализация
	ТипыРегистраторовОтражениеВРегламентированномУчете = Метаданные.РегистрыСведений.ОтражениеДокументовВРеглУчете.СтандартныеРеквизиты.Регистратор.Тип;
	//-- Локализация
	
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ТипыРегистраторовОтражениеДокументовВМеждународномУчете = Метаданные.РегистрыСведений.ОтражениеДокументовВМеждународномУчете.СтандартныеРеквизиты.Регистратор.Тип;
	//-- НЕ УТКА
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл
	
		//++ НЕ УТ
		
		//++ Локализация
		Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеВРегламентированномУчете Тогда
			ТаблицаДокументовКОтражению = РегистрыСведений.ОтражениеДокументовВРеглУчете.ТаблицаДокументовКОтражениюВРегламентированномУчете();
			Для Каждого ТекущиеДанные Из ДанныеКРегистрацииЗаданий Цикл
				Если ТипыРегистраторовОтражениеВРегламентированномУчете.СодержитТип(ТипЗнч(ТекущиеДанные.Документ)) Тогда
					НоваяСтрока = ТаблицаДокументовКОтражению.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
					НоваяСтрока.ДатаОтражения = НачалоДня(ТекущиеДанные.ПериодЗадания);
				КонецЕсли;
			КонецЦикла;
			ТаблицаДокументовКОтражению.Свернуть("Документ, Организация, ДатаОтражения");
			Возврат ТаблицаДокументовКОтражению;
		КонецЕсли;
		//-- Локализация
		
		//-- НЕ УТ
		
		//++ НЕ УТКА
		Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеВМеждународномУчете Тогда
			ТаблицаДокументовКОтражению = РегистрыСведений.ОтражениеДокументовВМеждународномУчете.ТаблицаДокументовКОтражениюВМеждународномУчете();
			Для Каждого ТекущиеДанные Из ДанныеКРегистрацииЗаданий Цикл
				Если ТипыРегистраторовОтражениеДокументовВМеждународномУчете.СодержитТип(ТипЗнч(ТекущиеДанные.Документ)) Тогда
					НоваяСтрока = ТаблицаДокументовКОтражению.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
					НоваяСтрока.ДатаОтражения = НачалоДня(ТекущиеДанные.ПериодЗадания);
					НоваяСтрока.Период = НачалоДня(ТекущиеДанные.ПериодЗадания);
				КонецЕсли;
			КонецЦикла;
			ТаблицаДокументовКОтражению.Свернуть("Документ, Организация, ДатаОтражения, Период");
			Возврат ТаблицаДокументовКОтражению;
		КонецЕсли;
		//-- НЕ УТКА
		
		Если ТекущийВидЗадания <> Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоАкцизам Тогда
			Для Каждого ТекущиеДанные Из ДанныеКРегистрацииЗаданий Цикл
				НоваяСтрока = ТаблицаДанных.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
				НоваяСтрока.ВидЗадания = ТекущийВидЗадания;
			КонецЦикла;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ДанныеКРегистрацииЗаданий, ТаблицаДанных);
		
	КонецЦикла;
	
	Возврат ТаблицаДанных;

КонецФункции

#КонецОбласти

#Область Проведение

// Записывает задание на фоновое проведение проведение.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
Процедура СоздатьЗаданиеДляНеоперативногоПроведенияДокумента(Документ, Свойства) Экспорт
	
	УчетПодакцизныхТоваров.СоздатьЗаданиеНаЗаписьСуммыАкциза(Документ, Свойства);
	
КонецПроцедуры

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	Если Свойства.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		Параметры.НеоперативноеПроведение = Истина;
	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Процедура формирования движений по регистру.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	СоответствиеТекстовЗапросов.Вставить("ТаблицаИзмененийДвиженияПоАкцизам", 
		РегистрыНакопления.ДвиженияПоАкцизам.ТекстЗапросаКонтрольДатыЗапрета(Запрос));
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	
	ИмяРегистра = Метаданные.РегистрыНакопления.ДвиженияПоАкцизам.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийДвиженияПоАкцизам"; 
	
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	Возврат СтруктураТекстовЗапросов

КонецФункции

// Формирует тексты запросов для контроля изменений записанных движений регистров.
//
// Параметры:
//  Запрос - Запрос - запрос, хранящий параметры используемые в списке запросов
//  ТекстыЗапроса - СписокЗначений - список текстов запросов и их имен.
//  Документ - ДокументОбъект - записываемый документ.
//
Процедура ИнициализироватьДанныеКонтроляИзменений(Запрос, ТекстыЗапроса, Документ) Экспорт
	
КонецПроцедуры

// Выводит сообщения пользователю при наличии ошибок контроля изменений записанных движений регистров.
//
// Параметры:
//  РезультатыКонтроля - Структура - таблицы с результатами контроля изменений
//  Документ - ДокументОбъект - записываемый документ
//  Отказ - Булево - признак отказа от проведения документа.
//
Процедура СообщитьОРезультатахКонтроляИзменений(РезультатыКонтроля, Документ, Отказ) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//++ НЕ УТ

// Определяет показатели регистра
//
// Параметры:
//  ПараметрыОтражения - см. ПараметрыОтраженияДвиженийВФинансовомУчете
//
Процедура ОпределитьПоказатели(ПараметрыОтражения)
	
	// Сумма акциза
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("СуммаАкцизаУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("СуммаАкцизаРегл");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаДопРасходов, ПараметрыОтраженияПоказателя);
	
	ПараметрыОтраженияПоказателя = Перечисления.ПоказателиАналитическихРегистров.ПараметрыОтраженияПоказателя();
	ПараметрыОтраженияПоказателя.РесурсыУпр.Добавить("СуммаАкцизаУпр");
	ПараметрыОтраженияПоказателя.РесурсыРегл.Добавить("0");
	ПараметрыОтраженияПоказателя.РесурсыКоличество.Добавить("0");
	ПараметрыОтражения.Показатели.Вставить(Перечисления.ПоказателиАналитическихРегистров.СуммаДопРасходовБезНДС, ПараметрыОтраженияПоказателя);
	
КонецПроцедуры

//-- НЕ УТ

#КонецОбласти

#КонецЕсли