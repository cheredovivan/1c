#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	// Текущее состояние набора помещается во временную таблицу,
	// чтобы при записи получить изменение нового набора относительно текущего.
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ЭтоНовый",    ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|	Таблица.Организация             КАК Организация,
	|	Таблица.ПартияПроизводства      КАК ПартияПроизводства,
	|	Таблица.Распоряжение            КАК Распоряжение,
	|	Таблица.ВидРабот                КАК ВидРабот,
	|	Таблица.Исполнитель             КАК Исполнитель,
	|	Таблица.Подразделение           КАК Подразделение,
	|	Таблица.КодСтрокиРаспоряжения   КАК КодСтрокиРаспоряжения,
	// Контролировать необходимо только уменьшение остатка - для этого показатель берется по модулю.
	|	ВЫБОР
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И Таблица.Количество > 0
	|			ТОГДА Таблица.Количество
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			И Таблица.Количество < 0
	|			ТОГДА -Таблица.Количество
	|		КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			И Таблица.Количество > 0
	|			ТОГДА -Таблица.Количество
	|		ИНАЧЕ Таблица.Количество
	|	КОНЕЦ                           КАК КоличествоПередЗаписью
	|ПОМЕСТИТЬ ДвиженияТрудозатратыКОформлениюПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТрудозатратыКОформлению КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|	И НЕ &ЭтоНовый
	// Исправление и сторно может формировать отрицательные остатки по исходным распоряжениям.
	|	И НЕ Таблица.Сторно
	|";
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Или Не ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу.
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Месяц                         КАК Месяц,
	|	ТаблицаИзменений.Организация                   КАК Организация,
	|	ТаблицаИзменений.ПартияПроизводства            КАК ПартияПроизводства,
	|	ТаблицаИзменений.Распоряжение                  КАК Распоряжение,
	|	ТаблицаИзменений.ВидРабот                      КАК ВидРабот,
	|	ТаблицаИзменений.Исполнитель                   КАК Исполнитель,
	|	ТаблицаИзменений.Подразделение                 КАК Подразделение,
	|	ТаблицаИзменений.КодСтрокиРаспоряжения         КАК КодСтрокиРаспоряжения,
	|	СУММА(ТаблицаИзменений.КоличествоИзменение)    КАК КоличествоИзменение
	|ПОМЕСТИТЬ ДвиженияТрудозатратыКОформлениюИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Месяц                   КАК Месяц,
	|		Таблица.Организация             КАК Организация,
	|		Таблица.ПартияПроизводства      КАК ПартияПроизводства,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.ВидРабот                КАК ВидРабот,
	|		Таблица.Исполнитель             КАК Исполнитель,
	|		Таблица.Подразделение           КАК Подразделение,
	|		Таблица.КодСтрокиРаспоряжения   КАК КодСтрокиРаспоряжения,
	|		Таблица.КоличествоПередЗаписью  КАК КоличествоИзменение
	|	ИЗ
	|		ДвиженияТрудозатратыКОформлениюПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(Таблица.Период, МЕСЯЦ) КАК Месяц,
	|		Таблица.Организация             КАК Организация,
	|		Таблица.ПартияПроизводства      КАК ПартияПроизводства,
	|		Таблица.Распоряжение            КАК Распоряжение,
	|		Таблица.ВидРабот                КАК ВидРабот,
	|		Таблица.Исполнитель             КАК Исполнитель,
	|		Таблица.Подразделение           КАК Подразделение,
	|		Таблица.КодСтрокиРаспоряжения   КАК КодСтрокиРаспоряжения,
	// Контролировать необходимо только уменьшение остатка - для этого показатель берется по модулю.
	|		ВЫБОР
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И Таблица.Количество > 0
	|				ТОГДА -Таблица.Количество
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И Таблица.Количество < 0
	|				ТОГДА Таблица.Количество
	|			КОГДА Таблица.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|				И Таблица.Количество > 0
	|				ТОГДА Таблица.Количество
	|			ИНАЧЕ -Таблица.Количество
	|		КОНЕЦ                           КАК КоличествоИзменение
	|	ИЗ
	|		РегистрНакопления.ТрудозатратыКОформлению КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор
	// Исправление и сторно может формировать отрицательные остатки по исходным распоряжениям.
	|		И НЕ Таблица.Сторно) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Месяц,
	|	ТаблицаИзменений.Организация,
	|	ТаблицаИзменений.ПартияПроизводства,
	|	ТаблицаИзменений.Распоряжение,
	|	ТаблицаИзменений.ВидРабот,
	|	ТаблицаИзменений.Исполнитель,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.КодСтрокиРаспоряжения
	|
	|ИМЕЮЩИЕ
	|	СУММА(ТаблицаИзменений.КоличествоИзменение) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация,
	|	ПартияПроизводства,
	|	Распоряжение,
	|	ВидРабот,
	|	Исполнитель,
	|	Подразделение,
	|	КодСтрокиРаспоряжения
	|;
	|УНИЧТОЖИТЬ ДвиженияТрудозатратыКОформлениюПередЗаписью
	|";
	
	Выборка = Запрос.ВыполнитьПакет()[0].Выбрать();
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
		"ДвиженияТрудозатратыКОформлениюИзменение", Выборка.Следующий() И Выборка.Количество > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли