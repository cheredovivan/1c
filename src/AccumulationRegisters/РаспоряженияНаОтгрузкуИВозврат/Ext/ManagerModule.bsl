#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Обеспечение

// Получает оформленное накладными по заказам количество.
//
// Параметры:
// 	ТаблицаОтбора - ТаблицаЗначений - Таблица с полями "Ссылка" и "КодСтроки", строки должны быть уникальными.
// 	ОтборПоИзмерениям - Структура - ключ структуры определяет имя измерения, по которому будет накладываться отбор,
//  	а значение структуры - искомое значение.
//  ИсключитьЗаказ - Булево - Признак исключающий заказ из списка оформленных накладных.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица содержит оформленное накладными количество:
//   * Ссылка - ДокументСсылка.
//   * КодСтроки - Число.
//   * Количество - Число.
//
Функция ТаблицаОформлено(ТаблицаОтбора, ОтборПоИзмерениям = Неопределено, ИсключитьЗаказ = Ложь) Экспорт
	
	ПараметрыФормированияТаблицы = НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.Периодичность = "Регистратор";
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"(Распоряжение, КодСтроки) В
		|		(ВЫБРАТЬ
		|				Ссылка,
		|				КодСтроки
		|			ИЗ
		|				ВтОтбор)";
	ПараметрыФормированияТаблицы.Группировка = "Регистратор, Распоряжение, Номенклатура, Характеристика, КодСтроки, Склад, Серия";
	ПараметрыФормированияТаблицы.Показатели = "Оформлено, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.Индексы = "Распоряжение, КодСтроки";
	ПараметрыФормированияТаблицы.ИмяВременнойТаблицы = "ВТ_РаспоряженияНаОтгрузкуОборотыРазвернутая";
	
	ВТ_РаспоряженияНаОтгрузкуОборотыРазвернутая =
		ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.КодСтроки КАК КодСтроки
	|ПОМЕСТИТЬ ВтОтбор
	|ИЗ
	|	&ТаблицаОтбора КАК Таблица
	|ГДЕ
	|	Таблица.КодСтроки > 0
	|
	|"
	+ ОбщегоНазначения.РазделительПакетаЗапросов()
	+ ВТ_РаспоряженияНаОтгрузкуОборотыРазвернутая
	+ ОбщегоНазначения.РазделительПакетаЗапросов()
	+ "
	|ВЫБРАТЬ
	|	Отбор.КодСтроки КАК КодСтроки,
	|	Отбор.Ссылка КАК Ссылка,
	|	МАКСИМУМ(РегистрРаспоряжения.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(РегистрРаспоряжения.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(РегистрРаспоряжения.Склад) КАК Склад,
	|	МАКСИМУМ(РегистрРаспоряжения.Серия) КАК Серия,
	|	СУММА(РегистрРаспоряжения.ОтгруженоКоличествоОборот) КАК Количество
	|ИЗ
	|	ВтОтбор КАК Отбор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РегистрРаспоряжения
	|		ПО РегистрРаспоряжения.Распоряжение = Отбор.Ссылка
	|		И РегистрРаспоряжения.КодСтроки = Отбор.КодСтроки
	|		И &ОтборУсловие
	|ГДЕ
	|	&ОтборПоТипамРаспоряженийПродажи
	|СГРУППИРОВАТЬ ПО
	|	Отбор.Ссылка,
	|	Отбор.КодСтроки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Отбор.КодСтроки КАК КодСтроки,
	|	Отбор.Ссылка КАК Ссылка,
	|	МАКСИМУМ(РегистрРаспоряжения.Номенклатура) КАК Номенклатура,
	|	МАКСИМУМ(РегистрРаспоряжения.Характеристика) КАК Характеристика,
	|	МАКСИМУМ(РегистрРаспоряжения.Склад) КАК Склад,
	|	МАКСИМУМ(РегистрРаспоряжения.Серия) КАК Серия,
	|	СУММА(РегистрРаспоряжения.ОформленоКоличествоОборот) КАК Количество
	|ИЗ
	|	ВтОтбор КАК Отбор
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РегистрРаспоряжения
	|		ПО РегистрРаспоряжения.Распоряжение = Отбор.Ссылка
	|		И РегистрРаспоряжения.КодСтроки = Отбор.КодСтроки
	|		И &ОтборУсловие
	|ГДЕ
	|	НЕ &ОтборПоТипамРаспоряженийПродажи
	|СГРУППИРОВАТЬ ПО
	|	Отбор.Ссылка,
	|	Отбор.КодСтроки";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ОтборПоТипамРаспоряженийПродажи",
		ПродажиСервер.ТекстУсловияДокументПродажи("РегистрРаспоряжения.Распоряжение"));
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ТаблицаОтбора", ТаблицаОтбора);
	
	Отбор = Новый Массив;
	
	Если ИсключитьЗаказ Тогда
		Отбор.Добавить("РегистрРаспоряжения.Распоряжение <> РегистрРаспоряжения.Регистратор"); // @query-part
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборПоИзмерениям) Тогда
		
		Для Каждого КлючЗначение Из ОтборПоИзмерениям Цикл
			
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			Отбор.Добавить("РегистрРаспоряжения."
				+ КлючЗначение.Ключ
				+ " В (&" + КлючЗначение.Ключ + ")");
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Отбор) Тогда
		ПодстрокаЗамены = СтрСоединить(Отбор, " И ");
	Иначе
		ПодСтрокаЗамены = "ИСТИНА"; // @query-part
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ОтборУсловие",
		ПодстрокаЗамены);
	
	УстановитьПривилегированныйРежим(Истина);
	Таблица = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Таблица.Индексы.Добавить("Ссылка, КодСтроки");
	
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Возвращает описание ограничений доступа.
//
// Параметры:
// 	Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт 

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Распоряжение.Организация)
	|	И ЗначениеРазрешено(Склад)
	|	И ЗначениеРазрешено(Распоряжение.Партнер)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВнешниеПользователи КАК ВнешниеПользователиПартнеры
	|	ПО ВнешниеПользователиПартнеры.ОбъектАвторизации = ЭтотСписок.Распоряжение.Партнер
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ВнешниеПользователиПартнеры.Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// При определении доступных видов оповещений пользователей.
// 
// Параметры:
//  ОписаниеВидовОповещений - см. ОповещенияПользователейОСобытиях.ОписаниеВидовОповещенийПользователей
Процедура ПриОпределенииДоступныхВидовОповещенийПользователей(ОписаниеВидовОповещений) Экспорт
	
	ГруппаВидаОповещенийПродажи = ОповещенияПользователейОСобытиях.ГруппаВидаОповещений("Продажи");
	
	НовыйВидОповещений = ОповещенияПользователейОСобытиях.ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений);
	НовыйВидОповещений.ГруппаВидаОповещений = ГруппаВидаОповещенийПродажи;
	НовыйВидОповещений.Идентификатор = "1f2c2d2f-d120-42fe-9d9b-2352dfb40657";
	НовыйВидОповещений.Наименование = НСтр("ru = 'Оформление накладной (Продажи)';
											|en = 'Create invoice (Sales)'", ОбщегоНазначения.КодОсновногоЯзыка());
	НовыйВидОповещений.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное;
	НовыйВидОповещений.ИмяШаблонаСКД = Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.Макеты.Оповещения_ОформлениеНакладной.ПолноеИмя();
	НовыйВидОповещений.ШаблонТекстаОповещения = "ru = 'Оформлена накладная. [ИсточникОповещения]';
												|en = 'Invoice is registered. [ИсточникОповещения]'"; // @NStr-1
	ОповещенияПользователейОСобытиях.ДобавитьОписаниеИсточникаДанных(НовыйВидОповещений,
		Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	
	НовыйВидОповещений = ОповещенияПользователейОСобытиях.ДобавитьОписаниеВидаОповещенийПользователей(ОписаниеВидовОповещений);
	НовыйВидОповещений.ГруппаВидаОповещений = ГруппаВидаОповещенийПродажи;
	НовыйВидОповещений.Идентификатор = "88d110fa-0f41-413b-bb39-d955c9220723";
	НовыйВидОповещений.Наименование = НСтр("ru = 'Оформление отгрузки товаров клиенту';
											|en = 'Create goods shipment to customer'", ОбщегоНазначения.КодОсновногоЯзыка());
	НовыйВидОповещений.ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное;
	НовыйВидОповещений.ИмяШаблонаСКД = Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.Макеты.Оповещения_ОформлениеОтгрузки.ПолноеИмя();
	НовыйВидОповещений.ШаблонТекстаОповещения = "ru = 'Оформлена отгрузка товаров клиенту. [ИсточникОповещения]';
												|en = 'Goods shipment to customer is created. [ИсточникОповещения]'"; // @NStr-1
	ОповещенияПользователейОСобытиях.ДобавитьОписаниеИсточникаДанных(НовыйВидОповещений,
		Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	
КонецПроцедуры

// Текст запроса получает остаток по ресурсам КОформлению и Заказано
// Остаток дополняется движениями, сделанными накладной заданной в параметре Регистратор.
//
// Параметры:
// 	ИмяВременнойТаблицы - Строка - Поместить результат во временную таблицу с заданным именем. 
// 	ОтборПоИзмерениям - Структура - Ключ - имя измерения, Значение - имя параметра в запросе, например:
// 		Новый Структура("Номенклатура", "Товар") будет преобразовано в тексте запроса в:
// 		"Номенклатура В(&Товар)".
// 	Ресурсы - Строка - Условие для секции ИМЕЮЩИЕ по ресурсам.
//  	Например, строка вида "КОформлению <> 0" будет преобразована в тексте запроса в:
//  	"СУММА(Набор.КОформлению) <> 0".
//
// Возвращаемое значение:
// 	Строка.
//
Функция ТекстЗапросаОстатки(ИмяВременнойТаблицы = "", ОтборПоИзмерениям = Неопределено, Ресурсы = "") Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Набор.Распоряжение КАК Распоряжение,
	|	Набор.Номенклатура КАК Номенклатура,
	|	Набор.Характеристика КАК Характеристика,
	|	Набор.КодСтроки КАК КодСтроки,
	|	Набор.Серия КАК Серия,
	|	Набор.Склад КАК Склад,
	|	СУММА(Набор.Заказано) КАК Заказано,
	|	СУММА(Набор.КОформлению) КАК КОформлению,
	|	СУММА(Набор.КПередаче) КАК КПередаче
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Распоряжение КАК Распоряжение,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.КодСтроки КАК КодСтроки,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Склад КАК Склад,
	|		(Таблица.ЗаказаноКоличествоОборот -
	|			Таблица.ОформленоКоличествоОборот) КАК Заказано,
	|		(Таблица.КОформлениюКоличествоОборот -
	|			Таблица.ОформленоКоличествоОборот) КАК КОформлению,
	|		(Таблица.КОтгрузкеКоличествоОборот -
	|			Таблица.ОтгруженоКоличествоОборот) КАК КПередаче
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК Таблица
	|	ГДЕ
	|		(Таблица.ЗаказаноКоличествоОборот -
	|			Таблица.ОформленоКоличествоОборот) <> 0
	|		ИЛИ (Таблица.КОформлениюКоличествоОборот -
	|			Таблица.ОформленоКоличествоОборот) <> 0
	|		ИЛИ (Таблица.КОтгрузкеКоличествоОборот -
	|			Таблица.ОтгруженоКоличествоОборот) <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Распоряжение КАК Распоряжение,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.КодСтроки КАК КодСтроки,
	|		Таблица.Серия КАК Серия,
	|		Таблица.Склад КАК Склад,
	|		Таблица.ОформленоКоличество КАК Заказано,
	|		Таблица.ОформленоКоличество КАК КОформлению,
	|		Таблица.ОтгруженоКоличество КАК КПередаче
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК Таблица) КАК Набор
	|СГРУППИРОВАТЬ ПО
	|	Набор.Распоряжение,
	|	Набор.Номенклатура,
	|	Набор.Характеристика,
	|	Набор.КодСтроки,
	|	Набор.Серия,
	|	Набор.Склад
	|ИМЕЮЩИЕ
	|	&ОтборИмеющие";
	
	ПараметрыФормированияТаблицы = НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"&ОтборПоИзмерениям";
	ПараметрыФормированияТаблицы.Показатели = "Заказано, КОформлению, Оформлено, КОтгрузке, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая =
		ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
		РаспоряженияНаОтгрузкуОборотыРазвернутая);
	
	ПараметрыФормированияТаблицы = НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Активность
		|		И Регистратор = &Регистратор
		|		И &ОтборПоИзмерениям";
	ПараметрыФормированияТаблицы.Показатели = "Оформлено, Отгружено";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);
	
	Если Не ПустаяСтрока(ИмяВременнойТаблицы) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"ПОМЕСТИТЬ ИмяТаблицы",
			"ПОМЕСТИТЬ " + ИмяВременнойТаблицы); // @query-part-1 @query-part-2
		
		ТекстЗапроса = ТекстЗапроса
			+ "
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	КодСтроки"; // @query-part
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ИмяТаблицы", ""); // @query-part
	КонецЕсли;
	
	ТекстОтбораПоИзмерениям = ОбщегоНазначенияУТ.ТекстОтбораПоКоллекцииОтборов(ОтборПоИзмерениям);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИзмерениям", ТекстОтбораПоИзмерениям);
	
	Если Не ПустаяСтрока(Ресурсы) Тогда
		
		Если СтрНайти(Ресурсы, "КОформлению") <> 0 Тогда
			
			Ресурсы = СтрЗаменить(Ресурсы,
				"КОформлению",
				"СУММА(КОформлению)"); // @query-part-2
			
		КонецЕсли;
		
		Если СтрНайти(Ресурсы, "Заказано") <> 0 Тогда
			
			Ресурсы = СтрЗаменить(Ресурсы,
				"Заказано",
				"СУММА(Заказано)"); // @query-part-2
			
		КонецЕсли;
		
		Если СтрНайти(Ресурсы, "КПередаче") <> 0 Тогда
			
			Ресурсы = СтрЗаменить(Ресурсы,
				"КПередаче",
				"СУММА(КПередаче)"); // @query-part-2
			
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ОтборИмеющие",
			Ресурсы);
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ОтборИмеющие",
			"ИСТИНА");
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Текст запроса получает остаток по ресурсам КОформлению и Заявлено
// Остаток дополняется движениями, сделанными накладной заданной в параметре Регистратор.
//
// Параметры:
// 	ИмяВременнойТаблицы - Строка - Поместить результат во временную таблицу с заданным именем. 
// 	ОтборПоИзмерениям - Структура - Ключ - имя измерения, Значение - имя параметра в запросе, например:
//  	Новый Структура("Номенклатура", "Товар") будет преобразовано в тексте запроса в:
//  	"Номенклатура В(&Товар)".
// 	Выражение - Строка - Условие для секции ИМЕЮЩИЕ по ресурсам.
//  	Например, строка вида "КОформлению <> 0" будет преобразована в тексте запроса в:
//  	"СУММА(Набор.КОформлению) <> 0".
// 
// Возвращаемое значение:
// 	Строка - текст запроса.
//
Функция ТекстЗапросаОстаткиПоВозвратам(ИмяВременнойТаблицы = "", ОтборПоИзмерениям = Неопределено, Выражение = "") Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Набор.Распоряжение КАК ЗаказПоставщику,
	|	Набор.Номенклатура КАК Номенклатура,
	|	Набор.Характеристика КАК Характеристика,
	|	Набор.КодСтроки КАК КодСтроки,
	|	СУММА(Набор.Заказано) КАК Заказано,
	|	СУММА(Набор.КОформлению) КАК КОформлению
	|ПОМЕСТИТЬ ИмяТаблицы
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Распоряжение КАК Распоряжение,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.КодСтроки КАК КодСтроки,
	|		(Таблица.ЗаявленоНаВозвратКоличествоОборот
	|			- Таблица.ОформленоПоВозвратамКоличествоОборот) КАК Заказано,
	|		(Таблица.КОформлениюПоВозвратамКоличествоОборот
	|			- Таблица.ОформленоПоВозвратамКоличествоОборот) КАК КОформлению
	|	ИЗ
	|		#ЗаявкиНаВозвратОборотыРазвернутая КАК Таблица
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Распоряжение КАК Распоряжение,
	|		Таблица.Номенклатура КАК Номенклатура,
	|		Таблица.Характеристика КАК Характеристика,
	|		Таблица.КодСтроки КАК КодСтроки,
	|		Таблица.ОформленоПоВозвратамКоличество КАК Заказано,
	|		Таблица.ОформленоПоВозвратамКоличество КАК КОформлению
	|	ИЗ
	|		#РаспоряженияНаОтгрузкуДвиженияРазвернутая КАК Таблица) КАК Набор
	|СГРУППИРОВАТЬ ПО
	|	Набор.Распоряжение,
	|	Набор.Номенклатура,
	|	Набор.Характеристика,
	|	Набор.КодСтроки
	|ИМЕЮЩИЕ
	|	&ОтборИмеющие";
	
	ПараметрыФормированияТаблицы = НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"&ОтборПоИзмерениям";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, КодСтроки";
	ПараметрыФормированияТаблицы.Показатели = "ЗаявленоНаВозврат, КОформлениюПоВозвратам, ОформленоПоВозвратам";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	ЗаявкиНаВозвратОборотыРазвернутая =
		ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#ЗаявкиНаВозвратОборотыРазвернутая",
		ЗаявкиНаВозвратОборотыРазвернутая);
	
	ПараметрыФормированияТаблицы = НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Активность
		|		И Регистратор В (&Регистратор)
		|		И &ОтборПоИзмерениям";
	ПараметрыФормированияТаблицы.Показатели = "ОформленоПоВозвратам";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	ПараметрыФормированияТаблицы.ФизическаяТаблица = Истина;
	
	РаспоряженияНаОтгрузкуДвиженияРазвернутая =
		ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуДвиженияРазвернутая",
		РаспоряженияНаОтгрузкуДвиженияРазвернутая);
	
	Если Не ПустаяСтрока(ИмяВременнойТаблицы) Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ИмяТаблицы", "ПОМЕСТИТЬ " + ИмяВременнойТаблицы); // @query-part-1 @query-part-2
		ТекстЗапроса = ТекстЗапроса
			+ "
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	КодСтроки"; // @query-part
		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ИмяТаблицы", ""); // @query-part
	КонецЕсли;
	
	ТекстОтбораПоИзмерениям = ОбщегоНазначенияУТ.ТекстОтбораПоКоллекцииОтборов(ОтборПоИзмерениям);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОтборПоИзмерениям", ТекстОтбораПоИзмерениям);
	
	Если Не ПустаяСтрока(Выражение) Тогда
		
		Если СтрНайти(Выражение, "КОформлению") <> 0 Тогда
			Выражение = СтрЗаменить(Выражение, "КОформлению", "СУММА(Набор.КОформлению)"); // @query-part-2
		КонецЕсли;
		Если СтрНайти(Выражение, "Заказано") <> 0 Тогда
			Выражение = СтрЗаменить(Выражение, "Заказано", "СУММА(Набор.Заказано)"); // @query-part-2
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ОтборИмеющие",
			Выражение);
		
	Иначе
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ОтборИмеющие",
			"ИСТИНА");
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает структуру параметров для выборки из таблицы оборотов в развернутом виде.
//
// Возвращаемое значение:
// 	Структура:
// 		* Группировка - Неопределено, Строка, Массив Из Строка - массив получаемых в группировке измерений.
// 		* Показатели - Неопределено, Строка, Массив Из Строка - массив получаемых показателей.
// 		* Ресурсы - Неопределено, Строка, Массив Из Строка - массив получаемых ресурсов.
// 		* Индексы - Неопределено, Строка, Массив Из Строка - массив индексов.
// 		* ИмяВременнойТаблицы - Строка - имя временной таблицы, в которую помещается результаты запроса.
// 		* ФизическаяТаблица - Булево - признак формирования выборки из физической таблицы регистра.
// 		* ТекстУсловия - Строка - параметры виртуальной таблицы оборотов или текст условия физической таблицы.
// 		* НачалоПериода - Строка - начало периода (для таблицы оборотов).
// 		* КонецПериода - Строка - конец периода (для таблицы оборотов).
// 		* Периодичность - Строка - периодичность (для таблицы оборотов).
// 		* СинонимТаблицы - Строка - синоним таблицы в запросе.
// 		* ФорматироватьЗапрос - Булево - признак форматирования сформированного запроса.
// 		* ТолькоРазрешенные - Булево - признак выбора только разрешенных записей.
//
Функция НовыйПараметрыФормированияРазвернутойТаблицы() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ТекстУсловия", "");
	Результат.Вставить("Группировка", Неопределено);
	Результат.Вставить("Показатели", Неопределено);
	Результат.Вставить("Ресурсы", Неопределено);
	Результат.Вставить("Индексы", Неопределено);
	Результат.Вставить("ИмяВременнойТаблицы", "");
	Результат.Вставить("ФизическаяТаблица", Ложь);
	Результат.Вставить("НачалоПериода", "");
	Результат.Вставить("КонецПериода", "");
	Результат.Вставить("Периодичность", "");
	Результат.Вставить("СинонимТаблицы", "ТаблицаРегистра");
	Результат.Вставить("ФорматироватьЗапрос", Ложь);
	Результат.Вставить("ТолькоРазрешенные", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Возвращает текст подзапроса или временной таблицы в развернутом виде.
//
// Параметры:
// 	ПараметрыФормированияТаблицы - см. НовыйПараметрыФормированияРазвернутойТаблицы.
//
// Возвращаемое значение:
// 	Строка - текст запроса временной таблицы.
//
Функция ТекстЗапросаРазвернутаяТаблица(Знач ПараметрыФормированияТаблицы) Экспорт
	
	ТекстУсловия = ПараметрыФормированияТаблицы.ТекстУсловия;
	Группировка = ПараметрыФормированияТаблицы.Группировка;
	МассивПоказателей = ПараметрыФормированияТаблицы.Показатели;
	МассивРесурсов = ПараметрыФормированияТаблицы.Ресурсы;
	МассивИндексов = ПараметрыФормированияТаблицы.Индексы;
	ИмяВременнойТаблицы = ПараметрыФормированияТаблицы.ИмяВременнойТаблицы;
	ФизическаяТаблица = ПараметрыФормированияТаблицы.ФизическаяТаблица;
	НачалоПериода = ПараметрыФормированияТаблицы.НачалоПериода;
	КонецПериода = ПараметрыФормированияТаблицы.КонецПериода;
	Периодичность = ПараметрыФормированияТаблицы.Периодичность;
	СинонимТаблицы = ПараметрыФормированияТаблицы.СинонимТаблицы;
	ФорматироватьЗапрос = ПараметрыФормированияТаблицы.ФорматироватьЗапрос;
	ТолькоРазрешенные = ПараметрыФормированияТаблицы.ТолькоРазрешенные;
	
	СформироватьМассивПолейГруппировки(Группировка);
	СформироватьМассивИндексов(МассивИндексов);
	СформироватьМассивПоказателей(МассивПоказателей);
	СформироватьМассивРесурсов(МассивРесурсов);
	
	ТекстИзмерений = "";
	ТекстГруппировки = "";
	ТекстРесурсов = "";
	ТекстИндекса = "";
	ОтборПоПоказателям = "";
	
	// формирование списка индексов
	Для Каждого ИмяИндекса Из МассивИндексов Цикл
		
		ТекстИндекса = ТекстИндекса + СтрШаблон(
			"%1,
			|",
			ИмяИндекса);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТекстИндекса) Тогда
		
		ТекстИндекса = "ИНДЕКСИРОВАТЬ ПО
			|" + ТекстИндекса; // @query-part
		
	КонецЕсли;
	
	// формирование списка ресурсов
	Для Каждого ИмяПоказателя Из МассивПоказателей Цикл
		
		Для Каждого ИмяРесурса Из МассивРесурсов Цикл
			
			ОписаниеПоляРесурса = ОписаниеПоляРесурса(
				ИмяПоказателя,
				ИмяРесурса,
				Не ФизическаяТаблица);
			
			ТекстРесурсов = ТекстРесурсов + ОписаниеПоляРесурса;
			
		КонецЦикла;
		
		ОтборПоПоказателям = ОтборПоПоказателям + СтрШаблон(
			"ЗНАЧЕНИЕ(%1.%2),
			|",
			Метаданные.Перечисления.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ПолноеИмя(),
			ИмяПоказателя); // @query-part
		
	КонецЦикла;
	
	// формирование условия
	ОтборПоПоказателям = Лев(СокрЛП(ОтборПоПоказателям), СтрДлина(СокрЛП(ОтборПоПоказателям)) - 1);
	
	Если ФизическаяТаблица Тогда
		
		Если ЗначениеЗаполнено(ТекстУсловия) Тогда
			
			ТекстУсловия = СтрШаблон("ГДЕ
				|%1
				|И #СинонимТаблицы.Показатель В (%2)",
				ТекстУсловия,
				ОтборПоПоказателям); // @query-part
			
		Иначе
			
			ТекстУсловия = СтрШаблон("ГДЕ
				|#СинонимТаблицы.Показатель В (%1)",
				ОтборПоПоказателям); // @query-part
			
		КонецЕсли;
		
		ТекстИсточникаДанных = СокрЛП(СтрШаблон(
			"РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК #СинонимТаблицы
			|%1",
			ТекстУсловия)); // @query-part
		
	Иначе
		
		Если ЗначениеЗаполнено(ТекстУсловия) Тогда
			
			ТекстУсловия = ТекстУсловия + СтрШаблон("
				|И Показатель В (%1)",
				ОтборПоПоказателям); // @query-part
			
		Иначе
			
			ТекстУсловия = СтрШаблон(
				"Показатель В (%1)",
				ОтборПоПоказателям); // @query-part
			
		КонецЕсли;
		
		ТекстИсточникаДанных = СтрШаблон(
			"РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.Обороты(%1, %2, %3, %4) КАК #СинонимТаблицы",
			НачалоПериода,
			КонецПериода,
			Периодичность,
			ТекстУсловия); // @query-part
		
	КонецЕсли;
	
	// формирование списка полей выборки
	Для Каждого ЭлементГруппировки Из Группировка Цикл
		
		ТекстИзмерений = ТекстИзмерений + СтрШаблон(
			"#СинонимТаблицы.%1 КАК %1,
			|",
			ЭлементГруппировки); // @query-part
		
		ТекстГруппировки = ТекстГруппировки + СтрШаблон(
			"#СинонимТаблицы.%1,
			|",
			ЭлементГруппировки);
		
	КонецЦикла;
	
	ТекстРесурсов = Лев(СокрЛП(ТекстРесурсов), СтрДлина(СокрЛП(ТекстРесурсов)) - 1);
	ТекстГруппировки = Лев(СокрЛП(ТекстГруппировки), СтрДлина(СокрЛП(ТекстГруппировки)) - 1);
	ТекстИндекса = Лев(СокрЛП(ТекстИндекса), СтрДлина(СокрЛП(ТекстИндекса)) - 1);
	ТекстВыборка = ?(ТолькоРазрешенные, "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ВЫБРАТЬ"); // @query-part-1 @query-part-2
	
	// формирование текста запроса
	Если ЗначениеЗаполнено(ИмяВременнойТаблицы) Тогда
		
		ТекстЗапроса = СокрЛП(СтрШаблон(
			"%1
			|%2%3
			|ПОМЕСТИТЬ %4
			|ИЗ %5
			|СГРУППИРОВАТЬ ПО
			|%6
			|%7",
			ТекстВыборка,
			ТекстИзмерений,
			ТекстРесурсов,
			ИмяВременнойТаблицы,
			ТекстИсточникаДанных,
			ТекстГруппировки,
			ТекстИндекса));
		
	Иначе
		
		ТекстЗапроса = СокрЛП(СтрШаблон(
			"%1
			|%2%3
			|ИЗ %4
			|СГРУППИРОВАТЬ ПО
			|%5",
			ТекстВыборка,
			ТекстИзмерений,
			ТекстРесурсов,
			ТекстИсточникаДанных,
			ТекстГруппировки));
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#СинонимТаблицы",
		СинонимТаблицы);
	
	// форматирование текста запроса
	Если ФорматироватьЗапрос Тогда
		
		СхемаЗапроса = Новый СхемаЗапроса();
		СхемаЗапроса.УстановитьТекстЗапроса(СокрЛП(ТекстЗапроса));
		Результат = СхемаЗапроса.ПолучитьТекстЗапроса();
		
	Иначе
		Результат = ТекстЗапроса;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяВременнойТаблицы) Тогда
		Результат = "(" + Результат + ")";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает соответствие кодов ошибок контроля и текстов шаблонов ошибок.
//
// Возвращаемое значение:
// 	Соответствие Из Строка.
//
Функция ТекстыОшибокКонтроляОборотовПоПоказателям() Экспорт
	
	Результат = Новый Соответствие;
	
	Результат.Вставить("Заказано",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'заказано';
				|en = 'ordered'")));
	
	Результат.Вставить("ЗаявленоНаВозврат",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'заявлено на возврат';
				|en = 'requested for return'")));
	
	Результат.Вставить("КОформлению",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'заявлено к оформлению';
				|en = 'requested to register'")));
	
	Результат.Вставить("КОформлениюПоВозвратам",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено по возвратам';
				|en = 'registered on returns'"),
			НСтр("ru = 'заявлено к оформлению';
				|en = 'requested to register'")));
	
	Результат.Вставить("КОтгрузке",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'отгружено';
				|en = 'shipped'"),
			НСтр("ru = 'заявлено к отгрузке';
				|en = 'requested to ship'")));
	
	Результат.Вставить("КПриемке",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'принято';
				|en = 'accepted'"),
			НСтр("ru = 'заявлено к приемке';
				|en = 'requested to receive'")));
	
	Результат.Вставить("Продажи_КОтгрузке",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'заявлено к оформлению';
				|en = 'requested to register'"),
			НСтр("ru = 'заявлено к отгрузке';
				|en = 'requested to ship'")));
	
	Результат.Вставить("Продажи_КОформлению",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'отгружено';
				|en = 'shipped'"),
			НСтр("ru = 'заявлено к оформлению';
				|en = 'requested to register'")));
	
	Результат.Вставить("Продажи_Отгружено",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'отгружено';
				|en = 'shipped'")));
	
	Результат.Вставить("Продажи_КПриемке",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'заявлено к приемке';
				|en = 'requested to receive'"),
			НСтр("ru = 'отгружено';
				|en = 'shipped'")));
	
	Результат.Вставить("Возвраты_ЗаявленоНаВозврат",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'заявлено на возврат';
				|en = 'requested for return'")));
	
	Результат.Вставить("Возвраты_КОформлениюПоВозвратам",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'принято';
				|en = 'accepted'"),
			НСтр("ru = 'заявлено к оформлению';
				|en = 'requested to register'")));
	
	Результат.Вставить("Возвраты_КПриемке",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'оформлено';
				|en = 'registered'"),
			НСтр("ru = 'заявлено к приемке';
				|en = 'requested to receive'")));
	
	Результат.Вставить("Возвраты_ОформленоПоВозвратам",
		ТекстОшибкиКонтроля(
			НСтр("ru = 'принято';
				|en = 'accepted'"),
			НСтр("ru = 'оформлено';
				|en = 'registered'")));
	
	Результат.Вставить("Продажи_Оформлено",
		НСтр("ru = 'По распоряжению %1 оформлено отрицательное количество: %2 %3';
			|en = 'A negative quantity is registered under the ""%1"" reference: %2 %3'"));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	#Область ОбработкаАктуальныхДокументов
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументов";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("417c4cfa-4a72-4d6c-8470-db93bd907396");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ЗарегистрироватьДанныеАктуальныхДокументов";
	Обработчик.ПроцедураПроверки = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра накопления ""Распоряжения на отгрузку и возврат"" для актуальных (незакрытых) распоряжений:
		|- перенос движений из регистра накопления ""Распоряжения на отгрузку""
		|- формирование движений по показателям
		|- вычисление состояний распоряжений';
		|en = 'Fill the ""Shipment and return references"" accumulation register for open references:
		|- Transfer records from the ""Shipment reference"" accumulation register
		|- Generate records by indicators
		|- Calculate reference states'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьРаспоряженияНаОтгрузку.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ОтгрузкаТоваровСХранения.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаРегистров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаТоваровХранителю.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.АктВыполненныхРабот.ПолноеИмя());
	
	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	Блокируемые.Добавить(Метаданные.Документы.ЗаказПереработчику.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаСырьяПереработчику.ПолноеИмя());
	//-- Устарело_Переработка24
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказПереработчику2_5.ПолноеИмя());
	//-- НЕ УТ
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказКлиента.ПолноеИмя());
	
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	Блокируемые.Добавить(Метаданные.Документы.ЗаказДавальца.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаДавальцу.ПолноеИмя());
	//-- Устарело_Переработка24
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказДавальца2_5.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОтчетДавальцу2_5.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ЭтапПроизводства2_2.ПолноеИмя());
	//-- НЕ УТКА
	
	Блокируемые.Добавить(Метаданные.Документы.ОтчетКомитентуОЗакупках.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПоступлениеТоваровОтКлиента.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя());
	
	//++ Локализация
	Блокируемые.Добавить(Метаданные.Документы.СчетФактураВыданный.ПолноеИмя());
	//-- Локализация
	
	Блокируемые.Добавить(Метаданные.Документы.СписаниеОтгруженныхТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ВводОстатковТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОприходованиеИзлишковОтгруженныхТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОтгрузкаТоваровКлиенту.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеДокументовЗаявкаНаВозврат";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументов";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	#КонецОбласти
	
	#Область ОбработкаЗаявокНаВозврат
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеДокументовЗаявкаНаВозврат";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("69925c0b-1bf1-4201-8dfa-273820bfba47");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ЗарегистрироватьДанныеДокументовЗаявкаНаВозврат";
	Обработчик.ПроцедураПроверки = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра накопления ""Распоряжения на отгрузку и возврат"" для документов возврата:
		|- перенос движений из регистра накопления ""Заявки на возврат товаров от клиентов""
		|- формирование движений по показателям
		|- вычисление состояний распоряжений';
		|en = 'Fill the ""Shipment and return references"" accumulation register for return documents:
		|- Transfer records from the ""Sales return requests"" accumulation register
		|- Generate records by indicators
		|- Calculate reference states'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ЗаявкаНаВозвратТоваровОтКлиента.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ВозвратТоваровОтКлиента.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПоступлениеТоваровОтХранителя.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументов";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументов";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	#КонецОбласти
	
	#Область ОбработкаАктуальныхДокументовСторно
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументовСторно";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("dfc64e9a-f553-45b3-80a2-8f534e2c26f3");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ЗарегистрироватьДанныеАктуальныхДокументовСторно";
	Обработчик.ПроцедураПроверки = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра накопления ""Распоряжения на отгрузку и возврат"" для актуальных документов Сторно';
									|en = 'Fill the ""Shipment and return references"" accumulation register for relevant Storno documents'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьРаспоряженияНаОтгрузку.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.Сторно.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументов";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеДокументовЗаявкаНаВозврат";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументов";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	#КонецОбласти
	
	#Область ОбработкаНеактуальныхДокументов
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументов";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("38389743-a028-42e5-b6d5-f75b20ef318e");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ЗарегистрироватьДанныеНеактуальныхДокументов";
	Обработчик.ПроцедураПроверки = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра накопления ""Распоряжения на отгрузку и возврат"" для неактуальных (закрытых) распоряжений';
									|en = 'Fill the ""Shipment and return references"" accumulation register for closed references'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьРаспоряженияНаОтгрузку.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.ОтгрузкаТоваровСХранения.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаРегистров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаТоваровХранителю.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.АктВыполненныхРабот.ПолноеИмя());
	
	//++ НЕ УТ
	
	//++ Устарело_Переработка24
	Блокируемые.Добавить(Метаданные.Документы.ЗаказПереработчику.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаСырьяПереработчику.ПолноеИмя());
	//-- Устарело_Переработка24
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказПереработчику2_5.ПолноеИмя());
	//-- НЕ УТ
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказКлиента.ПолноеИмя());
	
	//++ НЕ УТКА
	
	//++ Устарело_Переработка24
	Блокируемые.Добавить(Метаданные.Документы.ЗаказДавальца.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПередачаДавальцу.ПолноеИмя());
	//-- Устарело_Переработка24
	
	Блокируемые.Добавить(Метаданные.Документы.ЗаказДавальца2_5.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОтчетДавальцу2_5.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ЭтапПроизводства2_2.ПолноеИмя());
	//-- НЕ УТКА
	
	Блокируемые.Добавить(Метаданные.Документы.ОтчетКомитентуОЗакупках.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ПоступлениеТоваровОтКлиента.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.КорректировкаРеализации.ПолноеИмя());
	
	//++ Локализация
	Блокируемые.Добавить(Метаданные.Документы.СчетФактураВыданный.ПолноеИмя());
	//-- Локализация
	
	Блокируемые.Добавить(Метаданные.Документы.СписаниеОтгруженныхТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ВводОстатковТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОприходованиеИзлишковОтгруженныхТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ОтгрузкаТоваровКлиенту.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументов";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеДокументовЗаявкаНаВозврат";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументовСторно";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументовСторно";
	НоваяСтрока.Порядок = "До";
	
	#КонецОбласти
	
	#Область ОбработкаНеактуальныхДокументовСторно
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументовСторно";
	Обработчик.Версия = "11.5.23.24";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("33e6dc7f-eb64-4461-b829-85b9551d8759");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ЗарегистрироватьДанныеНеактуальныхДокументовСторно";
	Обработчик.ПроцедураПроверки = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполнение регистра накопления ""Распоряжения на отгрузку и возврат"" для неактуальных документов Сторно';
									|en = 'Fill the ""Shipment and return references"" accumulation register for irrelevant Storno documents'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.УдалитьРаспоряженияНаОтгрузку.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Документы.Сторно.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументов";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеДокументовЗаявкаНаВозврат";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеАктуальныхДокументовСторно";
	НоваяСтрока.Порядок = "После";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ОбработатьДанныеНеактуальныхДокументов";
	НоваяСтрока.Порядок = "После";
	
	#КонецОбласти
	
КонецПроцедуры

// Отмечает порцию данных для обработки.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
// 	МассивДокументов - Массив Из ДокументСсылка - массив ссылок на документы.
// 	ПолноеИмяРегистра - Строка - имя метаданных регистра накопления.
//
Процедура ОтметитьПорциюКОбработке(Параметры, Знач МассивДокументов, Знач ПолноеИмяРегистра)
	
	ОбновлениеИнформационнойБазы.ОтметитьРегистраторыКОбработке(
		Параметры,
		МассивДокументов,
		ПолноеИмяРегистра);
	
КонецПроцедуры

// Отмечает все данные из выборки для обработки.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
// 	ВыборкаДокументов - ВыборкаИзРезультатаЗапроса - выборка из результата запроса.
// 	ПолноеИмяРегистра - Строка - имя метаданных регистра накопления.
//
Процедура ОтметитьДокументыКОбработке(Параметры, Знач ВыборкаДокументов, Знач ПолноеИмяРегистра)
	
	РазмерПорции = 1000;
	МассивДокументов = Новый Массив;
	
	Пока ВыборкаДокументов.Следующий() Цикл
		
		Если МассивДокументов.Количество() = РазмерПорции Тогда
			
			ОтметитьПорциюКОбработке(Параметры, МассивДокументов, ПолноеИмяРегистра);
			МассивДокументов.Очистить();
			
		КонецЕсли;
		
		МассивДокументов.Добавить(ВыборкаДокументов.Регистратор);
		
	КонецЦикла;
	
	Если МассивДокументов.Количество() Тогда
		
		ОтметитьПорциюКОбработке(Параметры, МассивДокументов, ПолноеИмяРегистра);
		МассивДокументов.Очистить();
		
	КонецЕсли;
	
КонецПроцедуры

// Отмечает к обработке актуальные документы.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеАктуальныхДокументов(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаОтраженныеРегистраторы
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ТаблицаАктуальныеРаспоряжения
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьРаспоряженияНаОтгрузку КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.Сторно)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет)
	|	И Таблица.Регистратор НЕ В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)
	|	И (Таблица.Распоряжение В
	|		(ВЫБРАТЬ
	|			ТаблицаАктуальныеРаспоряжения.Распоряжение
	|		ИЗ
	|			ТаблицаАктуальныеРаспоряжения КАК ТаблицаАктуальныеРаспоряжения)
	|	ИЛИ ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК
	|		Документ.ЗаказКлиента).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Закрыт))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтметитьДокументыКОбработке(Параметры, Выборка, ПолноеИмяРегистра);
	
КонецПроцедуры

// Отмечает к обработке документы.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеДокументовЗаявкаНаВозврат(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаОтраженныеРегистраторы
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.Сторно)
	|	И Таблица.Регистратор НЕ В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтметитьДокументыКОбработке(Параметры, Выборка, ПолноеИмяРегистра);
	
КонецПроцедуры

// Отмечает к обработке документы "Сторно" для актуальных распоряжений.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеАктуальныхДокументовСторно(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаОтраженныеРегистраторы
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.Сторно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ТаблицаАктуальныеРаспоряжения
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьРаспоряженияНаОтгрузку КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.Сторно)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет)
	|	И НЕ Таблица.Регистратор В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)
	|	И (Таблица.Распоряжение В
	|		(ВЫБРАТЬ
	|			ТаблицаАктуальныеРаспоряжения.Распоряжение
	|		ИЗ
	|			ТаблицаАктуальныеРаспоряжения КАК ТаблицаАктуальныеРаспоряжения)
	|	ИЛИ ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК
	|		Документ.ЗаказКлиента).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Закрыт))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.Сторно)
	|	И НЕ Таблица.Регистратор В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтметитьДокументыКОбработке(Параметры, Выборка, ПолноеИмяРегистра);
	
КонецПроцедуры

// Отмечает к обработке неактуальные документы.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеНеактуальныхДокументов(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаОтраженныеРегистраторы
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ТаблицаАктуальныеРаспоряжения
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьРаспоряженияНаОтгрузку КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) <> ТИП(Документ.Сторно)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет)
	|	И Таблица.Регистратор НЕ В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)
	|	И Таблица.Распоряжение НЕ В
	|		(ВЫБРАТЬ
	|			ТаблицаАктуальныеРаспоряжения.Распоряжение
	|		ИЗ
	|			ТаблицаАктуальныеРаспоряжения КАК ТаблицаАктуальныеРаспоряжения)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК
	|		Документ.ЗаказКлиента).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Закрыт))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтметитьДокументыКОбработке(Параметры, Выборка, ПолноеИмяРегистра);
	
КонецПроцедуры

// Отмечает к обработке документы "Сторно" для неактуальных распоряжений.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеНеактуальныхДокументовСторно(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ТаблицаОтраженныеРегистраторы
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.Сторно)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Распоряжение КАК Распоряжение
	|ПОМЕСТИТЬ ТаблицаАктуальныеРаспоряжения
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Регистратор КАК Регистратор
	|ИЗ
	|	РегистрНакопления.УдалитьРаспоряженияНаОтгрузку КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор.Ссылка ЕСТЬ НЕ NULL
	|	И ТИПЗНАЧЕНИЯ(Таблица.Регистратор) = ТИП(Документ.Сторно)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаказКлиента).ЭтоЗаказКакСчет)
	|	И НЕ Таблица.Регистратор В
	|		(ВЫБРАТЬ
	|			ТаблицаОтраженныеРегистраторы.Регистратор
	|		ИЗ
	|			ТаблицаОтраженныеРегистраторы КАК ТаблицаОтраженныеРегистраторы)
	|	И Таблица.Распоряжение НЕ В
	|		(ВЫБРАТЬ
	|			ТаблицаАктуальныеРаспоряжения.Распоряжение
	|		ИЗ
	|			ТаблицаАктуальныеРаспоряжения КАК ТаблицаАктуальныеРаспоряжения)
	|	И НЕ (ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) = ТИП(Документ.ЗаказКлиента)
	|	И ВЫРАЗИТЬ(Таблица.Распоряжение КАК
	|		Документ.ЗаказКлиента).Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовКлиентов.Закрыт))";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ОтметитьДокументыКОбработке(Параметры, Выборка, ПолноеИмяРегистра);
	
КонецПроцедуры

// Отражает заново движения актуальных документов без учета документов Сторно.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
//
Процедура ОбработатьДанныеАктуальныхДокументов(Параметры) Экспорт
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры, Истина);
КонецПроцедуры

// Отражает заново движения документов возврата.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
//
Процедура ОбработатьДанныеДокументовЗаявкаНаВозврат(Параметры) Экспорт
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры, Истина);
КонецПроцедуры

// Отражает заново движения актуальных документов Сторно.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
//
Процедура ОбработатьДанныеАктуальныхДокументовСторно(Параметры) Экспорт
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры, Истина);
КонецПроцедуры

// Отражает заново движения неактуальных документов без учета документов Сторно.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
//
Процедура ОбработатьДанныеНеактуальныхДокументов(Параметры) Экспорт
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры);
КонецПроцедуры

// Отражает заново движения неактуальных документов Сторно.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
//
Процедура ОбработатьДанныеНеактуальныхДокументовСторно(Параметры) Экспорт
	ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры);
КонецПроцедуры

// Отражает заново движения зарегистрированных документов в регистре накопления РаспоряженияНаОтгрузкуИВозврат.
//
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки.
// 	ВключитьБизнесЛогику - Булево - признак включения бизнес-логики при записи набора записей регистра.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры, ВключитьБизнесЛогику = Ложь) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ПолноеИмя();
	ИмяРегистра = Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.Имя;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь,
			ПолноеИмяРегистра);
		
		Возврат;
		
	КонецЕсли;
	
	Рекомендация = НСтр("ru = 'Документ содержит ошибочные данные.
		|Проверьте правильность заполнения документа и перепроведите его.';
		|en = 'The document contains incorrect data.
		|Check that the document is filled in correctly and post it again.'");
	
	Для Каждого ТекущиеДанные Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", ТекущиеДанные.Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Регистратор.Установить(ТекущиеДанные.Регистратор);
			
			ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(
				ТекущиеДанные.Регистратор,
				ИмяРегистра);
			
			ЕстьПроблемныеДанные = Ложь;
			
			ИмяТаблицы = "Таблица" + ИмяРегистра;
			Если ТаблицыДляДвижений.Свойство(ИмяТаблицы) Тогда
				ТаблицаНабораЗаписей = ТаблицыДляДвижений[ИмяТаблицы];
			Иначе
				ЕстьПроблемныеДанные = Истина;
			КонецЕсли;
			
			Если Не ЕстьПроблемныеДанные Тогда
				
				Если ТаблицаНабораЗаписей.Количество() Тогда
					
					Для Каждого СтрокаТаблицы Из ТаблицаНабораЗаписей Цикл
						
						Если Не ЗначениеЗаполнено(СтрокаТаблицы.Распоряжение)
							Или Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
							
							ЕстьПроблемныеДанные = Истина;
							Прервать;
							
						КонецЕсли;
						
					КонецЦикла;
					
					Если Не ЕстьПроблемныеДанные Тогда
						
						НаборЗаписей.Загрузить(ТаблицаНабораЗаписей);
						ОбновлениеИнформационнойБазы.ЗаписатьДанные(НаборЗаписей,, ВключитьБизнесЛогику);
						
					КонецЕсли;
					
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЕстьПроблемныеДанные Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
					ТекущиеДанные.Регистратор,
					Рекомендация,
					Параметры);
					
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ТекущиеДанные.Регистратор);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

// Функция для проверки объектов при открытии форм документов и перед записью.
//
// Параметры:
//  МетаданныеИОтбор - см. ОбновлениеИнформационнойБазы.МетаданныеИОтборПоДанным.
//
// Возвращаемое значение:
//  Булево - Истина, если объект обновлен и доступен для изменения.
//
Функция ДанныеОбновленыНаНовуюВерсиюПрограммы(МетаданныеИОтбор) Экспорт
	
	МетаданныеОбъекта = МетаданныеИОтбор.Метаданные; // ОбъектМетаданных
	Если Метаданные.Документы.Найти(МетаданныеОбъекта.Имя) = Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЗначениеСсылочногоТипа(МетаданныеИОтбор.Отбор) Тогда
		СсылкаНаОбъект = МетаданныеИОтбор.Отбор;
	Иначе
		СсылкаНаОбъект = МетаданныеИОтбор.Данные.Ссылка;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СсылкаНаОбъект", СсылкаНаОбъект);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА
	|ГДЕ
	|	НЕ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат
	|		ГДЕ
	|			Регистратор = &СсылкаНаОбъект)
	|	И (ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрНакопления.УдалитьРаспоряженияНаОтгрузку
	|		ГДЕ
	|			Регистратор = &СсылкаНаОбъект)
	|	ИЛИ ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрНакопления.УдалитьЗаявкиНаВозвратТоваровОтКлиентов
	|		ГДЕ
	|			Регистратор = &СсылкаНаОбъект))";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#Область ПроцедурыИФункцииФормированияРазвернутойТаблицы

Процедура СформироватьМассивПолейГруппировки(Группировка)
	
	Если ТипЗнч(Группировка) = Тип("Строка") Тогда
		
		Группировка = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			Группировка,,
			Истина,
			Истина);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Группировка) Тогда
		
		Группировка = Новый Массив;
		
		Для Каждого Измерение Из Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.Измерения Цикл
			
			Если СтрНачинаетсяС(Измерение.Имя, "Удалить")
				Или Измерение.Имя = "Показатель" Тогда
				Продолжить;
			КонецЕсли;
			
			Группировка.Добавить(Измерение.Имя);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьМассивИндексов(МассивИндексов)
	
	Если ТипЗнч(МассивИндексов) = Тип("Строка") Тогда
		
		МассивИндексов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			МассивИндексов,,
			Истина,
			Истина);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МассивИндексов) Тогда
		МассивИндексов = Новый Массив;
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьМассивПоказателей(МассивПоказателей)
	
	Если ТипЗнч(МассивПоказателей) = Тип("Строка") Тогда
		
		МассивПоказателей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			МассивПоказателей,,
			Истина,
			Истина);
			
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МассивПоказателей) Тогда
		
		МассивПоказателей = Новый Массив;
		
		Для Каждого ЗначениеПеречисления Из Метаданные.Перечисления.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ЗначенияПеречисления Цикл
			МассивПоказателей.Добавить(ЗначениеПеречисления.Имя);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СформироватьМассивРесурсов(МассивРесурсов)
	
	Если ТипЗнч(МассивРесурсов) = Тип("Строка") Тогда
		
		МассивРесурсов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(
			МассивРесурсов,,
			Истина,
			Истина);
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(МассивРесурсов) Тогда
		
		МассивРесурсов = Новый Массив;
		
		Для Каждого Ресурс Из Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.Ресурсы Цикл
			
			Если Не СтрНачинаетсяС(Ресурс.Имя, "Удалить") Тогда
				МассивРесурсов.Добавить(Ресурс.Имя);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеПоляРесурса(ИмяПоказателя, ИмяРесурса, Оборот)
	
	Результат =
	"СУММА(ВЫБОР
	|	КОГДА #СинонимТаблицы.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.[Показатель])
	|		ТОГДА #СинонимТаблицы.[ИмяПоляТаблицы]
	|	ИНАЧЕ 0
	|КОНЕЦ) КАК [ИмяПоляВыборки],
	|"; // @query-part
	
	ИменаПолейВыборки = РаботаСРаспоряжениямиПовтИсп.ИменаПолейВыборки(ИмяПоказателя, ИмяРесурса, Оборот);
	
	ПараметрыШаблона = Новый Структура;
	ПараметрыШаблона.Вставить("Показатель", ИмяПоказателя);
	ПараметрыШаблона.Вставить("ИмяПоляТаблицы", ИменаПолейВыборки.ИмяПоляТаблицы);
	ПараметрыШаблона.Вставить("ИмяПоляВыборки", ИменаПолейВыборки.ИмяПоляВыборки);
	Результат = СтроковыеФункцииКлиентСервер.ВставитьПараметрыВСтроку(Результат, ПараметрыШаблона);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Формирует текст сообщения ошибки контроля по набору переданных действий.
//
// Возвращаемое значение:
// 	Строка.
//
Функция ТекстОшибкиКонтроля(Действие1, Действие2)
	
	Возврат СтрШаблон(
		НСтр("ru = 'По распоряжению %%1 %1 больше, чем %2, на %%2 %%3';
			|en = 'Under the %%1 reference, %1 is greater than %2 by %%2 %%3'"),
		Действие1,
		Действие2);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
