#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		БлокироватьДляИзменения = Истина;
		
		// Текущее состояние набора помещается во временную таблицу "ДвиженияТоварыКОтгрузкеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Распоряжение КАК Распоряжение,
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Серия КАК Серия,
		|	Таблица.КодСтроки КАК КодСтроки,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Показатель КАК Показатель,
		|	Таблица.Количество КАК КоличествоПередЗаписью,
		|	Таблица.Сумма КАК СуммаПередЗаписью,
		|	Таблица.СуммаНДС КАК СуммаНДСПередЗаписью,
		|	Таблица.СуммаНДСУпр КАК СуммаНДСУпрПередЗаписью,
		|	Таблица.СуммаНДСРегл КАК СуммаНДСРеглПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуПередЗаписью
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
		|ГДЕ
		|	Таблица.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|";
		
		Запрос.Выполнить();
		
	КонецЕсли;

	Если Количество() = 0 Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение КАК Распоряжение
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияНаОтгрузкуИВозврат
		|ГДЕ
		|	РаспоряженияНаОтгрузкуИВозврат.Регистратор = &Регистратор";
		
		РаспоряженияДляРасчетаСостояния = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Распоряжение");
		ДополнительныеСвойства.Вставить("РаспоряженияДляРасчетаСостояния", РаспоряженияДляРасчетаСостояния);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Регистратор = Отбор.Регистратор.Значение;
	
	Если ПроведениеДокументов.РассчитыватьИзменения(ДополнительныеСвойства) Тогда
		
		// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
		// и помещается во временную таблицу.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаИзменений.Распоряжение КАК Распоряжение,
		|	ТаблицаИзменений.Номенклатура КАК Номенклатура,
		|	ТаблицаИзменений.Характеристика КАК Характеристика,
		|	ТаблицаИзменений.Серия КАК Серия,
		|	ТаблицаИзменений.КодСтроки КАК КодСтроки,
		|	ТаблицаИзменений.Склад КАК Склад,
		|	ТаблицаИзменений.Показатель КАК Показатель,
		|	СУММА(ТаблицаИзменений.КоличествоИзменение) КАК КоличествоИзменение,
		|	СУММА(ТаблицаИзменений.СуммаИзменение) КАК СуммаИзменение,
		|	СУММА(ТаблицаИзменений.СуммаНДСИзменение) КАК СуммаНДСИзменение,
		|	СУММА(ТаблицаИзменений.СуммаНДСУпрИзменение) КАК СуммаНДСУпрИзменение,
		|	СУММА(ТаблицаИзменений.СуммаНДСРеглИзменение) КАК СуммаНДСРеглИзменение
		|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуИзменение
		|ИЗ
		|	(ВЫБРАТЬ
		|		Таблица.Распоряжение КАК Распоряжение,
		|		Таблица.Номенклатура КАК Номенклатура,
		|		Таблица.Характеристика КАК Характеристика,
		|		Таблица.Серия КАК Серия,
		|		Таблица.КодСтроки КАК КодСтроки,
		|		Таблица.Склад КАК Склад,
		|		Таблица.Показатель КАК Показатель,
		|		Таблица.КоличествоПередЗаписью КАК КоличествоИзменение,
		|		Таблица.СуммаПередЗаписью КАК СуммаИзменение,
		|		Таблица.СуммаНДСПередЗаписью КАК СуммаНДСИзменение,
		|		Таблица.СуммаНДСУпрПередЗаписью КАК СуммаНДСУпрИзменение,
		|		Таблица.СуммаНДСРеглПередЗаписью КАК СуммаНДСРеглИзменение
		|	ИЗ
		|		ДвиженияРаспоряженияНаОтгрузкуПередЗаписью КАК Таблица
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Таблица.Распоряжение,
		|		Таблица.Номенклатура,
		|		Таблица.Характеристика,
		|		Таблица.Серия,
		|		Таблица.КодСтроки,
		|		Таблица.Склад,
		|		Таблица.Показатель,
		|		-Таблица.Количество,
		|		-Таблица.Сумма,
		|		-Таблица.СуммаНДС,
		|		-Таблица.СуммаНДСУпр,
		|		-Таблица.СуммаНДСРегл
		|	ИЗ
		|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Таблица
		|	ГДЕ
		|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаИзменений.Распоряжение,
		|	ТаблицаИзменений.Номенклатура,
		|	ТаблицаИзменений.Характеристика,
		|	ТаблицаИзменений.Серия,
		|	ТаблицаИзменений.КодСтроки,
		|	ТаблицаИзменений.Склад,
		|	ТаблицаИзменений.Показатель
		|ИМЕЮЩИЕ
		|	СУММА(ТаблицаИзменений.КоличествоИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.СуммаИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.СуммаНДСИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.СуммаНДСУпрИзменение) <> 0
		|	ИЛИ СУММА(ТаблицаИзменений.СуммаНДСРеглИзменение) <> 0";
		
		МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
		РезультатЗапроса = МассивРезультатовЗапроса[0]; // РезультатЗапроса
		Выборка = РезультатЗапроса.Выбрать();
		
		ЕстьИзменения = Выборка.Следующий() И Выборка.Количество > 0;
		ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства,
			"ДвиженияРаспоряженияНаОтгрузкуИзменение",
			ЕстьИзменения);
		
		Если ЕстьИзменения
			И ПолучитьФункциональнуюОпцию("ИспользоватьДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров") Тогда
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
			Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров",
				Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());
			Запрос.УстановитьПараметр("МерныеТипыЕдиницИзмерений",
				Справочники.УпаковкиЕдиницыИзмерения.МерныеТипыЕдиницИзмерений());
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТаблицаОборотов.Распоряжение КАК Распоряжение,
			|	ТаблицаОборотов.КодСтроки КАК КодСтроки,
			|	МАКСИМУМ(ТаблицаОборотов.Склад) КАК Склад,
			|	МАКСИМУМ(ТаблицаОборотов.Серия) КАК Серия
			|ПОМЕСТИТЬ ТаблицаСкладыСерии
			|ИЗ
			|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.Обороты(,,, (Распоряжение, Номенклатура, Характеристика) В
			|		(ВЫБРАТЬ
			|			Таблица.Распоряжение,
			|			Таблица.Номенклатура,
			|			Таблица.Характеристика
			|		ИЗ
			|			ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица)
			|	И КодСтроки <> 0
			|	И Показатель В (ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Заказано),
			|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
			|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено))) КАК ТаблицаОборотов
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаОборотов.Распоряжение,
			|	ТаблицаОборотов.КодСтроки
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Распоряжение,
			|	КодСтроки
			|;
			|
			|ВЫБРАТЬ
			|	Таблица.Распоряжение КАК Распоряжение,
			|	Таблица.Номенклатура КАК Номенклатура,
			|	Таблица.Характеристика КАК Характеристика,
			|	Таблица.КодСтроки КАК КодСтроки,
			|	ВЫБОР
			|		КОГДА Таблица.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
			|		ИНАЧЕ Таблица.Серия
			|	КОНЕЦ КАК Серия,
			|	ВЫБОР
			|		КОГДА Таблица.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
			|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
			|		ИНАЧЕ Таблица.Склад
			|	КОНЕЦ КАК Склад,
			|	Таблица.Показатель КАК Показатель
			|ПОМЕСТИТЬ ДвиженияРаспоряженияНаОтгрузкуИзменениеМерныеТовары
			|ИЗ
			|	ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ТаблицаНоменклатуры
			|		ПО Таблица.Номенклатура = ТаблицаНоменклатуры.Ссылка
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ТаблицаЕдиницИзмерения
			|		ПО ТаблицаНоменклатуры.ЕдиницаИзмерения = ТаблицаЕдиницИзмерения.Ссылка
			|		И ТаблицаЕдиницИзмерения.ТипИзмеряемойВеличины В (&МерныеТипыЕдиницИзмерений)
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладыСерии КАК ТаблицаСкладыСерии
			|		ПО Таблица.Распоряжение = ТаблицаСкладыСерии.Распоряжение
			|		И Таблица.КодСтроки = ТаблицаСкладыСерии.КодСтроки
			|		И Таблица.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)";
			
			// Основная таблица
			ТекстЗапроса = ТекстЗапроса
				+ ОбщегоНазначения.РазделительПакетаЗапросов()
				+ "ВЫБРАТЬ
				|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение КАК Распоряжение,
				|	РаспоряженияНаОтгрузкуИВозврат.Номенклатура КАК Номенклатура,
				|	РаспоряженияНаОтгрузкуИВозврат.Характеристика КАК Характеристика,
				|	РаспоряженияНаОтгрузкуИВозврат.КодСтроки КАК КодСтроки,
				|	ВЫБОР
				|		КОГДА РаспоряженияНаОтгрузкуИВозврат.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
				|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
				|		ИНАЧЕ РаспоряженияНаОтгрузкуИВозврат.Склад
				|	КОНЕЦ КАК Склад,
				|	ВЫБОР
				|		КОГДА РаспоряженияНаОтгрузкуИВозврат.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
				|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
				|		ИНАЧЕ РаспоряженияНаОтгрузкуИВозврат.Серия
				|	КОНЕЦ КАК Серия,
				|	РаспоряженияНаОтгрузкуИВозврат.Показатель КАК Показатель,
				|	РаспоряженияНаОтгрузкуИВозврат.Количество КАК Количество
				|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузку
				|ИЗ
				|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияНаОтгрузкуИВозврат
				|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладыСерии КАК ТаблицаСкладыСерии
				|		ПО РаспоряженияНаОтгрузкуИВозврат.Распоряжение = ТаблицаСкладыСерии.Распоряжение
				|		И РаспоряженияНаОтгрузкуИВозврат.КодСтроки = ТаблицаСкладыСерии.КодСтроки
				|		И РаспоряженияНаОтгрузкуИВозврат.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
				|ГДЕ
				|	(РаспоряженияНаОтгрузкуИВозврат.Распоряжение, РаспоряженияНаОтгрузкуИВозврат.Номенклатура,
				|		РаспоряженияНаОтгрузкуИВозврат.Характеристика) В
				|		(ВЫБРАТЬ
				|			Таблица.Распоряжение,
				|			Таблица.Номенклатура,
				|			Таблица.Характеристика
				|		ИЗ
				|			ДвиженияРаспоряженияНаОтгрузкуИзменение КАК Таблица)
				|	И РаспоряженияНаОтгрузкуИВозврат.Показатель В
				|	(ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлениюПоВозвратам),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ОформленоПоВозвратам),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КПриемке),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Принято),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ПринятоПоВозвратам))
				|	И ТИПЗНАЧЕНИЯ(РаспоряженияНаОтгрузкуИВозврат.Регистратор) <>
				|		ТИП(Документ.СписаниеОтгруженныхТоваров)";
			
			// Допустимые отклонения
			ТекстЗапроса = ТекстЗапроса
				+ ОбщегоНазначения.РазделительПакетаЗапросов()
				+ "ВЫБРАТЬ
				|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение КАК Распоряжение,
				|	РаспоряженияНаОтгрузкуИВозврат.Номенклатура КАК Номенклатура,
				|	РаспоряженияНаОтгрузкуИВозврат.Характеристика КАК Характеристика,
				|	РаспоряженияНаОтгрузкуИВозврат.Склад КАК Склад,
				|	РаспоряженияНаОтгрузкуИВозврат.Серия КАК Серия,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) * &ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100 КАК ДопустимоеОтклонениеКОформлению,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) * &ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100 КАК ДопустимоеОтклонениеКОтгрузке,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлениюПоВозвратам)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) * &ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100 КАК ДопустимоеОтклонениеКВозврату,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КПриемке)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) * &ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров / 100 КАК ДопустимоеОтклонениеКПриемке
				|ПОМЕСТИТЬ ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку
				|ИЗ
				|	ВТРаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузкуИВозврат
				|ГДЕ
				|	(РаспоряженияНаОтгрузкуИВозврат.Распоряжение, РаспоряженияНаОтгрузкуИВозврат.Номенклатура,
				|		РаспоряженияНаОтгрузкуИВозврат.Характеристика) В
				|		(ВЫБРАТЬ
				|			ТаблицаИзменений.Распоряжение,
				|			ТаблицаИзменений.Номенклатура,
				|			ТаблицаИзменений.Характеристика
				|		ИЗ
				|			ДвиженияРаспоряженияНаОтгрузкуИзменениеМерныеТовары КАК ТаблицаИзменений)
				|	И РаспоряженияНаОтгрузкуИВозврат.Показатель В
				|	(ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлениюПоВозвратам),
				|		ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КПриемке))
				|СГРУППИРОВАТЬ ПО
				|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение,
				|	РаспоряженияНаОтгрузкуИВозврат.Номенклатура,
				|	РаспоряженияНаОтгрузкуИВозврат.Характеристика,
				|	РаспоряженияНаОтгрузкуИВозврат.Склад,
				|	РаспоряженияНаОтгрузкуИВозврат.Серия
				|
				|ИНДЕКСИРОВАТЬ ПО
				|	Распоряжение,
				|	Номенклатура,
				|	Характеристика,
				|	Склад,
				|	Серия";
			
			//Остатки
			ТекстЗапроса = ТекстЗапроса
				+ ОбщегоНазначения.РазделительПакетаЗапросов()
				+ "ВЫБРАТЬ
				|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение КАК Распоряжение,
				|	РаспоряженияНаОтгрузкуИВозврат.Номенклатура КАК Номенклатура,
				|	РаспоряженияНаОтгрузкуИВозврат.Характеристика КАК Характеристика,
				|	РаспоряженияНаОтгрузкуИВозврат.Склад КАК Склад,
				|	РаспоряженияНаОтгрузкуИВозврат.Серия КАК Серия,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
				|			ТОГДА -РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) КАК КОформлениюОстаток,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено)
				|			ТОГДА -РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) КАК КОтгрузкеОстаток,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлениюПоВозвратам)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ОформленоПоВозвратам)
				|			ТОГДА -РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) КАК КВозвратуОстаток,
				|	СУММА(ВЫБОР
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КПриемке)
				|			ТОГДА РаспоряженияНаОтгрузкуИВозврат.Количество
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Принято)
				|			ТОГДА -РаспоряженияНаОтгрузкуИВозврат.Количество
				|		КОГДА
				|			РаспоряженияНаОтгрузкуИВозврат.Показатель =
				|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.ПринятоПоВозвратам)
				|			ТОГДА -РаспоряженияНаОтгрузкуИВозврат.Количество
				|		ИНАЧЕ 0
				|	КОНЕЦ) КАК КПриемкеОстаток
				|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузкуОстатки
				|ИЗ
				|	ВТРаспоряженияНаОтгрузку КАК РаспоряженияНаОтгрузкуИВозврат
				|СГРУППИРОВАТЬ ПО
				|	РаспоряженияНаОтгрузкуИВозврат.Распоряжение,
				|	РаспоряженияНаОтгрузкуИВозврат.Номенклатура,
				|	РаспоряженияНаОтгрузкуИВозврат.Характеристика,
				|	РаспоряженияНаОтгрузкуИВозврат.Склад,
				|	РаспоряженияНаОтгрузкуИВозврат.Серия";
			
			ТекстЗапроса = ТекстЗапроса
				+ ОбщегоНазначения.РазделительПакетаЗапросов()
				+ "ВЫБРАТЬ РАЗЛИЧНЫЕ
				|	ОстаткиПоРаспоряжениям.Распоряжение КАК Распоряжение
				|ИЗ
				|	ВТРаспоряженияНаОтгрузкуОстатки КАК ОстаткиПоРаспоряжениям
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДопустимыеОтклоненияРаспоряженияНаОтгрузку КАК ДопустимыеОтклонения
				|		ПО ОстаткиПоРаспоряжениям.Распоряжение = ДопустимыеОтклонения.Распоряжение
				|		И ОстаткиПоРаспоряжениям.Номенклатура = ДопустимыеОтклонения.Номенклатура
				|		И ОстаткиПоРаспоряжениям.Характеристика = ДопустимыеОтклонения.Характеристика
				|		И ОстаткиПоРаспоряжениям.Склад = ДопустимыеОтклонения.Склад
				|		И ОстаткиПоРаспоряжениям.Серия = ДопустимыеОтклонения.Серия
				|ГДЕ
				|	ОстаткиПоРаспоряжениям.КОформлениюОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКОформлению
				|	И ОстаткиПоРаспоряжениям.КОформлениюОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКОформлению
				|	И ОстаткиПоРаспоряжениям.КОформлениюОстаток <> 0
				|	ИЛИ ОстаткиПоРаспоряжениям.КОтгрузкеОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКОтгрузке
				|	И ОстаткиПоРаспоряжениям.КОтгрузкеОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКОтгрузке
				|	И ОстаткиПоРаспоряжениям.КОтгрузкеОстаток <> 0
				|	ИЛИ ОстаткиПоРаспоряжениям.КВозвратуОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКВозврату
				|	И ОстаткиПоРаспоряжениям.КВозвратуОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКВозврату
				|	И ОстаткиПоРаспоряжениям.КВозвратуОстаток <> 0
				|	ИЛИ ОстаткиПоРаспоряжениям.КПриемкеОстаток <= ДопустимыеОтклонения.ДопустимоеОтклонениеКПриемке
				|	И ОстаткиПоРаспоряжениям.КПриемкеОстаток >= -ДопустимыеОтклонения.ДопустимоеОтклонениеКПриемке
				|	И ОстаткиПоРаспоряжениям.КПриемкеОстаток <> 0";
			
			Запрос.Текст = ТекстЗапроса;
			
			ВыборкаРаспоряжение = Запрос.Выполнить().Выбрать();
			Пока ВыборкаРаспоряжение.Следующий() Цикл
				РегистрыСведений.ОчередьЗаказовККорректировкеСтрокМерныхТоваров.ДобавитьЗаказВОчередь(ВыборкаРаспоряжение.Распоряжение);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Распоряжения = Новый Массив;
	ТипыРаспоряжений = Новый Массив;
	
	Если Количество() Тогда
		
		Для Каждого Запись Из ЭтотОбъект Цикл
			
			Если Распоряжения.Найти(Запись.Распоряжение) = Неопределено Тогда
				Распоряжения.Добавить(Запись.Распоряжение);
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Если ДополнительныеСвойства.Свойство("РаспоряженияДляРасчетаСостояния") Тогда
			Распоряжения = ДополнительныеСвойства.РаспоряженияДляРасчетаСостояния;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Распоряжения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Распоряжение Из Распоряжения Цикл
		
		Если ТипыРаспоряжений.Найти(ТипЗнч(Распоряжение)) = Неопределено Тогда
			ТипыРаспоряжений.Добавить(ТипЗнч(Распоряжение));
		КонецЕсли;
		
	КонецЦикла;
	
	МинимальнаяДатаРаспоряжений = МинимальнаяДатаРаспоряжений(Распоряжения);
	Если Не ЗначениеЗаполнено(МинимальнаяДатаРаспоряжений) Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению");
		ЭлементБлокировки.ИсточникДанных = Выгрузить();
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Распоряжение", "Распоряжение");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Попытка
			
			Блокировка.Заблокировать();
			
			Для Каждого Распоряжение Из Распоряжения Цикл
				
				НаборРаспоряженияНаОтгрузкуКВыполнению = РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.СоздатьНаборЗаписей();
				НаборРаспоряженияНаОтгрузкуКВыполнению.Отбор.Распоряжение.Установить(Распоряжение);
				НаборРаспоряженияНаОтгрузкуКВыполнению.Записать();
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение(ОписаниеОшибки());
		КонецПопытки;
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
		Запрос.УстановитьПараметр("НачалоПериода", МинимальнаяДатаРаспоряжений);
		Запрос.УстановитьПараметр("Регистратор", Регистратор);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	РаспоряженияНаОтгрузку.Распоряжение КАК Распоряжение,
		|	РаспоряженияНаОтгрузку.Номенклатура КАК Номенклатура,
		|	РаспоряженияНаОтгрузку.Характеристика КАК Характеристика,
		|	РаспоряженияНаОтгрузку.КодСтроки КАК КодСтроки,
		|	РаспоряженияНаОтгрузку.Склад КАК Склад,
		|	РаспоряженияНаОтгрузку.Серия КАК Серия,
		|	СУММА(РаспоряженияНаОтгрузку.Заказано) КАК Заказано,
		|	СУММА(РаспоряженияНаОтгрузку.ЗаявленоНаВозврат) КАК ЗаявленоНаВозврат,
		|	СУММА(РаспоряженияНаОтгрузку.КОформлению) КАК КОформлению,
		|	СУММА(РаспоряженияНаОтгрузку.Оформлено) КАК Оформлено,
		|	СУММА(РаспоряженияНаОтгрузку.ОформленоСумма) КАК ОформленоСумма,
		|	СУММА(РаспоряженияНаОтгрузку.КОформлениюПоВозвратам) КАК КОформлениюПоВозвратам,
		|	СУММА(РаспоряженияНаОтгрузку.ОформленоПоВозвратам) КАК ОформленоПоВозвратам,
		|	СУММА(РаспоряженияНаОтгрузку.КОтгрузке) КАК КОтгрузке,
		|	СУММА(РаспоряженияНаОтгрузку.Отгружено) КАК Отгружено,
		|	СУММА(РаспоряженияНаОтгрузку.КПриемке) КАК КПриемке,
		|	СУММА(РаспоряженияНаОтгрузку.Принято) КАК Принято,
		|	СУММА(РаспоряженияНаОтгрузку.ПринятоПоВозвратам) КАК ПринятоПоВозвратам,
		|	СУММА(РаспоряженияНаОтгрузку.ОформленоСчетовФактур) КАК ОформленоСчетовФактур,
		|	СУММА(РаспоряженияНаОтгрузку.ОформленоСчетовФактурСумма) КАК ОформленоСчетовФактурСумма
		|ПОМЕСТИТЬ РаспоряженияНаОтгрузкуРазвернутаяОбороты
		|ИЗ
		|	(ВЫБРАТЬ
		|		РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
		|		РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
		|		РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
		|		РаспоряженияНаОтгрузкуОбороты.КодСтроки КАК КодСтроки,
		|		РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
		|		РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
		|		РаспоряженияНаОтгрузкуОбороты.ЗаказаноКоличествоОборот КАК Заказано,
		|		РаспоряженияНаОтгрузкуОбороты.ЗаявленоНаВозвратКоличествоОборот КАК ЗаявленоНаВозврат,
		|		РаспоряженияНаОтгрузкуОбороты.КОформлениюКоличествоОборот КАК КОформлению,
		|		РаспоряженияНаОтгрузкуОбороты.ОформленоКоличествоОборот КАК Оформлено,
		|		РаспоряженияНаОтгрузкуОбороты.ОформленоСуммаНДСРеглОборот КАК ОформленоСумма,
		|		РаспоряженияНаОтгрузкуОбороты.КОформлениюПоВозвратамКоличествоОборот КАК КОформлениюПоВозвратам,
		|		РаспоряженияНаОтгрузкуОбороты.ОформленоПоВозвратамКоличествоОборот КАК ОформленоПоВозвратам,
		|		РаспоряженияНаОтгрузкуОбороты.КОтгрузкеКоличествоОборот КАК КОтгрузке,
		|		РаспоряженияНаОтгрузкуОбороты.ОтгруженоКоличествоОборот КАК Отгружено,
		|		РаспоряженияНаОтгрузкуОбороты.КПриемкеКоличествоОборот КАК КПриемке,
		|		РаспоряженияНаОтгрузкуОбороты.ПринятоКоличествоОборот КАК Принято,
		|		РаспоряженияНаОтгрузкуОбороты.ПринятоПоВозвратамКоличествоОборот КАК ПринятоПоВозвратам,
		|		РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактурКоличествоОборот КАК ОформленоСчетовФактур,
		|		РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактурСуммаНДСРеглОборот КАК ОформленоСчетовФактурСумма
		|	ИЗ
		|		#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОбороты
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		РаспоряженияНаОтгрузкуДвижения.Распоряжение КАК Распоряжение,
		|		РаспоряженияНаОтгрузкуДвижения.Номенклатура КАК Номенклатура,
		|		РаспоряженияНаОтгрузкуДвижения.Характеристика КАК Характеристика,
		|		РаспоряженияНаОтгрузкуДвижения.КодСтроки КАК КодСтроки,
		|		РаспоряженияНаОтгрузкуДвижения.Склад КАК Склад,
		|		РаспоряженияНаОтгрузкуДвижения.Серия КАК Серия,
		|		РаспоряженияНаОтгрузкуДвижения.Количество КАК Заказано,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0
		|	ИЗ
		|		РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияНаОтгрузкуДвижения
		|	ГДЕ
		|		РаспоряженияНаОтгрузкуДвижения.Распоряжение В (&Распоряжения)
		|		И
		|			РаспоряженияНаОтгрузкуДвижения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОформлению)
		|		И РаспоряженияНаОтгрузкуДвижения.Количество < 0
		|		И ТИПЗНАЧЕНИЯ(РаспоряженияНаОтгрузкуДвижения.Регистратор) В (ТИП(Документ.СписаниеОтгруженныхТоваров),
		|			ТИП(Документ.ПоступлениеТоваровОтКлиента))) КАК РаспоряженияНаОтгрузку
		|СГРУППИРОВАТЬ ПО
		|	РаспоряженияНаОтгрузку.Распоряжение,
		|	РаспоряженияНаОтгрузку.Номенклатура,
		|	РаспоряженияНаОтгрузку.Характеристика,
		|	РаспоряженияНаОтгрузку.КодСтроки,
		|	РаспоряженияНаОтгрузку.Склад,
		|	РаспоряженияНаОтгрузку.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОборотов.Распоряжение КАК Распоряжение,
		|	ТаблицаОборотов.КодСтроки КАК КодСтроки,
		|	МАКСИМУМ(ТаблицаОборотов.Склад) КАК Склад,
		|	МАКСИМУМ(ТаблицаОборотов.Серия) КАК Серия
		|ПОМЕСТИТЬ ТаблицаСкладыСерии
		|ИЗ
		|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат.Обороты(,,, Распоряжение В (&Распоряжения)
		|		И КодСтроки <> 0
		|		И Показатель В
		|			(ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Заказано),
		|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.КОтгрузке),
		|			ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено))) КАК ТаблицаОборотов
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОборотов.Распоряжение,
		|	ТаблицаОборотов.КодСтроки
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	КодСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОборотов.Распоряжение КАК Распоряжение,
		|	ТаблицаОборотов.Номенклатура КАК Номенклатура,
		|	ТаблицаОборотов.Характеристика КАК Характеристика,
		|	ВЫБОР
		|		КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|		ИНАЧЕ ТаблицаОборотов.Склад
		|	КОНЕЦ КАК Склад,
		|	ВЫБОР
		|		КОГДА ТаблицаОборотов.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
		|		ИНАЧЕ ТаблицаОборотов.Серия
		|	КОНЕЦ КАК Серия,
		|	СУММА(ТаблицаОборотов.Заказано) КАК Заказано,
		|	СУММА(ТаблицаОборотов.ЗаявленоНаВозврат) КАК ЗаявленоНаВозврат,
		|	СУММА(ТаблицаОборотов.КОформлению) КАК КОформлению,
		|	СУММА(ТаблицаОборотов.Оформлено) КАК Оформлено,
		|	СУММА(ТаблицаОборотов.ОформленоСумма) КАК ОформленоСумма,
		|	СУММА(ТаблицаОборотов.КОформлениюПоВозвратам) КАК КОформлениюПоВозвратам,
		|	СУММА(ТаблицаОборотов.ОформленоПоВозвратам) КАК ОформленоПоВозвратам,
		|	СУММА(ТаблицаОборотов.КОтгрузке) КАК КОтгрузке,
		|	СУММА(ТаблицаОборотов.Отгружено) КАК Отгружено,
		|	СУММА(ТаблицаОборотов.КПриемке) КАК КПриемке,
		|	СУММА(ТаблицаОборотов.Принято) КАК Принято,
		|	СУММА(ТаблицаОборотов.ПринятоПоВозвратам) КАК ПринятоПоВозвратам,
		|	СУММА(ТаблицаОборотов.ОформленоСчетовФактур) КАК ОформленоСчетовФактур,
		|	СУММА(ТаблицаОборотов.ОформленоСчетовФактурСумма) КАК ОформленоСчетовФактурСумма
		|ПОМЕСТИТЬ ВТРаспоряженияНаОтгрузкуОбороты
		|ИЗ
		|	РаспоряженияНаОтгрузкуРазвернутаяОбороты КАК ТаблицаОборотов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладыСерии КАК ТаблицаСкладыСерии
		|		ПО ТаблицаОборотов.Распоряжение = ТаблицаСкладыСерии.Распоряжение
		|		И ТаблицаОборотов.КодСтроки = ТаблицаСкладыСерии.КодСтроки
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаОборотов.Распоряжение,
		|	ТаблицаОборотов.Номенклатура,
		|	ТаблицаОборотов.Характеристика,
		|	ТаблицаОборотов.КодСтроки,
		|	ВЫБОР
		|		КОГДА ТаблицаОборотов.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
		|		ИНАЧЕ ТаблицаОборотов.Склад
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ТаблицаОборотов.Серия = ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			ТОГДА ЕСТЬNULL(ТаблицаСкладыСерии.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
		|		ИНАЧЕ ТаблицаОборотов.Серия
		|	КОНЕЦ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
		|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
		|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
		|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
		|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.Заказано <= РаспоряженияНаОтгрузкуОбороты.Оформлено
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.Оформлено = 0
		|		И РаспоряженияНаОтгрузкуОбороты.Заказано > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеЗаказано,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОформлению <= РаспоряженияНаОтгрузкуОбороты.Оформлено
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.Оформлено = 0
		|		И РаспоряженияНаОтгрузкуОбороты.КОформлению > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКОформлению,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОтгрузке <= РаспоряженияНаОтгрузкуОбороты.Отгружено
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.Отгружено = 0
		|		И РаспоряженияНаОтгрузкуОбороты.КОтгрузке > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКОтгрузке,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ЗаявленоНаВозврат <= РаспоряженияНаОтгрузкуОбороты.ОформленоПоВозвратам
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоПоВозвратам = 0
		|		И РаспоряженияНаОтгрузкуОбороты.ЗаявленоНаВозврат > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеЗаявленоНаВозврат,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОформлениюПоВозвратам <= РаспоряженияНаОтгрузкуОбороты.ОформленоПоВозвратам
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоПоВозвратам = 0
		|		И РаспоряженияНаОтгрузкуОбороты.КОформлениюПоВозвратам > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКОформлениюПоВозвратам,
		|	ВЫБОР
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КПриемке <= РаспоряженияНаОтгрузкуОбороты.Принято +
		|			РаспоряженияНаОтгрузкуОбороты.ПринятоПоВозвратам
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.Принято + РаспоряженияНаОтгрузкуОбороты.ПринятоПоВозвратам = 0
		|		И РаспоряженияНаОтгрузкуОбороты.КПриемке > 0
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКПриемке,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РаспоряженияНаОтгрузкуОбороты.Распоряжение) НЕ В (ТИП(Документ.ЗаказКлиента),
		|			ТИП(Документ.ОтгрузкаТоваровКлиенту), ТИП(Документ.ПервичныйДокумент),
		|			ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента), ТИП(Документ.КорректировкаРеализации))
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОтгрузке -
		|			РаспоряженияНаОтгрузкуОбороты.Отгружено = РаспоряженияНаОтгрузкуОбороты.КОформлению -
		|			РаспоряженияНаОтгрузкуОбороты.Оформлено
		|			ТОГДА 1
		|		КОГДА РаспоряженияНаОтгрузкуОбороты.КОформлению - РаспоряженияНаОтгрузкуОбороты.Оформлено > 0
		|		И РаспоряженияНаОтгрузкуОбороты.КОформлению -
		|			РаспоряженияНаОтгрузкуОбороты.Оформлено = РаспоряженияНаОтгрузкуОбороты.КОтгрузке
		|			ТОГДА 2
		|		ИНАЧЕ 3
		|	КОНЕЦ КАК СостояниеКПереходуПраваСобственности,
		|	ВЫБОР
		|		КОГДА ТИПЗНАЧЕНИЯ(РаспоряженияНаОтгрузкуОбороты.Распоряжение) НЕ В (ТИП(Документ.ЗаказКлиента),
		|			ТИП(Документ.ОтгрузкаТоваровКлиенту), ТИП(Документ.ПервичныйДокумент),
		|			ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента), ТИП(Документ.КорректировкаРеализации))
		|			ТОГДА 1
		|		КОГДА ВЫБОР
		|			КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур = РаспоряженияНаОтгрузкуОбороты.Оформлено
		|			ИЛИ РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур = 0
		|				ТОГДА 1
		|			КОГДА РаспоряженияНаОтгрузкуОбороты.Оформлено = 0
		|			И РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур > 0
		|				ТОГДА 2
		|			ИНАЧЕ 3
		|		КОНЕЦ = 1
		|			ТОГДА ВЫБОР
		|				КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактурСумма = РаспоряженияНаОтгрузкуОбороты.ОформленоСумма
		|				ИЛИ РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактурСумма = 0
		|					ТОГДА 1
		|				КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоСумма = 0
		|				И РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактурСумма > 0
		|					ТОГДА 2
		|				ИНАЧЕ 3
		|			КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|			КОГДА РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур = РаспоряженияНаОтгрузкуОбороты.Оформлено
		|			ИЛИ РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур = 0
		|				ТОГДА 1
		|			КОГДА РаспоряженияНаОтгрузкуОбороты.Оформлено = 0
		|			И РаспоряженияНаОтгрузкуОбороты.ОформленоСчетовФактур > 0
		|				ТОГДА 2
		|			ИНАЧЕ 3
		|		КОНЕЦ
		|	КОНЕЦ КАК СостояниеКОформлениюСчетовФактур
		|ПОМЕСТИТЬ ВТСостоянияРаспоряжений
		|ИЗ
		|	ВТРаспоряженияНаОтгрузкуОбороты КАК РаспоряженияНаОтгрузкуОбороты
		|ГДЕ
		|	НЕ РаспоряженияНаОтгрузкуОбороты.Распоряжение ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СостоянияРаспоряжений.Распоряжение КАК Распоряжение,
		|	СостоянияРаспоряжений.Склад КАК Склад,
		|	&Организация КАК Организация,
		|	&Номер КАК Номер,
		|	&Дата КАК Дата,
		|	&СуммаДокумента КАК СуммаДокумента,
		|	&Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА СостоянияРаспоряжений.Распоряжение ССЫЛКА Документ.ПервичныйДокумент
		|			ТОГДА &Регистратор_ХозяйственнаяОперация
		|		ИНАЧЕ &ХозяйственнаяОперация
		|	КОНЕЦ КАК ХозяйственнаяОперация,
		|	&Партнер КАК Партнер,
		|	&Контрагент КАК Контрагент,
		|	ВЫБОР
		|		КОГДА СостоянияРаспоряжений.Распоряжение ССЫЛКА Документ.ПервичныйДокумент
		|			ТОГДА &Регистратор_Менеджер
		|		ИНАЧЕ &Менеджер
		|	КОНЕЦ КАК Менеджер,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеЗаказано,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКОформлению,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОтгрузке) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОтгрузке) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКОтгрузке,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаявленоНаВозврат) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаявленоНаВозврат) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеЗаявленоНаВозврат,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюПоВозвратам) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюПоВозвратам) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКОформлениюПоВозвратам,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПриемке) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПриемке) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКПриемке,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПереходуПраваСобственности) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПереходуПраваСобственности) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКПереходуПраваСобственности,
		|	ВЫБОР
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюСчетовФактур) = 1
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
		|		КОГДА МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюСчетовФактур) = 2
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены)
		|	КОНЕЦ КАК СостояниеКОформлениюСчетовФактур
		|ИЗ
		|	ВТСостоянияРаспоряжений КАК СостоянияРаспоряжений
		|СГРУППИРОВАТЬ ПО
		|	СостоянияРаспоряжений.Распоряжение,
		|	СостоянияРаспоряжений.Склад,
		|	ВЫБОР
		|		КОГДА СостоянияРаспоряжений.Распоряжение ССЫЛКА Документ.ПервичныйДокумент
		|			ТОГДА &Регистратор_ХозяйственнаяОперация
		|		ИНАЧЕ &ХозяйственнаяОперация
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА СостоянияРаспоряжений.Распоряжение ССЫЛКА Документ.ПервичныйДокумент
		|			ТОГДА &Регистратор_Менеджер
		|		ИНАЧЕ &Менеджер
		|	КОНЕЦ
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаказано) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлению) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОтгрузке) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеЗаявленоНаВозврат) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюПоВозвратам) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПриемке) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКПереходуПраваСобственности) > 1
		|	ИЛИ МАКСИМУМ(СостоянияРаспоряжений.СостояниеКОформлениюСчетовФактур) > 1";
		
		ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
		ПараметрыФормированияТаблицы.ТекстУсловия =
			"Распоряжение В (&Распоряжения)
			|	И ТИПЗНАЧЕНИЯ(Распоряжение) <> ТИП(Документ.КорректировкаРегистров)";
		ПараметрыФормированияТаблицы.НачалоПериода = "&НачалоПериода";
		ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, КодСтроки, Склад, Серия";
		ПараметрыФормированияТаблицы.Показатели = "Заказано, ЗаявленоНаВозврат, КОформлению, Оформлено,
			|КОформлениюПоВозвратам, ОформленоПоВозвратам, КОтгрузке,
			|Отгружено, КПриемке, Принято, ПринятоПоВозвратам,
			|ОформленоСчетовФактур";
		ПараметрыФормированияТаблицы.Ресурсы = "Количество, СуммаНДСРегл";
		
		РаспоряженияНаОтгрузкуОборотыРазвернутая =
			РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
				ПараметрыФормированияТаблицы);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
			РаспоряженияНаОтгрузкуОборотыРазвернутая);
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Организация",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Организация",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Номер",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Номер",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Дата",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Дата",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&СуммаДокумента",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"СуммаДокумента",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Валюта",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Валюта",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Партнер",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Партнер",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Контрагент",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Контрагент",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&Менеджер",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"Менеджер",
				ТипыРаспоряжений));
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"&ХозяйственнаяОперация",
			ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(
				"СостоянияРаспоряжений.Распоряжение",
				"ХозяйственнаяОперация",
				ТипыРаспоряжений));
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Регистратор, "Менеджер") Тогда
			Регистратор_Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Менеджер");
		ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Регистратор, "Ответственный") Тогда
			Регистратор_Менеджер = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Ответственный");
		Иначе
			Регистратор_Менеджер = Неопределено;
		КонецЕсли;
		
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Регистратор, "ХозяйственнаяОперация") Тогда
			Регистратор_ХозяйственнаяОперация =
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "ХозяйственнаяОперация");
		Иначе
			Регистратор_ХозяйственнаяОперация = Неопределено;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Регистратор_Менеджер", Регистратор_Менеджер);
		Запрос.УстановитьПараметр("Регистратор_ХозяйственнаяОперация", Регистратор_ХозяйственнаяОперация);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению");
		ЭлементБлокировки.ИсточникДанных = Выгрузить();
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Распоряжение", "Распоряжение");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Попытка
			
			Блокировка.Заблокировать();
			
			ТаблицаОборотовПоРаспоряжениям = Запрос.Выполнить().Выгрузить();
			СтруктураПоиска = Новый Структура("Распоряжение");
			
			Для Каждого Распоряжение Из Распоряжения Цикл
				
				НаборРаспоряженияНаОтгрузкуКВыполнению = РегистрыСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению.СоздатьНаборЗаписей();
				НаборРаспоряженияНаОтгрузкуКВыполнению.Отбор.Распоряжение.Установить(Распоряжение);
				
				СтруктураПоиска.Распоряжение = Распоряжение;
				СтрокиПоРаспоряжению = ТаблицаОборотовПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
				
				Если СтрокиПоРаспоряжению.Количество() = 0 Тогда
					НаборРаспоряженияНаОтгрузкуКВыполнению.Записать();
				Иначе
					
					Для Каждого СтрокаПоРаспоряжению Из СтрокиПоРаспоряжению Цикл
					
						Запись = НаборРаспоряженияНаОтгрузкуКВыполнению.Добавить();
						ЗаполнитьЗначенияСвойств(Запись, СтрокаПоРаспоряжению);
						
					КонецЦикла;
					
					НаборРаспоряженияНаОтгрузкуКВыполнению.Записать();
					
				КонецЕсли;
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение(ОписаниеОшибки());
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Текст запроса значение реквизита из составного типа.
// 
// Параметры:
//  ИмяТаблицы - Строка - Имя таблицы
//  ИмяРеквизита - Строка - Имя реквизита
//  ТипыРеквизита - Массив Из Тип - Массив типов реквизита
// 
// Возвращаемое значение:
//  Строка - Текст запроса значение реквизита из составного типа
Функция ТекстЗапросаЗначениеРеквизитаИзСоставногоТипа(ИмяТаблицы, ИмяРеквизита, ТипыРеквизита) Экспорт
	
	Результат = "";
	
	ШаблонТекстаЗапроса =
	"	КОГДА ТИПЗНАЧЕНИЯ(&ПолеСсылка) = ТИП(&ТипСсылки)
	|		ТОГДА ВЫРАЗИТЬ(&ПолеСсылка КАК &ТипСсылки).%ИмяРеквизита
	|"; // @query-part
	
	СтандартныеРеквизиты = Новый Массив;
	СтандартныеРеквизиты.Добавить("Номер");
	СтандартныеРеквизиты.Добавить("Дата");
	
	Для Каждого Тип Из ТипыРеквизита Цикл
		
		МетаданныеРеквизита = Метаданные.НайтиПоТипу(Тип);
		
		Если ОбщегоНазначения.ЕстьРеквизитОбъекта(ИмяРеквизита, МетаданныеРеквизита)
			Или СтандартныеРеквизиты.Найти(ИмяРеквизита) <> Неопределено Тогда
			
			ПолноеИмя = СтрШаблон("Документ.%1", МетаданныеРеквизита.Имя);
			ЧастьЗапроса = СтрЗаменить(ШаблонТекстаЗапроса, "%ИмяРеквизита", ИмяРеквизита);
			ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "&ПолеСсылка", ИмяТаблицы);
			ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "&ТипСсылки", ПолноеИмя);
			Результат = Результат + ЧастьЗапроса;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Результат) Тогда
	
		Результат = СтрШаблон(
			"ВЫБОР
			|	%1
			|КОНЕЦ", // @query-part
			СокрЛП(Результат));

	Иначе
		
		Результат = "НЕОПРЕДЕЛЕНО"

	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция МинимальнаяДатаРаспоряжений(Распоряжения)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НАЧАЛОПЕРИОДА(РаспоряженияНаОтгрузку.Период, ДЕНЬ) КАК Дата
	|ИЗ
	|	РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК РаспоряженияНаОтгрузку
	|ГДЕ
	|	РаспоряженияНаОтгрузку.Распоряжение В (&Распоряжения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	РаспоряженияНаОтгрузку.Период";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Дата;
	Иначе
		Результат = Дата(1, 1, 1);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли