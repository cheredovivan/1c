#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Собирает структуру из текстов запросов для дальнейшей проверки даты запрета.
// 
// Параметры:
// 	Запрос - Запрос - Запрос по проверке даты запрета, можно установить параметры
// Возвращаемое значение:
// 	Структура - см. ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов
Функция ТекстЗапросаКонтрольДатыЗапрета(Запрос) Экспорт
	ИмяРегистра = Метаданные.РегистрыНакопления.ДенежныеСредстваВПути.Имя;
	ИмяТаблицыИзменений = "ТаблицаИзмененийДенежныеСредстваВПути"; 
	СтруктураТекстовЗапросов = ПроведениеДокументов.ШаблонТекстЗапросаКонтрольДатыЗапрета(Запрос, 
		ИмяРегистра, 
		ИмяТаблицыИзменений, 
		"ФинансовыйКонтур");
	Возврат СтруктураТекстовЗапросов

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(Отправитель, Неопределено КАК Истина)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет параметры отражения движений регистра в финансовом учете
//
// Параметры:
//  МетаданныеРегистра - ОбъектМетаданныхРегистрНакопления - Метаданные регистра накопления
//  РегистрацияКОтражению - Булево - Признак получения параметров для регистрации к отражению в учете
//
// Возвращаемое значение:
// 	см. ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете
//
Функция ПараметрыОтраженияДвиженийВФинансовомУчете(МетаданныеРегистра = Неопределено, РегистрацияКОтражению = Ложь) Экспорт
	
	ПараметрыОтражения = ФинансовыйУчетПоДаннымБалансовыхРегистров.ПараметрыОтраженияДвиженийВФинансовомУчете(РегистрацияКОтражению);
	
	Если РегистрацияКОтражению Тогда
		Возврат ПараметрыОтражения;
	КонецЕсли;
	
	ПараметрыОтражения.ПутьКДаннымОбъектНастройки = "ВидПереводаДенежныхСредств";
	ПараметрыОтражения.ПутьКДаннымВалюта = "Валюта";
	ПараметрыОтражения.РесурсыУпр.Добавить("СуммаУпр");
	ПараметрыОтражения.РесурсыРегл.Добавить("СуммаРегл");
	ПараметрыОтражения.РесурсыВал.Добавить("Сумма");
	
	//++ НЕ УТ
	
	ПараметрыОтражения.ТипДанныхУчета = Перечисления.ТипыДанныхУчета.ДенежныеСредства;
	ПараметрыОтражения.СтруктураАналитики = ОбщегоНазначения.СкопироватьРекурсивно(ИсточникиДанныхПовтИсп.СтруктураАналитикиПоТипуДанныхУчета(ПараметрыОтражения.ТипДанныхУчета));
	ПараметрыОтражения.СтруктураАналитики.ДенежныеСредства.ПутьКДанным = СтрЗаменить("
		|ВЫБОР
		|	КОГДА ПсевдонимИсточникаДанных.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ТОГДА ПсевдонимИсточникаДанных.Контрагент
		|	КОГДА ПсевдонимИсточникаДанных.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|		ТОГДА ПсевдонимИсточникаДанных.Получатель
		|	ИНАЧЕ ПсевдонимИсточникаДанных.Отправитель
		|КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных); 
	ПараметрыОтражения.СтруктураАналитики.ТипДенежныхСредств.ПутьКДанным = СтрЗаменить("
		|ВЫБОР
		|	КОГДА ПсевдонимИсточникаДанных.ВидПереводаДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПеречислениеФизическимЛицам)
		|		И ПсевдонимИсточникаДанных.НастройкаХозяйственнойОперации В (ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВыдачаДенежныхСредствПодотчетнику),
		|			ЗНАЧЕНИЕ(Справочник.НастройкиХозяйственныхОпераций.ВозвратДенежныхСредствОтПодотчетника))
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУПодотчетногоЛица)
		|	КОГДА ПсевдонимИсточникаДанных.ВидПереводаДенежныхСредств = ЗНАЧЕНИЕ(Перечисление.ВидыПереводовДенежныхСредств.ПоступлениеОтБанкаПоЭквайрингу)
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваУЭквайера)
		|	ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.ДенежныеСредстваВПути)
		|КОНЕЦ",
		"ПсевдонимИсточникаДанных",
		ПараметрыОтражения.ПсевдонимИсточникаДанных);
	ПараметрыОтражения.СтруктураАналитики.СтатьяКалькуляции.Вставить("Значение", Справочники.СтатьиКалькуляции.ПустаяСсылка());
	ПараметрыОтражения.СтруктураАналитики.ПодразделениеПлатежа.Вставить("Значение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	//++ Локализация
	ПараметрыОтражения.СтруктураАналитики.ТипПлатежаФЗ275.Вставить("Значение", Справочники.ТипыПлатежейФЗ275.ПустаяСсылка());
	ПараметрыОтражения.СтруктураАналитики.СтатьяЦелевыхСредств.Вставить("Значение", Неопределено);
	//-- Локализация
	
	//-- НЕ УТ
	
	Если МетаданныеРегистра = Неопределено Тогда
		МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	КонецЕсли;
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ЗаполнитьПараметрыОтраженияПоМетаданнымРегистра(ПараметрыОтражения, МетаданныеРегистра);
	
	Возврат ПараметрыОтражения;
	
КонецФункции

#КонецОбласти

#Область НеоперативнаяОчередьЗаданий

// Данные к формированию заданий неоперативного допроведения документов.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано новое задание.
// * ПериодЗадания  - Дата - Период задания. Совпадает с периодом записываемых данных в регистре.
// * Документ - ДокументСсылка - Ссылка на документ, по которому будет зарегистрировано задание.
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид регистрируемого задания.
//
Функция ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(НаборЗаписей, ВидыЗаданий,
	МенеджерВременныхТаблиц = Неопределено, ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	Если Не ДанныеКРегистрацииЗаданий = Неопределено Тогда
		Возврат ДанныеКРегистрацииЗаданий;
	КонецЕсли;
	
	ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
