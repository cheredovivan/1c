#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	ИзмененныеПериоды = Новый Массив;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		
		Если ДополнительныеСвойства.Свойство("ДополнитьДвижениямиИзИБЗаПериод") Тогда
			
			Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
				Возврат;
			КонецЕсли;
			
			ДополнительныеСвойства.Вставить("ТочноНужноСохранитьДвижения");
			
			// Сохраним неизмененные первичные движения.
			СохранятьДвижения = СохранитьНеИзмененныеПервичныеДвижения(ИзмененныеПериоды);
			
			// Сохраним расчетные движения за периоды, в которых есть первичные движения.
			РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение, ИзмененныеПериоды);
			
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	// Сохраним неизмененные первичные движения.
	СохранятьДвижения = СохранитьНеИзмененныеПервичныеДвижения(ИзмененныеПериоды);
	
	Если СохранятьДвижения
	 ИЛИ ДополнительныеСвойства.Свойство("ИзменилосьТолькоСостояниеПереходаПраваСобственности")
	 И ДополнительныеСвойства.ИзменилосьТолькоСостояниеПереходаПраваСобственности Тогда
		// Сохраним расчетные движения за периоды, в которых есть первичные движения.
		РасчетСебестоимостиПрикладныеАлгоритмы.СохранитьДвиженияСформированныеРасчетомПартийИСебестоимости(ЭтотОбъект, Замещение, ИзмененныеПериоды);
	КонецЕсли;
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ СебестоимостьТоваровПередЗаписью
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	Себестоимость.Регистратор = &Регистратор";
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПрикладныеАлгоритмы.ФормироватьДвиженияРегистровУчетаСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.ВидДвижения КАК ВидДвижения,
	|	Таблица.ОперацияУчетаСебестоимости КАК ОперацияУчетаСебестоимости,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.РазделУчета КАК РазделУчета,
	|	Таблица.ВидЗапасов КАК ВидЗапасов,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Партия КАК Партия,
	|	Таблица.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Таблица.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Таблица.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Таблица.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	Таблица.КорРазделУчета КАК КорРазделУчета,
	|	Таблица.КорВидЗапасов КАК КорВидЗапасов,
	|	Таблица.КорОрганизация КАК КорОрганизация,
	|	Таблица.КорСтоимость КАК КорСтоимость,
	|	Таблица.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Таблица.ЗаказКлиента КАК ЗаказКлиента,
	|	Таблица.Подразделение КАК Подразделение,
	|	Таблица.АналитикаРасходов КАК АналитикаРасходов,
	|	Таблица.СтатьяРасходовСписания КАК СтатьяРасходовСписания,
	|	Таблица.СтатьяДоходов КАК СтатьяДоходов,
	|	Таблица.АналитикаДоходов КАК АналитикаДоходов,
	|	Таблица.ПериодПродажи КАК ПериодПродажи,
	|	Таблица.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	Таблица.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	Таблица.ДокументДвижения КАК ДокументДвижения,
	|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Таблица.ГруппаПродукции КАК ГруппаПродукции,
	|	Таблица.КорПартия КАК КорПартия,
	|	Таблица.КорАналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
	|	Таблица.КорАналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	Таблица.КорВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	Таблица.ВидДеятельностиНДСДокумента КАК ВидДеятельностиНДСДокумента,
	|	Таблица.АналитикаФинансовогоУчетаДокумента КАК АналитикаФинансовогоУчетаДокумента,
	|	СУММА(Таблица.НДСРегл) КАК НДСРегл,
	|	СУММА(Таблица.НДСУпр) КАК НДСУпр,
	|	Таблица.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Таблица.ТипЗаписи КАК ТипЗаписи,
	|	Таблица.ДокументИсточник КАК ДокументИсточник,
	|	Таблица.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	Таблица.Сторно КАК Сторно,
	|	СУММА(Таблица.Количество) КАК КоличествоИзменение,
	|	СУММА(Таблица.Стоимость) КАК СтоимостьИзменение,
	|	СУММА(Таблица.СтоимостьБезНДС) КАК СтоимостьБезНДСИзменение,
	|	СУММА(Таблица.ДопРасходы) КАК ДопРасходыИзменение,
	|	СУММА(Таблица.ДопРасходыБезНДС) КАК ДопРасходыБезНДСИзменение,
	|	СУММА(Таблица.СтоимостьРегл) КАК СтоимостьРеглИзменение,
	|	СУММА(Таблица.ПостояннаяРазница) КАК ПостояннаяРазницаИзменение,
	|	СУММА(Таблица.ВременнаяРазница) КАК ВременнаяРазницаИзменение,
	|	СУММА(Таблица.СтоимостьНДД) КАК СтоимостьНДДИзменение,
	|	СУММА(Таблица.СтоимостьЗабалансовая) КАК СтоимостьЗабалансоваяИзменение,
	|	СУММА(Таблица.Трудозатраты) КАК ТрудозатратыИзменение,
	|	СУММА(Таблица.ПостатейныеПостоянныеСНДС) КАК ПостатейныеПостоянныеСНДСИзменение,
	|	СУММА(Таблица.ПостатейныеПеременныеСНДС) КАК ПостатейныеПеременныеСНДСИзменение,
	|	СУММА(Таблица.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДСИзменение,
	|	СУММА(Таблица.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДСИзменение,
	|	СУММА(Таблица.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРеглИзменение,
	|	СУММА(Таблица.ДопРасходыРегл) КАК ДопРасходыРеглИзменение,
	|	СУММА(Таблица.ТрудозатратыРегл) КАК ТрудозатратыРеглИзменение,
	|	СУММА(Таблица.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРеглИзменение,
	|	СУММА(Таблица.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРеглИзменение,
	|	СУММА(Таблица.СтоимостьУпр) КАК СтоимостьУпрИзменение,
	|	СУММА(Таблица.ДопРасходыУпр) КАК ДопРасходыУпрИзменение,
	|	СУММА(Таблица.ТрудозатратыУпр) КАК ТрудозатратыУпрИзменение,
	|	СУММА(Таблица.ПостатейныеПостоянныеУпр) КАК ПостатейныеПостоянныеУпрИзменение,
	|	СУММА(Таблица.ПостатейныеПеременныеУпр) КАК ПостатейныеПеременныеУпрИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийСебестоимостьТоваров
	|ИЗ
	|(ВЫБРАТЬ
	|	Себестоимость.Период КАК Период,
	|	Себестоимость.ВидДвижения КАК ВидДвижения,
	|	Себестоимость.ОперацияУчетаСебестоимости КАК ОперацияУчетаСебестоимости,
	|	Себестоимость.Регистратор КАК Регистратор,
	|	Себестоимость.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Себестоимость.РазделУчета КАК РазделУчета,
	|	Себестоимость.ВидЗапасов КАК ВидЗапасов,
	|	Себестоимость.Организация КАК Организация,
	|	Себестоимость.Партия КАК Партия,
	|	Себестоимость.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Себестоимость.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Себестоимость.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	-Себестоимость.Количество КАК Количество,
	|	-Себестоимость.Стоимость КАК Стоимость,
	|	-Себестоимость.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	-Себестоимость.ДопРасходы КАК ДопРасходы,
	|	-Себестоимость.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|	-Себестоимость.СтоимостьРегл КАК СтоимостьРегл,
	|	-Себестоимость.ПостояннаяРазница КАК ПостояннаяРазница,
	|	-Себестоимость.ВременнаяРазница КАК ВременнаяРазница,
	|	-Себестоимость.СтоимостьНДД КАК СтоимостьНДД,
	|	-Себестоимость.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	-Себестоимость.Трудозатраты КАК Трудозатраты,
	|	-Себестоимость.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
	|	-Себестоимость.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
	|	-Себестоимость.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
	|	-Себестоимость.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
	|	-Себестоимость.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|	-Себестоимость.ДопРасходыРегл КАК ДопРасходыРегл,
	|	-Себестоимость.ТрудозатратыРегл КАК ТрудозатратыРегл,
	|	-Себестоимость.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
	|	-Себестоимость.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
	|	-Себестоимость.СтоимостьУпр КАК СтоимостьУпр,
	|	-Себестоимость.ДопРасходыУпр КАК ДопРасходыУпр,
	|	-Себестоимость.ТрудозатратыУпр КАК ТрудозатратыУпр,
	|	-Себестоимость.ПостатейныеПостоянныеУпр КАК ПостатейныеПостоянныеУпр,
	|	-Себестоимость.ПостатейныеПеременныеУпр КАК ПостатейныеПеременныеУпр,
	|	Себестоимость.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	Себестоимость.КорРазделУчета КАК КорРазделУчета,
	|	Себестоимость.КорВидЗапасов КАК КорВидЗапасов,
	|	Себестоимость.КорОрганизация КАК КорОрганизация,
	|	Себестоимость.КорСтоимость КАК КорСтоимость,
	|	Себестоимость.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Себестоимость.ЗаказКлиента КАК ЗаказКлиента,
	|	Себестоимость.Подразделение КАК Подразделение,
	|	Себестоимость.АналитикаРасходов КАК АналитикаРасходов,
	|	Себестоимость.СтатьяРасходовСписания КАК СтатьяРасходовСписания,
	|	Себестоимость.СтатьяДоходов КАК СтатьяДоходов,
	|	Себестоимость.АналитикаДоходов КАК АналитикаДоходов,
	|	Себестоимость.ПериодПродажи КАК ПериодПродажи,
	|	Себестоимость.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	Себестоимость.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	Себестоимость.ДокументДвижения КАК ДокументДвижения,
	|	Себестоимость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Себестоимость.ГруппаПродукции КАК ГруппаПродукции,
	|	Себестоимость.КорПартия КАК КорПартия,
	|	Себестоимость.КорАналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
	|	Себестоимость.КорАналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	Себестоимость.КорВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	Себестоимость.ВидДеятельностиНДСДокумента КАК ВидДеятельностиНДСДокумента,
	|	Себестоимость.АналитикаФинансовогоУчетаДокумента КАК АналитикаФинансовогоУчетаДокумента,
	|	-Себестоимость.НДСРегл КАК НДСРегл,
	|	-Себестоимость.НДСУпр КАК НДСУпр,
	|	Себестоимость.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Себестоимость.ТипЗаписи КАК ТипЗаписи,
	|	Себестоимость.ДокументИсточник КАК ДокументИсточник,
	|	Себестоимость.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	Себестоимость.Сторно КАК Сторно
	|ИЗ
	|	СебестоимостьТоваровПередЗаписью КАК Себестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Себестоимость.Период КАК Период,
	|	Себестоимость.ВидДвижения КАК ВидДвижения,
	|	Себестоимость.ОперацияУчетаСебестоимости КАК ОперацияУчетаСебестоимости,
	|	Себестоимость.Регистратор КАК Регистратор,
	|	Себестоимость.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Себестоимость.РазделУчета КАК РазделУчета,
	|	Себестоимость.ВидЗапасов КАК ВидЗапасов,
	|	Себестоимость.Организация КАК Организация,
	|	Себестоимость.Партия КАК Партия,
	|	Себестоимость.АналитикаУчетаПартий КАК АналитикаУчетаПартий,
	|	Себестоимость.АналитикаФинансовогоУчета КАК АналитикаФинансовогоУчета,
	|	Себестоимость.ВидДеятельностиНДС КАК ВидДеятельностиНДС,
	|	Себестоимость.Количество КАК Количество,
	|	Себестоимость.Стоимость КАК Стоимость,
	|	Себестоимость.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	Себестоимость.ДопРасходы КАК ДопРасходы,
	|	Себестоимость.ДопРасходыБезНДС КАК ДопРасходыБезНДС,
	|	Себестоимость.СтоимостьРегл КАК СтоимостьРегл,
	|	Себестоимость.ПостояннаяРазница КАК ПостояннаяРазница,
	|	Себестоимость.ВременнаяРазница КАК ВременнаяРазница,
	|	Себестоимость.СтоимостьНДД КАК СтоимостьНДД,
	|	Себестоимость.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	Себестоимость.Трудозатраты КАК Трудозатраты,
	|	Себестоимость.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
	|	Себестоимость.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
	|	Себестоимость.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
	|	Себестоимость.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
	|	Себестоимость.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
	|	Себестоимость.ДопРасходыРегл КАК ДопРасходыРегл,
	|	Себестоимость.ТрудозатратыРегл КАК ТрудозатратыРегл,
	|	Себестоимость.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
	|	Себестоимость.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
	|	Себестоимость.СтоимостьУпр КАК СтоимостьУпр,
	|	Себестоимость.ДопРасходыУпр КАК ДопРасходыУпр,
	|	Себестоимость.ТрудозатратыУпр КАК ТрудозатратыУпр,
	|	Себестоимость.ПостатейныеПостоянныеУпр КАК ПостатейныеПостоянныеУпр,
	|	Себестоимость.ПостатейныеПеременныеУпр КАК ПостатейныеПеременныеУпр,
	|	Себестоимость.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Себестоимость.КорАналитикаУчетаНоменклатуры КАК КорАналитикаУчетаНоменклатуры,
	|	Себестоимость.КорРазделУчета КАК КорРазделУчета,
	|	Себестоимость.КорВидЗапасов КАК КорВидЗапасов,
	|	Себестоимость.КорОрганизация КАК КорОрганизация,
	|	Себестоимость.КорСтоимость КАК КорСтоимость,
	|	Себестоимость.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Себестоимость.ЗаказКлиента КАК ЗаказКлиента,
	|	Себестоимость.Подразделение КАК Подразделение,
	|	Себестоимость.АналитикаРасходов КАК АналитикаРасходов,
	|	Себестоимость.СтатьяРасходовСписания КАК СтатьяРасходовСписания,
	|	Себестоимость.СтатьяДоходов КАК СтатьяДоходов,
	|	Себестоимость.АналитикаДоходов КАК АналитикаДоходов,
	|	Себестоимость.ПериодПродажи КАК ПериодПродажи,
	|	Себестоимость.СтатьяАктивовПассивов КАК СтатьяАктивовПассивов,
	|	Себестоимость.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	Себестоимость.ДокументДвижения КАК ДокументДвижения,
	|	Себестоимость.ИдентификаторСтроки КАК ИдентификаторСтроки,
	|	Себестоимость.ГруппаПродукции КАК ГруппаПродукции,
	|	Себестоимость.КорПартия КАК КорПартия,
	|	Себестоимость.КорАналитикаУчетаПартий КАК КорАналитикаУчетаПартий,
	|	Себестоимость.КорАналитикаФинансовогоУчета КАК КорАналитикаФинансовогоУчета,
	|	Себестоимость.КорВидДеятельностиНДС КАК КорВидДеятельностиНДС,
	|	Себестоимость.ВидДеятельностиНДСДокумента КАК ВидДеятельностиНДСДокумента,
	|	Себестоимость.АналитикаФинансовогоУчетаДокумента КАК АналитикаФинансовогоУчетаДокумента,
	|	Себестоимость.НДСРегл КАК НДСРегл,
	|	Себестоимость.НДСУпр КАК НДСУпр,
	|	Себестоимость.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	Себестоимость.ТипЗаписи КАК ТипЗаписи,
	|	Себестоимость.ДокументИсточник КАК ДокументИсточник,
	|	Себестоимость.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
	|	Себестоимость.Сторно КАК Сторно
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Себестоимость
	|ГДЕ
	|	Себестоимость.Регистратор = &Регистратор
	|) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.ВидДвижения,
	|	Таблица.ОперацияУчетаСебестоимости,
	|	Таблица.Регистратор,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.РазделУчета,
	|	Таблица.ВидЗапасов,
	|	Таблица.Организация,
	|	Таблица.Партия,
	|	Таблица.АналитикаУчетаПартий,
	|	Таблица.АналитикаФинансовогоУчета,
	|	Таблица.ВидДеятельностиНДС,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.КорАналитикаУчетаНоменклатуры,
	|	Таблица.КорРазделУчета,
	|	Таблица.КорВидЗапасов,
	|	Таблица.КорОрганизация,
	|	Таблица.КорСтоимость,
	|	Таблица.АналитикаУчетаПоПартнерам,
	|	Таблица.ЗаказКлиента,
	|	Таблица.Подразделение,
	|	Таблица.АналитикаРасходов,
	|	Таблица.СтатьяРасходовСписания,
	|	Таблица.СтатьяДоходов,
	|	Таблица.АналитикаДоходов,
	|	Таблица.ПериодПродажи,
	|	Таблица.СтатьяАктивовПассивов,
	|	Таблица.АналитикаАктивовПассивов,
	|	Таблица.ДокументДвижения,
	|	Таблица.ИдентификаторСтроки,
	|	Таблица.ГруппаПродукции,
	|	Таблица.КорПартия,
	|	Таблица.КорАналитикаУчетаПартий,
	|	Таблица.КорАналитикаФинансовогоУчета,
	|	Таблица.КорВидДеятельностиНДС,
	|	Таблица.ВидДеятельностиНДСДокумента,
	|	Таблица.АналитикаФинансовогоУчетаДокумента,
	|	Таблица.СтатьяКалькуляции,
	|	Таблица.ТипЗаписи,
	|	Таблица.ДокументИсточник,
	|	Таблица.КорНаправлениеДеятельности,
	|	Таблица.Сторно
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.Стоимость) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходы) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостояннаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.ВременнаяРазница) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьНДД) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьЗабалансовая) <> 0
	|	ИЛИ СУММА(Таблица.Трудозатраты) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеСНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеСНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеБезНДС) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьЗабалансоваяРегл) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыРегл) <> 0
	|	ИЛИ СУММА(Таблица.ТрудозатратыРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеРегл) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеРегл) <> 0
	|	ИЛИ СУММА(Таблица.СтоимостьУпр) <> 0
	|	ИЛИ СУММА(Таблица.ДопРасходыУпр) <> 0
	|	ИЛИ СУММА(Таблица.ТрудозатратыУпр) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеУпр) <> 0
	|	ИЛИ СУММА(Таблица.ПостатейныеПеременныеУпр) <> 0
	|	ИЛИ СУММА(Таблица.НДСРегл) <> 0
	|	ИЛИ СУММА(Таблица.НДСУпр) <> 0
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ СебестоимостьТоваровПередЗаписью
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация,
	|	Т.Период
	|ПОМЕСТИТЬ ВТОрганизацииИПериодыВСебестоимостиТоваров
	|ИЗ
	|	ТаблицаИзмененийСебестоимостьТоваров КАК Т
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.Период,
	|	МАКСИМУМ(УчетнаяПолитика.Период) КАК ПериодПолитики,
	|	МИНИМУМ(ЕСТЬNULL(УчетнаяПолитикаСледующая.Период, ДАТАВРЕМЯ(1,1,1))) КАК КонецПериода
	|ПОМЕСТИТЬ ВТПериодыУчетныхПолитикСебестоимостиТоваров
	|ИЗ
	|	ВТОрганизацииИПериодыВСебестоимостиТоваров КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
	|		ПО Т.Организация = УчетнаяПолитика.Организация
	|		И Т.Период >= УчетнаяПолитика.Период
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитикаСледующая
	|		ПО Т.Организация = УчетнаяПолитикаСледующая.Организация
	|		И Т.Период < УчетнаяПолитикаСледующая.Период
	|		И УчетнаяПолитикаСледующая.МетодОценкиСтоимостиТоваров <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)
	|СГРУППИРОВАТЬ ПО
	|	Т.Организация,
	|	Т.Период
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация КАК Организация,
	|	Т.Период КАК Период,
	|	Т.КонецПериода КАК КонецПериода
	|ПОМЕСТИТЬ ВТСреднескользящаяСебестоимостьТоваров
	|ИЗ
	|	ВТПериодыУчетныхПолитикСебестоимостиТоваров КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
	|		ПО Т.Организация = УчетнаяПолитика.Организация
	|		И Т.ПериодПолитики = УчетнаяПолитика.Период
	|ГДЕ
	|	ЕСТЬNULL(УчетнаяПолитика.МетодОценкиСтоимостиТоваров, НЕОПРЕДЕЛЕНО) = ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Период
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТОрганизацииИПериодыВСебестоимостиТоваров
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПериодыУчетныхПолитикСебестоимостиТоваров
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Т.Организация,
	|	МИНИМУМ(Т.Период) КАК Период
	|ИЗ
	|	ВТСреднескользящаяСебестоимостьТоваров КАК Т
	|СГРУППИРОВАТЬ ПО
	|	Организация
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|";
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	ОрганизацииСреднескользящая = Запрос.Выполнить().Выгрузить();
	
	ПроведениеДокументов.ЗарегистрироватьТаблицуКонтроля(ДополнительныеСвойства, "ВТСреднескользящаяСебестоимостьТоваров",
		ОрганизацииСреднескользящая.Количество() > 0, Новый Структура("ОрганизацииСреднескользящая", ОрганизацииСреднескользящая));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СохранитьНеИзмененныеПервичныеДвижения(ИзмененныеПериоды)
	
	Если РасчетСебестоимостиПовтИсп.ЗначенияТехнологическихПараметров().НеСохранятьРасчетныеДвижения
	 ИЛИ ДополнительныеСвойства.Свойство("НеСохранятьРасчетныеДвижения") Тогда
		Возврат Истина;
	КонецЕсли;
	
	Регистратор     = Отбор.Регистратор.Значение;
	МетаРегистратор = Регистратор.Метаданные(); // ОбъектМетаданныхРегистрНакопления
	
	Если ДополнительныеСвойства.Свойство("ДатаРегистратора") Тогда
		ДатаРегистратора = ДополнительныеСвойства.ДатаРегистратора;
	Иначе
		ДатаРегистратора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Регистратор, "Дата");
	КонецЕсли;
	
	Если НЕ РасчетСебестоимостиПовтИсп.ПартионныйУчетВерсии22(НачалоМесяца(ДатаРегистратора)) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("ТочноНужноСохранитьДвижения") Тогда
		Если Не ДополнительныеСвойства.Свойство("СвойстваДокумента")
 		 Или ДополнительныеСвойства.СвойстваДокумента.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Регистратор", Регистратор);
	Запрос.УстановитьПараметр("ДатаРегистратора", ДатаРегистратора);
	Запрос.УстановитьПараметр("ИгнорироватьИзменения",
		ДополнительныеСвойства.Свойство("ИзменилосьТолькоСостояниеПереходаПраваСобственности")
		И ДополнительныеСвойства.ИзменилосьТолькоСостояниеПереходаПраваСобственности);
		
	ТаблицаЗаписей = ЭтотОбъект.Выгрузить();
	Запрос.УстановитьПараметр("НаборЗаписей", ТаблицаЗаписей);
	
	#Область ШаблоныТекстаЗапроса
	
	// Подготовим шаблоны для формирования текстов запросов.
	ПоляИсключения    = Новый Структура("НомерСтроки, Активность, Регистратор, МоментВремени, ДатаОпределенияСебестоимости");
	ПоляПартий     	  = Новый Структура("Партия, АналитикаУчетаПартий, АналитикаФинансовогоУчета, ВидДеятельностиНДС,
		|КорПартия, КорАналитикаУчетаПартий, КорАналитикаФинансовогоУчета,
		|КорАналитикаУчетаНоменклатуры, КорВидДеятельностиНДС, ДокументИсточник, РасчетНеЗавершен,
		|ВидДеятельностиНДСДокумента, АналитикаФинансовогоУчетаДокумента,
		|ВыполненРасчетПартий, РежимЗакрытияМесяца, СпособОпределенияСебестоимости");
	НесуммируемыеПоля = Новый Структура("КодСтроки");
	СуммовыеПоляСреднескользящей = Новый Структура(РасчетСебестоимостиПроведениеДокументов.СуммовыеПоляСреднескользящей());
	
	Если МетаРегистратор <> Метаданные.Документы.ПеремещениеТоваров
	//++ НЕ УТ

	//++ Устарело_Производство21
	 И МетаРегистратор <> Метаданные.Документы.ПередачаМатериаловВПроизводство
	 //-- Устарело_Производство21
	 И МетаРегистратор <> Метаданные.Документы.ДвижениеПродукцииИМатериалов
	//-- НЕ УТ
	 И МетаРегистратор <> Метаданные.Документы.КорректировкаНазначенияТоваров
	 И МетаРегистратор <> Метаданные.Документы.ВнутреннееПотребление
	 И МетаРегистратор <> Метаданные.Документы.ПересортицаТоваров
	 И МетаРегистратор <> Метаданные.Документы.ПорчаТоваров Тогда
		// Для перечисленных типов документов кор. вид деятельности НДС определяется в РасчетСебестоимостиЗаполнениеПартий.ТекстТекущиеОборотыПартийТоваров()
		// Для остальных документов этот реквизит определяется при проведении документа.
		ПоляПартий.Удалить("КорВидДеятельностиНДС");
 	КонецЕсли;
 	
	ТекстПоляРегистра = "";
	ТекстПоляРегистраСМинусом = "";
	ТекстПоляРегистраБезПартий = "";
	ТекстПоляГруппировкиБезПартий = "";
	ТекстПоляСуммирования = "";
	ТекстПоляУсловия = "";
	ТекстПоляУсловияСреднескользящая = "";
	
	Для Каждого ТекущаяКолонка Из ТаблицаЗаписей.Колонки Цикл
		
		Если ПоляИсключения.Свойство(ТекущаяКолонка.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоСуммируемоеПоле = Метаданные().Ресурсы.Найти(ТекущаяКолонка.Имя) <> Неопределено
			ИЛИ (Метаданные().Реквизиты.Найти(ТекущаяКолонка.Имя) <> Неопределено
					И ТекущаяКолонка.ТипЗначения.СодержитТип(Тип("Число"))
					И НЕ НесуммируемыеПоля.Свойство(ТекущаяКолонка.Имя));
		 
		ТекстПоляРегистра = ТекстПоляРегистра + ?(ТекстПоляРегистра = "", "", ",
			|	") + "Т." + ТекущаяКолонка.Имя;
		ТекстПоляРегистраСМинусом = ТекстПоляРегистраСМинусом + ?(ТекстПоляРегистраСМинусом = "", "", ",
			|	") + ?(ЭтоСуммируемоеПоле, "-", "") + "Т." + ТекущаяКолонка.Имя;
			
		Если ЭтоСуммируемоеПоле Тогда
			
			ТекстПоляСуммирования = ТекстПоляСуммирования + ?(ТекстПоляСуммирования = "", "", ",
				|	") + "СУММА(Т." + ТекущаяКолонка.Имя + ") КАК " + ТекущаяКолонка.Имя;
			
			Если СуммовыеПоляСреднескользящей.Свойство(ТекущаяКолонка.Имя) Тогда
				ТекстПоляУсловияСреднескользящая = ТекстПоляУсловияСреднескользящая + ?(ТекстПоляУсловияСреднескользящая = "", "", "
					|			ИЛИ ") + "СУММА(Т." + ТекущаяКолонка.Имя + ") <> 0";
			Иначе
				ТекстПоляУсловия = ТекстПоляУсловия + ?(ТекстПоляУсловия = "", "", "
					|	ИЛИ ") + "СУММА(Т." + ТекущаяКолонка.Имя + ") <> 0";
			КонецЕсли;
			
		Иначе
			
			Если НЕ ПоляПартий.Свойство(ТекущаяКолонка.Имя) Тогда
				
				Если НРег(ТекущаяКолонка.Имя) = НРег("КорВидДеятельностиНДС") Тогда
					
					// Для приходных движений КорВидДеятельностиНДС не заполняется при проведении документа, он заполняется при расчете партий для целей учета НДС.
					// Для расходных движений КорВидДеятельностиНДС всегда заполняется самим документом при проведении.
					// Для реализации многооборотной тары и для реализации по регл. учету КорВидДеятельностиНДС не заполняется.
					ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
						|	") + "ВЫБОР КОГДА (Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
						|	  И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
						|" + ?(МетаРегистратор = Метаданные.Документы.РеализацияТоваровУслуг, "
						|	  ИЛИ Т.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.ВыбытиеПоФиксированнойСтоимости)
						|	  ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
						|	  ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)", "") + "
						|		ТОГДА НЕОПРЕДЕЛЕНО
						| ИНАЧЕ Т." + ТекущаяКолонка.Имя + " КОНЕЦ КАК " + ТекущаяКолонка.Имя;
					ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
						|	") + "ВЫБОР КОГДА (Т.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейПартий.Перемещение)
						|	  И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход))
						|" + ?(МетаРегистратор = Метаданные.Документы.РеализацияТоваровУслуг, "
						|	  ИЛИ Т.ОперацияУчетаСебестоимости = ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.ВыбытиеПоФиксированнойСтоимости)
						|	  ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности)
						|	  ИЛИ Т.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.РеализацияКлиентуРеглУчет)", "") + "
						|		ТОГДА НЕОПРЕДЕЛЕНО
						| ИНАЧЕ Т." + ТекущаяКолонка.Имя + " КОНЕЦ";
					
				Иначе
						
					ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
						|	") + "Т." + ТекущаяКолонка.Имя;
					ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
						|	") + "Т." + ТекущаяКолонка.Имя;
					
				КонецЕсли;
				
			Иначе
				
				Если НРег(ТекущаяКолонка.Имя) = НРег("ДокументИсточник") Тогда
					ДопУсловиеДляПоля =
						 "ИЛИ (Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) И Правила.ДокументИсточникВПриходе)
					|	  ИЛИ (Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) И Правила.ДокументИсточникВРасходе)
					|	  ";
				ИначеЕсли НРег(ТекущаяКолонка.Имя) = НРег("КорВидДеятельностиНДС")
				 ИЛИ НРег(ТекущаяКолонка.Имя) = НРег("КорАналитикаУчетаНоменклатуры") Тогда
					ДопУсловиеДляПоля =
						 "И Т.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
					|	  ";
				Иначе
					ДопУсловиеДляПоля = "";
				КонецЕсли;
				
				ТекстПоляРегистраБезПартий = ТекстПоляРегистраБезПартий + ?(ТекстПоляРегистраБезПартий = "", "", ",
					|	") + "ВЫБОР КОГДА Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
					|	  " + ДопУсловиеДляПоля + "
					//++ НЕ УТ
					|	  ИЛИ Т.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
					//-- НЕ УТ
					|		ТОГДА Т." + ТекущаяКолонка.Имя
					+ " ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ КАК " + ТекущаяКолонка.Имя;
				ТекстПоляГруппировкиБезПартий = ТекстПоляГруппировкиБезПартий + ?(ТекстПоляГруппировкиБезПартий = "", "", ",
					|	") + "ВЫБОР КОГДА Т.ТипЗаписи В (&ТипыЗаписейПервичныхПартий)
					|	  " + ДопУсловиеДляПоля + "
					//++ НЕ УТ
					|	  ИЛИ Т.СтатьяКалькуляции <> ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка)
					//-- НЕ УТ
					|		ТОГДА Т." + ТекущаяКолонка.Имя
					+ " ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ";
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	#КонецОбласти

	#Область ЗагрузкаДвиженийВоВременнуюТаблицу
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) КАК Месяц,
	|	&Регистратор КАК Регистратор,
	|	&ТекстВыборПоляРегистра,
	|	Т.ДатаОпределенияСебестоимости
	|ПОМЕСТИТЬ ВТНаборЗаписей
	|ИЗ
	|	&НаборЗаписей КАК Т
	|ГДЕ
	|	НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|";
	
	Если ДополнительныеСвойства.Свойство("ДополнитьДвижениямиИзИБЗаПериод") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВТНаборЗаписей", "ВТНаборЗаписейПредварительный");
	КонецЕсли;

	#КонецОбласти
	
	#Область ДополнитьДвижениямиИзИБЗаПериод
	
	// Если при проведении документа известно, что данные какого-то периода точно не изменились,
	// то надо просто добавить их в набор записей из ИБ.
	Если ДополнительныеСвойства.Свойство("ДополнитьДвижениямиИзИБЗаПериод") Тогда
		
		Запрос.УстановитьПараметр("НачалоПериода", ДополнительныеСвойства.ДополнитьДвижениямиИзИБЗаПериод.НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода",  ДополнительныеСвойства.ДополнитьДвижениямиИзИБЗаПериод.КонецПериода);
		
		Запрос.Текст = Запрос.Текст + "
		|;
		|" + "
		|ВЫБРАТЬ
		|	Т.Месяц,
		|	&Регистратор КАК Регистратор,
		|	&ТекстВыборПоляРегистра,
		|	Т.ДатаОпределенияСебестоимости
		|ПОМЕСТИТЬ ВТНаборЗаписей
		|ИЗ
		|	ВТНаборЗаписейПредварительный КАК Т
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) КАК Месяц,
		|	&Регистратор КАК Регистратор,
		|	&ТекстВыборПоляРегистра,
		|	Т.ДатаОпределенияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.Регистратор = &Регистратор
		|	И Т.Период МЕЖДУ &НачалоПериода И &КонецПериода
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|";
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область ПроверкаУчетнойПолитики
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстВыборПоляРегистра", ТекстПоляРегистра);
	
	Запрос.Текст = Запрос.Текст + "
	|;
	|" + "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Организация КАК Организация,
	|	Т.Месяц КАК Месяц
	|ПОМЕСТИТЬ ВТОрганизацииПериоды
	|ИЗ
	|	ВТНаборЗаписей КАК Т
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация";
	
	Если РасчетСебестоимостиПовтИсп.ИспользуетсяСреднескользящаяОценка() Тогда
		
		// При методе оценки стоимости Среднескользящая расчет себестоимости не изменяет первичные движения документа
		// и не добавляет рассчетные движения, поэтому нет необходимости их сохранять.
		Запрос.Текст = Запрос.Текст + "
		|;
		|" + "
		|ВЫБРАТЬ
		|	Т.Организация КАК Организация,
		|	Т.Месяц КАК Месяц,
		|	МАКСИМУМ(ЕСТЬNULL(УчетнаяПолитика.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодПолитики
		|ПОМЕСТИТЬ ВТПериодыПолитик
		|ИЗ
		|	ВТОрганизацииПериоды КАК Т
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
		|		ПО Т.Организация = УчетнаяПолитика.Организация
		|			И Т.Месяц >= УчетнаяПолитика.Период
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Организация,
		|	Т.Месяц
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	ПериодПолитики
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Т.СохранятьДвижения КАК СохранятьДвижения
		|ИЗ
		|	(ВЫБРАТЬ
		|		Т.Организация КАК Организация,
		|		Т.Месяц КАК Месяц,
		|		ЕСТЬNULL(УчетнаяПолитика.МетодОценкиСтоимостиТоваров, НЕОПРЕДЕЛЕНО) <> ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая) КАК СохранятьДвижения
		|	ИЗ
		|		ВТПериодыПолитик КАК Т
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитика
		|			ПО Т.Организация = УчетнаяПолитика.Организация
		|				И Т.ПериодПолитики = УчетнаяПолитика.Период) КАК Т
		|ГДЕ
		|	Т.СохранятьДвижения";
		
		СохранятьДвижения = НЕ Запрос.Выполнить().Пустой();
		
		Если НЕ СохранятьДвижения Тогда
			
			Если РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(Запрос, "ВТНаборЗаписей") <> ТаблицаЗаписей.Количество() Тогда

				Запрос.Текст =
				"ВЫБРАТЬ
				|	*
				|ИЗ
				|	ВТНаборЗаписей КАК Т
				|";
				
				ЭтотОбъект.Загрузить(Запрос.Выполнить().Выгрузить());
				
			КонецЕсли;
			
			Возврат Ложь;
			
		КонецЕсли;
		
	Иначе
		Запрос.Выполнить();
	КонецЕсли;
	
	#КонецОбласти
	
	#Область СозданиеВременныхТаблиц
	
	Запрос.УстановитьПараметр("ТипыЗаписейПервичныхПартий", РасчетСебестоимостиПрикладныеАлгоритмы.ТипыЗаписейПервичныхПартий());
	
	// Создадим ВТПравилаЗаполненияПоляТипЗаписи
	РасчетСебестоимостиПрикладныеАлгоритмы.СоздатьТаблицуПравилЗаполненияПоляТипЗаписи(
		Запрос.МенеджерВременныхТаблиц,
		РасчетСебестоимостиПовтИсп.ПравилаЗаполненияПоляТипЗаписи(МетаРегистратор.Имя));
	
	// Проанализируем наличие измененных первичных движений в разрезе периодов (месяцев).
	ШаблонТекстаЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Т.Период,
	|	Т.ОперацияУчетаСебестоимости
	|ПОМЕСТИТЬ ВТИзмененныеДвижения
	|ИЗ
	|(ВЫБРАТЬ
	//++ НЕ УТ
	|	АналитикиНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	//-- НЕ УТ
	|	&ТекстВыборПоляРегистра
	|ИЗ
	|	РегистрНакопления.СебестоимостьТоваров КАК Т
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиНоменклатуры
	|		ПО Т.АналитикаУчетаНоменклатуры = АналитикиНоменклатуры.Ссылка
	//-- НЕ УТ
	|ГДЕ
	|	Т.Регистратор = &Регистратор
	|	И НЕ Т.РасчетПартий
	|	И НЕ Т.РасчетСебестоимости
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	//++ НЕ УТ
	|	АналитикиНоменклатуры.СтатьяКалькуляции КАК СтатьяКалькуляции,
	//-- НЕ УТ
	|	&ТекстПоляРегистраСМинусом
	|ИЗ
	|	ВТНаборЗаписей КАК Т
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиНоменклатуры
	|		ПО Т.АналитикаУчетаНоменклатуры = АналитикиНоменклатуры.Ссылка
	//-- НЕ УТ
	|) КАК Т
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТПравилаЗаполненияПоляТипЗаписи КАК Правила
	|		ПО Правила.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|			ИЛИ Т.ХозяйственнаяОперация = Правила.ХозяйственнаяОперация
	|
	|СГРУППИРОВАТЬ ПО
	|	&ТекстПоляГруппировкиБезПартий
	|ИМЕЮЩИЕ
	|	&ТекстПоляУсловияОбщие
	// При учете по Среднескользящей не проверяем ресурсы, заполняемые при этом методе оценки
	|	ИЛИ (НЕ МАКСИМУМ(Т.СпособОпределенияСебестоимости В
	|			(ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимостиТоваров.ПоСреднескользящей),
	|			 ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимостиТоваров.ПоКорДвижению),
	|			 ЗНАЧЕНИЕ(Перечисление.СпособыОпределенияСебестоимостиТоваров.ПоДвижениямДокументаИсточника)))
	|		И (&ТекстПоляУсловияСреднескользящая)
	|		)
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Т.Период КАК Период,
	|	МАКСИМУМ(Т.ЕстьИзмененныеДвижения) КАК ЕстьИзмененныеДвижения
	|ПОМЕСТИТЬ ВТПериодыДвижений
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Т.Месяц КАК Период,
	|		ЛОЖЬ КАК ЕстьИзмененныеДвижения
	|	ИЗ
	|		ВТОрганизацииПериоды КАК Т
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) КАК Период,
	|		ВЫБОР КОГДА &ИгнорироватьИзменения И &ДатаРегистратора = Т.Период
	|         И Т.ОперацияУчетаСебестоимости <> ЗНАЧЕНИЕ(Перечисление.ОперацииУчетаСебестоимости.Реализация)
	|           ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА КОНЕЦ КАК ЕстьИзмененныеДвижения
	|	ИЗ
	|		ВТИзмененныеДвижения КАК Т
	|	) КАК Т
	|
	|СГРУППИРОВАТЬ ПО
	|	Т.Период
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Т.ЕстьИзмененныеДвижения
	|ИЗ
	|	ВТПериодыДвижений КАК Т
	|ГДЕ
	|	Т.ЕстьИзмененныеДвижения
	|";
	
	Запрос.Текст = СтрЗаменить(ШаблонТекстаЗапроса, "&ТекстВыборПоляРегистра", ТекстПоляРегистра);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляРегистраСМинусом", ТекстПоляРегистраСМинусом);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляРегистраБезПартий", ТекстПоляРегистраБезПартий);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляСуммирования", ТекстПоляСуммирования);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляГруппировкиБезПартий", ТекстПоляГруппировкиБезПартий);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляУсловияОбщие", ТекстПоляУсловия);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТекстПоляУсловияСреднескользящая", ТекстПоляУсловияСреднескользящая);
	
	ЕстьИзмененные = НЕ Запрос.Выполнить().Пустой(); // есть измененные движения
	
	#КонецОбласти
	
	#Область ЗаполнениеНабораЗаписей
	
	Если ЕстьИзмененные
	 ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(Запрос, "ВТНаборЗаписей") <> ТаблицаЗаписей.Количество() Тогда
		
		// Движения не измененных периодов возьмем из ИБ, а движения измененных периодов - из набора записей.
		ШаблонТекстаЗапроса =
		"ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	&ТекстПоляРегистра,
		|	Т.ДатаОпределенияСебестоимости
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК Т
		|ГДЕ
		|	Т.Регистратор = &Регистратор
		|	И НЕ Т.РасчетПартий
		|	И НЕ Т.РасчетСебестоимости
		|	И НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) В
		|		(ВЫБРАТЬ Т.Период
		|		 ИЗ ВТПериодыДвижений КАК Т
		|		 ГДЕ НЕ Т.ЕстьИзмененныеДвижения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	&Регистратор КАК Регистратор,
		|	&ТекстПоляРегистра,
		|	Т.ДатаОпределенияСебестоимости
		|ИЗ
		|	ВТНаборЗаписей КАК Т
		|ГДЕ
		|	НАЧАЛОПЕРИОДА(Т.Период, МЕСЯЦ) В
		|		(ВЫБРАТЬ Т.Период
		|		 ИЗ ВТПериодыДвижений КАК Т
		|		 ГДЕ Т.ЕстьИзмененныеДвижения)
		|";
		
		Запрос.Текст = СтрЗаменить(ШаблонТекстаЗапроса, "&ТекстПоляРегистра", ТекстПоляРегистра);
		
		ЭтотОбъект.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	#КонецОбласти
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Т.Период
	|ИЗ
	|	ВТПериодыДвижений КАК Т
	|ГДЕ
	|	Т.ЕстьИзмененныеДвижения
	|";
	
	ИзмененныеПериоды = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Период");
	
	СохранятьДвижения = НЕ ЕстьИзмененные
		ИЛИ РасчетСебестоимостиПрикладныеАлгоритмы.РазмерВременнойТаблицы(Запрос, "ВТПериодыДвижений") > 1;
	
	Возврат СохранятьДвижения;
	
КонецФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Организация КАК Организация,
		|	НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|	НачальныеЗаписи.КорВидЗапасов КАК КорВидЗапасов,
		|	НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|	НачальныеЗаписи.РазделУчета КАК РазделУчета,
		|	НачальныеЗаписи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	НачальныеЗаписи.Стоимость КАК Стоимость,
		|	НачальныеЗаписи.ДопРасходы КАК ДопРасходы,
		|	НачальныеЗаписи.Трудозатраты КАК Трудозатраты,
		|	НачальныеЗаписи.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
		|	НачальныеЗаписи.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
		|	НачальныеЗаписи.РасчетСебестоимости КАК РасчетСебестоимости,
		|	НачальныеЗаписи.РасчетПартий КАК РасчетПартий,
		|	НачальныеЗаписи.Сторно КАК Сторно
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.СебестоимостьТоваров КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если РасчетСебестоимостиПрикладныеАлгоритмы.ДвиженияЗаписываютсяРасчетомПартийИСебестоимости(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Организация КАК Организация,
		|		НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|		НачальныеЗаписи.КорВидЗапасов КАК КорВидЗапасов,
		|		НачальныеЗаписи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		НачальныеЗаписи.РазделУчета КАК РазделУчета,
		|		НачальныеЗаписи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		НачальныеЗаписи.Стоимость КАК Стоимость,
		|		НачальныеЗаписи.ДопРасходы КАК ДопРасходы,
		|		НачальныеЗаписи.Трудозатраты КАК Трудозатраты,
		|		НачальныеЗаписи.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
		|		НачальныеЗаписи.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
		|		НачальныеЗаписи.РасчетСебестоимости КАК РасчетСебестоимости,
		|		НачальныеЗаписи.РасчетПартий КАК РасчетПартий,
		|		НачальныеЗаписи.Сторно КАК Сторно
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.Период КАК Период,
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Организация КАК Организация,
		|		Записи.ВидЗапасов КАК ВидЗапасов,
		|		Записи.КорВидЗапасов КАК КорВидЗапасов,
		|		Записи.КорНаправлениеДеятельности КАК КорНаправлениеДеятельности,
		|		Записи.РазделУчета КАК РазделУчета,
		|		Записи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		-Записи.Стоимость КАК Стоимость,
		|		-Записи.ДопРасходы КАК ДопРасходы,
		|		-Записи.Трудозатраты КАК Трудозатраты,
		|		-Записи.ПостатейныеПостоянныеСНДС КАК ПостатейныеПостоянныеСНДС,
		|		-Записи.ПостатейныеПеременныеСНДС КАК ПостатейныеПеременныеСНДС,
		|		Записи.РасчетСебестоимости КАК РасчетСебестоимости,
		|		Записи.РасчетПартий КАК РасчетПартий,
		|		Записи.Сторно КАК Сторно
		|	ИЗ
		|		РегистрНакопления.СебестоимостьТоваров КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.Период,
		|	Таблица.Активность,
		|	Таблица.ВидДвижения,
		|	Таблица.Организация,
		|	Таблица.ВидЗапасов,
		|	Таблица.КорВидЗапасов,
		|	Таблица.КорНаправлениеДеятельности,
		|	Таблица.РазделУчета,
		|	Таблица.АналитикаУчетаНоменклатуры,
		|	Таблица.РасчетСебестоимости,
		|	Таблица.РасчетПартий,
		|	Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.Стоимость) <> 0 ИЛИ СУММА(Таблица.ДопРасходы) <> 0 ИЛИ СУММА(Таблица.Трудозатраты) <> 0
		|	ИЛИ СУММА(Таблица.ПостатейныеПостоянныеСНДС) <> 0 ИЛИ СУММА(Таблица.ПостатейныеПеременныеСНДС) <> 0
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект, Неопределено,
			ДанныеКРегистрацииЗаданий);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
