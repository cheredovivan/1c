#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	БлокироватьДляИзменения = Истина;
	
	ДополнительныеСвойства.Вставить("ПартионныйУчетВключен",
		РасчетСебестоимостиПовтИсп.ПартионныйУчетВключен(
			НачалоМесяца(ДополнительныеСвойства.ДатаРегистратора)));
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	Товары.Период                     КАК Период,
	|	Товары.Регистратор                КАК Регистратор,
	|	Товары.ВидЗапасов                 КАК ВидЗапасов,
	|	Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Товары.Валюта                     КАК Валюта,
	|	Товары.НомерГТД                   КАК НомерГТД,
	|
	|	Товары.Количество                 КАК Количество,
	|	Товары.СуммаВыручки               КАК СуммаВыручки,
	|	Товары.КоличествоСписано          КАК КоличествоСписано,
	|
	|	Товары.Количество                 КАК КоличествоКОформлению,
	|	Товары.СуммаВыручки               КАК СуммаВыручкиКОформлению,
	|	Товары.КоличествоСписано          КАК КоличествоСписаноКОформлению,
	|
	|	Товары.ХозяйственнаяОперация      КАК ХозяйственнаяОперация,
	|	Товары.СуммаВознаграждения        КАК СуммаВознаграждения,
	|	Товары.ДокументРеализации         КАК ДокументРеализации,
	|	Товары.ДатаСчетаФактуры           КАК ДатаСчетаФактуры,
	|	Товары.Покупатель                 КАК Покупатель,
	|	Товары.КорВидЗапасов              КАК КорВидЗапасов,
	|	Товары.Номенклатура               КАК Номенклатура,
	|	Товары.Характеристика             КАК Характеристика,
	|	Товары.НомерСчетаФактуры          КАК НомерСчетаФактуры
	|ПОМЕСТИТЬ ТоварыКОформлениюПередЗаписью
	|ИЗ
	|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК Товары
	|ГДЕ
	|	Товары.Регистратор = &Регистратор
	|	И &ПартионныйУчетВключен
	|");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ПартионныйУчетВключен", ДополнительныеСвойства.ПартионныйУчетВключен);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
		Возврат;
	КонецЕсли;
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства)
		Или ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Регистратор КАК Регистратор,
	|	Таблица.Организация КАК Организация,
	|	Таблица.ВидЗапасов КАК ВидЗапасов,
	|	Таблица.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	Таблица.Валюта КАК Валюта,
	|	Таблица.НомерГТД КАК НомерГТД,
	|	Таблица.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	Таблица.СуммаВознаграждения КАК СуммаВознаграждения,
	|	Таблица.ДокументРеализации КАК ДокументРеализации,
	|	Таблица.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|	Таблица.Покупатель КАК Покупатель,
	|	Таблица.КорВидЗапасов КАК КорВидЗапасов,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.НомерСчетаФактуры КАК НомерСчетаФактуры,
	|	СУММА(Таблица.Количество) КАК КоличествоИзменение,
	|	СУММА(Таблица.СуммаВыручки) КАК СуммаВыручкиИзменение,
	|	СУММА(Таблица.КоличествоСписано) КАК КоличествоСписаноИзменение
	|ПОМЕСТИТЬ ТаблицаИзмененийТоварыКОформлениюОтчетовКомитенту
	|ИЗ
	|	(ВЫБРАТЬ
	|		Товары.Период КАК Период,
	|		Товары.Регистратор КАК Регистратор,
	|		ВидыЗапасов.Организация КАК Организация,
	|		Товары.ВидЗапасов КАК ВидЗапасов,
	|		Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.Валюта КАК Валюта,
	|		Товары.НомерГТД КАК НомерГТД,
	|		Товары.Количество КАК Количество,
	|		Товары.СуммаВыручки КАК СуммаВыручки,
	|		Товары.КоличествоСписано КАК КоличествоСписано,
	|		Товары.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		Товары.СуммаВознаграждения КАК СуммаВознаграждения,
	|		Товары.ДокументРеализации КАК ДокументРеализации,
	|		Товары.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|		Товары.Покупатель КАК Покупатель,
	|		Товары.КорВидЗапасов КАК КорВидЗапасов,
	|		Товары.Номенклатура КАК Номенклатура,
	|		Товары.Характеристика КАК Характеристика,
	|		Товары.НомерСчетаФактуры КАК НомерСчетаФактуры
	|	ИЗ
	|		ТоварыКОформлениюПередЗаписью КАК Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|			ПО Товары.ВидЗапасов = ВидыЗапасов.Ссылка
	|	ГДЕ
	|		&ПартионныйУчетВключен
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Товары.Период КАК Период,
	|		Товары.Регистратор КАК Регистратор,
	|		ВидыЗапасов.Организация КАК Организация,
	|		Товары.ВидЗапасов КАК ВидЗапасов,
	|		Товары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|		Товары.Валюта КАК Валюта,
	|		Товары.НомерГТД КАК НомерГТД,
	|		-Товары.Количество КАК Количество,
	|		-Товары.СуммаВыручки КАК СуммаВыручки,
	|		-Товары.КоличествоСписано КАК КоличествоСписано,
	|		Товары.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|		Товары.СуммаВознаграждения КАК СуммаВознаграждения,
	|		Товары.ДокументРеализации КАК ДокументРеализации,
	|		Товары.ДатаСчетаФактуры КАК ДатаСчетаФактуры,
	|		Товары.Покупатель КАК Покупатель,
	|		Товары.КорВидЗапасов КАК КорВидЗапасов,
	|		Товары.Номенклатура КАК Номенклатура,
	|		Товары.Характеристика КАК Характеристика,
	|		Товары.НомерСчетаФактуры КАК НомерСчетаФактуры
	|	ИЗ
	|		РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК Товары
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|			ПО Товары.ВидЗапасов = ВидыЗапасов.Ссылка
	|	ГДЕ
	|		Товары.Регистратор = &Регистратор
	|		И &ПартионныйУчетВключен) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.Период,
	|	Таблица.Регистратор,
	|	Таблица.Организация,
	|	Таблица.ВидЗапасов,
	|	Таблица.АналитикаУчетаНоменклатуры,
	|	Таблица.Валюта,
	|	Таблица.НомерГТД,
	|	Таблица.ХозяйственнаяОперация,
	|	Таблица.СуммаВознаграждения,
	|	Таблица.ДокументРеализации,
	|	Таблица.ДатаСчетаФактуры,
	|	Таблица.Покупатель,
	|	Таблица.КорВидЗапасов,
	|	Таблица.Номенклатура,
	|	Таблица.Характеристика,
	|	Таблица.НомерСчетаФактуры
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.Количество) <> 0
	|	ИЛИ СУММА(Таблица.СуммаВыручки) <> 0
	|	ИЛИ СУММА(Таблица.КоличествоСписано) <> 0
	|");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.УстановитьПараметр("ПартионныйУчетВключен", ДополнительныеСвойства.ПартионныйУчетВключен);
	
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчереди()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	НачальныеЗаписи.Регистратор КАК Регистратор,
		|	НачальныеЗаписи.Период КАК Период,
		|	НачальныеЗаписи.Активность КАК Активность,
		|	НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|	НачальныеЗаписи.Организация КАК Организация,
		|	НачальныеЗаписи.Валюта КАК Валюта,
		|	НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|	НачальныеЗаписи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|	НачальныеЗаписи.Сторно КАК Сторно,
		|	НачальныеЗаписи.СуммаВыручки КАК СуммаВыручки,
		|	НачальныеЗаписи.СуммаВыручкиУпр КАК СуммаВыручкиУпр
		|ПОМЕСТИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи
		|ИЗ
		|	РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК НачальныеЗаписи
		|ГДЕ
		|	НачальныеЗаписи.Регистратор = &Регистратор";
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Выполнить();
		
		ДополнительныеСвойства.Вставить("МенеджерВременныхТаблицОчередьЗаданий", Запрос.МенеджерВременныхТаблиц);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") 
		И ПолучитьФункциональнуюОпцию("ФормироватьУправленческийБаланс") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблицОчередьЗаданий;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Документ,
		|	Таблица.Период КАК ПериодЗадания,
		|	Таблица.Организация КАК Организация,
		|	ЗНАЧЕНИЕ(Справочник.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) КАК ВидЗадания
		|ИЗ
		|	(ВЫБРАТЬ
		|		НачальныеЗаписи.Регистратор КАК Регистратор,
		|		НачальныеЗаписи.Период КАК Период,
		|		НачальныеЗаписи.Активность КАК Активность,
		|		НачальныеЗаписи.ВидДвижения КАК ВидДвижения,
		|		НачальныеЗаписи.Организация КАК Организация,
		|		НачальныеЗаписи.Валюта КАК Валюта,
		|		НачальныеЗаписи.ВидЗапасов КАК ВидЗапасов,
		|		НачальныеЗаписи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		НачальныеЗаписи.Сторно КАК Сторно,
		|		НачальныеЗаписи.СуммаВыручки КАК СуммаВыручки,
		|		НачальныеЗаписи.СуммаВыручкиУпр КАК СуммаВыручкиУпр
		|	ИЗ
		|		ВТ_ОчередьЗаданийНачальныеЗаписи КАК НачальныеЗаписи
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Записи.Регистратор КАК Регистратор,
		|		Записи.Период КАК Период,
		|		Записи.Активность КАК Активность,
		|		Записи.ВидДвижения КАК ВидДвижения,
		|		Записи.Организация КАК Организация,
		|		Записи.Валюта КАК Валюта,
		|		Записи.ВидЗапасов КАК ВидЗапасов,
		|		Записи.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
		|		Записи.Сторно КАК Сторно,
		|		-Записи.СуммаВыручки КАК СуммаВыручки,
		|		-Записи.СуммаВыручкиУпр КАК СуммаВыручкиУпр
		|	ИЗ
		|		РегистрНакопления.ТоварыКОформлениюОтчетовКомитенту КАК Записи
		|	ГДЕ
		|		Записи.Регистратор = &Регистратор) КАК Таблица
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.Период,
		|	Таблица.Активность,
		|	Таблица.ВидДвижения,
		|	Таблица.Организация,
		|	Таблица.Валюта,
		|	Таблица.ВидЗапасов,
		|	Таблица.АналитикаУчетаНоменклатуры,
		|	Таблица.Сторно
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаВыручки) <> 0 ИЛИ СУММА(Таблица.СуммаВыручкиУпр) <> 0
		|;
		|УНИЧТОЖИТЬ ВТ_ОчередьЗаданийНачальныеЗаписи";

		ДанныеКРегистрацииЗаданий = Запрос.Выполнить().Выгрузить();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект, Неопределено,
			ДанныеКРегистрацииЗаданий);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли