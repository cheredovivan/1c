#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Поиск/Создание узла обмена с сервисом интеграции
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  СоздатьПриОтсутствии - Булево - Указание о необходимости создания узла, если поиск окончился неудачей
//  Наименование - Строка - Наименование нового узла. Используется при СоздатьПриОтсутствии = Истина
// 
// Возвращаемое значение:
//  ПланОбменаСсылка.МаркетплейсыСИ - узел плана обмена с сервисом интеграции
Функция НайтиСоздатьУзел(Знач УчетнаяЗапись, Знач СоздатьПриОтсутствии = Ложь, Знач Наименование = "") Экспорт
	
	Результат = ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ПланСервисИнтеграции.Ссылка КАК Узел
		|ИЗ
		|	ПланОбмена.МаркетплейсыСИ КАК ПланСервисИнтеграции
		|ГДЕ
		|	ПланСервисИнтеграции.УчетнаяЗапись = &УчетнаяЗапись
		|	И НЕ ПланСервисИнтеграции.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Если СоздатьПриОтсутствии Тогда

			ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
			
			Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) Тогда
				Префикс = ИнтеграцияСМаркетплейсомWildberriesКлиентСервер.ПрефиксМаркетплейса();
			ИначеЕсли ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса) Тогда
				Префикс = ИнтеграцияСМаркетплейсомLamodaКлиентСервер.ПрефиксМаркетплейса();
			Иначе
				Префикс = "";
			КонецЕсли;
			
			Узел = СоздатьУзел();
			Узел.УстановитьНовыйКод(Префикс);
			Узел.Наименование = Наименование;
			Узел.УчетнаяЗапись = УчетнаяЗапись;
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("ПланОбмена.МаркетплейсыСИ");
			ЭлементБлокировки.УстановитьЗначение("УчетнаяЗапись", УчетнаяЗапись);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				
			НачатьТранзакцию();
				
			Попытка
				Блокировка.Заблокировать();
				
				Узел.Записать();
				Результат = Узел.Ссылка;
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					
				ЗаписьЖурналаРегистрации(ИнтеграцияСМаркетплейсамиСИ.СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
				
			КонецПопытки;
			
		КонецЕсли;
	Иначе
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Результат = Выборка.Узел;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Поиск узлов обмена с сервисом интеграции
//
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ИгнорироватьПометкуУдаленияУчетнойЗаписи - Булево - Признак получения узлов по пометке удаления учетной записи
// 
// Возвращаемое значение:
//  Массив Из ПланОбменаСсылка.МаркетплейсыСИ - узлы плана обмена с сервисом интеграции
Функция НайтиУзлы(Знач УчетнаяЗапись, ИгнорироватьПометкуУдаленияУчетнойЗаписи = Ложь) Экспорт
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Узлы.Ссылка КАК Узел
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК Узлы
		|		ПО УчетныеЗаписи.Ссылка = Узлы.УчетнаяЗапись
		|			И (НЕ Узлы.ПометкаУдаления)
		|ГДЕ
		|	УчетныеЗаписи.Ссылка = &УчетнаяЗапись
		|	И (&ИгнорироватьПометкуУдаленияУчетнойЗаписи
		|	ИЛИ НЕ УчетныеЗаписи.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИгнорироватьПометкуУдаленияУчетнойЗаписи", ИгнорироватьПометкуУдаленияУчетнойЗаписи);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Добавить(Выборка.Узел);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли