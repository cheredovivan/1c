#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПроверитьУчетнуюЗаписьМаркетплейса(Отказ);
	Если Отказ Тогда
		Сообщение = СтрШаблон(НСтр("ru = 'Для учетной записи %1 уже существует настройка обмена с сервисом интеграции';
									|en = 'An exchange setting with the integration service already exists for the ""%1"" account'"), УчетнаяЗапись);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, ЭтотОбъект, "УчетнаяЗапись", Ссылка, Отказ);
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьУчетнуюЗаписьМаркетплейса(Отказ)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Отказ = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПланСервисИнтеграции.Ссылка КАК Ссылка
	|ИЗ
	|	ПланОбмена.МаркетплейсыСИ КАК ПланСервисИнтеграции
	|ГДЕ
	|	НЕ ПланСервисИнтеграции.Ссылка = &СсылкаНаОбъект
	|	И ПланСервисИнтеграции.УчетнаяЗапись = &УчетнаяЗапись
	|	И НЕ ПланСервисИнтеграции.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СсылкаНаОбъект", Ссылка);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли