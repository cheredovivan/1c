#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
	
		Если Параметры = Неопределено Тогда
			Параметры = Новый Структура();
		КонецЕсли;
		
		Параметры.Вставить("РежимВыбора", Истина);
		
		ВыбраннаяФорма       = "Справочник.КлассификаторТранспортныхСредствСАТУРН.Форма.ФормаСписка";
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторТранспортныхСредствСАТУРН - Ссылка на страну.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	СообщенияJSON = Новый Массив;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаКлассификатораТранспортныхСредствJSON(ДополнительныеПараметры);
		
	КонецЕсли;

	Возврат СообщенияJSON;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					Продолжить;
				КонецЕсли;
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обмен

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * Идентификатор - Строка
// * Наименование - Строка - наименование транспортного средства
// * КодТС - Строка - Код вида ТС
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеТранспортногоСредства     = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеТранспортногоСредства.Вставить("Идентификатор", Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеТранспортногоСредства.Вставить("Наименование",  ЭлементДанных.transportName);
	ДанныеТранспортногоСредства.Вставить("КодТС",         ЭлементДанных.transportCode);
	
	Возврат ДанныеТранспортногоСредства;
	
КонецФункции

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.ЗагрузкаКлассификаторов;
КонецФункции

#КонецОбласти

#Область ПоискСсылок

Функция КлассификаторТС(ЗначениеИдентификатора, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Идентификатор = Формат(ЗначениеИдентификатора, "ЧГ=0;");
	Если Не ЗначениеЗаполнено(Идентификатор)
		Или ЗначениеИдентификатора = -1 Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ИмяТаблицы       = Метаданные.Справочники.КлассификаторТранспортныхСредствСАТУРН.ПолноеИмя();
	СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				ДанныеОбъекта = ИнтеграцияСАТУРНСлужебный.ДанныеОбъекта(
					Идентификатор,
					Метаданные.Справочники.КлассификаторТранспортныхСредствСАТУРН, ПараметрыОбмена);
				Если ДанныеОбъекта = Неопределено Тогда
					СправочникСсылка = СоздатьТранспортноеСредство(Идентификатор, ПараметрыОбмена);
					ИнтеграцияСАТУРНСлужебный.ДобавитьКЗагрузке(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка, ОрганизацияСАТУРН);
				Иначе
					СправочникСсылка = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,, Ложь, ОрганизацияСАТУРН);
				КонецЕсли;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании транспортного средства с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании транспортного средства с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании транспортного средства с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании транспортного средства с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с классификатором транспортных средств';
					|en = 'Работа с классификатором транспортных средств'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьОбъекты(СписокКлассификатораТранспортныхСредств, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	ИзмененныеОбъекты = Новый Массив;
	
	Если СписокКлассификатораТранспортныхСредств.Количество() = 0 Тогда
		Возврат ИзмененныеОбъекты;
	КонецЕсли;
	
	ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
		ПустаяСсылка().Метаданные(), СписокКлассификатораТранспортныхСредств, ПараметрыОбмена);
	ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
	
	Попытка
		
		Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
			
			ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
			СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена);
			
			Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ИзмененныеОбъекты;
	
КонецФункции

Функция ЗагрузитьОбъект(ДанныеТранспортногоСредства, ПараметрыОбмена, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Если ДанныеТранспортногоСредства = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового  = Ложь;
	ИмяТаблицы    = Метаданные.Справочники.КлассификаторТранспортныхСредствСАТУРН.ПолноеИмя();
	Идентификатор = Формат(ДанныеТранспортногоСредства.Идентификатор, "ЧРГ=; ЧГ=;");
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(
				ПараметрыОбмена, ИмяТаблицы, Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникОбъект.УстановитьСсылкуНового(ПолучитьСсылку(ИдентификаторОбъекта));
			ЗаписьНового = Истина;
			
		ИначеЕсли ДанныеТранспортногоСредства.Идентификатор = 1 Тогда
			СправочникОбъект = АвтомобильныйТранспорт.ПолучитьОбъект();
		ИначеЕсли ДанныеТранспортногоСредства.Идентификатор = 2 Тогда
			СправочникОбъект = ЖелезнодородныйТранспорт.ПолучитьОбъект();
		ИначеЕсли ДанныеТранспортногоСредства.Идентификатор = 3 Тогда
			СправочникОбъект = АвиационноеСообщение.ПолучитьОбъект();
		ИначеЕсли ДанныеТранспортногоСредства.Идентификатор = 4 Тогда
			СправочникОбъект = МорскоеСообщение.ПолучитьОбъект();
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Наименование  = ДанныеТранспортногоСредства.Наименование;
	СправочникОбъект.Идентификатор = Идентификатор;
	СправочникОбъект.КодТС         = ДанныеТранспортногоСредства.КодТС;
	
	СправочникОбъект.ТребуетсяЗагрузка = Ложь;
	СправочникОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеТранспортногоСредства.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция СоздатьТранспортноеСредство(Идентификатор, ПараметрыОбмена)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор     = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка = Истина;
	СправочникОбъект.Наименование      = НСтр("ru = '<Требуется загрузка>';
												|en = '<Требуется загрузка>'");
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

Функция ЗагрузкаКлассификатораТранспортныхСредствJSON(ПараметрыОбмена)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	
	АргументыЗапроса = СообщениеJSON.АргументыОперации;
	ИмяСловаря = "transport";
	
	ИнтерфейсСАТУРН.ДобавитьАргумент(АргументыЗапроса, "dictionary", ИмяСловаря);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли