#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") И Не ЗначениеЗаполнено(Страна) Тогда
		Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	КонецЕсли;
	
	Если ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Адвалорная Тогда
		Валюта = Справочники.Валюты.ПустаяСсылка();
		ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Адвалорная Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмерения");
		МассивНепроверяемыхРеквизитов.Добавить("Валюта");
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Страна");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Адвалорная Тогда
		
		БлокировкаДанных = Новый БлокировкаДанных();
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СтавкиКатегорийПодакцизныхТоваров");
		ЭлементБлокировкиДанных.УстановитьЗначение("КатегорияПодакцизногоТовара", Ссылка);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.СтавкиКатегорийПодакцизныхТоваров.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.КатегорияПодакцизногоТовара.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Выбран() Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли