
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриСозданииНаСервере(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность	

	// Запрет редактирования
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	Элементы.Страна.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность	
	
	ПриЧтенииСозданииНаСервере();
	
	ТипИсходнойСтавки = ТекущийОбъект.ТипСтавки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьВидимостьПоТипуСтавки();
	ОбновитьДоступностьКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ТипИсходнойСтавки = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая")
		И Объект.ТипСтавки <> ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая")
		И ЕстьДанныеВРегистре
		И Не ЕстьСогласие Тогда
		
		ТекстВопроса = НСтр("ru = 'Не рекомендуется менять тип ставки с твердой на адвалорную, т.к. по ней могут быть начисления. Если продолжить, то будет удалена вся история ставок по данной категории. Продолжить?';
							|en = 'We do not recommend that you change the rate type from fixed to ad valorem, as there may be accruals on it. If you continue, the entire rate history for this category will be deleted. Continue?'");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ПродолжитьЗаписьЗавершение", ЭтотОбъект),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
			
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЕдиницаИзмеренияДоИзменения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Ссылка, "ЕдиницаИзмерения");
	КонецЕсли;
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПередЗаписьюНаСервере(ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
		
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая Тогда
		
		Категория = ТекущийОбъект.Ссылка;
		ДанныеАкциза = СтавкаПоКатегорииНаДату(Категория, ДатаС);
		Если Не ЗначениеЗаполнено(ДанныеАкциза.Период) Или ДанныеАкциза.Ставка <> Ставка Тогда
			
			РегистрыСведений.СтавкиКатегорийПодакцизныхТоваров.УстановитьСтавкуПоКатегории(Категория, Ставка, ДатаС);
			
		ИначеЕсли ДанныеАкциза.Период <> ДатаС Тогда
			
			Отказ = Истина;
			ТекстСообщения = СтрШаблон(НСтр("ru = 'По данному виду подакцизного товара уже действует данная ставка с %1.';
											|en = 'For this type of excisable goods, this rate is already in effect from %1.'"), Формат(ДанныеАкциза.Период,"ДЛФ=D" ));
			Если ЗначениеЗаполнено(ЕдиницаИзмеренияДоИзменения) И Объект.ЕдиницаИзмерения <> ЕдиницаИзмеренияДоИзменения Тогда
				ТекстСообщения = ТекстСообщения + СтрШаблон(НСтр("ru = 'Для изменения единицы измерения акциза необходимо выбрать уже записанный период.';
																|en = 'To change the excise unit of measure, select an already saved period.'"));
			КонецЕсли;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьТекущиеДанные(Ложь,Истина);
	Модифицированность = Ложь;
	ТипИсходнойСтавки = Объект.ТипСтавки;
	
	// Запрет редактирования
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбновитьДоступностьКнопок();
	Оповестить("Запись_ПодакцизнаяКатегория", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "Запись_СтавкаПодакцизнойКатегории"
		Или ИмяСобытия = "Удалить_СтавкаПодакцизнойКатегории")
		И Источник = Объект.Ссылка Тогда
		ОбновитьТекущиеДанные(,, Ложь);
		ОбновитьДоступностьКнопок();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Объект.ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая Тогда
		
		Если Не ЗначениеЗаполнено(ДатаС) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , Элементы.ДатаС.Заголовок);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Элементы.ДатаС, , Отказ);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_Открытие(Элемент, СтандартнаяОбработка)
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиент.ПриОткрытии(ЭтотОбъект, Объект, Элемент, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
КонецПроцедуры

&НаКлиенте
Процедура ДатаСПриИзменении(Элемент)
	
	ОбновитьТекущиеДанные(,, Ложь);
	ОбновитьДоступностьКнопок();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипСтавкиПриИзменении(Элемент)
	
	УстановитьВидимостьПоТипуСтавки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИсторииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИсторияИзменений" Тогда
		Если Модифицированность Тогда
			ЗадатьВопросФормаМодифицирована();
		Иначе
			ОткрытьИсториюИзменений();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеИсторииНажатие(Элемент)
	
	Если Модифицированность Тогда
		ЗадатьВопросФормаМодифицирована();
	Иначе
		ОткрытьИсториюИзменений();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаПриИзменении(Элемент)
	
	СтавкаИзРегистра = Ложь;
	ОбновитьДоступностьКнопок();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Обработчик команды, создаваемой механизмом запрета редактирования ключевых реквизитов.
//
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)

	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюПоСтавкам(Команда)
	
	ОбновитьТекущиеДанные(,, Ложь);
	ОбновитьДоступностьКнопок();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	ДатаС = ТекущаяДатаСеанса();
	ЦветТекстаПроблема = ЦветаСтиля.ЦветТекстаПроблема;
	ЦветТекстаЗаписи = ЦветаСтиля.ЦветРазделаПанелиФункций;
	
	ОбновитьТекущиеДанные(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТекущиеДанные(ПоТекущемуПериоду = Ложь, ОбновлятьДанные = Истина, ОбновлятьПериод = Истина)
	
	Если Объект.ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая Тогда
		
		Если ПоТекущемуПериоду Тогда
			ДатаСеанса = ТекущаяДатаСеанса();
			Если ОбновлятьПериод Тогда
				ДатаС = ДатаСеанса;
			КонецЕсли;
			Период = ДатаСеанса;
		Иначе
			Период = ДатаС;
		КонецЕсли;
		
		ТекстДатаПо = НСтр("ru = 'действует бессрочно.';
							|en = 'valid indefinitely.'");
		ЕстьДанныеВРегистре = Ложь;
		СтавкаИзРегистра = Ложь;
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			
			Результат = СтавкаПоКатегорииНаДату(Объект.Ссылка, Период);
				
			Если ЗначениеЗаполнено(Результат.Период) Тогда
				
				ЕстьДанныеВРегистре = Истина;
				
				Если ОбновлятьДанные Тогда
					Если ОбновлятьПериод Тогда
						ДатаС = Результат.Период;
					КонецЕсли;
					Ставка = Результат.Ставка;
					СтавкаИзРегистра = Истина;
				КонецЕсли;
			ИначеЕсли ОбновлятьДанные Тогда
				Если ОбновлятьПериод И ЗначениеЗаполнено(Результат.ДатаПо) Тогда
					ДатаС = Результат.ДатаПо+1;
					ОбновитьТекущиеДанные(Ложь, Истина, Истина);
					Возврат;
				КонецЕсли;
				Ставка = 0;
			КонецЕсли;
			
			ГиперссылкаТекст = НСтр("ru = 'История изменений';
									|en = 'Change history'") + " (" + Результат.КоличествоПериодов + ")";
			Элементы.ОписаниеИстории.Заголовок = ГиперссылкаТекст;
			Элементы.ОписаниеИстории.Видимость = Истина;
			
			Если ЗначениеЗаполнено(Результат.ДатаПо) Тогда
				ТекстДатаПо = НСтр("ru = 'действует по';
									|en = 'Valid until'") + " " + Формат(Результат.ДатаПо, "ДЛФ=DD");
			КонецЕсли;
		
		Иначе
			Элементы.ОписаниеИстории.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ДатаПо.Заголовок = ТекстДатаПо;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзменений()
	
	ПараметрыФормы = Новый Структура("ТолькоПросмотр, КатегорияПодакцизногоТовара", ТолькоПросмотр, Объект.Ссылка);
	
	ОткрытьФорму("РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.Форма.РедактированиеИстории",
		ПараметрыФормы,
		ЭтотОбъект, , , ,
		Новый ОписаниеОповещения("ОткрытьИсториюИзмененийЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ДатаС = Результат;
		ОбновитьТекущиеДанные(Ложь, Истина);
		ОбновитьДоступностьКнопок();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьВопросФормаМодифицирована()
	
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
						|en = 'The data has changed. Do you want to save the changes?'");
	Оповещение = Новый ОписаниеОповещения("ОткрытьИсториюИзмененийПродолжение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИсториюИзмененийПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ОткрытьИсториюИзменений();
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		Если Не ЗаписатьИзменения() Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаписатьИзменения(Закрытие = Ложь)
	
	ОчиститьСообщения();
	Записать();
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция СтавкаПоКатегорииНаДату(КатегорияПодакцизногоТовара, ПериодСтавки)
	
	Результат = РегистрыСведений.СтавкиКатегорийПодакцизныхТоваров.СтавкаПоКатегорииНаДату(
		КатегорияПодакцизногоТовара, ПериодСтавки);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьДоступностьКнопок()
	
	Если Не ЕстьДанныеВРегистре
	Или Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ИнформацияПоДанным.ЦветТекста = ЦветТекстаПроблема;
		Элементы.Ставка.ЦветТекста = ЦветТекстаПроблема;
		Элементы.ИнформацияПоДанным.Заголовок = НСтр("ru = 'Данные по ставке не записаны';
													|en = 'Rate data is not saved'");
	ИначеЕсли Не СтавкаИзРегистра Тогда
		Элементы.ИнформацияПоДанным.ЦветТекста = ЦветТекстаПроблема;
		Элементы.Ставка.ЦветТекста = Новый Цвет;
		Элементы.ИнформацияПоДанным.Заголовок = НСтр("ru = 'Данные по ставке изменены';
													|en = 'Rate data is changed'");
	Иначе
		Элементы.ИнформацияПоДанным.ЦветТекста = ЦветТекстаЗаписи;
		Элементы.Ставка.ЦветТекста = Новый Цвет;
		Элементы.ИнформацияПоДанным.Заголовок = НСтр("ru = 'Данные по ставке записаны';
													|en = 'Rate data is saved'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьПоТипуСтавки()

	Если Объект.ТипСтавки = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая") Тогда
		Элементы.ТекущиеДанные.Видимость = Истина;
		Элементы.ИнформацияПоАдвалорнойСтавке.Видимость = Ложь;
	Иначе
		Элементы.ТекущиеДанные.Видимость = Ложь;
		Элементы.ИнформацияПоАдвалорнойСтавке.Видимость = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьЗаписьЗавершение(Результат, ДополнительныеДанные) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЕстьСогласие = Истина;
		Записать();
	Иначе
		Объект.ТипСтавки = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая");
		ОбновитьТекущиеДанные(Истина, Истина);
		УстановитьВидимостьПоТипуСтавки();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
