#Область ОбработчикиСобытийФорм

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОтображатьДопЭлементы = НЕ ОбщегоНазначенияБПО.ЭтоМобильнаяПлатформа();
	
	ПравоДоступаСохранениеДанныхПользователя = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	СозданиеНовогоЭлемента = Объект.Ссылка = Справочники.ПодключаемоеОборудование.ПустаяСсылка();
	
	// Защита от изменения уже созданного экземпляра устройства.
	Элементы.ДрайверОборудования.ТолькоПросмотр = НЕ СозданиеНовогоЭлемента;
	Элементы.ДрайверОборудования.КнопкаОткрытия = Не СозданиеНовогоЭлемента;
	Элементы.ТипОборудования.ТолькоПросмотр = НЕ СозданиеНовогоЭлемента;
	     
	ПроверкаПараметров = СозданиеНовогоЭлемента;
	
	Если ОтображатьДопЭлементы Тогда
		Элементы.РабочееМесто.Видимость = Объект.ТипПодключения = Перечисления.ТипыПодключенияОборудования.ЛокальноеПодключение;
		Элементы.ОграничениеДоступа.Видимость = Объект.ТипПодключения = Перечисления.ТипыПодключенияОборудования.ОбщийДоступ;
	Иначе
		Элементы.РабочееМесто.Видимость = Ложь;
		Элементы.ТипПодключения.Видимость = Ложь;
		Элементы.ОграничениеДоступа.Видимость = Ложь; 
		Элементы.ПерейтиНаСайтИнструкции.Видимость = Ложь;
	КонецЕсли;

	Если СозданиеНовогоЭлемента Тогда 
		Если ПустаяСтрока(Объект.РабочееМесто) 
			И Объект.ТипПодключения = Перечисления.ТипыПодключенияОборудования.ЛокальноеПодключение Тогда
			Объект.РабочееМесто = МенеджерОборудованияВызовСервера.РабочееМестоКлиента();
		КонецЕсли;
		Объект.УстройствоИспользуется = Истина;
	Иначе
		Если Объект.ПометкаУдаления Или Не Объект.УстройствоИспользуется Тогда
			Элементы.ПараметрыПодключения.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбщегоНазначенияБПО.ИспользуетсяРаспределеннаяФискализация() Тогда
		МодульРаспределеннаяФискализация = ОбщегоНазначенияБПО.ОбщийМодуль("РаспределеннаяФискализация");
		Элементы.АвтоматическаяФискализация.Видимость = МодульРаспределеннаяФискализация.ДоступноРаспределеннаяФискализация();
	Иначе
		Элементы.АвтоматическаяФискализация.Видимость = Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначенияБПО.ЭтоМобильнаяПлатформа() Тогда
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.ТипПодключения);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.ТипОборудования);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.ДрайверОборудования);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.Организация);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.Наименование);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.РабочееМесто);
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.СерийныйНомер);   
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.СпособФорматноЛогическогоКонтроля);                                           
		ОбщегоНазначенияБПО.ПодготовитьЭлементУправления(Элементы.ДопустимоеРасхождениеФорматноЛогическогоКонтроля);
	КонецЕсли;
	
	Если СозданиеНовогоЭлемента Тогда
		Элементы.ДрайверОборудования.РежимВыбораИзСписка = Истина;
		ЗаполнитьСписокДрайверов();
	КонецЕсли;

	ОбновитьИнтерфейсФормы();    
	
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияПриСозданииНаСервере(Объект, ЭтотОбъект, Отказ, Параметры, СтандартнаяОбработка);
	УстановитьВидимостьОрганизацииНаСервере();   
	
	ОбновитьПараметрыРегистрации();
	
	Если СозданиеНовогоЭлемента Тогда     
		Элементы.ФормаЗагрузитьССайтаИТС.Видимость = Ложь;
	Иначе                    
		ДрайверОборудованияСсылка = Объект.ДрайверОборудования;
		ДрайверОборудованияРеквизиты = ОбщегоНазначенияБПО.ЗначенияРеквизитовОбъекта(ДрайверОборудованияСсылка, "СпособПодключения, СнятСПоддержки");
		ДоступнаЗагрузкаССайтаИТС = ДрайверОборудованияРеквизиты.СпособПодключения = Перечисления.СпособПодключенияДрайвера.ИзИнформационнойБазы 
			И НЕ ДрайверОборудованияРеквизиты.СнятСПоддержки;
		Элементы.ФормаЗагрузитьССайтаИТС.Видимость = ДоступнаЗагрузкаССайтаИТС 
			И ОбщегоНазначенияБПО.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент");
		
		ДанныеДрайвера = МенеджерОборудования.ДанныеДрайвераОборудования(ДрайверОборудованияСсылка);
	КонецЕсли;
	
	Элементы.ФормаСообщениеВТехническуюПоддержку.Видимость = ОбщегоНазначенияБПО.ИспользуетсяСообщенияВСлужбуТехническойПоддержки()
			И ОбщегоНазначенияБПОСлужебныйВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	Элементы.ФормаОперацииСОборудованием.Видимость = 
		ОбщегоНазначенияБПО.ИспользуетсяПодсистемаЛогирования()
		И Не ОбщегоНазначенияБПО.ЭтоМобильнаяПлатформа();
		
	Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Ложь;
	ОбновитьИнформациюОЛогахДрайвераНаСервере();
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКоманды = ОбщегоНазначенияБПО.ОбщийМодуль("ПодключаемыеКоманды");
		МодульПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияПриЧтенииНаСервере(ТекущийОбъект, ЭтотОбъект);
	
	// Вызов БСП
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначенияБПО.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
		МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// Конец Вызов БСП
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ОбщегоНазначенияБПОКлиент.ИспользуетсяСообщенияВСлужбуТехническойПоддержки() Тогда
		МодульСообщенияВСлужбуТехническойПоддержкиБПОКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиБПОКлиент");
		Элементы.ФормаСообщениеВТехническуюПоддержку.Видимость 
			= МодульСообщенияВСлужбуТехническойПоддержкиБПОКлиент.ОтправлятьСообщенияВТехПоддержку();
	КонецЕсли;
		
	Элементы.ФормаЗагрузитьССайтаИТС.Доступность = 
		ЗначениеЗаполнено(Объект.ТипОборудования) И ЗначениеЗаполнено(Объект.ДрайверОборудования);
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияПриОткрытии(Объект, ЭтаФорма, Отказ);
	
	ОбновитьИнформациюОЛогахДрайвера();
	
	// Вызов БСП
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияБПОКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// Конец Вызов БСП
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не Отказ И Модифицированность Тогда
		ОбновитьПовторноИспользуемыеЗначения();
	КонецЕсли;
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияПередЗаписью(Объект, ЭтотОбъект, Отказ, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	ПараметрыЗаписи.Вставить("НовыйЭлементБПО", ТекущийОбъект.ЭтоНовый());
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.НовыйЭлементБПО И ЗначениеЗаполнено(ТекущийОбъект.ДрайверОборудования) Тогда
		Если Не ОбщегоНазначенияБПО.РазделениеВключено() Тогда
			ДобавитьДрайверВСправочникВнешнихКомпонентИзМакета(ТекущийОбъект.ДрайверОборудования);
		КонецЕсли;
	КонецЕсли;
	ПараметрыЗаписи.Удалить("НовыйЭлементБПО");
	
	ПараметрыУстройства = МенеджерОборудования.ПараметрыУстройства(Объект.Ссылка);
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи);
	
	ДрайверОборудованияСсылка = Объект.ДрайверОборудования;
	ДрайверОборудованияРеквизиты = 
		ОбщегоНазначенияБПО.ЗначенияРеквизитовОбъекта(ДрайверОборудованияСсылка, "СпособПодключения, СнятСПоддержки");
	ДоступнаЗагрузкаССайтаИТС = 
		ДрайверОборудованияРеквизиты.СпособПодключения = Перечисления.СпособПодключенияДрайвера.ИзИнформационнойБазы 
		И НЕ ДрайверОборудованияРеквизиты.СнятСПоддержки;
	Элементы.ФормаЗагрузитьССайтаИТС.Видимость = 
		ДоступнаЗагрузкаССайтаИТС 
		И ОбщегоНазначенияБПО.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ПолучениеВнешнихКомпонент");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	СозданиеНовогоЭлемента = Ложь;
	
	Элементы.ПараметрыПодключения.Видимость = НЕ СозданиеНовогоЭлемента;
	
	ОбновитьИнтерфейсФормы();
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияПослеЗаписи(Объект, ЭтотОбъект, ПараметрыЗаписи);
	
	// Вызов БСП
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияБПОКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// Конец Вызов БСП
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МенеджерОборудованияВызовСервераПереопределяемый.ЭкземплярОборудованияОбработкаПроверкиЗаполненияНаСервере(Объект, ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если Модифицированность Или СозданиеНовогоЭлемента Тогда
			Если Записать() Тогда
				ПроверкаПараметров = Ложь;
				Закрыть();
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			ПроверкаПараметров = Ложь;
			Закрыть();
		КонецЕсли;
		МенеджерОборудованияКлиент.ВыполнитьНастройкуОборудования(Объект.Ссылка);
	Иначе
		ПроверкаПараметров = Ложь;
		Закрыть();
	КонецЕсли;   
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		Если ПроверкаПараметров И Не ЗавершениеРаботы Тогда
			Отказ = Истина;
			Текст = НСтр("ru = 'Для использования устройства необходимо настроить параметры подключения.
			|Перейти к настройке параметров подключения?';
			|en = 'To use the device, set up connection parameters.
			|Go to connection parameter setup?'");
			Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение",  ЭтотОбъект);
			ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
		МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияПередЗакрытием(Объект, ЭтотОбъект, Отказ, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияОбработкаНавигационнойСсылки(Объект, ЭтотОбъект, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "БПО.ЗагруженАрхивЛоговДрайвера" Тогда
		ОбновитьИнформациюОЛогахДрайвераНаСервере();
		ОбновитьИнформациюОЛогахДрайвера();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ТипОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если Объект.ТипОборудования <> ВыбранноеЗначение Тогда
		Объект.ТипОборудования = ВыбранноеЗначение;
		Модифицированность = Истина;
		ЗаполнитьСписокДрайверов();
		Элементы.ФормаЗагрузитьССайтаИТС.Доступность = Ложь;
	Иначе
		Элементы.ФормаЗагрузитьССайтаИТС.Доступность = ЗначениеЗаполнено(Объект.ДрайверОборудования);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ОбновитьИнтерфейсФормы();
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияТипОборудованияВыбор(Объект, ЭтотОбъект, ЭтотОбъект, Элемент, ВыбранноеЗначение);
	
	Если Элементы.Организация.Видимость <> ОрганизацияВидимость Тогда
		УстановитьВидимостьОрганизацииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДрайверОборудованияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Объект.ДрайверОборудования Тогда
		Объект.Наименование = "'" + Строка(ВыбранноеЗначение) + "'"
						+ ?(ПустаяСтрока(Строка(Объект.РабочееМесто)),
							"",
							" " + НСтр("ru = 'на';
										|en = 'to'") + " " + Строка(Объект.РабочееМесто));
	КонецЕсли;
	Элементы.ФормаЗагрузитьССайтаИТС.Доступность = ЗначениеЗаполнено(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура УстройствоИспользуетсяПриИзменении(Элемент)
	
	ВыбранноеОборудование = Объект.Ссылка;
	Если ВыбранноеОборудование.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.УстройствоИспользуется Тогда
		Возврат;
	КонецЕсли;

	ВыдаватьПредупреждение = Истина;
	ТекстПредупреждения = НСтр("ru = 'Оборудование %1 используется в приложении.
							   |Отключение может привести к некорректной работе с этим оборудованием.
							   |Продолжить выполнение операции?';
							   |en = 'The %1 peripheral is used in the application.
							   |Disabling it may lead to incorrect operation of this peripheral.
							   |Continue?'");
	ТекстПредупреждения = СтрШаблон(ТекстПредупреждения, ВыбранноеОборудование);
	
	ОпределитьВозможностьОтключенияОборудования(ВыбранноеОборудование, ВыдаватьПредупреждение, ТекстПредупреждения);
	
	Если ВыдаватьПредупреждение Тогда
		Если Ждать ВопросАсинх(ТекстПредупреждения, РежимДиалогаВопрос.ДаНет) = КодВозвратаДиалога.Нет Тогда
			Объект.УстройствоИспользуется = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогиДрайвераНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(СостояниеЗапросаАрхиваЛогаДрайвера.ДатаАрхива) Тогда
		СохранитьЛогиДрайвера(Неопределено);
	Иначе
		Если Объект.РабочееМесто = МенеджерОборудованияКлиент.РабочееМестоКлиента() Тогда
			ЗагрузитьЛогиДрайвера(Неопределено);
		Иначе
			ЗапроситьЛогиДрайвера(Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПараметрыПодключения(Команда)
	
	Если СозданиеНовогоЭлемента Или Модифицированность Тогда
		Если Записать() Тогда
			ПроверкаПараметров = Ложь;
			Закрыть();
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		ПроверкаПараметров = Ложь;
		Закрыть();
	КонецЕсли;
	
	Если Объект.ТипОборудования = ПредопределенноеЗначение("Перечисление.ТипыПодключаемогоОборудования.ОблачнаяККТ") Тогда
		МодульОблачныеКассы = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОблачныеКассыКлиент");
		МодульОблачныеКассы.НастройкаПодключения(Объект.Ссылка);
	Иначе
		МенеджерОборудованияКлиент.ВыполнитьНастройкуОборудования(Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрацияФискальногоНакопителя(Команда)
	
	ОперацияФискальногоНакопителя(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеПараметровРегистрацииФискальногоНакопителя(Команда)
	
	ОперацияФискальногоНакопителя(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеФискальногоНакопителя(Команда)
	
	ОперацияФискальногоНакопителя(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСайтИнструкцииНажатие(Элемент)
	
	АдресЗагрузки = "https://its.1c.ru/db/metod81#browse:13:-1:2115:2511";
	ОткрытьНавигационнуюСсылку(АдресЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеИнфоПисьмоОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	АдресЗагрузки = "https://its.1c.ru/bmk/envd_kkt";
	ОткрытьНавигационнуюСсылку(АдресЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеВТехническуюПоддержку(Команда)
	
	Если СозданиеНовогоЭлемента Или Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	ИдентификаторОборудования = Объект.Ссылка;
	Если МенеджерОборудованияВызовСервера.ДрайверПоставляетсяВКонфигурации(ИдентификаторОборудования) Тогда
		МодульСообщенияВСлужбуТехническойПоддержкиБПОКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("СообщенияВСлужбуТехническойПоддержкиБПОКлиент");
		ПараметрыДляСообщения = МодульСообщенияВСлужбуТехническойПоддержкиБПОКлиент.ПараметрыОтправкиСообщенияОбОшибке();
		ПараметрыДляСообщения.ИдентификаторОборудования = ИдентификаторОборудования;
		ПараметрыДляСообщения.ТекстОшибки = НСтр("ru = 'Информация для тех.поддержки';
												|en = 'Information for support'");
		ОписаниеОповещения = Новый ОписаниеОповещения("СообщениеВТехническуюПоддержкуЗавершение", ЭтотОбъект);
		МодульСообщенияВСлужбуТехническойПоддержкиБПОКлиент.НачатьОтправкуСообщенияОбОшибке(ОписаниеОповещения, ПараметрыДляСообщения);
	Иначе              
		ОбщегоНазначенияБПОКлиент.ПоказатьПредупреждениеДрайверНеСертифицирован();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СообщениеВТехническуюПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ПустаяСтрока(Результат.КодОшибки) Тогда
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(Результат.СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура ОперацииСОборудованием(Команда)
	
	МенеджерОборудованияКлиент.ОткрытьОперацииСОборудованием(Объект.Ссылка);
	
КонецПроцедуры

#Область ЗагрузкаЛоговДрайверов

&НаКлиенте
Асинх Процедура ЗагрузитьЛогиДрайвера(Команда)
	
	Доступность = Ложь;
	ЛогиДрайвера = НСтр("ru = 'Идет загрузка файлов...';
						|en = 'Importing files...'");
	Элементы.ЛогиДрайвера.Гиперссылка = Ложь;
	
	УстановитьРасширениеРаботыСфайлами = Истина;
	Результат = Ждать МенеджерОборудованияКлиент.ЗагрузитьАрхивЛоговДрайвераАсинх(
		Объект.Ссылка, 
		Объект.ДрайверОборудования,
		УстановитьРасширениеРаботыСфайлами);
		
	Элементы.ЛогиДрайвера.Гиперссылка = Истина;
	Доступность = Истина;
	Если Результат.Успешно Тогда
		Ждать ПредупреждениеАсинх(НСтр("ru = 'Файлы с логами драйвера успешно загружены.';
										|en = 'Files with driver logs are imported successfully.'"), , НСтр("ru = 'Успех';
																									|en = 'Success'"));
	Иначе
		Ждать ПредупреждениеАсинх(Результат.ТекстОшибки, , НСтр("ru = 'Ошибка';
																|en = 'Error'"));
		
	КонецЕсли;
	ОбновитьИнформациюОЛогахДрайвераНаСервере();
	ОбновитьИнформациюОЛогахДрайвера();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьЛогиДрайвера(Команда)
	
	Элементы.ЛогиДрайвера.Гиперссылка = Ложь;
	
	ЗапроситьЛогиДрайвераНаСервере(Объект.Ссылка);
	ОбновитьИнформациюОЛогахДрайвера();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура СохранитьЛогиДрайвера(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПередаваемыеФайлы = ПолучаемыеФайлыАрхиваЛоговДрайвераНаСервере(Объект.Ссылка);
	ПараметрыДиалогаПолученияФайлов = Новый ПараметрыДиалогаПолученияФайлов;
	ПараметрыДиалогаПолученияФайлов.Заголовок = НСтр("ru = 'Сохранение архива логов файла';
													|en = 'Save an archive of file logs'");
	ПараметрыПолученияАрхиваФайлов = Новый ПараметрыПолученияАрхиваФайлов(, РежимПолученияАрхиваФайлов.ПолучатьАрхивВсегда);
	
	Ждать ПолучитьФайлыССервераАсинх(ПередаваемыеФайлы, ПараметрыДиалогаПолученияФайлов, ПараметрыПолученияАрхиваФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьЛогиДрайвера(Команда)
	
	ОчиститьФайлЛоговДрайвераНаСервере();
	ОбновитьИнформациюОЛогахДрайвера();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьИнформациюОЛогахДрайвераНаСервере()
	
	ВремяОжиданияЗапроса = 60*10;
	СостояниеЗапросаАрхиваЛогаДрайвера = Справочники.ПодключаемоеОборудование.СостояниеЗапросаАрхиваЛоговДрайвера(Объект.Ссылка);
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если СостояниеЗапросаАрхиваЛогаДрайвера.Состояние = "Запрос"
			И ТекущаяДатаСеанса() - СостояниеЗапросаАрхиваЛогаДрайвера.ДатаЗапроса > ВремяОжиданияЗапроса Тогда
			Справочники.ПодключаемоеОборудование.ОчиститьАрхивЛоговДрайвера(Объект.Ссылка);
			СостояниеЗапросаАрхиваЛогаДрайвера = Справочники.ПодключаемоеОборудование.СостояниеЗапросаАрхиваЛоговДрайвера(Объект.Ссылка);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОЛогахДрайвера()
	ТекущееРабочееМесто = МенеджерОборудованияКлиент.РабочееМестоКлиента();
	
	Если ТекущееРабочееМесто = Объект.РабочееМесто Тогда
		Элементы.ЗагрузитьЛогиДрайвера.Видимость = Истина;
		Элементы.ЗапроситьЛогиДрайвера.Видимость = Ложь;
	Иначе
		Элементы.ЗагрузитьЛогиДрайвера.Видимость = Ложь;
		Элементы.ЗапроситьЛогиДрайвера.Видимость = Истина;
	КонецЕсли;
	
	ИспользуетсяБСП = ОбщегоНазначенияБПОКлиент.ИспользуетсяБСП();
	Если Не ИспользуетсяБСП Тогда
		Элементы.ЗапроситьЛогиДрайвера.Видимость = Ложь;
	КонецЕсли;
	
	Если СостояниеЗапросаАрхиваЛогаДрайвера.Состояние = "Получен" Тогда
		ДатаАрхива = СостояниеЗапросаАрхиваЛогаДрайвера.ДатаАрхива;
		ЛогиДрайвера = СтрШаблон(НСтр("ru = 'Загружены файлы с логами драйвера от %1.';
										|en = 'Files with driver logs dated %1 are imported successfully.'"), Формат(ДатаАрхива, ""));
		Элементы.ЛогиДрайвера.Гиперссылка = Истина;
		Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Истина;
	ИначеЕсли СостояниеЗапросаАрхиваЛогаДрайвера.Состояние = "Запрос" Тогда
		ДатаЗапроса = СостояниеЗапросаАрхиваЛогаДрайвера.ДатаЗапроса;
		ЛогиДрайвера = СтрШаблон(НСтр("ru = 'Архив логов драйвера запрошен %1.';
										|en = 'Driver log archive is requested on %1.'"), Формат(ДатаЗапроса, ""));
		Элементы.ЛогиДрайвера.Гиперссылка = Ложь;
		Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Ложь;
	ИначеЕсли СостояниеЗапросаАрхиваЛогаДрайвера.Состояние = "Отсутствует" Тогда
		Если ТекущееРабочееМесто = Объект.РабочееМесто Тогда
			Элементы.ЛогиДрайвера.Гиперссылка = Истина;
			ЛогиДрайвера = СтрШаблон(НСтр("ru = 'Загрузить файлы с логами драйвера в базу данных.';
											|en = 'Import files with driver logs to the database.'"));
			Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Ложь;
		Иначе
			Если ИспользуетсяБСП Тогда
				Элементы.ЛогиДрайвера.Гиперссылка = Истина;
				ЛогиДрайвера = СтрШаблон(НСтр("ru = 'Запросить файлы с логами драйвера с клиентского компьютера.';
												|en = 'Request files with driver logs from the client computer.'"));
				Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Ложь;
			Иначе
				Элементы.ЛогиДрайвера.Гиперссылка = Ложь;
				ЛогиДрайвера = СтрШаблон(НСтр("ru = 'Недоступен запрос файлов с логами драйвера с клиентского компьютера без БСП.';
												|en = 'Cannot request files with driver logs from a client computer without SSL.'"));
				Элементы.ГруппаУправлениеАрхивомЛоговДрайвера.Видимость = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если ОбщегоНазначенияБПОКлиент.ЭтоВебКлиент() Тогда
		Элементы.СохранитьЛогиДрайвера.Видимость = Ложь;
	Иначе
		Элементы.СохранитьЛогиДрайвера.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнтерфейсФормы();
	
	ТипОборудованияККТ         = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ККТ;
	ТипОборудованияФР          = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ФискальныйРегистратор;
	ТипОборудованияПЧ          = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ПринтерЧеков;
	ТипОборудованияОблачнаяККТ = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
	
	ОрганизацияВидимость = ТипОборудованияККТ Или ТипОборудованияФР Или ТипОборудованияПЧ Или ТипОборудованияОблачнаяККТ;
	
	Элементы.ФискальныеДанные.Видимость = ТипОборудованияККТ И НЕ СозданиеНовогоЭлемента;
	
	Если Элементы.ФискальныеДанные.Видимость Или Элементы.ОграничениеДоступа.Видимость Тогда
		Элементы.Закладки.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху       
	Иначе
		Элементы.Закладки.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	Элементы.Организация.Видимость = ОрганизацияВидимость; 
	
	Если Объект.ПометкаУдаления Или Не Объект.УстройствоИспользуется Тогда
		Элементы.ПараметрыПодключения.Доступность = Ложь;     
	Иначе
		Элементы.ПараметрыПодключения.Доступность = Истина;     
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияФискальногоНакопителя_Завершение(РезультатВыполнения, Параметры) Экспорт
	
	ОчиститьСообщения();
	
	Если РезультатВыполнения.Результат Тогда
		ТекстСообщения = НСтр("ru = 'Операция завершена.';
								|en = 'Operation completed.'");
	Иначе
		ТекстСообщения = РезультатВыполнения.ОписаниеОшибки;
	КонецЕсли;
	
	ОбщегоНазначенияБПОКлиент.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияФискальногоНакопителя_Продолжить(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения <> Неопределено 
		И ТипЗнч(РезультатВыполнения) = Тип("Структура")
		И ОбщегоНазначенияБПОКлиент.ИспользуетсяЧекопечатающиеУстройства() Тогда
		
		ФискальноеУстройство = Объект.Ссылка;
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ОперацияФискальногоНакопителя_Завершение", ЭтотОбъект);
		МодульОборудованиеЧекопечатающиеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройстваКлиент");
		МодульОборудованиеЧекопечатающиеУстройстваКлиент.НачатьОперациюФНДляФискальногоУстройства(ОповещениеПриЗавершении,
			УникальныйИдентификатор, ФискальноеУстройство, РезультатВыполнения); 
			
	КонецЕсли;
	
	ЭтотОбъект.Прочитать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияФискальногоНакопителя(ТипОперации)
	
#Если ВебКлиент Тогда
	ПоказатьПредупреждение(, НСтр("ru = 'Данный функционал доступен только в режиме тонкого и толстого клиента.';
									|en = 'This functionality is available only in the thin and thick client mode.'"));
	Возврат;
#КонецЕсли
	
	ФискальноеУстройство = Объект.Ссылка;
	Если Не ЗначениеЗаполнено(ФискальноеУстройство) Тогда
		Возврат;
	КонецЕсли;               
	
	МодульОборудованиеЧекопечатающиеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройстваКлиент");
	ПараметрыОткрытия = МодульОборудованиеЧекопечатающиеУстройстваКлиент.ПараметрыФормаНастройкиРегистрацииККТ();
	ПараметрыОткрытия.ФискальноеУстройство = ФискальноеУстройство;
	ПараметрыОткрытия.Организация = Объект.Организация;
	ПараметрыОткрытия.ТипОперации = ТипОперации; 
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ОперацияФискальногоНакопителя_Продолжить", ЭтотОбъект);
	МодульОборудованиеЧекопечатающиеУстройстваКлиент.ОткрытьФормуНастройкиРегистрацииККТ(ОповещениеПриЗавершении, ПараметрыОткрытия);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДрайверов()
	
	Если ОбщегоНазначенияБПО.ИспользуютсяОблачныеККТ() Тогда
		ТипОборудованияОблачнаяККТ = Объект.ТипОборудования = Перечисления.ТипыПодключаемогоОборудования.ОблачнаяККТ;
		
		Элементы.ДрайверОборудования.СписокВыбора.Очистить();
		Элементы.ДрайверОборудования.Видимость = НЕ ТипОборудованияОблачнаяККТ;
		
		// ++ Локализация
		Если ТипОборудованияОблачнаяККТ Тогда
			Если ЗначениеЗаполнено(Справочники.ДрайверыОборудования.ПредопределенныйЭлемент("Справочник.ДрайверыОборудования.ОбработчикОблачныхККТ")) Тогда
				Объект.ДрайверОборудования = ПредопределенноеЗначение("Справочник.ДрайверыОборудования.ОбработчикОблачныхККТ");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		// -- Локализация
	КонецЕсли;
	
	ДрайвераОборудования = МенеджерОборудования.ДрайверыПоТипуОборудования(Объект.ТипОборудования);
	Для Каждого ДрайверОборудования Из ДрайвераОборудования Цикл
		Элементы.ДрайверОборудования.СписокВыбора.Добавить(ДрайверОборудования.Значение, ДрайверОборудования.Представление);
	КонецЦикла;
	
	Объект.ДрайверОборудования = ПредопределенноеЗначение("Справочник.ДрайверыОборудования.ПустаяСсылка");
	Объект.Наименование = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНавигационнуюСсылку(НавигационнаяСсылка, Знач Оповещение = Неопределено)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		// Особенность платформы: ПерейтиПоНавигационнойСсылке не доступен в толстом клиенте обычного приложения.
		ОповещениеПриЗавершении = Новый ОписаниеОповещения();
		НачатьЗапускПриложения(ОповещениеПриЗавершении, НавигационнаяСсылка); // АПК:534 передаются фиксированные строки
	#Иначе
		ПерейтиПоНавигационнойСсылке(НавигационнаяСсылка); // АПК:534 передаются фиксированные строки
	#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьОрганизацииНаСервере()
	
	Элементы.Организация.Видимость = ОрганизацияВидимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьССайтаИТС(Команда)
	
	// Вставить содержимое обработчика.
	Если СозданиеНовогоЭлемента Или Модифицированность Тогда
		Записать();
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьССайтаИТСЗавершение", ЭтотОбъект);
	МенеджерОборудованияКлиент.УстановитьДрайверОборудования(Оповещение, Объект.ДрайверОборудования);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьССайтаИТСЗавершение(РезультатВыполнения, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда
		Заголовок = НСтр("ru = 'Компонента загружена.';
						|en = 'Add-in is imported.'");
		Текст     = СтрШаблон(НСтр("ru = 'Компонента %1 успешно загружена и установлена.';
									|en = 'The %1 add-in is successfully imported and installed.'"),
						РезультатВыполнения.ИдентификаторУстройства);
	Иначе
		Заголовок = НСтр("ru = 'Ошибка загрузки компоненты.';
						|en = 'Add-in import error.'");
		Текст     = СтрШаблон(НСтр("ru = 'Компонента %1 не загружена, по причине:
						|%2.';
						|en = 'The %1 add-in is not imported. Reason:
						|%2.'"),
						РезультатВыполнения.ИдентификаторУстройства,
						РезультатВыполнения.ОписаниеОшибки);
	КонецЕсли;
	ПоказатьПредупреждение(,Текст,,Заголовок);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОпределитьВозможностьОтключенияОборудования(ПодключаемоеОборудование, ВыдаватьПредупреждение, ТекстПредупреждения)

	СтандартнаяОбработка = Истина;
	МенеджерОборудованияВызовСервераПереопределяемый.ОпределитьВозможностьОтключенияОборудования(
		ПодключаемоеОборудование, ВыдаватьПредупреждение, ТекстПредупреждения, СтандартнаяОбработка);
	Если СтандартнаяОбработка Тогда
		ВыдаватьПредупреждение = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьДрайверВСправочникВнешнихКомпонентИзМакета(СсылкаНаДрайвер)
	
	Если Не ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.ВнешниеКомпоненты") Тогда
		Возврат
	КонецЕсли;

	// ++ НеМобильноеПриложение
	
	РеквизитыДрайвера = ОбщегоНазначенияБПО.ЗначенияРеквизитовОбъекта(СсылкаНаДрайвер, 
		"ИдентификаторОбъекта, СпособПодключения, Наименование, ИмяПредопределенныхДанных, ИмяМакетаДрайвера, ВерсияДрайвера");
	Если РеквизитыДрайвера.СпособПодключения = Перечисления.СпособПодключенияДрайвера.ИзМакета Тогда
	
		МодульВнешниеКомпонентыСервер = ОбщегоНазначенияБПО.ОбщийМодуль("ВнешниеКомпонентыСервер");
		
		ЗагруженныеВнешниеКомпоненты = МодульВнешниеКомпонентыСервер.ИспользуемыеКомпоненты("ДляОбновления");
		НайденнаяСтрока = ЗагруженныеВнешниеКомпоненты.Найти(РеквизитыДрайвера.ИдентификаторОбъекта, "Идентификатор");
		Если НайденнаяСтрока = Неопределено Тогда
			
			МакетДоступен = Ложь;
			ИмяМакетаДрайвера = РеквизитыДрайвера.ИмяМакетаДрайвера;
			ИмяДрайвера = РеквизитыДрайвера.ИмяПредопределенныхДанных;
			МенеджерОборудования.ЗаполнитьДанныеМакетов(ИмяДрайвера, ИмяМакетаДрайвера, МакетДоступен, Неопределено);
			Если МакетДоступен Тогда
				
				ДанныеДляДобавления = МенеджерОборудования.НовыеДанныеДляДобавленияВСправочникВнешнихКомпонент();
				ДанныеДляДобавления.Идентификатор = РеквизитыДрайвера.ИдентификаторОбъекта;
				ДанныеДляДобавления.ИмяМакета = СтрЗаменить(ИмяМакетаДрайвера, "ОбщийМакет.", "");
				ДанныеДляДобавления.Наименование = РеквизитыДрайвера.Наименование;
				ДанныеДляДобавления.Версия = РеквизитыДрайвера.ВерсияДрайвера;
				ДанныеДляДобавления.Ссылка = СсылкаНаДрайвер;
				
				СтандартнаяОбработка = Истина;
				МенеджерОборудованияВызовСервераПереопределяемый.ПриЗагрузкеДрайвераВСправочникВнешниеКомпонентыПриДобавленииОборудования(ДанныеДляДобавления, СтандартнаяОбработка);
				Если Не СтандартнаяОбработка Тогда
					Возврат;
				КонецЕсли;
				
				ДрайверыДляДобавления = Новый Массив();
				ДрайверыДляДобавления.Добавить(ДанныеДляДобавления);
				МенеджерОборудования.ДобавитьВСправочникВнешнихКомпонентИзМакетаСлужебный(ДрайверыДляДобавления);
				
			КонецЕсли;
		Иначе
			МенеджерОборудования.УстановитьСпособПодключенияДрайвера(СсылкаНаДрайвер, Перечисления.СпособПодключенияДрайвера.ИзИнформационнойБазы);
		КонецЕсли;
	КонецЕсли;
	
	// -- НеМобильноеПриложение
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОборудованияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый СписокЗначений();
	ЗаполнитьДоступныеТипыОборудования(ДанныеВыбора, Объект.ТипПодключения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДоступныеТипыОборудования(ДанныеВыбора, ТипПодключения)
	
	Если ТипПодключения = Перечисления.ТипыПодключенияОборудования.ОбщийДоступ Тогда
		ДоступныеТипыОборудования = МенеджерОборудования.ДоступныеТипыОбщедоступногоОборудования();
	Иначе
		ДоступныеТипыОборудования = МенеджерОборудования.ДоступныеТипыОборудования();
	КонецЕсли;
	Для Каждого Тип Из ДоступныеТипыОборудования Цикл
		ДанныеВыбора.Добавить(Тип);
	КонецЦикла;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	// Вызов БСП
	Если ОбщегоНазначенияБПОКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	КонецЕсли;
	// Конец Вызов БСП
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры)
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

&НаСервере
Процедура ОбновитьПараметрыРегистрации()
	
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодульОборудованиеЧекопечатающиеУстройства = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства");
		МодульОборудованиеЧекопечатающиеУстройства.ОбновитьПараметрыРегистрацииККТ(ПараметрыРегистрацииККТ, Объект.ПараметрыРегистрации);
	КонецЕсли;             
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	// Вызов БСП
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКоманды = ОбщегоНазначенияБПО.ОбщийМодуль("ПодключаемыеКоманды");
		МодульПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	КонецЕсли;
	// Конец Вызов БСП
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	// Вызов БСП
	Если ОбщегоНазначенияБПОКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
		МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец Вызов БСП
	
КонецПроцедуры           

&НаКлиенте
Процедура ЗапроситьДанныеККТ(Команда)
	
	ФискальноеУстройство = Объект.Ссылка;
	Если Не ЗначениеЗаполнено(ФискальноеУстройство) Тогда
		Возврат;
	КонецЕсли;               
	
	ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗапроситьДанныеККТ_Продолжить", ЭтотОбъект);
	МодульОборудованиеЧекопечатающиеУстройстваКлиент = ОбщегоНазначенияБПОКлиент.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройстваКлиент");
	МодульОборудованиеЧекопечатающиеУстройстваКлиент.НачатьПолучениеПараметровФискальногоУстройства(ОповещениеПриЗавершении, УникальныйИдентификатор, ФискальноеУстройство);
	
КонецПроцедуры         

&НаКлиенте
Процедура ЗапроситьДанныеККТ_Продолжить(РезультатВыполнения, Параметры) Экспорт
	
	Если РезультатВыполнения.Результат Тогда 
		ЭтотОбъект.Прочитать();
		ОбновитьПараметрыРегистрации();    
	Иначе
		ОбщегоНазначенияБПОКлиент.СообщитьПользователю(РезультатВыполнения.ОписаниеОшибки);
	КонецЕсли;
	
КонецПроцедуры

#Область ЗагрузкаЛоговДрайверов

&НаСервереБезКонтекста
Функция ПолучаемыеФайлыАрхиваЛоговДрайвераНаСервере(ПодключаемоеОборудование)
	
	АрхивЛогов = Справочники.ПодключаемоеОборудование.АрхивЛоговДрайвера(ПодключаемоеОборудование);
	
	ПередаваемыеФайлы = Новый Массив;
	
	Если АрхивЛогов.ЭтоАрхив Тогда
		Адрес = ПоместитьВоВременноеХранилище(АрхивЛогов.ДанныеАрхива);
		ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
		ПередаваемыйФайл.Хранение = Адрес;
		ПередаваемыйФайл.Имя = СтрШаблон("logs-%1.zip", Формат(АрхивЛогов.ДатаАрхива, "ДФ=dd-MM-yyyy-hh-mm-ss"));
		ПередаваемыеФайлы.Добавить(ПередаваемыйФайл);
	Иначе
		Для Каждого Файл Из АрхивЛогов.ДанныеАрхива Цикл
			Адрес = ПоместитьВоВременноеХранилище(Файл.Данные);
			ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
			ПередаваемыйФайл.Хранение = Адрес;
			ПередаваемыйФайл.Имя = Файл.Имя;
			ПередаваемыеФайлы.Добавить(ПередаваемыйФайл);
		КонецЦикла;
	КонецЕсли;
	
	// ++ НеМобильноеПриложение
	ДанныеОпераций = РегистрыСведений.ОперацииСПодключаемымОборудованием.АрхивОперацийОборудования(ПодключаемоеОборудование);
	Адрес = ПоместитьВоВременноеХранилище(ДанныеОпераций.ДанныеАрхива);
	ПередаваемыйФайл = Новый ОписаниеПередаваемогоФайла;
	ПередаваемыйФайл.Хранение = Адрес;
	ПередаваемыйФайл.Имя = СтрШаблон("operations-%1.txt", Формат(ДанныеОпераций.ДатаАрхива, "ДФ=dd-MM-yyyy-hh-mm-ss"));
	ПередаваемыеФайлы.Добавить(ПередаваемыйФайл);
	// -- НеМобильноеПриложение
	
	Возврат ПередаваемыеФайлы;
	
КонецФункции

&НаСервере
Процедура ОчиститьФайлЛоговДрайвераНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Справочники.ПодключаемоеОборудование.ОчиститьАрхивЛоговДрайвера(Объект.Ссылка);
		ОбновитьИнформациюОЛогахДрайвераНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗапроситьЛогиДрайвераНаСервере(ПодключаемоеОборудование)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Справочники.ПодключаемоеОборудование.ОтправитьЗапросАрхиваЛоговДрайвера(ПодключаемоеОборудование);
		ОбновитьИнформациюОЛогахДрайвераНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветНаЗапросАрхиваЛоговДрайвера(ДанныеОтвета, ДополнительныеПараметры) Экспорт
	
	ОбработатьЗавершениеОжиданияЗапросаЛогов(ДанныеОтвета);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗавершениеОжиданияЗапросаЛогов(ДанныеОтвета = Неопределено)
	
	ДополнительныеПараметры = Новый Структура("Текст, Заголовок", "", "");
	ПриНажатииОповещенияПользователя = Новый ОписаниеОповещения("ПриНажатииОповещенияПользователя", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ДанныеОтвета = Неопределено Тогда
		ДополнительныеПараметры.Заголовок = НСтр("ru = 'Ошибка.';
												|en = 'Error.'");
		ДополнительныеПараметры.Текст = НСтр("ru = 'Нет ответа от клиента';
											|en = 'No response from the client'");
		ПоказатьОповещениеПользователя(
			ДополнительныеПараметры.Заголовок, 
			ПриНажатииОповещенияПользователя,
			ДополнительныеПараметры.Текст,
			БиблиотекаКартинок.ОформлениеЗнакКрест, 
			СтатусОповещенияПользователя.Важное);
		
	ИначеЕсли ДанныеОтвета.Успешно Тогда
		ДополнительныеПараметры.Заголовок = НСтр("ru = 'Успех.';
												|en = 'Success.'");
		ДополнительныеПараметры.Текст = НСтр("ru = 'Архив логов успешно получен.';
											|en = 'The log archive is successfully received.'");
		ПоказатьОповещениеПользователя(
			ДополнительныеПараметры.Заголовок, 
			ПриНажатииОповещенияПользователя,
			ДополнительныеПараметры.Текст,
			БиблиотекаКартинок.ОформлениеЗнакФлажок, 
			СтатусОповещенияПользователя.Важное);
		
	Иначе
		ДополнительныеПараметры.Заголовок = НСтр("ru = 'Ошибка.';
												|en = 'Error.'");
		ДополнительныеПараметры.Текст = ДанныеОтвета.ТекстОшибки;
		ПоказатьОповещениеПользователя(
			ДополнительныеПараметры.Заголовок, 
			ПриНажатииОповещенияПользователя,
			ДополнительныеПараметры.Текст,
			БиблиотекаКартинок.ОформлениеЗнакКрест, 
			СтатусОповещенияПользователя.Важное);
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("ЗавершениеОжиданияЗапросаЛогов");
	ОбщегоНазначенияБПОКлиент.УведомленияКлиента().
		ОтключитьОбработчик(
			МенеджерОборудованияКлиентСервер.КлючОтветаНаЗапросАрхиваЛоговДрайвера());
	Элементы.ГруппаЛогДрайвера.Доступность = Истина;
	ЭтаФорма.Доступность = Истина;
	
	ОбновитьИнформациюОЛогахДрайвераНаСервере();
	ОбновитьИнформациюОЛогахДрайвера();
		
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОжиданияЗапросаЛогов() Экспорт
	
	ОбработатьЗавершениеОжиданияЗапросаЛогов();
	
КонецПроцедуры


&НаКлиенте
Процедура ПриНажатииОповещенияПользователя(ДополнительныеПараметры) Экспорт
	
	ПредупреждениеАсинх(ДополнительныеПараметры.Текст,, ДополнительныеПараметры.Заголовок);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ПриИзмененииЭлементаФормы(Элемент)
	
	МенеджерОборудованияКлиентПереопределяемый.ЭкземплярОборудованияПриИзмененииЭлементаФормы(Объект, ЭтотОбъект, Элемент);
	
КонецПроцедуры

#КонецОбласти