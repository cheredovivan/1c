
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	Объект.ФискальныеЧекиФормируетПлатежнаяСистема = ОписаниеТиповБулево.ПривестиЗначение(Объект.ФискальныеЧекиФормируетПлатежнаяСистема);
	ФискальныеЧекиФормируетПлатежнаяСистема = Объект.ФискальныеЧекиФормируетПлатежнаяСистема;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
	УстановитьДоступностьЭлементовНаСервере();
	
	// Обработчик подсистемы "Свойства"
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Объект", Объект);
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтаФорма, ДополнительныеПараметры);
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
	// СтандартныеПодсистемы.РаботаСФайлами
	ПараметрыГиперссылки = РаботаСФайлами.ГиперссылкаФайлов();
	ПараметрыГиперссылки.Размещение = "КоманднаяПанель";
	РаботаСФайлами.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыГиперссылки);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// СтандартныеПодсистемы.РаботаСФайлами
	РаботаСФайламиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	// Конец СтандартныеПодсистемы.РаботаСФайлами
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();
	
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
		
	ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии = Ложь;
	Если Не Объект.ВестиУчетКомиссииВоВзаиморасчетах Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКомиссии");
		ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии = Истина;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДоговорКомиссии) Тогда
		ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии = Истина;
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	Если ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии Тогда
		ДокументОбъект = РеквизитФормыВЗначение("Объект");
		ДокументОбъект.ДополнительныеСвойства.Вставить("ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии", Истина);
		
		Если Не ДокументОбъект.ПроверитьЗаполнение() Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ПроверяемыйРеквизитОбъект = ПроверяемыеРеквизиты.Найти("Объект");
		Если ПроверяемыйРеквизитОбъект <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ПроверяемыйРеквизитОбъект);
		КонецЕсли;
	КонецЕсли;
		
	//++ НЕ УТ
	НастройкаСчетовУчетаСервер.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	//-- НЕ УТ
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// Подсистема "Свойства"
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтаФорма, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
	Если ИмяСобытия = "Запись_ЭквайринговыеТерминалы" Тогда
		СформироватьНадписьЭквайринговыеТерминалы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Обработчик подсистемы "Свойства"
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);

	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец ИнтеграцияС1СДокументооборотом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// подсистема запрета редактирования ключевых реквизитов объектов	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	СобытияФорм.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды") Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	//++ НЕ УТ
	НастройкаСчетовУчетаСервер.ПриЗаписиОбъектаНастройкиСчетовУчета(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	ПартнерПриИзмененииСервер(
		Объект.Партнер, Объект.Контрагент, Объект.БанковскийСчетКонтрагента, Объект.КонтактноеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	КонтрагентПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПроведенияПлатежаПриИзменении(Элемент)

	Объект.ИспользуютсяЭквайринговыеТерминалы = Ложь;
	Если Объект.СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.ИнтернетЭквайринг") Тогда
		НастроитьЗависимыеЭлементыФормы("СпособПроведенияПлатежа");
	Иначе
		СпособПроведенияПлатежаПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СпособПроведенияПлатежаПриИзмененииНаСервере()
	
	Объект.ИспользуютсяЭквайринговыеТерминалы = Ложь;
	Объект.ФискальныеЧекиФормируетПлатежнаяСистема = Ложь;
	Если Объект.СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.ЭквайринговыйТерминал") Тогда
		Объект.ИспользуютсяЭквайринговыеТерминалы = Истина;
		СформироватьНадписьЭквайринговыеТерминалы();
	ИначеЕсли Объект.СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей") Тогда
		СформироватьНадписьНастройкаПодключенияПлатежнойСистемы();
	КонецЕсли;
	НастроитьЗависимыеЭлементыФормыНаСервере("СпособПроведенияПлатежа");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьЭквайринговыеТерминалыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	ОчиститьСообщения();
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "СписокТерминалов" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыФормы = Новый Структура;
		Отбор = Новый Структура;
		Отбор.Вставить("Владелец", Объект.Ссылка);
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Справочник.ЭквайринговыеТерминалы.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "Добавить" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьЭквайринговыйТерминал", ЭтотОбъект);
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, "");
		Иначе
			ТекстВопроса = Нстр("ru = 'Перед добавлением эквайрингового терминала договор будет записан, продолжить?';
								|en = 'The contract will be written before adding a POS terminal. Continue?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭквайринговыйТерминал(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ЭлементЗаписан = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ЭлементЗаписан = Записать();
	Иначе
		ЭлементЗаписан = Истина;
	КонецЕсли;
	
	Если ЭлементЗаписан Тогда
		ПараметрыФормы = Новый Структура;
		Отбор = Новый Структура;
		Отбор.Вставить("Владелец", Объект.Ссылка);
		Отбор.Вставить("РазрешитьПлатежиБезУказанияЗаявок", Объект.РазрешитьПлатежиБезУказанияЗаявок);
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Отбор);
		
		ОткрытьФорму("Справочник.ЭквайринговыеТерминалы.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНастройкаПодключенияПлатежнойСистемыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	НадписьНастройкаПодключенияПлатежнойСистемыОбработкаНавигационнойСсылкиЛокализация(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФиксированнаяСтавкаКомиссииПриИзменении(Элемент)
	
	Объект.ФиксированнаяСтавкаКомиссии = Булево(ФиксированнаяСтавкаКомиссии);
	
	НастроитьЗависимыеЭлементыФормы("ФиксированнаяСтавкаКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияКомиссииПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы("СпособОтраженияКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияКомиссииВОтчетеБанкаПриИзменении(Элемент)
	
	СпособОтраженияКомиссииПриЗачислении = Неопределено;
	СпособОтраженияКомиссииВоВремяТранзакции = Неопределено;
	СпособОтраженияКомиссииВОтдельномСписании = Неопределено;
	
	Объект.СпособОтраженияКомиссии = ПредопределенноеЗначение("Перечисление.СпособыОтраженияЭквайринговойКомиссии.ВОтчете");
	
	НастроитьЗависимыеЭлементыФормы("СпособОтраженияКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияКомиссииПриЗачисленииПриИзменении(Элемент)
	
	СпособОтраженияКомиссииВОтчетеБанка = Неопределено;
	СпособОтраженияКомиссииВоВремяТранзакции = Неопределено;
	СпособОтраженияКомиссииВОтдельномСписании = Неопределено;
	
	Объект.СпособОтраженияКомиссии = ПредопределенноеЗначение("Перечисление.СпособыОтраженияЭквайринговойКомиссии.ПриЗачислении");
	
	НастроитьЗависимыеЭлементыФормы("СпособОтраженияКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияКомиссииВоВремяТранзакцииПриИзменении(Элемент)
	
	СпособОтраженияКомиссииВОтчетеБанка = Неопределено;
	СпособОтраженияКомиссииПриЗачислении = Неопределено;
	СпособОтраженияКомиссииВОтдельномСписании = Неопределено;
	
	Объект.СпособОтраженияКомиссии = ПредопределенноеЗначение("Перечисление.СпособыОтраженияЭквайринговойКомиссии.ВоВремяТранзакции");
	
	НастроитьЗависимыеЭлементыФормы("СпособОтраженияКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияКомиссииВОтдельномСписанииПриИзменении(Элемент)
	
	СпособОтраженияКомиссииВОтчетеБанка = Неопределено;
	СпособОтраженияКомиссииПриЗачислении = Неопределено;
	СпособОтраженияКомиссииВоВремяТранзакции = Неопределено;
	
	Объект.СпособОтраженияКомиссии = ПредопределенноеЗначение("Перечисление.СпособыОтраженияЭквайринговойКомиссии.ВОтдельномСписании");
	
	НастроитьЗависимыеЭлементыФормы("СпособОтраженияКомиссии");
	
КонецПроцедуры

&НаКлиенте
Процедура ВестиУчетКомиссииВоВзаиморасчетахПереключательПриИзменении(Элемент)
	
	Объект.ВестиУчетКомиссииВоВзаиморасчетах = ВестиУчетКомиссииВоВзаиморасчетахПереключатель;
	
	Если Не Объект.ВестиУчетКомиссииВоВзаиморасчетах Тогда
		ДоговорКомиссии = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
		Объект.ОбъектРасчетовКомиссии = ПредопределенноеЗначение("Справочник.ОбъектыРасчетов.ПустаяСсылка");
	КонецЕсли;
	НастроитьЗависимыеЭлементыФормы("ВестиУчетКомиссииВоВзаиморасчетах");
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссииНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Организация"" не заполнено.';
														|en = 'The ""Company"" field is required.'"), , "Организация",
			"Объект", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Эквайер"" не заполнено.';
														|en = '""Acquirer"" is required.'"), , "Партнер",
			"Объект", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Контрагент"" не заполнено';
														|en = 'The ""Counterparty"" field is required'"), , "Контрагент",
			"Объект", Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.БанковскийСчет) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Поле ""Банковский счет для зачисления"" не заполнено';
														|en = 'The ""Bank account for payouts"" field is not filled in'"), ,
			"БанковскийСчет", "Объект", Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияОтбора = Новый Структура;
	ЗначенияОтбора.Вставить("Организация", Объект.Организация);
	ЗначенияОтбора.Вставить("Партнер", Объект.Партнер);
	ЗначенияОтбора.Вставить("Контрагент", Объект.Контрагент);
	ЗначенияОтбора.Вставить("ВалютаВзаиморасчетов", ВалютаБанковскогоСчета(Объект.БанковскийСчет));
	ЗначенияОтбора.Вставить("ПорядокРасчетов", ПредопределенноеЗначение("Перечисление.ПорядокРасчетов.ПоДоговорамКонтрагентов"));
	
	СтруктураПараметровВыбора = Новый Структура;
	СтруктураПараметровВыбора.Вставить("РежимВыбора", Истина);
	СтруктураПараметровВыбора.Вставить("ТипыДоговоров", ВзаиморасчетыКлиентСервер.ТипыДоговоровСПоставщиком());
	СтруктураПараметровВыбора.Вставить("Отбор", ЗначенияОтбора);
	
	ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора", СтруктураПараметровВыбора, Элемент, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКомиссииПриИзменении(Элемент)
	
	ДоговорКомиссииПриИзмененииНаСервере();
	
КонецПроцедуры

// Параметры:
// 	Элемент - ПолеФормы - 
&НаКлиенте
Процедура Подключаемый_ПриИзменении_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПриИзменении.Свойство(Элемент.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПриИзменении(Элемент);
	КонецЕсли;

КонецПроцедуры

// Параметры:
// 	Команда - КомандаФормы - 
&НаКлиенте
Процедура Подключаемый_Команда_УстановитьДоступностьЭлементовПоСтатусуСервер(Команда)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.Команды.Свойство(Команда.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеКоманды(Команда);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Элемент - ПолеФормы - 
&НаКлиенте
Процедура Подключаемый_ПередНачаломИзменения_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередНачаломИзменения.Свойство(Элемент.Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломИзменения(Элемент, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Элемент - ПолеФормы - 
&НаКлиенте
Процедура Подключаемый_ПередУдалением_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Имя = Элемент.Имя;
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередУдалением.Свойство(Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередУдалением(Элемент, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
// 	Элемент - ПолеФормы - 
&НаКлиенте
Процедура Подключаемый_ПередНачаломДобавления_УстановитьДоступностьЭлементовПоСтатусуСервер(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ОбщегоНазначенияУТКлиент.ПриДействииСЭлементомЗависящимОтСтатуса(ЭтаФорма) Тогда
		Возврат;
	КонецЕсли;
	
	Имя = Элемент.Имя;
	Если СтруктураДействийКонтрольНеСогласованныхИзменений.ПередНачаломДобавления.Свойство(Имя) Тогда
		КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииРеквизитаЗависящегоОтСтатуса()
	
	УстановитьДоступностьЭлементовПоСтатусуСервер();
	ОбщегоНазначенияУТКлиент.ПослеИзмененияРеквизитаЗависящегоОтСтатуса(ЭтаФорма);
	
КонецПроцедуры

#Область КонтрольНесогласованныхИзменений

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПриИзменении(Элемент)
	
	Если Элемент.Имя = "Партнер" Тогда
		ПартнерПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Контрагент" Тогда
		КонтрагентПриИзменении(Элемент);
	ИначеЕсли Элемент.Имя = "Организация" Тогда
		ОрганизацияПриИзменении(Элемент);
	Иначе
		ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеКоманды(Команда)
	
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломИзменения(Элемент, Отказ)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередУдалением(Элемент, Отказ)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура КонтрольНеСогласованныхИзмененийОбработатьСобытиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	ОбщегоНазначенияУТКлиент.КонтрольНеСогласованныхИзмененийВызватьИсключение(ЭтаФорма, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ФискальныеЧекиФормируетПлатежнаяСистемаПриИзменении(Элемент)
	
	Объект.ФискальныеЧекиФормируетПлатежнаяСистема = ФискальныеЧекиФормируетПлатежнаяСистема;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ЗаполнитьДоговорОбъектРасчетовКомиссии();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_КомандаПанелиПрисоединенныхФайлов(Команда)
	 РаботаСФайламиКлиент.КомандаУправленияПрисоединеннымиФайлами(ЭтотОбъект, Команда);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраПроверкаПеретаскивания(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраПеретаскивание(ЭтотОбъект, Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

// СтандартныеПодсистемы.РаботаСФайлами
&НаКлиенте
Процедура Подключаемый_ПолеПредпросмотраНажатие(Элемент, СтандартнаяОбработка)
	 РаботаСФайламиКлиент.ПолеПредпросмотраНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.РаботаСФайлами

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтаФорма);
	
КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// СтандартныеПодсистемы.Свойства

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	СпособОтраженияКомиссииВОтчетеБанка = Объект.СпособОтраженияКомиссии;
	СпособОтраженияКомиссииПриЗачислении = Объект.СпособОтраженияКомиссии;
	СпособОтраженияКомиссииВоВремяТранзакции = Объект.СпособОтраженияКомиссии;
	СпособОтраженияКомиссииВОтдельномСписании = Объект.СпособОтраженияКомиссии;
	ВестиУчетКомиссииВоВзаиморасчетахПереключатель = Объект.ВестиУчетКомиссииВоВзаиморасчетах;
	
	Если ЗначениеЗаполнено(Объект.ОбъектРасчетовКомиссии) Тогда
		ДоговорКомиссии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ОбъектРасчетовКомиссии, "Договор");
	КонецЕсли;
	
	ДенежныеСредстваСервер.УправлениеЭлементамиФормыПриЧтенииСозданииНаСервере(ЭтотОбъект);
	
	ФиксированнаяСтавкаКомиссии = Число(Объект.ФиксированнаяСтавкаКомиссии);
	
	СформироватьНадписьЭквайринговыеТерминалы();
	СформироватьНадписьНастройкаПодключенияПлатежнойСистемы();
	НастроитьЗависимыеЭлементыФормыНаСервере();
	
	//++ НЕ УТ
	НастройкаСчетовУчетаСервер.ПриЧтенииСозданииОбъектаНастройкиСчетовУчета(ЭтотОбъект);
	//-- НЕ УТ
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьБанковскийСчетОрганизацииПоУмолчанию(Организация, БанковскийСчет)
	
	Если ЗначениеЗаполнено(БанковскийСчет) Или НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	БанковскиеСчетаОрганизаций.Ссылка КАК БанковскийСчет
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|ГДЕ
	|	БанковскиеСчетаОрганизаций.Владелец = &Организация
	|	И НЕ БанковскиеСчетаОрганизаций.Закрыт
	|	И НЕ БанковскиеСчетаОрганизаций.ПометкаУдаления
	|");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
		БанковскийСчет = Выборка.БанковскийСчет;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПартнерПриИзмененииСервер(Партнер, Контрагент, БанковскийСчетКонтрагента, КонтактноеЛицо)
	
	ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(Партнер, Контрагент);
	ПартнерыИКонтрагенты.ЗаполнитьКонтактноеЛицоПартнераПоУмолчанию(Партнер, КонтактноеЛицо);
	ЗаполнитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент, БанковскийСчетКонтрагента);
	ЗаполнитьДоговорОбъектРасчетовКомиссии();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьБанковскийСчетКонтрагентаПоУмолчанию(Контрагент, БанковскийСчетКонтрагента)
	
	Если (ЗначениеЗаполнено(БанковскийСчетКонтрагента)
		И Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчетКонтрагента, "Владелец"))
		Или Не ЗначениеЗаполнено(Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 2
	|	БанковскиеСчетаКонтрагентов.Ссылка КАК БанковскийСчетКонтрагента
	|ИЗ
	|	Справочник.БанковскиеСчетаКонтрагентов КАК БанковскиеСчетаКонтрагентов
	|ГДЕ
	|	БанковскиеСчетаКонтрагентов.Владелец = &Контрагент
	|	И НЕ БанковскиеСчетаКонтрагентов.Закрыт
	|	И НЕ БанковскиеСчетаКонтрагентов.ПометкаУдаления
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 И Выборка.Следующий() Тогда
		БанковскийСчетКонтрагента = Выборка.БанковскийСчетКонтрагента;
	Иначе
		БанковскийСчетКонтрагента = Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьЭквайринговыеТерминалы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ДанныеСправочника.Ссылка) КАК КоличествоТерминалов
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Владелец = &Ссылка
	|	И НЕ ДанныеСправочника.ПометкаУдаления
	|;
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеСправочника.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ЭквайринговыеТерминалы КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Владелец = &Ссылка
	|	И НЕ ДанныеСправочника.ПометкаУдаления
	|";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Результат = Запрос.ВыполнитьПакет();
	
	СтрокаЭквайринговыеТерминалы = "";
	
	КоличествоТерминалов = Результат[0].Выгрузить()[0].КоличествоТерминалов;
	Если КоличествоТерминалов > 1 Тогда
		
		СтрокаЭквайринговыеТерминалы =
			"<a href = СписокТерминалов>" + НСтр("ru = 'Терминалы';
												|en = 'Terminals'") + " (" + КоличествоТерминалов + ")</a>"
			+ "   "
			+ "<a href = Добавить>+ " + НСтр("ru = 'Добавить';
											|en = 'Add'") + "</a>";
		
	ИначеЕсли КоличествоТерминалов = 1 Тогда
		
		ТерминалСсылка = Результат[1].Выгрузить()[0].Ссылка;
		СтрокаЭквайринговыеТерминалы =
			"<a href = " + ПолучитьНавигационнуюСсылку(ТерминалСсылка) + ">" + Строка(ТерминалСсылка) + "</a>"
			+ "   "
			+ "<a href = Добавить>+ " + НСтр("ru = 'Добавить';
											|en = 'Add'") + "</a>";
	Иначе
		СтрокаЭквайринговыеТерминалы =
			"<a href = Добавить>+ " + НСтр("ru = 'Добавить';
											|en = 'Add'") + "</a>";
	КонецЕсли;
	
	НадписьЭквайринговыеТерминалы = СтроковыеФункции.ФорматированнаяСтрока(СтрокаЭквайринговыеТерминалы);
	
	Если Не ПравоДоступа("Добавление", Метаданные.Справочники.ЭквайринговыеТерминалы) Тогда
		Элементы.НадписьЭквайринговыеТерминалы.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьЭлементовПоСтатусуСервер()
	
	УстановитьПодписку = Ложь;
	
	Если Объект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.НеСогласован Тогда
		УстановитьПодписку = Ложь;
	ИначеЕсли Объект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Закрыт
		Или Объект.Статус = Перечисления.СтатусыДоговоровКонтрагентов.Действует Тогда
		УстановитьПодписку = Объект.Согласован;
	КонецЕсли;
	
	МассивЭлементов = Новый Массив;
	
	// Элементы управления шапки
	МассивЭлементов.Добавить("Дата");
	МассивЭлементов.Добавить("Номер");
	МассивЭлементов.Добавить("Партнер");
	МассивЭлементов.Добавить("Контрагент");
	МассивЭлементов.Добавить("Организация");
	
	ОбщегоНазначенияУТ.УстановитьПодпискуНаСобытияИзмененияЭлементовФормы(ЭтаФорма, МассивЭлементов, УстановитьПодписку);
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьЗависимыеЭлементыФормы(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтотОбъект, ИзмененныйРеквизит, РеквизитыФормы(ЭтотОбъект));
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЗависимыеЭлементыФормыНаСервере(ИзмененныйРеквизит = "")
	
	ДенежныеСредстваКлиентСервер.НастроитьЭлементыФормы(ЭтаФорма, ИзмененныйРеквизит, РеквизитыФормы(ЭтаФорма));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыФормы(Форма)
	
	РеквизитыФормы = Новый Структура;
	
	Возврат РеквизитыФормы;
	
КонецФункции

// Установить доступность реквизитов формы
//
//
&НаСервере
Процедура УстановитьДоступностьЭлементовНаСервере()
	
	ДоступноРедактированиеДоговораЭквайринга = ПравоДоступа("Изменение", Метаданные.Справочники.ДоговорыЭквайринга);
	
	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить("ФискальныеЧекиФормируетПлатежнаяСистема");
	МассивЭлементов.Добавить("ГруппаСпособОтраженияКомиссии");
	МассивЭлементов.Добавить("ВзимаетсяКомиссияПриВозврате");
	ОбщегоНазначенияУТКлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "Доступность", ДоступноРедактированиеДоговораЭквайринга);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВалютаБанковскогоСчета(Знач БанковскийСчет)
	
	Возврат Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет).Валюта
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДоговорОбъектРасчетовКомиссии()
	
	ВалютаСчета = ВалютаБанковскогоСчета(Объект.БанковскийСчет);
	
	Если Не ЗначениеЗаполнено(Объект.Организация)
		Или Не ЗначениеЗаполнено(Объект.Партнер)
		Или Не ЗначениеЗаполнено(Объект.Контрагент)
		Или Не ЗначениеЗаполнено(ВалютаСчета) Тогда
		ДоговорКомиссии = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	КонецЕсли;

	Если ЗначениеЗаполнено(ДоговорКомиссии) Тогда
		
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКомиссии,
			"Организация, Партнер, Контрагент, ВалютаВзаиморасчетов");
		
		ОчиститьДоговор = (Объект.Организация <> РеквизитыДоговора.Организация
			Или Объект.Партнер <> РеквизитыДоговора.Партнер
			Или Объект.Контрагент <> РеквизитыДоговора.Контрагент
			Или ВалютаСчета <> РеквизитыДоговора.ВалютаВзаиморасчетов);
		
		Если ОчиститьДоговор Тогда
			ДоговорКомиссии = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

	Объект.ОбъектРасчетовКомиссии = ?(ЗначениеЗаполнено(ДоговорКомиссии),
		ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(ДоговорКомиссии), Справочники.ОбъектыРасчетов.ПустаяСсылка());

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ЗаполнитьБанковскийСчетОрганизацииПоУмолчанию(Объект.Организация, Объект.БанковскийСчет);
	ЗаполнитьДоговорОбъектРасчетовКомиссии();
	
КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()
	
	ЗаполнитьБанковскийСчетКонтрагентаПоУмолчанию(Объект.Контрагент, Объект.БанковскийСчетКонтрагента);
	ЗаполнитьДоговорОбъектРасчетовКомиссии();
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКомиссииПриИзмененииНаСервере()
	
	Объект.ОбъектРасчетовКомиссии = ?(ЗначениеЗаполнено(ДоговорКомиссии),
		ОбъектыРасчетовСервер.ПолучитьОбъектРасчетовПоСсылке(ДоговорКомиссии), Справочники.ОбъектыРасчетов.ПустаяСсылка());
	
КонецПроцедуры

#Область Локализация

&НаСервере
Процедура СформироватьНадписьНастройкаПодключенияПлатежнойСистемы()

	//++ Локализация
	
	НадписьНастройкаПодключенияПлатежнойСистемы = РозничныеПродажиЛокализация.СформироватьНадписьНастройкаПодключенияПлатежнойСистемы(Объект.Ссылка);
	
	Если Не ПравоДоступа("Добавление", Метаданные.Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей)
		Или Не ПравоДоступа("Чтение", Метаданные.Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей) Тогда
		Элементы.НадписьНастройкаПодключенияПлатежнойСистемы.Доступность = Ложь;
	КонецЕсли;
	//-- Локализация

КонецПроцедуры

&НаКлиенте
Процедура НадписьНастройкаПодключенияПлатежнойСистемыОбработкаНавигационнойСсылкиЛокализация(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	//++ Локализация
	ОчиститьСообщения();
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "НастроитьПодключениеКПлатежнойСистеме"
		ИЛИ НавигационнаяСсылкаФорматированнойСтроки = "Добавить" Тогда
		
		СтандартнаяОбработка = Ложь;

		ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьПодключениеКПлатежнойСистеме", ЭтотОбъект);
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, "");
		Иначе
			ТекстВопроса = Нстр("ru = 'Перед настройкой подключения к платежной системе договор будет записан, продолжить?';
								|en = 'Before you set up connection to the payment system, the contract will be saved. Continue?'");
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СписокНастроекПодключения" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", НастройкиПодключенияПоДоговору());
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"НастроитьПодключениеКПлатежнойСистемеЗавершение",
			ЭтотОбъект);
		
		ОткрытьФорму(
			"РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ.Форма.ФормаНастройкиПодключенияСОтбором",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			ОписаниеОповещения);
		
	КонецЕсли
	//-- Локализация
	
КонецПроцедуры

//++ Локализация

&НаКлиенте
Процедура НастроитьПодключениеКПлатежнойСистеме(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		ЭлементЗаписан = Ложь;
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ЭлементЗаписан = Записать();
	Иначе
		ЭлементЗаписан = Истина;
	КонецЕсли;
	
	Если ЭлементЗаписан Тогда

		ОписаниеОповещения = Новый ОписаниеОповещения(
			"НастроитьПодключениеКПлатежнойСистемеЗавершение",
			ЭтотОбъект);
		
		ПараметрыПодключения = НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения(
				Неопределено,
				Неопределено,
				ОписаниеОповещения,
				"c2b");
		ПараметрыПодключения.Вставить("ТорговаяТочка", Объект.Ссылка);
	
		НастройкиПлатежныхСистемКлиент.ПодключитьПлатежнуюСистему(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП(), ПараметрыПодключения);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НастройкиПодключенияПоДоговору()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НастройкиИнтеграции.Интеграция КАК Интеграция
	|ИЗ
	|	РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграции
	|ГДЕ
	|	НастройкиИнтеграции.Договор = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Интеграция");
	
КонецФункции

&НаКлиенте
Процедура НастроитьПодключениеКПлатежнойСистемеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СпособПроведенияПлатежаПриИзмененииНаСервере();
	
КонецПроцедуры

//-- Локализация

#КонецОбласти

#КонецОбласти

