#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	// Пропускаем обработку, чтобы гарантировать получение формы объекта при передаче параметра "АвтоТест".
	Если ДанныеЗаполнения = "АвтоТест" Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("БанковскийСчет");
	
	Если Не ЗначениеЗаполнено(БанковскийСчет) Тогда
		
		ТекстОшибки = НСтр("ru = 'Поле ""Банковский счет для зачисления"" не заполнено';
							|en = 'The ""Bank account for payouts"" field is not filled in'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.БанковскийСчет", , Отказ);
		
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Партнер");
	
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		
		ТекстОшибки = НСтр("ru = 'Поле ""Эквайер"" не заполнено';
							|en = '""Acquirer"" is required'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.Партнер", , Отказ);
		
	КонецЕсли;
	
	Если Не ВестиУчетКомиссииВоВзаиморасчетах Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектРасчетовКомиссии");
	КонецЕсли;

	Если ДополнительныеСвойства.Свойство("ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии")
		И ДополнительныеСвойства.ИсключитьПроверкуЗаполненияОбъектРасчетовКомиссии Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектРасчетовКомиссии");
	КонецЕсли;

	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	Если ОплатаВИнтернетМагазине И Не ЗначениеЗаполнено(АдресИнтернетМагазина) Тогда
		ТекстОшибки = НСтр("ru = 'В случае установки флага ""Оплата в интернет-магазине"" поле ""Адрес интернет-магазина"" должно быть заполнено';
							|en = 'If the ""Payment in online store"" checkbox is selected, the ""Online store address"" field is required'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.АдресИнтернетМагазина", , Отказ);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(АдресИнтернетМагазина) 
		И ОбщегоНазначенияУТКлиентСервер.ПроверитьАдресСайта(АдресИнтернетМагазина) Тогда
			ТекстОшибки = НСтр("ru = 'Неверный формат адреса интернет-магазина';
								|en = 'Incorrect format of online store address'");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, , "Объект.АдресИнтернетМагазина", "Объект", Отказ);
	КонецЕсли;
	
	//++ Локализация
	
	ДенежныеСредстваСерверЛокализация.ПроверитьДоговорНаВозможностьКорреспонденцииСоСчетомЦифровогоРубля(ЭтотОбъект, Отказ);
	
	//-- Локализация
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	ОписаниеТиповБулево = Новый ОписаниеТипов("Булево");
	ФискальныеЧекиФормируетПлатежнаяСистема = ОписаниеТиповБулево.ПривестиЗначение(ФискальныеЧекиФормируетПлатежнаяСистема);

	ИспользуютсяЭквайринговыеТерминалы = (СпособПроведенияПлатежа = Перечисления.СпособыПроведенияПлатежей.ЭквайринговыйТерминал);

	Если Не ФиксированнаяСтавкаКомиссии Тогда
		СтавкаКомиссии = 0;
	КонецЕсли;
	
	Если СпособОтраженияКомиссии = Перечисления.СпособыОтраженияЭквайринговойКомиссии.ВОтчете Тогда
		ВзимаетсяКомиссияПриВозврате = Ложь;
	КонецЕсли;
	
	Если Не (СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.ИнтернетЭквайринг")
		Или СпособПроведенияПлатежа = ПредопределенноеЗначение("Перечисление.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей")) Тогда
		ОплатаВИнтернетМагазине = Ложь;
		АдресИнтернетМагазина = "";
	КонецЕсли;
	
	Если ВестиУчетКомиссииВоВзаиморасчетах Тогда
		ПодразделениеРасходов = Справочники.СтруктураПредприятия.ПустаяСсылка();
		НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
		СтатьяРасходов = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
		АналитикаРасходов = Неопределено;
	Иначе
		ОбъектРасчетовКомиссии = Справочники.ОбъектыРасчетов.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли