#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция определяет реквизиты выбранного договора подключения к платежной системе.
//
// Параметры:
//  Договор - СправочникСсылка.ДоговорыЭквайринга - Ссылка на договор подключения к платежной системе.
//
// Возвращаемое значение:
//  Структура - Реквизиты договора подключения к платежной системе:
//   * Организация - СправочникСсылка.Организации.
//   * БанковскийСчет - СправочникСсылка.БанковскиеСчетаОрганизаций.
//   * Валюта - СправочникСсылка.Валюты.
//   * Эквайер - СправочникСсылка.Контрагенты.
//   * Партнер - СправочникСсылка.Партнеры.
//   * БанковскийСчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов.
//   * СпособПроведенияПлатежа - ПеречислениеСсылка.СпособыПроведенияПлатежей.
//   * ИспользуютсяЭквайринговыеТерминалы - Булево.
//   * ДетальнаяСверкаТранзакций - Булево.
//   * СпособОтраженияКомиссии - ПеречислениеСсылка.СпособыОтраженияЭквайринговойКомиссии.
//   * ФиксированнаяСтавкаКомиссии - Булево.
//   * СтавкаКомиссии - Число.
//   * ВзимаетсяКомиссияПриВозврате - Булево.
//   * РазрешитьПлатежиБезУказанияЗаявок - Булево.
//   * СтатьяРасходов - ПланВидовХарактеристикСсылка.СтатьиРасходов.
//   * АналитикаРасходов - Характеристика.СтатьиРасходов.
//   * ПодразделениеРасходов - СправочникСсылка.СтруктураПредприятия.
//   * НаправлениеДеятельности - СправочникСсылка.НаправленияДеятельности.
//   * СтатьяДвиженияДенежныхСредствПоступлениеОплаты - СправочникСсылка.СтатьиДвиженияДенежныхСредств.
//   * СтатьяДвиженияДенежныхСредствВозврат - СправочникСсылка.СтатьиДвиженияДенежныхСредств.
//   * СтатьяДвиженияДенежныхСредствКомиссия - СправочникСсылка.СтатьиДвиженияДенежныхСредств.
//   * ВестиУчетКомиссииВоВзаиморасчетах - Булево.
//   * ОбъектРасчетовКомиссии - СправочникСсылка.ОбъектыРасчетов.
//
Функция РеквизитыДоговора(Договор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеСправочника.Организация                                    КАК Организация,
	|	ДанныеСправочника.БанковскийСчет                                 КАК БанковскийСчет,
	|	ДанныеСправочника.БанковскийСчет.ВалютаДенежныхСредств           КАК Валюта,
	|	ДанныеСправочника.Контрагент                                     КАК Эквайер,
	|	ДанныеСправочника.Партнер                                        КАК Партнер,
	|	ДанныеСправочника.БанковскийСчетКонтрагента                      КАК БанковскийСчетКонтрагента,
	|	ДанныеСправочника.СпособПроведенияПлатежа                        КАК СпособПроведенияПлатежа,
	|	ДанныеСправочника.ИспользуютсяЭквайринговыеТерминалы             КАК ИспользуютсяЭквайринговыеТерминалы,
	|	ДанныеСправочника.ДетальнаяСверкаТранзакций                      КАК ДетальнаяСверкаТранзакций,
	|	ДанныеСправочника.СпособОтраженияКомиссии                        КАК СпособОтраженияКомиссии,
	|	ДанныеСправочника.ФиксированнаяСтавкаКомиссии                    КАК ФиксированнаяСтавкаКомиссии,
	|	ДанныеСправочника.СтавкаКомиссии                                 КАК СтавкаКомиссии,
	|	ДанныеСправочника.ВзимаетсяКомиссияПриВозврате                   КАК ВзимаетсяКомиссияПриВозврате,
	|	ДанныеСправочника.РазрешитьПлатежиБезУказанияЗаявок              КАК РазрешитьПлатежиБезУказанияЗаявок,
	|	ДанныеСправочника.СтатьяРасходов                                 КАК СтатьяРасходов,
	|	ДанныеСправочника.АналитикаРасходов                              КАК АналитикаРасходов,
	|	ДанныеСправочника.ПодразделениеРасходов                          КАК ПодразделениеРасходов,
	|	ДанныеСправочника.НаправлениеДеятельности                        КАК НаправлениеДеятельности,
	|	ДанныеСправочника.СтатьяДвиженияДенежныхСредствПоступлениеОплаты КАК СтатьяДвиженияДенежныхСредствПоступлениеОплаты,
	|	ДанныеСправочника.СтатьяДвиженияДенежныхСредствВозврат           КАК СтатьяДвиженияДенежныхСредствВозврат,
	|	ДанныеСправочника.СтатьяДвиженияДенежныхСредствКомиссия          КАК СтатьяДвиженияДенежныхСредствКомиссия,
	|	ДанныеСправочника.ВестиУчетКомиссииВоВзаиморасчетах              КАК ВестиУчетКомиссииВоВзаиморасчетах,
	|	ДанныеСправочника.ОбъектРасчетовКомиссии                         КАК ОбъектРасчетовКомиссии
	|ИЗ
	|	Справочник.ДоговорыЭквайринга КАК ДанныеСправочника
	|ГДЕ
	|	ДанныеСправочника.Ссылка = &Договор
	|";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	
	СтруктураРеквизитов = Новый Структура;
	СтруктураРеквизитов.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("БанковскийСчет", Справочники.БанковскиеСчетаОрганизаций.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Эквайер", Справочники.Контрагенты.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("БанковскийСчетКонтрагента", Справочники.БанковскиеСчетаКонтрагентов.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("ИспользуютсяЭквайринговыеТерминалы", Ложь);
	СтруктураРеквизитов.Вставить("ДетальнаяСверкаТранзакций", Ложь);
	СтруктураРеквизитов.Вставить("СпособОтраженияКомиссии", Перечисления.СпособыОтраженияЭквайринговойКомиссии.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("ФиксированнаяСтавкаКомиссии", Ложь);
	СтруктураРеквизитов.Вставить("СтавкаКомиссии", 0);
	СтруктураРеквизитов.Вставить("ВзимаетсяКомиссияПриВозврате", Ложь);
	СтруктураРеквизитов.Вставить("РазрешитьПлатежиБезУказанияЗаявок", Ложь);
	СтруктураРеквизитов.Вставить("СтатьяРасходов", ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("АналитикаРасходов");
	СтруктураРеквизитов.Вставить("ПодразделениеРасходов", Справочники.Организации.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("НаправлениеДеятельности", Справочники.НаправленияДеятельности.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("СтатьяДвиженияДенежныхСредствПоступлениеОплаты",
		Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("СтатьяДвиженияДенежныхСредствВозврат", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("СтатьяДвиженияДенежныхСредствКомиссия", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	СтруктураРеквизитов.Вставить("ВестиУчетКомиссииВоВзаиморасчетах", Ложь);
	СтруктураРеквизитов.Вставить("ОбъектРасчетовКомиссии", Справочники.ОбъектыРасчетов.ПустаяСсылка());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(СтруктураРеквизитов, Выборка);
	КонецЕсли;
	
	Возврат СтруктураРеквизитов;
	
КонецФункции

// Устарела. Функция определяет договор подключения подключения к платежной системе по выбранной организации.
//
// Возвращает договор, если найден единственный договор.
// Возвращает пустую ссылку, в противном случае.
//
// Параметры:
//    Организация - СправочникСсылка.Организации - Выбранная организация.
//    Эквайер - СправочникСсылка.Контрагенты - Выбранный контрагент.
//    Валюта - СправочникСсылка.Валюты - Выбранная валюта.
//    СпособПроведенияПлатежа - ПеречислениеСсылка.СпособыПроведенияПлатежей - отбор по платежной системе.
//
// Возвращаемое значение:
//    СправочникСсылка.ДоговорыЭквайринга - Найденный договор.
//
Функция ДоговорПоУмолчанию(
			Организация, Эквайер, Валюта = Неопределено, СпособПроведенияПлатежа = Неопределено) Экспорт
	
	Отбор = ПараметрыОтбораДоговораЭквайринга();
	
	Отбор.Организация = Организация;
	Отбор.Эквайер = Эквайер;
	Отбор.Валюта = Валюта;
	Отбор.СпособПроведенияПлатежа = СпособПроведенияПлатежа;
	
	Результат = ДоговорЭквайрингаПоУмолчанию(Отбор);
	
	Возврат Результат;
	
КонецФункции

// Возращает договор подключения к платежной системе по заданному отбору.
//
// Параметры:
//  ПараметрыОтбора - см. ПараметрыОтбораДоговораЭквайринга.
// 
// Возвращаемое значение:
//  СправочникСсылка.ДоговорыЭквайринга - договор эквайринга.
//
Функция ДоговорЭквайрингаПоУмолчанию(ПараметрыОтбора) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
	|	ДанныеСправочника.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ДоговорыЭквайринга КАК ДанныеСправочника
	|ГДЕ
	|	НЕ ДанныеСправочника.ПометкаУдаления
	|	И ДанныеСправочника.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
	|	И (ДанныеСправочника.Организация = &Организация
	|		ИЛИ &Организация = Неопределено)
	|	И (ДанныеСправочника.Контрагент = &Эквайер
	|		ИЛИ &Эквайер = Неопределено)
	|	И (ДанныеСправочника.БанковскийСчет.ВалютаДенежныхСредств = &Валюта
	|		ИЛИ &Валюта = Неопределено
	|		ИЛИ ДанныеСправочника.БанковскийСчет.ВалютаДенежныхСредств ЕСТЬ NULL)
	|	И (ДанныеСправочника.СпособПроведенияПлатежа = &СпособПроведенияПлатежа
	|		ИЛИ &СпособПроведенияПлатежа = Неопределено)
	|	И (ДанныеСправочника.БанковскийСчет = &БанковскийСчет
	|		ИЛИ &БанковскийСчет = Неопределено)
	|";
	
	Запрос.УстановитьПараметр("Организация", ПараметрыОтбора.Организация);
	Запрос.УстановитьПараметр("Эквайер", ПараметрыОтбора.Эквайер);
	Запрос.УстановитьПараметр("Валюта", ПараметрыОтбора.Валюта);
	Запрос.УстановитьПараметр("СпособПроведенияПлатежа", ПараметрыОтбора.СпособПроведенияПлатежа);
	Запрос.УстановитьПараметр("БанковскийСчет", ПараметрыОтбора.БанковскийСчет);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество()=1 И Выборка.Следующий() Тогда
		Результат = Выборка.Ссылка;
	Иначе
		Результат = Справочники.ДоговорыЭквайринга.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значение:
//  Массив - список имен реквизитов объекта.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("Партнер");
	Результат.Добавить("Контрагент");
	Результат.Добавить("Организация");
	Результат.Добавить("Подразделение");
	Результат.Добавить("БанковскийСчет");
	Результат.Добавить("НаправлениеДеятельности");
	
	Возврат Результат;
	
КонецФункции

// Добавляет команду создания справочника "Договоры лизинга".
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//
// Возвращаемое значение:
//  СтрокаТаблицыЗначений, Неопределено - описание добавленной команды.
//
Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	МетаданныеСправочника = Метаданные.Справочники.ДоговорыЭквайринга;
	
	Если ПравоДоступа("Добавление", МетаданныеСправочника) Тогда
		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = МетаданныеСправочника.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначенияУТ.ПредставлениеОбъекта(МетаданныеСправочника);
		Возврат КомандаСоздатьНаОсновании;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Документы.ОтчетБанкаПоОперациямЭквайринга.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

// Определяет свойства полей формы в зависимости от данных
//
// Параметры:
//	Настройки - ТаблицаЗначений - таблица с колонками:
//		* Поля - Массив - поля, для которых определяются настройки отображения
//		* Условие - ОтборКомпоновкиДанных - условия применения настройки
//		* Свойства - Структура - имена и значения свойств
//
Процедура ЗаполнитьНастройкиПолейФормы(Настройки) Экспорт
	
	Финансы = ФинансоваяОтчетностьСервер;
	
	// Основание
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("СтавкаКомиссии");
	Финансы.НовыйОтбор(Элемент.Условие, "ФиксированнаяСтавкаКомиссии", Истина);
	Элемент.Свойства.Вставить("Доступность");
	
	// ВзимаетсяКомиссияПриВозврате
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ВзимаетсяКомиссияПриВозврате");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "СпособОтраженияКомиссии", Перечисления.СпособыОтраженияЭквайринговойКомиссии.ПриЗачислении);
	Финансы.НовыйОтбор(ГруппаИли, "СпособОтраженияКомиссии", Перечисления.СпособыОтраженияЭквайринговойКомиссии.ВоВремяТранзакции);
	Элемент.Свойства.Вставить("Доступность");
	
	// НадписьЭквайринговыеТерминалы
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьЭквайринговыеТерминалы");
	Финансы.НовыйОтбор(Элемент.Условие, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.ЭквайринговыйТерминал);
	Элемент.Свойства.Вставить("Видимость");

	// НадписьНастройкаПодключенияПлатежнойСистемы
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("НадписьНастройкаПодключенияПлатежнойСистемы");
	Финансы.НовыйОтбор(Элемент.Условие, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей);
	Элемент.Свойства.Вставить("Видимость");
	
	// ФискальныеЧекиФормируетПлатежнаяСистема
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ФискальныеЧекиФормируетПлатежнаяСистема");
	Финансы.НовыйОтбор(Элемент.Условие, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.ИнтернетЭквайринг);
	Элемент.Свойства.Вставить("Видимость");
	
	// ОплатаВИнтернетМагазине
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ОплатаВИнтернетМагазине");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.ИнтернетЭквайринг);
	Финансы.НовыйОтбор(ГруппаИли, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей);
	Элемент.Свойства.Вставить("Видимость");
	
	// АдресИнтернетМагазина
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("АдресИнтернетМагазина");
	ГруппаИли = Финансы.НовыйОтбор(Элемент.Условие,,, Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	Финансы.НовыйОтбор(ГруппаИли, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.ИнтернетЭквайринг);
	Финансы.НовыйОтбор(ГруппаИли, "СпособПроведенияПлатежа", Перечисления.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей);
	Элемент.Свойства.Вставить("Видимость");
	
	// ПодразделениеРасходов
	// НаправлениеДеятельности
	// СтатьяРасходов
	// АналитикаРасходов
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ПодразделениеРасходов");
	Элемент.Поля.Добавить("НаправлениеДеятельности");
	Элемент.Поля.Добавить("СтатьяРасходов");
	Элемент.Поля.Добавить("АналитикаРасходов");
	Финансы.НовыйОтбор(Элемент.Условие, "ВестиУчетКомиссииВоВзаиморасчетах", Ложь);
	Элемент.Свойства.Вставить("Видимость");
	
	// ДоговорКомиссии
	Элемент = Настройки.Добавить();
	Элемент.Поля.Добавить("ДоговорКомиссии");
	Финансы.НовыйОтбор(Элемент.Условие, "ВестиУчетКомиссииВоВзаиморасчетах", Истина);
	Элемент.Свойства.Вставить("Видимость");
	
КонецПроцедуры

// Возвращает пустую структуру параметров для отбора договоров эквайринга по умолчанию.
// 
// Возвращаемое значение:
//  Структура - параметры отбора:
//  *Организация - Неопределено - организация договора.
//  *Эквайер - Неопределено - эквайер договора.
//  *БанковскийСчет - Неопределено - банковский счет договора.
//  *Валюта - Неопределено - валюта договора.
//  *СпособПроведенияПлатежа - Неопределено - способ проведения платежа.
//
Функция ПараметрыОтбораДоговораЭквайринга() Экспорт

	Отбор = Новый Структура;
	Отбор.Вставить("Организация", Неопределено);
	Отбор.Вставить("Эквайер", Неопределено);
	Отбор.Вставить("БанковскийСчет", Неопределено);
	Отбор.Вставить("Валюта", Неопределено);
	Отбор.Вставить("СпособПроведенияПлатежа", Неопределено);
	
	Возврат Отбор;

КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК Т
	|ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИерархияПартнеров КАК Т2 
	|	ПО Т2.Родитель = Т.Партнер
	|;
	|РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Т2.Партнер)
	|	И ЗначениеРазрешено(Т.Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

// Заполняет реквизиты параметров настройки счетов учета эквайринга, которые влияют на настройку,
// 	соответствующими им именам реквизитов аналитики учета.
//
// Параметры:
// 	СоответствиеИмен - Соответствие - ключом выступает имя реквизита, используемое в настройке счетов учета,
// 		значением является соответствующее имя реквизита аналитики учета.
// 
Процедура ЗаполнитьСоответствиеРеквизитовНастройкиСчетовУчета(СоответствиеИмен) Экспорт
	
	СоответствиеИмен.Организация = "Организация";
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		
		ТекстСоединений = "";
		ТекстУсловий    = "";
		НомерОтбора     = 0;
		Если Параметры.Свойство("Отбор") Тогда
			
			Для Каждого ТекущийОтбор Из Параметры.Отбор Цикл
				
				Если НастройкаИнтеграцииЛокализация(ТекущийОтбор.Ключ, ТекстСоединений, ТекстУсловий) Тогда
					Продолжить;
				КонецЕсли;
				
				НомерОтбора = НомерОтбора + 1;
				
				ТекстУсловий = ТекстУсловий + "
				|	И ДоговорыЭквайринга." + ТекущийОтбор.Ключ + " В (&ЗначениеОтбора" + СокрЛП(НомерОтбора) + ")";
				
				Запрос.УстановитьПараметр("ЗначениеОтбора" + СокрЛП(НомерОтбора), ТекущийОтбор.Значение);
				
			КонецЦикла;
			
		КонецЕсли;
		
		// Текст запроса содержит литералы для переопределения/подстановки &ТекстСоединений &ТекстУсловий
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	ДоговорыЭквайринга.Ссылка КАК Ссылка,
				|	ДоговорыЭквайринга.Наименование КАК Наименование,
				|	ДоговорыЭквайринга.Наименование КАК Совпадение,
				|	1 КАК Порядок
				|ПОМЕСТИТЬ ДоговорыЭквайрингаПоиск 
				|ИЗ
				|	Справочник.ДоговорыЭквайринга КАК ДоговорыЭквайринга
				|
				|,&ТекстСоединений
				|
				|ГДЕ
				|	ДоговорыЭквайринга.Наименование ПОДОБНО &СтрокаПоиска СПЕЦСИМВОЛ ""~""
				|	И &ТекстУсловий
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	ДоговорыЭквайринга.Ссылка,
				|	ДоговорыЭквайринга.Наименование,
				|	ДоговорыЭквайринга.Номер,
				|	2
				|ИЗ
				|	Справочник.ДоговорыЭквайринга КАК ДоговорыЭквайринга
				|
				|,&ТекстСоединений
				|
				|ГДЕ
				|	ДоговорыЭквайринга.Номер ПОДОБНО &СтрокаПоиска СПЕЦСИМВОЛ ""~""
				|	И &ТекстУсловий
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ДоговорыЭквайрингаПоиск.Ссылка КАК Ссылка,
				|	МИНИМУМ(ДоговорыЭквайрингаПоиск.Порядок) КАК Порядок
				|ПОМЕСТИТЬ ДоговорыЭквайрингаПоПорядку
				|ИЗ
				|	ДоговорыЭквайрингаПоиск КАК ДоговорыЭквайрингаПоиск
				|
				|СГРУППИРОВАТЬ ПО
				|	ДоговорыЭквайрингаПоиск.Ссылка
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Ссылка, ЗНАЧЕНИЕ(Справочник.ДоговорыЭквайринга.ПустаяСсылка)) КАК Ссылка,
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Наименование, """") КАК Наименование,
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Совпадение, """") КАК Совпадение,
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Порядок, 0) КАК Порядок
				|ИЗ
				|	ДоговорыЭквайрингаПоПорядку КАК ДоговорыЭквайрингаПоПорядку
				|		ЛЕВОЕ СОЕДИНЕНИЕ ДоговорыЭквайрингаПоиск КАК ДоговорыЭквайрингаПоиск
				|		ПО ДоговорыЭквайрингаПоПорядку.Ссылка = ДоговорыЭквайрингаПоиск.Ссылка
				|			И ДоговорыЭквайрингаПоПорядку.Порядок = ДоговорыЭквайрингаПоиск.Порядок
				|
				|УПОРЯДОЧИТЬ ПО
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Порядок, 0),
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Совпадение, """"),
				|	ЕСТЬNULL(ДоговорыЭквайрингаПоиск.Наименование, """")";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстСоединений", ТекстСоединений);
		
		Если Не ПустаяСтрока(ТекстУсловий) Тогда
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстУсловий", ТекстУсловий);
			
		Иначе

			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТекстУсловий", "");
			
		КонецЕсли;
		
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("СтрокаПоиска", ОбщегоНазначения.СформироватьСтрокуДляПоискаВЗапросе(Параметры.СтрокаПоиска) + "%");
		
		ДанныеВыбора = Новый СписокЗначений;
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ТекстЗначение = ?(
				Выборка.Порядок = 1,
				Выборка.Наименование,
				СокрП(Выборка.Совпадение) + " (" + Выборка.Наименование + ")");
				
			ДанныеВыбора.Добавить(Выборка.Ссылка, ТекстЗначение);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	Возврат;
	
КонецПроцедуры

#Область Локализация

Функция НастройкаИнтеграцииЛокализация(Ключ, ТекстСоединений, ТекстУсловий)
	
	ЕстьНастройкаИнтеграции = Ложь;
	
	//++ Локализация
	Если Ключ = "ЕстьНастройкаИнтеграции" Тогда
		
		ТекстСоединений = ТекстСоединений + "
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиИнтеграцииСПлатежнымиСистемамиУТ КАК НастройкиИнтеграции
		|	ПО ДоговорыЭквайринга.Ссылка = НастройкиИнтеграции.Договор";

		ТекстУсловий = ТекстУсловий + "
		|	И (ВЫБОР КОГДА ЕСТЬNULL(НастройкиИнтеграции.Интеграция, ЗНАЧЕНИЕ(Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ПустаяСсылка)) = ЗНАЧЕНИЕ(Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ПустаяСсылка)
		|		ТОГДА ЛОЖЬ ИНАЧЕ ИСТИНА	КОНЕЦ)";
		
		ЕстьНастройкаИнтеграции = Истина;
		
	КонецЕсли;
	//-- Локализация
	
	Возврат ЕстьНастройкаИнтеграции;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли

