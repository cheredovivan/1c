#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс    

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
//
// Возвращаемое значение:
//	Массив Из Строка - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	РеквизитыСправочника = ПустаяСсылка().Метаданные().Реквизиты;

	Результат = Новый Массив;
	Результат.Добавить("Наименование");
	Для каждого Реквизит Из РеквизитыСправочника Цикл
		Результат.Добавить(Реквизит.Имя);
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

// Наименование характеристики.
// 
// Параметры:
//  РегистрацияВНалоговомОргане - СправочникСсылка.РегистрацииВНалоговомОргане - Регистрация в налоговом органе
//  ВариантНалогообложенияПрибыли - СправочникСсылка.ВариантыНалогообложенияПрибыли - Вариант налогообложения прибыли
//  ВидРасходовНаАмортизируемоеИмущество - ПеречислениеСсылка.ВидыРасходовПоИнвестиционномуВычету - Вид расходов на амортизируемое имущество
//  НалоговыйПериод - Дата - Налоговый период
// 
// Возвращаемое значение:
//  Строка - Наименование характеристики
Функция НаименованиеХарактеристики(Знач РегистрацияВНалоговомОргане, Знач ВариантНалогообложенияПрибыли, 
	Знач ВидРасходовНаАмортизируемоеИмущество, Знач НалоговыйПериод) Экспорт
			
	ПредставлениеНалоговогоПериода = "";
	
	Если ЗначениеЗаполнено(НалоговыйПериод) Тогда
		ПредставлениеНалоговогоПериода = Формат(НалоговыйПериод, "ДФ=yyyy;");
	КонецЕсли;
	
	ШаблонНаименования = НСтр("ru = '%1, %2, %3, %4';
								|en = '%1, %2, %3, %4'");
	Наименование = СтрШаблон(ШаблонНаименования, РегистрацияВНалоговомОргане, ВариантНалогообложенияПрибыли, 
		ВидРасходовНаАмортизируемоеИмущество, ПредставлениеНалоговогоПериода);
	
	Возврат Наименование;
	
КонецФункции

// Производит поиск/создание элемента справочника ХарактеристикиПримененияИНВ по входным данным.
// Возвращает массив ссылок на найденные/созданные элементы справочника ХарактеристикиПримененияИНВ.
//
// Параметры:
//  ПараметрыАналитики - Структура - Коллекция параметров для поиска/создания элемента справочника.
// 
// Возвращаемое значение:
//  - СправочникСсылка.ХарактеристикиПримененияИНВ
//  - Неопределено.
//
Функция ЗначениеХарактеристики(ПараметрыАналитики) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ХарактеристикиПримененияИНВ.Ссылка
		|ИЗ
		|	Справочник.ХарактеристикиПримененияИНВ КАК ХарактеристикиПримененияИНВ
		|ГДЕ
		|	ХарактеристикиПримененияИНВ.ВидРасходовНаАмортизируемоеИмущество = &ВидРасходовНаАмортизируемоеИмущество
		|	И ХарактеристикиПримененияИНВ.ВариантНалогообложенияПрибыли = &ВариантНалогообложенияПрибыли
		|	И ХарактеристикиПримененияИНВ.РегистрацияВНалоговомОргане = &РегистрацияВНалоговомОргане
		|	И ХарактеристикиПримененияИНВ.НалоговыйПериод = &НалоговыйПериод";
	
	Запрос.УстановитьПараметр("ВидРасходовНаАмортизируемоеИмущество", ПараметрыАналитики.ВидРасходовНаАмортизируемоеИмущество);
	Запрос.УстановитьПараметр("ВариантНалогообложенияПрибыли", ПараметрыАналитики.ВариантНалогообложенияПрибыли);
	Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", ПараметрыАналитики.РегистрацияВНалоговомОргане);
	Запрос.УстановитьПараметр("НалоговыйПериод", ПараметрыАналитики.НалоговыйПериод);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат СоздатьХарактеристики(ПараметрыАналитики);
	КонецЕсли;
	
КонецФункции

// Производит поиск/создание элементов справочника ХарактеристикиПримененияИНВ по входным данным.
// Возвращает массив ссылок на найденные/созданные элементы справочника ХарактеристикиПримененияИНВ.
//
// Параметры:
//  ИсходныеДанные - Структура, ТаблицаЗначений, МенеджерВременныхТаблиц - Коллекция данных для поиска/создания элемента справочника.
//  ИмяТаблицы - Строка - Имя временной таблицы, содержащей исходные данные, для случая, когда в качестве параметра ИсходныеДанные передается МенеджерВременныхТаблиц.
// 
// Возвращаемое значение:
//  - СправочникСсылка.ХарактеристикиПримененияИНВ
//  - Массив из СправочникСсылка.ХарактеристикиПримененияИНВ
//  - Неопределено.
//
Функция СоздатьХарактеристики(ИсходныеДанные, ИмяТаблицы = "ВтДанныеИНВ") Экспорт
	
	МассивСозданныхЭлементов = Новый Массив;
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив;
	
	Если ТипЗнч(ИсходныеДанные) = Тип("Структура") Тогда
		
		Запрос.УстановитьПараметр("ВидРасходовНаАмортизируемоеИмущество", ИсходныеДанные.ВидРасходовНаАмортизируемоеИмущество);
		Запрос.УстановитьПараметр("ВариантНалогообложенияПрибыли", ИсходныеДанные.ВариантНалогообложенияПрибыли);
		Запрос.УстановитьПараметр("РегистрацияВНалоговомОргане", ИсходныеДанные.РегистрацияВНалоговомОргане);
		Запрос.УстановитьПараметр("НалоговыйПериод", ИсходныеДанные.НалоговыйПериод);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&ВидРасходовНаАмортизируемоеИмущество КАК ВидРасходовНаАмортизируемоеИмущество,
		|	&ВариантНалогообложенияПрибыли КАК ВариантНалогообложенияПрибыли,
		|	&РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	&НалоговыйПериод КАК НалоговыйПериод
		|ПОМЕСТИТЬ ВтДанныеИНВ
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	ИначеЕсли ТипЗнч(ИсходныеДанные) = Тип("ТаблицаЗначений") 
		И ИсходныеДанные.Колонки.Найти("ВидРасходовНаАмортизируемоеИмущество") <> Неопределено
		И ИсходныеДанные.Колонки.Найти("ВариантНалогообложенияПрибыли") <> Неопределено
		И ИсходныеДанные.Колонки.Найти("РегистрацияВНалоговомОргане") <> Неопределено 
		И ИсходныеДанные.Колонки.Найти("НалоговыйПериод") <> Неопределено Тогда
		
		Запрос.УстановитьПараметр("ДанныеИНВ", ИсходныеДанные);
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Данные.ВидРасходовНаАмортизируемоеИмущество КАК ВидРасходовНаАмортизируемоеИмущество,
		|	Данные.ВариантНалогообложенияПрибыли КАК ВариантНалогообложенияПрибыли,
		|	Данные.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
		|	Данные.НалоговыйПериод КАК НалоговыйПериод
		|ПОМЕСТИТЬ ВтДанныеИНВ
		|ИЗ
		|	&ДанныеИНВ КАК Данные
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидРасходовНаАмортизируемоеИмущество,
		|	ВариантНалогообложенияПрибыли,
		|	РегистрацияВНалоговомОргане,
		|	НалоговыйПериод
		|";
		
		ТекстыЗапроса.Добавить(ТекстЗапроса);
		
	ИначеЕсли ТипЗнч(ИсходныеДанные) = Тип("МенеджерВременныхТаблиц")
		И ИсходныеДанные.Таблицы.Найти(ИмяТаблицы) <> Неопределено Тогда
		
		Запрос.МенеджерВременныхТаблиц = ИсходныеДанные;
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Данные.ВидРасходовНаАмортизируемоеИмущество КАК ВидРасходовНаАмортизируемоеИмущество,
	|	Данные.ВариантНалогообложенияПрибыли КАК ВариантНалогообложенияПрибыли,
	|	Данные.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане,
	|	Данные.НалоговыйПериод КАК НалоговыйПериод
	|ИЗ
	|	ВтДанныеИНВ КАК Данные
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиПримененияИНВ КАК ХарактеристикиПримененияИНВ
	|		ПО Данные.ВидРасходовНаАмортизируемоеИмущество = ХарактеристикиПримененияИНВ.ВидРасходовНаАмортизируемоеИмущество
	|			И Данные.ВариантНалогообложенияПрибыли = ХарактеристикиПримененияИНВ.ВариантНалогообложенияПрибыли
	|			И Данные.РегистрацияВНалоговомОргане = ХарактеристикиПримененияИНВ.РегистрацияВНалоговомОргане
	|			И Данные.НалоговыйПериод = ХарактеристикиПримененияИНВ.НалоговыйПериод
	|ГДЕ
	|	ХарактеристикиПримененияИНВ.Ссылка ЕСТЬ NULL
	|";
	
	Если ТипЗнч(ИсходныеДанные) = Тип("МенеджерВременныхТаблиц") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВтДанныеИНВ", ИмяТаблицы);
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.ХарактеристикиПримененияИНВ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных.Заблокировать();
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ХарактеристикиПримененияИНВ = Справочники.ХарактеристикиПримененияИНВ.СоздатьЭлемент();
			ХарактеристикиПримененияИНВ.Заполнить(Неопределено);
			ЗаполнитьЗначенияСвойств(ХарактеристикиПримененияИНВ, Выборка);
			ХарактеристикиПримененияИНВ.Записать();
			
			МассивСозданныхЭлементов.Добавить(ХарактеристикиПримененияИНВ.Ссылка);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			СтрШаблон(НСтр("ru = 'Не удалось создать элементы справочника %1';
							|en = 'Cannot create the %1 catalog items'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Метаданные.Справочники.ХарактеристикиПримененияИНВ.ПолноеИмя()),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Если НЕ МассивСозданныхЭлементов.Количество() Тогда
		Возврат Неопределено;
	ИначеЕсли
		МассивСозданныхЭлементов.Количество() = 1 Тогда
		Возврат МассивСозданныхЭлементов[0];
	Иначе
		Возврат МассивСозданныхЭлементов;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли