#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.Наименование = НаименованиеХарактеристики(Объект.РегистрацияВНалоговомОргане,
			Объект.ВариантНалогообложенияПрибыли, Объект.ВидРасходовНаАмортизируемоеИмущество, Объект.НалоговыйПериод);
	КонецЕсли;
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидРасходовНаАмортизируемоеИмуществоПриИзменении(Элемент)
	
	ЗаполнитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНалогообложенияПрибылиПриИзменении(Элемент)

	ЗаполнитьНаименование();

КонецПроцедуры

&НаКлиенте
Процедура РегистрацияВНалоговомОрганеПриИзменении(Элемент)
	
	ЗаполнитьНаименование();

КонецПроцедуры

&НаКлиенте
Процедура НалоговыйПериодПриИзменении(Элемент)

	Объект.НалоговыйПериод = НачалоГода(Объект.НалоговыйПериод);
	
	ЗаполнитьНаименование();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьНаименование()
	
	Объект.Наименование = НаименованиеХарактеристики(Объект.РегистрацияВНалоговомОргане,
		Объект.ВариантНалогообложенияПрибыли, Объект.ВидРасходовНаАмортизируемоеИмущество, Объект.НалоговыйПериод);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция НаименованиеХарактеристики(Знач РегистрацияВНалоговомОргане, Знач ВариантНалогообложенияПрибыли, 
	Знач ВидРасходовНаАмортизируемоеИмущество, Знач НалоговыйПериод)
	
	Возврат Справочники.ХарактеристикиПримененияИНВ.НаименованиеХарактеристики(РегистрацияВНалоговомОргане,
		ВариантНалогообложенияПрибыли, ВидРасходовНаАмортизируемоеИмущество, НалоговыйПериод);
		
КонецФункции

#КонецОбласти