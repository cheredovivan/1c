
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("ОбъектыПечати") Тогда
		ВызватьИсключение НСтр("ru = 'Не заполнено значение параметра ""Объекты печати""';
								|en = 'Parameter ""Print objects"" is required'"); 
	КонецЕсли;
	
	//++ Локализация
	ИспользуетсяУчетТрудозатратВРазрезеСотрудников = ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(
		ТекущаяДатаСеанса());
	
	Подразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ОбъектыПечати[0], "ВидРабочегоЦентра.Подразделение");
	
	//-- Локализация
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Элементы.Работники.Доступность = ПечататьРаботников;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПечататьИсполнителейПриИзменении(Элемент)
	
	Элементы.Работники.Доступность = ПечататьРаботников;
	
	Если Не ПечататьРаботников Тогда
		ОчиститьТаблицу();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРаботники

&НаКлиенте
Процедура РаботникиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	//++ Локализация
	
	ТекущиеДанные = Элементы.Работники.ТекущиеДанные;
	
	УправлениеПроизводствомКлиентСервер.УстановитьТипИсполнителя(
		ТекущиеДанные.Работник,
		Ложь,
		ИспользуетсяУчетТрудозатратВРазрезеСотрудников);
	
	//-- Локализация
КонецПроцедуры

&НаКлиенте
Процедура РаботникиРаботникНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	//++ Локализация
	
	ТекущиеДанные = Элементы.Работники.ТекущиеДанные;
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("РаботникиРаботникНачалоВыбораЗавершение", ЭтотОбъект);
	
	Если НЕ ИспользуетсяУчетТрудозатратВРазрезеСотрудников Тогда
		ПараметрыФормы = Новый Структура("РежимВыбора", Истина);
		ОткрытьФорму(
			"Справочник.ФизическиеЛица.ФормаСписка",
			ПараметрыФормы,
			ЭтотОбъект,
			УникальныйИдентификатор
			,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПроизводствоКлиент.ОткрытьФормуВыбораИсполнителя(
			Неопределено,
			Подразделение,
			ТекущиеДанные.Работник,
			ОбщегоНазначенияКлиент.ДатаСеанса(),
			,
			ОписаниеОповещения);
	
	КонецЕсли;
	//-- Локализация
	
КонецПроцедуры

&НаКлиенте
Процедура РаботникиРаботникОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	//++ Локализация
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение);
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

&НаКлиенте
Процедура РаботникиРаботникАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	//++ Локализация
	Если ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение);
	КонецЕсли;
	//-- Локализация
КонецПроцедуры

&НаКлиенте
Процедура РаботникиРаботникОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	//++ Локализация
	ЗаполнитьРеквизитЭтоСотрудник()
	//-- Локализация
КонецПроцедуры

&НаКлиенте
Процедура РаботникиРаботникПриИзменении(Элемент)
	//++ Локализация
	ЗаполнитьРеквизитЭтоСотрудник()
	//-- Локализация
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	СписокОбъектов = Новый Массив();
	
	Для каждого Ссылка Из Параметры.ОбъектыПечати Цикл
		СписокОбъектов.Добавить(Ссылка);
	КонецЦикла;
	
	Если ПечататьРаботников Тогда
		
		//++ Локализация
		ОшибочныеСтроки = Новый Массив;
		//-- Локализация
		
		Для каждого Строка Из Работники Цикл
			
			//++ Локализация
			ЭтоСотрудник = ТипЗнч(Строка.Работник) = Тип("СправочникСсылка.Сотрудники");
			
			Строка.ЭтоСотрудник = ЭтоСотрудник;
			
			Если ИспользуетсяУчетТрудозатратВРазрезеСотрудников И НЕ ЭтоСотрудник Тогда
				ОшибочныеСтроки.Добавить(Строка.ПолучитьИдентификатор() + 1);
			ИначеЕсли НЕ ИспользуетсяУчетТрудозатратВРазрезеСотрудников И ЭтоСотрудник Тогда
				ОшибочныеСтроки.Добавить(Строка.ПолучитьИдентификатор() + 1);
			Иначе
			//-- Локализация
				СписокОбъектов.Добавить(Строка.Работник);
			//++ Локализация
			КонецЕсли;
			//-- Локализация
		КонецЦикла;
		
		//++ Локализация
		Если ОшибочныеСтроки.Количество() > 0 Тогда
			
			НомераСтрокой = СтрСоединить(ОшибочныеСтроки, ", ");
			
			ШаблонОшибкиСотрудники = СтрШаблон(НСтр("ru = 'В строках %1 указаны физические лица. Необходимо указать сотрудников';
													|en = 'In lines %1, persons are specified. Specify employees.'"), НомераСтрокой);
			ШаблонОшибкиФизЛица    = СтрШаблон(НСтр("ru = 'В строках %1 указаны сотрудники. Необходимо указать физические лица';
													|en = 'In lines %1, employees are specified. Specify persons.'"), НомераСтрокой);
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				?(ИспользуетсяУчетТрудозатратВРазрезеСотрудников, ШаблонОшибкиСотрудники, ШаблонОшибкиФизЛица),
				,
				"Работники");
			
			УстановитьУсловноеОформление();
			
			Возврат;
			
		КонецЕсли;
		//-- Локализация
		
	КонецЕсли;
	
	ИменаМакетов = Новый Массив();
	ИменаМакетов.Добавить("РабочийЦентрЭтикеткаЭлемент");
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Справочник.РабочиеЦентры",
			ИменаМакетов,
			СписокОбъектов,
			ВладелецФормы,
			ЗначенияПараметровПечати());
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ЗначенияПараметровПечати()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПечататьФизЛица", ПечататьРаботников);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОчиститьТаблицу()
	Работники.Очистить();
КонецПроцедуры

//++ Локализация

&НаКлиенте
Процедура РаботникиРаботникНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеДанные = Элементы.Работники.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(Результат) Тогда
		
		ТекущиеДанные.Работник = Результат;
		ЗаполнитьРеквизитЭтоСотрудник();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение)
	
	ДанныеВыбора = Новый СписокЗначений;
	
	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Подразделение", Подразделение);
	ПараметрыВыбора.Вставить("Дата", ТекущаяДатаСеанса());
	
	ПроизводствоСервер.ЗаполнитьДанныеВыбораПриВводеИсполнителя(ДанныеВыбора, Текст, ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитЭтоСотрудник()
	ТекущиеДанные = Элементы.Работники.ТекущиеДанные;
	ТекущиеДанные.ЭтоСотрудник = ТипЗнч(ТекущиеДанные.Работник) = Тип("СправочникСсылка.Сотрудники");
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Работники.Имя);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Работники.ЭтоСотрудник");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = НЕ ИспользуетсяУчетТрудозатратВРазрезеСотрудников;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ПолеСОшибкойФон);
	
КонецПроцедуры

//-- Локализация

#КонецОбласти
