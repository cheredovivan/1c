// @strict-types
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияБЭД.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	НачальноеЗаполнениеРеквизитов();
	
	Если ЭтоАдресВременногоХранилища(Параметры.ХранилищеДанных) Тогда
		ДанныеЗаполнения = ПолучитьИзВременногоХранилища(Параметры.ХранилищеДанных);
		ЗначениеВРеквизитФормы(ДанныеЗаполнения, "СвУпПред");
	КонецЕсли;
	
	ТипыПредставителей = ТипыПредставителей();
	ЗаполнитьСписокРегионов();
	
	Если СвУпПред[0].ТипПред = ТипыПредставителей.ЮридическоеЛицо Тогда
		ОтображаемаяГруппа = Элементы.ГруппаПредставитель_Организация;
	ИначеЕсли СвУпПред[0].ТипПред = ТипыПредставителей.ИндивидуальныйПредприниматель Тогда
		ОтображаемаяГруппа = Элементы.ГруппаПредставитель_ИндивидуальныйПредприниматель;
	Иначе
		ОтображаемаяГруппа = Элементы.ГруппаПредставитель_ФизическоеЛицо;
	КонецЕсли;
	
	ПоказатьГруппу(Элементы.ГруппаПредставитель, ОтображаемаяГруппа);
	НастроитьЭлементыСФиксированнымиСпискамиЗначений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СвУпПредПредСведФизЛСведФЛАдрМЖАдрРФНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗаголовокОкнаРедактирования = НСтр("ru = 'Адрес места проживания';
										|en = 'Residential address'");
	НачатьРедактированиеАдресаОрганизации(ЗаголовокОкнаРедактирования);
	
КонецПроцедуры

&НаКлиенте
Процедура СвУпПред_ПредставительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбработчикВыбранногоЗначения = Новый ОписаниеОповещения("ВыбратьПредставителяЗавершение", ЭтотОбъект);
	НачатьВыборЗначенияСубъекта(ОбработчикВыбранногоЗначения, СписокВыбораТиповПредставителя, Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СвУпПред_ПредставительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание,
	СтандартнаяОбработка)
	Если Ожидание > 0 И ЗначениеЗаполнено(Текст) Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = МашиночитаемыеДоверенностиВызовСервера.ДанныеВыбораСубъекта(Текст,
			СписокВыбораТиповПредставителя);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СвУпПред_ПредставительПриИзменении(Элемент)
	ПерезаполнитьСведенияОПредставителе(СвУпПред[0]._Представитель);
КонецПроцедуры

&НаКлиенте
Процедура СвУпПредПредСведФизЛСведФЛАдрМЖРегионАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбщегоНазначенияБЭДКлиент.АвтоПодборНайтиИВыделитьОформлением(Элемент.СписокВыбора, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СвУпПредПредСведФизЛСведФЛУдЛичнФЛКодВидДокПриИзменении(Элемент)
	
	ПоляУдостоверяющиеЛичностьФизическогоЛица = СвУпПред[0].Пред[0].СведФизЛ[0].СведФЛ[0].УдЛичнФЛ[0];
	МашиночитаемыеДоверенностиКлиент.СброситьОформлениеИОчистититьПоляДокументаУдостоверяющегоЛичность(Элемент,
		ПоляУдостоверяющиеЛичностьФизическогоЛица);

КонецПроцедуры

&НаКлиенте
Процедура СвУпПредПредСведФизЛСведФЛУдЛичнФЛКодВидДокОчистка(Элемент, СтандартнаяОбработка)
	
	ПоляУдостоверяющиеЛичностьФизическогоЛица = СвУпПред[0].Пред[0].СведФизЛ[0].СведФЛ[0].УдЛичнФЛ[0];
	МашиночитаемыеДоверенностиКлиент.СброситьОформлениеИОчистититьПоляДокументаУдостоверяющегоЛичность(Элемент,
		ПоляУдостоверяющиеЛичностьФизическогоЛица);

КонецПроцедуры

&НаКлиенте
Процедура СвУпПредПредСведИПСведФЛУдЛичнФЛКодВидДокПриИзменении(Элемент)
	
	ПоляУдостоверяющиеЛичностьФизическогоЛица = СвУпПред[0].Пред[0].СведИП[0].СведФЛ[0].УдЛичнФЛ[0];
	МашиночитаемыеДоверенностиКлиент.СброситьОформлениеИОчистититьПоляДокументаУдостоверяющегоЛичность(Элемент,
		ПоляУдостоверяющиеЛичностьФизическогоЛица);

КонецПроцедуры

&НаКлиенте
Процедура СвУпПредПредСведИПСведФЛУдЛичнФЛКодВидДокОчистка(Элемент, СтандартнаяОбработка)
	
	ПоляУдостоверяющиеЛичностьФизическогоЛица = СвУпПред[0].Пред[0].СведИП[0].СведФЛ[0].УдЛичнФЛ[0];
	МашиночитаемыеДоверенностиКлиент.СброситьОформлениеИОчистититьПоляДокументаУдостоверяющегоЛичность(Элемент,
		ПоляУдостоверяющиеЛичностьФизическогоЛица);

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	Отказ = ПроверитьЗаполнениеДанныхФормыПередЗакрытием();
	
	Если Отказ Тогда 
		Возврат;
	КонецЕсли;
	
	Закрыть(ПолучитьАдресДанныхФормы(ВладелецФормы.УникальныйИдентификатор));
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьАдресДанныхФормы(ИдентификаторВладельца)
	ДанныеПредставителя = ДанныеФормыВЗначение(СвУпПред, Тип("ТаблицаЗначений"));
	Возврат ПоместитьВоВременноеХранилище(ДанныеПредставителя, ИдентификаторВладельца);
КонецФункции

&НаКлиенте
Процедура НачатьВыборЗначенияСубъекта(ОбработчикВыбораЗначения, СписокТипов, Элемент)
	КоличествоТипов = СписокТипов.Количество();
	ОбработчикВыбораТипа = Новый ОписаниеОповещения("ВыбратьТипСубъектаЗавершение", ЭтотОбъект,
		Новый Структура("ОбработчикВыбораЗначения", ОбработчикВыбораЗначения));
	Если КоличествоТипов = 0 Тогда
		Возврат;
	ИначеЕсли КоличествоТипов = 1 Тогда
		ВыполнитьОбработкуОповещения(ОбработчикВыбораТипа, СписокТипов[0]);
	Иначе
		ПоказатьВыборИзМеню(ОбработчикВыбораТипа, СписокТипов, Элемент);
	КонецЕсли;
КонецПроцедуры

// Подключаемый выбор типа субъекта завершение.
// 
// Параметры:
//  ВыбранныйТип - ЭлементСпискаЗначений:
//  * Значение - Тип - тип субъекта, для которого нужно открыть форму выбора
//  ДополнительныеПараметры - Структура:
//  * ОбработчикВыбораЗначения - ОписаниеОповещения
&НаКлиенте
Процедура ВыбратьТипСубъектаЗавершение(ВыбранныйТип, ДополнительныеПараметры) Экспорт
	Если ТипЗнч(ВыбранныйТип) = Тип("ЭлементСпискаЗначений") Тогда
		ПоказатьВводЗначения(ДополнительныеПараметры.ОбработчикВыбораЗначения, Неопределено,
			ВыбранныйТип.Представление, ВыбранныйТип.Значение);
	КонецЕсли;
КонецПроцедуры

// Параметры:
//  ВыбранноеЗначение - ОпределяемыйТип.Организация
//                    - ОпределяемыйТип.КонтрагентБЭД
//                    - ОпределяемыйТип.ФизическоеЛицо
//  ДополнительныеПараметры - Неопределено
&НаКлиенте
Процедура ВыбратьПредставителяЗавершение(ВыбранноеЗначение, ДополнительныеПараметры = Неопределено) Экспорт
	ТипЗначения = ТипЗнч(ВыбранноеЗначение);
	ЗначениеДопустимогоТипа = Ложь;
	Для Каждого ЭлементСписка Из СписокВыбораТиповПредставителя Цикл
		Если ЭлементСписка.Значение = ТипЗначения Тогда
			ЗначениеДопустимогоТипа = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеДопустимогоТипа И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ПерезаполнитьСведенияОПредставителе(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьСведенияОПредставителе(Представитель)
	Модифицированность = Истина;
	СвУпПред.Очистить();
	ИнициализироватьРеквизитыФормата(СвУпПред, "СвУпПред");
	ПредставительПоУмолчанию(ЭтотОбъект);
	ТипЗначения = ТипЗнч(Представитель);
	ТипыПредставителей = ТипыПредставителей();
	Если ЗначениеЗаполнено(Представитель) И ОбщегоНазначения.ЭтоСсылка(ТипЗначения) Тогда
		СвУпПред[0]._Представитель = Представитель;
		Если Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип.СодержитТип(ТипЗначения) Тогда
			СтруктураСведений = МашиночитаемыеДоверенности.ДанныеФизЛица(Представитель);
			СвУпПред[0].ТипПред = ТипыПредставителей.ФизическоеЛицо;
			СведенияОФизЛице = СвУпПред[0].Пред[0].СведФизЛ[0];
			СведенияОФизЛице.ИННФЛ = СтруктураСведений.ИНН;
			СведенияОФизЛице.СНИЛС = СтруктураСведений.СтраховойНомерПФР;
			ЗаполнитьТаблицуТипаСведФЛ(СведенияОФизЛице.СведФЛ[0], СтруктураСведений);
			ПоказатьГруппу(Элементы.ГруппаПредставитель, Элементы.ГруппаПредставитель_ФизическоеЛицо);
			СброситьОформлениеЭлементовФормы(Элементы.ГруппаПредставитель_ФизическоеЛицо, Истина);
		Иначе
			СтруктураСведений = ОбщегоНазначенияБЭД.ДанныеЮрФизЛица(Представитель);
			Если ИнтеграцияЭДО.ЭтоФизЛицо(Представитель) Тогда
				СвУпПред[0].ТипПред = ТипыПредставителей.ИндивидуальныйПредприниматель;
				СведенияОбИП = СвУпПред[0].Пред[0].СведИП[0];
				СведенияОбИП.НаимИП = СтруктураСведений.ПолноеНаименование;
				СведенияОбИП.ИННФЛ = СтруктураСведений.ИНН;
				СведенияОбИП.ОГРНИП = СтруктураСведений.ОГРН;
				СведенияОбИП.СведФЛ[0].ФИО[0].Фамилия = СтруктураСведений.Фамилия;
				СведенияОбИП.СведФЛ[0].ФИО[0].Имя = СтруктураСведений.Имя;
				СведенияОбИП.СведФЛ[0].ФИО[0].Отчество = СтруктураСведений.Отчество;
				ПоказатьГруппу(Элементы.ГруппаПредставитель, Элементы.ГруппаПредставитель_ИндивидуальныйПредприниматель);
				СброситьОформлениеЭлементовФормы(Элементы.ГруппаПредставитель_ИндивидуальныйПредприниматель, Истина);
			Иначе
				СвУпПред[0].ТипПред = ТипыПредставителей.ЮридическоеЛицо;
				СведенияОЮрЛице = СвУпПред[0].Пред[0].СведОрг[0];
				СведенияОЮрЛице.НаимОрг = СтруктураСведений.ПолноеНаименование;
				СведенияОЮрЛице.ИННЮЛ = СтруктураСведений.ИНН;
				СведенияОЮрЛице.КПП = СтруктураСведений.КПП;
				СведенияОЮрЛице.ОГРН = СтруктураСведений.ОГРН;
				СведенияОЮрЛице.АдрРег[0].АдрРФ = СтруктураСведений.ЮридическийАдрес;
				ПоказатьГруппу(Элементы.ГруппаПредставитель, Элементы.ГруппаПредставитель_Организация);
				СброситьОформлениеЭлементовФормы(Элементы.ГруппаПредставитель_Организация, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьТаблицуТипаСведФЛ(СведФЛ, СтруктураСведений)
	СведФЛ.ФИО[0].Фамилия = СтруктураСведений.Фамилия;
	СведФЛ.ФИО[0].Имя = СтруктураСведений.Имя;
	СведФЛ.ФИО[0].Отчество = СтруктураСведений.Отчество;
	СведФЛ.ДатаРожд = СтруктураСведений.ДатаРождения;
	Ключи = "КемВыдан, ДатаВыдачи, КодФНС, КодПодразделения, Серия, Номер";
	СведенияУдостоверения = Новый Структура(Ключи);
	ЗаполнитьЗначенияСвойств(СведенияУдостоверения, СтруктураСведений);
	СведФЛ.УдЛичнФЛ[0].ВыдДок = СведенияУдостоверения.КемВыдан;
	СведФЛ.УдЛичнФЛ[0].ДатаДок = СведенияУдостоверения.ДатаВыдачи;
	СведФЛ.УдЛичнФЛ[0].КодВидДок = СведенияУдостоверения.КодФНС;
	СведФЛ.УдЛичнФЛ[0].КодВыдДок = СведенияУдостоверения.КодПодразделения;
	СведФЛ.УдЛичнФЛ[0].СерНомДок = СтрШаблон("%1 %2", СведенияУдостоверения.Серия, СведенияУдостоверения.Номер);
	СведФЛ.УдЛичнФЛ[0].expDate = СтруктураСведений.СрокДействия;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПредставительПоУмолчанию(Форма)
	Форма.СвУпПред[0]._Представитель = "";
	Форма.СвУпПред[0].ТипПред = ТипыПредставителей().ФизическоеЛицо;
	ПоказатьГруппу(Форма.Элементы.ГруппаПредставитель, Форма.Элементы.ГруппаПредставитель_ФизическоеЛицо);
КонецПроцедуры

// Возвращаемое значение:
//  ФиксированнаяСтруктура:
// * ЮридическоеЛицо - Строка
// * ИндивидуальныйПредприниматель - Строка
// * ФизическоеЛицо - Строка
// * ФилиалЮридическогоЛица - Строка
// * ФилиалИностраннойОрганизации - Строка
&НаКлиентеНаСервереБезКонтекста
Функция ТипыПредставителей()
	ТипыПредставителей = Новый Структура;
	ТипыПредставителей.Вставить("ЮридическоеЛицо", "1");
	ТипыПредставителей.Вставить("ИндивидуальныйПредприниматель", "2");
	ТипыПредставителей.Вставить("ФизическоеЛицо", "3");
	ТипыПредставителей.Вставить("ФилиалЮридическогоЛица", "4");
	ТипыПредставителей.Вставить("ФилиалИностраннойОрганизации", "5");
	Возврат Новый ФиксированнаяСтруктура(ТипыПредставителей);
КонецФункции

&НаСервере
Процедура ИнициализироватьРеквизитыФормата(НовыйОбъект, Путь)
	Запись = НовыйОбъект.Добавить();
	Для Каждого Реквизит Из ПолучитьРеквизиты(Путь) Цикл
		Если Реквизит.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений")) Тогда
			ИнициализироватьРеквизитыФормата(Запись[Реквизит.Имя], СтрШаблон("%1.%2", Путь, Реквизит.Имя));
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НачальноеЗаполнениеРеквизитов()
	ИнициализироватьРеквизитыФормата(СвУпПред, "СвУпПред");
	ПредставительПоУмолчанию(ЭтотОбъект);
	ЗаполнитьСпискиВыбораТипов();
	
	ЭлементыВыбораВидаДокумента = Новый Массив; // Массив Из ПолеФормы
	ЭлементыВыбораВидаДокумента.Добавить(Элементы.СвУпПредПредСведФизЛСведФЛУдЛичнФЛКодВидДок);
	ЭлементыВыбораВидаДокумента.Добавить(Элементы.СвУпПредПредСведИПСведФЛУдЛичнФЛКодВидДок);
	МашиночитаемыеДоверенности.УстановитьСписокВыбораДокументовФизическихЛиц(ЭлементыВыбораВидаДокумента);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбораТипов()
	ТипыСубъектов = Новый Структура("ФизическоеЛицо, Организация, Контрагент",
		Метаданные.ОпределяемыеТипы.ФизическоеЛицо.Тип.Типы(), Метаданные.ОпределяемыеТипы.Организация.Тип.Типы(),
		Метаданные.ОпределяемыеТипы.КонтрагентБЭД.Тип.Типы());

	ШаблонПредставления = НСтр("ru = 'Выбрать %1';
								|en = 'Select %1'");
	Для Каждого КлючИЗначение Из ТипыСубъектов Цикл
		Для Каждого ТипСубъекта Из КлючИЗначение.Значение Цикл
			МетаданныеТипа = Метаданные.НайтиПоТипу(ТипСубъекта);
			Если МетаданныеТипа = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ВыбранноеИмя = ОбщегоНазначения.ПредставлениеОбъекта(МетаданныеТипа);
			Представление = СтрШаблон(ШаблонПредставления, ПолучитьСклоненияСтроки(НРег(ВыбранноеИмя), ,
				"ПД=Винительный;")[0]);
			СписокВыбораТиповПредставителя.Добавить(ТипСубъекта, Представление);
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПоказатьГруппу(Родитель, Потомок)
	Для Каждого ВложеннаяГруппа Из Родитель.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(ВложеннаяГруппа) = Тип("ГруппаФормы") Тогда
			ВложеннаяГруппа.Видимость = ВложеннаяГруппа = Потомок;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыСФиксированнымиСпискамиЗначений()
	// В редакторе формы, для элементов также снимается автоматическое заполнение контекстного меню
	НастраиваемыеЭлементы = Новый Массив; // Массив Из ПолеФормы
	НастраиваемыеЭлементы.Добавить(Элементы.СвУпПредПредСведФизЛСведФЛАдрМЖРегион);
	
	Для Каждого ПолеСФиксированнымСписком Из НастраиваемыеЭлементы Цикл
		ПолеСФиксированнымСписком.УстановитьДействие("Открытие", "Подключаемый_ПодавитьСобытиеОткрытие");
		ПолеСФиксированнымСписком.УстановитьДействие("Очистка", "Подключаемый_ПодавитьСобытиеОчистка");
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодавитьСобытиеОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодавитьСобытиеОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура НачатьРедактированиеАдресаОрганизации(ЗаголовокОкнаРедактирования = "")
	
	Адрес = СвУпПред[0].Пред[0].СведФизЛ[0].СведФЛ[0].АдрМЖ[0].АдрРФ;
	
	ТипАдрес = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес");
	
	ДанныеКонтактнойИнформации = ИнтеграцияБСПБЭДСлужебныйВызовСервера.ДанныеКонтактнойИнформацииПоПредставлению(
		Адрес, ТипАдрес);
		
	ПараметрыВида = ДанныеКонтактнойИнформации.ПараметрыВида;
	
	ПараметрыВида.Наименование = НСтр("ru = 'Юридический адрес организации';
										|en = 'Company legal address'");
	Если ЗначениеЗаполнено(ЗаголовокОкнаРедактирования) Тогда
		ПараметрыВида.Наименование = ЗаголовокОкнаРедактирования;
	КонецЕсли;
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ЗакончитьРедактированиеАдресаОрганизации", ЭтотОбъект);
	ИнтеграцияБСПБЭДКлиент.НачатьРедактированиеАдреса(Адрес, , ОбработкаЗавершения, ДанныеКонтактнойИнформации);

КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактированиеАдресаОрганизации(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗакончитьРедактированиеАдресаОрганизацииНаСервере(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ЗакончитьРедактированиеАдресаОрганизацииНаСервере(Результат, ДополнительныеПараметры)
	
	СведенияОбАдресе = ИнтеграцияБСПБЭД.СведенияОбАдресеПоЗначению(Результат.Значение);
	
	СвУпПред[0].Пред[0].СведФизЛ[0].СведФЛ[0].АдрМЖ[0].АдрРФ = Результат.Представление;
	СвУпПред[0].Пред[0].СведФизЛ[0].СведФЛ[0].АдрМЖ[0].Регион = СведенияОбАдресе.Регион;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРегионов()
	
	СписокРегионов = Неопределено;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.АдресныйКлассификатор") Тогда
		МодульАдресныйКлассификатор = ОбщегоНазначения.ОбщийМодуль("АдресныйКлассификатор");
		СписокРегионов = МодульАдресныйКлассификатор.СубъектыРФ();
	КонецЕсли;
	
	Если СписокРегионов = Неопределено Тогда
		Элементы.СвУпПредПредСведФизЛСведФЛАдрМЖРегион.РежимВыбораИзСписка = Ложь;
	Иначе
		Элементы.СвУпПредПредСведФизЛСведФЛАдрМЖРегион.РежимВыбораИзСписка = Истина;
		Для Каждого Регион Из СписокРегионов Цикл
			Код = Формат(Регион.КодСубъектаРФ, "ЧЦ=2; ЧВН=");
			Наименование = Код + " " + Регион.Наименование;
			Элементы.СвУпПредПредСведФизЛСведФЛАдрМЖРегион.СписокВыбора.Добавить(Код, Наименование);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьЗаполнениеДанныхФормыПередЗакрытием()
	
	ЕстьОшибки = Ложь;
	
	ДанныеМЧД = ДанныеМЧДРекурсивно(СвУпПред[0], "СвУпПред"); // АПК:1036 не проверять строку на орфографию.
	ОшибкиЗаполнения = Справочники.МЧД003.ПроверитьЗаполнениеОбъектXDTOПредставителя(ДанныеМЧД);
	
	ЕстьОшибки = ОшибкиЗаполнения.Количество() > 0;
	
	Если ЕстьОшибки Тогда 
		ПодсветитьЭлементыСОшибками(ОшибкиЗаполнения);
		СообщитьОбОшибках(ОшибкиЗаполнения);
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СброситьОформлениеЭлементовФормы(Элемент, Рекурсивно)
	
	МашиночитаемыеДоверенностиКлиентСервер.СброситьОформлениеЭлементовФормы(Элемент, Рекурсивно);	
	
КонецПроцедуры

#Область ЗаполнениеОбъектаXDTO

&НаСервере
Функция ДанныеМЧДРекурсивно(СтрокаДанных, Путь)
	
	Реквизиты = ПолучитьРеквизиты(Путь);
	
	ДанныеМЧД = Новый Структура;
	Для Каждого Реквизит Из Реквизиты Цикл
		
		ИмяТекущегоРеквизита = Реквизит.Имя; // Строка
		
		Если ЭтоСлужебнаяКолонка(ИмяТекущегоРеквизита) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭтоТаблица = Реквизит.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений"));
		НовыйПуть = СтрШаблон("%1.%2", Путь, ИмяТекущегоРеквизита);
		
		Если ЭтоНезаполняемыйПутьВДанныхМЧД(НовыйПуть) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоТаблица Тогда
			
			ПростыеЗначения = ЭтоТаблицаПростыхЗначенийМЧД(НовыйПуть);
			
			НаборЗначений = Новый Массив(); // Массив Из Структура, Строка, Дата
			
			СтрокиЗначений = СтрокаДанных[ИмяТекущегоРеквизита]; // ДанныеФормыКоллекция
			Для Каждого СтрокаЗначения Из СтрокиЗначений Цикл
				
				Если ПростыеЗначения Тогда
					Значение = СтрокаЗначения[ИмяКолонкиПростогоЗначения()]; // Строка, Дата
					Если ЗначениеЗаполнено(Значение) Тогда
						НаборЗначений.Добавить(Значение);
					КонецЕсли;
				Иначе
					ДанныеСтроки = ДанныеМЧДРекурсивно(СтрокаЗначения, НовыйПуть);
					НаборЗначений.Добавить(ДанныеСтроки);
				КонецЕсли;
				
			КонецЦикла;
			
			ДанныеМЧД.Вставить(ИмяТекущегоРеквизита, НаборЗначений);
			
		Иначе
			
			Значение = СтрокаДанных[ИмяТекущегоРеквизита]; // Строка, Дата, Число
			Если ЗначениеЗаполнено(Значение) Тогда
				ДанныеМЧД.Вставить(ИмяТекущегоРеквизита, Значение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеМЧД;
	
КонецФункции 

// Возвращает, является ли таблица - списком простых значений в данных МЧД.
// 
// Параметры:
//  Путь - Строка
// 
// Возвращаемое значение:
//  Булево
&НаСервере
Функция ЭтоТаблицаПростыхЗначенийМЧД(Путь)
	
	Реквизиты = ПолучитьРеквизиты(Путь);
	
	Если Реквизиты.Количество() = 1 
		И Реквизиты[0].Имя = ИмяКолонкиПростогоЗначения() Тогда
		
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Говорит, что путь не нужно заполнять в данных МЧД.
// 
// Параметры:
//  Путь - Строка - Путь
// 
// Возвращаемое значение:
//  Булево
&НаСервереБезКонтекста
Функция ЭтоНезаполняемыйПутьВДанныхМЧД(Знач Путь)
	
	НеподдерживаемыеРеквизиты = Справочники.МЧД003.НеподдерживаемыеСвойстваМЧД003();
	
	Путь = "Доверенность.Документ.Довер." + Путь; 
	
	Если НеподдерживаемыеРеквизиты.Найти(Путь) <> Неопределено Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Возвращает имя колонки для списка простых значений.
// 
// Возвращаемое значение:
//  Строка
&НаСервереБезКонтекста
Функция ИмяКолонкиПростогоЗначения()
	
	Возврат "_Значение";
	
КонецФункции

// Возвращает признак, является ли колонка служебной.
// 
// Параметры:
//  ИмяКолонки - Строка
// 
// Возвращаемое значение:
//  Булево
&НаСервереБезКонтекста
Функция ЭтоСлужебнаяКолонка(ИмяКолонки)
	
	ИменаСлужебныхКолонок = Новый Соответствие();
	ИменаСлужебныхКолонок["_Представитель"] = Истина;
	ИменаСлужебныхКолонок["_ТипЛица"] = Истина;
	ИменаСлужебныхКолонок["_ФизическоеЛицо"] = Истина;
		
	Возврат ИменаСлужебныхКолонок[ИмяКолонки] = Истина;
	
КонецФункции   

#КонецОбласти

#Область ОбработкаОшибокЗаполнения

&НаСервере
Процедура ПодсветитьЭлементыСОшибками(ОшибкиФормирования)
	
	Для Каждого Элемент Из ОшибкиФормирования Цикл
		
		ПутьРеквизита = Элемент.Ключ;
		
		ЭлементыФормы = ЭлементыПоПутиРеквизита(ПутьРеквизита);
		Для Каждого ЭлементФормы Из ЭлементыФормы Цикл
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭлементФормы, "ЦветРамки") Тогда
				ЭлементФормы.ЦветРамки = ЦветаСтиля.ЦветОтрицательногоЧисла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает элемент формы по пути реквизита.
// 
// Параметры:
//  Путь - Строка
// 
// Возвращаемое значение:
//  Массив Из ДекорацияФормы, ГруппаФормы, КнопкаФормы, ТаблицаФормы, ПолеФормы, Неопределено - 
&НаСервере
Функция ЭлементыПоПутиРеквизита(Путь)
	
	ЧастиПутиБезУказанияИндекса = Новый Массив(); // Массив Из Строка
	
	ЧастиПути = СтрРазделить(Путь, ".");
	Для Каждого ЧастьПути Из ЧастиПути Цикл
		ДанныеЧастиПути = ДанныеЧастиПути(ЧастьПути);
		ЧастиПутиБезУказанияИндекса.Добавить(ДанныеЧастиПути.Имя);
	КонецЦикла;
	
	ЭлементыПоПути = Новый Массив(); // Массив Из ДекорацияФормы, ГруппаФормы, КнопкаФормы, ТаблицаФормы, ПолеФормы
	ПредполагаемоеИмяЭлемента = СтрСоединить(ЧастиПутиБезУказанияИндекса, "");
	НайденныйЭлемент = Элементы.Найти(ПредполагаемоеИмяЭлемента);
	
	Если НайденныйЭлемент <> Неопределено Тогда
		ЭлементыПоПути.Добавить(НайденныйЭлемент);
	КонецЕсли;
	
	Возврат ЭлементыПоПути;
	
КонецФункции  

// Конструктор данных части пути дерева МЧД.
// 
// Возвращаемое значение:
//  Структура:
// * Имя - Строка
// * Индекс - Число
&НаКлиентеНаСервереБезКонтекста
Функция НовыеДанныеЧастиПути()
	
	ДанныеПути = Новый Структура;
	ДанныеПути.Вставить("Имя", "");
	ДанныеПути.Вставить("Индекс", 0);
	
	Возврат ДанныеПути;
	
КонецФункции

// Возвращает данные части пути по строковому представлению.
// 
// Параметры:
//  ЧастьПути - Строка
// 
// Возвращаемое значение:
//  см. НовыеДанныеЧастиПути
&НаКлиентеНаСервереБезКонтекста
Функция ДанныеЧастиПути(ЧастьПути)
	
	Данные = НовыеДанныеЧастиПути();
	Данные.Имя = ЧастьПути;
	
	Если Не СтрЗаканчиваетсяНа(ЧастьПути, "]") Тогда
		Возврат Данные;
	КонецЕсли;
	
	ДлинаЧастиПути = СтрДлина(ЧастьПути);
	
	КоличествоЦифрИндекса = 0;
	
	ЭтоЧастьПутиБезИндекса = Истина;
	
	ПозицияВСтроке = ДлинаЧастиПути - 1;
	Пока ПозицияВСтроке > 1 Цикл
		
		Символ = Сред(ЧастьПути, ПозицияВСтроке, 1);
		ЭтоЦифра = СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Символ);
		
		Если ЭтоЦифра Тогда
			
			КоличествоЦифрИндекса = КоличествоЦифрИндекса + 1;
			ПозицияВСтроке = ПозицияВСтроке - 1;
			
		Иначе
			
			Если Сред(ЧастьПути, ПозицияВСтроке, 1) <> "[" Тогда
				ЭтоЧастьПутиБезИндекса = Ложь;
			КонецЕсли;
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЭтоЧастьПутиБезИндекса Тогда
		Возврат Данные;
	КонецЕсли;
	
	СтрокаБезИндекса = Лев(ЧастьПути, ДлинаЧастиПути - КоличествоЦифрИндекса - 2);
	Индекс = Число(Сред(ЧастьПути, ДлинаЧастиПути - КоличествоЦифрИндекса, КоличествоЦифрИндекса));
	
	Если СтрДлина(СтрокаБезИндекса) > 0 Тогда
		Данные.Имя = СтрокаБезИндекса;
		Данные.Индекс = Индекс;
		Возврат Данные;
	Иначе
		Возврат Данные;
	КонецЕсли;
	
КонецФункции

// Возвращает путь, очищенный от индексов.
// 
// Параметры:
//  Путь - Строка - Путь в формате Доверенность[0].Документ[0].Довер[0]...
// 
// Возвращаемое значение:
//  Строка - Путь в формате Доверенность.Документ.Довер...
&НаСервереБезКонтекста
Функция ПутьБезИндексов(Путь)
	
	ЧастиПутиБезУказанияИндекса = Новый Массив(); // Массив Из Строка
	
	ЧастиПути = СтрРазделить(Путь, ".");
	Для Каждого ЧастьПути Из ЧастиПути Цикл
		ДанныеЧастиПути = ДанныеЧастиПути(ЧастьПути);
		ЧастиПутиБезУказанияИндекса.Добавить(ДанныеЧастиПути.Имя);
	КонецЦикла;
	
	Возврат СтрСоединить(ЧастиПутиБезУказанияИндекса, ".");
	
КонецФункции

// Сообщает пользователю ошибки формирования МЧД, группируя их по разделам МЧД.
// 
// Параметры:
//  ОшибкиФормирования - см. Справочники.МЧД003.НовыйНаборОшибокЗаполненияОбъектаМЧД
//
&НаСервере
Процедура СообщитьОбОшибках(ОшибкиФормирования)
	
	ОшибкиПредставителей = ПолучитьПредставленияОшибокСЗаголовками(ОшибкиФормирования);
	
	ШаблонСообщения = НСтр("ru = 'Не удалось заполнить сведения о представителе(ях):
							   |%1';
							   |en = 'Cannot fill the information on the representatives:
							   |%1'");
	ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонСообщения, СтрСоединить(
		ОшибкиПредставителей, Символы.ПС)));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПредставленияОшибокСЗаголовками(ОшибкиФормирования)
	
	Результат = Новый Массив(); // Массив Из Строка
		
	ЗаголовкиРеквизитов = Новый Соответствие();
	ЗаполнитьЗаголовкиРеквизитовТаблицыФормыРекурсивно("СвУпПред", ЗаголовкиРеквизитов); // АПК:1036 не проверять строку на орфографию.
	
	Для Каждого Элемент Из ОшибкиФормирования Цикл
		
		ПутьРеквизита = Элемент.Ключ;
		СписокОшибок = Элемент.Значение;
		
		ПутьБезИндексов = ПутьБезИндексов(ПутьРеквизита);
		
		ЗаголовокРеквизита = ЗаголовкиРеквизитов[ПутьБезИндексов];
		
		ПредставленияОшибокСЗаголовками = Новый Массив(); // Массив Из Строка
		Для Каждого ТекстОшибки Из СписокОшибок Цикл
			ТекстСообщения = СтрШаблон("	%1 - %2", ЗаголовокРеквизита, ТекстОшибки);
			ПредставленияОшибокСЗаголовками.Добавить(ТекстСообщения);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ПредставленияОшибокСЗаголовками);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Заголовки реквизитов таблицы формы рекурсивно.
// 
// Параметры:
//  Путь - Строка
//  ЗаголовкиРеквизитов - Соответствие Из КлючИЗначение:
//    * Ключ - Строка - Путь до реквизита
//    * Значение - Строка - Заголовок реквизита
// 
&НаСервере
Процедура ЗаполнитьЗаголовкиРеквизитовТаблицыФормыРекурсивно(Путь, ЗаголовкиРеквизитов)
	
	Реквизиты = ПолучитьРеквизиты(Путь);
	
	Для Каждого Реквизит Из Реквизиты Цикл
		
		ИмяТекущегоРеквизита = Реквизит.Имя; // Строка
		
		ЭтоТаблица = Реквизит.ТипЗначения.СодержитТип(Тип("ТаблицаЗначений"));
		НовыйПуть = СтрШаблон("%1.%2", Путь, ИмяТекущегоРеквизита);
		
		Если ЗначениеЗаполнено(Реквизит.Заголовок) Тогда
			ЗаголовкиРеквизитов[НовыйПуть] = Реквизит.Заголовок;
		КонецЕсли;
		
		Если ЭтоТаблица Тогда
			
			ЗаполнитьЗаголовкиРеквизитовТаблицыФормыРекурсивно(НовыйПуть, ЗаголовкиРеквизитов);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти