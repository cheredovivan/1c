#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);

	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект); 
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриЧтенииСозданииНаСервере();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
	Если Объект.ПараллельнаяЗагрузка Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ЕдиницаИзмеренияЗагрузки) Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Единица измерения загрузки"" не заполнено.';
									|en = '""Load unit of measure"" is required.'");
   			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ЕдиницаИзмеренияЗагрузки", "Объект", Отказ);
		КонецЕсли; 
		
		Если Объект.ВариантЗагрузки = Перечисления.ВариантыЗагрузкиРабочихЦентров.Синхронный 
			И НЕ Объект.ИспользуютсяВариантыНаладки Тогда
			
			Если НЕ ЗначениеЗаполнено(Объект.ВремяРаботы) Тогда
				ТекстСообщения = НСтр("ru = 'Поле ""Время работы"" не заполнено.';
										|en = '""Working time"" is required.'");
	   			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ВремяРаботы", "Объект", Отказ);
			КонецЕсли; 
			Если НЕ ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
				ТекстСообщения = НСтр("ru = 'Поле ""Единица измерения времени"" не заполнено.';
										|en = '""Time unit of measure"" is required.'");
	   			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ЕдиницаИзмерения", "Объект", Отказ);
			КонецЕсли; 
			
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если Объект.ИспользуютсяВариантыНаладки Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ЕдиницаВремениПереналадки) Тогда
			ТекстСообщения = НСтр("ru = 'Поле ""Единица времени переналадки"" не заполнено.';
									|en = '""Changeover time unit"" is required.'");
   			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "ЕдиницаИзмеренияЗагрузки", "Объект", Отказ);
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.ДлительностьПереналадки.УстановитьДлительностьПереналадки(ТекущийОбъект.Ссылка,,, ВремяПереналадки);
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтаФорма, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ВидыРабочихЦентров");

	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтаФорма, ПараметрыЗаписи);
	
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтаФорма, Отказ, ТекущийОбъект, ПараметрыЗаписи);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ВариантыНаладки" И Источник = Объект.Ссылка Тогда
		// Нужно прочитать время, т.к. оно записывается при записи вида РЦ
		ПрочитатьВремяПереналадки();
		
	ИначеЕсли ИмяСобытия = "Запись_СтруктураПредприятия" Тогда
		ПрочитатьПараметрыПодразделения();
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства 
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОписанияРабочихЦентровПриИзменении(Элемент)
	
	Объект.ВводитьДоступностьДляВидаРЦ = (ВариантОписанияРабочихЦентров = 1);
	
	Если Не Объект.ВводитьДоступностьДляВидаРЦ Тогда
		Объект.Календарь = Неопределено;
		Объект.КоличествоРабочихЦентров = 0;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ВводитьДоступностьДляВидаРЦ");
	
КонецПроцедуры

&НаКлиенте
Процедура УчитыватьДоступностьПоГрафикуРаботыПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПланироватьРаботуРабочихЦентровПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПараллельнаяЗагрузкаПриИзменении(Элемент)

	Если Объект.ПараллельнаяЗагрузка
		И Объект.ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиРабочихЦентров.ПустаяСсылка") Тогда
		Объект.ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиРабочихЦентров.Асинхронный");
	ИначеЕсли Не Объект.ПараллельнаяЗагрузка
		И Объект.ВариантЗагрузки <> ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиРабочихЦентров.ПустаяСсылка") Тогда
		Объект.ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиРабочихЦентров.ПустаяСсылка");
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ПараллельнаяЗагрузка");

КонецПроцедуры
 
&НаКлиенте
Процедура ВариантЗагрузкиАсинхронныйПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ВариантЗагрузки");
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗагрузкиСинхронный2ПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ВариантЗагрузки");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользуютсяВариантыНаладкиПриИзменении(Элемент)

	Если Объект.ИспользуютсяВариантыНаладки И Объект.ЕдиницаВремениПереналадки.Пустая() Тогда
		Объект.ЕдиницаВремениПереналадки = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Минута");
	ИначеЕсли НЕ Объект.ИспользуютсяВариантыНаладки И НЕ Объект.ЕдиницаВремениПереналадки.Пустая() Тогда
		Объект.ЕдиницаВремениПереналадки = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.ПустаяСсылка");
	КонецЕсли; 
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ИспользуютсяВариантыНаладки"); 
	
	НастроитьПанельНавигации();
	
КонецПроцедуры


&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	Элементы.СтраницаДополнительно.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Описание);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
   
&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	
	ПараметрыПроцедуры = ОбщегоНазначенияУТКлиент.ПараметрыРазрешенияРедактированияРеквизитовОбъекта();
	ОбщегоНазначенияУТКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтаФорма, ПараметрыПроцедуры);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтаФорма, Команда);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
	
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	#Область ПараллельнаяЗагрузка
	// ОтметкаНезаполненного для переналадки
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаВремениПереналадки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИспользуютсяВариантыНаладки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

	// ОтметкаНезаполненного для ед. изм. загрузки
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаИзмеренияЗагрузки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПараллельнаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ОтметкаНезаполненного для времени работы
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяРаботы.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаИзмерения.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВариантЗагрузки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ВариантыЗагрузкиРабочихЦентров.Синхронный;

	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	НастройкиПодсистемыПроизводство = ПроизводствоСерверПовтИсп.НастройкиПодсистемыПроизводство();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиПодсистемыПроизводство);
	
	ПрочитатьПараметрыПодразделения();
	
	ПрочитатьВремяПереналадки(); // Нужно прочитать время, т.к. оно записывается при записи вида РЦ
	
	ВариантОписанияРабочихЦентров = Число(Объект.ВводитьДоступностьДляВидаРЦ);
	
	ПравоПереходаКВводуДоступности = ПравоДоступа("Просмотр",
		Метаданные.Обработки.ДоступностьВидовРабочихЦентров.Команды.ДоступностьВидовРабочихЦентровКонтекст);
	
	ЗаполнитьВыборЕдиницыИзмеренияЗагрузки();
	
	НастроитьПанельНавигации();
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
	Элементы.СтраницаДополнительно.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Описание);
	
	Элементы.ПланироватьРаботуРабочихЦентров.Видимость = ИспользоватьПооперационноеПланирование Или ИспользуетсяПроизводство21;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("ВводитьДоступностьДляВидаРЦ")
		ИЛИ Инициализация Тогда   
		Элементы.Календарь.Видимость = Объект.ВводитьДоступностьДляВидаРЦ Или ЗначениеЗаполнено(Объект.Календарь);
		Элементы.КоличествоРабочихЦентров.Видимость = Объект.ВводитьДоступностьДляВидаРЦ;
		Элементы.ОтступГруппаОписаниеРабочихЦентров.Видимость = Объект.ВводитьДоступностьДляВидаРЦ;
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ПараллельнаяЗагрузка")
		ИЛИ Инициализация Тогда 
		Элементы.ЕдиницаИзмеренияЗагрузки.Доступность   = Объект.ПараллельнаяЗагрузка; 
		Элементы.ВариантЗагрузкиАсинхронный.Доступность = Объект.ПараллельнаяЗагрузка;
		Элементы.ВариантЗагрузкиСинхронный1.Доступность = Объект.ПараллельнаяЗагрузка; 
		Элементы.ВариантЗагрузкиСинхронный2.Доступность = Объект.ПараллельнаяЗагрузка;
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ВариантЗагрузки")
		ИЛИ СтруктураРеквизитов.Свойство("ПараллельнаяЗагрузка")
		ИЛИ СтруктураРеквизитов.Свойство("ИспользуютсяВариантыНаладки")
		ИЛИ Инициализация Тогда 

		Если Объект.ПараллельнаяЗагрузка И Объект.ИспользуютсяВариантыНаладки Тогда
			Элементы.СтраницыСинхроннаяЗагрузка.ТекущаяСтраница = Элементы.СтраницаСинхроннаяЗагрузкаБезВремениРаботы;
		Иначе
			Элементы.СтраницыСинхроннаяЗагрузка.ТекущаяСтраница = Элементы.СтраницаСинхроннаяЗагрузкаСВременемРаботы;
		КонецЕсли;
		
		СинхроннаяЗагрузка = (Объект.ВариантЗагрузки = ПредопределенноеЗначение("Перечисление.ВариантыЗагрузкиРабочихЦентров.Синхронный"));
		Элементы.ВремяРаботы.Доступность = СинхроннаяЗагрузка;
		Элементы.ЕдиницаИзмерения.Доступность = СинхроннаяЗагрузка;
		
	КонецЕсли;	
	
	Если СтруктураРеквизитов.Свойство("ИспользуютсяВариантыНаладки")
		ИЛИ Инициализация Тогда 
		Элементы.ВремяПереналадки.Доступность = Объект.ИспользуютсяВариантыНаладки;
		Элементы.ЕдиницаВремениПереналадки.Доступность = Объект.ИспользуютсяВариантыНаладки;
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("Подразделение")
		ИЛИ СтруктураРеквизитов.Свойство("ВводитьДоступностьДляВидаРЦ")
		ИЛИ Инициализация Тогда
		РабочиеЦентрыТолькоИндивидуально = (Форма.ИнтервалПланирования = ПредопределенноеЗначение("Перечисление.ТочностьГрафикаПроизводства.Час")
			ИЛИ Форма.ИнтервалПланирования = ПредопределенноеЗначение("Перечисление.ТочностьГрафикаПроизводства.Смена"))
			И НЕ Объект.ВводитьДоступностьДляВидаРЦ;
		Элементы.ВариантОписанияРабочихЦентров.Доступность = НЕ РабочиеЦентрыТолькоИндивидуально;
		
		Если РабочиеЦентрыТолькоИндивидуально Тогда
			Элементы.ВариантОписанияРабочихЦентров.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
			Элементы.ВариантОписанияРабочихЦентров.Подсказка = СтрШаблон(
				НСтр("ru = 'Для подразделений с интервалом планирования ""%1"" рабочие центры задаются только индивидуально.';
					|en = 'You can set work centers for business units with the ""%1"" planning interval only individually.'"),
				Форма.ИнтервалПланирования);
		Иначе
			Элементы.ВариантОписанияРабочихЦентров.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
			Элементы.ВариантОписанияРабочихЦентров.Подсказка = "";
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ГруппаМинимальныйЗначимыйБуфер.Видимость = Объект.УчитыватьДоступностьПоГрафикуРаботы И Форма.ИспользуетсяПроизводство21;
	Элементы.ГруппаМаксимальнаяЗагрузка.Видимость = Объект.ПараллельнаяЗагрузка;
	
	Если Форма.ПравоПереходаКВводуДоступности Тогда
		Элементы.ДоступностьВидовРабочихЦентровКонтекст.Доступность = Объект.УчитыватьДоступностьПоГрафикуРаботы
			ИЛИ Объект.ПланироватьРаботуРабочихЦентров И (Форма.ИспользоватьПооперационноеПланирование Или Форма.ИспользуетсяПроизводство21);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПанельНавигации()
	
	СтруктураНастроек = Новый Структура;
	СтруктураНастроек.Вставить("ИспользоватьВариантыНаладки", Объект.ИспользуютсяВариантыНаладки);
	
	ОбщегоНазначенияУТ.НастроитьФормуПоПараметрам(ЭтаФорма, СтруктураНастроек);
	
КонецПроцедуры

#Область ПараметрыПодразделения

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере()

	ИнтервалПланированияДоИзменения = ИнтервалПланирования; 
	
	ПрочитатьПараметрыПодразделения();

	Если ИнтервалПланирования <> ИнтервалПланированияДоИзменения Тогда
		// Нужно сбросить параметры которые не подходят для нового интервала планирования
		Если (ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час
			ИЛИ ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена)
			И Объект.ВводитьДоступностьДляВидаРЦ Тогда
			Объект.ВводитьДоступностьДляВидаРЦ = Ложь;
			ВариантОписанияРабочихЦентров = 0;
		КонецЕсли;
	КонецЕсли;
	
	Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
		Объект.МаксимальнаяДоступностьРЦ = 1;
		Объект.ЕдиницаИзмеренияДоступностиРЦ = Перечисления.ЕдиницыИзмеренияВремени.Час;
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День
		ИЛИ ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена Тогда
		Объект.МаксимальнаяДоступностьРЦ = 24;
		Объект.ЕдиницаИзмеренияДоступностиРЦ = Перечисления.ЕдиницыИзмеренияВремени.Час;
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
		Объект.МаксимальнаяДоступностьРЦ = 7;
		Объект.ЕдиницаИзмеренияДоступностиРЦ = Перечисления.ЕдиницыИзмеренияВремени.Сутки;
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		Объект.МаксимальнаяДоступностьРЦ = 30;
		Объект.ЕдиницаИзмеренияДоступностиРЦ = Перечисления.ЕдиницыИзмеренияВремени.Сутки;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыПодразделения()
	
	ПараметрыПодразделения = ПроизводствоСерверПовтИсп.ПараметрыПроизводственногоПодразделения(Объект.Подразделение);
	ИнтервалПланирования = ПараметрыПодразделения.ИнтервалПланирования;
	ИспользоватьПооперационноеПланирование = ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
	
	ЗаполнитьПараметрыМаксимальнойДоступности();
	
КонецПроцедуры
 
&НаСервере
Процедура ЗаполнитьПараметрыМаксимальнойДоступности()

	МаксимумЧасовСтрокой = "";

	Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Час Тогда
		МаксимумЧасовСтрокой = НСтр("ru = '1 час';
									|en = '1 hour'");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.День Тогда
		МаксимумЧасовСтрокой = НСтр("ru = '24 часа';
									|en = '24 hours'");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя Тогда
		МаксимумЧасовСтрокой = НСтр("ru = '7 дней';
									|en = '7 days'");
	ИначеЕсли ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		МаксимумЧасовСтрокой = НСтр("ru = '30 дней';
									|en = '30 days'");
	КонецЕсли;
	
	Если ПустаяСтрока(МаксимумЧасовСтрокой) Тогда
		ЗаголовокНадписи = "";
	Иначе
		ЗаголовокНадписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '(максимум %1)';
				|en = '(%1 maximum)'"),
			МаксимумЧасовСтрокой);
	КонецЕсли;
	
	Элементы.ДекорацияМаксимальнаяДоступностьРЦ.Заголовок = ЗаголовокНадписи;
	
	СписокВыбора = Элементы.ЕдиницаИзмеренияДоступностиРЦ.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(Перечисления.ЕдиницыИзмеренияВремени.Минута);
	СписокВыбора.Добавить(Перечисления.ЕдиницыИзмеренияВремени.Час);
	Если ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Неделя
		Или ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Месяц Тогда
		СписокВыбора.Добавить(Перечисления.ЕдиницыИзмеренияВремени.Сутки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПараллельнаяЗагрузка

&НаСервере
Процедура ЗаполнитьВыборЕдиницыИзмеренияЗагрузки()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕдиницыИзмерения.Наименование КАК Наименование
	|ИЗ
	|	Справочник.УпаковкиЕдиницыИзмерения КАК ЕдиницыИзмерения
	|ГДЕ
	|	НЕ ЕдиницыИзмерения.ПометкаУдаления
	|	И ЕдиницыИзмерения.Владелец = ЗНАЧЕНИЕ(Справочник.НаборыУпаковок.БазовыеЕдиницыИзмерения)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование";
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");
	Элементы.ЕдиницаИзмеренияЗагрузки.СписокВыбора.ЗагрузитьЗначения(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область Переналадка
 
&НаСервере
Процедура ПрочитатьВремяПереналадки()

	ВремяПереналадки = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДлительностьПереналадки.ВремяПереналадки
	|ИЗ
	|	РегистрСведений.ДлительностьПереналадки КАК ДлительностьПереналадки
	|ГДЕ
	|	ДлительностьПереналадки.ВидРабочегоЦентра = &ВидРабочегоЦентра
	|	И ДлительностьПереналадки.ТекущийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)
	|	И ДлительностьПереналадки.СледующийВариантНаладки = ЗНАЧЕНИЕ(Справочник.ВариантыНаладки.ПустаяСсылка)";
	
	Запрос.УстановитьПараметр("ВидРабочегоЦентра", Объект.Ссылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВремяПереналадки = Выборка.ВремяПереналадки;
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

// СтандартныеПодсистемы.Свойства 

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()

	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры

// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти
