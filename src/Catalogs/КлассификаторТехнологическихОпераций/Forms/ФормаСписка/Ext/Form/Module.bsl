
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Список.РежимВыбора Тогда
	
		СтандартнаяОбработка = Ложь;
		ОбработатьВыборИЗакрыть(ВыбраннаяСтрока);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Элементы.Список.РежимВыбора Тогда
	
		СтандартнаяОбработка = Ложь;
		ОбработатьВыборИЗакрыть(Значение);
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзКТО(Команда)
	
	//++ Локализация
	ОткрытьФорму("Справочник.КлассификаторТехнологическихОпераций.Форма.ФормаПодбораПоКТО",, ЭтотОбъект);
	//-- Локализация
	
	Возврат; // пустой обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.ФормаПодобратьИзКТО.Видимость = Ложь;
	//++ Локализация
	Элементы.ФормаПодобратьИзКТО.Видимость = ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.КлассификаторТехнологическихОпераций);
	//-- Локализация
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда
		
		Элементы.Список.РежимВыбора = Истина;
		
		ТекстЗапроса = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	Классификатор.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КлассификаторТехнологическихОпераций КАК Классификатор
		|ГДЕ
		|	Классификатор.Ссылка = &Ссылка
		|	ИЛИ (Классификатор.Наименование = &Наименование И &Наименование <> """")
		|";
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Ссылка",       Параметры.ТекущаяСсылка);
		Запрос.УстановитьПараметр("Наименование", Параметры.ТекущееНаименование);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Элементы.Список.ТекущаяСтрока = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборИЗакрыть(ВыбранноеЗначение)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Элементы.Список.ДанныеСтроки(ВыбранноеЗначение);
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОВыборе(ДанныеСтроки.Ссылка);
	
КонецПроцедуры

#КонецОбласти