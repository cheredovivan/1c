#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗакрыватьПриВыборе = Ложь;
	
	ЗаполнитьКлассификатор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификатор

&НаКлиенте
Процедура КлассификаторВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КлассификаторВыборНаКлиенте(ВыбраннаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КлассификаторВыборНаКлиенте(Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.Классификатор.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Классификатор.СуществующаяСсылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветФонаВыделенияПоля);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКлассификатор()
	
	Макет = Справочники.КлассификаторТехнологическихОпераций.ПолучитьМакет("МакетКТО");
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.УстановитьСтроку(Макет.ПолучитьТекст());
	
	ТекущаяСтрока = Неопределено; // ДанныеФормыЭлементДерева
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ИмяЭлемента = ЧтениеXML.Имя;
			Если ЧтениеXML.Имя = "Элемент" Тогда
				Коллекция = ?(ТекущаяСтрока = Неопределено, Классификатор, ТекущаяСтрока);
				ТекущаяСтрока = Коллекция.ПолучитьЭлементы().Добавить();
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если ЧтениеXML.Имя = "Элемент" Тогда
				ТекущаяСтрока = ТекущаяСтрока.ПолучитьРодителя();
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			ТекущаяСтрока[ИмяЭлемента] = ЧтениеXML.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ЗаполнитьСлужебныеРеквизитыДереваКлассификатора();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыДереваКлассификатора()
	
	РеквизитыПоиска = "Код,ЭтоГруппа";
	
	СуществующиеЭлементы = СуществующиеЭлементы();
	СуществующиеЭлементы.Индексы.Добавить(РеквизитыПоиска);
	
	Отбор = Новый Структура(РеквизитыПоиска);
	
	Для каждого СтрокаГруппа Из Классификатор.ПолучитьЭлементы() Цикл
		
		СтрокаГруппа.ЭтоГруппа = Истина;
		ЗаполнитьСлужебныеРеквизитыСтрокиДереваКлассификатора(СтрокаГруппа, СуществующиеЭлементы, Отбор);
		
		Для каждого Строка Из СтрокаГруппа.ПолучитьЭлементы() Цикл
			ЗаполнитьСлужебныеРеквизитыСтрокиДереваКлассификатора(Строка, СуществующиеЭлементы, Отбор);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыСтрокиДереваКлассификатора(ДанныеСтроки, СуществующиеЭлементы, Отбор)
	
	ЗаполнитьЗначенияСвойств(Отбор, ДанныеСтроки);
	
	НайденныеСтроки = СуществующиеЭлементы.НайтиСтроки(Отбор);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		ДанныеСтроки.СуществующаяСсылка = НайденныеСтроки[0].СуществующаяСсылка;
	КонецЕсли;
	
	ДанныеСтроки.ИндексКартинки = ?(ДанныеСтроки.ЭтоГруппа, 2, 5);
	
КонецПроцедуры

&НаСервере
Функция СуществующиеЭлементы()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Классификатор.Ссылка    КАК СуществующаяСсылка,
	|	Классификатор.Код       КАК Код,
	|	Классификатор.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.КлассификаторТехнологическихОпераций КАК Классификатор";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура КлассификаторВыборНаКлиенте(ВыбранныеСтроки)
	
	Если ЕстьГруппыВВыборке(ВыбранныеСтроки) Тогда
		ДополнительныеПараметры = Новый Структура("ВыбранныеСтроки", ВыбранныеСтроки);
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ДобавитьВложенныеЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			НСтр("ru = 'Для выбранных групп добавить все вложенные элементы?';
				|en = 'Add all the nested items for the selected groups?'"),
			РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;
	
	Если КлассификаторВыборНаСервере(ВыбранныеСтроки) Тогда
		ОповеститьФормуИПользователя();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВложенныеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если КлассификаторВыборНаСервере(ДополнительныеПараметры.ВыбранныеСтроки, Результат = КодВозвратаДиалога.Да) Тогда
			ОповеститьФормуИПользователя();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция КлассификаторВыборНаСервере(Знач ВыбранныеСтроки, ДобавитьВложенные = Ложь)
	
	ЭлементыДобавлены = Ложь;
	
	Для Каждого ИдентификаторСтроки Из ВыбранныеСтроки Цикл
		ДанныеСтроки = Классификатор.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ДанныеСтроки.ЭтоГруппа Тогда
			ДобавитьВКлассификатор(ДанныеСтроки, ЭлементыДобавлены);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого ИдентификаторСтроки Из ВыбранныеСтроки Цикл
		ДанныеСтроки = Классификатор.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ДанныеСтроки.ЭтоГруппа Тогда
			Если ДобавитьВложенные Тогда
				Для каждого ПодчиненнаяСтрока Из ДанныеСтроки.ПолучитьЭлементы() Цикл
					ДобавитьВКлассификатор(ПодчиненнаяСтрока, ЭлементыДобавлены, ДанныеСтроки.СуществующаяСсылка);
				КонецЦикла;
			КонецЕсли;
		Иначе
			ДобавитьВКлассификатор(ДанныеСтроки, ЭлементыДобавлены);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ЭлементыДобавлены;
	
КонецФункции

&НаКлиенте
Функция ЕстьГруппыВВыборке(ВыбранныеСтроки)
	
	Для Каждого ИдентификаторСтроки Из ВыбранныеСтроки Цикл
		ДанныеСтроки = Классификатор.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ДанныеСтроки.ЭтоГруппа Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаСервере
Функция ДобавитьВКлассификатор(ДанныеСтроки, ЭлементыДобавлены, Родитель = Неопределено)
	
	Если ЗначениеЗаполнено(ДанныеСтроки.СуществующаяСсылка) Тогда
		Возврат ДанныеСтроки.СуществующаяСсылка;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеСтроки.ЭтоГруппа Тогда
		НовыйОбъект = Справочники.КлассификаторТехнологическихОпераций.СоздатьГруппу();
	Иначе
		НовыйОбъект = Справочники.КлассификаторТехнологическихОпераций.СоздатьЭлемент();
	КонецЕсли;
	
	Если Родитель = Неопределено Тогда
		РодительСтроки = ДанныеСтроки.ПолучитьРодителя();
		Если РодительСтроки <> Неопределено Тогда
			Родитель = РодительСтроки.СуществующаяСсылка;
		КонецЕсли;
	КонецЕсли;
	
	НовыйОбъект.Заполнить(Неопределено);
	ЗаполнитьЗначенияСвойств(НовыйОбъект, ДанныеСтроки);
	НовыйОбъект.Родитель = Родитель;
	НовыйОбъект.Записать();
	
	ДанныеСтроки.СуществующаяСсылка = НовыйОбъект.Ссылка;
	ЭлементыДобавлены = Истина;
	
	Возврат НовыйОбъект.Ссылка;
	
КонецФункции

&НаКлиенте
Процедура ОповеститьФормуИПользователя()
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.КлассификаторТехнологическихОпераций"));
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Сохранение';
			|en = 'Save'"),,
		НСтр("ru = 'Новые элементы классификатора добавлены';
			|en = 'New classifier items are added'"),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти