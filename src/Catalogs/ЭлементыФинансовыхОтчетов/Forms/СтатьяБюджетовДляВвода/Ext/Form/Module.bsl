
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ИспользоватьФильтрПоСтатусу") Тогда
		ИспользоватьФильтрПоСтатусу = Параметры.ИспользоватьФильтрПоСтатусу;
	ИначеЕсли Параметры.Свойство("МодельБюджетирования") Тогда
		ИспользоватьФильтрПоСтатусу = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МодельБюджетирования,
			"ИспользоватьУтверждениеБюджетов");
	КонецЕсли;
	
	ДанныеОбъекта = Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтотОбъект);
	ИдентификаторГлавногоХранилища = Параметры.ИдентификаторГлавногоХранилища;
	ИдентификаторСтрокиЭлементаОтчета = Параметры.ИдентификаторСтрокиЭлементаОтчета;
	
	Если Не ЗначениеЗаполнено(СтатьяБюджетов) Тогда
		Заголовок = НСтр("ru = '<Статьи бюджетирования, по которым есть обороты>';
						|en = '<Items of budgeting with turnovers>'");
	ИначеЕсли ТипЗнч(СтатьяБюджетов) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Показатель бюджетов ""%1""';
				|en = 'Budget balance item ""%1""'"),
			СтатьяБюджетов);
		Элементы.КартинкаСтатьяБюджетов.Видимость = Ложь;
		Элементы.КартинкаПоказательБюджетов.Видимость = Истина;
	Иначе
		Заголовок = Строка(Параметры.ВидЭлемента) + ": " + СтатьяБюджетов;
	КонецЕсли;
	
	Если Не Параметры.Свойство("НастройкаЯчеек") Тогда
		// авторасчет доступен только в сложной таблице
		Элементы.СпособЗаполнения.СписокВыбора.Удалить(1);
		
		ДеревоЭлементов = ДанныеФормыВЗначение(Параметры.ЭлементыОтчета, Тип("ДеревоЗначений"));
		АдресЭлементовОтчета = ПоместитьВоВременноеХранилище(ДеревоЭлементов, УникальныйИдентификатор);
		АдресТаблицыЭлементов = Неопределено;
	Иначе
		АдресТаблицыЭлементов = Параметры.АдресТаблицыЭлементов;
		АдресЭлементовОтчета = Параметры.АдресЭлементовОтчета;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтатьяБюджетов) ИЛИ ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтатьяБюджетов, "УчитыватьПоКоличеству") Тогда
		Элементы.ВыводимыеПоказатели.СписокВыбора.Добавить(Перечисления.ТипыВыводимыхПоказателейБюджетногоОтчета.Количество);
		Если Не Параметры.Свойство("НастройкаЯчеек")
			И Не Параметры.Свойство("ПроизвольныйПоказатель") Тогда
			Элементы.ВыводимыеПоказатели.СписокВыбора.Добавить(Перечисления.ТипыВыводимыхПоказателейБюджетногоОтчета.КоличествоИСумма);
		КонецЕсли;
	КонецЕсли;
	
	ОперандыФормулы.Очистить();
	Для Каждого СтрокаОперанда Из ДанныеОбъекта.ОперандыФормулы Цикл
		НоваяСтрока = ОперандыФормулы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаОперанда);
	КонецЦикла;
	Для Каждого СтрокаПравило Из ДанныеОбъекта.ПравилаРасчета Цикл
		НоваяСтрока = ПравилаРасчета.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПравило);
	КонецЦикла;

	УправлениеФормой(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ЗначениеЗаполнено(Формула) Тогда
		ТекущийОбъект.ЕстьНастройки = Истина;
	КонецЕсли;
	
	Если СтрНачинаетсяС(Формула, "<") И СтрЗаканчиваетсяНа(Формула, ">") Тогда
		Формула = "";
	КонецЕсли;
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтруктураЭлемента = ПолучитьИзВременногоХранилища(АдресЭлементаВХранилище);
		СтруктураЭлемента.ОперандыФормулы.Очистить();
		Для Каждого СтрокаОперанда Из ОперандыФормулы Цикл
			Если Не ЗначениеЗаполнено(СтрокаОперанда.АдресСтруктурыЭлемента) Тогда
				СтрокаОперанда.АдресСтруктурыЭлемента = 
					БюджетнаяОтчетностьКлиентСервер.ПоместитьЭлементВХранилище(СтрокаОперанда.Операнд, ИдентификаторГлавногоХранилища);
			КонецЕсли;
			ЗаполнитьЗначенияСвойств(СтруктураЭлемента.ОперандыФормулы.Добавить(), СтрокаОперанда);
		КонецЦикла;
		СтруктураЭлемента.ПравилаРасчета.Очистить();
		Для Каждого СтрокаПравило Из ПравилаРасчета Цикл
			ЗаполнитьЗначенияСвойств(СтруктураЭлемента.ПравилаРасчета.Добавить(), СтрокаПравило);
		КонецЦикла;
		СтруктураЭлемента.Вставить("ЕстьПривилегированныйРежим", ЕстьПривилегированныйРежим);
		ПоместитьВоВременноеХранилище(СтруктураЭлемента, АдресЭлементаВХранилище);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СтатьиБюджетов" Тогда
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Прочитать();
		Иначе
			Объект.НаименованиеДляПечати = Параметр;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура ФормулаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрНачинаетсяС(Формула, "<") И СтрЗаканчиваетсяНа(Формула, ">") Тогда
		Формула = "";
	КонецЕсли;
	
	Если СпособЗаполнения = 3 Тогда
		ВидЭлементаНастройки = ПредопределенноеЗначение("Перечисление.ВидыЭлементовФинансовогоОтчета.НастройкаПравилРасчета");
		АдресЭлементаНастройкиВХранилище = ПолучитьАдресВременногоОбъектаДляНастройкиПравилРасчета();
	Иначе
		ВидЭлементаНастройки = ПредопределенноеЗначение("Перечисление.ВидыЭлементовФинансовогоОтчета.ПроизводныйПоказатель");
		АдресЭлементаНастройкиВХранилище = ПолучитьАдресВременногоОбъектаДляРедактированияФормулы();
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидЭлемента",                      ВидЭлементаНастройки);
	ПараметрыФормы.Вставить("АдресЭлементаВХранилище",          АдресЭлементаНастройкиВХранилище);
	ПараметрыФормы.Вставить("АдресРедактируемогоЭлемента",      АдресЭлементаВХранилище);
	ПараметрыФормы.Вставить("ИдентификаторГлавногоХранилища",   ИдентификаторГлавногоХранилища);
	ПараметрыФормы.Вставить("ИспользоватьДляВводаПлана",        ИспользоватьДляВводаПлана);
	ПараметрыФормы.Вставить("СпособЗаполнения",                 СпособЗаполнения);
	ПараметрыФормы.Вставить("АдресТаблицыЭлементов",            АдресТаблицыЭлементов);
	ПараметрыФормы.Вставить("АдресЭлементовОтчета",             АдресЭлементовОтчета);
	ПараметрыФормы.Вставить("ВариантРасположенияГраницыФактическихДанных", ВариантРасположенияГраницыФактическихДанных);
	ПараметрыФормы.Вставить("ИдентификаторСтрокиЭлементаОтчета",ИдентификаторСтрокиЭлементаОтчета);
	ПараметрыФормы.Вставить("РодительВидЭлемента",              Объект.ВидЭлемента);
	ПараметрыФормы.Вставить("ИспользоватьФильтрПоСценарию",     ИспользоватьФильтрПоСценарию);
	ПараметрыФормы.Вставить("ИспользоватьФильтрПоОрганизации",  ИспользоватьФильтрПоОрганизации);
	ПараметрыФормы.Вставить("ИспользоватьФильтрПоПодразделению",ИспользоватьФильтрПоПодразделению);
	ПараметрыФормы.Вставить("ИспользоватьФильтрПоСтатусу",      ИспользоватьФильтрПоСтатусу);
	
	Оповещение = Новый ОписаниеОповещения("ОбновитьПолеПослеИзмененияЭлемента", ЭтотОбъект, ПараметрыФормы);
	
	ОткрытьФорму("Справочник.ЭлементыФинансовыхОтчетов.ФормаОбъекта",
								ПараметрыФормы, ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияПриИзменении(Элемент)
	
	Формула = "";
	ОперандыФормулы.Очистить();
	ПравилаРасчета.Очистить();
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Если СпособЗаполнения = 1 Тогда
		ИсключитьДанныеВводимогоДокументаПриРасчете = Ложь;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеПредставленияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	БюджетнаяОтчетностьКлиент.ИзменениеВыраженияПредставленияСортировкиВРедактореФормул(ЭтотОбъект,
		"ВыражениеПредставления", Объект.НаименованиеДляПечати);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ДополнительныеСведения = Новый Структура();
	ДополнительныеСведения.Вставить("ЕстьПривилегированныйРежим", ЕстьПривилегированныйРежим);
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтотОбъект, ДополнительныеСведения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Если Форма.СпособЗаполнения = 0 Тогда
		Форма.Элементы.Формула.Гиперссылка = Ложь;
		Если ПустаяСтрока(Форма.Формула) Тогда
			Форма.Формула = "";
		КонецЕсли;
	Иначе
		Форма.Элементы.Формула.Гиперссылка = Истина;
		Если ПустаяСтрока(Форма.Формула) Тогда
			Если Форма.СпособЗаполнения = 1 Тогда
				Форма.Формула = НСтр("ru = '<настроить авторасчет>';
									|en = '<configure autocalculation>'");
			ИначеЕсли Форма.СпособЗаполнения = 2 Тогда
				Форма.Формула = НСтр("ru = '<настроить автозаполнение>';
									|en = '<configure autofilling>'");
			Иначе
				Форма.Формула = НСтр("ru = '<настроить правила>';
									|en = '<configure rules>'");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Форма.Элементы.ВыражениеПредставления.Видимость = ЗначениеЗаполнено(Форма.СтатьяБюджетов);
	
	Форма.Элементы.НаименованиеДляПечати.Доступность = ПустаяСтрока(Форма.ВыражениеПредставления);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресВременногоОбъектаДляРедактированияФормулы()
	
	БуферныйОбъект = ФинансоваяОтчетностьКлиентСервер.СтруктураЭлементаОтчета();
	БуферныйОбъект.НаименованиеДляПечати = Объект.НаименованиеДляПечати;
	БуферныйОбъект.ВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.ПроизводныйПоказатель;
	ФинансоваяОтчетностьВызовСервера.УстановитьЗначениеДополнительногоРеквизита(БуферныйОбъект, "Формула", Формула);
	БуферныйОбъект.ОперандыФормулы = ОперандыФормулы.Выгрузить();
	
	Возврат ПоместитьВоВременноеХранилище(БуферныйОбъект, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ПолучитьАдресВременногоОбъектаДляНастройкиПравилРасчета()
	
	БуферныйОбъект = ФинансоваяОтчетностьКлиентСервер.СтруктураЭлементаОтчета();
	БуферныйОбъект.НаименованиеДляПечати = Объект.НаименованиеДляПечати;
	БуферныйОбъект.ВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.НастройкаПравилРасчета;
	ФинансоваяОтчетностьВызовСервера.УстановитьЗначениеДополнительногоРеквизита(БуферныйОбъект, "СтатьяБюджетов", СтатьяБюджетов);
	БуферныйОбъект.ПравилаРасчета = ПравилаРасчета.Выгрузить();
	БуферныйОбъект.ДополнительныйОтбор = Новый ХранилищеЗначения(Компоновщик.ПолучитьНастройки());
	
	Возврат ПоместитьВоВременноеХранилище(БуферныйОбъект, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеФормулыИзВременногоХранилища(Адрес)
	
	БуферныйОбъект = ПолучитьИзВременногоХранилища(Адрес);
	Если БуферныйОбъект.ВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.НастройкаПравилРасчета Тогда
		ПравилаРасчета.Загрузить(БуферныйОбъект.ПравилаРасчета);
		КоличествоПравил = ПравилаРасчета.Количество();
		Формула = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'выбрано правил: %1';
																				|en = 'Rules selected: %1'"), КоличествоПравил);
		Компоновщик.ЗагрузитьНастройки(БуферныйОбъект.ДополнительныйОтбор.Получить());
	Иначе
		Формула = ФинансоваяОтчетностьВызовСервера.ЗначениеДополнительногоРеквизита(БуферныйОбъект, "Формула");
		ОперандыФормулы.Загрузить(БуферныйОбъект.ОперандыФормулы);
	КонецЕсли;
	ПривилегированныйРежим = ФинансоваяОтчетностьВызовСервера.ЗначениеДополнительногоРеквизита(БуферныйОбъект, "ПривилегированныйРежим");
 	
	Если ПустаяСтрока(Формула) Тогда
		Если БуферныйОбъект.ВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.ПроизводныйПоказатель Тогда
			Формула = НСтр("ru = '<настроить автозаполнение>';
							|en = '<configure autofilling>'");
		Иначе
			Формула = НСтр("ru = '<настроить правила>';
							|en = '<configure rules>'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПолеПослеИзмененияЭлемента(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ПрочитатьДанныеФормулыИзВременногоХранилища(ДополнительныеПараметры.АдресЭлементаВХранилище);
		Если Результат.ВидЭлемента = ПредопределенноеЗначение("Перечисление.ВидыЭлементовФинансовогоОтчета.НастройкаПравилРасчета") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат, "ИспользоватьФильтрПоОрганизации, ИспользоватьФильтрПоПодразделению, ИспользоватьФильтрПоСценарию");
		КонецЕсли;
	КонецЕсли;
	Если Результат <> Неопределено И Результат.Свойство("ЕстьПривилегированныйРежим") Тогда
		ЕстьПривилегированныйРежим = Результат.ЕстьПривилегированныйРежим;
	КонецЕсли;
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти