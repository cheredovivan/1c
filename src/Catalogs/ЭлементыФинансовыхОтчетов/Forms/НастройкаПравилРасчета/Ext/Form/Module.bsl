
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ИспользоватьФильтрПоСтатусу") Тогда
		ИспользоватьФильтрПоСтатусу = Параметры.ИспользоватьФильтрПоСтатусу;
	ИначеЕсли Параметры.Свойство("МодельБюджетирования") Тогда
		ИспользоватьФильтрПоСтатусу = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.МодельБюджетирования,
			"ИспользоватьУтверждениеБюджетов");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ИспользоватьФильтрПоОрганизации, ИспользоватьФильтрПоПодразделению, ИспользоватьФильтрПоСценарию");
	
	Справочники.ЭлементыФинансовыхОтчетов.ФормаПриСозданииНаСервере(ЭтотОбъект);
	ИдентификаторГлавногоХранилища = Параметры.ИдентификаторГлавногоХранилища;
	
	Заголовок = "";
	Если ТипЗнч(СтатьяБюджетов) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1: настройка правил расчета остатков показателя';
																				|en = '%1: set up item balance calculation rules'"), СтатьяБюджетов);
		Элементы.КартинкаСтатьяБюджетов.Видимость = Ложь;
		Элементы.КартинкаПоказательБюджетов.Видимость = Истина;
	Иначе
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1: настройка правил расчета оборотов статьи';
																				|en = '%1: set up item turnover calculation rules'"), СтатьяБюджетов);
	КонецЕсли;
	
	ЗаполнитьПравилаРасчета();
	
	ПривилегированныйРежимПереключатель = Число(ПривилегированныйРежим);
	
	АдресЭлементовОтчета = Параметры.АдресЭлементовОтчета;
	АдресТаблицыЭлементов = Параметры.АдресТаблицыЭлементов;
	АдресРедактируемогоЭлемента = Параметры.АдресРедактируемогоЭлемента;
	
	ПараметрыОпределенияФильтров = Новый Структура;
	ПараметрыОпределенияФильтров.Вставить("АдресРедактируемогоЭлемента", АдресРедактируемогоЭлемента);
	ПараметрыОпределенияФильтров.Вставить("АдресЭлементовОтчета", АдресЭлементовОтчета);
	Если ЗначениеЗаполнено(АдресТаблицыЭлементов) Тогда
		ПараметрыОпределенияФильтров.Вставить("АдресТаблицыЭлементов", АдресТаблицыЭлементов);
	КонецЕсли;
	
	ПараметрыДоступностиФильтров = БюджетнаяОтчетностьРасчетКэшаСервер.ПараметрыДоступностиФильтров(Неопределено, ПараметрыОпределенияФильтров);
	
	Для Каждого КлючИЗначение Из ПараметрыДоступностиФильтров Цикл
		Если КлючИЗначение.Ключ = "Сценарий" Тогда
			Элементы.ИспользоватьФильтрПоСценарий.Видимость = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "Организация" Тогда
			Элементы.ИспользоватьФильтрПоОрганизация.Видимость = КлючИЗначение.Значение;
		ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
			Элементы.ИспользоватьФильтрПоПодразделение.Видимость = КлючИЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если Параметры.Свойство("РодительВидЭлемента")
	   И Параметры.РодительВидЭлемента = Перечисления.ВидыЭлементовФинансовогоОтчета.ПроизводныйПоказатель
	   И Параметры.Свойство("ИспользоватьДляВводаПлана")
	   И Параметры.ИспользоватьДляВводаПлана Тогда
		Элементы.ИсключитьДанныеВводимогоДокументаПриРасчете.Видимость = Истина;
	Иначе
		Элементы.ИсключитьДанныеВводимогоДокументаПриРасчете.Видимость = Ложь;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗаполнитьПравилаРасчета();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ Отказ Тогда
		ПроверкаОтбораПоПараметрамДоступности();
		Справочники.ЭлементыФинансовыхОтчетов.ФормаПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, Отказ);
		Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
			СтруктураЭлемента = ПолучитьИзВременногоХранилища(АдресЭлементаВХранилище);
			СтруктураЭлемента.ПравилаРасчета.Очистить();
			Для Каждого СтрокаПравило Из ПравилаРасчета Цикл
				Если Не СтрокаПравило.Пометка Тогда
					Продолжить;
				КонецЕсли;
				НоваяСтрокаПравило = СтруктураЭлемента.ПравилаРасчета.Добавить();
				НоваяСтрокаПравило.Правило = СтрокаПравило.Правило;
			КонецЦикла;
			ПоместитьВоВременноеХранилище(СтруктураЭлемента, АдресЭлементаВХранилище);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(АдресЭлементаВХранилище) Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ВыбранныеПравилаРасчета = ПравилаРасчета.НайтиСтроки(Новый Структура("Пометка", Истина));
	Если ВыбранныеПравилаРасчета.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбрано ни одного правила расчета.';
													|en = 'No calculation rules selected.'"), , , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, 
		ЭтотОбъект, 
		"Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура НеИспользоватьФильтрПриИзменении(Элемент)
	
	УстановитьОтборНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПравилаРасчета

&НаКлиенте
Процедура ПравилаРасчетаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ПоказатьЗначение(, Элемент.ТекущиеДанные.Правило);

КонецПроцедуры

&НаКлиенте
Процедура ПравилаРасчетаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ЗначенияЗаполнения = Новый Структура("Приемник", СтатьяБюджетов);
	ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьПравилаРасчета(Команда)
	ЗаполнитьПравилаРасчета();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ОчиститьСообщения();
	
	ДополнительныеСведения = Новый Структура();
	ДополнительныеСведения.Вставить("ИспользоватьФильтрПоОрганизации", ИспользоватьФильтрПоОрганизации);
	ДополнительныеСведения.Вставить("ИспользоватьФильтрПоПодразделению", ИспользоватьФильтрПоПодразделению);
	ДополнительныеСведения.Вставить("ИспользоватьФильтрПоСценарию", ИспользоватьФильтрПоСценарию);
	ФинансоваяОтчетностьКлиент.ЗавершитьРедактированиеЭлементаОтчета(ЭтотОбъект, ДополнительныеСведения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаОтбораПоПараметрамДоступности()
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОпределенияФильтров = Новый Структура;
	ПараметрыОпределенияФильтров.Вставить("АдресРедактируемогоЭлемента", АдресРедактируемогоЭлемента);
	ПараметрыОпределенияФильтров.Вставить("АдресЭлементовОтчета", АдресЭлементовОтчета);
	Если ЗначениеЗаполнено(АдресТаблицыЭлементов) Тогда
		ПараметрыОпределенияФильтров.Вставить("АдресТаблицыЭлементов", АдресТаблицыЭлементов);
	КонецЕсли;
	
	ПараметрыДоступностиФильтров = БюджетнаяОтчетностьРасчетКэшаСервер.ПараметрыДоступностиФильтров(Неопределено, ПараметрыОпределенияФильтров);
	ПараметрыДоступностиФильтров.Вставить("Статус", ИспользоватьФильтрПоСтатусу);
	
	Для Каждого КлючИЗначение Из ПараметрыДоступностиФильтров Цикл
		СписокЭлементовОтбора = Новый СписокЗначений;
		ВыполнятьПоискЭлементовОтбора = Ложь;
		Если КлючИЗначение.Ключ = "Сценарий"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоСценарию Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если КлючИЗначение.Ключ = "Организация"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоОрганизации Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если КлючИЗначение.Ключ = "Подразделение"
			И НЕ КлючИЗначение.Значение
			И ИспользоватьФильтрПоПодразделению Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если КлючИЗначение.Ключ = "Статус"
			И ИспользоватьФильтрПоСтатусу Тогда
			ВыполнятьПоискЭлементовОтбора = Истина;
		КонецЕсли;
		Если ВыполнятьПоискЭлементовОтбора Тогда
			СписокЭлементовОтбора = Новый СписокЗначений;
			БюджетнаяОтчетностьРасчетКэшаСервер.НайтиОтборПоИмени(
				Компоновщик.Настройки,
				КлючИЗначение.Ключ,
				СписокЭлементовОтбора,,
				Ложь);
			
			// Пересечение отборов в колонке и в доп.отборе. Флаг использования фильтра был включен.
			// Если Список элементов отбора пуст, значит пользователь очистил отбор,
			// необходимо выключить флаг использования фильтра.
			Если СписокЭлементовОтбора.Количество() = 0 Тогда
				Если КлючИЗначение.Ключ = "Сценарий" Тогда
					ИспользоватьФильтрПоСценарию = 0;
				ИначеЕсли КлючИЗначение.Ключ = "Организация" Тогда
					ИспользоватьФильтрПоОрганизации = 0;
				ИначеЕсли КлючИЗначение.Ключ = "Подразделение" Тогда
					ИспользоватьФильтрПоПодразделению = 0;
				ИначеЕсли КлючИЗначение.Ключ = "Статус" Тогда
					ИспользоватьФильтрПоСтатусу = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборНаСервере()
	
	Справочники.ЭлементыФинансовыхОтчетов.УстановитьНастройкиОтбора(ЭтотОбъект, Компоновщик, Объект.ВидЭлемента, Компоновщик.Настройки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПривилегированныйРежимПриИзменении(Элемент)
	
	ПривилегированныйРежим = Булево(ПривилегированныйРежимПереключатель);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПравилаРасчета()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяБюджетов", СтатьяБюджетов);
	Запрос.УстановитьПараметр("ПравилаРасчета", Объект.ПравилаРасчета.Выгрузить().ВыгрузитьКолонку("Правило"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Ссылка КАК Правило,
	|	ВЫБОР
	|		КОГДА ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Ссылка В (&ПравилаРасчета)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Пометка
	|ИЗ
	|	Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов КАК ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов
	|ГДЕ
	|	ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Приемник = &СтатьяБюджетов
	|	И ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПравилБюджетирования.Действует)";
	ПравилаРасчета.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

#КонецОбласти

