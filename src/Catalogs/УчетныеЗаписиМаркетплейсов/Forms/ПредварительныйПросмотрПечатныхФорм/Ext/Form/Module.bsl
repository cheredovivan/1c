
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ПечатныеДокументы.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПечатьСразу = Параметры.ПечатьСразу;
	
	ОбъектДляПечати = Новый Структура;
	ОбъектДляПечати.Вставить("Пометка", Ложь);
	ОбъектДляПечати.Вставить("Наименование", "");
	ОбъектДляПечати.Вставить("Адрес", "");
	ОбъектДляПечати.Вставить("Расширение", "");
	ОбъектДляПечати.Вставить("НомерСтроки", 0);
	
	НомерСтроки = 1;
	
	Для Каждого КлючЗначение Из Параметры.ПечатныеДокументы Цикл
		СтрокаТаблицыЗначений              = ОбъектыДляПечати.Добавить();
		СтрокаТаблицыЗначений.Пометка      = Ложь;
		СтрокаТаблицыЗначений.Наименование = КлючЗначение.Ключ;
		СтрокаТаблицыЗначений.Адрес        = КлючЗначение.Значение.АдресДвоичныхДанных;
		СтрокаТаблицыЗначений.Расширение   = КлючЗначение.Значение.РасширениеФайла;
		СтрокаТаблицыЗначений.НомерСтроки  = НомерСтроки;
		
		НомерСтроки = НомерСтроки + 1;
		
		Если ЗначениеЗаполнено(СтрокаТаблицыЗначений.Адрес)
				И ЭтоАдресВременногоХранилища(СтрокаТаблицыЗначений.Адрес) Тогда
			СтрокаТаблицыЗначений.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ОбъектыДляПечати.Количество() = 1 Тогда
		Элементы.ГруппаФлажки.Видимость     = Ложь;
		Элементы.ОбъектыДляПечати.Видимость = Ложь;
		
		ЗаполнитьЗначенияСвойств(ОбъектДляПечати, ОбъектыДляПечати[0]);
		УстановитьТекущуюСтраницу(Элементы, ОбъектДляПечати);
		ПоказатьДокументНаСервере(ОбъектДляПечати);
	ИначеЕсли Параметры.СортироватьПоНаименованию Тогда
		ОбъектыДляПечати.Сортировать("Наименование");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПечатьСразу Тогда
		Отказ = Истина;
		Печать();
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыДляПечати

&НаКлиенте
Процедура ОбъектыДляПечатиПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтраницаИзменена = УстановитьТекущуюСтраницу(Элементы, ТекущиеДанные);
	
	Если СтраницаИзменена Или ОбъектДляПечати.НомерСтроки <> ТекущиеДанные.НомерСтроки Тогда
		ПредварительныйПросмотрPDF    = Новый ДокументPDF;
		ПредварительныйПросмотрПрочие = Новый ФорматированныйДокумент;
		
		ЗаполнитьЗначенияСвойств(ОбъектДляПечати, ТекущиеДанные);
		ПодключитьОбработчикОжидания("ПоказатьДокумент", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПечать(Команда)
	
	Печать();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаТаблицыЗначений Из ОбъектыДляПечати Цикл
		СтрокаТаблицыЗначений.Пометка = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаТаблицыЗначений Из ОбъектыДляПечати Цикл
		СтрокаТаблицыЗначений.Пометка = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьТекущуюСтраницу(Элементы, ОбъектДляПечати)
	
	СтраницаИзменена = Ложь;
	
	ЭтоPDF = (ВРег(ОбъектДляПечати.Расширение) = "PDF");
	
	Если ЭтоPDF Тогда
		ТекущаяСтраница = Элементы.СтраницаДокументыPDF;
	Иначе
		ТекущаяСтраница = Элементы.СтраницаДокументыПрочие;
	КонецЕсли;
	
	Если Элементы.СтраницыПросмотра.ТекущаяСтраница <> ТекущаяСтраница Тогда
		Элементы.СтраницыПросмотра.ТекущаяСтраница = ТекущаяСтраница;
		СтраницаИзменена = Истина;
	КонецЕсли;
	
	Возврат СтраницаИзменена;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьДокумент()
	
	ПоказатьДокументНаСервере(ОбъектДляПечати);
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьДокументНаСервере(ОбъектДляПечати)
	
	Если ОбъектДляПечати = Неопределено Тогда
		ОбъектДляПечати = ОбъектыДляПечати.НайтиПоИдентификатору(Элементы.ОбъектыДляПечати.ТекущаяСтрока);
	КонецЕсли;
	
	ЭтоPDF = (ВРег(ОбъектДляПечати.Расширение) = "PDF");
	
	Если ЭтоPDF Тогда
		ПоказатьДокументPDF(ПредварительныйПросмотрPDF, ОбъектДляПечати.Адрес);
	Иначе
		ПоказатьДокументПрочие(ПредварительныйПросмотрПрочие, ОбъектДляПечати.Адрес, ОбъектДляПечати.Расширение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПоказатьДокументPDF(ПолеПросмотра, Адрес)
	
	ПотокДанных = Новый ПотокВПамяти;
	
	Если ЗначениеЗаполнено(Адрес)
			И ЭтоАдресВременногоХранилища(Адрес) Тогда
		Попытка
			ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных);
			ЗаписьДанных.Записать(ПолучитьИзВременногоХранилища(Адрес));
			ЗаписьДанных.Закрыть();
		Исключение
			ПотокДанных = Новый ПотокВПамяти;
		КонецПопытки;
	КонецЕсли;
	
	ПолеПросмотра.Прочитать(ПотокДанных);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПоказатьДокументПрочие(ПолеПросмотра, Адрес, Расширение = "")
	
	РасширенияКартинок = ИнтеграцияСМаркетплейсамиСервер.РасширенияКартинок();
	
	Если ЗначениеЗаполнено(Адрес)
			И ЭтоАдресВременногоХранилища(Адрес) Тогда
		Если Не ПустаяСтрока(Расширение)
					И СтрНайти(РасширенияКартинок, ВРег(Расширение)) <> Неопределено Тогда
			Попытка
				ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
				Картинка = Новый Картинка(ДвоичныеДанные);
				
				ПолеПросмотра.Добавить(Картинка, ТипЭлементаФорматированногоДокумента.Картинка);
			Исключение
				ПолеПросмотра.Добавить(НСтр("ru = 'Печатная форма недоступна.';
											|en = 'Print form is unavailable.'"), ТипЭлементаФорматированногоДокумента.Текст);
			КонецПопытки;
		Иначе
			ПолеПросмотра.Добавить(НСтр("ru = 'Печатная форма недоступна.';
										|en = 'Print form is unavailable.'"), ТипЭлементаФорматированногоДокумента.Текст);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Печать()
	
	#Если ВебКлиент Тогда
		ТекстОшибки = НСтр("ru = 'Печать документов в Web-клиенте не доступна.';
							|en = 'Cannot print documents in Web client.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		
	#Иначе
		Для Каждого СтрокаТаблицыЗначений Из ОбъектыДляПечати Цикл
			Если СтрокаТаблицыЗначений.Пометка
					И ЗначениеЗаполнено(СтрокаТаблицыЗначений.Адрес)
					И ЭтоАдресВременногоХранилища(СтрокаТаблицыЗначений.Адрес) Тогда
				СодержимоеПечатнойФормы = ПолучитьСодержимоеПечатнойФормы(СтрокаТаблицыЗначений.Адрес, СтрокаТаблицыЗначений.Расширение);
				
				Если СодержимоеПечатнойФормы = Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Попытка
					ДокументPDF = Новый ДокументPDF;
					ПотокДанных = Новый ПотокВПамяти;
					
					ЗаписьДанных = Новый ЗаписьДанных(ПотокДанных);
					Ждать ЗаписьДанных.ЗаписатьАсинх(СодержимоеПечатнойФормы);
					Ждать ЗаписьДанных.ЗакрытьАсинх();
					
					ДокументPDF.Прочитать(ПотокДанных);
					ДокументPDF.Напечатать(РежимИспользованияДиалогаПечати.НеИспользовать);
					
				Исключение
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Не удалось напечатать документ ""%1"" по причине: %2';
							|en = 'Cannot print the ""%1"" document due to: %2'"),
						СтрокаТаблицыЗначений.Наименование,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСодержимоеПечатнойФормы(АдресДвоичныхДанных, Расширение)
	
	ЭтоPDF = (ВРег(Расширение) = "PDF");
	
	Если Не ЭтоPDF Тогда
		ИнтеграцияСМаркетплейсамиСервер.ПреобразоватьВPDF(АдресДвоичныхДанных, Расширение);
		
		Если ПустаяСтрока(АдресДвоичныхДанных) Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПолучитьИзВременногоХранилища(АдресДвоичныхДанных);
	
КонецФункции

#КонецОбласти
