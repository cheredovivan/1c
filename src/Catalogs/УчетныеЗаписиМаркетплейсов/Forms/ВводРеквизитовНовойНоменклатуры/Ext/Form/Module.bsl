//@strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресНабораНоменклатурыКонтрагентов) Тогда
		АдресНабораНоменклатурыКонтрагентов = Параметры.АдресНабораНоменклатурыКонтрагентов;
	Иначе
		
		ТекстИсключения = НСтр("ru = 'Не передан набор номенклатуры контрагентов для создания номенклатуры.';
								|en = 'The counterparty item set for creating items is not passed.'");
		ВызватьИсключение ТекстИсключения;
		
	КонецЕсли;
	
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	
	ОпределитьВидНовойНоменклатурыИЗаполнитьПодсказки();
	
	ЗагрузитьНастройкиФормы();
	
	ПостроитьДеревоОператоровВводаФормулы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы Тогда
		СохранитьНастройкиФормы();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидНоменклатурыПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		Возврат;
		
	ИначеЕсли ВидНоменклатуры = СтарыйВидНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	ВидНоменклатурыПриИзмененииНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаРеквизитов

&НаКлиенте
Процедура ТаблицаРеквизитовЗначениеРеквизитаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	
	ТекущаяСтрока.ЗначениеЗаполнено = ЗначениеЗаполнено(ТекущаяСтрока.ЗначениеРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовЗначениеРеквизитаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	
	ПредставленияБазовыхТипов = ПредставленияБазовыхТипов();
	
	Если ТекущаяСтрока.ЭтоДопРеквизит И ТекущаяСтрока.ПредставлениеТипа = ПредставленияБазовыхТипов.Другое Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФормуВыбораЗначенияСвойства(Элемент, ТекущаяСтрока);
		
	ИначеЕсли ТекущаяСтрока.ИмяРеквизита = ИмяРеквизитаТоварнаяКатегория() Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОткрытьФормуВыбораТоварныхКатегорий(Элемент, ТекущаяСтрока);
		
	ИначеЕсли ТекущаяСтрока.ЭтоТипСтрока Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВводСтрокиЗавершение", ЭтотОбъект);
		ПоказатьВводСтроки(
			ОписаниеОповещения,
			ТекущаяСтрока.ЗначениеРеквизита,
			ТекущаяСтрока.ПредставлениеРеквизита, ,
			ТекущаяСтрока.ЭтоМногострочнаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаРеквизитовЗначениеРеквизитаБулевоПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	
	ТекущаяСтрока.ЗначениеРеквизита = ТекущаяСтрока.ЗначениеРеквизитаБулево;
	ТекущаяСтрока.ЗначениеЗаполнено = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	Если ПустаяСтрока(ТекущаяСтрока.Номенклатура)
			И ПустаяСтрока(ТекущаяСтрока.Характеристика)
			И ПустаяСтрока(ТекущаяСтрока.Упаковка) Тогда
		
		ТекущаяСтрока.Пометка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПометкаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.Товары.ТекущиеДанные;
	
	ПараметрыПримененияШаблона = НовыеПараметрыПримененияШаблона();
	
	ПараметрыПримененияШаблона.ИспользоватьХарактеристики = ИспользоватьХарактеристики;
	ПараметрыПримененияШаблона.ИспользоватьУпаковки       = ИспользоватьУпаковки;
	
	Если ТекущаяСтрока.Пометка Тогда
		
		ОчиститьСообщения();
		ПараметрыПримененияШаблона.ОперандыШаблона = ОперандыШаблонаНаименования(ШаблонНаименованияСлужебный);
		
	КонецЕсли;
	
	ИндексСтроки = Товары.Индекс(ТекущаяСтрока);
	
	ПрименитьШаблонНаименованияКСтроке(ПараметрыПримененияШаблона, ШаблонНаименования, ТекущаяСтрока, ИндексСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Отказ = УсловияПереходаПоСтраницамНеВыполнены();
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРеквизитыПоУмолчанию Тогда
		ВыполнитьПереходНаСтраницуПодбораНоменклатурыХарактеристикиУпаковки();
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодборНоменклатурыХарактеристикиУпаковки Тогда
		
		Если ЗначениеКатегории1СНеЗаполнено() Тогда
			ЗадатьВопросОПродолженииБезЗаполненияКатегории1С();
		Иначе
			ОжидатьСозданиеНовойНоменклатуры();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	ВыполнитьПереходНаСтраницуРеквизитовПоУмолчанию();
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьШаблонНаименования(Команда)
	ЗадатьВопросПередИзменениемШаблона(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьШаблонПоУказаннымРеквизитам(Команда)
	ЗадатьВопросПередИзменениемШаблона(Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсе(Команда)
	
	ОчиститьСообщения();
	УстановитьФлажкиВТаблице(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	УстановитьФлажкиВТаблице(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Общее

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Оформление характеристик номенклатуры
	
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, Элементы.ТоварыХарактеристика.Имя, "ИспользоватьХарактеристики");
	
	// Оформление реквизита с типом Булево
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизита.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовПредставлениеЗначенияРеквизита.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ЭтоТипБулево");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизитаБулево.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ЭтоТипБулево");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Оформление реквизита "Набор упаковок"
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизита.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизитаБулево.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ИмяРеквизита");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ИмяРеквизитаНаборУпаковок();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовПредставлениеЗначенияРеквизита.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ИмяРеквизита");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = ИмяРеквизитаНаборУпаковок();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Выделение незаполненных значений
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизита.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовПредставлениеЗначенияРеквизита.Имя);
	
	ОтборГруппа           = Элемент.Отбор.Элементы.Добавить(
		Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
		ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ЗаполнятьОбязательно");
		ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Истина;
		
		ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ЗначениеЗаполнено");
		ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// Отображение подсказки ввода для пустых значений
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизита.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ЗначениеЗаполнено");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Текст", Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ПредставлениеТипа"));
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	// Запрет редактирования неизменяемых реквизитов
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизита.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовЗначениеРеквизитаБулево.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаРеквизитовПредставлениеЗначенияРеквизита.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаРеквизитов.ТолькоПросмотр");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
	
	// Оформление строк, по которым не будет выполняться создание элементов
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Товары.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Товары.Пометка");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыНоменклатура.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыХарактеристика.Имя);
	
	ПолеЭлемента      = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыУпаковка.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Товары.Пометка");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьВидНовойНоменклатурыИЗаполнитьПодсказки()
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		УчетнаяЗапись, "ВидМаркетплейса, ИсточникКатегории");
	
	ИсточникКатегории = РеквизитыУчетнойЗаписи.ИсточникКатегории;
	
	ЭтоOzon = (РеквизитыУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon);
	
	Если ЭтоOzon Тогда
		
		НаборНоменклатурыКонтрагентов = НаборНоменклатурыИзВременногоХранилища(АдресНабораНоменклатурыКонтрагентов);
		
		ВидНоменклатуры = ИнтеграцияСМаркетплейсамиВызовСервера.ВидНовойНоменклатуры(
			УчетнаяЗапись,
			НаборНоменклатурыКонтрагентов);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		ВидНоменклатурыПриИзмененииНаСервере();
	Иначе
		Элементы.ГруппаТаблицаРеквизитов.Доступность = Ложь;
	КонецЕсли;
	
	ЗаполнитьПодсказкиПоИсточникуКатегории();
	
КонецПроцедуры

// Параметры:
//  АдресНабора - Строка -
// 
// Возвращаемое значение:
//  Массив из см. СопоставлениеНоменклатурыКонтрагентовКлиентСервер.НоваяНоменклатураКонтрагента
//
&НаСервереБезКонтекста
Функция НаборНоменклатурыИзВременногоХранилища(АдресНабора)
	Возврат ПолучитьИзВременногоХранилища(АдресНабора);
КонецФункции

&НаСервере
Процедура ЗаполнитьПодсказкиПоИсточникуКатегории()
	
	ТекстПодсказкиВидНоменклатуры = НСтр(
		"ru = 'Вид номенклатуры используется для:
			  |	1. Установки в качестве вида номенклатуры для новых элементов.
			  |	2. Для получения значений реквизитов по умолчанию, используемых при создании новых элементов.';
			  |en = 'The item kind is used to:
			  |	1. Be set as an item kind for new items.
			  |	2. Get the default attribute values used when creating new items.'");
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ВидНоменклатуры Тогда
		
		ТекстПодсказкиВидНоменклатуры = ТекстПодсказкиВидНоменклатуры + Символы.ПС + Символы.Таб + НСтр(
			"ru = '3. В качестве категории 1С при поиске настроек сопоставления категорий.';
			|en = '3. As a 1C category when searching for category mapping settings.'");
		
	КонецЕсли;
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры Тогда
		
		ТекстПодсказкиРодитель = НСтр(
			"ru = 'Группа списка используется для:
				  |	1. Установки в качестве группы списка для новых элементов.
				  |	2. В качестве категории 1С при поиске настроек сопоставления категорий.';
				  |en = 'The list group is used:
				  |	1. To be set as a list group for new items.
				  |	2. As a 1C category when searching for category mapping settings.'");
		
	Иначе
		
		ТекстПодсказкиРодитель = НСтр(
			"ru = 'Группа списка используется для установки в качестве группы списка для новых элементов.';
			|en = 'The list group is used to be set as a list group for new items.'");
		
	КонецЕсли;
	
	ТекстПодсказкиТаблицаРеквизитов = НСтр(
		"ru = 'В таблице указаны обязательные реквизиты, без указания значений которых создание номенклатуры невозможно.';
		|en = 'The table contains the required attributes which must be specified to create items.'");
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ТоварнаяКатегория Тогда
		
		ТекстПодсказкиТаблицаРеквизитов = ТекстПодсказкиТаблицаРеквизитов + Символы.ПС + НСтр(
			"ru = 'Товарная категория, указанная в таблице, будет использована в качестве категории 1С при поиске настроек сопоставления категорий.';
			|en = 'The product category specified in the table will be used as a 1C category when searching for category mapping settings.'");
		
	КонецЕсли;
	
	Элементы.ВидНоменклатуры.Подсказка   = ТекстПодсказкиВидНоменклатуры;
	Элементы.Родитель.Подсказка          = ТекстПодсказкиРодитель;
	Элементы.ТаблицаРеквизитов.Подсказка = ТекстПодсказкиТаблицаРеквизитов;
	
КонецПроцедуры

&НаКлиенте
Функция УсловияПереходаПоСтраницамНеВыполнены()
	
	Отказ = Ложь;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРеквизитыПоУмолчанию Тогда
		Отказ = РеквизитыЗаполненыНеПравильно();
		
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодборНоменклатурыХарактеристикиУпаковки Тогда
		Отказ = ТоварыЗаполненыНеПравильно();
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Функция РеквизитыЗаполненыНеПравильно()
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(ВидНоменклатуры) Тогда
		
		ТекстСообщения = НСтр("ru = 'Поле ""Вид номенклатуры"" не заполнено.';
								|en = 'The ""Item kind"" field is required.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ВидНоменклатуры", , Отказ);
		
	КонецЕсли;
	
	Нпп = -1;
	
	Для Каждого Строка Из ТаблицаРеквизитов Цикл
		
		Нпп = Нпп + 1;
		
		Если Не Строка.ЗаполнятьОбязательно Тогда
			Продолжить;
			
		ИначеЕсли Строка.ЗначениеЗаполнено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено.';
										|en = '""%1"" is required.'"), Строка.ПредставлениеРеквизита);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ТаблицаРеквизитов", Нпп + 1, "ЗначениеРеквизита");
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
		
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Функция ТоварыЗаполненыНеПравильно()
	
	Отказ = Ложь;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Функция ЗначениеКатегории1СНеЗаполнено()
	
	Результат = Ложь;
	
	Если Не ЭтоOzon Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИсточникИерархия = ПредопределенноеЗначение("Перечисление.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры");
	
	Если ИсточникКатегории = ИсточникИерархия Тогда
		Результат = Не ЗначениеЗаполнено(Родитель);
		
	Иначе // ТоварнаяКатегория
		
		ИмяРеквизитаТоварнаяКатегория = ИмяРеквизитаТоварнаяКатегория();
		
		Для Каждого Строка Из ТаблицаРеквизитов Цикл
			
			Если Строка.ИмяРеквизита = ИмяРеквизитаТоварнаяКатегория Тогда
				
				Результат = Не Строка.ЗначениеЗаполнено;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросОПродолженииБезЗаполненияКатегории1С()
	
	Тексты = Новый Массив; // Массив из Строка
	
	ИсточникИерархия = ПредопределенноеЗначение("Перечисление.ИсточникиКатегорийДляМаркетплейса.ИерархияНоменклатуры");
	
	Если ИсточникКатегории = ИсточникИерархия Тогда
		
		ТекстВопроса = НСтр("ru = 'Группа списка не указана.';
							|en = 'The list group is not specified.'");
		Тексты.Добавить(ТекстВопроса);
		
	Иначе // ТоварнаяКатегория
		
		ТекстВопроса = НСтр("ru = 'Товарная категория не указана.';
							|en = 'The product category is not specified.'");
		Тексты.Добавить(ТекстВопроса);
		
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Заполнение реквизитов номенклатуры по сопоставлению категорий не будет выполнено. Продолжить?';
						|en = 'Item attributes for mapping categories will not be populated. Continue?'");
	Тексты.Добавить(ТекстВопроса);
	
	ТекстВопроса = СтрСоединить(Тексты, " ");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьВопросПользователюЗавершение", ЭтотОбъект);
	
	ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
	
	ПараметрыВопроса.КнопкаПоУмолчанию = КодВозвратаДиалога.Нет;
	ПараметрыВопроса.Таймаут           = 60;
	ПараметрыВопроса.КнопкаТаймаута    = КодВозвратаДиалога.Отмена;
	ПараметрыВопроса.Заголовок         = НСтр("ru = 'Заполнение реквизитов по сопоставлению категорий';
												|en = 'Populate attributes for mapping categories'");
	
	ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
	
	КнопкиВопроса = Новый СписокЗначений; // СписокЗначений из КодВозвратаДиалога
	
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Да);
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Вернуться к заполнению';
														|en = 'Return to filling'"));
	КнопкиВопроса.Добавить(КодВозвратаДиалога.Отмена);
	
	СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
		ОписаниеОповещения, ТекстВопроса, КнопкиВопроса, ПараметрыВопроса);
	
КонецПроцедуры

// Параметры:
//  РезультатВопроса - Структура:
//   * Значение - КодВозвратаДиалога - 
//   * БольшеНеЗадаватьЭтотВопрос - Булево - 
//  ДополнительныеПараметры - Структура - 
//
&НаКлиенте
Процедура ПоказатьВопросПользователюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатВопроса.Значение = КодВозвратаДиалога.Нет Тогда
		ВыполнитьПереходНаСтраницуРеквизитовПоУмолчанию();
		
	ИначеЕсли РезультатВопроса.Значение = КодВозвратаДиалога.Да Тогда
		ОжидатьСозданиеНовойНоменклатуры();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходНаСтраницуРеквизитовПоУмолчанию()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРеквизитыПоУмолчанию;
	
	Элементы.Назад.Доступность = Ложь;
	Элементы.Далее.Заголовок = НСтр("ru = 'Далее >>';
									|en = 'Next >>'");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходНаСтраницуПодбораНоменклатурыХарактеристикиУпаковки()
	
	Если Не ИспользоватьХарактеристики И Не ИспользоватьУпаковки Тогда
		
		Элементы.ТоварыНаименования.Заголовок = НСтр("ru = 'Наименование номенклатуры';
													|en = 'Item name'");
		Элементы.ТоварыНоменклатура.ОтображатьВШапке = Ложь;
		
	Иначе
		
		Элементы.ТоварыНаименования.Заголовок = НСтр("ru = 'Наименования';
													|en = 'Names'");
		Элементы.ТоварыНоменклатура.ОтображатьВШапке = Истина;
		
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПодборНоменклатурыХарактеристикиУпаковки;
	
	Элементы.Назад.Доступность = Истина;
	Элементы.Далее.Заголовок = НСтр("ru = 'Создать номенклатуру';
									|en = 'Create products'");
	
	ВыполнитьПереходНаСтраницуПодбораНоменклатурыХарактеристикиУпаковкиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПереходНаСтраницуПодбораНоменклатурыХарактеристикиУпаковкиНаСервере()
	
	Если ПустаяСтрока(ШаблонНаименованияСлужебный) Тогда
		ЗаполнитьШаблонНаименованияПоУмолчанию();
	КонецЕсли;
	
	Элементы.ТоварыХарактеристика.Видимость = ИспользоватьХарактеристики;
	Элементы.ТоварыУпаковка.Видимость       = ИспользоватьУпаковки;
	
	ТекстПодсказкиШаблонНаименования = "";
	
	Если ИспользоватьХарактеристики И ИспользоватьУпаковки Тогда
		ТекстПодсказкиШаблонНаименования = НСтр("ru = 'Шаблон наименования используется для формирования правил, согласно которым представление товара на маркетплейсе будет разбито на наименования номенклатуры, характеристики и упаковки.';
												|en = 'The name template is used to generate the rules according to which the item presentation on the marketplace will be divided into names of items, variants, and packaging units.'");
		
	ИначеЕсли ИспользоватьХарактеристики Тогда
		ТекстПодсказкиШаблонНаименования = НСтр("ru = 'Шаблон наименования используется для формирования правил, согласно которым представление товара на маркетплейсе будет разбито на наименования номенклатуры и характеристики.';
												|en = 'The name template is used to generate the rules according to which the item presentation on the marketplace will be divided into names of items and variants.'");
		
	ИначеЕсли ИспользоватьУпаковки Тогда
		ТекстПодсказкиШаблонНаименования = НСтр("ru = 'Шаблон наименования используется для формирования правил, согласно которым представление товара на маркетплейсе будет разбито на наименования номенклатуры и упаковки.';
												|en = 'The name template is used to generate the rules according to which the item presentation on the marketplace will be divided into names of items and packaging units.'");
	КонецЕсли;
	
	Элементы.ПодсказкаШаблонНаименования.Заголовок = ТекстПодсказкиШаблонНаименования;
	Элементы.ПодсказкаШаблонНаименования.Видимость = Не ПустаяСтрока(ТекстПодсказкиШаблонНаименования);
	
	ТекстПодсказкиТовары      = НСтр("ru = 'Наименования новых элементов будут указаны согласно данным таблицы.';
									|en = 'Names of new items will be specified according to the table data.'");
	Элементы.Товары.Подсказка = ТекстПодсказкиТовары;
	
	ПроверитьСоответствиеШаблонаУказаннымНастройкам();
	
	ПрименитьШаблонНаименованияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораЗначенияСвойства(Элемент, ТекущаяСтрока)
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("Владелец", ТекущаяСтрока.Свойство);
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("РежимВыбора",   Истина);
	ПараметрыОткрытия.Вставить("ТекущаяСтрока", ТекущаяСтрока.ЗначениеРеквизита);
	ПараметрыОткрытия.Вставить("Отбор",         Отбор);
	
	ОткрытьФорму("Справочник.ЗначенияСвойствОбъектов.ФормаВыбора", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораТоварныхКатегорий(Элемент, ТекущаяСтрока)
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("Владелец",                  ВидНоменклатуры);
	Отбор.Вставить("ВладелецТоварныхКатегорий", ВладелецТоварныхКатегорий);
	
	ПараметрыОткрытия = Новый Структура;
	
	ПараметрыОткрытия.Вставить("РежимВыбора",   Истина);
	ПараметрыОткрытия.Вставить("ТекущаяСтрока", ТекущаяСтрока.ЗначениеРеквизита);
	ПараметрыОткрытия.Вставить("Отбор",         Отбор);
	
	ОткрытьФорму("Справочник.ТоварныеКатегории.ФормаВыбора", ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

// Параметры:
//  Строка - Строка -
//  ДополнительныеПараметры - Структура -
//
&НаКлиенте
Процедура ВводСтрокиЗавершение(Строка, ДополнительныеПараметры) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ТаблицаРеквизитов.ТекущиеДанные;
	
	ТекущаяСтрока.ЗначениеРеквизита = Строка;
	ТекущаяСтрока.ЗначениеЗаполнено = Не ПустаяСтрока(ТекущаяСтрока.ЗначениеРеквизита);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	ШаблонНаименованияСлужебный = СохраненныйШаблонНаименования(ИмяФормы, ШаблонНаименованияСлужебный);
	
	Если ПустаяСтрока(ШаблонНаименованияСлужебный) Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонНаименования = ШаблонНаименованияДляПользователя(ШаблонНаименованияСлужебный);
	
КонецПроцедуры

// Параметры:
//  ИмяФормы - Строка - 
//  ЗначениеПоУмолчанию - Строка - 
// 
// Возвращаемое значение:
//  Строка
//
&НаСервереБезКонтекста
Функция СохраненныйШаблонНаименования(ИмяФормы, ЗначениеПоУмолчанию)
	
	Возврат ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(
		ИмяФормы, КлючНастроекШаблонНаименования(), ЗначениеПоУмолчанию);
		
КонецФункции

&НаСервере
Процедура СохранитьНастройкиФормы()

	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(
		ИмяФормы, КлючНастроекШаблонНаименования(), ШаблонНаименованияСлужебный);

КонецПроцедуры

&НаСервереБезКонтекста
Функция КлючНастроекШаблонНаименования()
	Возврат "ШаблонНаименования";
КонецФункции

#КонецОбласти

#Область ЗаполнениеСпискаРеквизитов

&НаКлиенте
Процедура ВидНоменклатурыПриИзмененииНаКлиенте()
	
	ВидНоменклатурыПриИзмененииНаСервере();
	
	Элементы.ГруппаТаблицаРеквизитов.Доступность = ЗначениеЗаполнено(ВидНоменклатуры);
	
КонецПроцедуры

&НаСервере
Процедура ВидНоменклатурыПриИзмененииНаСервере()
	
	СтарыйВидНоменклатуры = ВидНоменклатуры;
	
	ВладелецТоварныхКатегорий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "ВладелецТоварныхКатегорий");
	
	ТаблицаРеквизитов.Очистить();
	
	ОбязательныеРеквизиты = ОбязательныеРеквизитыДляЗаполненияПоВидуНоменклатуры(ВидНоменклатуры);
	
	ШаблонНоменклатуры = Справочники.Номенклатура.СоздатьЭлемент();
	ШаблонНоменклатуры.ВидНоменклатуры = ВидНоменклатуры;
	
	Справочники.Номенклатура.ЗаполнитьРеквизитыПоВидуНоменклатуры(ШаблонНоменклатуры);
	
	ИспользоватьХарактеристики = 
		(ШаблонНоменклатуры.ИспользованиеХарактеристик
			<> Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать);
	
	ИспользоватьУпаковки = ШаблонНоменклатуры.ИспользоватьУпаковки;
	
	ПропускаемыеРеквизиты     = ПропускаемыеРеквизиты();
	ПредставленияБазовыхТипов = ПредставленияБазовыхТипов();
	РеквизитыТолькоПросмотр   = РеквизитыТолькоПросмотр();
	
	Для Каждого ОписаниеРеквизита Из ОбязательныеРеквизиты Цикл
		
		Если ПропускаемыеРеквизиты.Найти(ОписаниеРеквизита.ИмяРеквизита) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Строка = ТаблицаРеквизитов.Добавить();
		
		Строка.ИмяРеквизита   = ОписаниеРеквизита.ИмяРеквизита;
		Строка.ЭтоДопРеквизит = ОписаниеРеквизита.ЭтоДопРеквизит;
		
		Если Строка.ЭтоДопРеквизит Тогда
			
			Строка.ПредставлениеРеквизита = ОписаниеРеквизита.ИмяРеквизита;
			Строка.Свойство               = ОписаниеРеквизита.Свойство;
			
			//@skip-check statement-type-change
			// Нет смены типа значения
			Строка.ЗначениеРеквизита = ОписаниеРеквизита.Тип.ПривестиЗначение(Неопределено);
			
		Иначе
			
			Строка.ПредставлениеРеквизита = ОписаниеРеквизита.Представление;
			
			//@skip-check statement-type-change
			// Нет смены типа значения
			Строка.ЗначениеРеквизита = ШаблонНоменклатуры[Строка.ИмяРеквизита];
			
		КонецЕсли;
		
		Строка.ЗаполнятьОбязательно = ОписаниеРеквизита.ЗаполнятьОбязательно;
		
		ЗаполнитьСлужебныеРеквизитыВСтроке(Строка, ПредставленияБазовыхТипов, ОписаниеРеквизита.Тип);
		
	КонецЦикла;
	
	ТаблицаРеквизитов.Сортировать("ПредставлениеРеквизита");
	
	ДобавитьПостоянныеРеквизитыВСписокРеквизитов(
		УчетнаяЗапись, ТаблицаРеквизитов, ПредставленияБазовыхТипов, ИсточникКатегории, ШаблонНоменклатуры);
	
	ЗапретитьРедактированиеРеквизитовТолькоПросмотр(ТаблицаРеквизитов, РеквизитыТолькоПросмотр);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеРеквизитыВСтроке(Строка, ПредставленияБазовыхТипов, ОписаниеТипаРеквизита = Неопределено)
	
	ТипЗначенияРеквизита = ТипЗнч(Строка.ЗначениеРеквизита);
	
	ЭтоТипБулево = Ложь;
	ЭтоТипСтрока = Ложь;
	
	Строка.ПредставлениеТипа = ПредставлениеТипа(
		ПредставленияБазовыхТипов, ТипЗначенияРеквизита, ЭтоТипБулево, ЭтоТипСтрока);
		
	Строка.НомерКартинкиКоллекцииТипов = ОпределитьНомерКартинкиКоллекцииТипов(
		ПредставленияБазовыхТипов, Строка.ПредставлениеТипа);
	
	Строка.ЭтоТипБулево = ЭтоТипБулево;
	Строка.ЭтоТипСтрока = ЭтоТипСтрока;
	
	Если Строка.ЭтоТипБулево Тогда
		
		Строка.ЗначениеРеквизитаБулево = Строка.ЗначениеРеквизита;
		Строка.ЗначениеРеквизита       = Формат(Строка.ЗначениеРеквизита, "БЛ=Нет; БИ=Да");
		
	КонецЕсли;
	
	Если Строка.ЭтоТипСтрока И ОписаниеТипаРеквизита <> Неопределено Тогда
		Строка.ЭтоМногострочнаяСтрока = (ОписаниеТипаРеквизита.КвалификаторыСтроки.Длина = 0);
	КонецЕсли;
	
	Строка.ЗначениеЗаполнено = ЗначениеЗаполнено(Строка.ЗначениеРеквизита);
	
КонецПроцедуры

// Параметры:
//  ВидНоменклатуры - СправочникСсылка.ВидыНоменклатуры - 
// 
// Возвращаемое значение:
//  См. НоваяТаблицаОбязательныхРеквизитов
//
&НаСервереБезКонтекста
Функция ОбязательныеРеквизитыДляЗаполненияПоВидуНоменклатуры(ВидНоменклатуры)
	
	Результат = НоваяТаблицаОбязательныхРеквизитов();
	
	ИменаРеквизитов = Новый Массив; // Массив из Строка
	
	ИменаРеквизитов.Добавить("ТипНоменклатуры");
	ИменаРеквизитов.Добавить("ОсобенностьУчета");
	ИменаРеквизитов.Добавить("ИспользованиеХарактеристик");
	ИменаРеквизитов.Добавить("ИспользоватьСрокГодностиСерии");

	РеквизитыВидаНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидНоменклатуры, ИменаРеквизитов);

	//@skip-check invocation-parameter-type-intersect
	// Функция поддерживает передачу ссылки
	ВсеРеквизиты = Справочники.Номенклатура.ТаблицаНастроекРеквизитов(
		ВидНоменклатуры,
		РеквизитыВидаНоменклатуры.ТипНоменклатуры,
		РеквизитыВидаНоменклатуры.ОсобенностьУчета,
		РеквизитыВидаНоменклатуры.ИспользованиеХарактеристик,
		РеквизитыВидаНоменклатуры.ИспользоватьСрокГодностиСерии,
		"Номенклатура");
	
	Отбор = Новый Структура;
	
	Отбор.Вставить("ЗаполнятьОбязательно", Истина);
	
	РеквизитыДляКонтроля = ВсеРеквизиты.НайтиСтроки(Отбор);
	
	Для Каждого Строка Из РеквизитыДляКонтроля Цикл
		ЗаполнитьЗначенияСвойств(Результат.Добавить(), Строка);
	КонецЦикла;
	
	Возврат РеквизитыДляКонтроля;
	
КонецФункции

// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица обязательных реквизитов:
// * ИмяРеквизита - Строка - 
// * Представление - Строка - 
// * Тип - ОписаниеТипов - 
// * ЭтоДопРеквизит - Булево - 
// * Свойство - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения - 
// * ЗаполнятьОбязательно - Булево - 
//
&НаСервереБезКонтекста
Функция НоваяТаблицаОбязательныхРеквизитов()
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("ИмяРеквизита", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	Результат.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(150));
	
	Результат.Колонки.Добавить("Тип", Новый ОписаниеТипов("ОписаниеТипов"));
	
	Результат.Колонки.Добавить("ЭтоДопРеквизит", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить(
		"Свойство", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
	
	Результат.Колонки.Добавить("ЗаполнятьОбязательно", Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Массив из Строка
//
&НаСервереБезКонтекста
Функция ПропускаемыеРеквизиты()
	
	Результат = Новый Массив; // Массив из Строка
	
	Результат.Добавить("ВидНоменклатуры");
	Результат.Добавить("Родитель");
	
	Результат.Добавить("Артикул");
	Результат.Добавить("НаименованиеПолное");
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Массив из Строка
//
&НаСервереБезКонтекста
Функция РеквизитыТолькоПросмотр()
	
	Результат = Новый Массив; // Массив из Строка
	
	Результат.Добавить("ИспользоватьУпаковки");
	Результат.Добавить("НаборУпаковок");
	
	СтрогоБерутсяИзВида = Справочники.Номенклатура.РеквизитыПризнаковОсобенностейУчета(
		Метаданные.Справочники.Номенклатура); // Массив из Строка
	
	СтрогоБерутсяИзВида.Добавить("ТипНоменклатуры");
	СтрогоБерутсяИзВида.Добавить("ВариантОформленияПродажи");
	СтрогоБерутсяИзВида.Добавить("ГруппаДоступа");
	СтрогоБерутсяИзВида.Добавить("ИспользованиеХарактеристик");
	СтрогоБерутсяИзВида.Добавить("ВладелецСерий");
	СтрогоБерутсяИзВида.Добавить("ВладелецХарактеристик");
	СтрогоБерутсяИзВида.Добавить("ВладелецТоварныхКатегорий");
	СтрогоБерутсяИзВида.Добавить("ОсобенностьУчета");
	СтрогоБерутсяИзВида.Добавить("ЕдиницаИзмеренияСрокаГодности");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, СтрогоБерутсяИзВида, Истина);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
// * ПредставлениеСтроки - Строка - 
// * ПредставлениеЧисла - Строка - 
// * ПредставлениеДаты - Строка - 
// * ПредставлениеБулево - Строка - 
// * Другое - Строка - 
//
&НаКлиентеНаСервереБезКонтекста
Функция ПредставленияБазовыхТипов()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПредставлениеСтроки", НСтр("ru = '<Строка>';
													|en = '<String>'"));
	Результат.Вставить("ПредставлениеЧисла",  НСтр("ru = '<Число>';
													|en = '<Number>'"));
	Результат.Вставить("ПредставлениеДаты",   НСтр("ru = '<Дата>';
													|en = '<Date>'"));
	Результат.Вставить("ПредставлениеБулево", НСтр("ru = '<Булево>';
													|en = '<Boolean>'"));
	
	Результат.Вставить("Другое", НСтр("ru = '<Значение из списка>';
										|en = '<Value from the list>'"));
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ПредставленияБазовыхТипов - См. ПредставленияБазовыхТипов
//  ТипЗначения - Тип - 
//  ЭтоТипБулево - Булево - 
//  ЭтоТипСтрока - Булево - 
// 
// Возвращаемое значение:
//  Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеТипа(ПредставленияБазовыхТипов, ТипЗначения, ЭтоТипБулево, ЭтоТипСтрока)
	
	Результат = ПредставленияБазовыхТипов.Другое;
	
	Если ТипЗначения = Тип("Строка") Тогда
		
		Результат = ПредставленияБазовыхТипов.ПредставлениеСтроки;
		ЭтоТипСтрока = Истина;
		
	ИначеЕсли ТипЗначения = Тип("Число") Тогда
		Результат = ПредставленияБазовыхТипов.ПредставлениеЧисла;
		
	ИначеЕсли ТипЗначения = Тип("Дата") Тогда
		Результат = ПредставленияБазовыхТипов.ПредставлениеДаты;
		
	ИначеЕсли ТипЗначения = Тип("Булево") Тогда
		
		Результат = ПредставленияБазовыхТипов.ПредставлениеБулево;
		ЭтоТипБулево = Истина;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьНомерКартинкиКоллекцииТипов(ПредставленияБазовыхТипов, ПредставлениеТипа)
	
	Результат = 9; // ПредставленияБазовыхТипов.Другое
	
	Если ПредставлениеТипа = ПредставленияБазовыхТипов.ПредставлениеСтроки Тогда
		Результат = 8;
		
	ИначеЕсли ПредставлениеТипа = ПредставленияБазовыхТипов.ПредставлениеЧисла Тогда
		Результат = 13;
		
	ИначеЕсли ПредставлениеТипа = ПредставленияБазовыхТипов.ПредставлениеДаты Тогда
		Результат = 2;
		
	ИначеЕсли ПредставлениеТипа = ПредставленияБазовыхТипов.ПредставлениеБулево Тогда
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПостоянныеРеквизитыВСписокРеквизитов(УчетнаяЗапись, ТаблицаРеквизитов,
			ПредставленияБазовыхТипов, ИсточникКатегории, ШаблонНоменклатуры)
	
	ШагСдвига = 0;
	
	Если ИсточникКатегории = Перечисления.ИсточникиКатегорийДляМаркетплейса.ТоварнаяКатегория Тогда
		
		ИмяРеквизита = ИмяРеквизитаТоварнаяКатегория();
		
		ДобавитьРеквизитВСписокРеквизитов(
			ТаблицаРеквизитов,
			ПредставленияБазовыхТипов,
			ИмяРеквизита,
			ПредставлениеРеквизита(ИмяРеквизита),
			ШаблонНоменклатуры[ИмяРеквизита],
			ШагСдвига);
		
	КонецЕсли;
	
	ИспользоватьХарактеристикиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	Если ИспользоватьХарактеристикиНоменклатуры Тогда
		
		ИмяРеквизита = "ИспользованиеХарактеристик";
		ВариантИспользованияХарактеристик = ШаблонНоменклатуры[ИмяРеквизита];
		
		ДобавитьРеквизитВСписокРеквизитов(
			ТаблицаРеквизитов,
			ПредставленияБазовыхТипов,
			ИмяРеквизита,
			ПредставлениеРеквизита(ИмяРеквизита),
			ВариантИспользованияХарактеристик,
			ШагСдвига);
		
		Если ВариантИспользованияХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры Тогда
			
			ИмяРеквизита = "ВладелецХарактеристик";
			
			Строка = ДобавитьРеквизитВСписокРеквизитов(
				ТаблицаРеквизитов,
				ПредставленияБазовыхТипов,
				ИмяРеквизита,
				ПредставлениеРеквизита(ИмяРеквизита),
				ШаблонНоменклатуры[ИмяРеквизита],
				ШагСдвига);
			
			Строка.ЗаполнятьОбязательно = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ИспользоватьУпаковкиНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьУпаковкиНоменклатуры");
	
	Если ИспользоватьУпаковкиНоменклатуры Тогда
		
		ИмяРеквизита = "ИспользоватьУпаковки";
		ИспользоватьУпаковки = ШаблонНоменклатуры[ИмяРеквизита];
		
		ДобавитьРеквизитВСписокРеквизитов(
			ТаблицаРеквизитов,
			ПредставленияБазовыхТипов,
			ИмяРеквизита,
			ПредставлениеРеквизита(ИмяРеквизита),
			ИспользоватьУпаковки,
			ШагСдвига);
		
		Если ИспользоватьУпаковки Тогда
			
			ИмяРеквизита = ИмяРеквизитаНаборУпаковок();
			
			Строка = ДобавитьРеквизитВСписокРеквизитов(
				ТаблицаРеквизитов,
				ПредставленияБазовыхТипов,
				ИмяРеквизита,
				ПредставлениеРеквизита(ИмяРеквизита),
				ШаблонНоменклатуры[ИмяРеквизита],
				ШагСдвига);
				
			Если Строка.ЗначениеРеквизита = Справочники.НаборыУпаковок.ИндивидуальныйДляНоменклатуры Тогда
				Строка.ПредставлениеЗначенияРеквизита = НСтр("ru = 'Индивидуальный набор';
															|en = 'Custom set of packaging units'");
			Иначе
				
				Строка.ПредставлениеЗначенияРеквизита = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Общий набор (%1)';
						|en = 'Existing packaging group (%1)'"), Строка.ЗначениеРеквизита);
				
			КонецЕсли;
			
			Строка.ЗаполнятьОбязательно = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРеквизитаТоварнаяКатегория()
	Возврат "ТоварнаяКатегория";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяРеквизитаНаборУпаковок()
	Возврат "НаборУпаковок";
КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеРеквизита(ИмяРеквизита)
	
	Результат = Метаданные.Справочники.ВидыНоменклатуры.Реквизиты[ИмяРеквизита].Представление();
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДобавитьРеквизитВСписокРеквизитов(ТаблицаРеквизитов, ПредставленияБазовыхТипов, ИмяРеквизита,
			ПредставлениеРеквизита, ЗначениеРеквизита, ШагСдвига)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяРеквизита", ИмяРеквизита);
	
	НайденныеСтроки = ТаблицаРеквизитов.НайтиСтроки(Отбор);
	
	Если НайденныеСтроки.Количество() = 0 Тогда
		
		Строка = ТаблицаРеквизитов.Вставить(ШагСдвига);
	
		Строка.ИмяРеквизита           = ИмяРеквизита;
		Строка.ПредставлениеРеквизита = ПредставлениеРеквизита;
		
		Строка.ЗначениеРеквизита = ЗначениеРеквизита;
		
		ЗаполнитьСлужебныеРеквизитыВСтроке(Строка, ПредставленияБазовыхТипов);
		
	Иначе
		
		Строка = НайденныеСтроки[0];
		
		// Сдвигаем в начало коллекции
		//
		ИндексСтроки = ТаблицаРеквизитов.Индекс(Строка);
		ТаблицаРеквизитов.Сдвинуть(ИндексСтроки, -ИндексСтроки + ШагСдвига);
		
	КонецЕсли;
	
	ШагСдвига = ШагСдвига + 1;
	
	Возврат Строка;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗапретитьРедактированиеРеквизитовТолькоПросмотр(ТаблицаРеквизитов, РеквизитыТолькоПросмотр)
	
	Для Каждого ИмяРеквизита Из РеквизитыТолькоПросмотр Цикл
		
		Отбор = Новый Структура;
		Отбор.Вставить("ИмяРеквизита", ИмяРеквизита);
		
		НайденныеСтроки = ТаблицаРеквизитов.НайтиСтроки(Отбор);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			
			Строка.ТолькоПросмотр = Истина;
			Строка.ЭтоТипБулево   = Ложь; // Будет отображаться колонка со значением
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ШаблонНаименования

&НаСервере
Процедура ОбновитьШаблонПоУказаннымРеквизитамНаСервере()
	
	ЗаполнитьШаблонНаименованияПоУмолчанию();
	ШаблонНаименования = ШаблонНаименованияДляПользователя(ШаблонНаименованияСлужебный);
	
	ПрименитьШаблонНаименованияНаСервере(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПостроитьДеревоОператоровВводаФормулы()
	
	Дерево = РаботаСФормулами.ПолучитьПустоеДеревоОператоров();
	
	ПредставлениеГруппыОператора = НСтр("ru = 'Разделители';
										|en = 'Separators'");
	ГруппаОператоров = РаботаСФормулами.ДобавитьГруппуОператоров(Дерево, "Разделители", ПредставлениеГруппыОператора);
	
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "/", " + ""/"" + ", "/");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "\", " + ""\"" + ", "\");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "|", " + ""|"" + ", "|");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "_", " + ""_"" + ", "_");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, ",", " + "","" + ", ",");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, ".", " + ""."" + ", ".");
	
	ПредставлениеОператора = НСтр("ru = 'Пробел';
									|en = 'Whitespace'");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "Пробел", " + "" "" + ", ПредставлениеОператора);
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, "(", " + ""("" + ", "(");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, ")", " + "")"" + ", ")");
	РаботаСФормулами.ДобавитьОператор(ГруппаОператоров, """", " + """""""" + ", """");
	
	АдресХранилищаДереваОператоров = ПоместитьВоВременноеХранилище(Дерево, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ПодготовитьДеревоОперандовВводаФормулы()
	
	ДеревоОперандов = РаботаСФормулами.ПолучитьПустоеДеревоОперандов();
	
	ЗначенияКолонок = ЗначенияКолонокПоШаблонуМассив(ИспользоватьХарактеристики, ИспользоватьУпаковки);
	
	Для Каждого ИмяКолонки Из ЗначенияКолонок Цикл
		
		СтрокаДерева = НоваяСтрокаДереваОперандов(ДеревоОперандов);
		
		СтрокаДерева.Идентификатор = ИмяКолонки;
		СтрокаДерева.Представление = ИмяКолонки;
		СтрокаДерева.РазрешаетсяВыборОперанда = Истина;
		СтрокаДерева.ТипЗначения = Новый ОписаниеТипов("Строка");
		
	КонецЦикла;
	
	АдресХранения = ПоместитьВоВременноеХранилище(ДеревоОперандов, УникальныйИдентификатор);
	Возврат АдресХранения;
	
КонецФункции

// Параметры:
//  ИспользоватьХарактеристики - Булево - 
//  ИспользоватьУпаковки       - Булево - 
//
// Возвращаемое значение:
//  Массив из Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ЗначенияКолонокПоШаблонуМассив(ИспользоватьХарактеристики, ИспользоватьУпаковки)
	
	Результат = Новый Массив; // Массив из Строка
	
	Результат.Добавить("Номенклатура");
	
	Если ИспользоватьХарактеристики Тогда
		Результат.Добавить("Характеристика");
	КонецЕсли;
	
	Если ИспользоватьУпаковки Тогда
		Результат.Добавить("Упаковка");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИспользоватьХарактеристики - Булево - 
//  ИспользоватьУпаковки       - Булево - 
//
// Возвращаемое значение:
//  Структура:
//   * Номенклатура   - Строка - представление номенклатуры.
//   * Характеристика - Строка - представление характеристики. Возвращается, если в параметре ИспользоватьХарактеристики
//                      передано Истина.
//   * Упаковка       - Строка - представление упаковки. Возвращается, если в параметре ИспользоватьУпаковки
//                      передано Истина.
//
&НаКлиентеНаСервереБезКонтекста
Функция ЗначенияКолонокПоШаблону(ИспользоватьХарактеристики, ИспользоватьУпаковки)
	
	ЗначенияКолонок = ЗначенияКолонокПоШаблонуМассив(ИспользоватьХарактеристики, ИспользоватьУпаковки);
	
	Результат = Новый Структура;
	
	Для Каждого ИмяКолонки Из ЗначенияКолонок Цикл
		Результат.Вставить(ИмяКолонки, "");
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ДеревоОперандов - см. РаботаСФормулами.ПолучитьПустоеДеревоОперандов
// 
// Возвращаемое значение:
//  СтрокаДереваЗначений:
//   * Идентификатор - Строка - 
//   * Представление - Строка - 
//   * ТипЗначения - ОписаниеТипов - 
//   * РазрешаетсяВыборОперанда - Булево - 
//
&НаСервереБезКонтекста
Функция НоваяСтрокаДереваОперандов(ДеревоОперандов)
	Возврат РаботаСФормулами.НоваяСтрокаДереваОперанда(ДеревоОперандов);
КонецФункции

&НаСервере
Процедура ЗаполнитьШаблонНаименованияПоУмолчанию()
	
	ШаблонНаименованияСлужебный = "[Номенклатура]";
	
	Если ИспользоватьХарактеристики Тогда
		ШаблонНаименованияСлужебный = ШаблонНаименованияСлужебный + " + "","" + "" "" + [Характеристика]";
	КонецЕсли;
	
	Если ИспользоватьУпаковки Тогда
		ШаблонНаименованияСлужебный = ШаблонНаименованияСлужебный + " + "" "" + ""("" + [Упаковка] + "")""";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьСоответствиеШаблонаУказаннымНастройкам()
	
	ОперандыФормулы      = ОперандыШаблонаНаименования(ШаблонНаименованияСлужебный);
	ОперандыПоНастройкам = ЗначенияКолонокПоШаблонуМассив(ИспользоватьХарактеристики, ИспользоватьУпаковки);
	
	ШаблонСоответствует = ОбщегоНазначенияУТКлиентСервер.МассивыРавны(ОперандыФормулы, ОперандыПоНастройкам);
	
	Элементы.ПодсказкаШаблонНаименованияНеСоответствуетНастройкам.Видимость = Не ШаблонСоответствует;
	
	Если ШаблонСоответствует Тогда
		Возврат;
	КонецЕсли;
	
	УказаныХарактеристики = Ложь;
	УказаныУпаковки       = Ложь;
	
	Если Не ИспользоватьХарактеристики И ОперандыФормулы.Найти("Характеристика") <> Неопределено Тогда
		УказаныХарактеристики = Истина;
	КонецЕсли;
	
	Если Не ИспользоватьУпаковки И ОперандыФормулы.Найти("Упаковка") <> Неопределено Тогда
		УказаныУпаковки = Истина;
	КонецЕсли;
	
	Если УказаныХарактеристики И УказаныУпаковки Тогда
		ТекстПодсказки = НСтр("ru = 'отсутствует получение наименования Характеристики и Упаковки';
								|en = 'receiving Variant and Packaging unit names is missing'");
		
	ИначеЕсли УказаныХарактеристики Тогда
		ТекстПодсказки = НСтр("ru = 'отсутствует получение наименования Характеристики';
								|en = 'receiving Variant name is missing'");
		
	ИначеЕсли УказаныУпаковки Тогда
		ТекстПодсказки = НСтр("ru = 'отсутствует получение наименования Упаковки';
								|en = 'receiving Packaging unit name is missing'");
	КонецЕсли;
	
	ТекстПодсказкиШаблонНаименованияНеСоответствуетНастройкам = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Состав реквизитов из предыдущего шага не соответствует составу шаблона Наименования: %1. Измените реквизиты или обновите шаблон, нажав на гиперссылку ""Обновить"".';
			|en = 'The set of attributes from the previous step does not match the Name template: %1. Change the attributes or update the template by clicking ""Update"".'"),
		ТекстПодсказки);
	
	Элементы.ПодсказкаШаблонНаименованияНеСоответствуетНастройкам.Подсказка = 
		ТекстПодсказкиШаблонНаименованияНеСоответствуетНастройкам;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросПередИзменениемШаблона(ОткрытьКонструктор = Ложь, ОбновитьШаблон = Ложь)
	
	Ответ = Ждать ВопросАсинх(
		Нстр("ru = 'После изменения шаблона наименования таблица с данными будет перезаполнена по шаблону. Продолжить?';
			|en = 'After changing the name template, the data table will be refilled from template. Continue?'"),
		РежимДиалогаВопрос.ДаНет,
		60,
		КодВозвратаДиалога.Нет,
		Нстр("ru = 'Редактор шаблона';
			|en = 'Template editor'"),
		КодВозвратаДиалога.Нет); // КодВозвратаДиалога
		
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ОткрытьКонструктор Тогда
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("Формула",           ШаблонНаименованияСлужебный);
		ПараметрыОткрытия.Вставить("ДеревоОперандов",   ПодготовитьДеревоОперандовВводаФормулы());
		ПараметрыОткрытия.Вставить("ОперандыЗаголовок", НСтр("ru = 'Доступные реквизиты';
															|en = 'Available attributes'"));
		ПараметрыОткрытия.Вставить("Операторы",         АдресХранилищаДереваОператоров);
		ПараметрыОткрытия.Вставить("ТипРезультата",     Новый ОписаниеТипов("Строка"));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ИзменениеВРедактореШаблонаЗавершение", ЭтотОбъект);
		
		ОткрытьФорму(
			"ОбщаяФорма.КонструкторФормул",
			ПараметрыОткрытия,
			ЭтотОбъект, , , ,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
	КонецЕсли;
	
	Если ОбновитьШаблон Тогда
		
		ОчиститьСообщения();
		ОбновитьШаблонПоУказаннымРеквизитамНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Формула - Строка -
//  ДополнительныеПараметры - Структура -
//
&НаКлиенте
Процедура ИзменениеВРедактореШаблонаЗавершение(Формула, ДополнительныеПараметры) Экспорт
	
	Если Формула = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ШаблонНаименованияСлужебный = Формула;
	ПрименитьШаблонНаименованияНаСервере(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПрименитьШаблонНаименованияНаСервере(ЭтоПринудительноеОбновлениеДанных = Ложь)
	
	Если Не ЭтоПринудительноеОбновлениеДанных И Товары.Количество() <> 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоПринудительноеОбновлениеДанных Тогда
		
		Товары.Очистить();
		ПроверитьСоответствиеШаблонаУказаннымНастройкам();
		
	КонецЕсли;
	
	ОперандыШаблона = ОперандыШаблонаНаименования(ШаблонНаименованияСлужебный);
	
	ШаблонНаименования = ШаблонНаименованияДляПользователя(ШаблонНаименованияСлужебный, ОперандыШаблона);
	
	НаборНоменклатурыКонтрагентов = НаборНоменклатурыИзВременногоХранилища(АдресНабораНоменклатурыКонтрагентов);
	
	ПараметрыПримененияШаблона = НовыеПараметрыПримененияШаблона();
	
	ПараметрыПримененияШаблона.ИспользоватьХарактеристики = ИспользоватьХарактеристики;
	ПараметрыПримененияШаблона.ИспользоватьУпаковки       = ИспользоватьУпаковки;
	ПараметрыПримененияШаблона.ОперандыШаблона            = ОперандыШаблона;
	
	Для Каждого НоменклатураКонтрагента Из НаборНоменклатурыКонтрагентов Цикл
		
		Строка = Товары.Добавить();
		
		Строка.Пометка = Истина;
		
		ЗаполнитьЗначенияСвойств(Строка, НоменклатураКонтрагента);
		
		ИндексСтроки = Товары.Индекс(Строка);
		
		ПрименитьШаблонНаименованияКСтроке(ПараметрыПримененияШаблона, ШаблонНаименования, Строка, ИндексСтроки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ШаблонНаименованияДляПользователя(ШаблонСПараметрами, ОперандыШаблона = Неопределено)
	
	Если ОперандыШаблона = Неопределено Тогда
		ОперандыШаблона = ОперандыШаблонаНаименования(ШаблонСПараметрами);
	КонецЕсли;
	
	ЗначенияПараметров = ОбщегоНазначенияУТКлиентСервер.ПреобразоватьМассивВСтруктуруИлиСоответствие(
		ОперандыШаблона, Ложь);
	
	ШаблонБезПараметров = ОбщегоНазначенияБЭД.РезультатВычисленияФормулы(ШаблонСПараметрами, ЗначенияПараметров);
	
	Возврат ШаблонБезПараметров;
	
КонецФункции

// Параметры:
//  ШаблонНаименования - Строка - Шаблон наименования
// 
// Возвращаемое значение:
//  Массив из Строка
//
&НаКлиентеНаСервереБезКонтекста
Функция ОперандыШаблонаНаименования(ШаблонНаименования)
	Возврат РаботаСФормуламиКлиентСервер.ОперандыТекстовойФормулы(ШаблонНаименования, Истина);
КонецФункции

// Возвращаемое значение:
//  Структура:
//   * ИспользоватьХарактеристики - Булево - 
//   * ИспользоватьУпаковки - Булево - 
//   * ОперандыШаблона - см. ОперандыШаблонаНаименования 
//
&НаКлиентеНаСервереБезКонтекста
Функция НовыеПараметрыПримененияШаблона()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИспользоватьХарактеристики", Ложь);
	Результат.Вставить("ИспользоватьУпаковки",       Ложь);
	Результат.Вставить("ОперандыШаблона",            Новый Массив);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПрименитьШаблонНаименованияКСтроке(ПараметрыПримененияШаблона, ШаблонНаименования, Строка, ИндексСтроки)
	
	Если Строка.Пометка = Истина Тогда
		
		ЗначенияКолонок = ОпределитьЗначенияКолонокПоШаблону(
			ПараметрыПримененияШаблона.ИспользоватьХарактеристики,
			ПараметрыПримененияШаблона.ИспользоватьУпаковки,
			ПараметрыПримененияШаблона.ОперандыШаблона,
			ШаблонНаименования,
			Строка.Наименование);
			
	Иначе
		
		ЗначенияКолонок = ЗначенияКолонокПоШаблону(
			ПараметрыПримененияШаблона.ИспользоватьХарактеристики, ПараметрыПримененияШаблона.ИспользоватьУпаковки);
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Строка, ЗначенияКолонок);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьЗначенияКолонокПоШаблону(ИспользоватьХарактеристики, ИспользоватьУпаковки, ОперандыШаблона,
			Знач ШаблонНаименования, Знач Наименование)
	
	Результат = ЗначенияКолонокПоШаблону(ИспользоватьХарактеристики, ИспользоватьУпаковки);
	
	Если ОперандыШаблона.Количество() = 0 Тогда
		Возврат Результат;
		
	ИначеЕсли ОперандыШаблона.Количество() = 1 Тогда
		
		ЗначениеОперанда = Новый Структура(ОперандыШаблона[0], Наименование);
		ЗаполнитьЗначенияСвойств(Результат, ЗначениеОперанда);
		
		Возврат Результат;
		
	КонецЕсли;
	
	ПозицияНачалаПервогоОперанда = СтрНайти(ШаблонНаименования, ОперандыШаблона[0]);
	
	Если ПозицияНачалаПервогоОперанда <> 1 Тогда
		
		// Отсекаем префикс
		//
		Наименование       = Сред(Наименование, ПозицияНачалаПервогоОперанда);
		ШаблонНаименования = Сред(ШаблонНаименования, ПозицияНачалаПервогоОперанда);
		
	КонецЕсли;
	
	МаксимальныйИндекс = ОперандыШаблона.ВГраница();
	
	Разделители = Новый Массив; // Массив из Строка
	
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		
		Операнд = ОперандыШаблона[Индекс];
		
		ПозицияКонцаТекущего = СтрНайти(ШаблонНаименования, Операнд) + СтрДлина(Операнд);
		
		Если Индекс = МаксимальныйИндекс Тогда
			ДлинаРазделителя = СтрДлина(ШаблонНаименования) - ПозицияКонцаТекущего + 1;
		Иначе
			
			СледующийОперанд = ОперандыШаблона[Индекс + 1];
			ПозицияНачалаСледующего = СтрНайти(ШаблонНаименования, СледующийОперанд);
			
			ДлинаРазделителя = ПозицияНачалаСледующего - ПозицияКонцаТекущего;
			
		КонецЕсли;
		
		РазделительОперандов = Сред(ШаблонНаименования, ПозицияКонцаТекущего, ДлинаРазделителя);
		
		Разделители.Добавить(РазделительОперандов);
		
	КонецЦикла;
	
	ЗначенияОперандов = Новый Структура;
	
	Для Индекс = 0 По МаксимальныйИндекс Цикл
		
		Операнд = ОперандыШаблона[Индекс];
		
		ПозицияРазделителя = 0;
		Если Индекс = МаксимальныйИндекс Тогда
			ИскатьС = НаправлениеПоиска.СКонца;
		Иначе
			ИскатьС = НаправлениеПоиска.СНачала;
		КонецЕсли;
		
		Пока Индекс <= МаксимальныйИндекс Цикл
			
			Разделитель = Разделители[Индекс];
			
			Если ПустаяСтрока(Разделитель) Тогда
				
				Индекс = Индекс + 1;
				Продолжить;
				
			КонецЕсли;
			
			ПозицияРазделителя = СтрНайти(Наименование, Разделитель, ИскатьС);
			
			Если ПозицияРазделителя <> 0 Тогда
				Прервать;
			КонецЕсли;
			
			Индекс = Индекс + 1;
			
		КонецЦикла;
		
		Если ПозицияРазделителя = 0 Тогда
			ЗначениеОперанда = Наименование;
		Иначе
			ЗначениеОперанда = Лев(Наименование, ПозицияРазделителя - 1);
		КонецЕсли;
		
		ЗначенияОперандов.Вставить(Операнд, ЗначениеОперанда);
		
		Наименование = Сред(Наименование, ПозицияРазделителя + СтрДлина(Разделитель));
		
	КонецЦикла;
	
	ЗаполнитьЗначенияСвойств(Результат, ЗначенияОперандов);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьФлажкиВТаблице(Флаг)
	
	ПараметрыПримененияШаблона = НовыеПараметрыПримененияШаблона();
	
	ПараметрыПримененияШаблона.ИспользоватьХарактеристики = ИспользоватьХарактеристики;
	ПараметрыПримененияШаблона.ИспользоватьУпаковки       = ИспользоватьУпаковки;
	
	Если Флаг Тогда
		ПараметрыПримененияШаблона.ОперандыШаблона = ОперандыШаблонаНаименования(ШаблонНаименованияСлужебный);
	КонецЕсли;
	
	Для Каждого Строка Из Товары Цикл
		
		Строка.Пометка = Флаг;
		
		ИндексСтроки = Товары.Индекс(Строка);
		
		ПрименитьШаблонНаименованияКСтроке(ПараметрыПримененияШаблона, ШаблонНаименования, Строка, ИндексСтроки);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНоменклатуры

&НаКлиенте
Процедура ОжидатьСозданиеНовойНоменклатуры()
	
	Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаОжиданиеСозданияНоменклатуры;
	Элементы.ГруппаКоманднаяПанель.Видимость = Ложь;
	
	ДлительнаяОперация = ЗапуститьСозданиеНовойНоменклатуры();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СозданиеНовойНоменклатурыЗавершение", ЭтотОбъект);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания           = Ложь;
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ПрогрессВыполнения", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьСозданиеНовойНоменклатуры()
	
	ДанныеКонтрагентаДляСозданияНоменклатуры = 
		ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаДанныхКонтрагентаДляСозданияНоменклатуры();
	
	НаборНоменклатурыКонтрагентов = НаборНоменклатурыИзВременногоХранилища(АдресНабораНоменклатурыКонтрагентов);
	
	ВыделенныеСтроки = Товары.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		
		Индекс = Товары.Индекс(Строка);
		
		НоваяСтрока = ДанныеКонтрагентаДляСозданияНоменклатуры.Добавить();
		
		НоваяСтрока.НоменклатураНаименование   = Строка.Номенклатура;
		НоваяСтрока.ХарактеристикаНаименование = Строка.Характеристика;
		НоваяСтрока.УпаковкаНаименование       = Строка.Упаковка;
		
		НоваяСтрока.НоменклатураКонтрагента = НаборНоменклатурыКонтрагентов[Индекс];
		
	КонецЦикла;
	
	РеквизитыНоменклатуры = РеквизитыНовойНоменклатуры();
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	ПредставлениеМаркетплейса = ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(
		ВидМаркетплейса);
	
	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.СоздатьНоменклатуруПоДаннымКонтрагента";
	НаименованиеФоновогоЗадания = СтрШаблон(
		НСтр("ru = '%1. Создание номенклатуры по данным контрагента';
			|en = '%1. Create items based on counterparty data'"), ПредставлениеМаркетплейса);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, ИмяМетода, УчетнаяЗапись, ДанныеКонтрагентаДляСозданияНоменклатуры, РеквизитыНоменклатуры);
	
КонецФункции

&НаСервере
Функция РеквизитыНовойНоменклатуры()
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.НовыеРеквизитыНоменклатурыДляСозданияПоДаннымКонтрагента();
	
	Результат.ВидНоменклатуры   = ВидНоменклатуры;
	Результат.Родитель          = Родитель;
	Результат.ТоварнаяКатегория = Справочники.ТоварныеКатегории.ПустаяСсылка();
	
	Для Каждого СтрокаТаблицы Из ТаблицаРеквизитов Цикл
		
		Если СтрокаТаблицы.ЭтоДопРеквизит Тогда
			
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ЗначениеРеквизита) Тогда
				Продолжить;
			КонецЕсли;
			
			Результат.ДополнительныеРеквизиты.Вставить(СтрокаТаблицы.Свойство, СтрокаТаблицы.ЗначениеРеквизита);
			
		Иначе
			Результат.Вставить(СтрокаТаблицы.ИмяРеквизита, СтрокаТаблицы.ЗначениеРеквизита);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено - 
//
&НаКлиенте
Процедура СозданиеНовойНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	РезультатСоздания = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	УдалитьИзВременногоХранилища(АдресНабораНоменклатурыКонтрагентов);
	
	Закрыть(РезультатСоздания);
	
КонецПроцедуры

// Параметры:
//  Результат - см. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ДополнительныеПараметры - Неопределено - 
//
&НаКлиенте
Процедура ПрогрессВыполнения(Результат, ДополнительныеПараметры) Экспорт

	Если Результат.Статус = "Выполняется" Тогда
		
		Прогресс = ПрочитатьПрогресс(Результат.ИдентификаторЗадания);
		
		Если Прогресс <> Неопределено Тогда
			ФоновоеЗаданиеПроцент = Прогресс.Процент;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Параметры:
//   ИдентификаторЗадания - УникальныйИдентификатор - идентификатор фонового задания.
// 
// Возвращаемое значение:
//  См. ДлительныеОперации.ПрочитатьПрогресс
//
&НаСервереБезКонтекста
Функция ПрочитатьПрогресс(Знач ИдентификаторЗадания)
	Возврат ДлительныеОперации.ПрочитатьПрогресс(ИдентификаторЗадания);
КонецФункции

#КонецОбласти

#КонецОбласти
