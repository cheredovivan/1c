#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имена блокируемых реквизитов для механизма блокирования реквизитов БСП.
// 
// Возвращаемое значение:
// 	Массив из Строка - имена блокируемых реквизитов:
//		* БлокируемыйРеквизит - Строка - Имя блокируемого реквизита.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("Приемник");
	Результат.Добавить("Источник");
	
	Возврат Результат;
	
КонецФункции

// Обновляет все кэшируемые тексты запросов в правилах расчета.
//
Процедура ОбновитьХранилищаЗапросов() Экспорт
	
	Выборка = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Выбрать();
	Пока Выборка.Следующий() Цикл
		СправочникОбъект = Выборка.ПолучитьОбъект();
		Если СправочникОбъект.Статус = Перечисления.СтатусыПравилБюджетирования.Действует
			И Не СправочникОбъект.ПометкаУдаления Тогда
			ЗапросПоПравилу = ПолучитьЗапросПоНастройкамПравилаРасчетов(СправочникОбъект);
		Иначе
			ЗапросПоПравилу = Неопределено;
		КонецЕсли;
		СправочникОбъект.ХранилищеЗапроса = Новый ХранилищеЗначения(ЗапросПоПравилу);
		СправочникОбъект.ОбменДанными.Загрузка = Истина;
		СправочникОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Источник)
	|	И ЗначениеРазрешено(Приемник)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает схему компоновки данных для настройки отбора по оборотам (остаткам) статьи (показателя) бюджетов.
//
// Параметры:
//  СтатьяПоказатель - СправочникСсылка.СтатьиБюджетов,
//  				 СправочникСсылка.ПоказателиБюджетов
//  ВидыАналитикСтатьиПоказателя - Структура - структура видов аналитик статьи (показателя) бюджетов
//
// Возвращаемое значение:
//  СхемаКомпоновкиДанных
//
Функция ПолучитьСхемуКомпоновкиДанных(СтатьяПоказатель, ВидыАналитикСтатьиПоказателя) Экспорт
	
	СхемаКомпоновкиДанных = КомпоновкаДанныхСервер.ПустаяСхема();
	НаборДанных = КомпоновкаДанныхСервер.ДобавитьПустойНаборДанных(СхемаКомпоновкиДанных,
		Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"),
		"ОборотыБюджетов");
	НаборДанных.АвтоЗаполнениеДоступныхПолей = Ложь;
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтатьяПоказатель, "УчитыватьПоВалюте, УчитыватьПоКоличеству");
	
	ИзмеренияБюджетирования = Новый Соответствие;
	ИзмеренияБюджетирования.Вставить("ПериодПланирования", Новый Структура("Наименование, ТипЗначения", НСтр(
		"ru = 'Период планирования';
		|en = 'Planning period'"), ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата)));
	ИзмеренияБюджетирования.Вставить("Сценарий", Новый Структура("Наименование, ТипЗначения", НСтр("ru = 'Сценарий';
																									|en = 'Scenario'"), ,
		Новый ОписаниеТипов("СправочникСсылка.Сценарии")));
	ИзмеренияБюджетирования.Вставить("Статус", Новый Структура("Наименование, ТипЗначения", НСтр("ru = 'Статус';
																								|en = 'Status'"), ,
		Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыПланов")));
	ИзмеренияБюджетирования.Вставить("Организация", Новый Структура("Наименование, ТипЗначения", НСтр(
		"ru = 'Организация';
		|en = 'Company'"), , Новый ОписаниеТипов("СправочникСсылка.Организации")));
	ИзмеренияБюджетирования.Вставить("Подразделение", Новый Структура("Наименование, ТипЗначения", НСтр(
		"ru = 'Подразделение';
		|en = 'Department'"), , Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия")));
	Если Реквизиты.УчитыватьПоВалюте Тогда
		ИзмеренияБюджетирования.Вставить("Валюта", Новый Структура("Наименование, ТипЗначения", НСтр("ru = 'Валюта';
																									|en = 'Currency'"), ,
			Новый ОписаниеТипов("СправочникСсылка.Валюты")));
	КонецЕсли;
	
	РесурсыБюджетирования = Новый Соответствие;
	РесурсыБюджетирования.Вставить("СуммаУпр", Новый Структура("Наименование, ТипЗначения", НСтр("ru = 'Сумма (упр.)';
																								|en = 'Amount, mgmt'"),
		, РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля()));
	РесурсыБюджетирования.Вставить("СуммаРегл", Новый Структура("Наименование, ТипЗначения", НСтр("ru = 'Сумма (регл.)';
																									|en = 'Amount (local)'"),
		, РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля()));
	РесурсыБюджетирования.Вставить("СуммаСценария", Новый Структура("Наименование, ТипЗначения", НСтр(
		"ru = 'Сумма в валюте сценария';
		|en = 'Amount in the scenario currency'"), , РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля()));
	Если Реквизиты.УчитыватьПоВалюте Тогда
		РесурсыБюджетирования.Вставить("СуммаВВалюте", Новый Структура("Наименование, ТипЗначения", НСтр(
			"ru = 'Сумма сценария';
			|en = 'Scenario amount '"), , РаботаСКурсамиВалют.ОписаниеТипаДенежногоПоля()));
	КонецЕсли;
	Если Реквизиты.УчитыватьПоКоличеству Тогда
		РесурсыБюджетирования.Вставить("Количество", Новый Структура("Наименование, ТипЗначения", НСтр(
			"ru = 'Количество';
			|en = 'Quantity'"), , Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(15, 3))));
	КонецЕсли;
	
	ТекстЗапросаНабораДанных = 
	"ВЫБРАТЬ
	|	&ТекстПолейИзмерений,
	|	&ТекстПолейАналитик,
	|	&ТекстПолейРесурсов
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты({&НачалоПериода}, {&КонецПериода}, , {&ТекстУсловийИзмерений}) КАК ОборотыБюджетов
	|{ГДЕ
	|	&ТекстУсловийАналитик,
	|	&ТекстПолейРесурсов}";
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПолейРесурсов", "&ТекстПолейРесурсов
		|{ВЫБРАТЬ
		|	&ТекстПсевдонимовИзмерений,
		|	&ТекстПсевдонимовАналитик,
		|	&ТекстПсевдонимовРесурсов
		|}");
	
	ТекстПолейИзмерений = "";
	ТекстПолейАналитик = "";
	ТекстПолейРесурсов = "";
	ТекстПсевдонимовИзмерений = "";
	ТекстПсевдонимовАналитик = "";
	ТекстПсевдонимовРесурсов = "";
	ТекстУсловийИзмерений = "";
	ТекстУсловийАналитик = "";
	
	ФинОтчеты = ФинансоваяОтчетностьСервер;
	
	Для Каждого Измерение Из ИзмеренияБюджетирования Цикл
		ФинОтчеты.НовоеПолеНабора(НаборДанных, Измерение.Ключ, Измерение.Ключ, Измерение.Значение.Наименование,
			Измерение.Значение.ТипЗначения);
		ТекстПолейИзмерений = ТекстПолейИзмерений + ?(ТекстПолейИзмерений = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ОборотыБюджетов.%1 КАК %1", Измерение.Ключ);
		ТекстПсевдонимовИзмерений = ТекстПсевдонимовИзмерений + ?(ТекстПсевдонимовИзмерений = "", "", "," + Символы.ПС
			+ Символы.Таб) + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1.*", Измерение.Ключ);
		ТекстУсловийИзмерений = ТекстУсловийИзмерений + ?(ТекстУсловийИзмерений = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("(%1).* КАК %1", Измерение.Ключ);
	КонецЦикла;
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПолейИзмерений", ТекстПолейИзмерений);
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПсевдонимовИзмерений",
		ТекстПсевдонимовИзмерений);
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстУсловийИзмерений", ТекстУсловийИзмерений);
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		ВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
		Если Не ЗначениеЗаполнено(ВидыАналитикСтатьиПоказателя[ВидАналитики]) Тогда
			Продолжить;
		КонецЕсли;
		Поле = ФинОтчеты.НовоеПолеНабора(НаборДанных, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Аналитика%1", НомерАналитики));
		ВидАналитикиНаименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"ВидАналитики%1Наименование", НомерАналитики);
		Поле.Заголовок = ВидыАналитикСтатьиПоказателя[ВидАналитикиНаименование];
		ВидАналитикиТипЗначения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1ТипЗначения",
			НомерАналитики);
		Поле.ТипЗначения = ВидыАналитикСтатьиПоказателя[ВидАналитикиТипЗначения];
		ТекстПоляАналитики = "КОГДА ТИПЗНАЧЕНИЯ(ОборотыБюджетов.Аналитика%1) = ТИП(%2) ТОГДА ВЫРАЗИТЬ(ОборотыБюджетов.Аналитика%1 КАК %2)"; // @query-part
		ТекстыПолейАналитики = Новый Массив;
		Для Каждого ТипВидаАналитики Из Поле.ТипЗначения.Типы() Цикл
			ТекстыПолейАналитики.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПоляАналитики,
				НомерАналитики, ФинансовыйУчетПоДаннымБалансовыхРегистров.ПолноеИмяТипа(ТипВидаАналитики)));
		КонецЦикла;
		ТекстПолейАналитик = ТекстПолейАналитик + ?(ТекстПолейАналитик = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"ВЫБОР %1 ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ КАК Аналитика%2", СтрСоединить(ТекстыПолейАналитики, Символы.ПС
			+ Символы.Таб), НомерАналитики); //@ query-part
		ТекстПсевдонимовАналитик = ТекстПсевдонимовАналитик + ?(ТекстПсевдонимовАналитик = "", "", "," + Символы.ПС
			+ Символы.Таб) + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Аналитика%1.*", НомерАналитики); //@ query-part
		ТекстУсловийАналитик = ТекстУсловийАналитик + ?(ТекстУсловийАналитик = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"(ВЫБОР %1 ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ).* КАК Аналитика%2", СтрСоединить(ТекстыПолейАналитики, Символы.ПС
			+ Символы.Таб), НомерАналитики); //@ query-part
	КонецЦикла;
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПолейАналитик,", ?(ПустаяСтрока(
		ТекстПолейАналитик), "", ТекстПолейАналитик + ","));
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПсевдонимовАналитик,", ?(ПустаяСтрока(
		ТекстПсевдонимовАналитик), "", ТекстПсевдонимовАналитик + ","));
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстУсловийАналитик,", ?(ПустаяСтрока(
		ТекстУсловийАналитик), "", ТекстУсловийАналитик + ","));
	
	Для Каждого Ресурс Из РесурсыБюджетирования Цикл
		ФинОтчеты.НовоеПолеНабора(НаборДанных, Ресурс.Ключ, Ресурс.Ключ, Ресурс.Значение.Наименование,
			Ресурс.Значение.ТипЗначения);
		ТекстПолейРесурсов = ТекстПолейРесурсов + ?(ТекстПолейРесурсов = "", "", "," + Символы.ПС + Символы.Таб)
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ОборотыБюджетов.%1Оборот КАК %1", Ресурс.Ключ);
		ТекстПсевдонимовРесурсов = ТекстПсевдонимовРесурсов + ?(ТекстПсевдонимовРесурсов = "", "", "," + Символы.ПС
			+ Символы.Таб) + Ресурс.Ключ;
	КонецЦикла;
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПолейРесурсов", ТекстПолейРесурсов);
	ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&ТекстПсевдонимовРесурсов",
		ТекстПсевдонимовРесурсов);
	
	НаборДанных.Запрос = ТекстЗапросаНабораДанных;
	
	КомпоновкаДанныхСервер.ДобавитьПараметр(СхемаКомпоновкиДанных, "НачалоПериода", Новый ОписаниеТипов("Дата"));
	КомпоновкаДанныхСервер.ДобавитьПараметр(СхемаКомпоновкиДанных, "КонецПериода", Новый ОписаниеТипов("Дата"));
	Для Каждого Параметр Из СхемаКомпоновкиДанных.Параметры Цикл
		Параметр.ОграничениеИспользования = Истина;
		Параметр.ВключатьВДоступныеПоля = Ложь;
	КонецЦикла;
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

// Возвращает схему компоновки данных для настройки смещения периода в правиле.
//
// Параметры:
//  ВидАналитики - ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов - вид аналитики
//  ИмяТабличнойЧасти - Строка - имя табличной части, содержащей описание настроек смещения периода
//
// Возвращаемое значение:
//  СхемаКомпоновкиДанных
//
Функция ПолучитьСхемуКомпоновкиДанныхСмещенияПериода(ВидАналитики = Неопределено, ИмяТабличнойЧасти = "Произвольная") Экспорт
	
	СхемаКомпоновкиДанных = КомпоновкаДанныхСервер.ПустаяСхема();
	НаборДанных = КомпоновкаДанныхСервер.ДобавитьПустойНаборДанных(СхемаКомпоновкиДанных,
		Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"),
		ИмяТабличнойЧасти);
	
	Если ЗначениеЗаполнено(ВидАналитики) И ИмяТабличнойЧасти = "ЭтапыГрафикаОплаты" Тогда
		
		ИменаСвязанныхОбъектовМетаданных = БюджетированиеПовтИсп.ИменаТиповВидаАналитикиСодержащихТабличнуюЧасть(
			ВидАналитики, ИмяТабличнойЧасти);
		
		ШаблонЗапросаНабораДанных = 
		"ВЫБРАТЬ
		|	ТЧ_ЭтапыГрафикаОплаты.Ссылка КАК ЗначениеАналитики,
		|	ТЧ_ЭтапыГрафикаОплаты.НомерСтроки КАК НомерСтроки,
		|	ВЫБОР
		|		КОГДА ТЧ_ЭтапыГрафикаОплаты.Сдвиг > &СрокПоставки
		|			ТОГДА ТЧ_ЭтапыГрафикаОплаты.Сдвиг - &СрокПоставки
		|		ИНАЧЕ &СрокПоставки - ТЧ_ЭтапыГрафикаОплаты.Сдвиг
		|	КОНЕЦ КАК ПериодСмещения,
		|	ЗНАЧЕНИЕ(Перечисление.Периодичность.День) КАК ПериодичностьСмещения,
		|	ВЫБОР
		|		КОГДА ТЧ_ЭтапыГрафикаОплаты.Сдвиг > &СрокПоставки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СмещениеВперед,
		|	ТЧ_ЭтапыГрафикаОплаты.ПроцентПлатежа / 100 КАК Коэффициент
		|{ВЫБРАТЬ
		|	ЗначениеАналитики,
		|	НомерСтроки,
		|	ПериодСмещения,
		|	ПериодичностьСмещения,
		|	СмещениеВперед,
		|	Коэффициент}
		|ИЗ
		|	&ТабличнаяЧастьСвязанногоОбъектаМетаданных КАК ТЧ_ЭтапыГрафикаОплаты
		|ГДЕ
		|	ТЧ_ЭтапыГрафикаОплаты.ВариантОтсчета В (ЗНАЧЕНИЕ(Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыЗаказа), ЗНАЧЕНИЕ(Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыСогласования))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТЧ_ЭтапыГрафикаОплаты.Ссылка,
		|	ТЧ_ЭтапыГрафикаОплаты.НомерСтроки,
		|	ТЧ_ЭтапыГрафикаОплаты.Сдвиг,
		|	ЗНАЧЕНИЕ(Перечисление.Периодичность.День),
		|	ЛОЖЬ,
		|	ТЧ_ЭтапыГрафикаОплаты.ПроцентПлатежа / 100
		|ИЗ
		|	&ТабличнаяЧастьСвязанногоОбъектаМетаданных КАК ТЧ_ЭтапыГрафикаОплаты
		|ГДЕ
		|	ТЧ_ЭтапыГрафикаОплаты.ВариантОтсчета = ЗНАЧЕНИЕ(Перечисление.ВариантыОтсчетаДатыПлатежа.ДоДатыОтгрузки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТЧ_ЭтапыГрафикаОплаты.Ссылка,
		|	ТЧ_ЭтапыГрафикаОплаты.НомерСтроки,
		|	ТЧ_ЭтапыГрафикаОплаты.Сдвиг,
		|	ЗНАЧЕНИЕ(Перечисление.Периодичность.День),
		|	ИСТИНА,
		|	ТЧ_ЭтапыГрафикаОплаты.ПроцентПлатежа / 100
		|ИЗ
		|	&ТабличнаяЧастьСвязанногоОбъектаМетаданных КАК ТЧ_ЭтапыГрафикаОплаты
		|ГДЕ
		|	ТЧ_ЭтапыГрафикаОплаты.ВариантОтсчета = ЗНАЧЕНИЕ(Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыОтгрузки)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТЧ_ЭтапыГрафикаОплаты.Ссылка,
		|	ТЧ_ЭтапыГрафикаОплаты.НомерСтроки,
		|	ТЧ_ЭтапыГрафикаОплаты.Сдвиг + &СрокПереходаПраваСобственности,
		|	ЗНАЧЕНИЕ(Перечисление.Периодичность.День),
		|	ИСТИНА,
		|	ТЧ_ЭтапыГрафикаОплаты.ПроцентПлатежа / 100
		|ИЗ
		|	&ТабличнаяЧастьСвязанногоОбъектаМетаданных КАК ТЧ_ЭтапыГрафикаОплаты
		|ГДЕ
		|	ТЧ_ЭтапыГрафикаОплаты.ВариантОтсчета = ЗНАЧЕНИЕ(Перечисление.ВариантыОтсчетаДатыПлатежа.ОтДатыПереходаПраваСобственности)";
		
		ТекстыЗапросовНабораДанных = Новый Массив;
		Для Счетчик = 1 По ИменаСвязанныхОбъектовМетаданных.Количество() Цикл
			ИмяОбъектаМетаданных = ИменаСвязанныхОбъектовМетаданных[Счетчик-1];
			ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ИмяОбъектаМетаданных);
			ТабличнаяЧастьСвязанногоОбъектаМетаданных = ИмяОбъектаМетаданных + "." + ИмяТабличнойЧасти;
			ТекстЗапросаНабораДанных = СтрЗаменить(ШаблонЗапросаНабораДанных,
				"&ТабличнаяЧастьСвязанногоОбъектаМетаданных", ТабличнаяЧастьСвязанногоОбъектаМетаданных);
			Если Счетчик > 1 Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных,
				"{ВЫБРАТЬ
				|	ЗначениеАналитики,
				|	НомерСтроки,
				|	ПериодСмещения,
				|	ПериодичностьСмещения,
				|	СмещениеВперед,
				|	Коэффициент}", ""); // @query-part
			КонецЕсли;
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СрокПоставки", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПоставки",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ТЧ_%1.Ссылка.СрокПоставки",
					ИмяТабличнойЧасти));
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПоступления", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПоставки",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ТЧ_%1.Ссылка.Дата, ДЕНЬ), ТЧ_%1.Ссылка.ДатаПоступления, ДЕНЬ)",
					ИмяТабличнойЧасти)); // @query-part
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаОтгрузки", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПоставки",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ТЧ_%1.Ссылка.Дата, ДЕНЬ), ТЧ_%1.Ссылка.ДатаОтгрузки, ДЕНЬ)",
					ИмяТабличнойЧасти)); // @query-part
			Иначе
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПоставки", "0");
			КонецЕсли;
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("СрокПереходаПраваСобственности", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПереходаПраваСобственности",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ТЧ_%1.Ссылка.СрокПереходаПраваСобственности",
					ИмяТабличнойЧасти)); // @query-part
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаПоступления", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПереходаПраваСобственности",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ТЧ_%1.Ссылка.Дата, ДЕНЬ), ТЧ_%1.Ссылка.ДатаПоступления, ДЕНЬ)",
					ИмяТабличнойЧасти)); // @query-part
			ИначеЕсли ОбщегоНазначения.ЕстьРеквизитОбъекта("ДатаОтгрузки", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПереходаПраваСобственности",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"РАЗНОСТЬДАТ(НАЧАЛОПЕРИОДА(ТЧ_%1.Ссылка.Дата, ДЕНЬ), ТЧ_%1.Ссылка.ДатаОтгрузки, ДЕНЬ)",
					ИмяТабличнойЧасти)); // @query-part
			Иначе
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных, "&СрокПереходаПраваСобственности", "0");
			КонецЕсли;
			ТекстыЗапросовНабораДанных.Добавить(ТекстЗапросаНабораДанных);
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("ГрафикОплаты", ОбъектМетаданных) Тогда
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ТЧ_%1.Ссылка", ИмяТабличнойЧасти), "СвязанныйОбъектМетаданных.Ссылка");
				ТекстЗапросаНабораДанных = СтрЗаменить(ТекстЗапросаНабораДанных,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"%1 КАК ТЧ_%2", ТабличнаяЧастьСвязанногоОбъектаМетаданных, ИмяТабличнойЧасти),
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"Справочник.ГрафикиОплаты.Этапы КАК ТЧ_%1
					|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ %2 КАК СвязанныйОбъектМетаданных
					|	ПО ТЧ_%1.Ссылка = СвязанныйОбъектМетаданных.ГрафикОплаты", ИмяТабличнойЧасти, ИмяОбъектаМетаданных)); //@ query-part
				ТекстыЗапросовНабораДанных.Добавить(ТекстЗапросаНабораДанных);
			КонецЕсли;
		КонецЦикла;
		ТекстЗапросаНабораДанных = СтрСоединить(ТекстыЗапросовНабораДанных,
			ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
		
	Иначе
		
		ТекстЗапросаНабораДанных = 
		"ВЫБРАТЬ
		|	НЕОПРЕДЕЛЕНО КАК ЗначениеАналитики,
		|	1 КАК НомерСтроки,
		|	0 КАК ПериодСмещения,
		|	ЗНАЧЕНИЕ(Перечисление.Периодичность.День) КАК ПериодичностьСмещения,
		|	ЛОЖЬ КАК СмещениеВперед,
		|	1 КАК Коэффициент
		|{ВЫБРАТЬ
		|	ЗначениеАналитики,
		|	НомерСтроки,
		|	ПериодСмещения,
		|	ПериодичностьСмещения,
		|	СмещениеВперед,
		|	Коэффициент}";
		
	КонецЕсли;
	
	НаборДанных.Запрос = ТекстЗапросаНабораДанных;
	
	ФинОтчеты = ФинансоваяОтчетностьСервер;
	
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "ЗначениеАналитики", "ЗначениеАналитики", ,
		Метаданные.ПланыВидовХарактеристик.АналитикиСтатейБюджетов.Тип);
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "НомерСтроки", "НомерСтроки", ,
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "ПериодСмещения", "ПериодСмещения", ,
		Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(10, 0)));
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "ПериодичностьСмещения", "ПериодичностьСмещения", ,
		Новый ОписаниеТипов("ПеречислениеСсылка.Периодичность"));
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "СмещениеВперед", "СмещениеВперед", , Новый ОписаниеТипов("Булево"));
	ФинОтчеты.НовоеПолеНабора(НаборДанных, "Коэффициент", "Коэффициент", , Новый ОписаниеТипов("Число",
		Новый КвалификаторыЧисла(31, 6)));
	
	КомпоновкаДанныхСервер.ДобавитьПараметр(СхемаКомпоновкиДанных, "ВидАналитики",
		Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов"), ВидАналитики, Истина);
	
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	ФинОтчеты.НоваяГруппировка(Настройки.Структура, , , Ложь);
	
	ФинОтчеты.НовоеПолеВыбора(Настройки, "ЗначениеАналитики");
	ФинОтчеты.НовоеПолеВыбора(Настройки, "НомерСтроки");
	ФинОтчеты.НовоеПолеВыбора(Настройки, "ПериодСмещения");
	ФинОтчеты.НовоеПолеВыбора(Настройки, "ПериодичностьСмещения");
	ФинОтчеты.НовоеПолеВыбора(Настройки, "СмещениеВперед");
	ФинОтчеты.НовоеПолеВыбора(Настройки, "Коэффициент");
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции

// Возвращает запрос, сгенерированный на основании настроек правила расчетов связанных остатков и оборотов бюджетов.
//
// Параметры:
//  ПравилоРасчета - СправочникОбъект.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов
//
// Возвращаемое значение:
//  Структура:
//   *ТекстЗапроса - Строка - текст запроса
//   *ТекстЗапросаВременныеТаблицы - Строка - текст запроса временных таблиц
//   *ТекстыЗапросовБазыРаспределения - Соответствие из КлючИЗначение - тексты запросов временных таблиц с базами распределения:
//    **Ключ - Строка - имя временной таблицы
//    **Значение - Строка - текст запроса
//   *ПараметрыЗапроса - Структура - параметры запроса:
//    **ИмяПараметра - Строка - значение параметра запроса
//   *ИдентификаторПравила - Строка - хеш-сумма настроек правила
//   *ИдентификаторИсточника - Строка - ГУИД ссылки источника
//   *ИдентификаторПриемника - Строка - ГУИД ссылки приемника
//
Функция ПолучитьЗапросПоНастройкамПравилаРасчетов(ПравилоРасчета) Экспорт
	
	ЗапросПоНастройкам = Новый Структура;
	ЗапросПоНастройкам.Вставить("ТекстЗапроса", "");
	ЗапросПоНастройкам.Вставить("ПараметрыЗапроса", Новый Структура);
	ЗапросПоНастройкам.Вставить("ТекстЗапросаВременныеТаблицы", "");
	ЗапросПоНастройкам.Вставить("ТекстыЗапросовБазыРаспределения", Новый Соответствие);
	ЗапросПоНастройкам.Вставить("ИдентификаторПравила", "");
	ЗапросПоНастройкам.Вставить("ИдентификаторИсточника", "");
	ЗапросПоНастройкам.Вставить("ИдентификаторПриемника", "");
	
	ВидыАналитикИсточника = БюджетированиеСервер.ВидыАналитик(ПравилоРасчета.Источник, Истина);
	СхемаКомпоновкиДанных = ПолучитьСхемуКомпоновкиДанных(ПравилоРасчета.Источник, ВидыАналитикИсточника);
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	НастройкиКомпоновкиДанных = Неопределено;
	
	ШаблонЗапроса = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(&Приемник_Идентификатор КАК Справочник.СтатьиБюджетов) КАК СтатьяБюджетов,
	|	ПсевдонимТаблицыИсточника.ПериодДень КАК ПериодДень,
	|	ПсевдонимТаблицыИсточника.ПериодПланирования КАК ПериодПланирования,
	|	ПсевдонимТаблицыИсточника.Сценарий КАК Сценарий,
	|	ПсевдонимТаблицыИсточника.Статус КАК Статус,
	|	ПсевдонимТаблицыИсточника.Организация КАК Организация,
	|	ПсевдонимТаблицыИсточника.Подразделение КАК Подразделение,
	|	ПсевдонимТаблицыИсточника.Валюта КАК Валюта,
	|	&ТекстПолейАналитик,
	|	&ТекстПолейАналитикИсточника,
	|	ПсевдонимТаблицыИсточника.КоличествоОборот * &ОбратныйЗнак КАК КоличествоОборот,
	|	ПсевдонимТаблицыИсточника.СуммаУпрОборот * &Коэффициент * &ОбратныйЗнак КАК СуммаУпрОборот,
	|	ПсевдонимТаблицыИсточника.СуммаСценарияОборот * &Коэффициент * &ОбратныйЗнак КАК СуммаСценарияОборот,
	|	ПсевдонимТаблицыИсточника.СуммаРеглОборот * &Коэффициент * &ОбратныйЗнак КАК СуммаРеглОборот,
	|	ПсевдонимТаблицыИсточника.СуммаВВалютеОборот * &Коэффициент * &ОбратныйЗнак КАК СуммаВВалютеОборот,
	|	&Расход КАК Расход
	|ПОМЕСТИТЬ Данные
	|ИЗ
	|	РегистрНакопления.ОборотыБюджетов.Обороты(
	|			{&ПустаяДата},
	|			{&ДатаАктуальности}, АВТО,
	|			&ПараметрыПериодаПланирования
	|				И СтатьяБюджетов В (&Источник_Идентификатор)) КАК ПсевдонимТаблицыИсточника
	|ГДЕ
	|	&ТекстУсловия
	|	И &УсловияПериодаПланирования";
	ТипПоказателяБюджетов = Неопределено;
	Если ТипЗнч(ПравилоРасчета.Приемник) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
		ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "КАК Справочник.СтатьиБюджетов", "КАК Справочник.ПоказателиБюджетов"); // @query-part
		ТипПоказателяБюджетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПравилоРасчета.Приемник, "ТипПоказателя");
		Если ТипПоказателяБюджетов = Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
			ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "КАК СтатьяБюджетов,", "КАК ПоказательБюджетов," + Символы.ПС
				+ Символы.Таб + "ПсевдонимТаблицыИсточника.СтатьяБюджетов КАК СтатьяБюджетов,"); // @query-part
		КонецЕсли;
	КонецЕсли;
	ШаблонЗапроса = СтрЗаменить(ШаблонЗапроса, "КАК ПсевдонимТаблицыИсточника", "КАК ПсевдонимТаблицыИсточника"
		+ Символы.ПС + Символы.Таб + "&ТекстСоединений"); // @query-part
	
	ТекстыЗапросов = Новый Массив;
	ТекстыЗапросовВременныеТаблицы = Новый Массив;
	ТекстыЗапросовБазыРаспределения = ЗапросПоНастройкам.ТекстыЗапросовБазыРаспределения;
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	
	МаксимальныйНомерИтерации = 0;
	НомераАналитикПриемникаПоНомерамИтераций = СоответствиеВсехИсходныхНомеровАналитикПриемникаНомерамИтераций(
		ПравилоРасчета);
	НомераАналитикПоНомерамИтераций = СоответствиеНомеровАналитикПриемникаПрофиляНомерамИтераций(ПравилоРасчета);
	ИсходныеНомераАналитикПоНомерамИтераций = СоответствиеИсходныхНомеровАналитикПриемникаПрофиляНомерамИтераций(
		ПравилоРасчета);
	
	ЗапросПоНастройкам.ИдентификаторПравила = ХешНастроекПравилаРасчета(ПравилоРасчета);
	ИдентификаторИсточника = СтрЗаменить(ПравилоРасчета.Источник.УникальныйИдентификатор(), "-", "");
	ИдентификаторПриемника = СтрЗаменить(ПравилоРасчета.Приемник.УникальныйИдентификатор(), "-", "");
	ИмяПараметраИсточника = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(?(ТипЗнч(ПравилоРасчета.Источник)
		= Тип("СправочникСсылка.СтатьиБюджетов"), "СтатьяБюджетов_%1", "ПоказательБюджетов_%1"), ИдентификаторИсточника);
	ИмяПараметраПриемника = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(?(ТипЗнч(ПравилоРасчета.Приемник)
		= Тип("СправочникСсылка.СтатьиБюджетов"), "СтатьяБюджетов_%1", "ПоказательБюджетов_%1"), ИдентификаторПриемника);
	
	Для Каждого СтрокаНастройка Из ПравилоРасчета.НастройкиРасчетаДанных Цикл
		
		НастройкиКомпоновкиДанных = СтрокаНастройка.КомпоновщикНастроек.Получить();
		КомпоновкаДанныхСервер.ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, СхемаКомпоновкиДанных,
			НастройкиКомпоновкиДанных);
		НастройкиКомпоновкиДанных = КомпоновщикНастроек.ПолучитьНастройки();

		ДанныеДляЗапроса = ИсточникиДанныхСервер.ДанныеДляЗапросаКИсточникуПоНастройкамРасчетаДанных(ПравилоРасчета,
			НастройкиКомпоновкиДанных, СтрокаНастройка.ВыражениеПериодСмещения);
		
		МассивПолейАналитикИсточника = Новый Массив;
		Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
			МассивПолейАналитикИсточника.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ПсевдонимТаблицыИсточника.Аналитика%1 КАК Аналитика%1Источника", НомерАналитики));
		КонецЦикла;
		ТекстЗапроса = СтрЗаменить(ШаблонЗапроса, "&ТекстПолейАналитикИсточника", СтрСоединить(
			МассивПолейАналитикИсточника, "," + Символы.ПС + Символы.Таб));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик",
			ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ТекстПолейАналитик);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстСоединений",
			ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ТекстСоединений);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстУсловия",
			ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ТекстУсловия);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Источник_Идентификатор", "Источник_"
			+ ЗапросПоНастройкам.ИдентификаторПравила);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Приемник_Идентификатор", "Приемник_"
			+ ЗапросПоНастройкам.ИдентификаторПравила);
		ДанныеДляЗапроса.ПараметрыЗапроса.Вставить(ИмяПараметраИсточника, ПравилоРасчета.Источник);
		ДанныеДляЗапроса.ПараметрыЗапроса.Вставить(ИмяПараметраПриемника, ПравилоРасчета.Приемник);
		ЗапросПоНастройкам.ИдентификаторИсточника = ИмяПараметраИсточника;
		ЗапросПоНастройкам.ИдентификаторПриемника = ИмяПараметраПриемника;
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НомерСтроки", СтрокаНастройка.НомерСтроки);
		ИдентификаторСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1_Строка%2",
			ЗапросПоНастройкам.ИдентификаторПравила, Формат(СтрокаНастройка.НомерСтроки, "ЧГ=0"));
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ЗапросПоНастройкам.ПараметрыЗапроса,
			ДанныеДляЗапроса.ПараметрыЗапроса, Ложь);
		
		Если ЗначениеЗаполнено(СтрокаНастройка.Коэффициент) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Коэффициент",
				ФинансовыйУчетПоДаннымБалансовыхРегистров.СтрокаВыборкиЗначения(СтрокаНастройка.Коэффициент));
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Коэффициент", "1");
		КонецЕсли;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ОбратныйЗнак", ?(СтрокаНастройка.ОбратныйЗнак И ТипПоказателяБюджетов
			<> Перечисления.ТипПоказателяБюджетов.Расчетный, "-1", "1"));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Расход", ФинансовыйУчетПоДаннымБалансовыхРегистров.СтрокаВыборкиЗначения(
			СтрокаНастройка.ОбратныйЗнак));
		
		Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда

			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрыПериодаПланирования", "ИСТИНА");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияПериодаПланирования", "ИСТИНА");
			
		Иначе
			
			Если ПустаяСтрока(СтрокаНастройка.ВыражениеПериодСмещения) Или СтрНайти(
				ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ВыражениеПериодСмещения,
				СтрокаНастройка.ВыражениеПериодСмещения) > 0 Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрыПериодаПланирования",
					"ПериодПланирования МЕЖДУ &ВыражениеНачалоПериода И &ВыражениеКонецПериода");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияПериодаПланирования", "ИСТИНА");
				Если Не ПустаяСтрока(СтрокаНастройка.ВыражениеПериодСмещения) Тогда
					ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ВыражениеПериодСмещения = СтрокаНастройка.ВыражениеПериодСмещения;
				КонецЕсли;
			Иначе
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрыПериодаПланирования", "ИСТИНА");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияПериодаПланирования",
					"ПсевдонимТаблицыИсточника.ПериодПланирования МЕЖДУ &ВыражениеНачалоПериода И &ВыражениеКонецПериода");
			КонецЕсли;
			Если ЗначениеЗаполнено(СтрокаНастройка.ВариантНастройкиСмещенияПериода) Тогда
				ПериодСмещения = ?(СтрокаНастройка.ВариантНастройкиСмещенияПериода
					= Перечисления.ВариантыНастройкиСмещенияПериода.ИзАналитики,
					ДанныеДляЗапроса.ФрагментыТекстаЗапроса.ВыражениеПериодСмещения, СтрокаНастройка.ПериодСмещения);
				ВыражениеПериод = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ДОБАВИТЬКДАТЕ(ПсевдонимТаблицыИсточника.ПериодПланирования, %1, %2%3)",
					ФинансоваяОтчетностьКлиентСерверПовтИсп.ПериодичностьСтрокой(СтрокаНастройка.ПериодичностьСмещения),
					Формат(СтрокаНастройка.СмещениеВперед, "БЛ=; БИ=-"), ПериодСмещения); //@ query-part
				ВыражениеНачалоПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ДОБАВИТЬКДАТЕ(&НачалоПериода, %1, %2%3)",
					ФинансоваяОтчетностьКлиентСерверПовтИсп.ПериодичностьСтрокой(СтрокаНастройка.ПериодичностьСмещения),
					Формат(СтрокаНастройка.СмещениеВперед, "БЛ=-; БИ="), ПериодСмещения); //@ query-part
				ВыражениеКонецПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ДОБАВИТЬКДАТЕ(КОНЕЦПЕРИОДА(&КонецПериода, %1), %1, %2%3)",
					ФинансоваяОтчетностьКлиентСерверПовтИсп.ПериодичностьСтрокой(СтрокаНастройка.ПериодичностьСмещения),
					Формат(СтрокаНастройка.СмещениеВперед, "БЛ=-; БИ="), ПериодСмещения); //@ query-part
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
					"ПсевдонимТаблицыИсточника.ПериодПланирования КАК ПериодПланирования", ВыражениеПериод
					+ " КАК ПериодПланирования"); // @query-part
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеНачалоПериода", ВыражениеНачалоПериода);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКонецПериода", ВыражениеКонецПериода);
			ИначеЕсли ТипПоказателяБюджетов <> Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеНачалоПериода", "&НачалоПериода");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКонецПериода", "КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)"); //@ query-part
			КонецЕсли;
			
		КонецЕсли;
		
		Если ПравилоРасчета.НастройкиРасчетаДанных.Индекс(СтрокаНастройка) = 0 Тогда
		
			ДобавитьТекстыЗапросовБазыРаспределенияДляПолученияАналитик(ТекстыЗапросовБазыРаспределения,
				ЗапросПоНастройкам.ПараметрыЗапроса, ПравилоРасчета, МаксимальныйНомерИтерации);
		
		КонецЕсли;
		
		ИспользоватьПредварительныеДанные = ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП)
			Или ЗначениеЗаполнено(СтрокаНастройка.ПрофильРаспределения)
			Или МаксимальныйНомерИтерации > 0
			Или СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику;
		Если ИспользоватьПредварительныеДанные И ТипПоказателяБюджетов = Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
			ТекстОшибки = НСтр("ru = 'Некорректные настройки правила для расчета показателя %1 по источнику %2.';
								|en = 'Incorrect settings of the rule for calculating the %1 indicator by the %2 source.'");
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, ПравилоРасчета.Приемник,
				ПравилоРасчета.Источник);
		КонецЕсли;
		Если ИспользоватьПредварительныеДанные Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ Данные",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПОМЕСТИТЬ ПредварительныеДанные_%1",
				ИдентификаторСтроки)); // @query-part
			ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
		ИначеЕсли ТекстыЗапросов.Количество() > 0 Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ Данные", ""); // @query-part
		КонецЕсли;

		Если ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП) Тогда
			ДобавитьТекстЗапросаКоэффициентыПересчетаПоНФП(ТекстыЗапросовБазыРаспределения, СтрокаНастройка,
				ЗапросПоНастройкам.ПараметрыЗапроса);
		КонецЕсли;
		
		ИмяВременнойТаблицыНастройкиСмещенияПериода = "";
		НомерВидаАналитики = "";
		Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда
			ДобавитьТекстыЗапросовНастройкиСмещенияПериодаСКД(ИмяВременнойТаблицыНастройкиСмещенияПериода, НомерВидаАналитики,
				ТекстыЗапросовБазыРаспределения, СтрокаНастройка, ПравилоРасчета);
		КонецЕсли;

		Если ЗначениеЗаполнено(СтрокаНастройка.ПрофильРаспределения) Тогда
			ДобавитьТекстыЗапросовБазыРаспределенияПоПериодам(ТекстыЗапросовБазыРаспределения, СтрокаНастройка,
				ЗапросПоНастройкам.ПараметрыЗапроса);
		КонецЕсли;
		
		Если ИспользоватьПредварительныеДанные Тогда
		
			ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ПредварительныеДанные.СтатьяБюджетов КАК СтатьяБюджетов,
			|	ПредварительныеДанные.ПериодДень КАК ПериодДень,
			|	ПредварительныеДанные.ПериодПланирования КАК ПериодПланирования,
			|	ПредварительныеДанные.Сценарий КАК Сценарий,
			|	ПредварительныеДанные.Статус КАК Статус,
			|	ПредварительныеДанные.Организация КАК Организация,
			|	ПредварительныеДанные.Подразделение КАК Подразделение,
			|	ПредварительныеДанные.Валюта КАК Валюта,
			|	&ТекстПолейАналитик,
			|	&ТекстПолейАналитикИсточника,
			|	ПредварительныеДанные.КоличествоОборот КАК КоличествоОборот,
			|	ПредварительныеДанные.СуммаУпрОборот * &КоэффициентНФП КАК СуммаУпрОборот,
			|	ПредварительныеДанные.СуммаСценарияОборот * &КоэффициентНФП КАК СуммаСценарияОборот,
			|	ПредварительныеДанные.СуммаРеглОборот * &КоэффициентНФП КАК СуммаРеглОборот,
			|	ПредварительныеДанные.СуммаВВалютеОборот * &КоэффициентНФП КАК СуммаВВалютеОборот,
			|	ПредварительныеДанные.Расход КАК Расход
			|ПОМЕСТИТЬ Данные
			|ИЗ
			|	ПредварительныеДанные КАК ПредварительныеДанные";
			Если ТипПоказателяБюджетов = Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КАК СтатьяБюджетов,", "КАК СтатьяБюджетов," + Символы.ПС + Символы.Таб
					+ "ПредварительныеДанные.ПоказательБюджетов КАК ПоказательБюджетов,"); // @query-part
			КонецЕсли;
			МассивПолейАналитик = Новый Массив;
			МассивПолейАналитикИсточника = Новый Массив;
			Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
				МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ПредварительныеДанные.Аналитика%1 КАК Аналитика%1", НомерАналитики));
				МассивПолейАналитикИсточника.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ПредварительныеДанные.Аналитика%1Источника КАК Аналитика%1Источника", НомерАналитики));
			КонецЦикла;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитикИсточника", СтрСоединить(МассивПолейАналитикИсточника, ","
				+ Символы.ПС + Символы.Таб));
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик", СтрСоединить(МассивПолейАналитик, ","
				+ Символы.ПС + Символы.Таб));
			
			Если ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП) Тогда
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоэффициентНФП", "ЕСТЬNULL(КоэффициентыНФП.ЗначениеПоказателя, 1)");
				ТекстЗапроса = ТекстЗапроса + "
				|		ЛЕВОЕ СОЕДИНЕНИЕ КоэффициентыНФП КАК КоэффициентыНФП
				|		ПО &УсловиеПоОрганизациям
				|			И &УсловиеПоПодразделениям
				|			И &УсловиеПоСценариям"; //@ query-part
				
				ИдентификаторНФП = СтрЗаменить(СтрокаНастройка.КоэффициентНФП.УникальныйИдентификатор(), "-", "");
				РеквизитыНФП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаНастройка.КоэффициентНФП,
					"ПоОрганизациям, ПоПодразделениям, ПоСценариям");
				Если РеквизитыНФП.ПоОрганизациям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"ПредварительныеДанные.Организация = КоэффициентыНФП.Организация");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"КоэффициентыНФП.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоПодразделениям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"ПредварительныеДанные.Подразделение = КоэффициентыНФП.Подразделение");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"КоэффициентыНФП.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоСценариям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"ПредварительныеДанные.Сценарий = КоэффициентыНФП.Сценарий");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"КоэффициентыНФП.Сценарий = ЗНАЧЕНИЕ(Справочник.Сценарии.ПустаяСсылка)"); //@ query-part
				КонецЕсли;

				СоответствиеАналитик = БюджетированиеПовтИсп.СоответствиеАналитикСтатьиПоказателяБюджетовСНФП(
					ПравилоРасчета.Источник, СтрокаНастройка.КоэффициентНФП);
				Если ТипЗнч(СоответствиеАналитик) = Тип("Соответствие") И СоответствиеАналитик.Количество() > 0 Тогда
					МассивУсловийСоединения = Новый Массив;
					Для Каждого КлючИЗначение Из СоответствиеАналитик Цикл
						МассивУсловийСоединения.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"И ПредварительныеДанные.Аналитика%1Источника = КоэффициентыНФП.Аналитика%2", // @query-part
							КлючИЗначение.Значение, КлючИЗначение.Ключ));
					КонецЦикла;
					ТекстЗапроса = ТекстЗапроса + Символы.ПС + Символы.Таб + СтрСоединить(МассивУсловийСоединения, Символы.ПС + Символы.Таб);
				КонецЕсли;

				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КоэффициентыНФП",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("КоэффициентыНФП_%1", ИдентификаторНФП));
			Иначе
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоэффициентНФП", "1");
			КонецЕсли;
			
			Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда
				
				ТекстЗапроса = ТекстЗапроса + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("
				|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиСмещенияПериода_Коэффициенты КАК НастройкиСмещенияПериода_Коэффициенты
				|		ПО ПредварительныеДанные.Аналитика%1Источника = НастройкиСмещенияПериода_Коэффициенты.ЗначениеАналитики
				|		ЛЕВОЕ СОЕДИНЕНИЕ  НастройкиСмещенияПериода_Сумма КАК  НастройкиСмещенияПериода_Сумма
				|		ПО ПредварительныеДанные.Аналитика%1Источника = НастройкиСмещенияПериода_Сумма.ЗначениеАналитики", НомерВидаАналитики); //@ query-part
				
				РесурсыИсточника = Новый Массив;
				РесурсыИсточника.Добавить("ПредварительныеДанные.КоличествоОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаУпрОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаСценарияОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаРеглОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаВВалютеОборот");
				Для Каждого Ресурс Из РесурсыИсточника Цикл
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Ресурс, ОбщегоНазначенияУТ.ТекстРаспределенияСуммы(Ресурс,
						"ЕСТЬNULL(НастройкиСмещенияПериода_Коэффициенты.Коэффициент, 0)",
						"ЕСТЬNULL(НастройкиСмещенияПериода_Коэффициенты.НакопленныйКоэффициент, 0)",
						"ЕСТЬNULL(НастройкиСмещенияПериода_Сумма.СуммарныйКоэффициент, 0)")); //@ query-part
				КонецЦикла;
			
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаНастройка.ПрофильРаспределения) Тогда
				
				ТекстЗапроса = ТекстЗапроса + "
				|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения_Коэффициенты КАК БазаРаспределения_Коэффициенты
				|		ПО &УсловиеПоОрганизациям
				|			И &УсловиеПоПодразделениям
				|			И &УсловиеПоСценариям
				|			И &УсловиеСоединенияПолейАналитикКоэффициенты
				|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения_Сумма КАК БазаРаспределения_Сумма
				|		ПО &УсловиеПоОрганизациям
				|			И &УсловиеПоПодразделениям
				|			И &УсловиеПоСценариям
				|			И &УсловиеСоединенияПолейАналитикСумма"; //@ query-part
				
				ИдентификаторНФП = СтрЗаменить(СтрокаНастройка.ПрофильРаспределения.УникальныйИдентификатор(), "-", "");
				РеквизитыНФП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаНастройка.ПрофильРаспределения,
					"ПоОрганизациям, ПоПодразделениям, ПоСценариям, Периодичность, ПериодичностьПодпериодов");
				Если РеквизитыНФП.ПоОрганизациям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"ПредварительныеДанные.Организация = БазаРаспределения_Коэффициенты.Организация");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"БазаРаспределения_Коэффициенты.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоПодразделениям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"ПредварительныеДанные.Подразделение = БазаРаспределения_Коэффициенты.Подразделение");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"БазаРаспределения_Коэффициенты.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоСценариям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"ПредварительныеДанные.Сценарий = БазаРаспределения_Коэффициенты.Сценарий");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"БазаРаспределения_Коэффициенты.Сценарий = ЗНАЧЕНИЕ(Справочник.Сценарии.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				
				РесурсыИсточника = Новый Массив;
				РесурсыИсточника.Добавить("ПредварительныеДанные.КоличествоОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаУпрОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаСценарияОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаРеглОборот");
				РесурсыИсточника.Добавить("ПредварительныеДанные.СуммаВВалютеОборот");
				Для Каждого Ресурс Из РесурсыИсточника Цикл
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Ресурс, ОбщегоНазначенияУТ.ТекстРаспределенияСуммы(Ресурс,
						"ЕСТЬNULL(БазаРаспределения_Коэффициенты.Коэффициент, 0)",
						"ЕСТЬNULL(БазаРаспределения_Коэффициенты.НакопленныйКоэффициент, 0)",
						"ЕСТЬNULL(БазаРаспределения_Сумма.СуммарныйКоэффициент, 0)"));
				КонецЦикла;
				
				СоответствиеАналитик = БюджетированиеПовтИсп.СоответствиеАналитикСтатьиПоказателяБюджетовСНФП(
					ПравилоРасчета.Источник, СтрокаНастройка.ПрофильРаспределения);
				Если ТипЗнч(СоответствиеАналитик) = Тип("Соответствие") И СоответствиеАналитик.Количество() > 0 Тогда
					МассивУсловийСоединенияКоэффициенты = Новый Массив;
					МассивУсловийСоединенияСумма = Новый Массив;
					Для Каждого КлючИЗначение Из СоответствиеАналитик Цикл
						МассивУсловийСоединенияКоэффициенты.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ПредварительныеДанные.Аналитика%1Источника = БазаРаспределения_Коэффициенты.Аналитика%2",
							КлючИЗначение.Значение, КлючИЗначение.Ключ));
						МассивУсловийСоединенияСумма.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ПредварительныеДанные.Аналитика%1Источника = БазаРаспределения_Сумма.Аналитика%2",
							КлючИЗначение.Значение, КлючИЗначение.Ключ));
					КонецЦикла;
					УсловиеСоединенияПолейАналитикКоэффициенты = СтрСоединить(МассивУсловийСоединенияКоэффициенты,
						Символы.ПС + Символы.Таб + "И ");
					УсловиеСоединенияПолейАналитикСумма = СтрСоединить(МассивУсловийСоединенияСумма, Символы.ПС
						+ Символы.Таб + "И ");
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикКоэффициенты", ?(
						ПустаяСтрока(УсловиеСоединенияПолейАналитикКоэффициенты), "ИСТИНА",
						УсловиеСоединенияПолейАналитикКоэффициенты));
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикСумма", ?(ПустаяСтрока(
						УсловиеСоединенияПолейАналитикСумма), "ИСТИНА", УсловиеСоединенияПолейАналитикСумма));
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикКоэффициенты", "ИСТИНА");
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикСумма", "ИСТИНА");
				КонецЕсли;
				
				ВыражениеПериод = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ВЫБОР
					|	КОГДА БазаРаспределения_Коэффициенты.НомерПодПериода ЕСТЬ NULL
					|		ИЛИ БазаРаспределения_Коэффициенты.НомерПодПериода = 1
					|		ТОГДА НАЧАЛОПЕРИОДА(ПредварительныеДанные.ПериодПланирования, %1)
					|	ИНАЧЕ ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ПредварительныеДанные.ПериодПланирования, %1), %2, БазаРаспределения_Коэффициенты.НомерПодПериода-1)
					|КОНЕЦ КАК ПериодПланирования",
					ФинансоваяОтчетностьКлиентСерверПовтИсп.ПериодичностьСтрокой(РеквизитыНФП.Периодичность),
					ФинансоваяОтчетностьКлиентСерверПовтИсп.ПериодичностьСтрокой(РеквизитыНФП.ПериодичностьПодпериодов));
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПредварительныеДанные.ПериодПланирования КАК ПериодПланирования", //@ query-part
					ВыражениеПериод);

				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("БазаРаспределения_%1",
					ИдентификаторНФП));
				
			КонецЕсли;
					
			Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда
				
				ТекстЗапроса = ТекстЗапроса + "
					|ГДЕ
					|	ПредварительныеДанные.ПериодПланирования МЕЖДУ &ВыражениеНачалоПериода И &ВыражениеКонецПериода"; //@ query-part
				
				ВыражениеУсловиеПериодичности = "ВЫБОР НастройкиСмещенияПериода_Коэффициенты.ПериодичностьСмещения
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%1, ДЕНЬ, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%2, НЕДЕЛЯ, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%3, ДЕКАДА, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%4, МЕСЯЦ, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%5, КВАРТАЛ, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	КОГДА ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
					|		ТОГДА ДОБАВИТЬКДАТЕ(%6, ПОЛУГОДИЕ, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|	ИНАЧЕ ДОБАВИТЬКДАТЕ(%7, ГОД, ВЫБОР
					|			КОГДА НастройкиСмещенияПериода_Коэффициенты.СмещениеВперед
					|				ТОГДА %8НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|			ИНАЧЕ %9НастройкиСмещенияПериода_Коэффициенты.ПериодСмещения
					|		КОНЕЦ)
					|КОНЕЦ"; // @query-part
				ВыражениеПериод = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ВыражениеУсловиеПериодичности,
					"ПредварительныеДанные.ПериодПланирования", "ПредварительныеДанные.ПериодПланирования",
					"ПредварительныеДанные.ПериодПланирования", "ПредварительныеДанные.ПериодПланирования",
					"ПредварительныеДанные.ПериодПланирования", "ПредварительныеДанные.ПериодПланирования",
					"ПредварительныеДанные.ПериодПланирования", "", "-");
				ВыражениеНачалоПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ВыражениеУсловиеПериодичности, "&НачалоПериода", "&НачалоПериода", "&НачалоПериода", "&НачалоПериода",
					"&НачалоПериода", "&НачалоПериода", "&НачалоПериода", "-", "");
				ВыражениеКонецПериода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ВыражениеУсловиеПериодичности, "КОНЕЦПЕРИОДА(&КонецПериода, ДЕНЬ)",
					"КОНЕЦПЕРИОДА(&КонецПериода, НЕДЕЛЯ)", "КОНЕЦПЕРИОДА(&КонецПериода, ДЕКАДА)",
					"КОНЕЦПЕРИОДА(&КонецПериода, МЕСЯЦ)", "КОНЕЦПЕРИОДА(&КонецПериода, КВАРТАЛ)",
					"КОНЕЦПЕРИОДА(&КонецПериода, ПОЛУГОДИЕ)", "КОНЕЦПЕРИОДА(&КонецПериода, ГОД)", "-", ""); //@ query-part
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
					"ПредварительныеДанные.ПериодПланирования КАК ПериодПланирования", ВыражениеПериод
					+ " КАК ПериодПланирования"); // @query-part
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеНачалоПериода", ВыражениеНачалоПериода);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКонецПериода", ВыражениеКонецПериода);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "НастройкиСмещенияПериода", ИмяВременнойТаблицыНастройкиСмещенияПериода);
				
			КонецЕсли;
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПредварительныеДанные",
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПредварительныеДанные_%1",
				ИдентификаторСтроки));
			
			Для НомерИтерации = 1 По МаксимальныйНомерИтерации Цикл
				
				ИдентификаторИтерацииСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"%1_Итерация%2Строка%3", ЗапросПоНастройкам.ИдентификаторПравила, НомерИтерации, Формат(
					СтрокаНастройка.НомерСтроки, "ЧГ=0"));

				Если Не ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП)
					И Не ЗначениеЗаполнено(СтрокаНастройка.ПрофильРаспределения)
					И НомерИтерации = 1 Тогда
					ТекстЗапроса = ТекстыЗапросовВременныеТаблицы[ТекстыЗапросовВременныеТаблицы.ВГраница()];
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПОМЕСТИТЬ ПредварительныеДанные_%1", ИдентификаторСтроки),
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПОМЕСТИТЬ ДанныеИтерации_%1", ИдентификаторИтерацииСтроки));
					ТекстыЗапросовВременныеТаблицы[ТекстыЗапросовВременныеТаблицы.ВГраница()] = ТекстЗапроса;
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ Данные",
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ПОМЕСТИТЬ ДанныеИтерации_%1", //@ query-part
						ИдентификаторИтерацииСтроки));
					ТекстЗапроса = ТекстЗапроса + "
						|
						|ИНДЕКСИРОВАТЬ ПО
						|	Организация,
						|	Подразделение,
						|	Сценарий"; //@ query-part
					ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
				КонецЕсли;
				
				Если НомерИтерации = 1 Тогда
					
					Если ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП)
						Или ЗначениеЗаполнено(СтрокаНастройка.ПрофильРаспределения) Тогда
						
						ФинансовыйУчетПоДаннымБалансовыхРегистров.ДобавитьЗапросУдаленияВременнойТаблицы(
							ТекстыЗапросовВременныеТаблицы, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ПредварительныеДанные_%1", ИдентификаторСтроки));
						
					КонецЕсли;
					
				Иначе
					
					ИдентификаторИтерацииПредыдущейСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"%1_Итерация%2Строка%3", ЗапросПоНастройкам.ИдентификаторПравила, НомерИтерации - 1, Формат(
						СтрокаНастройка.НомерСтроки, "ЧГ=0"));
				
					ФинансовыйУчетПоДаннымБалансовыхРегистров.ДобавитьЗапросУдаленияВременнойТаблицы(
						ТекстыЗапросовВременныеТаблицы, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"ДанныеИтерации_%1", ИдентификаторИтерацииПредыдущейСтроки));
					
				КонецЕсли;
				
				ТекстЗапроса = 
				"ВЫБРАТЬ
				|	ДанныеИтерации.СтатьяБюджетов КАК СтатьяБюджетов,
				|	ДанныеИтерации.ПериодДень КАК ПериодДень,
				|	ДанныеИтерации.ПериодПланирования КАК ПериодПланирования,
				|	ДанныеИтерации.Сценарий КАК Сценарий,
				|	ДанныеИтерации.Статус КАК Статус,
				|	ДанныеИтерации.Организация КАК Организация,
				|	ДанныеИтерации.Подразделение КАК Подразделение,
				|	ДанныеИтерации.Валюта КАК Валюта,
				|	&ТекстПолейАналитик,
				|	&ТекстПолейАналитикИсточника,
				|	ДанныеИтерации.КоличествоОборот КАК КоличествоОборот,
				|	ДанныеИтерации.СуммаУпрОборот КАК СуммаУпрОборот,
				|	ДанныеИтерации.СуммаСценарияОборот КАК СуммаСценарияОборот,
				|	ДанныеИтерации.СуммаРеглОборот КАК СуммаРеглОборот,
				|	ДанныеИтерации.СуммаВВалютеОборот КАК СуммаВВалютеОборот,
				|	ДанныеИтерации.Расход КАК Расход
				|ПОМЕСТИТЬ Данные
				|ИЗ
				|	ДанныеИтерации КАК ДанныеИтерации
				|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения_Коэффициенты КАК БазаРаспределения_Коэффициенты
				|		ПО &УсловиеПоОрганизациям
				|			И &УсловиеПоПодразделениям
				|			И &УсловиеПоСценариям
				|			И &УсловиеСоединенияПолейАналитикКоэффициенты
				|		ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения_Сумма КАК БазаРаспределения_Сумма
				|		ПО &УсловиеПоОрганизациям
				|			И &УсловиеПоПодразделениям
				|			И &УсловиеПоСценариям
				|			И &УсловиеСоединенияПолейАналитикСумма";
				
				СтрокаПрофильРаспределения = ПравилоРасчета.НастройкиЗаполненияАналитик.Найти(НомерИтерации,
					"НомерИтерации");
				ПрофильРаспределения = СтрокаПрофильРаспределения.ПрофильРаспределения;
				ТочностьОкругления = СтрокаПрофильРаспределения.ТочностьОкругления;
				
				ИдентификаторНФП = СтрЗаменить(ПрофильРаспределения.УникальныйИдентификатор(), "-", "");
				РеквизитыНФП = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПрофильРаспределения,
					"ПоОрганизациям, ПоПодразделениям, ПоСценариям");
				Если РеквизитыНФП.ПоОрганизациям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"ДанныеИтерации.Организация = БазаРаспределения_Коэффициенты.Организация");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизациям",
						"БазаРаспределения_Коэффициенты.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоПодразделениям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"ДанныеИтерации.Подразделение = БазаРаспределения_Коэффициенты.Подразделение");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоПодразделениям",
						"БазаРаспределения_Коэффициенты.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				Если РеквизитыНФП.ПоСценариям Тогда
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"ДанныеИтерации.Сценарий = БазаРаспределения_Коэффициенты.Сценарий");
				Иначе
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоСценариям",
						"БазаРаспределения_Коэффициенты.Сценарий = ЗНАЧЕНИЕ(Справочник.Сценарии.ПустаяСсылка)"); //@ query-part
				КонецЕсли;
				
				РесурсыИсточника = Новый Массив;
				РесурсыИсточника.Добавить("ДанныеИтерации.КоличествоОборот");
				РесурсыИсточника.Добавить("ДанныеИтерации.СуммаУпрОборот");
				РесурсыИсточника.Добавить("ДанныеИтерации.СуммаСценарияОборот");
				РесурсыИсточника.Добавить("ДанныеИтерации.СуммаРеглОборот");
				РесурсыИсточника.Добавить("ДанныеИтерации.СуммаВВалютеОборот");
				ШаблонОкругленияРесурса = "";
				Если ТочностьОкругления <> 1 Тогда
					ШаблонОкругленияРесурса = "ВЫРАЗИТЬ(%1 КАК ЧИСЛО(%2,0))"; // @query-part
				КонецЕсли;
				Для Каждого Ресурс Из РесурсыИсточника Цикл
					Если Не ПустаяСтрока(ШаблонОкругленияРесурса) Тогда
						ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Ресурс, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ШаблонОкругленияРесурса, Ресурс, ФинансовыйУчетПоДаннымБалансовыхРегистров.СтрокаВыборкиЗначения(ТочностьОкругления)));
					КонецЕсли;
					ТекстЗапроса = СтрЗаменить(ТекстЗапроса, Ресурс, ОбщегоНазначенияУТ.ТекстРаспределенияСуммы(Ресурс,
						"ЕСТЬNULL(БазаРаспределения_Коэффициенты.Коэффициент, 0)",
						"ЕСТЬNULL(БазаРаспределения_Коэффициенты.НакопленныйКоэффициент, 0)",
						"ЕСТЬNULL(БазаРаспределения_Сумма.СуммарныйКоэффициент, 0)")); //@ query-part
				КонецЦикла;
				
				НомераАналитикБезРаспределения = НомераАналитикПриемникаПоНомерамИтераций.Получить(НомерИтерации);
				СоответствиеНомеровАналитикСРаспределением = НомераАналитикПоНомерамИтераций.Получить(НомерИтерации);
				МассивПолейАналитик = Новый Массив;
				МассивПолейАналитикИсточника = Новый Массив;
				Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
					Если НомераАналитикБезРаспределения.Найти(НомерАналитики) <> Неопределено Тогда
						МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							"ДанныеИтерации.Аналитика%1 КАК Аналитика%1", НомерАналитики));
					Иначе
						НомерАналитикиПрофиля = СоответствиеНомеровАналитикСРаспределением.Получить(НомерАналитики);
						Если НомерАналитикиПрофиля <> Неопределено Тогда
							МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								"БазаРаспределения_Коэффициенты.Аналитика%1 КАК Аналитика%2", НомерАналитикиПрофиля,
								НомерАналитики));
						Иначе
							МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								"НЕОПРЕДЕЛЕНО КАК Аналитика%2", НомерАналитикиПрофиля, НомерАналитики));
						КонецЕсли;
					КонецЕсли;
					МассивПолейАналитикИсточника.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"НЕОПРЕДЕЛЕНО КАК Аналитика%1Источника", НомерАналитики)); //@ query-part
				КонецЦикла;
				ТекстПолейАналитикИсточника = СтрСоединить(МассивПолейАналитикИсточника, "," + Символы.ПС + Символы.Таб);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитикИсточника", ТекстПолейАналитикИсточника);
				ТекстПолейАналитик = СтрСоединить(МассивПолейАналитик, "," + Символы.ПС + Символы.Таб);
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик", ТекстПолейАналитик);
				
				МассивУсловийСоединенияПолейАналитикКоэффициенты = Новый Массив;
				МассивУсловийСоединенияПолейАналитикСумма = Новый Массив;
				Для Каждого КлючИЗначение Из ИсходныеНомераАналитикПоНомерамИтераций.Получить(НомерИтерации) Цикл
					МассивУсловийСоединенияПолейАналитикКоэффициенты.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"ДанныеИтерации.Аналитика%1 = БазаРаспределения_Коэффициенты.Аналитика%2",
						КлючИЗначение.Значение, КлючИЗначение.Ключ));
					МассивУсловийСоединенияПолейАналитикСумма.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						"ДанныеИтерации.Аналитика%1 = БазаРаспределения_Сумма.Аналитика%2", КлючИЗначение.Значение,
						КлючИЗначение.Ключ));
				КонецЦикла;
				УсловиеСоединенияПолейАналитикКоэффициенты = СтрСоединить(
					МассивУсловийСоединенияПолейАналитикКоэффициенты, Символы.ПС + Символы.Таб + "И ");
				УсловиеСоединенияПолейАналитикСумма = СтрСоединить(МассивУсловийСоединенияПолейАналитикСумма,
					Символы.ПС + Символы.Таб + "И ");
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикКоэффициенты", ?(ПустаяСтрока(
					УсловиеСоединенияПолейАналитикКоэффициенты), "ИСТИНА", УсловиеСоединенияПолейАналитикКоэффициенты));
				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитикСумма", ?(ПустаяСтрока(
					УсловиеСоединенияПолейАналитикСумма), "ИСТИНА", УсловиеСоединенияПолейАналитикСумма));

				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ДанныеИтерации",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ДанныеИтерации_%1",
					ИдентификаторИтерацииСтроки));

				ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения",
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("БазаРаспределения_%1",
					ИдентификаторНФП));
				
			КонецЦикла;
		
		КонецЕсли;

		ТекстыЗапросов.Добавить(ТекстЗапроса);
		
	КонецЦикла;
	
	ЗапросПоНастройкам.ТекстЗапроса = СтрСоединить(ТекстыЗапросов, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении());
	Если ТекстыЗапросовВременныеТаблицы.Количество() > 0 Тогда
		ЗапросПоНастройкам.ТекстЗапросаВременныеТаблицы = СтрСоединить(ТекстыЗапросовВременныеТаблицы,
			ОбщегоНазначения.РазделительПакетаЗапросов());
	КонецЕсли;
	
	Возврат ЗапросПоНастройкам;
	
КонецФункции

// Возвращает наименование правила расчета, сформированное по наименованиям источника и приемника правила.
//
// Параметры:
//  Наименование - Строка - наименование правила расчета
//  Источник - СправочникСсылка.СтатьиБюджетов, СправочникСсылка.ПоказателиБюджетов - источник данных для правила
//  Приемник - СправочникСсылка.СтатьиБюджетов, СправочникСсылка.ПоказателиБюджетов - приемник данных по правилу
//
// Возвращаемое значение:
//  Строка
//
Функция СформироватьНаименованиеПравила(Наименование, Источник, Приемник) Экспорт

	Если ЗначениеЗаполнено(Источник) И ЗначениеЗаполнено(Приемник) Тогда
		МассивЭлементов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Источник);
		МассивЭлементов.Добавить(Приемник);
		НаименованияЭлементов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивЭлементов, "Наименование");
		Наименование = НаименованияЭлементов.Получить(Источник) + " --> " + НаименованияЭлементов.Получить(Приемник);
	КонецЕсли;
	
	Возврат Наименование;

КонецФункции

// Возвращает массив действующих правил расчета для заданной статьи или показателя бюджетов
//
// Параметры:
//  СтатьяПоказатель - СправочникСсылка.СтатьиБюджетов,
//						СправочникСсылка.ПоказателиБюджетов
//
// Возвращаемое значение:
//  Массив из СправочникСсылка.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов
//
Функция ПравилаРасчетаСтатьиПоказателяБюджетов(СтатьяПоказатель) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СтатьяПоказатель", СтатьяПоказатель);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаРасчета.Ссылка КАК ПравилоРасчета
	|ИЗ
	|	Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов КАК ПравилаРасчета
	|ГДЕ
	|	ПравилаРасчета.Приемник = &СтатьяПоказатель
	|	И НЕ ПравилаРасчета.ПометкаУдаления
	|	И ПравилаРасчета.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыПравилБюджетирования.Действует)";
	УстановитьПривилегированныйРежим(Истина);
	МассивПравил = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПравилоРасчета");
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат МассивПравил;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ХешНастроекПравилаРасчета(ПравилоРасчета)
	
	СтруктураНастроекПравила = Новый Структура("НастройкиЗаполненияАналитик, НастройкиРасчетаДанных, СоответствиеЗначенийАналитик");
	СтруктураНастроекПравила.НастройкиЗаполненияАналитик = ПравилоРасчета.НастройкиЗаполненияАналитик.Выгрузить();
	СтруктураНастроекПравила.НастройкиЗаполненияАналитик.Сортировать("ВидАналитики");
	СтруктураНастроекПравила.НастройкиРасчетаДанных = ПравилоРасчета.НастройкиРасчетаДанных.Выгрузить();
	СтруктураНастроекПравила.НастройкиРасчетаДанных.Сортировать("Коэффициент, КоэффициентНФП, ПрофильРаспределения");
	СтруктураНастроекПравила.СоответствиеЗначенийАналитик = ПравилоРасчета.СоответствиеЗначенийАналитик.Выгрузить();
	СтруктураНастроекПравила.СоответствиеЗначенийАналитик.Сортировать("ЗначениеАналитики");
	
	СериализованныеНастройкиПравила = ОбщегоНазначения.ЗначениеВСтрокуXML(СтруктураНастроекПравила);
	ХешНастроекПравила = ОбщегоНазначенияУТ.ХешСуммаСтроки(СериализованныеНастройкиПравила, ХешФункция.MD5);
	
	Возврат ХешНастроекПравила;
	
КонецФункции

Процедура ДобавитьТекстыЗапросовНастройкиСмещенияПериодаСКД(ИмяВременнойТаблицы, НомерВидаАналитики,
	ТекстыЗапросовБазыРаспределения, Настройки, ПравилоРасчета)
	
	Если ЗначениеЗаполнено(Настройки.ИмяШаблонаСКД) Тогда
		ВидыАналитикИсточника = БюджетированиеСервер.ВидыАналитик(ПравилоРасчета.Источник, Истина);
		СвязанныйВидАналитики = ВидыАналитикИсточника[Лев(Настройки.ИмяШаблонаСКД, СтрНайти(Настройки.ИмяШаблонаСКД,
			"_") - 1)];
		НомерВидаАналитики = Сред(Настройки.ИмяШаблонаСКД, СтрНайти(Настройки.ИмяШаблонаСКД, "_") - 1, 1);
		ИмяТабличнойЧасти = Сред(Настройки.ИмяШаблонаСКД, СтрНайти(Настройки.ИмяШаблонаСКД, "_") + 1);
		СхемаКомпоновкиДанных = ПолучитьСхемуКомпоновкиДанныхСмещенияПериода(СвязанныйВидАналитики, ИмяТабличнойЧасти);
		НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Иначе
		ИмяТабличнойЧасти = "Произвольная";
		СхемаКомпоновкиДанных = Настройки.ХранилищеСхемыКомпоновкиДанных.Получить();
		НастройкиКомпоновкиДанных = Настройки.ХранилищеНастроекКомпоновкиДанных.Получить();
		ПараметрВидАналитики = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиКомпоновкиДанных, "ВидАналитики");
		Если ПараметрВидАналитики = Неопределено
			Или Не ЗначениеЗаполнено(ПараметрВидАналитики.Значение) Тогда
			ВызватьИсключение НСтр(
				"ru = 'В схеме компоновки данных настроек смещения периода не задан параметр ""Вид аналитики""';
				|en = 'The Dimension kind parameter is not specified in the data composition schema of the period offset settings'");
		КонецЕсли;
		НомерВидаАналитики = БюджетированиеПовтИсп.НомерВидаАналитикиСтатьиПоказателяБюджетов(ПравилоРасчета.Источник,
			ПараметрВидАналитики.Значение);
		Если Не ЗначениеЗаполнено(НомерВидаАналитики) Тогда
			ВызватьИсключение НСтр(
				"ru = 'В схеме компоновки данных настроек смещения периода некорректно указан параметр ""Вид аналитики""';
				|en = 'The ""Dimension kind"" parameter is incorrectly specified in the data composition schema of the period offset settings'");
		КонецЕсли;
	КонецЕсли;
	
	СериализованнаяСхемаКомпоновкиДанных = ОбщегоНазначения.ЗначениеВСтрокуXML(СхемаКомпоновкиДанных);
	ХешСхемыКомпоновкиДанных = ОбщегоНазначенияУТ.ХешСуммаСтроки(СериализованнаяСхемаКомпоновкиДанных, ХешФункция.MD5);
	ИмяВременнойТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("НастройкиСмещенияПериода_%1",
		ХешСхемыКомпоновкиДанных);
	ТекстыЗапросовВременныеТаблицы = Новый Массив;
	
	МакетКомпоновки = КомпоновкаДанныхСервер.ПолучитьМакетКомпоновки(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных);
	Запрос = КомпоновкаДанныхСервер.ПолучитьЗапросИзМакетаКомпоновки(МакетКомпоновки, ИмяТабличнойЧасти);
	ТекстЗапросаСмещенияПериода = СхемыЗапросов.УстановитьПомещениеВоВременнуюТаблицу(Запрос.Текст,
		ИмяВременнойТаблицы) + Символы.ПС + Символы.Таб + "ИНДЕКСИРОВАТЬ ПО ЗначениеАналитики, НомерСтроки";
	ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапросаСмещенияПериода);
	
	ТекстЗапросаСмещенияПериода =
	"ВЫБРАТЬ
	|	НастройкиСмещенияПериода.ЗначениеАналитики,
	|	НастройкиСмещенияПериода.ПериодСмещения,
	|	НастройкиСмещенияПериода.ПериодичностьСмещения,
	|	НастройкиСмещенияПериода.СмещениеВперед,
	|	НастройкиСмещенияПериода.НомерСтроки,
	|	НастройкиСмещенияПериода.Коэффициент,
	|	ЕСТЬNULL(СУММА(НастройкиСмещенияПериода_Предыдущие.Коэффициент), 0) КАК НакопленныйКоэффициент
	|ПОМЕСТИТЬ НастройкиСмещенияПериода_Коэффициенты
	|ИЗ
	|	НастройкиСмещенияПериода КАК НастройкиСмещенияПериода
	|		ЛЕВОЕ СОЕДИНЕНИЕ НастройкиСмещенияПериода КАК НастройкиСмещенияПериода_Предыдущие
	|		ПО НастройкиСмещенияПериода.ЗначениеАналитики = НастройкиСмещенияПериода_Предыдущие.ЗначениеАналитики
	|			И НастройкиСмещенияПериода.НомерСтроки > НастройкиСмещенияПериода_Предыдущие.НомерСтроки
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиСмещенияПериода.ЗначениеАналитики,
	|	НастройкиСмещенияПериода.ПериодСмещения,
	|	НастройкиСмещенияПериода.ПериодичностьСмещения,
	|	НастройкиСмещенияПериода.СмещениеВперед,
	|	НастройкиСмещенияПериода.НомерСтроки,
	|	НастройкиСмещенияПериода.Коэффициент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НастройкиСмещенияПериода.ЗначениеАналитики";
	ТекстыЗапросовВременныеТаблицы.Добавить(СтрЗаменить(ТекстЗапросаСмещенияПериода,
		"НастройкиСмещенияПериода", ИмяВременнойТаблицы));
	
	ТекстЗапросаСмещенияПериода = 
	"ВЫБРАТЬ
	|	НастройкиСмещенияПериода.ЗначениеАналитики,
	|	СУММА(НастройкиСмещенияПериода.Коэффициент) КАК СуммарныйКоэффициент
	|ПОМЕСТИТЬ НастройкиСмещенияПериода_Сумма
	|ИЗ
	|	НастройкиСмещенияПериода КАК НастройкиСмещенияПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиСмещенияПериода.ЗначениеАналитики
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗначениеАналитики";
	ТекстыЗапросовВременныеТаблицы.Добавить(СтрЗаменить(ТекстЗапросаСмещенияПериода,
		"НастройкиСмещенияПериода", ИмяВременнойТаблицы));
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ДобавитьЗапросУдаленияВременнойТаблицы(ТекстыЗапросовВременныеТаблицы,
		ИмяВременнойТаблицы);
	
	ТекстыЗапросовБазыРаспределения.Вставить(ИмяВременнойТаблицы, СтрСоединить(ТекстыЗапросовВременныеТаблицы,
		ОбщегоНазначения.РазделительПакетаЗапросов()));
	
КонецПроцедуры

Процедура ДобавитьТекстЗапросаКоэффициентыПересчетаПоНФП(ТекстыЗапросовБазыРаспределения, Настройки, ПараметрыЗапроса)

	ИдентификаторНФП = СтрЗаменить(Настройки.КоэффициентНФП.УникальныйИдентификатор(), "-", "");
	ИмяВременнойТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("КоэффициентыНФП_%1", ИдентификаторНФП);
	Если ТекстыЗапросовБазыРаспределения.Получить(ИмяВременнойТаблицы) <> Неопределено Тогда
		Возврат;
	КонецЕсли;

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КоэффициентыНФП.Сценарий КАК Сценарий,
	|	КоэффициентыНФП.Организация КАК Организация,
	|	КоэффициентыНФП.Подразделение КАК Подразделение,
	|	&ТекстПолейАналитик,
	|	ВЫРАЗИТЬ(КоэффициентыНФП.ЗначениеПоказателя КАК ЧИСЛО(31, 6)) КАК ЗначениеПоказателя
	|ПОМЕСТИТЬ КоэффициентыНФП
	|ИЗ
	|	РегистрСведений.ЗначенияНефинансовыхПоказателей.СрезПоследних({(&ДатаАктуальности)}, НефинансовыйПоказатель = &ПараметрНФП) КАК КоэффициентыНФП
	|{ГДЕ
	|	(КоэффициентыНФП.ДатаОкончания < &ДатаАктуальности)}";
	ИмяПараметраНФП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("НФП%1", ИдентификаторНФП);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПараметрНФП", ИмяПараметраНФП);
	ПараметрыЗапроса.Вставить(ИмяПараметраНФП, Настройки.КоэффициентНФП);
	
	МассивПолейАналитик = Новый Массив;
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	ВидыАналитикНФП = БюджетированиеСервер.ВидыАналитик(Настройки.КоэффициентНФП);
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
		Если Не ЗначениеЗаполнено(ВидыАналитикНФП[СтрокаВидАналитики]) Тогда
			Продолжить;
		КонецЕсли;
		МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"КоэффициентыНФП.Аналитика%1 КАК Аналитика%1", НомерАналитики));
	КонецЦикла;
	ТекстПолейАналитик = СтрСоединить(МассивПолейАналитик, "," + Символы.ПС + Символы.Таб);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
		ТекстПолейАналитик + ","));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "КоэффициентыНФП", ИмяВременнойТаблицы);
	ТекстыЗапросовБазыРаспределения.Вставить(ИмяВременнойТаблицы, ТекстЗапроса);

КонецПроцедуры

Процедура ДобавитьТекстыЗапросовБазыРаспределенияПоПериодам(ТекстыЗапросовБазыРаспределения, Настройки, ПараметрыЗапроса)

	ИдентификаторНФП = СтрЗаменить(Настройки.ПрофильРаспределения.УникальныйИдентификатор(), "-", "");
	ИмяВременнойТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("БазаРаспределения_%1", ИдентификаторНФП);
	Если ТекстыЗапросовБазыРаспределения.Получить(ИмяВременнойТаблицы) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыЗапросовВременныеТаблицы = Новый Массив;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	БазаРаспределения.Сценарий КАК Сценарий,
	|	БазаРаспределения.Организация КАК Организация,
	|	БазаРаспределения.Подразделение КАК Подразделение,
	|	&ТекстПолейАналитик,
	|	БазаРаспределения.НомерПодПериода КАК НомерПодПериода,
	|	ВЫРАЗИТЬ(БазаРаспределения.ЗначениеПоказателя КАК ЧИСЛО(31, 6)) КАК ЗначениеПоказателя
	|ПОМЕСТИТЬ БазаРаспределения
	|ИЗ
	|	РегистрСведений.ЗначенияНефинансовыхПоказателей.СрезПоследних({(&ДатаАктуальности)}, НефинансовыйПоказатель = &ПараметрНФП) КАК БазаРаспределения
	|{ГДЕ
	|	(БазаРаспределения.ДатаОкончания < &ДатаАктуальности)}
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сценарий,
	|	Организация,
	|	Подразделение";
	ТекстЗапроса = ТекстЗапроса + ",
	|	&ТекстПсевдонимовАналитик,
	|	НомерПодПериода";
	ИмяПараметраНФП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("НФП%1", ИдентификаторНФП);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПараметрНФП", ИмяПараметраНФП);
	ПараметрыЗапроса.Вставить(ИмяПараметраНФП, Настройки.ПрофильРаспределения);
	
	МассивПолейАналитик = Новый Массив;
	МассивПсевдонимовАналитик = Новый Массив;
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	ВидыАналитикНФП = БюджетированиеСервер.ВидыАналитик(Настройки.ПрофильРаспределения);
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
		Если Не ЗначениеЗаполнено(ВидыАналитикНФП[СтрокаВидАналитики]) Тогда
			Продолжить;
		КонецЕсли;
		МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"БазаРаспределения.Аналитика%1 КАК Аналитика%1", НомерАналитики));
		МассивПсевдонимовАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Аналитика%1", НомерАналитики));
	КонецЦикла;
	ТекстПолейАналитик = СтрСоединить(МассивПолейАналитик, "," + Символы.ПС + Символы.Таб);
	ТекстПсевдонимовАналитик = СтрСоединить(МассивПсевдонимовАналитик, "," + Символы.ПС + Символы.Таб);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
		ТекстПолейАналитик + ","));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПсевдонимовАналитик,", ?(ПустаяСтрока(
		ТекстПсевдонимовАналитик), "", ТекстПсевдонимовАналитик + ","));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
	ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	БазаРаспределения.Сценарий КАК Сценарий,
	|	БазаРаспределения.Организация КАК Организация,
	|	БазаРаспределения.Подразделение КАК Подразделение,
	|	&ТекстПолейАналитик,
	|	БазаРаспределения.НомерПодПериода КАК НомерПодПериода,
	|	БазаРаспределения.ЗначениеПоказателя КАК Коэффициент,
	|	ЕСТЬNULL(СУММА(БазаРаспределения_Предыдущие.ЗначениеПоказателя), 0) КАК НакопленныйКоэффициент
	|ПОМЕСТИТЬ БазаРаспределения_Коэффициенты
	|ИЗ
	|	БазаРаспределения КАК БазаРаспределения
	|	ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения_Предыдущие
	|		ПО БазаРаспределения.Сценарий = БазаРаспределения_Предыдущие.Сценарий
	|		И БазаРаспределения.Организация = БазаРаспределения_Предыдущие.Организация
	|		И БазаРаспределения.Подразделение = БазаРаспределения_Предыдущие.Подразделение
	|		И &УсловиеСоединенияПолейАналитик
	|		И БазаРаспределения.НомерПодПериода > БазаРаспределения_Предыдущие.НомерПодПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	БазаРаспределения.Сценарий,
	|	БазаРаспределения.Организация,
	|	БазаРаспределения.Подразделение,
	|	&ТекстГруппАналитик,
	|	БазаРаспределения.НомерПодПериода,
	|	БазаРаспределения.ЗначениеПоказателя
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сценарий,
	|	Организация,
	|	Подразделение";
	ТекстЗапроса = ТекстЗапроса + "
	|	,&ТекстПсевдонимовАналитик";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
		ТекстПолейАналитик + ","));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстПсевдонимовАналитик", ?(ПустаяСтрока(
		ТекстПсевдонимовАналитик), "", "," + ТекстПсевдонимовАналитик));
	
	МассивГруппАналитик = Новый Массив;
	МассивУсловийСоединенияПолейАналитик = Новый Массив;
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
		Если Не ЗначениеЗаполнено(ВидыАналитикНФП[СтрокаВидАналитики]) Тогда
			Продолжить;
		КонецЕсли;
		МассивГруппАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"БазаРаспределения.Аналитика%1", НомерАналитики));
		МассивУсловийСоединенияПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"БазаРаспределения.Аналитика%1 = БазаРаспределения_Предыдущие.Аналитика%1", НомерАналитики));
	КонецЦикла;
	ТекстГруппАналитик = СтрСоединить(МассивГруппАналитик, "," + Символы.ПС + Символы.Таб);
	УсловиеСоединенияПолейАналитик = СтрСоединить(МассивУсловийСоединенияПолейАналитик, Символы.ПС + Символы.Таб + "И ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстГруппАналитик,", ?(ПустаяСтрока(ТекстГруппАналитик), "",
		ТекстГруппАналитик + ","));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитик", ?(ПустаяСтрока(
		УсловиеСоединенияПолейАналитик), "ИСТИНА", УсловиеСоединенияПолейАналитик));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
	ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	БазаРаспределения.Сценарий КАК Сценарий,
	|	БазаРаспределения.Организация КАК Организация,
	|	БазаРаспределения.Подразделение КАК Подразделение,
	|	&ТекстПолейАналитик,
	|	СУММА(БазаРаспределения.ЗначениеПоказателя) КАК СуммарныйКоэффициент
	|ПОМЕСТИТЬ БазаРаспределения_Сумма
	|ИЗ
	|	БазаРаспределения КАК БазаРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	БазаРаспределения.Сценарий,
	|	БазаРаспределения.Организация,
	|	БазаРаспределения.Подразделение
	|	,&ТекстГруппАналитик
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сценарий,
	|	Организация,
	|	Подразделение";
	ТекстЗапроса = ТекстЗапроса + "
	|	,&ТекстПсевдонимовАналитик";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
		ТекстПолейАналитик + ","));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстГруппАналитик", ?(ПустаяСтрока(ТекстГруппАналитик), "",
		"," + ТекстГруппАналитик));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстПсевдонимовАналитик", ?(ПустаяСтрока(
		ТекстПсевдонимовАналитик), "", "," + ТекстПсевдонимовАналитик));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
	ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
	
	ФинансовыйУчетПоДаннымБалансовыхРегистров.ДобавитьЗапросУдаленияВременнойТаблицы(ТекстыЗапросовВременныеТаблицы,
		ИмяВременнойТаблицы);
	
	ТекстыЗапросовБазыРаспределения.Вставить(ИмяВременнойТаблицы, СтрСоединить(ТекстыЗапросовВременныеТаблицы,
		ОбщегоНазначения.РазделительПакетаЗапросов()));

КонецПроцедуры

Процедура ДобавитьТекстыЗапросовБазыРаспределенияДляПолученияАналитик(ТекстыЗапросовБазыРаспределения,
	ПараметрыЗапроса, ПравилоРасчета, МаксимальныйНомерИтерации)

	ПрофилиРаспределенияАналитик = ПравилоРасчета.НастройкиЗаполненияАналитик.Выгрузить(
		Новый Структура("СпособЗаполнения",
		Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения),
		"ПрофильРаспределения, НомерИтерации");
	ПрофилиРаспределенияАналитик.Свернуть("ПрофильРаспределения, НомерИтерации");
	ПрофилиРаспределенияАналитик.Сортировать("НомерИтерации УБЫВ");
	МаксимальныйНомерИтерации = ?(ПрофилиРаспределенияАналитик.Количество() = 0, 0,
		ПрофилиРаспределенияАналитик[0].НомерИтерации);
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	
	Для НомерИтерации = 1 По МаксимальныйНомерИтерации Цикл
		
		ПрофильРаспределения = ПрофилиРаспределенияАналитик.Найти(НомерИтерации, "НомерИтерации");
		Если ПрофильРаспределения = Неопределено Тогда
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
				"ru = 'Некорректные настройки профиля распределения: не найден профиль с номером %1';
				|en = 'Incorrect allocation profile settings: the profile with number %1 is not found'"), НомерИтерации);
		КонецЕсли;
		
		ИдентификаторНФП = СтрЗаменить(ПрофильРаспределения.ПрофильРаспределения.УникальныйИдентификатор(), "-", "");
		ИмяВременнойТаблицы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("БазаРаспределения_%1",
			ИдентификаторНФП);
		Если ТекстыЗапросовБазыРаспределения.Получить(ИмяВременнойТаблицы) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекстыЗапросовВременныеТаблицы = Новый Массив;
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
		|	БазаРаспределения.Сценарий КАК Сценарий,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.Подразделение КАК Подразделение,
		|	&ТекстПолейАналитик,
		|	ВЫРАЗИТЬ(БазаРаспределения.ЗначениеПоказателя КАК ЧИСЛО(31, 6)) КАК ЗначениеПоказателя
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	РегистрСведений.ЗначенияНефинансовыхПоказателей.СрезПоследних({(&ДатаАктуальности)}, НефинансовыйПоказатель = &ПараметрНФП) КАК БазаРаспределения
		|{ГДЕ
		|	(БазаРаспределения.ДатаОкончания < &ДатаАктуальности)}
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сценарий,
		|	Организация,
		|	Подразделение";
		ТекстЗапроса = ТекстЗапроса + ",
		|	&ТекстПсевдонимовАналитик,
		|	НомерСтроки";
		ИмяПараметраНФП = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("НФП%1", ИдентификаторНФП);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПараметрНФП", ИмяПараметраНФП);
		ПараметрыЗапроса.Вставить(ИмяПараметраНФП, ПрофильРаспределения.ПрофильРаспределения);

		МассивПолейАналитик = Новый Массив;
		МассивПсевдонимовАналитик = Новый Массив;
		ВидыАналитикНФП = БюджетированиеСервер.ВидыАналитик(ПрофильРаспределения.ПрофильРаспределения);
		Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
			СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
			Если Не ЗначениеЗаполнено(ВидыАналитикНФП[СтрокаВидАналитики]) Тогда
				Продолжить;
			КонецЕсли;
			МассивПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"БазаРаспределения.Аналитика%1 КАК Аналитика%1", НомерАналитики));
			МассивПсевдонимовАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Аналитика%1", НомерАналитики));
		КонецЦикла;
		ТекстПолейАналитик = СтрСоединить(МассивПолейАналитик, "," + Символы.ПС + Символы.Таб);
		ТекстПсевдонимовАналитик = СтрСоединить(МассивПсевдонимовАналитик, "," + Символы.ПС + Символы.Таб);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
			ТекстПолейАналитик + ","));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПсевдонимовАналитик,", ?(ПустаяСтрока(
			ТекстПсевдонимовАналитик), "", ТекстПсевдонимовАналитик + ","));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
		ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	БазаРаспределения.Сценарий КАК Сценарий,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.Подразделение КАК Подразделение,
		|	&ТекстПолейАналитик,
		|	БазаРаспределения.ЗначениеПоказателя КАК Коэффициент,
		|	ЕСТЬNULL(СУММА(БазаРаспределения_Предыдущие.ЗначениеПоказателя), 0) КАК НакопленныйКоэффициент
		|ПОМЕСТИТЬ БазаРаспределения_Коэффициенты
		|ИЗ
		|	БазаРаспределения КАК БазаРаспределения
		|	ЛЕВОЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения_Предыдущие
		|		ПО БазаРаспределения.Сценарий = БазаРаспределения_Предыдущие.Сценарий
		|		И БазаРаспределения.Организация = БазаРаспределения_Предыдущие.Организация
		|		И БазаРаспределения.Подразделение = БазаРаспределения_Предыдущие.Подразделение
		|		И &УсловиеСоединенияПолейАналитик
		|		И БазаРаспределения.НомерСтроки > БазаРаспределения_Предыдущие.НомерСтроки
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.Сценарий,
		|	БазаРаспределения.Организация,
		|	БазаРаспределения.Подразделение,
		|	&ТекстГруппАналитик,
		|	БазаРаспределения.ЗначениеПоказателя
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сценарий,
		|	Организация,
		|	Подразделение";
		ТекстЗапроса = ТекстЗапроса + "
		|	,&ТекстПсевдонимовАналитик";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
			ТекстПолейАналитик + ","));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстПсевдонимовАналитик", ?(ПустаяСтрока(
			ТекстПсевдонимовАналитик), "", "," + ТекстПсевдонимовАналитик));
		
		МассивГруппАналитик = Новый Массив;
		МассивУсловийСоединенияПолейАналитик = Новый Массив;
		Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
			СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", НомерАналитики);
			Если Не ЗначениеЗаполнено(ВидыАналитикНФП[СтрокаВидАналитики]) Тогда
				Продолжить;
			КонецЕсли;
			МассивГруппАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"БазаРаспределения.Аналитика%1", НомерАналитики));
			МассивУсловийСоединенияПолейАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"БазаРаспределения.Аналитика%1 = БазаРаспределения_Предыдущие.Аналитика%1", НомерАналитики));
		КонецЦикла;
		ТекстГруппАналитик = СтрСоединить(МассивГруппАналитик, "," + Символы.ПС + Символы.Таб);
		УсловиеСоединенияПолейАналитик = СтрСоединить(МассивУсловийСоединенияПолейАналитик, Символы.ПС + Символы.Таб + "И ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстГруппАналитик,", ?(ПустаяСтрока(ТекстГруппАналитик), "",
			ТекстГруппАналитик + ","));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеСоединенияПолейАналитик", ?(ПустаяСтрока(
			УсловиеСоединенияПолейАналитик), "ИСТИНА", УсловиеСоединенияПолейАналитик));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
		ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	БазаРаспределения.Сценарий КАК Сценарий,
		|	БазаРаспределения.Организация КАК Организация,
		|	БазаРаспределения.Подразделение КАК Подразделение,
		|	&ТекстПолейАналитик,
		|	СУММА(БазаРаспределения.ЗначениеПоказателя) КАК СуммарныйКоэффициент
		|ПОМЕСТИТЬ БазаРаспределения_Сумма
		|ИЗ
		|	БазаРаспределения КАК БазаРаспределения
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.Сценарий,
		|	БазаРаспределения.Организация,
		|	БазаРаспределения.Подразделение
		|	,&ТекстГруппАналитик
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сценарий,
		|	Организация,
		|	Подразделение";
		ТекстЗапроса = ТекстЗапроса + "
		|	,&ТекстПсевдонимовАналитик";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстПолейАналитик,", ?(ПустаяСтрока(ТекстПолейАналитик), "",
			ТекстПолейАналитик + ","));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстГруппАналитик", ?(ПустаяСтрока(ТекстГруппАналитик), "",
			"," + ТекстГруппАналитик));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ",&ТекстПсевдонимовАналитик", ?(ПустаяСтрока(
			ТекстПсевдонимовАналитик), "", "," + ТекстПсевдонимовАналитик));
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "БазаРаспределения", ИмяВременнойТаблицы);
		ТекстыЗапросовВременныеТаблицы.Добавить(ТекстЗапроса);
		
		ФинансовыйУчетПоДаннымБалансовыхРегистров.ДобавитьЗапросУдаленияВременнойТаблицы(ТекстыЗапросовВременныеТаблицы,
			ИмяВременнойТаблицы);
	
		ТекстыЗапросовБазыРаспределения.Вставить(ИмяВременнойТаблицы, СтрСоединить(ТекстыЗапросовВременныеТаблицы,
			ОбщегоНазначения.РазделительПакетаЗапросов()));
		
	КонецЦикла;

КонецПроцедуры

Функция СоответствиеВсехИсходныхНомеровАналитикПриемникаНомерамИтераций(ПравилоРасчета)
	
	СоответствиеНомеровАналитикИИтераций = Новый Соответствие;
	НомераАналитикБезРаспределения = Новый Массив;
	НомераАналитикСРаспределением = Новый Массив;
	
	Для Каждого СтрокаНастройки Из ПравилоРасчета.НастройкиЗаполненияАналитик Цикл
		НомерАналитики = БюджетированиеПовтИсп.НомерВидаАналитикиСтатьиПоказателяБюджетов(ПравилоРасчета.Приемник,
			СтрокаНастройки.ВидАналитики);
		Если СтрокаНастройки.СпособЗаполнения
			<> Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения Тогда
			НомераАналитикБезРаспределения.Добавить(НомерАналитики);
		Иначе
			Если СоответствиеНомеровАналитикИИтераций.Получить(СтрокаНастройки.НомерИтерации) = Неопределено Тогда
				СоответствиеНомеровАналитикИИтераций.Вставить(СтрокаНастройки.НомерИтерации,
					ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НомерАналитики));
			Иначе
				СоответствиеНомеровАналитикИИтераций[СтрокаНастройки.НомерИтерации].Добавить(НомерАналитики);
			КонецЕсли;
			НомераАналитикСРаспределением.Добавить(НомерАналитики);
		КонецЕсли;
	КонецЦикла;
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		Если НомераАналитикБезРаспределения.Найти(НомерАналитики) = Неопределено
			И НомераАналитикСРаспределением.Найти(НомерАналитики) = Неопределено Тогда
			НомераАналитикБезРаспределения.Добавить(НомерАналитики);
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Соответствие;
	
	Для НомерИтерации = 1 По СоответствиеНомеровАналитикИИтераций.Количество() Цикл
		Результат.Вставить(НомерИтерации, ОбщегоНазначения.СкопироватьРекурсивно(НомераАналитикБезРаспределения));
		НомераАналитикПоНомеруИтерации = СоответствиеНомеровАналитикИИтераций.Получить(НомерИтерации);
		Если НомераАналитикПоНомеруИтерации <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НомераАналитикБезРаспределения, НомераАналитикПоНомеруИтерации);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СоответствиеИсходныхНомеровАналитикПриемникаПрофиляНомерамИтераций(ПравилоРасчета)
	
	ПрофилиРаспределенияАналитик = ПравилоРасчета.НастройкиЗаполненияАналитик.Выгрузить(Новый Структура("СпособЗаполнения",
		Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения), "ПрофильРаспределения, НомерИтерации");
	ПрофилиРаспределенияАналитик.Свернуть("ПрофильРаспределения, НомерИтерации");
	
	Результат = Новый Соответствие;
	
	Для Каждого СтрокаПрофиль Из ПрофилиРаспределенияАналитик Цикл
		СоответствиеАналитик = БюджетированиеПовтИсп.СоответствиеАналитикСтатьиПоказателяБюджетовСНФП(
			ПравилоРасчета.Приемник, СтрокаПрофиль.ПрофильРаспределения);
		Результат.Вставить(СтрокаПрофиль.НомерИтерации, СоответствиеАналитик);
	КонецЦикла;
	
	Для Каждого СтрокаНастройка Из ПравилоРасчета.НастройкиЗаполненияАналитик Цикл
		Если СтрокаНастройка.СпособЗаполнения
			<> Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения Тогда
			Продолжить;
		КонецЕсли;
		НомерАналитики = БюджетированиеПовтИсп.НомерВидаАналитикиСтатьиПоказателяБюджетов(
			СтрокаНастройка.ПрофильРаспределения, СтрокаНастройка.ВидАналитики);
		Результат[СтрокаНастройка.НомерИтерации].Удалить(НомерАналитики);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция СоответствиеНомеровАналитикПриемникаПрофиляНомерамИтераций(ПравилоРасчета)
	
	Результат = Новый Соответствие;
	
	Для Каждого СтрокаНастройки Из ПравилоРасчета.НастройкиЗаполненияАналитик Цикл
		Если СтрокаНастройки.СпособЗаполнения
			<> Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения Тогда
			Продолжить;
		КонецЕсли;
		НомерВидаАналитикиПрофиля = БюджетированиеПовтИсп.НомерВидаАналитикиСтатьиПоказателяБюджетов(
			СтрокаНастройки.ПрофильРаспределения, СтрокаНастройки.ВидАналитики);
		НомерВидаАналитикиПриемника = БюджетированиеПовтИсп.НомерВидаАналитикиСтатьиПоказателяБюджетов(
			ПравилоРасчета.Приемник, СтрокаНастройки.ВидАналитики);
		Если Результат.Получить(СтрокаНастройки.НомерИтерации) = Неопределено Тогда
			СоответствиеНомеровАналитик = Новый Соответствие;
			СоответствиеНомеровАналитик.Вставить(НомерВидаАналитикиПриемника, НомерВидаАналитикиПрофиля);
			Результат.Вставить(СтрокаНастройки.НомерИтерации, СоответствиеНомеровАналитик);
		Иначе
			Результат[СтрокаНастройки.НомерИтерации].Вставить(НомерВидаАналитикиПриемника, НомерВидаАналитикиПрофиля);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.3";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("eef95c74-fc73-40ec-ba25-5ab76db9d602");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Перезапись справочника ""Правила расчета связанных остатков и оборотов бюджетов"":';
								|en = 'Overwrite the ""Rules for calculating related budget balances and turnovers"" catalog:'"));
	СписокОписаний.Добавить(НСтр("ru = 'добавление поля ""Расход"" в текст запроса правила';
								|en = 'Add the ""Expense"" field to the rule query text'"));
	
	Обработчик.Комментарий = СтрСоединить(СписокОписаний, Символы.ПС);
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = Метаданные.Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПравилаРасчета.Ссылка КАК ПравилоРасчета,
	|	ПравилаРасчета.ХранилищеЗапроса КАК ХранилищеЗапроса
	|ИЗ
	|	Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов КАК ПравилаРасчета
	|ГДЕ
	|	(ПравилаРасчета.Приемник ССЫЛКА Справочник.СтатьиБюджетов
	|			ИЛИ ВЫРАЗИТЬ(ПравилаРасчета.Приемник КАК Справочник.ПоказателиБюджетов).ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипПоказателяБюджетов.Целевой))";
	МассивСсылок = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ХранилищеЗапроса = Выборка.ХранилищеЗапроса.Получить();
		Если ТипЗнч(ХранилищеЗапроса) = Тип("Структура") Тогда
			Если СтрНайти(ХранилищеЗапроса.ТекстЗапроса, "КАК Расход") = 0 Тогда // @query-part
				МассивСсылок.Добавить(Выборка.ПравилоРасчета);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, МассивСсылок);
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеОбъекта = Метаданные.Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для каждого ЭлементСправочника Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементСправочника.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			СправочникОбъект = ЭлементСправочника.Ссылка.ПолучитьОбъект(); // СправочникОбъект.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов
			
			Если СправочникОбъект = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ЭлементСправочника.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
		
			ХранилищеЗапроса = СправочникОбъект.ХранилищеЗапроса.Получить();
			Если ТипЗнч(ХранилищеЗапроса) = Тип("Структура")
				И СтрНайти(ХранилищеЗапроса.ТекстЗапроса, "КАК Расход") = 0 Тогда // @query-part
				
				ПричинаИсключения = "ПлохиеДанные";
				Если СправочникОбъект.НастройкиЗаполненияАналитик.Количество() > 0 Тогда
					СправочникОбъект.РасширенныйРежимНастройкиЗаполненияАналитики = Истина;
				КонецЕсли;
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект, , Истина);
			
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ЭлементСправочника.Ссылка);
			КонецЕсли;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ЭлементСправочника.Ссылка);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ЭлементСправочника.Ссылка);
				ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(ЭлементСправочника.Ссылка,
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли