#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура")  Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЗначениеЗаполнено(Источник) И Источник = Приемник Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Источник и приемник по правилу расчета не должны совпадать.';
													|en = 'A source and a destination must be different according to the calculation rule.'"),
			, , , Отказ);
	ИначеЕсли Не ЗначениеЗаполнено(Источник) Или Не ЗначениеЗаполнено(Приемник) Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоСтрокНастроек = НастройкиРасчетаДанных.Количество();
	Если КоличествоСтрокНастроек = 1 Тогда
		ТекстОшибкиОтсутствуетКоэффициент = НСтр("ru = 'Не указан коэффициент пересчета.';
												|en = 'Conversion ratio is not specified.'");
		ТекстОшибкиНекорректныйКоэффициентНФП = НСтр("ru = 'Виды аналитик нефинансового показателя %2 не соответствуют видам аналитик %3';
													|en = 'Dimension kinds  of the non-financial %2 item mismatch the %3 dimension kinds'");
		ТекстОшибкиОтсутствуетПериодичность = НСтр("ru = 'Не указан период смещения.';
													|en = 'Offset period is not specified.'");
		ТекстОшибкиОтсутствуетКоличествоПериодов = НСтр("ru = 'Не указано количество периодов смещения.';
														|en = 'Number of offset periods is not specified.'");
	Иначе
		ТекстОшибкиОтсутствуетКоэффициент = НСтр("ru = 'В строке № %1 не указан коэффициент пересчета.';
												|en = 'Conversion ration is not specified in line #%1.'");
		ТекстОшибкиНекорректныйКоэффициентНФП = НСтр("ru = 'В строке № %1 виды аналитик %2 не соответствуют видам аналитик %3';
													|en = 'The %2 dimension kinds mismatch the %3 dimension kinds in line #%1'");
		ТекстОшибкиОтсутствуетПериодичность = НСтр("ru = 'В строке № %1 не указан период смещения.';
													|en = 'Offset period is not specified in line #%1.'");
		ТекстОшибкиОтсутствуетКоличествоПериодов = НСтр("ru = 'В строке № %1 не указано количество периодов смещения.';
														|en = 'Number of offset periods is not specified in line #%1.'");
	КонецЕсли;
	Для Каждого СтрокаНастройки Из НастройкиРасчетаДанных Цикл
		Если Не ЗначениеЗаполнено(СтрокаНастройки.Коэффициент) И Не ЗначениеЗаполнено(СтрокаНастройки.КоэффициентНФП) Тогда
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибкиОтсутствуетКоэффициент, СтрокаНастройки.НомерСтроки), , , , Отказ);
		ИначеЕсли ЗначениеЗаполнено(СтрокаНастройки.КоэффициентНФП)
			И БюджетированиеПовтИсп.СоответствиеАналитикСтатьиПоказателяБюджетовСНФП(Источник,
			СтрокаНастройки.КоэффициентНФП) = Неопределено Тогда
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибкиНекорректныйКоэффициентНФП, СтрокаНастройки.НомерСтроки, СтрокаНастройки.КоэффициентНФП,
				Источник), , , , Отказ);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаНастройки.ВариантНастройкиСмещенияПериода)
			И СтрокаНастройки.ВариантНастройкиСмещенияПериода <> Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику
			И Не ЗначениеЗаполнено(СтрокаНастройки.ПериодичностьСмещения) Тогда
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибкиОтсутствуетПериодичность, СтрокаНастройки.НомерСтроки), , , , Отказ);
		КонецЕсли;
		Если СтрокаНастройки.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.Вручную
			И Не ЗначениеЗаполнено(СтрокаНастройки.ПериодСмещения) Или СтрокаНастройки.ВариантНастройкиСмещенияПериода
			= Перечисления.ВариантыНастройкиСмещенияПериода.ИзАналитики И Не ЗначениеЗаполнено(
			СтрокаНастройки.ВыражениеПериодСмещения) Тогда
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибкиОтсутствуетКоличествоПериодов, СтрокаНастройки.НомерСтроки), , , , Отказ);
		КонецЕсли;
	КонецЦикла;
	
	ПрофилиРаспределения = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
		НастройкиЗаполненияАналитик.ВыгрузитьКолонку("ПрофильРаспределения"));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПрофилиРаспределения, НастройкиРасчетаДанных.ВыгрузитьКолонку(
		"ПрофильРаспределения"));
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(ПрофилиРаспределения,
		Справочники.НефинансовыеПоказателиБюджетов.ПустаяСсылка());
	
	ВидыАналитикПрофилей = Неопределено;
	Если ПрофилиРаспределения.Количество() > 0 Тогда
		МассивВидовАналитик = Новый Массив;
		Для Счетчик = 1 По БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик() Цикл
			МассивВидовАналитик.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1",
				Счетчик));
		КонецЦикла;
		ВидыАналитикПрофилей = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ПрофилиРаспределения, СтрСоединить(
			МассивВидовАналитик, ","));
	КонецЕсли;
	
	ТекстОшибкиОтсутствуетВыражение = НСтр("ru = 'Для вида аналитики %1 не указано выражение заполнения.';
											|en = 'Filling expression is not specified for the %1 dimension kind.'");
	ТекстОшибкиОтсутствуетПрофиль = НСтр("ru = 'Для вида аналитики %1 не указан профиль распределения.';
										|en = 'Allocation profile is not specified for the %1 dimension kind.'");
	ТекстОшибкиНекорректныйПрофиль = НСтр("ru = 'Для вида аналитики %1 указан некорректный профиль распределения.';
											|en = 'Incorrect allocation profile is specified for the %1 dimension kind.'");
	ТекстОшибкиНеУказанНомерИтерации = НСтр("ru = 'Для вида аналитики %1 не указан порядковый номер профиля распределения.';
											|en = 'Incorrect allocation profile is specified for the %1 dimension kind.'");
	ТекстОшибкиОтсутствуетВидАналитики = НСтр(
		"ru = 'Для соответствия значений вида аналитики %1 не указан вид аналитики источника.';
		|en = 'Source dimension kind is not specified for matching the %1 dimension kind values.'");
	ТекстОшибкиОтсутствуетСоответствие = НСтр(
		"ru = 'Для вида аналитики %1 не задано соответствие со значениями вида аналитики %2.';
		|en = 'The %2 dimension kind value match is not specified for the %1 dimension kind.'");
	Для Каждого СтрокаНастройки Из НастройкиЗаполненияАналитик Цикл
		Если СтрокаНастройки.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ИзИсточникаДанных Тогда
			Если Не ЗначениеЗаполнено(СтрокаНастройки.ВыражениеЗаполненияАналитики) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиОтсутствуетВыражение, СтрокаНастройки.ВидАналитики), , , , Отказ);
			КонецЕсли;
		ИначеЕсли СтрокаНастройки.СпособЗаполнения
			= Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения Тогда
			Если Не ЗначениеЗаполнено(СтрокаНастройки.ПрофильРаспределения) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиОтсутствуетПрофиль, СтрокаНастройки.ВидАналитики), , , , Отказ);
			Иначе
				ПрофильСоответствуетВидуАналитики = Ложь;
				ВидыАналитикПрофиля = ВидыАналитикПрофилей.Получить(СтрокаНастройки.ПрофильРаспределения);
				Для Каждого КлючИЗначение Из ВидыАналитикПрофиля Цикл
					Если КлючИЗначение.Значение = СтрокаНастройки.ВидАналитики Тогда
						ПрофильСоответствуетВидуАналитики = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если Не ПрофильСоответствуетВидуАналитики Тогда
					ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибкиНекорректныйПрофиль, СтрокаНастройки.ВидАналитики), , , , Отказ);
				КонецЕсли;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаНастройки.НомерИтерации) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиНеУказанНомерИтерации, СтрокаНастройки.ВидАналитики));
			КонецЕсли;
		ИначеЕсли СтрокаНастройки.СпособЗаполнения
			= Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию Тогда
			Если Не ЗначениеЗаполнено(СтрокаНастройки.ВидАналитикиИсточника) Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиОтсутствуетВидАналитики, СтрокаНастройки.ВидАналитики), , , , Отказ);
			Иначе
				СтрокаСоответствия = СоответствиеЗначенийАналитик.Найти(СтрокаНастройки.ВидАналитики, "ВидАналитики");
				Если СтрокаСоответствия = Неопределено Тогда
					ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибкиОтсутствуетСоответствие, СтрокаНастройки.ВидАналитики,
						СтрокаНастройки.ВидАналитикиИсточника), , , , Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не Отказ Тогда
	
		ТекстОшибкиНекорректныйПрофиль = НСтр(
			"ru = 'Профиль распределения %1 учитывается по видам аналитик (%2), отсутствующих у %3';
			|en = 'The %1 allocation profile is accounted for by dimension kinds (%2) missing in %3'");
		ВидыАналитикИсточника = ОбщегоНазначенияУТКлиентСервер.ПреобразоватьСоответствиеИлиСтруктуруВМассив(
			БюджетированиеСервер.ВидыАналитик(Источник), Ложь);
		ВидыАналитикПриемника = ОбщегоНазначенияУТКлиентСервер.ПреобразоватьСоответствиеИлиСтруктуруВМассив(
			БюджетированиеСервер.ВидыАналитик(Приемник), Ложь);
		Для Каждого ПрофильРаспределения Из ПрофилиРаспределения Цикл
			НекорректныеВидыАналитик = Новый Массив;
			ЭтоПрофильИсточника = НастройкиРасчетаДанных.Найти(ПрофильРаспределения, "ПрофильРаспределения") <> Неопределено;
			Если ЭтоПрофильИсточника Тогда
				СравниваемыеВидыАналитик = ВидыАналитикИсточника;
			Иначе
				СравниваемыеВидыАналитик = ВидыАналитикПриемника;
			КонецЕсли;
			Для Каждого КлючИЗначение Из ВидыАналитикПрофилей.Получить(ПрофильРаспределения) Цикл
				Если ЗначениеЗаполнено(КлючИЗначение.Значение)
					И СравниваемыеВидыАналитик.Найти(КлючИЗначение.Значение) = Неопределено Тогда
					НекорректныеВидыАналитик.Добавить(КлючИЗначение.Значение);
				КонецЕсли;
			КонецЦикла;
			Если НекорректныеВидыАналитик.Количество() > 0 Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиНекорректныйПрофиль, ПрофильРаспределения, СтрСоединить(НекорректныеВидыАналитик, ", "),
					?(ЭтоПрофильИсточника, Источник, Приемник)), , , , Отказ);
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
	
	Если Не Отказ Тогда
		
		ТекстОшибкиНекорректныйНомерИтерации = НСтр("ru = 'Для вида аналитики %1 указан неверный порядковый номер профиля распределения (должен быть перед профилем %2).';
													|en = 'Incorrect sequence number of the allocation profile is specified for the %1 dimension kind (must be before the %2 profile).'");
		ТекстОшибкиОтсутствуетНомерИтерации = НСтр("ru = 'Отсутствует профиль распределения с порядковым номером %1';
													|en = 'No allocation profile with sequence number %1'");
		НомераИтерацииПрофилей = НастройкиЗаполненияАналитик.Выгрузить(, "ПрофильРаспределения, НомерИтерации");
		НомераИтерацииПрофилей.Свернуть("ПрофильРаспределения, НомерИтерации");
		НомераИтерацииПрофилей.Сортировать("НомерИтерации");
		ТекущийНомерИтерации = 1;
		ПроверенныеПрофили = Новый Массив;
		Для Каждого ПрофильРаспределения Из НомераИтерацииПрофилей Цикл
			Если Не ЗначениеЗаполнено(ПрофильРаспределения.ПрофильРаспределения)
				Или Не ЗначениеЗаполнено(ПрофильРаспределения.НомерИтерации) Тогда
				Продолжить;
			ИначеЕсли ПрофильРаспределения.НомерИтерации <> ТекущийНомерИтерации Тогда
				ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибкиОтсутствуетНомерИтерации, ТекущийНомерИтерации), , , , Отказ);
			КонецЕсли;
			Для Каждого КлючИЗначение Из ВидыАналитикПрофилей.Получить(ПрофильРаспределения.ПрофильРаспределения) Цикл
				Если Не ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
					Продолжить;
				КонецЕсли;
				СтрокаНастройки = НастройкиЗаполненияАналитик.Найти(КлючИЗначение.Значение, "ВидАналитики");
				Если СтрокаНастройки.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения
					И СтрокаНастройки.ПрофильРаспределения <> ПрофильРаспределения.ПрофильРаспределения
					И ПроверенныеПрофили.Найти(СтрокаНастройки.ПрофильРаспределения) = Неопределено Тогда
					ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибкиНекорректныйНомерИтерации, СтрокаНастройки.ВидАналитики, ПрофильРаспределения.ПрофильРаспределения));
				КонецЕсли;
			КонецЦикла;
			ПроверенныеПрофили.Добавить(СтрокаНастройки.ПрофильРаспределения);
			ТекущийНомерИтерации = ТекущийНомерИтерации + 1;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Если Не ЭтоНовый() И ТипЗнч(Приемник) = Тип("СправочникСсылка.ПоказателиБюджетов") И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		Приемник, "ТипПоказателя") = Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
		ПроверкаИзмененияПравилаСвязи();
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если Статус = Перечисления.СтатусыПравилБюджетирования.Действует И Не ПометкаУдаления Тогда
		ЗапросПоПравилу = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолучитьЗапросПоНастройкамПравилаРасчетов(
			ЭтотОбъект);
	Иначе
		ЗапросПоПравилу = Неопределено;
	КонецЕсли;
	ХранилищеЗапроса = Новый ХранилищеЗначения(ЗапросПоПравилу);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Правило", Ссылка);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ВложенныйЗапрос.ВидБюджета КАК ВидБюджета
		|ИЗ
		|	(ВЫБРАТЬ
		|		ЭлементыФинансовыхОтчетов.Владелец КАК ВидБюджета
		|	ИЗ
		|		Справочник.ЭлементыФинансовыхОтчетов КАК ЭлементыФинансовыхОтчетов
		|	ГДЕ
		|		ЭлементыФинансовыхОтчетов.Владелец ССЫЛКА Справочник.ВидыБюджетов
		|		И ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ЭлементыФинансовыхОтчетов.ПравилаРасчета КАК ЭлементыФинансовыхОтчетовПравилаРасчета
		|				ГДЕ
		|					ЭлементыФинансовыхОтчетовПравилаРасчета.Ссылка = ЭлементыФинансовыхОтчетов.Ссылка
		|					И ЭлементыФинансовыхОтчетовПравилаРасчета.Правило = &Правило)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ВидыБюджетовКэшСтатейИПоказателей.Ссылка
		|	ИЗ
		|		Справочник.ВидыБюджетов.КэшСтатейИПоказателей КАК ВидыБюджетовКэшСтатейИПоказателей
		|	ГДЕ
		|		ВЫРАЗИТЬ(ВидыБюджетовКэшСтатейИПоказателей.СтатьяПоказатель КАК Справочник.ПоказателиБюджетов).ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипПоказателяБюджетов.Расчетный)
		|		И ИСТИНА В
		|				(ВЫБРАТЬ ПЕРВЫЕ 1
		|					ИСТИНА
		|				ИЗ
		|					Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов КАК ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов
		|				ГДЕ
		|					ВидыБюджетовКэшСтатейИПоказателей.СтатьяПоказатель = ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Приемник)) КАК ВложенныйЗапрос";
	Выборка = Запрос.Выполнить().Выбрать();
	
	ПравилоПоРасчетномуПоказателю = (ТипЗнч(Приемник) = Тип("СправочникСсылка.ПоказателиБюджетов")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Приемник, "ТипПоказателя")
		= Перечисления.ТипПоказателяБюджетов.Расчетный);
	
	НаборЗаписей = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ВидКэша.Установить(Перечисления.ВидыКэшаБюджета.СхемыКомпоновки);
	Если Не ПравилоПоРасчетномуПоказателю Тогда
		НаборЗаписей.Отбор.РежимФормирования.Установить(Перечисления.РежимыФормированияБюджетныхОтчетов.Заполнение);
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		НаборЗаписей.Отбор.ВидБюджета.Установить(Выборка.ВидБюджета);
		НаборЗаписей.Записать();
	КонецЦикла;
	
	Если ПравилоПоРасчетномуПоказателю Тогда
		Выборка.Сбросить();
		НаборЗаписей.Отбор.ВидКэша.Установить(Перечисления.ВидыКэшаБюджета.СтруктураОписанияВидаБюджета);
		Пока Выборка.Следующий() Цикл
			НаборЗаписей.Отбор.ВидБюджета.Установить(Выборка.ВидБюджета);
			НаборЗаписей.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ХранилищеЗапроса = Новый ХранилищеЗначения(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверкаИзмененияПравилаСвязи()
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ТекущийИсточник", Источник);
	Запрос.УстановитьПараметр("ТекущийПриемник", Приемник);
	Запрос.УстановитьПараметр("ПравилоРасчета", Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(&ТекущийИсточник КАК Справочник.СтатьиБюджетов) КАК СтатьяБюджетов,
	|	ВЫРАЗИТЬ(&ТекущийПриемник КАК Справочник.ПоказателиБюджетов) КАК ПоказательБюджетов
	|ПОМЕСТИТЬ ТекущиеЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ПравилаРасчета.Источник КАК Справочник.СтатьиБюджетов) КАК СтатьяБюджетов,
	|	ВЫРАЗИТЬ(ПравилаРасчета.Приемник КАК Справочник.ПоказателиБюджетов) КАК ПоказательБюджетов
	|ПОМЕСТИТЬ ПредыдущиеЗаписи
	|ИЗ
	|	Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов КАК ПравилаРасчета
	|ГДЕ
	|	ВЫРАЗИТЬ(ПравилаРасчета.Приемник КАК
	|		Справочник.ПоказателиБюджетов).ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипПоказателяБюджетов.Расчетный)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредыдущиеЗаписи.СтатьяБюджетов КАК СтатьяПоказатель
	|ПОМЕСТИТЬ ОбъектыПроверки
	|ИЗ
	|	ПредыдущиеЗаписи КАК ПредыдущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ПредыдущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|		И ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил
	|		И НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТекущиеЗаписи.СтатьяБюджетов КАК СтатьяПоказатель
	|ИЗ
	|	ТекущиеЗаписи КАК ТекущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ТекущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|		И ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил
	|		И НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ПредыдущиеЗаписи.ПоказательБюджетов
	|ИЗ
	|	ПредыдущиеЗаписи КАК ПредыдущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ПредыдущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|		И ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил
	|		И НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТекущиеЗаписи.ПоказательБюджетов
	|ИЗ
	|	ТекущиеЗаписи КАК ТекущиеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаПолученияФактаПоСтатьямБюджетов КАК ПравилаПолученияФактаПоСтатьямБюджетов
	|		ПО ТекущиеЗаписи.СтатьяБюджетов = ПравилаПолученияФактаПоСтатьямБюджетов.СтатьяБюджетов
	|		И ПравилаПолученияФактаПоСтатьямБюджетов.ПромежуточноеКэшированиеРезультатовРаботыПравил
	|		И НЕ ПравилаПолученияФактаПоСтатьямБюджетов.ПометкаУдаления
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	СтатьяПоказатель";
	
	ОчищатьКэшПоВидамБюджетов = Ложь;
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ТаблицаПравил = Результаты[2].Выгрузить();
	Если ТаблицаПравил.Количество() > 0
		И ТаблицаПравил[0].Количество <> 0 Тогда
		
		ОчищатьКэшПоВидамБюджетов = Истина;
	КонецЕсли;
	Если ОчищатьКэшПоВидамБюджетов Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Ссылка КАК ВидБюджета
		|ИЗ
		|	Справочник.ВидыБюджетов.КэшСтатейИПоказателей КАК Таблица
		|ГДЕ
		|	Таблица.СтатьяПоказатель В
		|			(ВЫБРАТЬ
		|				ОбъектыПроверки.СтатьяПоказатель
		|			ИЗ
		|				ОбъектыПроверки)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		УстановитьПривилегированныйРежим(Истина);
		
		Пока Выборка.Следующий() Цикл
			НаборПоВидуБюджета = РегистрыСведений.КэшВспомогательныхДанныхВидаБюджета.СоздатьНаборЗаписей();
			НаборПоВидуБюджета.Отбор.ВидБюджета.Установить(Выборка.ВидБюджета);
			НаборПоВидуБюджета.Записать(Истина);
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
