#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ПриемникПриИзмененииНаСервере();
		ПриЧтенииСозданииНаСервере();
		
		Если ЗначениеЗаполнено(Объект.Источник) Тогда
			Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
				МассивНастроекКомпоновкиДанных = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ЗначениеКопирования,
					"НастройкиРасчетаДанных.КомпоновщикНастроек").Выгрузить().ВыгрузитьКолонку("КомпоновщикНастроек");
				Для ИндексСтроки = 0 По Объект.НастройкиРасчетаДанных.Количество() - 1 Цикл
					НастройкиКомпоновкиДанных = МассивНастроекКомпоновкиДанных[ИндексСтроки].Получить();
					СтрокаТЧ = Объект.НастройкиРасчетаДанных[ИндексСтроки];
					СтрокаТЧ.АдресПараметровСКД = ПолучитьАдресПараметровСКДПоСтроке(НастройкиКомпоновкиДанных);
					Если СтрокаТЧ.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику
						И Не ЗначениеЗаполнено(СтрокаТЧ.ИмяШаблонаСКД) Тогда
						НастройкиСмещенияПериода = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.ЗначениеКопирования,
							"НастройкиРасчетаДанных.ХранилищеСхемыКомпоновкиДанных, НастройкиРасчетаДанных.ХранилищеНастроекКомпоновкиДанных");
						СхемаКомпоновкиДанных =
							НастройкиСмещенияПериода.НастройкиРасчетаДанныхХранилищеСхемыКомпоновкиДанных.Выгрузить()[0].ХранилищеСхемыКомпоновкиДанных.Получить();
						Если ТипЗнч(СхемаКомпоновкиДанных) = Тип("СхемаКомпоновкиДанных") Тогда
							АдресСхемыКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных,
								УникальныйИдентификатор);
						КонецЕсли;
						НастройкиКомпоновкиДанных =
							НастройкиСмещенияПериода.НастройкиРасчетаДанныхХранилищеСхемыКомпоновкиДанных.Выгрузить()[0].ХранилищеНастроекКомпоновкиДанных.Получить();
						Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
							АдресНастроекКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(НастройкиКомпоновкиДанных,
								УникальныйИдентификатор);
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			Иначе
				СтрокаТЧ = Объект.НастройкиРасчетаДанных[0];
				СтрокаТЧ.АдресПараметровСКД = ПолучитьАдресПараметровСКДПоСтроке();
			КонецЕсли;
		КонецЕсли;
	
		БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиЗаполненияАналитик");
		БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиРасчетаДанных");
	
		НастроитьЭлементыФормы(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ПриЧтенииСозданииНаСервере();
	
	Для Каждого СтрокаНастройка Из ТекущийОбъект.НастройкиРасчетаДанных Цикл
		НастройкиКомпоновкиДанных = СтрокаНастройка.КомпоновщикНастроек.Получить();
		ИндексСтроки = ТекущийОбъект.НастройкиРасчетаДанных.Индекс(СтрокаНастройка);
		СтрокаТЧ = Объект.НастройкиРасчетаДанных[ИндексСтроки];
		СтрокаТЧ.АдресПараметровСКД = ПолучитьАдресПараметровСКДПоСтроке(НастройкиКомпоновкиДанных);
		Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику
			И Не ЗначениеЗаполнено(СтрокаНастройка.ИмяШаблонаСКД) Тогда
			СхемаКомпоновкиДанных = СтрокаНастройка.ХранилищеСхемыКомпоновкиДанных.Получить();
			Если ТипЗнч(СхемаКомпоновкиДанных) = Тип("СхемаКомпоновкиДанных") Тогда
				АдресСхемыКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных,
					УникальныйИдентификатор);
			КонецЕсли;
			НастройкиКомпоновкиДанных = СтрокаНастройка.ХранилищеНастроекКомпоновкиДанных.Получить();
			Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
				АдресНастроекКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(НастройкиКомпоновкиДанных,
					УникальныйИдентификатор);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиЗаполненияАналитик");
	БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиРасчетаДанных");
	
	УстановитьСписокВыбораСпособовЗаполненияАналитики();
	УстановитьСписокВыбораСпособовЗаполненияКоэффициентаПерерасчета();
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ВключитьРасширеннуюНастройкуЗаполненияАналитикПриОшибкахВАвто() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Объект.НастройкиРасчетаДанных Цикл
		Если Не ПустаяСтрока(СтрокаТЧ.АдресПараметровСКД) Тогда
			НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(СтрокаТЧ.АдресПараметровСКД).Настройки;
		Иначе
			НастройкиКомпоновкиДанных = Неопределено;
		КонецЕсли;
		ИндексСтроки = Объект.НастройкиРасчетаДанных.Индекс(СтрокаТЧ);
		СтрокаНастройка = ТекущийОбъект.НастройкиРасчетаДанных[ИндексСтроки];
		СтрокаНастройка.КомпоновщикНастроек = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);
		Если СтрокаНастройка.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда
			Если ЗначениеЗаполнено(АдресСхемыКомпоновкиДанныхСмещенияПериода) Тогда
				СтрокаНастройка.ХранилищеСхемыКомпоновкиДанных = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(
					АдресСхемыКомпоновкиДанныхСмещенияПериода));
			Иначе
				СтрокаНастройка.ХранилищеСхемыКомпоновкиДанных = Неопределено;
			КонецЕсли;
			Если ЗначениеЗаполнено(АдресНастроекКомпоновкиДанныхСмещенияПериода) Тогда
				СтрокаНастройка.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(
					АдресНастроекКомпоновкиДанныхСмещенияПериода));
			Иначе
				СтрокаНастройка.ХранилищеНастроекКомпоновкиДанных = Неопределено;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыЗаписи.Вставить("НастройкиРасчетаДанных", Объект.НастройкиРасчетаДанных.Выгрузить( ,
		"СпособЗаполненияКоэффициентаПерерасчета,АдресПараметровСКД,ПредставлениеВыраженияПериодСмещения,НетВДоступныхПолях"));
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	// Обработчик подсистемы запрета редактирования реквизитов объектов
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтотОбъект);
	
	Если ТекущийОбъект.РасширенныйРежимНастройкиЗаполненияАналитики Тогда
		ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу();
	КонецЕсли;
	
	Для Каждого СтрокаНастройка Из ТекущийОбъект.НастройкиРасчетаДанных Цикл
		ИндексСтроки = ТекущийОбъект.НастройкиРасчетаДанных.Индекс(СтрокаНастройка);
		СтрокаТЧ = Объект.НастройкиРасчетаДанных[ИндексСтроки];
		ЗаполнитьЗначенияСвойств(СтрокаТЧ, ПараметрыЗаписи.НастройкиРасчетаДанных[ИндексСтроки]);
	КонецЦикла;
	ПараметрыЗаписи.Удалить("НастройкиРасчетаДанных");
	
	БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиЗаполненияАналитик");
	БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, , "НастройкиРасчетаДанных");
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ОповеститьОЗаписиНового(Объект.Ссылка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	
	ИсточникПриИзмененииНаСервере();
	
	Если ЗначениеЗаполнено(Объект.Источник) Тогда
		Элемент.ОграничениеТипа = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(
			Объект.Источник)));
	Иначе
		Элемент.ОграничениеТипа = Новый ОписаниеТипов;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриемникПриИзменении(Элемент)
	
	ПриемникПриИзмененииНаСервере();
	
	Если ЗначениеЗаполнено(Объект.Приемник) Тогда
		Элемент.ОграничениеТипа = Новый ОписаниеТипов(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ТипЗнч(
			Объект.Приемник)));
	Иначе
		Элемент.ОграничениеТипа = Новый ОписаниеТипов;
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключательНастроекРасчетаДанныхПриИзменении(Элемент)
	
	Если Не ПереключательНастроекРасчетаДанных Тогда
		Если Объект.НастройкиРасчетаДанных.Количество() = 0 Тогда
			НоваяСтрока = Объект.НастройкиРасчетаДанных.Добавить();
			Элементы.НастройкиРасчетаДанных.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
			НастройкиРасчетаДанныхПриНачалеРедактирования(Элементы.НастройкиРасчетаДанных, Истина, Ложь);
		ИначеЕсли Объект.НастройкиРасчетаДанных.Количество() = 1 Тогда
			Если Элементы.НастройкиРасчетаДанных.ТекущаяСтрока = Неопределено Тогда
				Элементы.НастройкиРасчетаДанных.ТекущаяСтрока = Объект.НастройкиРасчетаДанных[0].ПолучитьИдентификатор();
			КонецЕсли;
		ИначеЕсли Объект.НастройкиРасчетаДанных.Количество() > 1 Тогда
			ОчиститьСообщения();
			ТекстСообщения = НСтр(
				"ru = 'Переключение в режим без разбиения невозможно, если в настройках расчета данных введено более одной строки.';
				|en = 'You cannot switch to no-split mode if multiple lines are entered in the data calculation settings.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ПереключательНастроекРасчетаДанных = 1;
		КонецЕсли;
	Иначе
		Если Объект.НастройкиРасчетаДанных[0].ВариантНастройкиСмещенияПериода =
			ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ПоГрафику") Тогда
			ОчиститьСообщения();
			ТекстСообщения = НСтр(
				"ru = 'Переключение в режим списком невозможно если смещение периода определяется ""По графику"".';
				|en = 'You cannot switch to list mode if the period offset is determined On schedule.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			ПереключательНастроекРасчетаДанных = 0;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеОтбораНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(Объект.Источник) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан источник.';
														|en = 'Source is not specified.'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("АдресПараметров", ТекущиеДанные.АдресПараметровСКД);
	Если ТипЗнч(Объект.Источник) = Тип("СправочникСсылка.СтатьиБюджетов") Тогда
		СтруктураПараметров.Вставить("ЗаголовокФормы", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Отбор оборотов по статье %1';
			|en = 'Filter turnovers by the %1 item'"), Объект.Источник));
	Иначе
		СтруктураПараметров.Вставить("ЗаголовокФормы", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
			"ru = 'Отбор остатков по показателю %1';
			|en = 'Filter balances by the %1 item'"), Объект.Источник));
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("ПослеНастройкиОтбора", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.НастройкаПроизвольныхОтборов", СтруктураПараметров, ЭтотОбъект, УникальныйИдентификатор, ,
		, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗаполненияКоэффициентаПерерасчетаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	
	Если ТекущиеДанные.СпособЗаполненияКоэффициентаПерерасчета <> 1 Тогда
		ТекущиеДанные.Коэффициент = ?(ТекущиеДанные.СпособЗаполненияКоэффициентаПерерасчета = 0, 1, 0);
	КонецЕсли;
	Если ТекущиеДанные.СпособЗаполненияКоэффициентаПерерасчета <> 2 Тогда
		ТекущиеДанные.КоэффициентНФП = Неопределено;
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНастройкиСмещенияПериодаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	
	Если ТекущиеДанные.ВариантНастройкиСмещенияПериода <> ПредопределенноеЗначение(
		"Перечисление.ВариантыНастройкиСмещенияПериода.Вручную") Тогда
		ТекущиеДанные.ПериодСмещения = 0;
	КонецЕсли;
	Если ТекущиеДанные.ВариантНастройкиСмещенияПериода <> ПредопределенноеЗначение(
		"Перечисление.ВариантыНастройкиСмещенияПериода.ИзАналитики") Тогда
		ТекущиеДанные.ВыражениеПериодСмещения = Неопределено;
		ТекущиеДанные.ПредставлениеВыраженияПериодСмещения = Неопределено;
	КонецЕсли;
	ТекущиеДанные.ИмяШаблонаСКД = Неопределено;
	Если ТекущиеДанные.ВариантНастройкиСмещенияПериода = ПредопределенноеЗначение(
		"Перечисление.ВариантыНастройкиСмещенияПериода.ПоГрафику") Тогда
		ПриИзмененииШаблонаСКДСмещенияПериода(ТекущиеДанные.ИмяШаблонаСКД);
		ТекущиеДанные.ПрофильРаспределения = Неопределено;
	КонецЕсли;
	Если ТекущиеДанные.ВариантНастройкиСмещенияПериода = ПредопределенноеЗначение(
		"Перечисление.ВариантыНастройкиСмещенияПериода.ПустаяСсылка") Тогда
		ТекущиеДанные.ПрофильРаспределения = Неопределено;
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеПериодСмещенияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидАналитики", ТекущиеДанные.ПериодСмещения);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("ТекущееВыражение", ТекущиеДанные.ВыражениеПериодСмещения);
	
	ОткрытьФорму("ОбщаяФорма.ВыборПоляЗаполненияАналитики", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеПериодСмещенияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыражениеПериодСмещенияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	ТекущиеДанные.ВыражениеПериодСмещения = ВыбранноеЗначение;
	
	ВыражениеЗаполненияАналитикиОбработкаВыбораСервер(ТекущиеДанные.ПолучитьИдентификатор(), "НастройкиРасчетаДанных");
	Модифицированность = Истина;
	Элементы.НастройкиРасчетаДанных.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяШаблонаСКДПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиРасчетаДанных.ТекущиеДанные;
	ПриИзмененииШаблонаСКДСмещенияПериода(ТекущиеДанные.ИмяШаблонаСКД);
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиЗаполненияАналитик

&НаКлиенте
Процедура НастройкиЗаполненияАналитикПриАктивизацииСтроки(Элемент)
	
	ПараметрыВыбораФормы = Новый Массив;
	
	ТекущиеДанные = Элементы.НастройкиЗаполненияАналитик.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ВидАналитикиТипЗначения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1ТипЗначения",
			ТекущиеДанные.НомерАналитики);
		Элементы.НастройкиЗаполненияАналитикЗначениеАналитики.ОграничениеТипа = ВидыАналитикПриемника[ВидАналитикиТипЗначения];
		Если ЗначениеЗаполнено(ТекущиеДанные.ДополнительноеСвойство) Тогда
			НовыйПараметрВыбора = Новый ПараметрВыбора("Отбор.Владелец", ТекущиеДанные.ДополнительноеСвойство);
			ПараметрыВыбораФормы.Добавить(НовыйПараметрВыбора);
		КонецЕсли;
	КонецЕсли;

	Элементы.НастройкиЗаполненияАналитикЗначениеАналитики.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	Если Поле.Имя = Элементы.НастройкиЗаполненияАналитикПредставлениеСоответствия.Имя Тогда
	
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Объект.НастройкиЗаполненияАналитик.НайтиПоИдентификатору(ВыбраннаяСтрока);
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ВидАналитикиИсточника) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр(
				"ru = 'Перед заполнением необходимо указать вид аналитики источника.';
				|en = 'Specify the source dimension kind before filling.'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("ВидАналитики, ВидАналитикиИсточника, АдресСоответствияЗначенийАналитик");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
		ПараметрыФормы.АдресСоответствияЗначенийАналитик = АдресСоответствияЗначенийАналитик;
		
		ОткрытьФорму(
			"Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Форма.НастройкаСоответствияЗначенийАналитик",
			ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = АдресСоответствияЗначенийАналитик Тогда
		Модифицированность = Истина;
		СкорректироватьСоответствиеЗначенийАналитик();
		УстановитьПредставлениеСоответствияПоСтроке(Элемент.ТекущаяСтрока, Объект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикСпособЗаполненияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиЗаполненияАналитик.ТекущиеДанные;
	
	Если ТекущиеДанные.СпособЗаполнения <> ПредопределенноеЗначение(
		"Перечисление.СпособыЗаполненияАналитикСтатейБюджетов.УказаннымЗначением") Тогда
		ТекущиеДанные.ЗначениеАналитики = Неопределено;
	КонецЕсли;
	Если ТекущиеДанные.СпособЗаполнения <> ПредопределенноеЗначение(
		"Перечисление.СпособыЗаполненияАналитикСтатейБюджетов.ИзИсточникаДанных") Тогда
		ТекущиеДанные.ВыражениеЗаполненияАналитики = Неопределено;
		ТекущиеДанные.ПредставлениеВыраженияЗаполненияАналитики = Неопределено;
	КонецЕсли;
	Если ТекущиеДанные.СпособЗаполнения <> ПредопределенноеЗначение(
		"Перечисление.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения") Тогда
		ТекущиеДанные.ПрофильРаспределения = Неопределено;
		ТекущиеДанные.НомерИтерации = 0;
		ТекущиеДанные.ТочностьОкругления = 0;
	Иначе
		ТекущиеДанные.ТочностьОкругления = 1;
	КонецЕсли;
	УстановитьПредставлениеСоответствияПоСтроке(Элементы.НастройкиЗаполненияАналитик.ТекущаяСтрока, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикСпособЗаполненияОчистка(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВидАналитикиИсточникаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиЗаполненияАналитик.ТекущиеДанные;
	
	УстановитьПредставлениеСоответствияПоСтроке(Элементы.НастройкиЗаполненияАналитик.ТекущаяСтрока, Объект);
	
	Если ТекущиеДанные.ВидАналитикиИсточника = ТекущиеДанные.ВидАналитики Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Совпадают виды аналитики источника и приемника.';
														|en = 'The source dimension kind matches the destination dimension kind.'"));
		ТекущиеДанные.ВидАналитикиИсточника = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВидАналитикиИсточникаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	Если Элементы.НастройкиЗаполненияАналитикВидАналитикиИсточника.СписокВыбора.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Для источника %1 не заданы виды аналитик. Настройка соответствия недоступна.';
								|en = 'Dimension kinds are not specified for the %1 source. Cannot set up correspondence.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Объект.Источник));
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВыражениеЗаполненияАналитикиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением,
	СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиЗаполненияАналитик.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидАналитики", ТекущиеДанные.ВидАналитики);
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("ТекущееВыражение", ТекущиеДанные.ВыражениеЗаполненияАналитики);
	
	ОткрытьФорму("ОбщаяФорма.ВыборПоляЗаполненияАналитики", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВыражениеЗаполненияАналитикиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиЗаполненияАналитикВыражениеЗаполненияАналитикиОбработкаВыбора(Элемент, ВыбранноеЗначение,
	ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.НастройкиЗаполненияАналитик.ТекущиеДанные;
	ТекущиеДанные.ВыражениеЗаполненияАналитики = ВыбранноеЗначение;
	
	ВыражениеЗаполненияАналитикиОбработкаВыбораСервер(ТекущиеДанные.ПолучитьИдентификатор(), "НастройкиЗаполненияАналитик");
	Модифицированность = Истина;
	Элементы.НастройкиЗаполненияАналитик.ЗакончитьРедактированиеСтроки(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиРасчетаДанных

&НаКлиенте
Процедура НастройкиРасчетаДанныхПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		
		Если Не Копирование Тогда
			Элемент.ТекущиеДанные.ВариантНастройкиСмещенияПериода = ПредопределенноеЗначение(
				"Перечисление.ВариантыНастройкиСмещенияПериода.ПустаяСсылка");
			Элемент.ТекущиеДанные.Коэффициент = 1;
		КонецЕсли;
		
		Элемент.ТекущиеДанные.АдресПараметровСКД = ПолучитьАдресПараметровСКДПоСтроке();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхПредставлениеОтбораНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением,
	СтандартнаяОбработка)
	
	ПредставлениеОтбораНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхСпособЗаполненияКоэффициентаПерерасчетаПриИзменении(Элемент)
	
	СпособЗаполненияКоэффициентаПерерасчетаПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхВариантНастройкиСмещенияПериодаПриИзменении(Элемент)
	
	ВариантНастройкиСмещенияПериодаПриИзменении(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхВыражениеПериодСмещенияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением,
	СтандартнаяОбработка)
	
	ВыражениеПериодСмещенияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхВыражениеПериодСмещенияОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиРасчетаДанныхВыражениеПериодСмещенияОбработкаВыбора(Элемент, ВыбранноеЗначение,
	ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)

	ВыражениеПериодСмещенияОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)
	ЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьСхемуПолученияДанных(Команда)
	
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Настройки схемы получения смещения периода';
														|en = 'Settings of period offset receipt scheme'");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных",            АдресСхемыКомпоновкиДанныхСмещенияПериода);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных",         АдресНастроекКомпоновкиДанныхСмещенияПериода);
	ПараметрыФормы.Вставить("Заголовок",                             ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("УникальныйИдентификатор",               УникальныйИдентификатор);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление",       Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок",                  Истина);
	ПараметрыФормы.Вставить("НеНастраиватьОтбор",                    Истина);
	ПараметрыФормы.Вставить("НеНастраиватьВыбор",                    Истина);
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Ложь);
	ПараметрыФормы.Вставить("ПроцедураПомещенияНастроекВСхемуКомпоновкиДанных",
		"БюджетированиеСервер.ПоместитьНастройкиВСхемуПолученияФактическихДанных");
	ПараметрыФормы.Вставить("ПроцедураПроверкиСхемыКомпоновкиДанных",
		"БюджетированиеСервер.ПроверкаКорректностиСхемыПолученияФактическихДанных");
	ПараметрыФормы.Вставить("ПроцедураПриИнициализацииНастроекКомпоновки",
		"БюджетированиеСервер.ДозаполнениеНастроекПриИзмененииСхемыКомпоновки");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьСхемуКомпоновкиДанныхСмещенияПериода", ЭтотОбъект);
	
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных", 
		ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтключитьРежимРасширеннойНастройкиЗаполненияАналитики(Команда)
	
	ОчиститьСообщения();
	ОтключитьРежимРасширеннойНастройкиЗаполненияАналитикиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьРежимРасширеннойНастройкиЗаполненияАналитик(Команда)
	
	ОчиститьСообщения();
	ВключитьРежимРасширеннойНастройкиЗаполненияАналитикиСервер();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	АдресСоответствияЗначенийАналитик = ПоместитьВоВременноеХранилище(Объект.СоответствиеЗначенийАналитик.Выгрузить(),
		УникальныйИдентификатор);
	
	Если ЗначениеЗаполнено(Объект.Источник) Или ЗначениеЗаполнено(Объект.Приемник) Тогда
		ЗаполнитьВидыАналитикИсточникаПриемника();
		Если ЗначениеЗаполнено(Объект.Приемник) Тогда
			ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу();
			Если ТипЗнч(Объект.Приемник) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
				ТипПоказателяПриемника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Приемник, "ТипПоказателя");
			Иначе
				ТипПоказателяПриемника = Неопределено;
			КонецЕсли;
		КонецЕсли;
		Для Каждого СтрокаНастройка Из Объект.НастройкиРасчетаДанных Цикл
			Если ЗначениеЗаполнено(СтрокаНастройка.КоэффициентНФП) Тогда
				СтрокаНастройка.СпособЗаполненияКоэффициентаПерерасчета = 2;
			ИначеЕсли СтрокаНастройка.Коэффициент <> 1 Тогда
				СтрокаНастройка.СпособЗаполненияКоэффициентаПерерасчета = 1;
			Иначе
				СтрокаНастройка.СпособЗаполненияКоэффициентаПерерасчета = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Объект.НастройкиРасчетаДанных.Количество() = 0 Тогда
		НоваяСтрока = Объект.НастройкиРасчетаДанных.Добавить();
		НоваяСтрока.Коэффициент = 1;
		Элементы.НастройкиРасчетаДанных.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		ПереключательНастроекРасчетаДанных = 0;
	ИначеЕсли Объект.НастройкиРасчетаДанных.Количество() = 1 Тогда
		Элементы.НастройкиРасчетаДанных.ТекущаяСтрока = Объект.НастройкиРасчетаДанных[0].ПолучитьИдентификатор();
		ПереключательНастроекРасчетаДанных = 0;
	ИначеЕсли Объект.НастройкиРасчетаДанных.Количество() > 1 Тогда
		ПереключательНастроекРасчетаДанных = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсточникПриИзмененииНаСервере()

	Объект.Наименование = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.СформироватьНаименованиеПравила(
		Объект.Наименование, Объект.Источник, Объект.Приемник);
	
	ЗаполнитьВидыАналитикИсточникаПриемника(, Ложь);
	
	ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу(Истина);
	
	Если Не ПустаяСтрока(АдресСхемыКомпоновкиДанных)
		И Не ВключитьРасширеннуюНастройкуЗаполненияАналитикПриОшибкахВАвто() Тогда
		БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, Истина,
			"НастройкиЗаполненияАналитик");
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Объект.НастройкиРасчетаДанных Цикл
		Если ЗначениеЗаполнено(Объект.Источник) Тогда
			СтрокаТЧ.АдресПараметровСКД = ПолучитьАдресПараметровСКДПоСтроке();
			СтрокаТЧ.ПредставлениеОтбора = "";
		КонецЕсли;
		Если СтрокаТЧ.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ИзАналитики Тогда
			СтрокаТЧ.ВыражениеПериодСмещения = "";
		ИначеЕсли СтрокаТЧ.ВариантНастройкиСмещенияПериода = Перечисления.ВариантыНастройкиСмещенияПериода.ПоГрафику Тогда
			ПриИзмененииШаблонаСКДСмещенияПериода(СтрокаТЧ.ИмяШаблонаСКД);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из Объект.НастройкиЗаполненияАналитик Цикл
		Если СтрокаТЧ.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию Тогда
			УстановитьПредставлениеСоответствияПоСтроке(СтрокаТЧ.ПолучитьИдентификатор(), Объект, Истина);
		КонецЕсли;
	КонецЦикла;
	АдресСоответствияЗначенийАналитик = ПоместитьВоВременноеХранилище(Объект.СоответствиеЗначенийАналитик.Выгрузить(),
		УникальныйИдентификатор);
	
	НастроитьЭлементыФормы(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПриемникПриИзмененииНаСервере()

	Объект.Наименование = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.СформироватьНаименованиеПравила(
		Объект.Наименование, Объект.Источник, Объект.Приемник);
	
	ЗаполнитьВидыАналитикИсточникаПриемника(Ложь);
	
	ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу(Истина);
	
	Если Не ПустаяСтрока(АдресСхемыКомпоновкиДанных)
		И Не ВключитьРасширеннуюНастройкуЗаполненияАналитикПриОшибкахВАвто() Тогда
		БюджетированиеСервер.ПроверитьДоступностьПолейЗаполненияАналитики(ЭтотОбъект, Объект, Истина,
			"НастройкиЗаполненияАналитик");
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из Объект.НастройкиЗаполненияАналитик Цикл
		Если СтрокаТЧ.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию Тогда
			УстановитьПредставлениеСоответствияПоСтроке(СтрокаТЧ.ПолучитьИдентификатор(), Объект, Истина);
		КонецЕсли;
	КонецЦикла;
	АдресСоответствияЗначенийАналитик = ПоместитьВоВременноеХранилище(Объект.СоответствиеЗначенийАналитик.Выгрузить(),
		УникальныйИдентификатор);
	
	Если ТипЗнч(Объект.Приемник) = Тип("СправочникСсылка.ПоказателиБюджетов") Тогда
		ТипПоказателяПриемника = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Приемник, "ТипПоказателя");
	Иначе
		ТипПоказателяПриемника = Неопределено;
	КонецЕсли;
	Если ТипПоказателяПриемника = Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
		Для Каждого СтрокаТЧ Из Объект.НастройкиРасчетаДанных Цикл
			СтрокаТЧ.ВариантНастройкиСмещенияПериода = Неопределено;
			СтрокаТЧ.ПрофильРаспределения = Неопределено;
			СтрокаТЧ.КоэффициентНФП = Неопределено;
			СтрокаТЧ.СпособЗаполненияКоэффициентаПерерасчета = ?(СтрокаТЧ.СпособЗаполненияКоэффициентаПерерасчета = 2, 1,
				СтрокаТЧ.СпособЗаполненияКоэффициентаПерерасчета);
		КонецЦикла;
	КонецЕсли;
	
	УстановитьСписокВыбораСпособовЗаполненияАналитики();
	УстановитьСписокВыбораСпособовЗаполненияКоэффициентаПерерасчета();

КонецПроцедуры

&НаСервере
Процедура ПриИзмененииШаблонаСКДСмещенияПериода(ИмяШаблонаСКД)
	
	Если ЗначениеЗаполнено(ИмяШаблонаСКД) Тогда
		АдресСхемыКомпоновкиДанныхСмещенияПериода = Неопределено;
		АдресНастроекКомпоновкиДанныхСмещенияПериода = Неопределено;
	Иначе
		СхемаКомпоновкиДанных = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолучитьСхемуКомпоновкиДанныхСмещенияПериода();
		АдресСхемыКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных,
			УникальныйИдентификатор);
		Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
		КомпоновкаДанныхСервер.ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанных, Настройки);
		Настройки = КомпоновщикНастроек.ПолучитьНастройки();
		АдресНастроекКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкорректироватьСоответствиеЗначенийАналитик()
	
	Объект.СоответствиеЗначенийАналитик.Загрузить(ПолучитьИзВременногоХранилища(АдресСоответствияЗначенийАналитик));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВидыАналитикИсточникаПриемника(ЗаполнитьДляИсточника = Истина, ЗаполнитьДляПриемника = Истина)
	
	Если ЗаполнитьДляИсточника Тогда
		Если ЗначениеЗаполнено(Объект.Источник) Тогда
			ВидыАналитикИсточника = БюджетированиеСервер.ВидыАналитик(Объект.Источник, Истина);
			Для Счетчик = 1 По БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик() Цикл
				СтрокаВидАналитики = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ВидАналитики%1", Счетчик);
				Если БюджетированиеПовтИсп.ИменаТиповВидаАналитикиСодержащихТабличнуюЧасть(
					ВидыАналитикИсточника[СтрокаВидАналитики], "ЭтапыГрафикаОплаты").Количество() > 0 Тогда
					ВидыАналитикИсточника.Вставить(СтрокаВидАналитики + "_ЭтапыГрафикаОплаты", Истина);
				КонецЕсли;
			КонецЦикла;
		Иначе
			ВидыАналитикИсточника = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнитьДляПриемника Тогда
		Если ЗначениеЗаполнено(Объект.Приемник) Тогда
			ВидыАналитикПриемника = БюджетированиеСервер.ВидыАналитик(Объект.Приемник, Истина);
		Иначе
			ВидыАналитикПриемника = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВключитьРежимРасширеннойНастройкиЗаполненияАналитикиСервер(Знач ВыраженияЗаполненияАналитики = Неопределено)
	
	Объект.РасширенныйРежимНастройкиЗаполненияАналитики = Истина;
	ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу();
	БюджетированиеСервер.УстановитьНастройкиЗаполненияАналитикиАвтоматически(ЭтотОбъект,
		Объект.НастройкиЗаполненияАналитик, ВыраженияЗаполненияАналитики);
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтключитьРежимРасширеннойНастройкиЗаполненияАналитикиСервер()
	
	Объект.РасширенныйРежимНастройкиЗаполненияАналитики = Ложь;
	Если ВключитьРасширеннуюНастройкуЗаполненияАналитикПриОшибкахВАвто(Ложь) Тогда
		Объект.РасширенныйРежимНастройкиЗаполненияАналитики = Истина;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Имеются аналитики статьи, требующие ручной настройки заполнения';
													|en = 'There are item dimensions that require manual population setup'"));
	Иначе
		ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу();
		НастроитьЭлементыФормы(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНастройкиЗаполненияАналитикиПоПравилу(Перезаполнить = Ложь)
	
	Если Перезаполнить Тогда
		Объект.НастройкиЗаполненияАналитик.Очистить();
		Объект.СоответствиеЗначенийАналитик.Очистить();
		Если Не ЗначениеЗаполнено(Объект.Приемник) Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
	Для НомерАналитики = 1 По МаксимальноеКоличествоАналитик Цикл
		ВидАналитикиПриемника = ВидыАналитикПриемника["ВидАналитики"+НомерАналитики];
		Если Не ЗначениеЗаполнено(ВидАналитикиПриемника) Тогда
			Продолжить;
		КонецЕсли;
		СтрокиНастройки = Объект.НастройкиЗаполненияАналитик.НайтиСтроки(Новый Структура("ВидАналитики",
			ВидАналитикиПриемника));
		Если Перезаполнить Или СтрокиНастройки.Количество() = 0 Тогда
			СтрокаНастройки = Объект.НастройкиЗаполненияАналитик.Добавить();
			СтрокаНастройки.ВидАналитики = ВидАналитикиПриемника;
			СтрокаНастройки.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.УказаннымЗначением;
			СтрокаНастройки.ЗначениеАналитики = ВидыАналитикПриемника["ВидАналитики" + НомерАналитики
				+ "ТипЗначения"].ПривестиЗначение(СтрокаНастройки.ЗначениеАналитики);
			Если ТипЗнч(ВидыАналитикИсточника) = Тип("Структура") Тогда
				Для НомерАналитикиИсточника = 1 По МаксимальноеКоличествоАналитик Цикл
					ВидАналитикиИсточника = ВидыАналитикИсточника["ВидАналитики"+НомерАналитики];
					Если Не ЗначениеЗаполнено(ВидАналитикиИсточника) Тогда
						Продолжить;
					ИначеЕсли ВидАналитикиИсточника = ВидАналитикиПриемника Тогда
						СтрокаНастройки.ВыражениеЗаполненияАналитики = "Аналитика"+НомерАналитики;
						СтрокаНастройки.СпособЗаполнения = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ИзИсточникаДанных;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		Иначе
			СтрокаНастройки = СтрокиНастройки[0];
		КонецЕсли;
		СтрокаНастройки.НомерАналитики = НомерАналитики;
		Если СтрокаНастройки.СпособЗаполнения
			= Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию Тогда
			УстановитьПредставлениеСоответствияПоСтроке(СтрокаНастройки.ПолучитьИдентификатор(), Объект);
		КонецЕсли;
	КонецЦикла;
	
	УдаленыОшибочныеСтроки = Ложь;
	Для Каждого СтрокаНастройки Из Объект.НастройкиЗаполненияАналитик.НайтиСтроки(Новый Структура("НомерАналитики", 0)) Цикл
		Объект.НастройкиЗаполненияАналитик.Удалить(СтрокаНастройки);
		УдаленыОшибочныеСтроки = Истина;
	КонецЦикла;
	Если УдаленыОшибочныеСтроки Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Удалены некорректные строки заполнения аналитик приемника';
													|en = 'Incorrect lines for destination dimension filling are deleted'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВключитьРасширеннуюНастройкуЗаполненияАналитикПриОшибкахВАвто(ИзменитьНастройки = Истина)
	
	Результат = Ложь;
	Если Не Объект.РасширенныйРежимНастройкиЗаполненияАналитики Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных);
		ВидыАналитик = Объект.НастройкиЗаполненияАналитик.Выгрузить(, "НомерАналитики, ВидАналитики");
		ВыраженияЗаполненияАналитики = БюджетированиеСервер.ВыраженияЗаполненияАналитикиПоСхемеКомпоновкиДанных(СхемаКомпоновкиДанных, ВидыАналитик);
		ПараметрыПоиска = Новый Структура("Неоднозначно", Истина);
		НайденныеСтроки = ВыраженияЗаполненияАналитики.НайтиСтроки(ПараметрыПоиска);
		Если НайденныеСтроки.Количество() Тогда
			Если ИзменитьНастройки Тогда
				ВключитьРежимРасширеннойНастройкиЗаполненияАналитикиСервер(ВыраженияЗаполненияАналитики);
			КонецЕсли;
			Результат = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура ВыражениеЗаполненияАналитикиОбработкаВыбораСервер(ИдентификаторСтроки, ИмяТабличнойЧасти) 
	
	БюджетированиеСервер.ПроверитьВыражениеЗаполненияАналитикиПослеВыбора(ЭтотОбъект, ИдентификаторСтроки,
		ИмяТабличнойЧасти);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСхемуКомпоновкиДанныхСмещенияПериода(Результат, Параметры) Экспорт
	
	ОчиститьСообщения();
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ПоместитьНастройкиСКДСмещенияПериодаВоВременноеХранилище(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.СтраницаНастройкиЗаполненияАналитик.Видимость = ЗначениеЗаполнено(Объект.Источник) И ЗначениеЗаполнено(Объект.Приемник);
	
	Элементы.ВключитьРежимРасширеннойНастройкиЗаполненияАналитики.Видимость = Не Объект.РасширенныйРежимНастройкиЗаполненияАналитики;
	Элементы.ГруппаНастройкиЗаполненияАналитикПояснение.Видимость = Не Объект.РасширенныйРежимНастройкиЗаполненияАналитики;
	
	Элементы.ОтключитьРежимРасширеннойНастройкиЗаполненияАналитики.Видимость = Объект.РасширенныйРежимНастройкиЗаполненияАналитики;
	Элементы.НастройкиЗаполненияАналитик.Видимость = Объект.РасширенныйРежимНастройкиЗаполненияАналитики;
	
	Если ЗначениеЗаполнено(Объект.Источник) Тогда
		СписокВыбора = Элементы.НастройкиЗаполненияАналитикВидАналитикиИсточника.СписокВыбора;
		СписокВыбора.Очистить();
		МаксимальноеКоличествоАналитик = БюджетированиеКлиентСервер.МаксимальноеКоличествоАналитик();
		Для Счетчик = 1 По МаксимальноеКоличествоАналитик Цикл
			ВидАналитикиИсточника = Форма.ВидыАналитикИсточника[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ВидАналитики%1", Счетчик)];
			Если ЗначениеЗаполнено(ВидАналитикиИсточника) Тогда
				СписокВыбора.Добавить(ВидАналитикиИсточника);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Объект.НастройкиРасчетаДанных.Количество() > 1 Тогда
		Форма.ПереключательНастроекРасчетаДанных = 1;
	КонецЕсли;
	
	Если Форма.ПереключательНастроекРасчетаДанных = 1 Тогда
		Элементы.СтраницыНастроекРасчетаДанных.ТекущаяСтраница = Элементы.СтраницаНастройкиРасчетаДанныхСписком;
	Иначе
		Элементы.СтраницыНастроекРасчетаДанных.ТекущаяСтраница = Элементы.СтраницаНастройкиРасчетаДанныхБезРазбиения;
		ТекущаяСтрока = Объект.НастройкиРасчетаДанных.НайтиПоИдентификатору(
			Элементы.НастройкиРасчетаДанных.ТекущаяСтрока);
		Элементы.КоэффициентНФП.Видимость = (ТекущаяСтрока.СпособЗаполненияКоэффициентаПерерасчета = 2);
		Элементы.Коэффициент.Видимость = (ТекущаяСтрока.СпособЗаполненияКоэффициентаПерерасчета = 1);
		Элементы.ГруппаСмещениеПериода.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			<> ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ПоГрафику"));
		Элементы.ГруппаСмещениеПериодаПоГрафику.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			= ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ПоГрафику"));
		Элементы.ПериодСмещения.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			= ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.Вручную"));
		Элементы.ВыражениеПериодСмещения.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			= ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ИзАналитики"));
		Элементы.ГруппаНастройкиСмещенияПериодаПоВариантам.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			<> ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ПустаяСсылка"));
		Элементы.ПрофильРаспределения.Видимость = (ТекущаяСтрока.ВариантНастройкиСмещенияПериода
			<> ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСмещенияПериода.ПоГрафику"));
		
		Элементы.ИмяШаблонаСКД.СписокВыбора.Очистить();
		Элементы.ИмяШаблонаСКД.СписокВыбора.Добавить("", НСтр("ru = 'Произвольная';
																|en = 'Arbitrary'"));
		Если ЗначениеЗаполнено(Объект.Источник) Тогда
			Для Счетчик = 1 По МаксимальноеКоличествоАналитик Цикл
				ИмяШаблонаСКД = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"ВидАналитики%1_ЭтапыГрафикаОплаты", Счетчик);
				Если Форма.ВидыАналитикИсточника.Свойство(ИмяШаблонаСКД) Тогда
					ПредставлениеВидаАналитики = Форма.ВидыАналитикИсточника[СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					"ВидАналитики%1", Счетчик)];
					ПредставлениеШаблонаСКД = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru = 'График оплат (%1)';
					|en = 'Payment schedule (%1)'"), ПредставлениеВидаАналитики);
					Элементы.ИмяШаблонаСКД.СписокВыбора.Добавить(ИмяШаблонаСКД, ПредставлениеШаблонаСКД);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		Элементы.НастроитьСхемуПолученияДанных.Доступность = ПустаяСтрока(ТекущаяСтрока.ИмяШаблонаСКД);
		
		Элементы.ГруппаНастройкиСмещенияПериода.Видимость = (Форма.ТипПоказателяПриемника <> ПредопределенноеЗначение(
			"Перечисление.ТипПоказателяБюджетов.Расчетный"));
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	БюджетированиеСервер.ДополнитьУсловноеОформлениеНастройкамиОтображенияАналитик(УсловноеОформление,
		"Объект.НастройкиЗаполненияАналитик");
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикЗначениеАналитики.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.СпособЗаполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.УказаннымЗначением;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикНетВДоступныхПолях.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикВыражениеЗаполненияАналитики.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.СпособЗаполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ИзИсточникаДанных;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикПрофильРаспределения.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикНомерИтерации.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикТочностьОкругления.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.СпособЗаполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикПредставлениеСоответствия.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикВидАналитикиИсточника.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.СпособЗаполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиЗаполненияАналитикВидАналитикиИсточника.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.СпособЗаполнения");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию;
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиЗаполненияАналитик.ВидАналитикиИсточника");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<вид аналитики источника не указан>';
																					|en = '<source dimension kind is not specified>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхКоэффициент.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.СпособЗаполненияКоэффициентаПерерасчета");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = 1;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхКоэффициентНФП.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.СпособЗаполненияКоэффициентаПерерасчета");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = 2;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхСпособЗаполненияКоэффициентаПерерасчета.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.СпособЗаполненияКоэффициентаПерерасчета");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = 2;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'По нефинансовому показателю';
																					|en = 'Non-financial item'"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхСпособЗаполненияКоэффициентаПерерасчета.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.СпособЗаполненияКоэффициентаПерерасчета");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = 1;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Вручную';
																					|en = 'Manually'"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхСпособЗаполненияКоэффициентаПерерасчета.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.СпособЗаполненияКоэффициентаПерерасчета");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = 0;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Значением 1';
																					|en = 'Value 1'"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхПредставлениеОтбора.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.ПредставлениеОтбора");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Отбор не установлен>';
																					|en = '<Filter is not set>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхПериодСмещения.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.ВариантНастройкиСмещенияПериода");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.ВариантыНастройкиСмещенияПериода.Вручную;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхВыражениеПериодСмещения.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.ВариантНастройкиСмещенияПериода");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.ВариантыНастройкиСмещенияПериода.ИзАналитики;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхПериодичностьСмещения.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхСмещениеВперед.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.ВариантНастройкиСмещенияПериода");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхВариантНастройкиСмещенияПериода.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.НастройкиРасчетаДанных.ВариантНастройкиСмещенияПериода");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Не требуется';
																					|en = 'Not required'"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхСмещениеВперед.Имя);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Формат", НСтр("ru = 'БЛ=''В прошлое''; БИ=''В будущее''';
																					|en = 'BF=''To the past''; BT=''To the future'''"));
	
	//
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхВариантНастройкиСмещенияПериода.Имя);
	ОформляемоеПоле = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиРасчетаДанныхПрофильРаспределения.Имя);
	
	ЭлементОтбораДанных = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораДанных.Использование = Истина;
	ЭлементОтбораДанных.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТипПоказателяПриемника");
	ЭлементОтбораДанных.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбораДанных.ПравоеЗначение = Перечисления.ТипПоказателяБюджетов.Расчетный;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеСоответствияПоСтроке(ИдентификаторСтроки, Объект, ОчищатьСоответствие = Ложь)
	
	СтрокаНастройки = Объект.НастройкиЗаполненияАналитик.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтруктураПоиска = Новый Структура("ВидАналитики", СтрокаНастройки.ВидАналитики);
	
	СтрокиСоответствия = Объект.СоответствиеЗначенийАналитик.НайтиСтроки(СтруктураПоиска);
	КоличествоСтрокСоответствия = СтрокиСоответствия.Количество();
	Если СтрокаНастройки.СпособЗаполнения <> ПредопределенноеЗначение(
		"Перечисление.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию")
		Или ОчищатьСоответствие Тогда
		Для Каждого СтрокаСоответствия Из СтрокиСоответствия Цикл
			Объект.СоответствиеЗначенийАналитик.Удалить(СтрокаСоответствия);
		КонецЦикла;
		КоличествоСтрокСоответствия = 0;
	КонецЕсли;
	Если СтрокаНастройки.СпособЗаполнения <> ПредопределенноеЗначение(
		"Перечисление.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию") Тогда
		СтрокаНастройки.ПредставлениеСоответствия = "";
	ИначеЕсли КоличествоСтрокСоответствия = 0 Тогда
		СтрокаНастройки.ПредставлениеСоответствия = НСтр("ru = '<соответствие не задано>';
														|en = '<mapping is not set>'");
	Иначе
		СтрокаНастройки.ПредставлениеСоответствия = СтрШаблон(НСтр("ru = '<задано соответствие для элементов: %1>';
																	|en = '<mapping for items is set: %1>'"),
			КоличествоСтрокСоответствия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьАдресПараметровСКДПоСтроке(НастройкиКомпоновкиДанных = Неопределено)
	
	ИнициализироватьКомпоновщикНастроекПравила(НастройкиКомпоновкиДанных);
	
	Настройки = Новый Структура("СхемаКомпоновкиДанных, Настройки",
		ПолучитьИзВременногоХранилища(АдресСхемыКомпоновкиДанных), НастройкиКомпоновкиДанных);
	
	Возврат ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроекПравила(Настройки)
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	Если Настройки <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	КонецЕсли;
	Если ПустаяСтрока(АдресСхемыКомпоновкиДанных) Тогда
		СхемаКомпоновкиДанных = Справочники.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.ПолучитьСхемуКомпоновкиДанных(
			Объект.Источник, ВидыАналитикИсточника);
		АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	КонецЕсли;
	КомпоновкаДанныхСервер.ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанных, Настройки);
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеНастройкиОтбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("НастройкиКомпоновкиДанных") Тогда
		Если Результат.Отбор.Элементы.Количество() = 0 Тогда
			Результат = Неопределено;
		КонецЕсли;
		АдресНастроекКомпоновкиДанных = ПоместитьВоВременноеХранилище(Результат, УникальныйИдентификатор);
		ПоместитьНастройкиОтбораПоСтрокеВоВременноеХранилище(Элементы.НастройкиРасчетаДанных.ТекущаяСтрока,
			АдресНастроекКомпоновкиДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьНастройкиОтбораПоСтрокеВоВременноеХранилище(ИдентификаторСтроки, АдресНастроекКомпоновкиДанных)

	СтрокаНастройка = Объект.НастройкиРасчетаДанных.НайтиПоИдентификатору(ИдентификаторСтроки);
	Настройки = ПолучитьИзВременногоХранилища(СтрокаНастройка.АдресПараметровСКД);
	КомпоновкаДанныхКлиентСервер.УдалитьОтбор(Настройки.Настройки.Отбор);
	
	НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресНастроекКомпоновкиДанных);
	Если ТипЗнч(НастройкиКомпоновкиДанных) = Тип("НастройкиКомпоновкиДанных") Тогда
		КомпоновкаДанныхКлиентСервер.СкопироватьОтборКомпоновкиДанных(Настройки.СхемаКомпоновкиДанных,
			Настройки.Настройки, НастройкиКомпоновкиДанных);
		СтрокаНастройка.ПредставлениеОтбора = СокрЛП(НастройкиКомпоновкиДанных.Отбор);
		ЭлементыОтбора = КомпоновкаДанныхКлиентСервер.ПолучитьЭлементыОтбора(НастройкиКомпоновкиДанных.Отбор);
		Для Каждого ЭлементОтбора Из ЭлементыОтбора Цикл
			ИмяПоля = СокрЛП(ЭлементОтбора.ЛевоеЗначение);
			Если Не СтрНачинаетсяС(ИмяПоля, "Аналитика") Тогда
				Продолжить;
			КонецЕсли;
			ДоступноеПолеОтбора = Настройки.Настройки.ДоступныеПоляОтбора.НайтиПоле(ЭлементОтбора.ЛевоеЗначение);
			Если ТипЗнч(ДоступноеПолеОтбора) = Тип("ДоступноеПолеОтбораКомпоновкиДанных") Тогда
				СтрокаНастройка.ПредставлениеОтбора = СтрЗаменить(СтрокаНастройка.ПредставлениеОтбора, ИмяПоля,
					ДоступноеПолеОтбора.Заголовок);
			КонецЕсли;
		КонецЦикла;
	Иначе
		СтрокаНастройка.ПредставлениеОтбора = "";
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Настройки, СтрокаНастройка.АдресПараметровСКД);

КонецПроцедуры

&НаСервере
Процедура ПоместитьНастройкиСКДСмещенияПериодаВоВременноеХранилище(Адрес)
	
	НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(Адрес);
	
	Отказ = Ложь;
	ПараметрВидАналитики = КомпоновкаДанныхКлиентСервер.ПолучитьПараметр(НастройкиКомпоновкиДанных, "ВидАналитики");
	Если ПараметрВидАналитики = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не задан обязательный параметр ""Вид аналитики""';
													|en = 'The required Dimension kind parameter is not specified'"), , , , Отказ);
	ИначеЕсли Не ПараметрВидАналитики.Использование
		Или Не ЗначениеЗаполнено(ПараметрВидАналитики.Значение)
		Или ТипЗнч(ПараметрВидАналитики.Значение) <> Тип("ПланВидовХарактеристикСсылка.АналитикиСтатейБюджетов") Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Обязательный параметр ""Вид аналитики"" не заполнен или заполнен некорректно.';
													|en = 'The required Dimension kind  parameter is not filled or filled incorrectly.'"), , , , Отказ);
	КонецЕсли;
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	КомпоновкаДанныхСервер.ИнициализироватьКомпоновщикНастроек(КомпоновщикНастроек, АдресСхемыКомпоновкиДанныхСмещенияПериода, НастройкиКомпоновкиДанных);
	АдресНастроекКомпоновкиДанныхСмещенияПериода = ПоместитьВоВременноеХранилище(КомпоновщикНастроек.ПолучитьНастройки(), УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокВыбораСпособовЗаполненияАналитики()
	
	Элементы.НастройкиЗаполненияАналитикСпособЗаполнения.СписокВыбора.Очистить();
	Элементы.НастройкиЗаполненияАналитикСпособЗаполнения.СписокВыбора.Добавить(
		Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.УказаннымЗначением);
	Элементы.НастройкиЗаполненияАналитикСпособЗаполнения.СписокВыбора.Добавить(
		Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ИзИсточникаДанных);
	Элементы.НастройкиЗаполненияАналитикСпособЗаполнения.СписокВыбора.Добавить(
		Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоЗаданномуСоответствию);
	Если ТипПоказателяПриемника <> Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
		Элементы.НастройкиЗаполненияАналитикСпособЗаполнения.СписокВыбора.Добавить(
			Перечисления.СпособыЗаполненияАналитикСтатейБюджетов.ПоПрофилюРаспределения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСписокВыбораСпособовЗаполненияКоэффициентаПерерасчета()
	
	Для Счетчик = 1 По 2 Цикл
		СписокВыбора = ?(Счетчик = 1, Элементы.СпособЗаполненияКоэффициентаПерерасчета.СписокВыбора,
			Элементы.НастройкиРасчетаДанныхСпособЗаполненияКоэффициентаПерерасчета.СписокВыбора);
		СписокВыбора.Очистить();
		СписокВыбора.Добавить(0, НСтр("ru = 'Значением 1';
										|en = 'Value 1'"));
		СписокВыбора.Добавить(1, НСтр("ru = 'Вручную';
										|en = 'Manually'"));
		Если ТипПоказателяПриемника <> Перечисления.ТипПоказателяБюджетов.Расчетный Тогда
			СписокВыбора.Добавить(2, НСтр("ru = 'По нефинансовому показателю';
											|en = 'Non-financial item'"));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти