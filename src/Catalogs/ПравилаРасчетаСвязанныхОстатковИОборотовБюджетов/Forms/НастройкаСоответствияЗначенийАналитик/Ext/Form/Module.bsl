
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "ВидАналитики, ВидАналитикиИсточника, АдресСоответствияЗначенийАналитик");
	РеквизитыВидаАналитики = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидАналитики, "Наименование, ТипЗначения");
	РеквизитыВидаАналитикиИсточника = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидАналитикиИсточника, "Наименование, ТипЗначения");
	
	Если Не ПустаяСтрока(АдресСоответствияЗначенийАналитик) Тогда
		ТаблицаСоответствияЗначенийАналитик = ПолучитьИзВременногоХранилища(АдресСоответствияЗначенийАналитик);
		СоответствиеЗначенийАналитик.Загрузить(ТаблицаСоответствияЗначенийАналитик.Скопировать(Новый Структура("ВидАналитики", ВидАналитики)));
	КонецЕсли;
	
	Элементы.СоответствиеЗначенийАналитикЗначениеАналитикиИсточника.Заголовок = РеквизитыВидаАналитикиИсточника.Наименование;
	Элементы.СоответствиеЗначенийАналитикЗначениеАналитики.Заголовок = РеквизитыВидаАналитики.Наименование;
	Элементы.СоответствиеЗначенийАналитикЗначениеАналитикиИсточника.ОграничениеТипа = РеквизитыВидаАналитикиИсточника.ТипЗначения;
	Элементы.СоответствиеЗначенийАналитикЗначениеАналитики.ОграничениеТипа = РеквизитыВидаАналитики.ТипЗначения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	
	ПоместитьСоответствиеЗначенийАналитикВХранилище();
	
	ОповеститьОВыборе(АдресСоответствияЗначенийАналитик);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПоместитьСоответствиеЗначенийАналитикВХранилище()
	
	ТаблицаСоответствияЗначенийАналитик = ПолучитьИзВременногоХранилища(АдресСоответствияЗначенийАналитик);
	
	СтруктураПоиска = Новый Структура("ВидАналитики", ВидАналитики);
	
	СтрокиСоответствия = ТаблицаСоответствияЗначенийАналитик.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаСоответствия Из СтрокиСоответствия Цикл
		ТаблицаСоответствияЗначенийАналитик.Удалить(СтрокаСоответствия);
	КонецЦикла;
	Для Каждого СтрокаСоответствия Из СоответствиеЗначенийАналитик Цикл
		НоваяСтрока = ТаблицаСоответствияЗначенийАналитик.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСоответствия);
		НоваяСтрока.ВидАналитики = ВидАналитики;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТаблицаСоответствияЗначенийАналитик, АдресСоответствияЗначенийАналитик);
	
КонецПроцедуры

#КонецОбласти