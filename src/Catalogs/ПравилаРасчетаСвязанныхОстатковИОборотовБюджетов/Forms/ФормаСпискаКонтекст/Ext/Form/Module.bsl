
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Источник", Источник);
	Параметры.Свойство("Приемник", Приемник);
	
	Если ЗначениеЗаполнено(Источник) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Источник", Источник);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Приемник) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "Приемник", Приемник);
	КонецЕсли;
	
	НастроитьЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыОткрытия = Новый Структура;
	Если Копирование Тогда
		ПараметрыОткрытия.Вставить("ЗначениеКопирования", Элементы.Список.ТекущиеДанные.Ссылка);
	Иначе
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура("Источник, Приемник", Источник, Приемник));
	КонецЕсли;
	ОткрытьФорму("Справочник.ПравилаРасчетаСвязанныхОстатковИОборотовБюджетов.Форма.ФормаЭлемента", 
		ПараметрыОткрытия, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЭлементыФормы(Форма)

	Элементы = Форма.Элементы;
	
	Элементы.Источник.Видимость = Не ЗначениеЗаполнено(Форма.Источник);
	Элементы.Приемник.Видимость = Не ЗначениеЗаполнено(Форма.Приемник);
	
КонецПроцедуры

#КонецОбласти
