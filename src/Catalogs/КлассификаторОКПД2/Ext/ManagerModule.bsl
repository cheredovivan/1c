#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// См. ОблачныеКлассификаторыПереопределяемый.СоздатьОбновитьЭлементыОКПД2.
Процедура СоздатьОбновитьЭлементыОКПД2ИзОблачногоКлассификатора(ДанныеСервиса, Отказ) Экспорт
	
	КодыДанныхСервиса = ДанныеСервиса.ВыгрузитьКолонку("Код");
	СуществующиеЭлементыОКПД2 = ЭлементыПоКодам(КодыДанныхСервиса);
	
	Для Каждого СтрокаДанныхСервиса Из ДанныеСервиса Цикл
		СоздатьОбновитьЭлементКлассификатора(СтрокаДанныхСервиса, СуществующиеЭлементыОКПД2);
	КонецЦикла;
	
КонецПроцедуры

// Функция ищет по коду элементы в справочнике Классификатор ОКПД2.
// Если их нет, то создает элементы справочника в соответствии с классификатором ОКПД2.
// Если есть, то обновляет реквизиты этого элемента.
//
// Параметры:
//	СвойстваЭлемента - СтрокаТаблицыЗначений - см. ОблачныеКлассификаторыПереопределяемый.СоздатьОбновитьЭлементыТНВЭД,
//	СуществующиеЭлементыОКПД2 - Соответствие из Строка, СправочникСсылка.КлассификаторОКПД2 - Соответствие элементов справочника и кодов
//
// Возвращаемое значение:
//	СправочникСсылка.КлассификаторОКПД2 - ссылка на элемент классификатора.
//
Функция СоздатьОбновитьЭлементКлассификатора(СвойстваЭлемента, СуществующиеЭлементыОКПД2 = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(СуществующиеЭлементыОКПД2) Тогда
		СуществующиеЭлементыОКПД2ДляПоиска = СуществующиеЭлементыОКПД2;
	Иначе
		КодыДанныхСервиса = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СвойстваЭлемента.Код);
		СуществующиеЭлементыОКПД2ДляПоиска = ЭлементыПоКодам(КодыДанныхСервиса);
	КонецЕсли;
	
	СуществующийЭлемент = СуществующиеЭлементыОКПД2ДляПоиска.Получить(СвойстваЭлемента.Код);
	Если СуществующийЭлемент = Неопределено Тогда
		ОКПД2Объект = Справочники.КлассификаторОКПД2.СоздатьЭлемент();
	Иначе
		ОКПД2Объект = СуществующийЭлемент.ПолучитьОбъект();
		ЗаблокироватьДанныеДляРедактирования(СуществующийЭлемент);
	КонецЕсли;
	
	ОКПД2Объект.Код = СвойстваЭлемента.Код;
	ОКПД2Объект.Наименование = СвойстваЭлемента.Наименование;
	ОКПД2Объект.НаименованиеПолное = СвойстваЭлемента.Наименование;
	ОКПД2Объект.Записать();
	
	Возврат ОКПД2Объект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭлементыПоКодам(КодыОКПД2)
	
	СоответствияКодов = Новый Соответствие;
	Если Не ЗначениеЗаполнено(КодыОКПД2) Тогда
		Возврат СоответствияКодов;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодыОКПД2", КодыОКПД2);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КлассификаторОКПД2.Ссылка КАК Ссылка,
	|	КлассификаторОКПД2.Код КАК Код
	|ИЗ
	|	Справочник.КлассификаторОКПД2 КАК КлассификаторОКПД2
	|ГДЕ
	|	КлассификаторОКПД2.Код В(&КодыОКПД2)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СоответствияКодов.Вставить(Выборка.Код, Выборка.Ссылка);
	КонецЦикла;
	
	Возврат СоответствияКодов;
	
КонецФункции

#КонецОбласти

#КонецЕсли