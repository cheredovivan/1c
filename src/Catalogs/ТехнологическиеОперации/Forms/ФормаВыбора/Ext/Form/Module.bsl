
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("ОтборВладелецОпераций", ОтборВладелецОпераций) Тогда
		
		НастроитьЭлементОтборПоВладельцуОпераций();
		
		УстановитьОтборПоВладельцуОпераций(ЭтотОбъект);
		
	Иначе
		
		Элементы.ОтборВладелецОпераций.Видимость = Ложь;
		
	КонецЕсли; 

	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборВладелецОперацийПриИзменении(Элемент)
	
	УстановитьОтборПоВладельцуОпераций(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура НастроитьЭлементОтборПоВладельцуОпераций()
	
	МетаданныеВладельца = Метаданные.НайтиПоТипу(ТипЗнч(ОтборВладелецОпераций));
	Если МетаданныеВладельца <> Неопределено Тогда
		Элементы.ОтборВладелецОпераций.ТолькоПросмотр = Истина;
		Элементы.ОтборВладелецОпераций.Заголовок = МетаданныеВладельца.ПредставлениеОбъекта;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоВладельцуОпераций(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Владелец",
		Форма.ОтборВладелецОпераций,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
