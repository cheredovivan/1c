#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
// Возвращает имена реквизитов, которые не должны отображаться в списке реквизитов обработки ГрупповоеИзменениеОбъектов.
//
//	Возвращаемое значение:
//		Массив - массив имен реквизитов.
//
Функция РеквизитыНеРедактируемыеВГрупповойОбработке() Экспорт
	
	НеРедактируемыеРеквизиты = Новый Массив;
	
	Возврат НеРедактируемыеРеквизиты;
	
КонецФункции

// Возвращает имена блокруемых реквизитов для механизма блокирования реквизитов БСП
//
// Возвращаемое значение:
//	Массив - имена блокируемых реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	//++ НЕ УТКА
	Результат.Добавить("ПодразделениеДиспетчер");
	
	Результат.Добавить("ИнтервалПланирования;
		|НачалоИнтервалаПланированияДень, ОкончаниеИнтервалаПланированияДень,
		|НачалоИнтервалаПланированияНеделяДень, НачалоИнтервалаПланированияНеделяВремя,
		|ОкончаниеИнтервалаПланированияНеделяДень, ОкончаниеИнтервалаПланированияНеделяВремя");
	Результат.Добавить("УправлениеМаршрутнымиЛистами");
	Результат.Добавить("СпособПооперационногоУправления");
	Результат.Добавить("СпособПолученияПолуфабрикатов");
	Результат.Добавить("ИспользоватьПооперационноеУправление");
	Результат.Добавить("ИспользоватьМатериалыВОперациях");
	Результат.Добавить("ИспользоватьВыходныеИзделияВОперациях");
	Результат.Добавить("ИспользоватьПооперационноеПланирование");
	Результат.Добавить("ИспользоватьСменныеЗадания");
	Результат.Добавить("ИспользоватьПроизводственныеУчастки");
	//-- НЕ УТКА
	
	Результат.Добавить("ПериодичностьОформленияВыработки");
	Результат.Добавить("ИспользоватьКТУ");
	Результат.Добавить("ИспользоватьТарифныеСтавки");
	Результат.Добавить("ИспользоватьОтработанноеВремя");
	Результат.Добавить("ИспользоватьБригадныеНаряды;
		|ВидыНарядовБригады, ВидыНарядовРаботники, ВидыНарядовБригадыИРаботники");
	Результат.Добавить("ИспользоватьПерсональныеНаряды;
		|ВидыНарядовБригады, ВидыНарядовРаботники, ВидыНарядовБригадыИРаботники");
	
	Результат.Добавить("СпособНастройкиГрафикаРаботы");
	Результат.Добавить("ГрафикиРаботы; ГрафикРаботы");
	
	Результат.Добавить("ПроизводственноеПодразделение");
	Результат.Добавить("ПроизводствоПоЗаказам");
	Результат.Добавить("ПроизводствоБезЗаказов");
	Результат.Добавить("ИспользуетсяСписаниеЗатратНаВыпуск;
		|ОтдельнымДокументом, ПоПравиламРаспределения");
	
	Возврат Результат;
	
КонецФункции

// Получает подразделение-диспетчер, если оно одно в ИБ
//  Если подразделение-диспетчер не передано в качестве параметра или передано пустое,
//  возвращает единственное в информационной базе подразделение-диспетчер.
//  Если подразделений-диспетчеров в базе несколько,
//  возвращает подразделение-диспетчер из настроек пользователя.
//  Если подразделение-диспетчер не указано в настройках пользователя,
//  возвращает пустое подразделение.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - ссылка на пользователя.
// 
// Возвращаемое значение:
//  СправочникСсылка.СтруктураПредприятия - подразделение-диспетчер по умолчанию.
//
Функция ПолучитьПодразделениеДиспетчерПоУмолчанию(Знач Пользователь = Неопределено) Экспорт
	
//++ НЕ УТКА
	Запрос = Новый Запрос("
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	СтруктураПредприятия.Ссылка КАК Подразделение
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.ПодразделениеДиспетчер
		|	И СтруктураПредприятия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)
		|");
		
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		
		Подразделение = Выборка.Подразделение;
		
	ИначеЕсли ЗначениеЗаполнено(Пользователь) Тогда
		
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	Пользователи.Подразделение КАК Подразделение
			|ИЗ
			|	Справочник.Пользователи КАК Пользователи
			|ГДЕ
			|	Пользователи.Ссылка = &Пользователь
			|	И ЕСТЬNULL(Пользователи.Подразделение.ПодразделениеДиспетчер, ЛОЖЬ)
			|");
			
		Запрос.УстановитьПараметр("Пользователь", Пользователь);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Подразделение = Выборка.Подразделение;
		Иначе
			Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		
	Иначе
//-- НЕ УТКА
		Подразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
//++ НЕ УТКА
	КонецЕсли;
//-- НЕ УТКА
	
	Возврат Подразделение;

КонецФункции

// Возвращает количество подразделений-диспетчеров.
// Не учитывает предопределенные и помеченные на удаление элементы.
//
// Возвращаемое значение:
//     Число - количество подразделений-диспетчеров.
//
Функция КоличествоПодразделенийДиспетчеров() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(*) КАК Количество
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	НЕ СтруктураПредприятия.ПометкаУдаления
		|	И СтруктураПредприятия.ПодразделениеДиспетчер
		|	И СтруктураПредприятия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)");
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	Иначе
		Количество = 0;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

// Возвращает признак использования нескольких подразделений-диспетчеров.
// 
// Возвращаемое значение:
//  Булево - Истина, если используется несколько подразделений-диспетчеров.
//
Функция ИспользуетсяНесколькоПодразделенийДиспетчеров() Экспорт
	
	Возврат КоличествоПодразделенийДиспетчеров() > 1;
	
КонецФункции

//++ НЕ УТ

// Проверяет использование параметров производства
//
// Параметры:
//  Параметры		- Структура - содержит объект использование которого нужно проверить
//  АдресХранилища	- Строка - адрес хранилища в которое будут помещен результат проверки.
//
Процедура ПроверитьИспользованиеПараметровПроизводства(Параметры, АдресХранилища) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ДвижениеПродукцииИМатериалов КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Отправитель = &Подразделение
	|	И ТаблицаПоиска.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПередачаПродукцииИзПроизводства),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииНаСклад),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыпускПродукцииВПодразделение))
	|	И НЕ ТаблицаПоиска.ПоРаспоряжениям
	//++ Устарело_Производство21
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВыпускПродукции.Ссылка
	|ИЗ
	|	Документ.ВыпускПродукции КАК ВыпускПродукции
	|ГДЕ
	|	ВыпускПродукции.Подразделение = &Подразделение
	|	И НЕ ВыпускПродукции.ВыпускПоРаспоряжениям
	//-- Устарело_Производство21
	|;
	//++ НЕ УТКА
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ВидыРабочихЦентров.Ссылка
	|ИЗ
	|	Справочник.ВидыРабочихЦентров КАК ВидыРабочихЦентров
	|ГДЕ
	|	ВидыРабочихЦентров.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Подразделение = &Подразделение
	|	И ЭтапыПроизводства.ПланироватьРаботуВидовРабочихЦентров
	|;
	|
	//++ Устарело_Производство21
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаПроизводство.Ссылка
	|ИЗ
	|	Документ.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	//-- Устарело_Производство21
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЗаказНаПроизводство.Ссылка
	|ИЗ
	|	Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство
	|ГДЕ
	|	ЗаказНаПроизводство.Подразделение = &Подразделение
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭтапыПроизводства.Ссылка
	|ИЗ
	|	Справочник.ЭтапыПроизводства КАК ЭтапыПроизводства
	|ГДЕ
	|	ЭтапыПроизводства.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.Трудозатраты КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2 КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТаблицаПоиска.Ссылка
	|ИЗ
	|	Документ.СменноеЗадание КАК ТаблицаПоиска
	|ГДЕ
	|	ТаблицаПоиска.Подразделение = &Подразделение
	|
	|;
	//++ Устарело_Производство21
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	МаршрутныйЛистПроизводства.Ссылка
	|ИЗ
	|	Документ.МаршрутныйЛистПроизводства КАК МаршрутныйЛистПроизводства
	|ГДЕ
	|	МаршрутныйЛистПроизводства.Подразделение = &Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПооперационноеРасписание.ИдентификаторОперации
	|ИЗ
	|	РегистрСведений.ПооперационноеРасписание КАК ПооперационноеРасписание
	|ГДЕ
	|	ПооперационноеРасписание.Подразделение = &Подразделение
	|;
	//-- Устарело_Производство21	

	//-- НЕ УТКА
	|";
	
	Запрос.УстановитьПараметр("Подразделение", Параметры.Объект);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	РезультатПроверки = Новый Структура;
	
	РезультатПроверки.Вставить("ИспользуетсяДляПроизводстваБезЗаказов",       НЕ Результат[0].Пустой());
	
	//++ НЕ УТКА
	РезультатПроверки.Вставить("ИнтервалПланированияИспользуется",            НЕ Результат[1].Пустой());
	
	РезультатПроверки.Вставить("ИспользуетсяКакДиспетчер",                    НЕ Результат[2].Пустой());
	
	РезультатПроверки.Вставить("ИспользуетсяДляПроизводстваПоЗаказам",        НЕ Результат[3].Пустой());
	
	РезультатПроверки.Вставить("ИспользуетсяПооперационноеУправлениеЭтапами", НЕ Результат[4].Пустой());
	
	РезультатПроверки.Вставить("ИспользуютсяСменныеЗадания",                  НЕ Результат[5].Пустой());
	
	//++ Устарело_Производство21
	РезультатПроверки.Вставить("УправлениеМаршрутнымиЛистамиИспользуется",    НЕ Результат[6].Пустой());
	
	РезультатПроверки.Вставить("ПооперационноеРасписаниеИспользуется",        НЕ Результат[7].Пустой());
	//-- Устарело_Производство21
	
	//-- НЕ УТКА
	
	ПоместитьВоВременноеХранилище(РезультатПроверки, АдресХранилища);
	
КонецПроцедуры

// Определяет цеховую кладовую подразделения
//
// Параметры:
//	Подразделение	- СправочникСсылка.СтруктураПредприятия - Подразделение для которого определяется кладовая.
//
// Возвращаемое значение:
//		СправочникСсылка.Склады - Цеховая кладовая подразделения.
//
Функция ЦеховаяКладоваяПоУмолчанию(Подразделение) Экспорт

	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		
		Если НЕ ЗначениеЗаполнено(Подразделение) Тогда
			Возврат Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	Таблица.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ПометкаУдаления
		|	И НЕ Таблица.ЭтоГруппа
		|	И Таблица.Подразделение = &Подразделение
		|	И Таблица.ЦеховаяКладовая";
		
	Иначе
		
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 2
		|	Таблица.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ПометкаУдаления
		|	И НЕ Таблица.ЭтоГруппа
		|	И Таблица.ЦеховаяКладовая";
		
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() И Выборка.Количество() = 1 Тогда
		Склад = Выборка.Склад;
	Иначе
		Склад = Справочники.Склады.ПустаяСсылка();
	КонецЕсли;
	
	Возврат Склад;

КонецФункции

//-- НЕ УТ

//++ НЕ УТКА

// Возвращает даты по которые нужно заполнить доступность
//
// Параметры:
//  Подразделение	- СправочникСсылка.СтруктураПредприятия - подразделение для которого нужно получить данные.
//
// Возвращаемое значение:
//   Структура   - Даты по которые нужно заполнить доступность.
//
Функция ДоступностьТребуетсяЗаполнитьПо(Подразделение) Экспорт
	
	ДоступностьДляГрафикаНужноЗаполнитьПо = '000101010000';
	ГрафикРаботыРЦНужнозаполнитьПо = '000101010000';
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтруктураПредприятия.ЗаполнениеДоступностиДляГрафикаПроизводстваНапоминаниеДней,
	|	СтруктураПредприятия.ЗаполнениеДоступностиДляРасписанияРЦНапоминаниеДней
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка = &Подразделение";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Если Выборка.Следующий() Тогда
		ТекущаяДатаСеанса = НачалоДня(ТекущаяДатаСеанса());
		Если Выборка.ЗаполнениеДоступностиДляГрафикаПроизводстваНапоминаниеДней <> 0 Тогда
			ДоступностьДляГрафикаНужноЗаполнитьПо = ТекущаяДатаСеанса + Выборка.ЗаполнениеДоступностиДляГрафикаПроизводстваНапоминаниеДней * 86400;
		КонецЕсли; 
		Если Выборка.ЗаполнениеДоступностиДляРасписанияРЦНапоминаниеДней <> 0 Тогда
			ГрафикРаботыРЦНужнозаполнитьПо = ТекущаяДатаСеанса + Выборка.ЗаполнениеДоступностиДляРасписанияРЦНапоминаниеДней * 86400;
		КонецЕсли; 
	КонецЕсли; 
	
	ТребуетсяЗаполнитьПо = Новый Структура;
	ТребуетсяЗаполнитьПо.Вставить("ДоступностьДляГрафикаНужноЗаполнитьПо", ДоступностьДляГрафикаНужноЗаполнитьПо);
	ТребуетсяЗаполнитьПо.Вставить("ГрафикРаботыРЦНужнозаполнитьПо", ГрафикРаботыРЦНужнозаполнитьПо);
	
	Возврат ТребуетсяЗаполнитьПо;

КонецФункции

// Заполняет служебные константы:
//	 - ИспользоватьМетодикуББВ,
//	 - ИспользоватьМетодикуПооперационноеПланирование,
//	 - ИспользоватьМетодикуРегистрацияОпераций,
//	 - ИспользоватьПооперационноеУправление,
//	 - ИспользоватьПооперационноеПланирование.
//
// Параметры:
//  СлужебныеКонстанты - Неопределено, Структура - ключи, это имена служебных констант,
//                                                 если соответсвующий служебной константе реквизит(ы) справочника
//                                                 установлен, тогда в значение установлено ИСТИНА, иначе ЛОЖЬ.
//
Процедура ЗаполнитьИспользуемыеМетодикиУправления(СлужебныеКонстанты = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Таблицы = Новый Массив;
	Поля    = Новый Массив;
	
	Если ЗначениеЗаполнено(СлужебныеКонстанты) Тогда
		
		СтруктураСлужебныхКонстант = СлужебныеКонстанты;
		
	Иначе
		
		СтруктураСлужебныхКонстант = Новый Структура;
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьПооперационноеПланирование",         Ложь);
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьПооперационноеУправление",           Ложь);
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьСменныеЗадания",                     Ложь);
		
		//++ Устарело_Производство21
		
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьМетодикуББВ",                        Ложь);
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьМетодикуПооперационноеПланирование", Ложь);
		СтруктураСлужебныхКонстант.Вставить("ИспользоватьМетодикуРегистрацияОпераций",        Ложь);
		
		//-- Устарело_Производство21
		
	КонецЕсли;
	
	Для каждого СлужебнаяКонстанта Из СтруктураСлужебныхКонстант Цикл
		
		Если СлужебнаяКонстанта.Значение Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяКонстанты = СлужебнаяКонстанта.Ключ;
		
		Если ИмяКонстанты = "ИспользоватьПооперационноеУправление" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_2.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.ИспользоватьПооперационноеУправление
			|						И НЕ СтруктураПредприятия.ИспользоватьПооперационноеПланирование)
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьПооперационноеУправление");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2");
			КонецЕсли;
			
		ИначеЕсли ИмяКонстанты = "ИспользоватьПооперационноеПланирование" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_2.Значение И НЕ ДинамическаяСтруктура.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.ИспользоватьПооперационноеУправление
			|						И СтруктураПредприятия.ИспользоватьПооперационноеПланирование)
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьПооперационноеПланирование");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2");
			КонецЕсли;
			
			Таблицы.Добавить("Константа.ДинамическаяСтруктураЗаказовНаПроизводство КАК ДинамическаяСтруктура");
			
		ИначеЕсли ИмяКонстанты = "ИспользоватьСменныеЗадания" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_2.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.ИспользоватьСменныеЗадания)
			|		ИНАЧЕ ЛОЖЬ
			| 	КОНЕЦ КАК ИспользоватьСменныеЗадания");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством2_2 КАК Производство2_2");
			КонецЕсли;
			
		//++ Устарело_Производство21
		
		ИначеЕсли ИмяКонстанты = "ИспользоватьМетодикуББВ" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_1.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.УправлениеМаршрутнымиЛистами = ЗНАЧЕНИЕ(Перечисление.УправлениеМаршрутнымиЛистами.МетодикаББВ))
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьМетодикуББВ");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1");
			КонецЕсли;
			
		ИначеЕсли ИмяКонстанты = "ИспользоватьМетодикуПооперационноеПланирование" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_1.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.УправлениеМаршрутнымиЛистами = ЗНАЧЕНИЕ(Перечисление.УправлениеМаршрутнымиЛистами.ПооперационноеПланирование))
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьМетодикуПооперационноеПланирование");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1");
			КонецЕсли;
			
		ИначеЕсли ИмяКонстанты = "ИспользоватьМетодикуРегистрацияОпераций" Тогда
			
			Поля.Добавить("
			|	ВЫБОР
			|		КОГДА Производство2_1.Значение
			|			ТОГДА ИСТИНА В
			|					(ВЫБРАТЬ ПЕРВЫЕ 1
			|						ИСТИНА
			|					ИЗ
			|						Справочник.СтруктураПредприятия КАК СтруктураПредприятия
			|					ГДЕ
			|						СтруктураПредприятия.УправлениеМаршрутнымиЛистами = ЗНАЧЕНИЕ(Перечисление.УправлениеМаршрутнымиЛистами.РегистрацияОпераций))
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ИспользоватьМетодикуРегистрацияОпераций");
			
			Если Таблицы.Найти("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1") = Неопределено Тогда
				Таблицы.Добавить("Константа.ИспользоватьУправлениеПроизводством КАК Производство2_1");
			КонецЕсли;
			
		//-- Устарело_Производство21
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Поля.Количество() > 0 Тогда
		
		Разделитель = СтрШаблон("%1%2", ",", Символы.ПС);
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Значение
		|ИЗ
		|	Таблица КАК Таблица";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица.Значение",	СтрСоединить(Поля, Разделитель));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Таблица КАК Таблица", СтрСоединить(Таблицы, Разделитель));
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		
		РезультатЗапроса = Запрос.Выполнить();
		
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		
		Для каждого СлужебнаяКонстанта Из СтруктураСлужебныхКонстант Цикл
			ЭлементБлокировки = Блокировка.Добавить(СтрШаблон("Константа.%1", СлужебнаяКонстанта.Ключ));
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		КонецЦикла;
		
		Блокировка.Заблокировать();
		
		Если Поля.Количество() > 0 Тогда
			
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			
		КонецЕсли;
		
		Для каждого СлужебнаяКонстанта Из СтруктураСлужебныхКонстант Цикл
			
			ИмяКонстанты      = СлужебнаяКонстанта.Ключ;
			ЗначениеКонстанты = ?(СлужебнаяКонстанта.Значение, Истина, Выборка[ИмяКонстанты]);
			
			Если Константы[ИмяКонстанты].Получить() <> ЗначениеКонстанты Тогда
				Константы[ИмяКонстанты].Установить(ЗначениеКонстанты);
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстСообщения = НСтр("ru = 'Не удалось записать используемые методики управления производством';
								|en = 'Cannot save the used production management methods'");
		
		СобытиеЖурналаРегистрации = 
			НСтр("ru = 'Межцеховое управление';
				|en = 'Production scheduling and controlling'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())
			+ "."
			+ НСтр("ru = 'Ошибка заполнения методик управления производством';
					|en = 'Production management methods filling error'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации,
			УровеньЖурналаРегистрации.Предупреждение,
			, 
			, 
			ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

// Определяет используется ли пооперационное управление этапами производства в подразделении.
//
// Параметры:
//	Подразделение	- СправочникСсылка.СтруктураПредприятия - Подразделение.
//
// Возвращаемое значение:
//		Булево - Истина, если в подразделении используется пооперационное управление.
//
Функция ИспользоватьПооперационноеУправление(Подразделение) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка = &Ссылка
	|	И СтруктураПредприятия.ИспользоватьПооперационноеУправление
	|	И НЕ СтруктураПредприятия.ИспользоватьПооперационноеПланирование");
	Запрос.УстановитьПараметр("Ссылка", Подразделение);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Определяет используется ли пооперационное планирование операций этапов производства в подразделении.
//
// Параметры:
//	Подразделение	- СправочникСсылка.СтруктураПредприятия - Подразделение.
//
// Возвращаемое значение:
//		Булево - Истина, если в подразделении используется пооперационное планирование.
//
Функция ИспользоватьПооперационноеПланирование(Подразделение) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	СтруктураПредприятия.Ссылка = &Ссылка
	|	И СтруктураПредприятия.ИспользоватьПооперационноеУправление
	|	И СтруктураПредприятия.ИспользоватьПооперационноеПланирование");
	Запрос.УстановитьПараметр("Ссылка", Подразделение);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

//-- НЕ УТКА

// Возвращает значения по умолчанию для параметров подразделения.
// 
// Возвращаемое значение:
//  Структура - Значения по умолчанию:
//		* Ссылка - СправочникСсылка.СтруктураПредприятия - Ссылка на элемент справочника "Структура предприятия"
//		* ВариантОбособленногоУчетаТоваров - ПеречислениеСсылка.ВариантыОбособленногоУчетаТоваров - Вариант обособленного учета товаров
//		* ПроизводственноеПодразделение - Булево - Признак производственного подразделения
//		* ИнтервалПланирования - Неопределено - Интервал планирования
//		* НачалоИнтервалаПланирования - Дата - Начало интервала планирования
//		* ОкончаниеИнтервалаПланирования - Дата - Окончание интервала планирования
//		* ГрафикРаботы - СправочникСсылка.Календари, Неопределено - График работы подразделения
//		* ПодразделениеДиспетчер - Булево - Подразделение-диспетчер
//		* ИспользоватьСерииНоменклатуры - Булево - Использовать серии номенклатуры
//		* УчитыватьСебестоимостьПоСериям - Булево - Учитывать себестоимость по сериям
//		* ПроизводствоПоЗаказам - Булево - Производство по заказам
//		* ПроизводствоБезЗаказов - Булево - Производство без заказов
//		* ИспользуетсяСписаниеЗатратНаВыпуск - Булево - Используется списание затрат на выпуск
//		* ИспользоватьПооперационноеУправление - Булево - Использовать пооперационное управление
//		* ИспользоватьМатериалыВОперациях - Булево - Использовать материалы в операциях
//		* ИспользоватьВыходныеИзделияВОперациях - Булево - Использовать выходные изделия в операциях
//		* ИспользоватьПооперационноеПланирование - Булево - Использовать пооперационное планирование
//		* ПериодичностьОформленияВыработки - Неопределено - Периодичность оформления выработки
//		* ИспользоватьКТУ - Булево - Использовать КТУ
//		* ИспользоватьТарифныеСтавки - Булево - Использовать тарифные ставки
//		* ИспользоватьОтработанноеВремя - Булево - Использовать отработанное время
//		* ИспользоватьБригадныеНаряды - Булево - Использовать бригадные наряды
//		* ИспользоватьПерсональныеНаряды - Булево - Использовать персональные наряды
//		* СпособНастройкиГрафикаРаботы - ПеречислениеСсылка.СпособыНастройкиГрафикаРаботыПодразделений - Способ настройки графика работы
//		* ИспользоватьСменныеЗадания - Булево - Использовать сменные задания
//		* ИспользоватьПроизводственныеУчастки - Булево - Использовать производственные участки
//		* Статус - ПеречислениеСсылка.СтатусыОрганизацийПодразделений - Статус подразделения
Функция ЗначенияПоУмолчанию() Экспорт
	
	СтруктураЗначений = Новый Структура;
	
	СтруктураЗначений.Вставить("Ссылка", Справочники.СтруктураПредприятия.ПустаяСсылка());
	СтруктураЗначений.Вставить("ВариантОбособленногоУчетаТоваров", Перечисления.ВариантыОбособленногоУчетаТоваров.НеВедется);
	СтруктураЗначений.Вставить("ПроизводственноеПодразделение", Ложь);
	СтруктураЗначений.Вставить("ИнтервалПланирования", Неопределено);
	СтруктураЗначений.Вставить("НачалоИнтервалаПланирования", '00010101');
	СтруктураЗначений.Вставить("ОкончаниеИнтервалаПланирования", '00010101');
	СтруктураЗначений.Вставить("ГрафикРаботы", Неопределено);
	СтруктураЗначений.Вставить("ПодразделениеДиспетчер", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьСерииНоменклатуры", Ложь);
	СтруктураЗначений.Вставить("УчитыватьСебестоимостьПоСериям", Ложь);
	СтруктураЗначений.Вставить("ПроизводствоПоЗаказам", Ложь);
	СтруктураЗначений.Вставить("ПроизводствоБезЗаказов", Ложь);
	СтруктураЗначений.Вставить("ИспользуетсяСписаниеЗатратНаВыпуск", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьПооперационноеУправление", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьМатериалыВОперациях", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьВыходныеИзделияВОперациях", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьПооперационноеПланирование", Ложь);
	СтруктураЗначений.Вставить("ПериодичностьОформленияВыработки", Неопределено);
	СтруктураЗначений.Вставить("ИспользоватьКТУ", Ложь); 
	СтруктураЗначений.Вставить("ИспользоватьТарифныеСтавки", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьОтработанноеВремя", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьБригадныеНаряды", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьПерсональныеНаряды", Ложь);
	СтруктураЗначений.Вставить("СпособНастройкиГрафикаРаботы", Перечисления.СпособыНастройкиГрафикаРаботыПодразделений.ОсновнойКалендарьПредприятия);
	СтруктураЗначений.Вставить("ИспользоватьСменныеЗадания", Ложь);
	СтруктураЗначений.Вставить("ИспользоватьПроизводственныеУчастки", Ложь);
	СтруктураЗначений.Вставить("Статус", Перечисления.СтатусыОрганизацийПодразделений.Действует);
	
	Возврат СтруктураЗначений
	
КонецФункции

// Формирует действующие параметры учета по подраздедениям.
//
// Параметры:
//	Подразделения - Массив Из СправочникСсылка.СтруктураПредприятия, СправочникСсылка.СтруктураПредприятия, Неопределено - подразделения
// 
// Возвращаемое значение:
//   ТаблицаЗначений - таблица значений с колонками:
//		* Ссылка - СправочникСсылка.СтруктураПредприятия - Ссылка на элемент справочника "Структура предприятия"
//		* Представление - Строка - Представление подразделения
//		* ВариантОбособленногоУчетаТоваров - ПеречислениеСсылка.ВариантыОбособленногоУчетаТоваров - Вариант обособленного учета товаров
//		* ПроизводственноеПодразделение - Булево - Признак производственного подразделения
//		* ИнтервалПланирования - ПеречислениеСсылка.ТочностьГрафикаПроизводства - Интервал планирования
//		* НачалоИнтервалаПланирования - Дата - Начало интервала планирования
//		* ОкончаниеИнтервалаПланирования - Дата - Окончание интервала планирования
//		* ГрафикРаботы - СправочникСсылка.Календари - График работы подразделения
//		* ПодразделениеДиспетчер - Булево - Подразделение-диспетчер
//		* ИспользоватьСерииНоменклатуры - Булево - Использовать серии номенклатуры
//		* УчитыватьСебестоимостьПоСериям - Булево - Учитывать себестоимость по сериям
//		* ПроизводствоПоЗаказам - Булево - Производство по заказам
//		* ПроизводствоБезЗаказов - Булево - Производство без заказов
//		* ИспользуетсяСписаниеЗатратНаВыпуск - Булево - Используется списание затрат на выпуск
//		* ИспользоватьПооперационноеУправление - Булево - Использовать пооперационное управление
//		* ИспользоватьМатериалыВОперациях - Булево - Использовать материалы в операциях
//		* ИспользоватьВыходныеИзделияВОперациях - Булево - Использовать выходные изделия в операциях
//		* ИспользоватьПооперационноеПланирование - Булево - Использовать пооперационное планирование
//		* ПериодичностьОформленияВыработки - ПеречислениеСсылка.ПериодичностьОформленияВыработкиСотрудников - Периодичность оформления выработки
//		* ИспользоватьКТУ - Булево - Использовать КТУ
//		* ИспользоватьТарифныеСтавки - Булево - Использовать тарифные ставки
//		* ИспользоватьОтработанноеВремя - Булево - Использовать отработанное время
//		* ИспользоватьБригадныеНаряды - Булево - Использовать бригадные наряды
//		* ИспользоватьПерсональныеНаряды - Булево - Использовать персональные наряды
//		* СпособНастройкиГрафикаРаботы - ПеречислениеСсылка.СпособыНастройкиГрафикаРаботыПодразделений - Способ настройки графика работы
//		* ИспользоватьСменныеЗадания - Булево - Использовать сменные задания
//		* ИспользоватьПроизводственныеУчастки - Булево - Использовать производственные участки
//		* Статус - ПеречислениеСсылка.СтатусыОрганизацийПодразделений - Статус подразделения
Функция ДействующиеПараметрыУчета(Подразделения = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтруктураПредприятия.Ссылка КАК Ссылка,
		|	СтруктураПредприятия.Представление КАК Представление,
		|	СтруктураПредприятия.ВариантОбособленногоУчетаТоваров КАК ВариантОбособленногоУчетаТоваров,
		|	СтруктураПредприятия.ПроизводственноеПодразделение КАК ПроизводственноеПодразделение,
		|	СтруктураПредприятия.ИнтервалПланирования КАК ИнтервалПланирования,
		|	СтруктураПредприятия.НачалоИнтервалаПланирования КАК НачалоИнтервалаПланирования,
		|	СтруктураПредприятия.ОкончаниеИнтервалаПланирования КАК ОкончаниеИнтервалаПланирования,
		|	СтруктураПредприятия.ГрафикРаботы КАК ГрафикРаботы,
		|	СтруктураПредприятия.СпособПооперационногоУправления КАК СпособПооперационногоУправления,
		|	СтруктураПредприятия.ПодразделениеДиспетчер КАК ПодразделениеДиспетчер,
		|	СтруктураПредприятия.ИспользоватьСерииНоменклатуры КАК ИспользоватьСерииНоменклатуры,
		|	СтруктураПредприятия.УчитыватьСебестоимостьПоСериям КАК УчитыватьСебестоимостьПоСериям,
		|	СтруктураПредприятия.ПроизводствоПоЗаказам КАК ПроизводствоПоЗаказам,
		|	СтруктураПредприятия.ПроизводствоБезЗаказов КАК ПроизводствоБезЗаказов,
		|	СтруктураПредприятия.ИспользуетсяСписаниеЗатратНаВыпуск КАК ИспользуетсяСписаниеЗатратНаВыпуск,
		|	СтруктураПредприятия.ИспользоватьПооперационноеУправление КАК ИспользоватьПооперационноеУправление,
		|	СтруктураПредприятия.ИспользоватьМатериалыВОперациях КАК ИспользоватьМатериалыВОперациях,
		|	СтруктураПредприятия.ИспользоватьВыходныеИзделияВОперациях КАК ИспользоватьВыходныеИзделияВОперациях,
		|	СтруктураПредприятия.ИспользоватьПооперационноеПланирование КАК ИспользоватьПооперационноеПланирование,
		|	СтруктураПредприятия.ПериодичностьОформленияВыработки КАК ПериодичностьОформленияВыработки,
		|	СтруктураПредприятия.ИспользоватьКТУ КАК ИспользоватьКТУ,
		|	СтруктураПредприятия.ИспользоватьТарифныеСтавки КАК ИспользоватьТарифныеСтавки,
		|	СтруктураПредприятия.ИспользоватьОтработанноеВремя КАК ИспользоватьОтработанноеВремя,
		|	СтруктураПредприятия.ИспользоватьБригадныеНаряды КАК ИспользоватьБригадныеНаряды,
		|	СтруктураПредприятия.ИспользоватьПерсональныеНаряды КАК ИспользоватьПерсональныеНаряды,
		|	СтруктураПредприятия.СпособНастройкиГрафикаРаботы КАК СпособНастройкиГрафикаРаботы,
		|	СтруктураПредприятия.ИспользоватьСменныеЗадания КАК ИспользоватьСменныеЗадания,
		|	СтруктураПредприятия.ИспользоватьПроизводственныеУчастки КАК ИспользоватьПроизводственныеУчастки,
		|	СтруктураПредприятия.Статус КАК Статус
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	ВЫБОР
		|		КОГДА &ПоВсем
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ СтруктураПредприятия.Ссылка В (&Подразделения)
		|	КОНЕЦ
		|	И СтруктураПредприятия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)
		|	И НЕ СтруктураПредприятия.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Подразделения", Подразделения);
	Запрос.УстановитьПараметр("ПоВсем", Подразделения = Неопределено);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса;
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Подразделение - СправочникСсылка.СтруктураПредприятия - ссылка на подразделение.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеПараметрыУчета - СтрокаТаблицыЗначений Из см. ДействующиеПараметрыУчета
// 	
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Подразделение, ДатаДействия = Неопределено, ДействующиеПараметрыУчета = Неопределено) Экспорт
	
	Если ДействующиеПараметрыУчета = Неопределено Тогда
		ДействующиеПараметрыУчетаПоПодразделениям = ДействующиеПараметрыУчета(Подразделение);
		Если ДействующиеПараметрыУчетаПоПодразделениям.Количество() > 0 Тогда
			ДействующиеПараметрыУчета = ДействующиеПараметрыУчетаПоПодразделениям[0];
		Иначе
			ДействующиеПараметрыУчета = ЗначенияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	Если ДатаДействия = Неопределено Тогда
		ДатаДействия = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	
	СтрокаОписанияНастроек = СтрШаблон(СтрокаШаблон, 
		НСтр("ru = 'Статус';
			|en = 'Status'"),
		ДействующиеПараметрыУчета.Статус);
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон, 
				НСтр("ru = 'Обособленный учет товаров по подразделению';
					|en = 'Goods assignment by business unit'"),
				ДействующиеПараметрыУчета.ВариантОбособленногоУчетаТоваров);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство") Тогда
		
		Если ДействующиеПараметрыУчета.ПроизводственноеПодразделение Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Подразделение является производственным';
					|en = 'Business unit is the production one'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Подразделение не является производственным';
					|en = 'This is not a production business unit'"));
		КонецЕсли;
		
		//++ НЕ УТКА
		Если ДействующиеПараметрыУчета.ПодразделениеДиспетчер Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Подразделение-диспетчер, ответственное за выполнение заказов на производство';
					|en = 'Dispatching unit responsible for fulfilling production orders'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Подразделение не является диспетчером';
					|en = 'This is not a dispatching business unit'"));
		КонецЕсли;
		//-- НЕ УТКА
		
		Если ДействующиеПараметрыУчета.СпособНастройкиГрафикаРаботы = Перечисления.СпособыНастройкиГрафикаРаботыПодразделений.ОсновнойКалендарьПредприятия Тогда
			КалендарьПредприятия = Константы.ОсновнойКалендарьПредприятия.Получить();
			Если НЕ ЗначениеЗаполнено(КалендарьПредприятия) Тогда
				КалендарьПредприятия = НСтр("ru = 'не задан';
											|en = 'not specified'");
			КонецЕсли;
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон, 
					НСтр("ru = 'Основной график работы предпрития';
						|en = 'Main enterprise schedule'"),
					КалендарьПредприятия);
		ИначеЕсли ДействующиеПараметрыУчета.СпособНастройкиГрафикаРаботы = Перечисления.СпособыНастройкиГрафикаРаботыПодразделений.ИндивидуальныйГрафик Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон, 
					НСтр("ru = 'Индивидуальный график работы';
						|en = 'Individual schedule'"),
					ДействующиеПараметрыУчета.ГрафикРаботы);
		//++ НЕ УТКА
		ИначеЕсли ДействующиеПараметрыУчета.СпособНастройкиГрафикаРаботы = Перечисления.СпособыНастройкиГрафикаРаботыПодразделений.Смены Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Работа в несколько смен';
					|en = 'Work in several shifts'"));
		//-- НЕ УТКА
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Не указан график работы';
					|en = 'Work schedule is not specified'"));
		КонецЕсли;
		
		//++ НЕ УТКА
		Если ДействующиеПараметрыУчета.ПроизводственноеПодразделение Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон, 
					НСтр("ru = 'Интервал планирования';
						|en = 'Planning interval'"),
					ДействующиеПараметрыУчета.ИнтервалПланирования);
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Интервал планирования не задан';
					|en = 'Planning interval is not set'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьПроизводственныеУчастки Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Производственные участки используются';
					|en = 'Production sites are used'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Производственные участки не используются';
					|en = 'Production sites are not used'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьПооперационноеУправление Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Используется пооперационное управление этапами производства';
					|en = 'Operation management for production stages is used'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Пооперационное управление этапами производства не используется';
					|en = 'Operation management for production stages is not used'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьМатериалыВОперациях Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Ведется учет материалов в производственных операциях';
					|en = 'Records of materials in routing operations are kept'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Учет материалов в производственных операциях не ведется';
					|en = 'Records of materials in routing operations are not kept'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьВыходныеИзделияВОперациях Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Ведется учет выходных изделий в производственных операциях';
					|en = 'Records of finished products in routing operations are kept'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Учет выходных изделий в производственных операциях не ведется';
					|en = 'Records of finished products in routing operations are not kept'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьСменныеЗадания Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Производственные операции объединяются в сменные задания';
					|en = 'Production operations are combined into shift tasks'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Производственные операции не объединяются в сменные задания';
					|en = 'Production operations are not combined into shift tasks'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьПооперационноеПланирование Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Используется пооперационное планирование этапов производства';
					|en = 'Manufacturing execution system is used for production stages'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Пооперационное планирование этапов производства не используется';
					|en = 'Manufacturing execution system is not used for production stages'"));
		КонецЕсли;
		//-- НЕ УТКА
		
		Если ДействующиеПараметрыУчета.ПроизводственноеПодразделение Тогда
			Если ДействующиеПараметрыУчета.ИспользоватьБригадныеНаряды И ДействующиеПараметрыУчета.ИспользоватьПерсональныеНаряды Тогда
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево, 
					НСтр("ru = 'Исполнителями работ назначаются бригады и работники';
						|en = 'Work teams and employees are assigned as work performers'"));
			ИначеЕсли ДействующиеПараметрыУчета.ИспользоватьБригадныеНаряды Тогда
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево, 
					НСтр("ru = 'Исполнителями работ назначаются бригады';
						|en = 'Work teams are assigned as work performers'"));
			Иначе
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево, 
					НСтр("ru = 'Исполнителями работ назначаются работники';
						|en = 'Employees are assigned as work performers'"));
			КонецЕсли;
			
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьКТУ Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады с учетом коэффициентов трудового участия';
					|en = 'Performed works are allocated to work team members according to the labor participation rates'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады без учета коэффициентов трудового участия';
					|en = 'Performed works are allocated to work team members without taking into account the labor participation rates'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьОтработанноеВремя Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады с учетом отработанного времени';
					|en = 'Performed works are allocated to work team members according to hours worked'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады без учета отработанного времени';
					|en = 'Performed works are allocated to work team members without taking into account hours worked'"));
		КонецЕсли;
		
		Если ДействующиеПараметрыУчета.ИспользоватьТарифныеСтавки Тогда
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады с учетом тарифных ставок';
					|en = 'Performed works are allocated to work team members according to the tariff rates'"));
		Иначе
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево, 
				НСтр("ru = 'Выполненные работы распределяются по работникам бригады без учета тарифных ставок';
					|en = 'Performed works are allocated to work team members without taking into account tariff rates'"));
		КонецЕсли;
		
		//++ НЕ УТ
		Если Константы.ИспользоватьПериодичностьОформленияВыработки.Получить() Тогда
			Если ЗначениеЗаполнено(ДействующиеПараметрыУчета.ПериодичностьОформленияВыработки) Тогда
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблон, 
						НСтр("ru = 'Выработка сотрудников оформляется';
							|en = 'Timesheets charge is registered'"),
						ДействующиеПараметрыУчета.ПериодичностьОформленияВыработки);
			Иначе
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево, 
					НСтр("ru = 'Периодичность оформления выработка сотрудников не указана';
						|en = 'Frequency of timesheets charge registration is not specified'"));
			КонецЕсли;
		КонецЕсли;
		//-- НЕ УТ
	КонецЕсли;
	
	Возврат СтрокаОписанияНастроек
	
КонецФункции

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	
	Команда = Документы.ПоручениеЭкспедитору.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	Если Команда <> Неопределено Тогда
		Команда.РежимЗаписи = "";
	КонецЕсли;
	
	БизнесПроцессы.Задание.ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании);
	
КонецПроцедуры

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	//++ НЕ УТ
	Если Параметры.Свойство("Отбор") И (Параметры.Отбор.Свойство("ПодразделениеДиспетчерИлиПроизводственноеПодразделение") 
		Или Параметры.Отбор.Свойство("ПроизводственноеПодразделение")) Тогда
		ПроизводствоВызовСервера.СтруктураПредприятияОбработкаПолученияДанныхВыбора(
					ДанныеВыбора, 
					Параметры, 
					СтандартнаяОбработка);
		Возврат;	
	КонецЕсли; 
	//-- НЕ УТ
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Параметры.СтрокаПоиска) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 51
		|	СтруктураПредприятия.Ссылка КАК Ссылка,
		|	СтруктураПредприятия.ПометкаУдаления КАК ПометкаУдаления,
		|	СтруктураПредприятия.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА СтруктураПредприятия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.НеДействует)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК НеДействует,
		|	СтруктураПредприятия.Код КАК Код,
		|	ЛОЖЬ КАК ПодборПоКоду
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Наименование ПОДОБНО &Текст
		|	И НЕ СтруктураПредприятия.ПометкаУдаления
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 51
		|	СтруктураПредприятия.Ссылка,
		|	СтруктураПредприятия.ПометкаУдаления,
		|	СтруктураПредприятия.Наименование КАК Наименование,
		|	ВЫБОР
		|		КОГДА СтруктураПредприятия.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.НеДействует)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ,
		|	СтруктураПредприятия.Код,
		|	ИСТИНА
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
		|ГДЕ
		|	СтруктураПредприятия.Код ПОДОБНО &Текст
		|	И НЕ СтруктураПредприятия.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Наименование";
		
		Запрос.УстановитьПараметр("Текст", "%" + СокрЛП(Параметры.СтрокаПоиска) + "%");
		
		Если МультиязычностьСервер.ИспользуетсяДополнительныйЯзык("1") 
			Или МультиязычностьСервер.ИспользуетсяДополнительныйЯзык("2") Тогда
			
			ИменаРеквизитов = МультиязычностьСервер.ИменаРеквизитовСУчетомКодаЯзыка("Наименование");
			
			ШаблонЗамены = СтрЗаменить("СтруктураПредприятия.%1", "%1", ИменаРеквизитов["Наименование"]);
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "СтруктураПредприятия.Наименование",
				ШаблонЗамены);
			
		КонецЕсли;
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		ДанныеВыбора = Новый СписокЗначений;
		Пока Выборка.Следующий() Цикл       
			ШаблонПредставления = "%1 (%2)";
			Если Выборка.ПодборПоКоду Тогда
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
					Выборка.Код, Выборка.Наименование);
			Иначе
				Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
					Выборка.Наименование, Выборка.Код);
			КонецЕсли;
					
			Если Выборка.НеДействует Тогда 
				Элемент = Новый Структура;
				Элемент.Вставить("Значение", Выборка.Ссылка);
				Элемент.Вставить("ПометкаУдаления", Выборка.ПометкаУдаления);
				ТекстПредупреждения = НСтр("ru = 'Выбрано недействующее подразделение.';
											|en = 'An inactive business unit is selected.'"); 
				Элемент.Вставить("Предупреждение", ТекстПредупреждения);
				ДанныеВыбора.Добавить(Элемент, Представление);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка, Представление);
			КонецЕсли;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Печать

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область Отчеты

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.СтруктураПредприятия.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.24.25";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("3729d747-7cb7-426a-afb4-002f38ef369c");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.СтруктураПредприятия.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Обновление справочника ""Подразделение"":';
								|en = 'Update the ""Business unit"" catalog:'"));
	СписокОписаний.Добавить(НСтр("ru = '- Заполнение мультиязычных реквизитов справочника';
								|en = '- Fill multilingual catalog attributes'"));
	
	Обработчик.Комментарий = СтрСоединить(СписокОписаний, Символы.ПС);
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.СтруктураПредприятия.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	ИспользуютсяДопЯзыки = Ложь;
	Для Каждого ИспользуетсяДопЯзык Из МультиязычностьСервер.СведенияОЯзыках().Используется Цикл
		
		Если ИспользуетсяДопЯзык.Значение = Истина Тогда
			
			ИспользуютсяДопЯзыки = Истина;
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ИспользуютсяДопЯзыки Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	НЕ СтруктураПредприятия.Наименование = """"
	|	И СтруктураПредприятия.НаименованиеЯзык1 = """"
	|	
	|ОБЪЕДИНИТЬ 
	|
	|ВЫБРАТЬ
	|	СтруктураПредприятия.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|ГДЕ
	|	НЕ СтруктураПредприятия.Наименование = """"
	|	И СтруктураПредприятия.НаименованиеЯзык2 = """"";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
		
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Не удалось обработать справочник ""Подразделение"" по обработчику:';
								|en = 'Cannot process the ""Business unit"" catalog by the handler:'"));
	СписокОписаний.Добавить(НСтр("ru = '- Заполнение мультиязычных реквизитов справочника';
								|en = '- Fill multilingual catalog attributes'"));
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДанныеДляОбновления.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТ
	|ИЗ
	|	&ДанныеДляОбновления КАК ДанныеДляОбновления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ.Ссылка КАК Ссылка,
	|	СтруктураПредприятия.ВерсияДанных КАК ВерсияДанных
	|ИЗ
	|	ВТ КАК ВТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК СтруктураПредприятия
	|		ПО ВТ.Ссылка = СтруктураПредприятия.Ссылка";
	
	Запрос.УстановитьПараметр("ДанныеДляОбновления", ОбновляемыеДанные);
	
	Справочник = Запрос.Выполнить().Выбрать();
	
	Пока Справочник.Следующий() Цикл
		
		ПричинаИсключения = 0;
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = 1; // Блокировка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Справочник.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			СправочникОбъект = ОбновлениеИнформационнойБазыУТ.ПроверитьПолучитьОбъект(
				Справочник.Ссылка, Справочник.ВерсияДанных, Параметры.Очередь); // СправочникОбъект
			
			ПричинаИсключения = 2; // ПлохиеДанные
			Рекомендация = НСтр("ru = 'Проверьте данные справочника вручную.';
								|en = 'Check the catalog data manually.'");
			
			ОбъектИзменен = Ложь;
			
			Если СправочникОбъект <> Неопределено Тогда
				
				Если Не ЗначениеЗаполнено(СправочникОбъект.НаименованиеЯзык1) Тогда
					
					СправочникОбъект.НаименованиеЯзык1 = СправочникОбъект.Наименование;
					
				КонецЕсли;
				
				Если Не ЗначениеЗаполнено(СправочникОбъект.НаименованиеЯзык2) Тогда
					
					СправочникОбъект.НаименованиеЯзык2 = СправочникОбъект.Наименование;
					
				КонецЕсли;
				
				ОбъектИзменен = Истина;
				
			КонецЕсли;
			
			ПричинаИсключения = 3; // Запись
			
			Если ОбъектИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Справочник.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Справочник.Ссылка);
			
			Если ПричинаИсключения = 2 Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					Справочник.Ссылка, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = 3 Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
