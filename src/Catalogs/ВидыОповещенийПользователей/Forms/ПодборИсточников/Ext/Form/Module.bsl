#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьВидыМетаданных();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОбъекты

&НаКлиенте
Процедура ТаблицаОбъектыВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	Если НЕ ЗначениеЗаполнено(Значение) Тогда
		Возврат
	КонецЕсли;
	Результат = ВыбранныеИдентификаторыОбъектовМетаданных(Значение);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВидыМетаданных

&НаКлиенте
Процедура ВидыМетаданныхПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("ОбработатьАктивизациюСтрокиВидыМетаданных",0.2,Истина);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьВидыМетаданных()
	
	ПоддерживаемыеВидыМетаданных = ОповещенияПользователейОСобытиях.ПоддерживаемыеВидыМетаданных();
	Для Каждого ВидМетаданных Из ПоддерживаемыеВидыМетаданных Цикл
		НоваяСтрока = ВидыМетаданных.Добавить();
		НоваяСтрока.Вид = ВидМетаданных.Имя;
		НоваяСтрока.Синоним = ВидМетаданных.Синоним;
		НоваяСтрока.ИндексКартинки = ОповещенияПользователейОСобытиях.ИндексыКартинокПоТипам()[ВидМетаданных.Имя];
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбъектыМетаданныхПоВиду(ВидМетаданных)
	
	ИсточникиОповещенийИсключения = ОповещенияПользователейОСобытияхПовтИсп.ИсточникиОповещенийИсключения();
	
	ТаблицаОбъекты.Очистить();
	КоллекцияОбъектовМетаданных = Метаданные[ВидМетаданных];
	Для Каждого ОбъектМетаданных Из КоллекцияОбъектовМетаданных Цикл
		ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
		Если ПравоДоступа("Просмотр", ОбъектМетаданных)
			И НЕ СтрНачинаетсяС(ОбъектМетаданных.Имя, "Удалить") 
			И ИсточникиОповещенийИсключения.Найти(ПолноеИмя) = Неопределено Тогда
			
			НовыйЭлемент = ТаблицаОбъекты.Добавить();
			НовыйЭлемент.Наименование = ОбъектМетаданных.Представление();
			НовыйЭлемент.ПолноеИмя = ПолноеИмя;
			НовыйЭлемент.ИндексКартинки = ОповещенияПользователейОСобытиях.ИндексКартинки(НовыйЭлемент.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьАктивизациюСтрокиВидыМетаданных()
	ТекущиеДанные = Элементы.ВидыМетаданных.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЗаполнитьОбъектыМетаданныхПоВиду(ТекущиеДанные.Вид);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ВыбранныеИдентификаторыОбъектовМетаданных(ИдентификаторыСтрок)
	
	МассивИмен = Новый Массив;
	
	МассивРезультат = Новый Массив;
	
	Для Каждого Ид Из ИдентификаторыСтрок Цикл
		ТекСтрока = ТаблицаОбъекты.НайтиПоИдентификатору(Ид);
		МассивИмен.Добавить(ТекСтрока.ПолноеИмя);
		СтруктураРезультат = СтруктураРезультатПодбора();
		ЗаполнитьЗначенияСвойств(СтруктураРезультат, ТекСтрока);
		МассивРезультат.Добавить(СтруктураРезультат);
	КонецЦикла;
	
	Результат = Новый Соответствие;
	ИдентификаторыОбъектов = ОбщегоНазначения.ИдентификаторыОбъектовМетаданных(МассивИмен, Ложь);
	Для каждого СтрокаРезультат Из МассивРезультат Цикл
		СтрокаРезультат.РеквизитИсточника = ОповещенияПользователейОСобытиях.СтандартныйРеквизит(ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(СтрокаРезультат.ПолноеИмя));
		Результат.Вставить(ИдентификаторыОбъектов.Получить(СтрокаРезультат.ПолноеИмя), СтрокаРезультат);
	КонецЦикла;
	
	Возврат Результат
КонецФункции

&НаСервере
Функция СтруктураРезультатПодбора()
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("ИндексКартинки");
	СтруктураРезультат.Вставить("Наименование");
	СтруктураРезультат.Вставить("ПолноеИмя");
	СтруктураРезультат.Вставить("РеквизитИсточника");
	Возврат СтруктураРезультат
КонецФункции

#КонецОбласти