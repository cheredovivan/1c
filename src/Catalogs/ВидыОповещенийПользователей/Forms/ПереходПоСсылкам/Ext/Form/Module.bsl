#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если НЕ ТипЗнч(Параметры.СоответствиеСсылок) = Тип("Соответствие") Тогда
		ВызватьИсключение(НСтр("ru = 'В форму переданы некорректные данные.';
								|en = 'Incorrect data is sent to the form.'"),КатегорияОшибки.ОшибкаКонфигурации);
	КонецЕсли;
	
	Для Каждого ЭлементСоответствия Из Параметры.СоответствиеСсылок Цикл
		НоваяСтрока = ТаблицаТипов.Добавить();
		ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ЭлементСоответствия.Ключ);
		НоваяСтрока.ИмяТаблицы = ЭлементСоответствия.Ключ;
		НоваяСтрока.СписокСсылок.ЗагрузитьЗначения(ЭлементСоответствия.Значение);
		НоваяСтрока.ИмяФормы = СтрШаблон("%1.%2", НоваяСтрока.ИмяТаблицы, "ФормаСписка");
		ПредставлениеСписка = ОбщегоНазначения.ПредставлениеСписка(ОбъектМетаданных);
		Если НЕ ПравоДоступа("Просмотр", ОбъектМетаданных) Тогда
			ПредставлениеСписка = НСтр("ru = '<список объектов недоступен для просмотра>';
										|en = '<the list of objects is not available for preview>'");
			НоваяСтрока.ИмяФормы = "";
		КонецЕсли;
		НоваяСтрока.Представление = СтрШаблон("%1 (%2)", ПредставлениеСписка, ЭлементСоответствия.Значение.Количество());
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаТипов

&НаКлиенте
Процедура ТаблицаТиповВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ПерейтиКФормеСписка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиКФорме(Команда)
	ПерейтиКФормеСписка();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПерейтиКФормеСписка()
	
	ТекущиеДанные = Элементы.ТаблицаТипов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.ИмяФормы) Тогда
		СтруктураОтбор = Новый Структура("Ссылка", ТекущиеДанные.СписокСсылок);
		ПараметрыОткрытия = Новый Структура("Отбор", СтруктураОтбор);
		ОткрытьФорму(ТекущиеДанные.ИмяФормы, ПараметрыОткрытия);
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
