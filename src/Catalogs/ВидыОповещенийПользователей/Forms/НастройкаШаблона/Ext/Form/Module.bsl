#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Если Не ПравоДоступа("Изменение", Метаданные.Справочники.ВидыОповещенийПользователей) Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	РазобратьМультиязычнуюСтроку(Параметры.ШаблонТекстаОповещения);
	ДобавитьРеквизиты();
	ЗаполнитьПараметрыПодстановки(Параметры.ПараметрыПодстановки);
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ДоступныеПараметры = ПараметрыПодстановки.Выгрузить().ВыгрузитьКолонку("Параметр");
	РегулярноеВыражение = "(?<=\[)[^\[\]\s]+(?=\])"; //@Non-NLS-1
	Ошибки = Неопределено;
	
	Для Каждого ТекущаяСтрока Из ТаблицаТекст Цикл
		Если НЕ ЗначениеЗаполнено(ТекущаяСтрока.Текст) Тогда
			Если ТекущаяСтрока.КодЯзыка = КодОсновногоЯзыка Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
					"",
					СтрШаблон(НСтр("ru = 'Не заполнен текст сообщения на основном языке %1';
									|en = 'The message text in the %1 main language is not filled in'"),
						ТекущаяСтрока.Язык));
			КонецЕсли;
		Иначе
			РезультатыПоиска = СтрНайтиВсеПоРегулярномуВыражению(
				ТекущаяСтрока.Текст,
				РегулярноеВыражение);
			Для Каждого РезультатПоиска Из РезультатыПоиска Цикл
				Если ДоступныеПараметры.Найти(РезультатПоиска.Значение) = Неопределено Тогда
					ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки,
						"",
						СтрШаблон(НСтр("ru = 'Указан несуществующий шаблон [%1] для языка %2';
										|en = 'A non-existent [%1] template for the %2 language is specified'"),
							РезультатПоиска.Значение,
							ТекущаяСтрока.Язык));
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПараметрыПодстановки

&НаКлиенте
Процедура ПараметрыПодстановкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ТекущиеДанные = Элементы.ПараметрыПодстановки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И НЕ ТолькоПросмотр Тогда
		ДобавитьПараметрЗамены(ТекущиеДанные.Параметр);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗавершитьРедактированиеШаблона(Команда)
	ОчиститьСообщения();
	Если ПроверитьЗаполнение() Тогда
		Закрыть(СобратьМультиязычнуюСтроку());
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	ТекущиеДанные = Элементы.ПараметрыПодстановки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ДобавитьПараметрЗамены(ТекущиеДанные.Параметр);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
//@skip-check bsl-nstr-string-literal-format
Процедура РазобратьМультиязычнуюСтроку(ШаблонТекстаОповещения)
	ТаблицаТекст.Очистить();
	
	ШаблонТекстаОсновнойЯзык = НСтр(ШаблонТекстаОповещения, КодОсновногоЯзыка);
	ДоступныеЯзыки = ДоступныеЯзыки();
	Для Каждого Язык Из ДоступныеЯзыки Цикл
		НоваяСтрока = ТаблицаТекст.Добавить();
		НоваяСтрока.КодЯзыка = Язык.КодЯзыка;
		НоваяСтрока.Язык = Язык.Синоним;
		ШаблонТекста = НСтр(ШаблонТекстаОповещения, Язык.КодЯзыка);
		НоваяСтрока.Текст = ?(ЗначениеЗаполнено(ШаблонТекста),
			ШаблонТекста,
			ШаблонТекстаОсновнойЯзык);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДоступныеЯзыки()
	ДоступныеЯзыки = Новый Массив;
	Для каждого Язык Из Метаданные.Языки Цикл
		ОписаниеЯзыка = Новый Структура("КодЯзыка, Синоним");
		ОписаниеЯзыка.КодЯзыка = Язык.КодЯзыка;
		ОписаниеЯзыка.Синоним = Язык.Синоним;
		ДоступныеЯзыки.Добавить(ОписаниеЯзыка);
	КонецЦикла;
	Возврат ДоступныеЯзыки
КонецФункции

&НаСервере
Процедура ДобавитьРеквизиты()
	
	ЯзыковБольшеОдного = ТаблицаТекст.Количество() > 1;
	Если ЯзыковБольшеОдного Тогда
		ТекущаяСтраница = Элементы.ГруппаМногоЯзыков;
	Иначе
		ТекущаяСтраница = Элементы.ГруппаОдинЯзык;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = ТекущаяСтраница;
	
	НомерСтроки = 0;
	Для Каждого СтрокаТекст Из ТаблицаТекст Цикл
		ИмяРеквизита = ИмяЛокализуемогоРеквизита(СтрокаТекст.КодЯзыка);
		Если Элементы.Найти(ИмяРеквизита) = Неопределено Тогда
			НовыйЭлемент = Элементы.Добавить(ИмяРеквизита,
				Тип("ПолеФормы"),
				ТекущаяСтраница);
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеТекстовогоДокумента;
			НовыйЭлемент.ПутьКДанным = "ТаблицаТекст" + "[" + Формат(НомерСтроки,"ЧДЦ=0; ЧГ=")
				+ "]." + "Текст";
			НовыйЭлемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
			НовыйЭлемент.Высота = 2;
			НовыйЭлемент.Ширина = 50;
			Если НЕ ЯзыковБольшеОдного Тогда
				НовыйЭлемент.Заголовок = НСтр("ru = 'Шаблон оповещения';
												|en = 'Notification template'");
			Иначе
				НовыйЭлемент.Заголовок = СтрокаТекст.Язык;
			КонецЕсли;
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяЛокализуемогоРеквизита(КодЯзыка)
	
	Возврат "ТекстОповещения" + "_" + КодЯзыка;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПараметрыПодстановки(ИсходныеПараметры)
	Если Не ЗначениеЗаполнено(ИсходныеПараметры) Тогда
		Элементы.ПараметрыПодстановки.Видимость = Ложь;
		Элементы.Перенести.Видимость = Ложь;
		Возврат
	КонецЕсли;
	
	Для Каждого ПараметрПодстановки Из ИсходныеПараметры Цикл
		НоваяСтрока = ПараметрыПодстановки.Добавить();
		НоваяСтрока.Параметр = ПараметрПодстановки.Поле;
		НоваяСтрока.Представление = ПараметрПодстановки.Заголовок;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция СобратьМультиязычнуюСтроку()
	ШаблонТекстаОповещения = "";
	ТекстНаОсновномЯзыке = "";
	НайденныеСтроки = ТаблицаТекст.НайтиСтроки(Новый Структура("КодЯзыка", КодОсновногоЯзыка));
	Если НайденныеСтроки.Количество() > 0 Тогда
		ТекстНаОсновномЯзыке = НайденныеСтроки[0].Текст;
	КонецЕсли;
	
	Для каждого ТекСтрока Из ТаблицаТекст Цикл
		Текст = ?(ЗначениеЗаполнено(ТекСтрока.Текст), ТекСтрока.Текст, ТекстНаОсновномЯзыке);
		СтрокаТекста = ТекСтрока.КодЯзыка + " " + "=" + " " + "'";
		СтрокаТекста = СтрокаТекста + СтрЗаменить(Текст, "'", """") + "';";
		ШаблонТекстаОповещения = ШаблонТекстаОповещения + СтрокаТекста;
	КонецЦикла;
	Возврат ШаблонТекстаОповещения
КонецФункции

&НаКлиенте
Процедура ДобавитьПараметрЗамены(ПараметрЗамены)
	Для каждого ТекСтрока Из ТаблицаТекст Цикл
		ИмяРеквизита = ИмяЛокализуемогоРеквизита(ТекСтрока.КодЯзыка);
		Элементы[ИмяРеквизита].ВыделенныйТекст = "[" + ПараметрЗамены + "]";
	КонецЦикла;
КонецПроцедуры

#КонецОбласти