#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Элементы.ФормаПроверить.Доступность = ПравоДоступа("Изменение", Метаданные.Справочники.ВидыОповещенийПользователей);
	
	Если Объект.Ссылка.Пустая() Тогда
		ПриСозданииЧтенииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		Если МодульУправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
			ОбновитьЭлементыДополнительныхРеквизитов();
			МодульУправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
		КонецЕсли;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
	ПриСозданииЧтенииНаСервере(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства	
	
	СхемаКомпоновкиДанных = СхемаКомпоновкиДанныхПоУмолчанию();
	Если ЭтоАдресВременногоХранилища(АдресСКД) Тогда
		СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресСКД);
	КонецЕсли;
	Если ЕстьИзменения(СхемаКомпоновкиДанных, СхемаКомпоновкиДанныхПоУмолчанию()) Тогда
		ТекущийОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(СхемаКомпоновкиДанных);
	Иначе
		ТекущийОбъект.СхемаКомпоновкиДанных = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
	НастройкиКомпоновкиДанных = Неопределено;
	Если ЭтоАдресВременногоХранилища(АдресНастроекСКД) Тогда
		НастройкиКомпоновкиДанных = ПолучитьИзВременногоХранилища(АдресНастроекСКД);
	КонецЕсли;
	Если ЗначениеЗаполнено(НастройкиКомпоновкиДанных)
		И ЕстьИзменения(НастройкиКомпоновкиДанных, НастройкиСхемыПоУмолчанию(СхемаКомпоновкиДанных)) Тогда
		
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(НастройкиКомпоновкиДанных);
	Иначе
		ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьЗаголовокФормы();
	ЗаполнитьПредставленияИсточниковДанных();
	ЗаполнитьИнфоПоставляемый();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОповещенияПриИзменении(Элемент)
	УправлениеДоступностью(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ШаблонТекстаОповещенияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ШаблонТекстаОповещения", Объект.ШаблонТекстаОповещения);
	ПараметрыОткрытия.Вставить("ПараметрыПодстановки", ДоступныеПоля());
	Оповещение = Новый ОписаниеОповещения("ШаблонТекстаОповещенияНажатиеЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ВидыОповещенийПользователей.Форма.НастройкаШаблона",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,
		Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ТекстСгруппированногоОповещенияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ШаблонТекстаОповещения", Объект.ТекстСгруппированногоОповещения);
	Оповещение = Новый ОписаниеОповещения("ТекстСгруппированногоОповещенияНажатиеЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ВидыОповещенийПользователей.Форма.НастройкаШаблона",
		ПараметрыОткрытия,
		ЭтотОбъект,,,,
		Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ИнформационныйТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "Перезаполнить" Тогда
		Модифицированность = Истина;
		ПерезаполнитьВидОповещения();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИсточникиДанных

&НаКлиенте
Процедура ИсточникиДанныхПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Модифицированность = Истина;
	
	Оповещение = Новый ОписаниеОповещения("ПодборИсточниковЗавершение", ЭтотОбъект);
	ОткрытьФорму("Справочник.ВидыОповещенийПользователей.Форма.ПодборИсточников",,
		ЭтотОбъект, 
		УникальныйИдентификатор,,,
		Оповещение);
КонецПроцедуры

&НаКлиенте
Процедура ИсточникиДанныхРеквизитИсточникаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	ТекущаяСтрока = Элементы.ИсточникиДанных.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		СписокВыбораРеквизитов = СписокВыбораРеквизитов(ТекущаяСтрока);
		СписокВыбора = Элемент.СписокВыбора;
		СписокВыбора.Очистить();
		
		Для Каждого ЭлементСписка Из СписокВыбораРеквизитов Цикл
			СписокВыбора.Добавить(ЭлементСписка.Значение,
				ЭлементСписка.Представление,
				ЭлементСписка.Пометка,
				ЭлементСписка.Картинка);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Редактировать(Команда)
	
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = НСтр("ru = 'Запрос данных оповещения ""%ИмяВидаОповещений%""';
														|en = 'Request for %ИмяВидаОповещений% notification data'");
	ЗаголовокФормыНастройкиСхемыКомпоновкиДанных = СтрЗаменить(ЗаголовокФормыНастройкиСхемыКомпоновкиДанных, "%ИмяВидаОповещений%", Объект.Наименование);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресСхемыКомпоновкиДанных", АдресСКД);
	ПараметрыФормы.Вставить("АдресНастроекКомпоновкиДанных", АдресНастроекСКД);
	ПараметрыФормы.Вставить("ИсточникШаблонов", Неопределено);
	ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормыНастройкиСхемыКомпоновкиДанных);
	ПараметрыФормы.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыФормы.Вставить("ИмяШаблонаСКД", Объект.ИмяШаблонаСКД);
	ПараметрыФормы.Вставить("ВозвращатьИмяТекущегоШаблонаСКД", Истина);
	ПараметрыФормы.Вставить("ВозвращатьПолноеИмяТекущегоШаблонаСКД", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьУсловноеОформление", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПорядок", Истина);
	ПараметрыФормы.Вставить("НеНастраиватьПараметры", Истина);
	ПараметрыФормы.Вставить("НеПомещатьНастройкиВСхемуКомпоновкиДанных", Истина);
	Если Объект.ТипОповещения = ПредопределенноеЗначение("Перечисление.ТипыОповещенийПользователей.Оперативное") Тогда
		ПараметрыФормы.Вставить("ПроцедураПроверкиСхемыКомпоновкиДанных", 
			"ОповещенияПользователейОСобытиях.ПроверкаКорректностиСхемыПолученияДанныхОперативногоВидаОповещений");
	Иначе
		ПараметрыФормы.Вставить("ПроцедураПроверкиСхемыКомпоновкиДанных", 
			"ОповещенияПользователейОСобытиях.ПроверкаКорректностиСхемыПолученияДанныхНеоперативногоВидаОповещений");
	КонецЕсли;
	Оповещение = Новый ОписаниеОповещения("РедактироватьЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.УпрощеннаяНастройкаСхемыКомпоновкиДанных",
		ПараметрыФормы, 
		ЭтотОбъект,,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьОбъект(Команда)
	Если Не Объект.Поставляемый Тогда
		ПредупреждениеАсинх(НСтр("ru = 'Перезаполнение не требуется.';
								|en = 'No need to refill.'"));
		Возврат;
	КонецЕсли;
	ПерезаполнитьВидОповещения();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОповещение(Команда)
	Если Модифицированность Или НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПредупреждениеАсинх(НСтр("ru = 'Перед проверкой вид оповещения должен быть записан.';
								|en = 'Save the notification kind before checking.'"));
		Возврат;
	КонецЕсли;
	ОчиститьСообщения();
	Если ПроверитьКорректностьВидаОповещений(Объект.Ссылка) Тогда
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Виды оповещений пользователей.';
				|en = 'User notification kinds.'"),,
			НСтр("ru = 'Оповещение выполнено без ошибок.';
				|en = 'Notification is sent without errors.'"),
			БиблиотекаКартинок.Информация32);
	Иначе
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Виды оповещений пользователей.';
				|en = 'User notification kinds.'"),,
			НСтр("ru = 'В ходе оповещения пользователей возникли ошибки.';
				|en = 'Errors occurred while notifying users.'"),
			БиблиотекаКартинок.Ошибка32);
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
	КонецЕсли;
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсточникиДанныхРеквизитИсточника.Имя);

	ГруппаОтбораИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	ОтборЭлемента = ГруппаОтбораИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсточникиДанных.ИндексКартинки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ОтборЭлемента.ПравоеЗначение = РедактируемыеИндексы();

	ОтборЭлемента = ГруппаОтбораИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсточникиДанных.РеквизитИсточника");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);

КонецПроцедуры

&НаСервере
Функция РедактируемыеИндексы()
	
	ИндексыКартинокПоТипам = ОповещенияПользователейОСобытиях.ИндексыКартинокПоТипам();
	
	СписокРедактируемыхИндексов = Новый СписокЗначений;
	СписокРедактируемыхИндексов.Добавить(ИндексыКартинокПоТипам["РегистрСведений"]);
	СписокРедактируемыхИндексов.Добавить(ИндексыКартинокПоТипам["РегистрНакопления"]);
	СписокРедактируемыхИндексов.Добавить(ИндексыКартинокПоТипам["РегистрБухгалтерии"]);
	СписокРедактируемыхИндексов.Добавить(ИндексыКартинокПоТипам["РегистрРасчета"]);
	
	Возврат СписокРедактируемыхИндексов
	
КонецФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере(ТекущийОбъект = Неопределено)
	
	УстановитьЗаголовокФормы(ТекущийОбъект = Неопределено);
	ЗаполнитьИнфоПоставляемый();
	УправлениеДоступностью(ЭтотОбъект);
	ЗаполнитьПредставленияИсточниковДанных();
	
	Если ТекущийОбъект <> Неопределено Тогда
		СКД = ТекущийОбъект.СхемаКомпоновкиДанных.Получить();
		Настройки = ТекущийОбъект.ХранилищеНастроекКомпоновкиДанных.Получить();
	КонецЕсли;
	ЗаполнитьСхемуКомпоновки(СКД, Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнфоПоставляемый()
	Если Объект.Поставляемый Тогда
		Элементы.ИнформационныйТекст.Видимость = Истина;
		МассивСтрок = Новый Массив;
		МассивСтрок.Добавить(БиблиотекаКартинок.Информация);
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(" ");
		Если Объект.Изменен Тогда
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Вид оповещения отличается от поставляемого.';
					|en = 'The notification kind differs from the built-in one.'"), ,
				ЦветаСтиля.ПоясняющийТекст));
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Перезаполнить';
					|en = 'Refill'"), ,
				ЦветаСтиля.ГиперссылкаЦвет,
				Неопределено,
				"Перезаполнить"));
		Иначе
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(
				НСтр("ru = 'Вид оповещения поставляется с конфигурацией.';
					|en = 'The notification kind is supplied together with the configuration.'"), ,
				ЦветаСтиля.ПоясняющийТекст));
		КонецЕсли;
		ИнформационныйТекст = Новый ФорматированнаяСтрока(МассивСтрок);
	Иначе
		Элементы.ИнформационныйТекст.Видимость = Ложь;
	КонецЕсли;
	Элементы.ИнформационныйТекст.Доступность = НЕ ТолькоПросмотр;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Элементы.ГруппаГруппировка.Доступность = Объект.ТипОповещения = ПредопределенноеЗначение("Перечисление.ТипыОповещенийПользователей.Неоперативное");
	
	Если Объект.ТипОповещения = ПредопределенноеЗначение("Перечисление.ТипыОповещенийПользователей.Неоперативное") Тогда
		Элементы.ГруппаСтраницыПоТипу.ТекущаяСтраница = Элементы.ГруппаСтраницаНеоперативныеРеквизиты;
	ИначеЕсли Объект.ТипОповещения = ПредопределенноеЗначение("Перечисление.ТипыОповещенийПользователей.Оперативное") Тогда
		Элементы.ГруппаСтраницыПоТипу.ТекущаяСтраница = Элементы.ГруппаСтраницаОперативныеРеквизиты;
	КонецЕсли;
	
	ОбновитьМультиязычныеСтроки(Форма);
	Элементы.ШаблонТекстаОповещения.Доступность = НЕ Форма.ТолькоПросмотр;
	Элементы.ТекстСгруппированногоОповещения.Доступность = НЕ Форма.ТолькоПросмотр;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставленияИсточниковДанных()
	
	Если Объект.ИсточникиДанных.Количество() = 0 Тогда
		Возврат
	КонецЕсли;
	
	МассивИсточников = Объект.ИсточникиДанных.Выгрузить().ВыгрузитьКолонку("ТипИсточника");
	
	ОбъектыМетаданныхПоИдентификаторам = ОбщегоНазначения.ОбъектыМетаданныхПоИдентификаторам(
		МассивИсточников,
		Ложь);
		
	Для каждого ИсточникДанных Из Объект.ИсточникиДанных Цикл
		ОбъектМетаданных = ОбъектыМетаданныхПоИдентификаторам.Получить(ИсточникДанных.ТипИсточника);
		Если ОбъектМетаданных = Null Или ОбъектМетаданных = Неопределено Тогда
			ИсточникДанных.Наименование = НСтр("ru = '<неизвестный объект>';
												|en = '<unknown object>'");
			ИсточникДанных.ПолноеИмя = НСтр("ru = '<неизвестный тип объекта>';
											|en = '<unknown object type>'");
			ИсточникДанных.ИндексКартинки = 0;
		Иначе
			ИсточникДанных.Наименование = ОбъектМетаданных.Представление();
			ИсточникДанных.ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
			ИсточникДанных.ИндексКартинки = ОповещенияПользователейОСобытиях.ИндексКартинки(ИсточникДанных.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
//@skip-check bsl-nstr-string-literal-format
Процедура ОбновитьМультиязычныеСтроки(Форма)
	Объект = Форма.Объект;
	
	Если ЗначениеЗаполнено(Объект.ТекстСгруппированногоОповещения) Тогда
		Форма.ТекстСгруппированногоОповещения = НСтр(Объект.ТекстСгруппированногоОповещения);
		Если НЕ ЗначениеЗаполнено(Форма.ТекстСгруппированногоОповещения) Тогда
			Форма.ТекстСгруппированногоОповещения = НСтр(Объект.ТекстСгруппированногоОповещения, Форма.КодОсновногоЯзыка);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Форма.ТекстСгруппированногоОповещения) Тогда
		Форма.ТекстСгруппированногоОповещения = НСтр("ru = '<указать>';
													|en = '<specify>'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ШаблонТекстаОповещения) Тогда
		Форма.ШаблонТекстаОповещения = НСтр(Объект.ШаблонТекстаОповещения);
		Если НЕ ЗначениеЗаполнено(Форма.ШаблонТекстаОповещения) Тогда
			Форма.ШаблонТекстаОповещения = НСтр(Объект.ШаблонТекстаОповещения, Форма.КодОсновногоЯзыка);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(Форма.ШаблонТекстаОповещения) Тогда
		Форма.ШаблонТекстаОповещения = НСтр("ru = '<указать>';
											|en = '<specify>'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблонТекстаОповещенияНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ТипЗнч(Результат) = Тип("Строка") Тогда
		Объект.ШаблонТекстаОповещения = Результат;
		ОбновитьМультиязычныеСтроки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстСгруппированногоОповещенияНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ТипЗнч(Результат) = Тип("Строка") Тогда
		Объект.ТекстСгруппированногоОповещения = Результат;
		ОбновитьМультиязычныеСтроки(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборИсточниковЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("Соответствие") Тогда
			Для каждого Тип Из Результат Цикл
				НайденныеСтроки = Объект.ИсточникиДанных.НайтиСтроки(Новый Структура("ТипИсточника", Тип.Ключ));
				Если НайденныеСтроки.Количество() = 0 Тогда
					НовыйИсточник = Объект.ИсточникиДанных.Добавить();
					НовыйИсточник.ТипИсточника = Тип.Ключ;
					ЗаполнитьЗначенияСвойств(НовыйИсточник, Тип.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		АдресНастроекСКД = Результат.АдресХранилищаНастройкиКомпоновщика;
		ОбновитьСхемуКомпоновки(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСхемуКомпоновки(ПроверятьИзменение = Ложь)
	НастройкиСКД = Неопределено;
	Если ЭтоАдресВременногоХранилища(АдресНастроекСКД) Тогда
		НастройкиСКД = ПолучитьИзВременногоХранилища(АдресНастроекСКД);
	КонецЕсли;
	Если ЭтоАдресВременногоХранилища(АдресСКД) Тогда
		СКД = ПолучитьИзВременногоХранилища(АдресСКД);
	Иначе
		СКД = СхемаКомпоновкиДанныхПоУмолчанию();
		АдресСКД = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	КонецЕсли;
	ИнициализироватьКомпоновщикНастроек(СКД, НастройкиСКД);
	
	Если ПроверятьИзменение И Объект.Поставляемый Тогда
		Если ЕстьИзменения(СКД, СхемаКомпоновкиДанныхПоУмолчанию())
			Или ЗначениеЗаполнено(НастройкиСКД) И ЕстьИзменения(НастройкиСКД, НастройкиСхемыПоУмолчанию(СКД)) Тогда
			Объект.Изменен = Истина;
		КонецЕсли;
	КонецЕсли;
	ЗаполнитьИнфоПоставляемый();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСхемуКомпоновки(СКД, Настройки)

	Если СКД <> Неопределено Тогда
		АдресСКД = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор);
	Иначе
		СКД = СхемаКомпоновкиДанныхПоУмолчанию();
		АдресСКД = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	КонецЕсли;
	Если Настройки <> Неопределено Тогда
		АдресНастроекСКД = ПоместитьВоВременноеХранилище(Настройки, Новый УникальныйИдентификатор);
	Иначе
		АдресНастроекСКД = "";
	КонецЕсли;
	
	ИнициализироватьКомпоновщикНастроек(СКД, Настройки);
КонецПроцедуры

&НаСервере
Функция ДоступныеПоля()
	
	ДоступныеПоля = Новый Массив;
	Настройки = КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки();
	Для каждого ДоступноеПоле Из Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ДоступноеПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных")
			ИЛИ НЕ ДоступноеПоле.Использование 
			ИЛИ ДоступноеПоле.Поле = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеПоля = Новый Структура("Поле, Заголовок");
		ОписаниеПоля.Поле = ДоступноеПоле.Поле;
		НайденноеПоле = Настройки.ДоступныеПоляВыбора.НайтиПоле(ДоступноеПоле.Поле);
		Если НайденноеПоле <> Неопределено Тогда
			ОписаниеПоля.Заголовок = НайденноеПоле.Заголовок;
		КонецЕсли;
		ДоступныеПоля.Добавить(ОписаниеПоля);
	КонецЦикла;
	
	Возврат ДоступныеПоля
	
КонецФункции

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек(СхемаИсточника, Настройки = Неопределено)
	
	КомпоновщикНастроекКомпоновкиДанных = ОповещенияПользователейОСобытиях.ИнициализироватьКомпоновщикНастроек(СхемаИсточника, Настройки);
	
КонецПроцедуры

&НаСервере
Функция НастройкиСхемыПоУмолчанию(СхемаКомпоновкиДанных)
	КомпоновщикНастроек = ОповещенияПользователейОСобытиях.ИнициализироватьКомпоновщикНастроек(СхемаКомпоновкиДанных, 
		СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	Возврат КомпоновщикНастроек.ПолучитьНастройки()
КонецФункции

&НаСервере
Функция СхемаКомпоновкиДанныхПоУмолчанию()
	
	Схема = ОповещенияПользователейОСобытиях.МакетПоПолномуИмени(Объект.ИмяШаблонаСКД);
	
	Если Схема = Неопределено Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Для вида оповещения указан некорректный шаблон СКД. Необходимо перезаполнить вид оповещения.';
													|en = 'An incorrect DCS template is specified for the notification kind. Refill the notification kind.'"));
	КонецЕсли;
	
	Возврат Схема
КонецФункции

&НаСервере
Функция ЕстьИзменения(Значение, ЗначениеПоУмолчанию)
	Возврат ОповещенияПользователейОСобытиях.ЕстьИзменения(Значение, ЗначениеПоУмолчанию);
КонецФункции

&НаСервере
Функция СписокВыбораРеквизитов(ТекущаяСтрока)
	СписокВыбораРеквизитов = Новый СписокЗначений;
	
	ТекущиеДанные = Объект.ИсточникиДанных.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат СписокВыбораРеквизитов;
	КонецЕсли;
	
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ТекущиеДанные.ТипИсточника);
	Если ОбъектМетаданных <> Неопределено Тогда
		Если СтрНачинаетсяС(ТекущиеДанные.ПолноеИмя, "РегистрНакопления")
			Или СтрНачинаетсяС(ТекущиеДанные.ПолноеИмя, "РегистрБухгалтерии")
			Или СтрНачинаетсяС(ТекущиеДанные.ПолноеИмя, "РегистрРасчета") Тогда
			
			СписокВыбораРеквизитов.Добавить("Регистратор",
				НСтр("ru = 'Регистратор';
					|en = 'Recorder'"),,
				БиблиотекаКартинок.Реквизит);
			
			ДобавитьРеквизитыВСписок(СписокВыбораРеквизитов, ОбъектМетаданных.Измерения, БиблиотекаКартинок.Измерение);
			ДобавитьРеквизитыВСписок(СписокВыбораРеквизитов, ОбъектМетаданных.Реквизиты, БиблиотекаКартинок.Реквизит);
		ИначеЕсли СтрНачинаетсяС(ТекущиеДанные.ПолноеИмя, "РегистрСведений") Тогда
			Если ОбъектМетаданных.РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору Тогда
				СписокВыбораРеквизитов.Добавить("Регистратор",
					НСтр("ru = 'Регистратор';
						|en = 'Recorder'"),,
					БиблиотекаКартинок.Реквизит);
			КонецЕсли;
			ДобавитьРеквизитыВСписок(СписокВыбораРеквизитов, ОбъектМетаданных.Измерения, БиблиотекаКартинок.Измерение);
			ДобавитьРеквизитыВСписок(СписокВыбораРеквизитов, ОбъектМетаданных.Реквизиты, БиблиотекаКартинок.Реквизит);
		Иначе
			СписокВыбораРеквизитов.Добавить("Ссылка",
				НСтр("ru = 'Ссылка';
					|en = 'Reference'"),,
				БиблиотекаКартинок.Реквизит);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СписокВыбораРеквизитов
КонецФункции

&НаСервере
Процедура ДобавитьРеквизитыВСписок(СписокВыбораРеквизитов, РеквизитыОбъекта, Картинка)
	
	Для Каждого РеквизитОбъекта Из РеквизитыОбъекта Цикл
		
		Если СтрНачинаетсяС(РеквизитОбъекта.Имя, "Удалить") Тогда
			Продолжить;
		КонецЕсли;
		
		ТипыРеквизита = РеквизитОбъекта.Тип.Типы();
		ЭтоСсылочныйТип = Ложь;
		Для Каждого ТипРеквизита Из ТипыРеквизита Цикл
			ЭтоСсылочныйТип = ОбщегоНазначения.ЭтоСсылка(ТипРеквизита);
			Если ЭтоСсылочныйТип Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЭтоСсылочныйТип Тогда
			СписокВыбораРеквизитов.Добавить(РеквизитОбъекта.Имя,
				?(ЗначениеЗаполнено(РеквизитОбъекта.Синоним),РеквизитОбъекта.Синоним,РеквизитОбъекта.Имя),,
				Картинка);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПерезаполнитьВидОповещения()
	
	ТекстВопроса = НСтр("ru = 'Перезаполнить вид оповещения по поставляемому шаблону?';
						|en = 'Refill the notification kind using the built-in template?'");
	
	СписокКнопок = Новый СписокЗначений();
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Перезаполнить';
														|en = 'Refill'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет, НСтр("ru = 'Отмена';
														|en = 'Cancel'"));
	
	Ответ = Ждать ВопросАсинх(ТекстВопроса, СписокКнопок);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если ПерезаполнитьВидОповещенияНаСервере() Тогда
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Виды оповещений пользователей.';
					|en = 'User notification kinds.'"),,
				НСтр("ru = 'Перезаполнение вида оповещения завершено.';
					|en = 'The notification kind is refilled.'"),
				БиблиотекаКартинок.Информация32);
		Иначе
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Виды оповещений пользователей.';
					|en = 'User notification kinds.'"),,
				НСтр("ru = 'Перезаполнение вида оповещения не удалось.';
					|en = 'Cannot refill the notification kind.'"),
				БиблиотекаКартинок.Ошибка32);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПерезаполнитьВидОповещенияНаСервере()
	ИдентификаторВидаОповещений = Объект.Ссылка.УникальныйИдентификатор();
	ОписаниеВидаОповещений = ОповещенияПользователейОСобытиях.ОписаниеВидаОповещенийПользователейПоИдентификатору(ИдентификаторВидаОповещений);
	ОбъектПерезаполнен = Ложь;
	Если ОписаниеВидаОповещений <> Неопределено Тогда
		АдресСКД = "";
		АдресНастроекСКД = "";
		ЗаполнитьЗначенияСвойств(Объект, ОписаниеВидаОповещений, , "ИсточникиДанных");
		
		Объект.ИсточникиДанных.Очистить();
		Для Каждого ИсточникДанных Из ОписаниеВидаОповещений.ИсточникиДанных Цикл
			НоваяСтрока = Объект.ИсточникиДанных.Добавить();
			Если ТипЗнч(ИсточникДанных.ТипИсточника) = Тип("Строка") Тогда
				НоваяСтрока.ТипИсточника = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(ИсточникДанных.ТипИсточника);
			Иначе
				НоваяСтрока.ТипИсточника = ИсточникДанных.ТипИсточника;
			КонецЕсли;
			НоваяСтрока.РеквизитИсточника = ИсточникДанных.РеквизитИсточника;
		КонецЦикла;
		
		ОбновитьСхемуКомпоновки();
		УправлениеДоступностью(ЭтотОбъект);
		ЗаполнитьПредставленияИсточниковДанных();
		ОбъектПерезаполнен = Истина;
	КонецЕсли;
	
	Возврат ОбъектПерезаполнен
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьКорректностьВидаОповещений(ВидОповещения)
	ОповещенияПользователейОСобытиях.ПроверитьКорректностьВидаОповещенийПользователей(ВидОповещения);
	Возврат Истина;
КонецФункции

&НаСервере
Процедура УстановитьЗаголовокФормы(ЭтоНовый = Ложь)
	
	Если ЭтоНовый Тогда
		Заголовок = НСтр("ru = 'Вид оповещения пользователей (создание)';
						|en = 'User notification kinds (Create)'");
	Иначе
		Заголовок = Объект.Наименование;
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствами = ОбщегоНазначения.ОбщийМодуль("УправлениеСвойствами");
		МодульУправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.Свойства") Тогда
		МодульУправлениеСвойствамиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеСвойствамиКлиент");
		МодульУправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти