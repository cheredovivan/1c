#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Поставляемый = Ложь;
	Активен = Ложь;
	Изменен = Ложь;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	МассивНепроверяемыхРеквизитов = Новый Массив;
	Если ТипОповещения = Перечисления.ТипыОповещенийПользователей.Неоперативное Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ИсточникиДанных");
	ИначеЕсли ТипОповещения = Перечисления.ТипыОповещенийПользователей.Оперативное Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ТекстСгруппированногоОповещения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
	Если НЕ ЭтоГруппа И Активен И НЕ Обсуждения.СистемаВзаимодействийПодключена() Тогда
		ТекстОшибки = НСтр("ru = 'Обсуждения системы взаимодействия недоступны. Вид оповещения не может быть активным.';
							|en = 'Collaboration system notifications are not available. The notification kind cannot be active.'");
		
		ОбщегоНазначения.СообщитьПользователю(
			ТекстОшибки,
			ЭтотОбъект,
			"Активен",
			,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	Если НЕ ЭтоГруппа И ТипОповещения = Перечисления.ТипыОповещенийПользователей.Неоперативное Тогда
		ИсточникиДанных.Очистить();
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И Активен Тогда
		
		Если ИмяШаблонаСКД = ОповещенияПользователейОСобытиях.ИмяПредопределенногоШаблона() Тогда
			СКД = СхемаКомпоновкиДанных.Получить();
			Если СКД = Неопределено 
				ИЛИ НЕ ОповещенияПользователейОСобытиях.ЕстьИзменения(СКД, ОповещенияПользователейОСобытиях.МакетПоПолномуИмени(ИмяШаблонаСКД)) Тогда
				ТекстОшибки = НСтр("ru = 'Не настроен запрос получения данных оповещения. Вид оповещения не может быть активным.';
									|en = 'The request for notification data is not configured. The notification kind cannot be active.'");
				
				ОбщегоНазначения.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					"Активен",
					,
					Отказ);
			КонецЕсли;
		КонецЕсли;
		
		ОповещенияПользователейОСобытиях.ПроверкаКорректностиСхемыПолученияДанныхПередЗаписью(ЭтотОбъект, Отказ);
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И Поставляемый И Не Изменен Тогда
		ОписаниеВидаОповещений = ОповещенияПользователейОСобытиях.ОписаниеВидаОповещенийПользователейПоИдентификатору(Ссылка.УникальныйИдентификатор());
		Если ОписаниеВидаОповещений <> Неопределено Тогда
			Если СхемаКомпоновкиДанных.Получить() <> Неопределено
				Или ХранилищеНастроекКомпоновкиДанных.Получить() <> Неопределено Тогда
				
				Изменен = Истина;
				
			КонецЕсли;
			
			Если НЕ Изменен Тогда
				ОсновныеРеквизитыВидаОповещений = ОповещенияПользователейОСобытиях.ОсновныеРеквизитыВидаОповещений();
				Для Каждого ИмяРеквизита Из ОсновныеРеквизитыВидаОповещений Цикл
					Если ОписаниеВидаОповещений[ИмяРеквизита] <> ЭтотОбъект[ИмяРеквизита] Тогда
						Изменен = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если НЕ Изменен Тогда
				Для каждого СтрокаИсточник Из ОписаниеВидаОповещений.ИсточникиДанных Цикл
					Если ТипЗнч(СтрокаИсточник.ТипИсточника) = Тип("Строка") Тогда
						СтрокаИсточникТипИсточника = ОбщегоНазначения.ИдентификаторОбъектаМетаданных(СтрокаИсточник.ТипИсточника);
					Иначе
						СтрокаИсточникТипИсточника = СтрокаИсточник.ТипИсточника;
					КонецЕсли;
					СтруктураПоиска = Новый Структура("ТипИсточника, РеквизитИсточника",
						СтрокаИсточникТипИсточника,
						СтрокаИсточник.РеквизитИсточника);
						
					Если ИсточникиДанных.НайтиСтроки(СтруктураПоиска).Количество() = 0 Тогда
						Изменен = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Если НЕ ЭтоГруппа И Активен И Обсуждения.СистемаВзаимодействийПодключена() Тогда
		УстановитьПривилегированныйРежим(Истина);
		Отбор = Новый Структура();
		Отбор.Вставить("Метаданные", Метаданные.РегламентныеЗадания.ОповещенияПользователейОСобытиях);
		Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
		Если Задания.Количество() = 1 Тогда
			Если НЕ Задания[0].Использование Тогда
				Параметры = Новый Структура();
				Параметры.Вставить("Использование", Истина);
				РегламентныеЗаданияСервер.ИзменитьЗадание(Задания[0].УникальныйИдентификатор, Параметры);
			КонецЕсли;
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли