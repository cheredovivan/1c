#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает признак наличия фильтра по пользователям.
// 
//Параметры:
// НастройкаФильтра - число:
// 1 - Кроме пользователей из списка
// 2 - Только пользователям из списка
//
// Возвращаемое значение:
// Булево - Истина - фильтр задан.
//
Функция ЕстьФильтрПользователей(НастройкаФильтра) Экспорт

Запрос = Новый Запрос;

Запрос.Текст = 
"ВЫБРАТЬ ПЕРВЫЕ 1
|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка
|ИЗ
|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
|ГДЕ
|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА
|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ОтборПользователей = &НастройкаФильтра";

Запрос.УстановитьПараметр("НастройкаФильтра", НастройкаФильтра);

Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

// Возвращает ссылку на настройку оповещений для текущего пользователя. Если настройки нет, то создает.
// 
// Возвращаемое значение:
// КлючНастройки - СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка - ссылка на настройку пользователя.
//
Функция КлючНастройкиОповещенийТекущегоПользователя() Экспорт
	
	КлючНастройки = Неопределено;
	Если Не ОповещенияВключены() Тогда
		Возврат КлючНастройки;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		Возврат КлючНастройки;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КлючНастройки = Выборка.Ссылка;
	Иначе
		Настройка = Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей.СоздатьЭлемент();
		Настройка.Пользователь = ТекущийПользователь;
		Настройка.ОповещатьОРасхождениях = Истина;
		Настройка.КоличествоДнейБезДокумента = 30; // настройка по умолчанию
		Настройка.ОповещатьОбУстраненииРасхождений = Истина;
		Настройка.ОповещатьОРасхожденииПервичныхДокументов = Истина;
		Настройка.ОповещатьОРасхожденииСумм = Истина;
		Настройка.ОповещатьОРасхожденииСчетовФактур = Истина;
		Настройка.ОтборКонтрагентов = 0;
		Настройка.ОтборОрганизаций = 0;
		Настройка.Записать(); 
		КлючНастройки = Настройка.Ссылка;
	КонецЕсли;
	
	Возврат КлючНастройки;
	
КонецФункции

// Включает оповещения сервиса сверки.
// 
// Возвращаемое значение:
// ВключеноУспешно - Булево - признак успешного включения оповещений сверки.
//
Функция ВключитьОповещения() Экспорт
	
	ВключеноУспешно = Истина;
	КлючГлобальнойНастройкиОповещений = КлючГлобальнойНастройкиОповещений(Истина);
	Если КлючГлобальнойНастройкиОповещений <> Неопределено Тогда
		ГлобальнаяНастройкаОповещений = КлючГлобальнойНастройкиОповещений.ПолучитьОбъект();
		Если ГлобальнаяНастройкаОповещений <> Неопределено
			И НЕ ГлобальнаяНастройкаОповещений.ОповещатьОРасхождениях Тогда
			ГлобальнаяНастройкаОповещений.ОповещатьОРасхождениях = Истина;
			Попытка
				ГлобальнаяНастройкаОповещений.Записать();
			Исключение
				ВключеноУспешно = Ложь;
			КонецПопытки;
		КонецЕсли;
	Иначе
		ВключеноУспешно = Ложь;
	КонецЕсли;
	
	Возврат ВключеноУспешно;
	
КонецФункции

// Отключает оповещения сервиса сверки.
// 
// Возвращаемое значение:
// ОтключеноУспешно - Булево - признак успешного отключения оповещений сверки.
//
Функция ВыключитьОповещения() Экспорт

	ОтключеноУспешно = Истина;
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхождениях = ИСТИНА";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		ГлобальнаяНастройкаОповещений = Выборка.Ссылка.ПолучитьОбъект();
		Если ГлобальнаяНастройкаОповещений <> Неопределено Тогда
			ГлобальнаяНастройкаОповещений.ОповещатьОРасхождениях = Ложь;
			Попытка
				ГлобальнаяНастройкаОповещений.Записать();
			Исключение
				ОтключеноУспешно = Ложь;
			КонецПопытки;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОтключеноУспешно;
	
КонецФункции

// Возвращает ссылку на глобальную настройку оповещений. Если настройки нет, то создает.
// 
//Параметры:
// ЭтоВключениеСервиса - Булево - признак того, что ключ глобальной настройки получается при включении сервиса.
//
// Возвращаемое значение:
// КлючНастройки - СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка - ссылка на глобальную
// настройку или Неопределено .
//
Функция КлючГлобальнойНастройкиОповещений(ЭтоВключениеСервиса = Ложь) Экспорт
	
	КлючНастройки = Неопределено;
	Если НЕ ЭтоВключениеСервиса
		И Не ОповещенияВключены() Тогда
		Возврат КлючНастройки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА";
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КлючНастройки = Выборка.Ссылка;
	Иначе
		
		Настройка = Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей.СоздатьЭлемент();
		
		Настройка.ЭтоГлобальнаяНастройка = Истина;
		Настройка.КоличествоДнейБезДокумента = 30; // настройка по умолчанию
		Настройка.ОповещатьОбУстраненииРасхождений = Истина;
		Настройка.ОповещатьОРасхожденииПервичныхДокументов = Истина;
		Настройка.ОповещатьОРасхожденииСумм = Истина;
		Настройка.ОповещатьОРасхожденииСчетовФактур = Истина;
		Настройка.ОтборКонтрагентов = 0;
		Настройка.ОтборОрганизаций = 0;
		
		Попытка 
			Настройка.Записать();
			КлючНастройки = Настройка.Ссылка;
		Исключение
			КлючНастройки = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Возврат КлючНастройки;
	
КонецФункции

// Возвращает признак того, что оповещения сверки включены.
// 
// Возвращаемое значение:
// Булево - признак того, что оповешения сверки включены.
//
Функция ОповещенияВключены() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей КАК СервисСверкиРасчетовНастройкиОповещенийПользователей
	|ГДЕ
	|	СервисСверкиРасчетовНастройкиОповещенийПользователей.ПометкаУдаления = ЛОЖЬ
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ЭтоГлобальнаяНастройка = ИСТИНА
	|	И СервисСверкиРасчетовНастройкиОповещенийПользователей.ОповещатьОРасхождениях = ИСТИНА";
	
	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

// Возвращает признак того, что для текущего пользователя доступны оповещения сверки и их настройка.
// 
// Возвращаемое значение:
// Булево - признак того, что настройка сверки доступна.
//
Функция ПерсональнаяНастройкаДоступна() Экспорт
	
	ПерсональнаяНастройкаДоступна = Истина;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	Если Не ЗначениеЗаполнено(ТекущийПользователь) Тогда
		ПерсональнаяНастройкаДоступна = Ложь;
		Возврат ПерсональнаяНастройкаДоступна;
	КонецЕсли; 
	
	Если НЕ СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены() Тогда
		ПерсональнаяНастройкаДоступна = Ложь;
		Возврат ПерсональнаяНастройкаДоступна;
	КонецЕсли;
	
	ЕстьФильтрКромеПользователей  = ЕстьФильтрПользователей(1);
	ЕстьФильтрТолькоПользователям = ЕстьФильтрПользователей(2);

	Если ЕстьФильтрКромеПользователей 
		ИЛИ ЕстьФильтрТолькоПользователям Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Пользователи КАК СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи
		|ГДЕ
		|	СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Ссылка.ЭтоГлобальнаяНастройка = ИСТИНА
		|	И СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Ссылка.ПометкаУдаления = ЛОЖЬ
		|	И СервисСверкиРасчетовНастройкиОповещенийПользователейПользователи.Пользователь = &Пользователь";
		Запрос.УстановитьПараметр("Пользователь", ТекущийПользователь);
		
		ПустойРезультат = Запрос.Выполнить().Пустой();
		Если ЕстьФильтрКромеПользователей Тогда
			// Нет в фильтре "кого не оповещать".
			ПерсональнаяНастройкаДоступна = ПустойРезультат;
		Иначе
			// Есть в фильтре "кого оповещать".
			ПерсональнаяНастройкаДоступна = НЕ ПустойРезультат;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПерсональнаяНастройкаДоступна;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" ИЛИ ВидФормы = "ФормаВыбора" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЭтоГлобальнаяНастройка = Истина;
	Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
		ЭтоГлобальнаяНастройка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.Ключ, "ЭтоГлобальнаяНастройка");
	ИначеЕсли Параметры.Свойство("ПерсональнаяНастройка") Тогда
		Если Параметры.ПерсональнаяНастройка Тогда
			ЭтоГлобальнаяНастройка = Ложь;
		Иначе
			ЭтоГлобальнаяНастройка = Истина;
		КонецЕсли;
	Иначе
		ЭтоГлобальнаяНастройка = Ложь;
	КонецЕсли;
	
	Если ЭтоГлобальнаяНастройка Тогда
		ВыбраннаяФорма = "ФормаГлобальнойНастройки";
	Иначе
		ВыбраннаяФорма = "ФормаПерсональнойНастройки";
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("ЭтоГлобальнаяНастройка");
	Поля.Добавить("Пользователь");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	Если Данные.ЭтоГлобальнаяНастройка Тогда
		Представление = НСтр("ru = 'Глобальная настройка оповещений 1С:Сверка 2.0';
							|en = 'Global settings of 1C:Reconciliation 2.0 notifications'")
	Иначе
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1: настройка оповещений 1С:Сверка 2.0';
			|en = '%1: set up 1C:Reconciliation 2.0 notifications'"), Строка(Данные.Пользователь));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиОбновления

Функция ВключитьОповещенияОНовыхРасхождениях() Экспорт
	
	Если СервисСверкиРасчетов.ИспользуетсяСервисСверкиРасчетов()
		И СервисСверкиРасчетовВызовСервера.ОбсужденияПодключены() Тогда
		ОповещенияВключены = ВключитьОповещения();
		Если Не ОповещенияВключены Тогда
			МетаданныеОбъекта = Метаданные.Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей;
			ИмяСервиса = СервисСверкиРасчетов.ИмяСервиса();
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось включить оповещения сервиса %1';
										|en = 'Cannot enable the %1 service notifications'"), ИмяСервиса);
			ЗаписьЖурналаРегистрации(ИмяСервиса, УровеньЖурналаРегистрации.Ошибка, МетаданныеОбъекта,, ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли


