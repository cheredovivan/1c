
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьНесколькоОрганизаций = СервисСверкиРасчетовПереопределяемый.ИспользоватьНесколькоОрганизаций(); 
	ПерсональнаяНастройкаДоступна = Справочники.СервисСверкиРасчетовНастройкиОповещенийПользователей.ПерсональнаяНастройкаДоступна();
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Объект.Пользователь = Пользователи.ТекущийПользователь();
		УстановитьПерсональныеНастройкиПоУмолчанию(ЭтотОбъект);
		
	КонецЕсли;
	
	Если СервисСверкиРасчетов.ЭтоИнтерфейсТакси() Тогда
		
		Элементы.ДекорацияИнформационнаяНадпись.Ширина = 50;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КонтрагентыНастройки1ССверка" Тогда 
		
		ЗагрузитьТаблицуКонтрагентов(Параметр);
		УправлениеФормой(ЭтотОбъект);
		
	ИначеЕсли ИмяСобытия = "ОрганизацииНастройки1ССверка" Тогда
		
		ЗагрузитьТаблицуОрганизаций(Параметр);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборКонтрагентовПриИзменении(Элемент)

	Объект.Контрагенты.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКромеКонтрагентовНажатие(Элемент)
	
	ОткрытьФормуРедактированияКонтрагентов();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтборКонтрагентов1ПриИзменении(Элемент)

	Объект.Контрагенты.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентов2ПриИзменении(Элемент)

	Объект.Контрагенты.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещатьОбОтсутствииПриИзменении(Элемент)
	
	Если ОповещатьОбОтсутствии Тогда
		Объект.КоличествоДнейБезДокумента = 30; // значение по-умолчанию
	Иначе
		Объект.КоличествоДнейБезДокумента = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыбранныеКонтрагентыНажатие(Элемент)
	
	ОткрытьФормуРедактированияКонтрагентов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКромеОрганизацийНажатие(Элемент)
	
	ОткрытьФормуРедактированияОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыбранныеОрганизацииНажатие(Элемент)
	
	ОткрытьФормуРедактированияОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацийПриИзменении(Элемент)
	
	Объект.Организации.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизаций1ПриИзменении(Элемент)
	
	Объект.Организации.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизаций2ПриИзменении(Элемент)
	
	Объект.Организации.Очистить();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещатьОРасхожденияхПриИзменении(Элемент)
	
	Если Объект.ОповещатьОРасхождениях Тогда
			ОповещатьОбОтсутствии = Истина;
			Объект.КоличествоДнейБезДокумента = 30; // настройка по умолчанию
			Объект.ОповещатьОбУстраненииРасхождений = Истина;
			Объект.ОповещатьОРасхожденииПервичныхДокументов = Истина;
			Объект.ОповещатьОРасхожденииСумм = Истина;
			Объект.ОповещатьОРасхожденииСчетовФактур = Истина;
			Объект.ОтборКонтрагентов = 0;
			Объект.ОтборОрганизаций = 0;
	Иначе
		Объект.Контрагенты.Очистить();
		Объект.Организации.Очистить();
		ОповещатьОбОтсутствии = Ложь;
		Объект.КоличествоДнейБезДокумента = 0;
		Объект.ОповещатьОбУстраненииРасхождений = Ложь;
		Объект.ОповещатьОРасхожденииПервичныхДокументов = Ложь;
		Объект.ОповещатьОРасхожденииСумм = Ложь;
		Объект.ОповещатьОРасхожденииСчетовФактур = Ложь;
		Объект.ОтборКонтрагентов = 0;
		Объект.ОтборОрганизаций = 0;
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы  = Форма.Элементы;
	Объект    = Форма.Объект;
	
	Элементы.ОповещатьОРасхождениях.Видимость            = Форма.ПерсональнаяНастройкаДоступна;
	Элементы.ГруппаНастройки.Доступность                 = Объект.ОповещатьОРасхождениях;
	Элементы.ГруппаНастройки.Видимость                   = Форма.ПерсональнаяНастройкаДоступна;
	Элементы.ОповещенияОтключеныЦентрализовано.Видимость = НЕ Форма.ПерсональнаяНастройкаДоступна;
	Элементы.ДекорацияКромеКонтрагентов.Доступность      = Объект.ОтборКонтрагентов = 1;
	Элементы.ДекорацияВыбранныеКонтрагенты.Доступность   = Объект.ОтборКонтрагентов = 2;
	Элементы.ДекорацияКромеОрганизаций.Доступность       = Объект.ОтборОрганизаций = 1;
	Элементы.ДекорацияВыбранныеОрганизации.Доступность   = Объект.ОтборОрганизаций = 2;
	Элементы.ГруппаОтборОрганизации.Видимость            = Форма.ИспользоватьНесколькоОрганизаций;
	
	Форма.ОповещатьОбОтсутствии = Объект.КоличествоДнейБезДокумента > 0 ИЛИ Форма.ОповещатьОбОтсутствии;
	
	Элементы.КоличествоДнейБезДокумента.Доступность = Форма.ОповещатьОбОтсутствии;
	
	ЗаполнитьПредставлениеСтрок(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуКонтрагентов(АдресТаблицыКонтрагентов)
	
	ТаблицаКонтрагентов = ПолучитьИзВременногоХранилища(АдресТаблицыКонтрагентов);
	ТаблицаКонтрагентов.Свернуть("Контрагент");
	
	Объект.Контрагенты.Загрузить(ТаблицаКонтрагентов);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуОрганизаций(АдресТаблицыОрганизаций)
	
	ТаблицаОрганизаций = ПолучитьИзВременногоХранилища(АдресТаблицыОрганизаций);
	ТаблицаОрганизаций.Свернуть("Организация");
	
	Объект.Организации.Загрузить(ТаблицаОрганизаций);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияКонтрагентов()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресТаблицыКонтрагентов", ПоместитьВХранилище("Контрагенты"));
	
	ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Форма.ФормаПодборКонтрагентов", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры  

&НаКлиенте
Процедура ОткрытьФормуРедактированияОрганизаций()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресТаблицыОрганизаций", ПоместитьВХранилище("Организации"));
	
	ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Форма.ФормаПодборОрганизаций", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьВХранилище(НаименованиеТаблицы)
	
	Возврат ПоместитьВоВременноеХранилище(Объект[НаименованиеТаблицы].Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеСтрок(Форма)
	
	Элементы  = Форма.Элементы;
	Объект    = Форма.Объект;
	
	КонтрагентыПредставление = "";
	КонтрагентыПредставлениеПоУмолчанию = НСтр("ru = '<выбрать контрагентов>';
												|en = '<select counterparties>'");
	КонтрагентыКоличество = Объект.Контрагенты.Количество();
	
	Если КонтрагентыКоличество = 1 Тогда
		КонтрагентыПредставление = Строка(Объект.Контрагенты[0].Контрагент);
	ИначеЕсли КонтрагентыКоличество = 2 Тогда
		КонтрагентыПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 и %2';
					|en = '%1 and %2'"),
				Строка(Объект.Контрагенты[0].Контрагент), 
				Строка(Объект.Контрагенты[1].Контрагент));
	ИначеЕсли КонтрагентыКоличество > 2 Тогда
		КонтрагентыПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1, %2 и еще %3';
					|en = '%1, %2, and %3 more'"),
				Строка(Объект.Контрагенты[0].Контрагент), 
				Строка(Объект.Контрагенты[1].Контрагент),
				КонтрагентыКоличество - 2);
	Иначе
		КонтрагентыПредставление = КонтрагентыПредставлениеПоУмолчанию;
	КонецЕсли;
	
	Элементы.ДекорацияКромеКонтрагентов.Заголовок = ?(Объект.ОтборКонтрагентов = 1, КонтрагентыПредставление, КонтрагентыПредставлениеПоУмолчанию);
	Элементы.ДекорацияВыбранныеКонтрагенты.Заголовок = ?(Объект.ОтборКонтрагентов = 2, КонтрагентыПредставление, КонтрагентыПредставлениеПоУмолчанию);
	
	ОрганизацииПредставление = "";
	ОрганизацииПредставлениеПоУмолчанию = НСтр("ru = '<выбрать организации>';
												|en = '<select companies>'");
	ОрганизацииКоличество = Объект.Организации.Количество();
	
	Если ОрганизацииКоличество = 1 Тогда
		ОрганизацииПредставление = Строка(Объект.Организации[0].Организация);
	ИначеЕсли ОрганизацииКоличество = 2 Тогда
		ОрганизацииПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 и %2';
					|en = '%1 and %2'"),
				Строка(Объект.Организации[0].Организация), 
				Строка(Объект.Организации[1].Организация));
	ИначеЕсли ОрганизацииКоличество > 2 Тогда
		ОрганизацииПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1, %2 и еще %3';
					|en = '%1, %2, and %3 more'"),
				Строка(Объект.Организации[0].Организация), 
				Строка(Объект.Организации[1].Организация),
				ОрганизацииКоличество - 2);
	Иначе
		ОрганизацииПредставление = ОрганизацииПредставлениеПоУмолчанию;
	КонецЕсли;
	
	Элементы.ДекорацияКромеОрганизаций.Заголовок = ?(Объект.ОтборОрганизаций= 1, ОрганизацииПредставление, ОрганизацииПредставлениеПоУмолчанию);
	Элементы.ДекорацияВыбранныеОрганизации.Заголовок = ?(Объект.ОтборОрганизаций = 2, ОрганизацииПредставление, ОрганизацииПредставлениеПоУмолчанию);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПерсональныеНастройкиПоУмолчанию(Форма)

	Объект    = Форма.Объект;
	
	Форма.ОповещатьОбОтсутствии = Истина;
	Объект.КоличествоДнейБезДокумента = 30; // настройка по умолчанию
	Объект.ОповещатьОбУстраненииРасхождений = Истина;
	Объект.ОповещатьОРасхожденииПервичныхДокументов = Истина;
	Объект.ОповещатьОРасхожденииСумм = Истина;
	Объект.ОповещатьОРасхожденииСчетовФактур = Истина;
	Объект.ОтборКонтрагентов = 0;
	Объект.ОтборОрганизаций = 0;
	
КонецПроцедуры

#КонецОбласти
