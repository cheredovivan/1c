
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		УстановитьНастройкиПоУмолчанию(ЭтотОбъект);
		
	КонецЕсли;
	
	Если СервисСверкиРасчетов.ЭтоИнтерфейсТакси() Тогда
		
		Элементы.ДекорацияИнформация.Ширина = 50;
		
	КонецЕсли;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ПользователиНастройки1ССверка" Тогда
		
		ЗагрузитьТаблицуПользователей(Параметр);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПользователейПриИзменении(Элемент)
	Объект.Пользователи.Очистить();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборПользователей1ПриИзменении(Элемент)
	Объект.Пользователи.Очистить();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОтборПользователей2ПриИзменении(Элемент)
	Объект.Пользователи.Очистить();
	УправлениеФормой(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКромеПользователейНажатие(Элемент)
	
	ОткрытьФормуРедактированияПользователей();

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВыбранныеПользователиНажатие(Элемент)
	
	ОткрытьФормуРедактированияПользователей();
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация9ОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПерейтиКПерсональнойНастройке" Тогда
		СтандартнаяОбработка = Ложь;
		
		КлючНастройкиОповещенийТекущегоПользователя = СервисСверкиРасчетовВызовСервера.КлючНастройкиОповещенийТекущегоПользователя();
		Если КлючНастройкиОповещенийТекущегоПользователя <> Неопределено Тогда
			ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.ФормаОбъекта", 
				Новый Структура("Ключ, ПерсональнаяНастройка", КлючНастройкиОповещенийТекущегоПользователя, Истина));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы  = Форма.Элементы;
	Объект    = Форма.Объект;
	
	Элементы.ДекорацияКромеПользователей.Доступность    = Объект.ОтборПользователей = 1;
	Элементы.ДекорацияВыбранныеПользователи.Доступность = Объект.ОтборПользователей = 2;
	
	ЗаполнитьПредставлениеСтрок(Форма);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьТаблицуПользователей(АдресТаблицыПользователей)
	
	ТаблицаПользователей = ПолучитьИзВременногоХранилища(АдресТаблицыПользователей);
	ТаблицаПользователей.Свернуть("Пользователь");
	
	Объект.Пользователи.Загрузить(ТаблицаПользователей);
	
	Модифицированность = Истина;
	
КонецПроцедуры 

&НаСервере
Функция ПоместитьВХранилище(НаименованиеТаблицы)
	
	Возврат ПоместитьВоВременноеХранилище(Объект[НаименованиеТаблицы].Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеСтрок(Форма)
	
	Элементы  = Форма.Элементы;
	Объект    = Форма.Объект;
	
	ПользователиПредставление = "";
	ПользователиПредставлениеПоУмолчанию = НСтр("ru = '<выбрать пользователей>';
												|en = '<select users>'");
	ПользователиКоличество = Объект.Пользователи.Количество();
	
	Если ПользователиКоличество = 1 Тогда
		ПользователиПредставление = Строка(Объект.Пользователи[0].Пользователь);
	ИначеЕсли ПользователиКоличество = 2 Тогда
		ПользователиПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 и %2';
					|en = '%1 and %2'"),
				Строка(Объект.Пользователи[0].Пользователь), 
				Строка(Объект.Пользователи[1].Пользователь));
	ИначеЕсли ПользователиКоличество > 2 Тогда
		ПользователиПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1, %2 и еще %3';
					|en = '%1, %2, and %3 more'"),
				Строка(Объект.Пользователи[0].Пользователь), 
				Строка(Объект.Пользователи[1].Пользователь),
				ПользователиКоличество - 2);
	Иначе
		ПользователиПредставление = ПользователиПредставлениеПоУмолчанию;
	КонецЕсли;
	
	Элементы.ДекорацияКромеПользователей.Заголовок = ?(Объект.ОтборПользователей= 1, ПользователиПредставление, ПользователиПредставлениеПоУмолчанию);
	Элементы.ДекорацияВыбранныеПользователи.Заголовок = ?(Объект.ОтборПользователей = 2, ПользователиПредставление, ПользователиПредставлениеПоУмолчанию);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьНастройкиПоУмолчанию(Форма)

	Объект    = Форма.Объект;
	
	Объект.Пользователь = Неопределено;
	Объект.КоличествоДнейБезДокумента = 30; // настройка по умолчанию
	Объект.ОповещатьОбУстраненииРасхождений = Истина;
	Объект.ОповещатьОРасхожденииПервичныхДокументов = Истина;
	Объект.ОповещатьОРасхожденииСумм = Истина;
	Объект.ОповещатьОРасхожденииСчетовФактур = Истина;
	Объект.ОтборКонтрагентов = 0;
	Объект.ОтборОрганизаций = 0;
	Объект.Организации.Очистить();
	Объект.Контрагенты.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияПользователей()
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресТаблицыПользователей", ПоместитьВХранилище("Пользователи"));
	
	ОткрытьФорму("Справочник.СервисСверкиРасчетовНастройкиОповещенийПользователей.Форма.ФормаПодборПользователей", ПараметрыФормы,,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти
















