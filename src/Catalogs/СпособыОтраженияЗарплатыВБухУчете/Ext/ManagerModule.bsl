#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает текст запроса для получения параметров способа отражения.
//	Параметры:
//		ИмяВременнойТаблицы - Строка - имя таблицы, если требуется помещения результата во временную таблицу.
//	Возвращаемое значение:
//		Строка - текст запроса.
//
Функция ТекстЗапросаДанныхСпособов(ИмяВременнойТаблицы = "") Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ЛОЖЬ КАК ОблагаетсяЕНВД,
	|	СпособОтраженияЗарплата.ОплатаСдельныхРабот КАК ОплатаСдельныхРабот,
	
	|	СпособОтраженияЗарплата.БазаРаспределенияПоСдельнымРаботам КАК БазаРаспределения,
	|	СпособОтраженияЗарплата.ОтдельнаяСтатьяКалькуляции КАК ОтдельнаяСтатьяКалькуляции,
	|	СпособОтраженияЗарплата.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СпособОтраженияЗарплата.ОтдельнаяСтатьяКалькуляцииВзносов КАК ОтдельнаяСтатьяКалькуляцииВзносов,
	|	СпособОтраженияЗарплата.СтатьяКалькуляцииВзносов КАК СтатьяКалькуляцииВзносов,
	
	|	СпособОтраженияЗарплата.СтатьяРасходов КАК СтатьяРасходов,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределения,
	|	ВЫБОР
	|		КОГДА НЕ СпособОтраженияЗарплата.ОплатаСдельныхРабот
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходов.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.ПринятиеКНалоговомуУчетуПоСдельнымРаботам
	|	КОНЕЦ КАК ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходов КАК АналитикаРасходов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивов КАК АналитикаАктивовПассивов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета КАК НастройкаСчетовУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.СчетУчета КАК СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто1 КАК Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто2 КАК Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчета.Субконто3 КАК Субконто3,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов КАК СтатьяРасходовВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ВариантРаспределенияРасходовРегл КАК ВариантРаспределенияВзносов,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносов.ПринятиеКналоговомуУчету КАК ПринятиеКналоговомуУчетуВзносов,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносов КАК АналитикаРасходовВзносов,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносов КАК АналитикаАктивовПассивовВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов КАК НастройкаСчетовУчетаВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.СчетУчета КАК СчетУчетаВзносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто1 КАК Субконто1Взносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто2 КАК Субконто2Взносов,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто3 КАК Субконто3Взносов,
	
	// взносы травматизм
	|	СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм КАК РежимУчетаВзносовНаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов
	|	КОНЕЦ                                    КАК СтатьяРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ВариантРаспределенияРасходовРегл
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов.ВариантРаспределенияРасходовРегл
	|	КОНЕЦ                                    КАК ВариантРаспределенияТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносов.ПринятиеКналоговомуУчету
	|	КОНЕЦ                                    КАК ПринятиеКналоговомуУчетуТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаРасходовВзносов
	|	КОНЕЦ                                    КАК АналитикаРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаАктивовПассивовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносов
	|	КОНЕЦ                                    КАК АналитикаАктивовПассивовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов
	|	КОНЕЦ                                    КАК НастройкаСчетовУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.СчетУчета
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.СчетУчета
	|	КОНЕЦ                                    КАК СчетУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто1
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто1
	|	КОНЕЦ                                    КАК Субконто1Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто2
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто2
	|	КОНЕЦ                                    КАК Субконто2Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто3
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносов.Субконто3
	|	КОНЕЦ                                    КАК Субконто3Травматизм
	
	|//ПоместитьВоВременнуюТаблицу
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокСпособов.СпособОтражения,
	|	ИСТИНА,
	|	СпособОтраженияЗарплата.ОплатаСдельныхРабот,
	
	|	СпособОтраженияЗарплата.БазаРаспределенияПоСдельнымРаботам КАК БазаРаспределения,
	|	СпособОтраженияЗарплата.ОтдельнаяСтатьяКалькуляции КАК ОтдельнаяСтатьяКалькуляции,
	|	СпособОтраженияЗарплата.СтатьяКалькуляции КАК СтатьяКалькуляции,
	|	СпособОтраженияЗарплата.ОтдельнаяСтатьяКалькуляцииВзносов КАК ОтдельнаяСтатьяКалькуляцииВзносов,
	|	СпособОтраженияЗарплата.СтатьяКалькуляцииВзносов КАК СтатьяКалькуляцииВзносов,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД,
	|	СпособОтраженияЗарплата.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ВариантРаспределенияРасходовРегл,
	|	ВЫБОР
	|		КОГДА НЕ СпособОтраженияЗарплата.ОплатаСдельныхРабот
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовЕНВД.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.ПринятиеКНалоговомуУчетуПоСдельнымРаботам
	|	КОНЕЦ,
	|	СпособОтраженияЗарплата.АналитикаРасходовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаЕНВД.Субконто3,
	
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ВариантРаспределенияРасходовРегл,
	|	СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ПринятиеКналоговомуУчету,
	|	СпособОтраженияЗарплата.АналитикаРасходовВзносовЕНВД,
	|	СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.СчетУчета,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто1,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто2,
	|	СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто3,
	
	// взносы травматизм
	|	СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД
	|	КОНЕЦ                                    КАК СтатьяРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ВариантРаспределенияРасходовРегл
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ВариантРаспределенияРасходовРегл
	|	КОНЕЦ                                    КАК ВариантРаспределенияТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.СтатьяРасходовТравматизм.ПринятиеКналоговомуУчету
	|		ИНАЧЕ СпособОтраженияЗарплата.СтатьяРасходовВзносовЕНВД.ПринятиеКналоговомуУчету
	|	КОНЕЦ                                    КАК ПринятиеКналоговомуУчетуТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаРасходовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаРасходовВзносовЕНВД
	|	КОНЕЦ                                    КАК АналитикаРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.АналитикаАктивовПассивовТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.АналитикаАктивовПассивовВзносовЕНВД
	|	КОНЕЦ                                    КАК АналитикаАктивовПассивовТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД
	|	КОНЕЦ                                    КАК НастройкаСчетовУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.СчетУчета
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.СчетУчета
	|	КОНЕЦ                                    КАК СчетУчетаТравматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто1
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто1
	|	КОНЕЦ                                    КАК Субконто1Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто2
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто2
	|	КОНЕЦ                                    КАК Субконто2Травматизм,
	|	ВЫБОР
	|		КОГДА СпособОтраженияЗарплата.РежимУчетаВзносовНаТравматизм = 1
	|			ТОГДА СпособОтраженияЗарплата.НастройкаСчетовУчетаТравматизм.Субконто3
	|		ИНАЧЕ СпособОтраженияЗарплата.НастройкаСчетовУчетаВзносовЕНВД.Субконто3
	|	КОНЕЦ                                    КАК Субконто3Травматизм
	
	|ИЗ
	|	ВтСписокСпособовОтражения КАК СписокСпособов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СпособыОтраженияЗарплатыВБухУчете КАК СпособОтраженияЗарплата
	|		ПО СписокСпособов.СпособОтражения = СпособОтраженияЗарплата.Ссылка
	|ГДЕ
	|	СписокСпособов.СпособОтражения <> Значение(Справочник.СпособыОтраженияЗарплатыВБухУчете.ПустаяСсылка)";
	
	Если ЗначениеЗаполнено(ИмяВременнойТаблицы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПоместитьВоВременнуюТаблицу", "ПОМЕСТИТЬ " + ИмяВременнойТаблицы);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "//ПоместитьВоВременнуюТаблицу", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает текст запроса получения параметров способов отражения.
// Список вариантов отражения для отбора, следует размещать в таблице "ВТСпособыОтражения".
// Поля таблицы отбора параметров отражения:
//   * СпособОтражения - СправочникСсылка.СпособыОтраженияЗарплатыВБухУчете - способ отражения,
//   * ВидНачислений - Число - вид начислений (1 - оплата труда, 2 - страховые взносы, 3 - взносы на травматизм),
//   * ЕНВД - Булево - по основной деятельности или ЕНВД.
//
// Параметры:
//   ИмяТаблицыНастроек - Строка - имя формируемой таблицы с настройками отражения.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаНастройкиОтраженияЗарплаты(Знач ИмяТаблицыНастроек = "ВТНастройкиОтраженияЗарплаты") Экспорт
	
	// Виды начислений:
	// 1 - оплата труда
	// 2 - страховые взносы
	// 3 - взносы на травматизм
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Список.СпособОтражения КАК СпособОтражения,
	|	Список.ВидНачислений КАК ВидНачислений,
	|	ЛОЖЬ КАК СдельнаяОплата,
	|	
	|	Настройки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Настройки.СтатьяРасходов КАК Статья,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Настройки.АналитикаРасходов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаРасходов,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.АналитикаАктивовПассивов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК АналитикаАктивовПассивов,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.НастройкаСчетовУчета
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НастройкаСчетовУчета,
	|	
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(Настройки.СтатьяРасходов КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ПриниматьКНалоговомуУчету,
	|	
	|	НЕОПРЕДЕЛЕНО КАК БазаРаспределения,
	|	НЕОПРЕДЕЛЕНО КАК СтатьяКалькуляции,
	|	
	|	Список.ЕНВД КАК ЕНВД,
	|	
	|	Настройки.ВАрхиве КАК ВАрхиве
	|
	|ПОМЕСТИТЬ ВТНастройкиОтраженияЗарплаты
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И НЕ Настройки.ОплатаСдельныхРабот
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1 И Настройки.РежимУчетаВзносов <> 1
	|		КОГДА 2 ТОГДА Настройки.РежимУчетаВзносов <> 1
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И ВЫБОР
	|		КОГДА Список.ЕНВД ТОГДА Настройки.ОсобыйСоответствуетОсновному
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ИСТИНА,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	Настройки.ПринятиеКНалоговомуУчетуПоСдельнымРаботам,
	|	
	|	Настройки.БазаРаспределенияПоСдельнымРаботам,
	|	ВЫБОР
	|		КОГДА Настройки.ОтдельнаяСтатьяКалькуляции
	|			ТОГДА Настройки.СтатьяКалькуляции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	
	|	Список.ЕНВД,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Настройки.ОплатаСдельныхРабот
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1 И НЕ Настройки.ОтдельнаяСтатьяКалькуляцииВзносов
	|		КОГДА 2 ТОГДА НЕ Настройки.ОтдельнаяСтатьяКалькуляцииВзносов
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ЛОЖЬ,
	|	
	|	Настройки.НаправлениеДеятельности,
	|	Настройки.СтатьяРасходовВзносов,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Настройки.АналитикаРасходовВзносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.АналитикаАктивовПассивовВзносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносов ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.НастройкаСчетовУчетаВзносов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносов ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(Настройки.СтатьяРасходовВзносов КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	Список.ЕНВД,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Список.ВидНачислений > 1
	|		И НЕ Настройки.ОплатаСдельныхРабот
	|		И Настройки.РежимУчетаВзносов = 1
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И ВЫБОР
	|		КОГДА Список.ЕНВД ТОГДА Настройки.ОсобыйСоответствуетОсновному
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ИСТИНА,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	Настройки.ПринятиеКНалоговомуУчетуПоСдельнымРаботам,
	|	
	|	Настройки.БазаРаспределенияПоСдельнымРаботам,
	|	Настройки.СтатьяКалькуляцииВзносов,
	|	
	|	Список.ЕНВД,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Список.ВидНачислений > 1
	|		И Настройки.ОплатаСдельныхРабот
	|		И Настройки.ОтдельнаяСтатьяКалькуляцииВзносов
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ЛОЖЬ,
	|	
	|	Настройки.НаправлениеДеятельности,
	|	Настройки.СтатьяРасходовЕНВД,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Настройки.АналитикаРасходовЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.АналитикаАктивовПассивовЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.НастройкаСчетовУчетаЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(Настройки.СтатьяРасходовЕНВД КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	ИСТИНА,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Список.ЕНВД
	|		И НЕ Настройки.ОплатаСдельныхРабот
	|		И НЕ Настройки.ОсобыйСоответствуетОсновному
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1 И Настройки.РежимУчетаВзносовЕНВД <> 1
	|		КОГДА 2 ТОГДА Настройки.РежимУчетаВзносовЕНВД <> 1
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ЛОЖЬ,
	|	
	|	Настройки.НаправлениеДеятельности,
	|	Настройки.СтатьяРасходовВзносовЕНВД,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Настройки.АналитикаРасходовВзносовЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.АналитикаАктивовПассивовВзносовЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.НастройкаСчетовУчетаВзносовЕНВД
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовВзносовЕНВД ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(Настройки.СтатьяРасходовВзносовЕНВД КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	ИСТИНА,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Список.ЕНВД
	|		И Список.ВидНачислений > 2
	|		И НЕ Настройки.ОплатаСдельныхРабот
	|		И НЕ Настройки.ОсобыйСоответствуетОсновному
	|		И Настройки.РежимУчетаВзносовЕНВД = 1
	|
	|ГДЕ
	|	ВЫБОР Список.ВидНачислений
	|		КОГДА 3 ТОГДА Настройки.РежимУчетаВзносовНаТравматизм <> 1
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Список.СпособОтражения,
	|	Список.ВидНачислений,
	|	ЛОЖЬ,
	|	
	|	Настройки.НаправлениеДеятельности,
	|	Настройки.СтатьяРасходовТравматизм,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовТравматизм ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА Настройки.АналитикаРасходовТравматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовТравматизм ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.АналитикаАктивовПассивовТравматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовТравматизм ССЫЛКА ПланВидовХарактеристик.СтатьиАктивовПассивов
	|			ТОГДА Настройки.НастройкаСчетовУчетаТравматизм
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	
	|	ВЫБОР
	|		КОГДА Настройки.СтатьяРасходовТравматизм ССЫЛКА ПланВидовХарактеристик.СтатьиРасходов
	|			ТОГДА ВЫРАЗИТЬ(Настройки.СтатьяРасходовТравматизм КАК ПланВидовХарактеристик.СтатьиРасходов).ПринятиеКналоговомуУчету
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ,
	|	
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	
	|	Список.ЕНВД,
	|	
	|	Настройки.ВАрхиве
	|
	|ИЗ
	|	Справочник.СпособыОтраженияЗарплатыВБухУчете КАК Настройки
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТСпособыОтражения КАК Список
	|	ПО Настройки.Ссылка = Список.СпособОтражения
	|		И Настройки.РежимУчетаВзносовНаТравматизм = 1
	|		И Список.ВидНачислений = 3
	|
	|ИНДЕКСИРОВАТЬ ПО СпособОтражения";
	
	Если Не ПустаяСтрока(ИмяТаблицыНастроек) И ИмяТаблицыНастроек <> "ВТНастройкиОтраженияЗарплаты" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТНастройкиОтраженияЗарплаты", "ПОМЕСТИТЬ " + ИмяТаблицыНастроек);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает массив блокируемых реквизитов объекта.
//	Возвращаемое значение:
//		Массив из Строка - имена реквизитов.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("НаправлениеДеятельности");
	Результат.Добавить("СтатьяРасходов");
	Результат.Добавить("АналитикаРасходов");
	Результат.Добавить("АналитикаАктивовПассивов");
	Результат.Добавить("РежимУчетаВзносов");
	Результат.Добавить("СтатьяРасходовВзносов");
	Результат.Добавить("АналитикаРасходовВзносов");
	Результат.Добавить("АналитикаАктивовПассивовВзносов");
	Результат.Добавить("ОсобыйСоответствуетОсновному");
	Результат.Добавить("СтатьяРасходовЕНВД");
	Результат.Добавить("АналитикаРасходовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовЕНВД");
	Результат.Добавить("РежимУчетаВзносовЕНВД");
	Результат.Добавить("СтатьяРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаРасходовВзносовЕНВД");
	Результат.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	Результат.Добавить("ОтдельнаяСтатьяКалькуляции");
	Результат.Добавить("СтатьяКалькуляции");
	Результат.Добавить("ОтдельнаяСтатьяКалькуляцииВзносов");
	Результат.Добавить("БазаРаспределенияПоСдельнымРаботам");
	Результат.Добавить("ПринятиеКНалоговомуУчетуПоСдельнымРаботам");
	Результат.Добавить("РежимУчетаВзносовНаТравматизм");
	Результат.Добавить("СтатьяРасходовТравматизм");
	Результат.Добавить("АналитикаРасходовТравматизм");
	Результат.Добавить("АналитикаАктивовПассивовТравматизм");
	
	Результат.Добавить("ОплатаСдельныхРабот;ХарактерНачисления");
	
	Возврат Результат;
	
КонецФункции

// Проверяет использование объекта в документах Отражение зарплаты в финансовом учете и Начисление оценочных обязательств.
//	Параметры:
//		Параметры - Структура - структура, содержащая ссылку на проверяемый объект.
//		АдресХранилища - Строка - адрес, по которому следует поместить результат проверки.
//
Процедура ПроверитьИспользованиеОбъекта(Параметры, АдресХранилища) Экспорт
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Зарплата.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ОтражениеЗарплатыВФинансовомУчете.НачисленнаяЗарплатаИВзносы КАК Зарплата
	|ГДЕ
	|	Зарплата.Ссылка.Проведен
	|	И Зарплата.СпособОтраженияЗарплатыВБухучете = &Объект
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1
	|	Резервы.Ссылка
	|ИЗ
	|	Документ.НачислениеОценочныхОбязательствПоОтпускам.ОценочныеОбязательства КАК Резервы
	|ГДЕ
	|	Резервы.Ссылка.Проведен
	|	И Резервы.СпособОтраженияЗарплатыВБухучете = &Объект");
	
	Запрос.УстановитьПараметр("Объект", Параметры.Объект);
	Результат = Запрос.Выполнить();
	
	ПоместитьВоВременноеХранилище(Не Результат.Пустой(), АдресХранилища);
	
КонецПроцедуры

#Область Прочее

// Возвращает параметры выбора статей для элемента справочника.
//
// Возвращаемое значение:
//   Массив из см.ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики - параметры выбора статей и аналитик.
//
Функция ПараметрыВыбораСтатейИАналитик() Экспорт
	
	МассивПараметров = Новый Массив;
	
	#Область Параметры_выбора_статьи_расходов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходов";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивов");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_ЕНВД
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовЕНВД";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовЕНВД";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовЕНВД";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовЕНВД";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_взносов
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовВзносов";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовВзносов";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовВзносов";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносов";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовВзносов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовВзносов");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовВзносов");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_взносов_ЕНВД
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовВзносовЕНВД";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовВзносовЕНВД";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовВзносовЕНВД";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносовЕНВД";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовВзносовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовВзносовЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовВзносовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	#Область Параметры_выбора_статьи_расходов_травматизм
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Объект";
	ПараметрыВыбора.Статья      = "СтатьяРасходовТравматизм";
	ПараметрыВыбора.ТипСтатьи   = "ТипСтатьиРасходовТравматизм";
	ПараметрыВыбора.ЗначениеПоУмолчанию = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовТравматизм";
	
	ПараметрыВыбора.ВыборСтатьиАктивовПассивов = Истина;
	ПараметрыВыбора.АналитикаАктивовПассивов = "АналитикаАктивовПассивовТравматизм";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовТравматизм");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовТравматизм");
	ПараметрыВыбора.ЭлементыФормы.АналитикаАктивовПассивов.Добавить("АналитикаАктивовПассивовТравматизм");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

// Возвращает параметры настройки счетов учета.
//
// Возвращаемое значение:
//   Массив из см.НастройкаСчетовУчетаСервер.ПараметрыНастройки - параметры настройки счетов учета.
//
Функция ПараметрыНастройкиСчетовУчета() Экспорт
	
	МассивПараметров = Новый Массив;
	
	#Область НастройкаСчетаУчета
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчета";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивов";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходов";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчете";
	ПараметрыНастройки.Организация   = "";
	
	ПараметрыНастройки.ИсточникиПодбораСубконто.ЗагрузитьЗначения(ПодбираемыеАналитики());
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчете");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаЕНВД
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаЕНВД";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовЕНВД";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовЕНВД";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовЕНВД";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеЕНВД";
	ПараметрыНастройки.Организация = "";

	ПараметрыНастройки.ИсточникиПодбораСубконто.ЗагрузитьЗначения(ПодбираемыеАналитики());
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеЕНВД");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносов
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаВзносов";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовВзносов";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносов";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовВзносов";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеВзносов";
	ПараметрыНастройки.Организация = "";
	
	ПараметрыНастройки.ИсточникиПодбораСубконто.ЗагрузитьЗначения(ПодбираемыеАналитики());
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеВзносов");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносовЕНВД
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаВзносовЕНВД";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовВзносовЕНВД";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовВзносовЕНВД";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовВзносовЕНВД";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеВзносовЕНВД";
	ПараметрыНастройки.Организация = "";
	
	ПараметрыНастройки.ИсточникиПодбораСубконто.ЗагрузитьЗначения(ПодбираемыеАналитики());
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеВзносовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	#Область НастройкаСчетаУчетаВзносов
	ПараметрыНастройки = НастройкаСчетовУчетаСервер.ПараметрыНастройки();
	ПараметрыНастройки.ПутьКДанным = "Объект";
	ПараметрыНастройки.НастройкаСчетовУчета     = "НастройкаСчетовУчетаТравматизм";
	ПараметрыНастройки.СтатьяАктивовПассивов    = "СтатьяРасходовТравматизм";
	ПараметрыНастройки.АналитикаАктивовПассивов = "АналитикаАктивовПассивовТравматизм";
	ПараметрыНастройки.ТипСтатьи     = "ТипСтатьиРасходовТравматизм";
	ПараметрыНастройки.Представление = "НастроитьОтражениеВРеглУчетеТравматизм";
	ПараметрыНастройки.Организация = "";
	
	ПараметрыНастройки.ИсточникиПодбораСубконто.ЗагрузитьЗначения(ПодбираемыеАналитики());
	
	ПараметрыНастройки.ЭлементыФормы.Добавить("НастроитьОтражениеВРеглУчетеТравматизм");
	
	МассивПараметров.Добавить(ПараметрыНастройки);
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПодбираемыеАналитики() Экспорт
	
	ПодбираемыеАналитики = Новый Массив;
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ФизическиеЛица"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.ОбъектыЭксплуатации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.Организации"));
	ПодбираемыеАналитики.Добавить(Тип("СправочникСсылка.СтруктураПредприятия"));
	ПодбираемыеАналитики.Добавить(Тип("ПеречислениеСсылка.ВидыОперацийПоЗарплате"));
	
	Возврат ПодбираемыеАналитики;
	
КонецФункции

#КонецОбласти

#КонецЕсли
