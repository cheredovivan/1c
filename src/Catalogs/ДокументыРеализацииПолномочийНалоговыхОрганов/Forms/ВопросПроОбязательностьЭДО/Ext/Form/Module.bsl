
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазобратьПараметры(Параметры);
	
	УстановитьЗначениеПоУмолчанию();
	
	УправлениеФормойПриСоздании();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбязательностьПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ВступлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТребованияФНСКлиент.ПоказатьИнформациюоСроках(, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ЕслиВыНеУвереныОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ЕслиВыНеУвереныОбработкаНавигационнойСсылки_Завершение", 
		ЭтотОбъект);
		
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Больше100Сотрудников", Больше100Сотрудников);
	ДополнительныеПараметры.Вставить("ОбязаныПодаватьДекларацию", ОбязаныПодаватьДекларацию);
	ДополнительныеПараметры.Вставить("ОбязаныПодаватьЖурналСФ", ОбязаныПодаватьЖурналСФ);
	ДополнительныеПараметры.Вставить("СдаетПерсУчет", СдаетПерсУчет);
	ДополнительныеПараметры.Вставить("ЯвляетсяКрупнейшим", ЯвляетсяКрупнейшим);
	ДополнительныеПараметры.Вставить("РаботаетСГосТайной", РаботаетСГосТайной);
		
	ТребованияФНСКлиент.ОткрытьТестНаОбязательностьЭДО(Организация, ОписаниеОповещения, Ложь, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если Обязательность = ТребованияФНСКлиентСервер.НеУказано() Тогда
		Текст = НСтр("ru = 'Укажите, обязателен ли электронный документооборот для организации';
					|en = 'Укажите, обязателен ли электронный документооборот для организации'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
		Возврат;
	КонецЕсли;
	
	Если ДоступнаГалкаЗапомнить И Запомнить // Из группового подтверждения приема
		ИЛИ НЕ ДоступнаГалкаЗапомнить Тогда // Из Настроек обмена ФНС и Панели отправки требования/описи/этапов отправки
		ТребованияФНСКлиент.СохранитьРезультатВыбораЭДОПоОрганизации(Организация, Обязательность);
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_Закрыть", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗначениеПоУмолчанию()
	
	Если НЕ ДоступнаГалкаЗапомнить Тогда
		Запомнить = Истина;
	КонецЕсли;
	
	УдалосьПолучитьОтветыПоУмолчанию = ТребованияФНС.УстановитьОтветыТестаПоУмолчанию(ЭтотОбъект, Организация);
	
	Обязателен = ТребованияФНСВызовСервера.ЭДООбязательноПоОрганизации(Организация);
		
	Если Обязателен = Истина Тогда
		Обязательность = 1;
	ИначеЕсли Обязателен = Ложь Тогда
		Обязательность = 0;
	ИначеЕсли Обязателен = Неопределено Тогда
		
		Если ТребованияФНС.ЭДООбязателенСогласноТесту(ЭтотОбъект) Тогда
			Обязательность = 1;
		ИначеЕсли ТребованияФНС.ЭДОНеОбязателенСогласноТесту(ЭтотОбъект) Тогда
			Обязательность = 0;
		ИначеЕсли ТребованияФНС.ЭДОНеОпределенСогласноТесту(ЭтотОбъект) Тогда
			Обязательность = ТребованияФНСКлиентСервер.НеУказано();
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РазобратьПараметры(Параметры)
	
	Организация = Параметры.Организация;
	ДоступнаГалкаЗапомнить = Параметры.ДоступнаГалкаЗапомнить;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеФормойПриСоздании()
	
	Заголовок = СтрШаблон(Заголовок, Строка(Организация));
	
	Элементы.Вступление.Заголовок = Новый ФорматированнаяСтрока(
		Элементы.Вступление.Заголовок,
		" ",
		Новый ФорматированнаяСтрока(Строка(Организация), Новый Шрифт(,,Истина)),
		":");
		
	ИзменитьОформлениеПохоже();
	
	Если ДоступнаГалкаЗапомнить Тогда
		Элементы.ФормаПрименить.Заголовок = НСтр("ru = '   ОК   ';
												|en = '   ОК   '");
	Иначе
		Элементы.ФормаПрименить.Заголовок = НСтр("ru = 'Сохранить';
												|en = 'Сохранить'");
	КонецЕсли;
	
	УправлениеФормой();
		
КонецПроцедуры

&НаСервере
Процедура ИзменитьОформлениеПохоже()
	
	Элементы.Похоже.Видимость = УдалосьПолучитьОтветыПоУмолчанию И НЕ ТребованияФНС.ЭДОНеОпределенСогласноТесту(ЭтотОбъект);
	
	Если Элементы.Похоже.Видимость Тогда
		
		Если ТребованияФНС.ЭДООбязателенСогласноТесту(ЭтотОбъект) Тогда
			Текст = НСтр("ru = 'обязателен';
						|en = 'обязателен'");
		 ИначеЕсли ТребованияФНС.ЭДОНеОбязателенСогласноТесту(ЭтотОбъект) Тогда 
			Текст = НСтр("ru = 'не обязателен';
						|en = 'не обязателен'");
		Иначе
			Элементы.Похоже.Видимость = Ложь;
			Возврат;
		КонецЕсли;
			
		Элементы.Похоже.Заголовок = Новый ФорматированнаяСтрока(
			Элементы.Похоже.Заголовок,
			" ",
			Новый ФорматированнаяСтрока(Текст, Новый Шрифт(,,Истина)),
			".");
		
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	Элементы.ГруппаГалочка.Видимость = Обязательность <> ТребованияФНСКлиентСервер.НеУказано();
	Элементы.ГруппаГалочка.ТолькоПросмотр = НЕ ДоступнаГалкаЗапомнить;
	
	Если Элементы.ГруппаГалочка.Видимость Тогда
		ТребованияФНС.ИзменитьОформлениеВопросаЗапомнитьПроЭДО(ЭтотОбъект, Организация, Обязательность);
	КонецЕсли;
		
КонецПроцедуры
	
&НаКлиенте
Процедура ЕслиВыНеУвереныОбработкаНавигационнойСсылки_Завершение(Обязателен, ВходящийКонтекст) Экспорт
	
	Если Обязателен <> Неопределено Тогда
		
		Обязательность = ?(Обязателен, 1, 0);
		
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Закрыть()
	
	Если Обязательность = 0 Тогда
		Закрыть(Ложь);
	ИначеЕсли Обязательность = 1 Тогда
		Закрыть(Истина);
	ИначеЕсли Обязательность = ТребованияФНСКлиентСервер.НеУказано() Тогда
		Закрыть(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

