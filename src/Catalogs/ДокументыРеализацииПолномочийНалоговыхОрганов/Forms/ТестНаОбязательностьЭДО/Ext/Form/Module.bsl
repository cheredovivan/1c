
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РазобратьПараметры(Параметры);
	СкорректироватьПереключатели();
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФлагПриИзменении(Элемент)

	Если Элемент = Элементы.СдаетПерсУчет 
		И ТребованияФНСКлиентСервер.Нет(СдаетПерсУчет) Тогда
		
		Если ТребованияФНСКлиентСервер.НеЗнаю(ЯвляетсяКрупнейшим) Тогда
			ЯвляетсяКрупнейшим = 0;
		КонецЕсли;
		
		Если ТребованияФНСКлиентСервер.НеЗнаю(Больше100Сотрудников) Тогда
			Больше100Сотрудников = 0;
		КонецЕсли;
	
	КонецЕсли;
	
	Если Элемент = Элементы.Больше100Сотрудников 
		И ТребованияФНСКлиентСервер.Нет(Больше100Сотрудников)
		И ТребованияФНСКлиентСервер.НеЗнаю(ЯвляетсяКрупнейшим) Тогда
		
		ЯвляетсяКрупнейшим = 0;
		
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Применить(Команда)
	
	Если ЭДОНеОпределенСогласноТесту() Тогда
		
		Текст = НСтр("ru = 'Для определения обязательности электронного документооборота укажите больше ответов ""да"" или ""нет""';
					|en = 'Для определения обязательности электронного документооборота укажите больше ответов ""да"" или ""нет""'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(Текст);
		Возврат;
		
	Иначе
		
		Обязателен = ЭДООбязателенСогласноТесту();
		
		Если ПредлагатьЗапомнитьПоОрганизации И Запомнить Тогда
			ТребованияФНСВызовСервера.СохранитьРезультатВыбораЭДОПоОрганизации(Организация, Обязателен);
		КонецЕсли;
		
		Закрыть(Обязателен);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()

	Обязателен = ЭДООбязателенСогласноТесту();
	НеОпределен = ЭДОНеОпределенСогласноТесту();
	НеОбязателен = ЭДОНеОбязателенСогласноТесту();

	Если НеОпределен Тогда
		Элементы.ИтогЗаключение.Заголовок = НСтр("ru = 'Для определения обязательности электронного документооборота укажите больше ответов ""да"" или ""нет""';
												|en = 'Для определения обязательности электронного документооборота укажите больше ответов ""да"" или ""нет""'");
	ИначеЕсли Обязателен Тогда
		Элементы.ИтогЗаключение.Заголовок = НСтр("ru = 'Электронный документооборот обязателен';
												|en = 'Электронный документооборот обязателен'");
	ИначеЕсли НеОбязателен Тогда
		Элементы.ИтогЗаключение.Заголовок = НСтр("ru = 'Электронный документооборот не обязателен';
												|en = 'Электронный документооборот не обязателен'");
	КонецЕсли;
	
	Элементы.ЭДООбязателен.Видимость = Обязателен;
	Элементы.ЭДОНеОбязателен.Видимость = НеОбязателен;
	
	Элементы.ГруппаГалочка.Видимость = ПредлагатьЗапомнитьПоОрганизации И НЕ НеОпределен;
	
	Если Элементы.ГруппаГалочка.Видимость Тогда
		ТребованияФНС.ИзменитьОформлениеВопросаЗапомнитьПроЭДО(ЭтотОбъект, Организация, Обязателен);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура РазобратьПараметры(Параметры)
	
	Организация = Параметры.Организация;
	ПредлагатьЗапомнитьПоОрганизации = Параметры.ПредлагатьЗапомнитьПоОрганизации;
	
	КлючСохраненияПоложенияОкна = Строка(ПредлагатьЗапомнитьПоОрганизации);
	
	Если Параметры.Свойство("РаботаетСГосТайной") Тогда
	
		РаботаетСГосТайной = Параметры.РаботаетСГосТайной;
		Больше100Сотрудников = Параметры.Больше100Сотрудников;
		ОбязаныПодаватьДекларацию = Параметры.ОбязаныПодаватьДекларацию;
		СдаетПерсУчет = Параметры.СдаетПерсУчет;
		ЯвляетсяКрупнейшим = Параметры.ЯвляетсяКрупнейшим;
		ОбязаныПодаватьЖурналСФ = Параметры.ОбязаныПодаватьЖурналСФ;
		
	Иначе
		
		ТребованияФНС.УстановитьОтветыТестаПоУмолчанию(ЭтотОбъект, Организация);
		
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СкорректироватьПереключатели()
	
	Если (ТребованияФНСКлиентСервер.Нет(СдаетПерсУчет) ИЛИ ТребованияФНСКлиентСервер.Нет(Больше100Сотрудников))
		И ТребованияФНСКлиентСервер.НеЗнаю(ЯвляетсяКрупнейшим) Тогда
		ЯвляетсяКрупнейшим = 0;
	КонецЕсли;
	
	Если ТребованияФНСКлиентСервер.Нет(СдаетПерсУчет)
		И ТребованияФНСКлиентСервер.НеЗнаю(Больше100Сотрудников) Тогда
		Больше100Сотрудников = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭДООбязателенСогласноТесту() Экспорт

	Возврат ТребованияФНС.ЭДООбязателенСогласноТесту(ЭтотОбъект);

КонецФункции

&НаСервере
Функция ЭДОНеОбязателенСогласноТесту() Экспорт

	Возврат ТребованияФНС.ЭДОНеОбязателенСогласноТесту(ЭтотОбъект);

КонецФункции

&НаСервере
Функция ЭДОНеОпределенСогласноТесту() Экспорт

	Возврат ТребованияФНС.ЭДОНеОпределенСогласноТесту(ЭтотОбъект);

КонецФункции

#КонецОбласти

