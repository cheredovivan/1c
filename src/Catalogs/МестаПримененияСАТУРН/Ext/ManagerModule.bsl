#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
	
		Если Параметры = Неопределено Тогда
			Параметры = Новый Структура();
		КонецЕсли;
		
		Параметры.Вставить("РежимВыбора", Истина);
		
		ВыбраннаяФорма       = "Справочник.МестаПримененияСАТУРН.Форма.ФормаСписка";
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОрганизацииВладельцыМестаПрименения(МестоПрименения) Экспорт
	
	МассивВладельцев = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	КлассификаторОрганизацийСАТУРНМестаПрименения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КлассификаторОрганизацийСАТУРН.МестаПрименения КАК КлассификаторОрганизацийСАТУРНМестаПрименения
		|ГДЕ
		|	НЕ КлассификаторОрганизацийСАТУРНМестаПрименения.Ссылка.ПометкаУдаления
		|	И КлассификаторОрганизацийСАТУРНМестаПрименения.МестоПрименения = &МестоПрименения";
	
	Запрос.УстановитьПараметр("МестоПрименения", МестоПрименения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивВладельцев.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Возврат МассивВладельцев;
	
КонецФункции

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * GUID - Строка
// * Идентификатор - Строка
// * Статус - ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка - 
// * ДатаСоздания - Дата - 
// * ДатаИзменения - Дата - 
// * Наименование - Строка
// * НаименованиеСубъектаРФ - Строка
// * ОКТМО - Строка
// * КадастровыйНомер - Строка
// * ИдентификаторКонтураЕФИСЗСН - Строка
// * ПлощадьКонтураВГектарах - Число - 
// * ГеографическиеКоординаты - Строка
// * Комментарий - Строка
// * ПоднадзорныйОбъектНомерОбъекта - Строка
// * ПоднадзорныйОбъектНомерПлощадки - Строка
// * ПоднадзорныйОбъектТип - Строка
// * ПоднадзорныйОбъектРазрешенныеВидыДеятельности - Строка
// * КадастровыйНомерРайона - Строка - 
// * ГеографическаяФорма - Неопределено - 
// * ИдентификаторОрганизации - Строка
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеМестаПрименения           = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеМестаПрименения.Вставить("Идентификатор", Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеМестаПрименения.Вставить("Статус",        ИнтерфейсСАТУРН.Статус(ЭлементДанных.lcState));
	
	ДанныеМестаПрименения.Вставить("Наименование",             ЭлементДанных.name);
	ДанныеМестаПрименения.Вставить("НаименованиеСубъектаРФ",   ЭлементДанных.subName);
	ДанныеМестаПрименения.Вставить("ГеографическиеКоординаты", ЭлементДанных.location);
	ДанныеМестаПрименения.Вставить("Комментарий",              ЭлементДанных.description);

	ДанныеМестаПрименения.Вставить("ПоднадзорныйОбъектНомерОбъекта",                ЭлементДанных.uco_objectNumber);
	ДанныеМестаПрименения.Вставить("ПоднадзорныйОбъектНомерПлощадки",               ЭлементДанных.uco_areaNumber);
	ДанныеМестаПрименения.Вставить("ПоднадзорныйОбъектТип",                         ЭлементДанных.uco_objectType);
	ДанныеМестаПрименения.Вставить("ПоднадзорныйОбъектРазрешенныеВидыДеятельности", ЭлементДанных.uco_allowedActions_csv);
	
	// инициализация значений, которые могут отсутствовать в структуре данных
	ДанныеМестаПрименения.Вставить("КадастровыйНомерРайона",      "");
	ДанныеМестаПрименения.Вставить("КадастровыйНомер",            "");
	ДанныеМестаПрименения.Вставить("ОКТМО",                       "");
	ДанныеМестаПрименения.Вставить("ИдентификаторКонтураЕФИСЗСН", "");
	ДанныеМестаПрименения.Вставить("ПлощадьКонтураВГектарах",     0);
	ДанныеМестаПрименения.Вставить("ОКТМО",                       "");
	ДанныеМестаПрименения.Вставить("ОКТМО",                       "");
	ДанныеМестаПрименения.Вставить("ИдентификаторОрганизации",    "");
	ДанныеМестаПрименения.Вставить("ГеографическаяФорма",         Неопределено);
	ДанныеМестаПрименения.Вставить("ВидТерритории",               Неопределено);
	ДанныеМестаПрименения.Вставить("ТипМестаПрименения",          Неопределено);
	
	ИнтеграцияСАТУРНСлужебный.ДополнитьДанныеОбъектаОбщимиДанными(ДанныеМестаПрименения, ЭлементДанных);
	
	Если ЭлементДанных.Свойство("cadRegNum") Тогда
		ДанныеМестаПрименения.Вставить("КадастровыйНомерРайона", ЭлементДанных.cadRegNum);
	КонецЕсли;
	Если ЭлементДанных.Свойство("oktmo") Тогда
		ДанныеМестаПрименения.Вставить("ОКТМО", ЭлементДанных.oktmo);
	КонецЕсли;
	Если ЭлементДанных.Свойство("geoForm") Тогда
		ДанныеМестаПрименения.Вставить("ГеографическаяФорма", ЭлементДанных.geoForm);
	КонецЕсли;
	Если ЭлементДанных.Свойство("cadNum") Тогда
		ДанныеМестаПрименения.Вставить("КадастровыйНомер", ЭлементДанных.cadNum);
	КонецЕсли;
	Если ЭлементДанных.Свойство("efisId") Тогда
		ДанныеМестаПрименения.Вставить("ИдентификаторКонтураЕФИСЗСН", ЭлементДанных.efisId);
	КонецЕсли;
	Если ЭлементДанных.Свойство("area") Тогда
		ОписаниеТипаЧисло = ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Неотрицательный);
		ДанныеМестаПрименения.Вставить("ПлощадьКонтураВГектарах", ОписаниеТипаЧисло.ПривестиЗначение(ЭлементДанных.area));
	КонецЕсли;
	Если ЭлементДанных.Свойство("type_owner") Тогда
		ДанныеМестаПрименения.Вставить("ВидТерритории", ИнтерфейсСАТУРН.ВидИспользуемойТерритории(ЭлементДанных.type_owner));
	КонецЕсли;
	Если ЭлементДанных.Свойство("managingContractorId") Тогда
 		ДанныеМестаПрименения.Вставить("ИдентификаторОрганизации", Формат(ЭлементДанных.managingContractorId, ФорматОтображенияИдентификатора));
	КонецЕсли;
	Если ЗначениеЗаполнено(ЭлементДанных.uco_objectType) Тогда
		ДанныеМестаПрименения.Вставить("ТипМестаПрименения", ИнтерфейсСАТУРН.ТипМестаПрименения(ЭлементДанных.uco_objectType));
	КонецЕсли;
	
	Возврат ДанныеМестаПрименения;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					Продолжить;
				КонецЕсли;
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Если ТипЗнч(ПараметрыИсходящегоСообщенияОснования) = Тип("Структура") И ПараметрыИсходящегоСообщенияОснования.Свойство("ПорционнаяЗагрузка")
			И ПараметрыИсходящегоСообщенияОснования.ПорционнаяЗагрузка Тогда
			
			НомерТекущейСтраницы = ПараметрыИсходящегоСообщенияОснования.НомерСтраницы;
			РазмерПорции         = ПараметрыИсходящегоСообщенияОснования.РазмерПорции;
			
			Если ВходящиеДанные.Количество() < РазмерПорции Тогда
				Возврат;
			КонецЕсли;
			
			НомерТекущейСтраницы = НомерТекущейСтраницы + 1;
			
			СообщенияJSON = ЗагрузкаКлассификатораМестаПримененияJSON("", ПараметрыИсходящегоСообщенияОснования.ИдентификаторОрганизации, НомерТекущейСтраницы, РазмерПорции);
			ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
			
		КонецЕсли;
	
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.МестоПримененияСозданиеКлассификатора Тогда
		
		СообщенияJSON = Новый Массив;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Идентификатор = ПолученныеДанные.id;
		
		СправочникОбъект               = СоздатьЭлемент();
		СправочникОбъект.Идентификатор = Идентификатор;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта,, "Идентификатор");
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ИзмененныеОбъекты.Добавить(СправочникОбъект.Ссылка);
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ,, Истина);
		СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		
		СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеJSON.АргументыОперации, "id", Идентификатор);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.МестоПримененияЗапросКлассификатора;
КонецФункции

#Область ПоискСсылок

Функция МестоПрименения(ЗначениеИдентификатора, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Идентификатор = Формат(ЗначениеИдентификатора, "ЧГ=0;");
	
	Если Не ЗначениеЗаполнено(Идентификатор)
		Или ЗначениеИдентификатора = -1 Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ИмяТаблицы       = Метаданные.Справочники.МестаПримененияСАТУРН.ПолноеИмя();
	СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
		
				ДанныеОбъекта = ИнтеграцияСАТУРНСлужебный.ДанныеОбъекта(
					Идентификатор,
					Метаданные.Справочники.МестаПримененияСАТУРН, ПараметрыОбмена);
				Если ДанныеОбъекта = Неопределено Тогда
					СправочникСсылка = СоздатьМестоПрименения(Идентификатор, ПараметрыОбмена);
					ИнтеграцияСАТУРНСлужебный.ДобавитьКЗагрузке(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка, ОрганизацияСАТУРН);
				Иначе
					СправочникСсылка = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,, Ложь, ОрганизацияСАТУРН);
				КонецЕсли;
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Места применения с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Места применения с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Места применения с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Места применения с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с Местами применения';
					|en = 'Работа с Местами применения'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьОбъект(ДанныеМестаПрименения, ПараметрыОбмена, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеМестаПрименения = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового = Ложь;
	ИмяТаблицы   = Метаданные.Справочники.МестаПримененияСАТУРН.ПолноеИмя();
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, ДанныеМестаПрименения.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникСсылка = ПолучитьСсылку(ИдентификаторОбъекта);
			СправочникОбъект.УстановитьСсылкуНового(СправочникСсылка);

			ЗаписьНового = Истина;
			
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
	
	Иначе
		СправочникСсылка = СправочникОбъект.Ссылка;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Наименование           = ДанныеМестаПрименения.Наименование;
	СправочникОбъект.Идентификатор          = ДанныеМестаПрименения.Идентификатор;
	СправочникОбъект.Статус                 = ДанныеМестаПрименения.Статус;
	СправочникОбъект.НаименованиеСубъектаРФ = ДанныеМестаПрименения.НаименованиеСубъектаРФ;
	СправочникОбъект.КадастровыйНомер       = ДанныеМестаПрименения.КадастровыйНомер;
	СправочникОбъект.ВидТерритории          = ДанныеМестаПрименения.ВидТерритории;
	СправочникОбъект.Комментарий            = ДанныеМестаПрименения.Комментарий;
	СправочникОбъект.ТипМестаПрименения     = ДанныеМестаПрименения.ТипМестаПрименения;
	
	Если ЗначениеЗаполнено(ДанныеМестаПрименения.ИдентификаторОрганизации) Тогда
		
		Организация       = Справочники.КлассификаторОрганизацийСАТУРН.Организация(ДанныеМестаПрименения.ИдентификаторОрганизации, ПараметрыОбмена);
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		
		СтруктураОтбора = Новый Структура("МестоПрименения", СправочникСсылка);
		СтрокаСвязкиОрганизацииСМестомПрименения = ОрганизацияОбъект.МестаПрименения.НайтиСтроки(СтруктураОтбора);
		
		Если СтрокаСвязкиОрганизацииСМестомПрименения.Количество() = 0 Тогда
			
			НоваяСтрокаПривязки = ОрганизацияОбъект.МестаПрименения.Добавить();
			НоваяСтрокаПривязки.МестоПрименения = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СправочникОбъект.ТребуетсяЗагрузка = Ложь;
	СправочникОбъект.Записать();
	
	ОрганизацияОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеМестаПрименения.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция СоздатьМестоПрименения(Идентификатор, ПараметрыОбмена)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор     = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка = Истина;
	СправочникОбъект.Наименование      = НСтр("ru = '<Требуется загрузка>';
												|en = '<Требуется загрузка>'");
	
	РеквизитыСоздания = ИнтеграцияСАТУРН.РеквизитыСозданияОбъекта(
		Идентификатор, СправочникОбъект.Метаданные(), ПараметрыОбмена);
	
	Если РеквизитыСоздания <> Неопределено Тогда
		Для Каждого КлючИЗначение Из РеквизитыСоздания Цикл
			СправочникОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЦикла;
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбработки       = Неопределено;
	ДанныеОбъекта            = Неопределено;
	Идентификатор            = "";
	ИдентификаторОрганизации = "";
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПараметрыОбработкиДокумента")
		И ТипЗнч(ДополнительныеПараметры.ПараметрыОбработкиДокумента) = Тип("Структура")
		И ДополнительныеПараметры.ПараметрыОбработкиДокумента.Свойство("ДополнительныеПараметры") Тогда
		
		ПараметрыОбработки = ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры;
		
		Если ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
			
			Если ПараметрыОбработки.Свойство("ДанныеМестаПрименения") Тогда
				ДанныеОбъекта = ПараметрыОбработки.ДанныеМестаПрименения;
			ИначеЕсли ПараметрыОбработки.Свойство("Идентификатор") Тогда
				Идентификатор = ПараметрыОбработки.Идентификатор;
			ИначеЕсли ПараметрыОбработки.Свойство("ИдентификаторОрганизации") Тогда
				ИдентификаторОрганизации = ПараметрыОбработки.ИдентификаторОрганизации;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СообщенияJSON = Новый Массив;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные Тогда
		
		Если ДанныеОбъекта <> Неопределено Тогда
			
			Возврат СозданиеКлассификатораМестаПримененияJSON(СправочникСсылка, ДанныеОбъекта);
			
		КонецЕсли;
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаКлассификатораМестаПримененияJSON(Идентификатор, ИдентификаторОрганизации);
		
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

// Формирует JSON сообщения для создания/изменения места применения.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.МестаПримененияСАТУРН - Ссылка на организацию.
//  ДанныеОбъекта - Структура из См. ИнтеграцияСАТУРНКлиентСервер.СтруктураДанныхМестаПрименения - описание справочника
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
//
Функция СозданиеКлассификатораМестаПримененияJSON(СправочникСсылка, ДанныеОбъекта)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = СправочникСсылка;
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.МестоПримененияСозданиеКлассификатора;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СправочникСсылка);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
	СообщениеJSON.ПараметрыЗапроса.Вставить("ДанныеОбъекта", ДанныеОбъекта);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	МассивКоординат = Новый Массив;
	МассивКоординат.Добавить(ДанныеОбъекта.Широта);
	МассивКоординат.Добавить(ДанныеОбъекта.Долгота);
	
	СтруктураОписанияКоординат = Новый Структура();
	СтруктураОписанияКоординат.Вставить("type",        "Point");
	СтруктураОписанияКоординат.Вставить("coordinates", МассивКоординат);
	
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "name",         ДанныеОбъекта.Наименование);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "oktmo",        ДанныеОбъекта.ОКТМО);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "geoCenter",    СтруктураОписанияКоординат);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "address",      ДанныеОбъекта.Адрес);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "typeOwner",    ИнтерфейсСАТУРН.ВидИспользуемойТерритории(ДанныеОбъекта.ВидОтношений));
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "type",         ИнтерфейсСАТУРН.ТипМестаПрименения(ДанныеОбъекта.ТипМестаПрименения));
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗагрузкаКлассификатораМестаПримененияJSON(Идентификатор, ИдентификаторОрганизации = "", НомерСтраницы = 0, РазмерПорции = 100) Экспорт
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации      = СообщениеJSON.АргументыОперации;
	
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		
		АргументыОперации.pos  = 0;
		АргументыОперации.size = 100;
		
		Если Не ТипЗнч(Идентификатор) = Тип("Массив") Тогда
			Идентификатор = Формат(Идентификатор, "ЧГ=;");
		КонецЕсли;
		
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "id", Идентификатор);
		
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		
		АргументыОперации.pos  = НомерСтраницы * РазмерПорции;
		АргументыОперации.size = РазмерПорции;
		
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "managingContractorId", Формат(ИдентификаторОрганизации, "ЧГ=;"));
		
		СообщениеJSON.ПараметрыЗапроса = Новый Структура();
		СообщениеJSON.ПараметрыЗапроса.Вставить("ПорционнаяЗагрузка",       Истина);
		СообщениеJSON.ПараметрыЗапроса.Вставить("НомерСтраницы",            АргументыОперации.pos);
		СообщениеJSON.ПараметрыЗапроса.Вставить("РазмерПорции",             АргументыОперации.size);
		СообщениеJSON.ПараметрыЗапроса.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		
	Иначе
		СообщениеJSON.ТекстОшибки = 
			НСтр("ru = 'Некорректный вызов метода загрузки:
			|Для загрузки места применения должен быть передан идентификатор объекта или идентификатор организации-владельца.';
			|en = 'Некорректный вызов метода загрузки:
			|Для загрузки места применения должен быть передан идентификатор объекта или идентификатор организации-владельца.'");
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли