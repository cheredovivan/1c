#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяет настройки начального заполнения элементов
//
// Параметры:
//  Настройки - Структура - настройки заполнения:
//   * ПриНачальномЗаполненииЭлемента - Булево - Если Истина, то для каждого элемента будет
//      вызвана процедура индивидуального заполнения ПриНачальномЗаполненииЭлемента.
Процедура ПриНастройкеНачальногоЗаполненияЭлементов(Настройки) Экспорт
	
	Настройки.ПриНачальномЗаполненииЭлемента = Истина;
	
КонецПроцедуры

// Вызывается при начальном заполнении справочника.
//
// Параметры:
//  КодыЯзыков - Массив - список языков конфигурации. Актуально для мультиязычных конфигураций.
//  Элементы   - ТаблицаЗначений - данные заполнения. Состав колонок соответствует набору реквизитов
//                                 справочника.
//  ТабличныеЧасти - Структура - Ключ - Имя табличной части объекта.
//                               Значение - Выгрузка в таблицу значений пустой табличной части.
//
Процедура ПриНачальномЗаполненииЭлементов(КодыЯзыков, Элементы, ТабличныеЧасти) Экспорт

	// Данные для заполнения справочника берутся из описания операций закрытия месяца.
	ОписаниеЭтаповЗакрытияМесяца = Обработки.ОперацииЗакрытияМесяца.ЗаполнитьОписаниеЭтаповЗакрытияМесяца(Истина);
	
	Для Каждого ОписаниеЭтапаЗакрытияМесяца Из ОписаниеЭтаповЗакрытияМесяца Цикл
		Элемент = Элементы.Добавить();
		ЗаполнитьЗначенияСвойств(Элемент, ОписаниеЭтапаЗакрытияМесяца);
		
		Элемент.ИмяПредопределенныхДанных = Справочники.ОперацииЗакрытияМесяца.ПолучитьИмяПредопределенного(
			ОписаниеЭтапаЗакрытияМесяца.Код);
		Элемент.Родитель =
			ПредопределенноеЗначение("Справочник.ОперацииЗакрытияМесяца." + ОписаниеЭтапаЗакрытияМесяца.Родитель);
		Элемент.СледуетЗаЭтапом = ОписаниеЭтапаЗакрытияМесяца.СледуетЗаЭтапом;
		МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование",
			ОписаниеЭтапаЗакрытияМесяца.МультиязычноеНаименование, КодыЯзыков); // @НСтр-2
		
		Комментарий = ?(ЗначениеЗаполнено(ОписаниеЭтапаЗакрытияМесяца.МультиязычныйКомментарий),
			ОписаниеЭтапаЗакрытияМесяца.МультиязычныйКомментарий,
			"ru = ''"); // @НСтр-1
			
		МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Комментарий", Комментарий, КодыЯзыков);
		
		Если Не ОписаниеЭтапаЗакрытияМесяца.ПредшествующиеЭтапы.Количество() Тогда
			Продолжить;
		КонецЕсли;

		Элемент.ПредшествующиеЭтапы = ТабличныеЧасти.ПредшествующиеЭтапы.Скопировать();
		
		Для Каждого ПредшествующийЭтап Из ОписаниеЭтапаЗакрытияМесяца.ПредшествующиеЭтапы Цикл
			
			Если Элемент.ПредшествующиеЭтапы.Найти(ПредшествующийЭтап) = Неопределено Тогда
				Элемент.ПредшествующиеЭтапы.Добавить().ОперацияЗакрытияМесяца =  ПредшествующийЭтап;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Описание групп элементов
	ТаблицаГруппЭтапов = Обработки.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияГруппЭтапов();
	ЗакрытиеМесяцаСервер.ЗаполнитьОписаниеГруппЭтаповЗакрытияМесяца(ТаблицаГруппЭтапов);
	
	Для Каждого ОписаниеГруппыЗакрытияМесяца Из ТаблицаГруппЭтапов Цикл
		Элемент = Элементы.Добавить();
		Элемент.ИмяПредопределенныхДанных = ОписаниеГруппыЗакрытияМесяца.Код;
		МультиязычностьСервер.ЗаполнитьМультиязычныйРеквизит(Элемент, "Наименование",
			ОписаниеЭтапаЗакрытияМесяца.МультиязычноеНаименование, КодыЯзыков); // @НСтр-2
	КонецЦикла;

	// Прочие служебные элементы, не выводимые в рабочее месяца
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииЗакрытияМесяца.ИмяПредопределенныхДанных КАК ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ОперацииЗакрытияМесяца КАК ОперацииЗакрытияМесяца
		|ГДЕ
		|	НЕ ОперацииЗакрытияМесяца.ИмяПредопределенныхДанных В(&ПредопределенныеДанные)";
	
	Запрос.УстановитьПараметр("ПредопределенныеДанные", Элементы.ВыгрузитьКолонку("ИмяПредопределенныхДанных"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Элемент = Элементы.Добавить();
		Элемент.ИмяПредопределенныхДанных = ВыборкаДетальныеЗаписи.ИмяПредопределенныхДанных;
	КонецЦикла;
	
КонецПроцедуры

// Вызывается при начальном заполнении создаваемого элемента.
//
// Параметры:
//  Объект                  - СправочникОбъект.Партнеры - заполняемый объект.
//  Данные                  - СтрокаТаблицыЗначений - данные заполнения.
//  ДополнительныеПараметры - Структура - Дополнительные параметры.
//
Процедура ПриНачальномЗаполненииЭлемента(Объект, Данные, ДополнительныеПараметры) Экспорт
	
	Возврат;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

// Возвращает реквизиты справочника, которые образуют естественный ключ для элементов справочника.
//
// Возвращаемое значение:
//  Массив из Строка - имена реквизитов, образующих естественный ключ.
//
Функция ПоляЕстественногоКлюча() Экспорт
	
	Результат = Новый Массив;
	
	Результат.Добавить("ИмяПредопределенныхДанных");
	
	Возврат Результат;
	
КонецФункции

// Конец ТехнологияСервиса.ВыгрузкаЗагрузкаДанных

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка, Метаданные.Справочники.ВидыНеоперативныхЗаданий);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

#КонецЕсли

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьКлиентСервер.ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Возвращает неиспользуемые операции закрытия месяца, по которым не должно быть записей в регистре ЗаданияКЗакрытиюМесяца.
// 
// Возвращаемое значение:
// Массив из СправочникСсылка.ОперацииЗакрытияМесяца - неиспользуемые операции
Функция НеиспользуемыеОперации() Экспорт
	
	Результат = Новый Массив;
	
	Возврат Результат; 
	
КонецФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ОперацииЗакрытияМесяца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "*";
	Обработчик.РежимВыполнения = "Оперативно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("07cd5f88-4bfc-417b-ac20-7e0330993db4");
	Обработчик.ОбщиеДанные = Истина;
	
	Обработчик.Комментарий = НСтр("ru = 'Заполнение реквизитов справочника ""Операции закрытия месяца"":';
									|en = 'Fill in the attributes of the ""Month-end closing operations"" catalog:'");
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.ОперацииЗакрытияМесяца.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОперацииЗакрытияМесяца.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");

КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию() Экспорт
	
	МетаданныеОбъекта = СоздатьЭлемент().Метаданные();
	ПолноеИмяОбъекта = МетаданныеОбъекта.ПолноеИмя();

	СведенияОбЯзыках = МультиязычностьСервер.СведенияОЯзыках();
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	НастройкиОбновления = МультиязычностьСервер.НастройкиОбновлениеПредопределенныхДанных(МетаданныеОбъекта);
	ЭтапыИГруппы = Новый Массив;
	Для Каждого Элемент Из НастройкиОбновления.ПредопределенныеДанные Цикл
		ПредопределенныйЭлемент =
			ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ОперацииЗакрытияМесяца."
				+ Элемент.ИмяПредопределенныхДанных);
		Если ПредопределенныйЭлемент <> Неопределено Тогда
			ЭтапыИГруппы.Добавить(Справочники.ОперацииЗакрытияМесяца[Элемент.ИмяПредопределенныхДанных]);
		КонецЕсли;
	КонецЦикла;

	ДанныеСправочника = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ЭтапыИГруппы, ПоляКОбновлению());

	Для Каждого Этап Из НастройкиОбновления.ПредопределенныеДанные Цикл
		Операция = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ОперацииЗакрытияМесяца."
			+ Этап.ИмяПредопределенныхДанных);
		
		Если Операция <> Неопределено Тогда
			
			ЭтапСправочник = ДанныеСправочника.Получить(Операция);
			Для Каждого Свойство Из ЭтапСправочник Цикл
				Если ЭтапСправочник[Свойство.Ключ] <> Этап[Свойство.Ключ] Тогда

					НачатьТранзакцию();

					Попытка

						Блокировка = Новый БлокировкаДанных;
						ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
						ЭлементБлокировки.УстановитьЗначение("Ссылка", Операция);
						ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
						Блокировка.Заблокировать();

						ОперацияОбъект = Операция.ПолучитьОбъект();

						ОбъектИзменен = Ложь;

						Если ОперацияОбъект <> Неопределено Тогда
							Если Не ОперацияОбъект.ЭтоГруппа Тогда
								ЗаполнитьЗначенияСвойств(ОперацияОбъект, Этап, ПоляКОбновлению());
							КонецЕсли;

							Если РазделениеВключено Тогда

								Для Каждого ЛокализуемыйРеквизитОбъекта Из НастройкиОбновления.ЛокализуемыеРеквизитыОбъекта Цикл
									Имя = ЛокализуемыйРеквизитОбъекта.Ключ;
									ОперацияОбъект[Имя] = Этап[Имя + "_" + НастройкиОбновления.ОсновнойЯзык];

									Для Каждого ЯзыкИспользуется Из СведенияОбЯзыках.Используется Цикл
										Если ЯзыкИспользуется.Значение Тогда
											КодЯзыка = СведенияОбЯзыках[ЯзыкИспользуется.Ключ];
											ОперацияОбъект[Имя + ЯзыкИспользуется.Ключ] = Этап[Имя + "_" + КодЯзыка];
										КонецЕсли;
									КонецЦикла;
								КонецЦикла;

							КонецЕсли;

							ОбъектИзменен = Истина;
						КонецЕсли;

						Если ОбъектИзменен Тогда
							ОбновлениеИнформационнойБазы.ЗаписатьДанные(ОперацияОбъект);
						Иначе
							ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Операция);
						КонецЕсли;

						ЗафиксироватьТранзакцию();

					Исключение

						ОтменитьТранзакцию();
						ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Операция);

					КонецПопытки;

					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция ПоляКОбновлению()

	Возврат "ВыполняетсяВручную,ВыполняетсяПриПредварительномЗакрытииМесяца,ТекстВыполнить,ТекстПодробнее,
			|ОперативныйУчет,РегламентированныйУчет,МеждународныйУчет,Информационный,СледуетЗаЭтапом";

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли