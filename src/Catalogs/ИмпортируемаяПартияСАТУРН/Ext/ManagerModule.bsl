#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область Обмен

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * GUID - Строка
// * Идентификатор - Строка
// * Статус - ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка - 
// * ДатаПолучения - Дата, Null, Булево, Число, Строка, УникальныйИдентификатор, ДвоичныеДанные, ХранилищеЗначения, ВидСчета, ВидДвиженияБухгалтерии, ВидДвиженияНакопления, ДопустимыйЗнак, ДопустимаяДлина, ЧастиДаты, Неопределено - 
// * ДатаВвоза - Дата, Null, Булево, Число, Строка, УникальныйИдентификатор, ДвоичныеДанные, ХранилищеЗначения, ВидСчета, ВидДвиженияБухгалтерии, ВидДвиженияНакопления, ДопустимыйЗнак, ДопустимаяДлина, ЧастиДаты, Неопределено - 
// * НомерТТНАРГУС - Строка
// * КоличествоУпаковок - Строка
// * Наименование - Строка
// * Маркировка - Строка
// * УпаковочнаяЕдиница - Структура - :
// ** Идентификатор - Неопределено - 
// ** КоличествоСАТУРН - Строка
// ** НаименованиеУпаковки - Строка
// * ИдентификаторАРГУС - Строка
// * НаименованиеОтправителяАРГУС - Строка
// * ИдентификаторПредприятияРастаможиванияАРГУС - Строка
// * ДанныеМестаХранения - Неопределено - 
// * ИдентификаторМестаХранения - Строка - 
// * ДанныеПартии - Неопределено - 
// * ИдентификаторПартии - Строка - 
// * ДанныеОрганизации - Неопределено - 
// * ИдентификаторОрганизации - Строка - 
// * ДанныеПАТ - Неопределено - 
// * ИдентификаторПАТ - Строка - 
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеИмпортируемойПартии       = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеИмпортируемойПартии.Вставить("Идентификатор",           Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеИмпортируемойПартии.Вставить("Статус",                  ИнтерфейсСАТУРН.СтатусИмпортируемойПродукции(ЭлементДанных.lcState));
	ДанныеИмпортируемойПартии.Вставить("ДатаПолучения",           ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементДанных.receiveDate));
	ДанныеИмпортируемойПартии.Вставить("ДатаВвоза",               ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементДанных.invoiceDate));
	ДанныеИмпортируемойПартии.Вставить("Наименование",            ЭлементДанных.name);
	ДанныеИмпортируемойПартии.Вставить("НомерИсходногоДокумента", ЭлементДанных.docNum);
	ДанныеИмпортируемойПартии.Вставить("ДатаИсходногоДокумента",  ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементДанных.docDate));
	
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторТаможенногоПункта",                    Формат(ЭлементДанных.ppBorderId, ФорматОтображенияИдентификатора));
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторТипаТранспортногоСредстваПеревозчика", Формат(ЭлементДанных.typeTransport, ФорматОтображенияИдентификатора));
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторТерриториальногоУправления",           Формат(ЭлементДанных.tuId, ФорматОтображенияИдентификатора));
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторСтраныЭкспортера",                     Формат(ЭлементДанных.countryIdExporter, ФорматОтображенияИдентификатора));
	
	// инициализация значений, которые могут отсутствовать в структуре данных
	ДанныеИмпортируемойПартии.Вставить("ДанныеМестаХранения",        Неопределено);
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторМестаХранения", "");
	
	ДанныеИмпортируемойПартии.Вставить("ДанныеОрганизации",          Неопределено);
	ДанныеИмпортируемойПартии.Вставить("ИдентификаторОрганизации",   "");
	
	ДанныеИмпортируемойПартии.Вставить("Товары",                     Новый Массив);
	
	ИнтеграцияСАТУРНСлужебный.ДополнитьДанныеОбъектаОбщимиДанными(ДанныеИмпортируемойПартии, ЭлементДанных);
	
	Если Не ЭлементДанных.destinationWarehouseId = 0
		И Не ЭлементДанных.destinationWarehouseId = "-1"
		И Не ЭлементДанных.destinationWarehouseId = -1 Тогда
		
		ДанныеИмпортируемойПартии.Вставить("ИдентификаторМестаХранения", Формат(ЭлементДанных.destinationWarehouseId, ФорматОтображенияИдентификатора));
		
	КонецЕсли;
	
	Если Не ЭлементДанных.createdContractorId = 0
		И Не ЭлементДанных.createdContractorId = "-1"
		И Не ЭлементДанных.createdContractorId = -1 Тогда
			
		ДанныеИмпортируемойПартии.Вставить("ИдентификаторОрганизации", Формат(ЭлементДанных.createdContractorId, ФорматОтображенияИдентификатора));
		
	КонецЕсли;
	
	Если ЭлементДанных.Свойство("_tbrs") Тогда
		
		Для Каждого ЭлементСоставляющейИмпорта Из ЭлементДанных._tbrs Цикл
			
			СтруктураСоставляющей = Новый Структура();
			
			СтруктураСоставляющей.Вставить("ИдентификаторПА",             Формат(ЭлементСоставляющейИмпорта.patProductId, ФорматОтображенияИдентификатора));
			СтруктураСоставляющей.Вставить("КоличествоУпаковок",          ЭлементСоставляющейИмпорта.countPu);
			СтруктураСоставляющей.Вставить("КодТНВЭД",                    ЭлементСоставляющейИмпорта.tnved);
			СтруктураСоставляющей.Вставить("Упаковка",                    Справочники.КлючиЕдиницИзмеренияСАТУРН.ДанныеОбъекта(ЭлементСоставляющейИмпорта));
			СтруктураСоставляющей.Вставить("ТипИзмеряемойВеличиныСАТУРН", ИнтерфейсСАТУРН.ТипИзмеряемойВеличины(ЭлементСоставляющейИмпорта.baseUnitType));
			СтруктураСоставляющей.Вставить("КоличествоВУпаковкеСАТУРН",   ЭлементСоставляющейИмпорта.puKgWeight);
			СтруктураСоставляющей.Вставить("ТорговоеНаименование",        ЭлементСоставляющейИмпорта.manufacturingName);
			СтруктураСоставляющей.Вставить("СрокГодности",                ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементСоставляющейИмпорта.expirationDate));
			СтруктураСоставляющей.Вставить("ИдентификаторСтроки",         Формат(ЭлементСоставляющейИмпорта.id, ФорматОтображенияИдентификатора));
			СтруктураСоставляющей.Вставить("ИдентификаторПартии",         Формат(ЭлементСоставляющейИмпорта.batchId, ФорматОтображенияИдентификатора));
			
			ДанныеИмпортируемойПартии.Товары.Добавить(СтруктураСоставляющей);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ДанныеИмпортируемойПартии;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		ДатаИзменения = Дата(1, 1, 1);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				ДатаИзменения = ОбщегоНазначенияИСКлиентСервер.ДатаИзСтрокиUNIX(ЭлементДанных.sysChangedAt);
				
				Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				КонецЕсли;
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ПараметрыЗапроса = ЭлементОчереди.РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Если ПараметрыЗапроса.ОбновитьДатуСинхронизации Тогда
			
			Если ДатаИзменения > ПараметрыЗапроса.ДатаСинхронизации Тогда
				
				РегистрыСведений.СинхронизацияСтатусовДокументовСАТУРН.УстановитьДатуВыполненияСинхронизации(
					ЭлементОчереди.ОрганизацияСАТУРН,
					Перечисления.ВидыНастроекОбменаСАТУРН.ЗагрузкаИмпортируемыхПартий,
					ДатаИзменения);
				
			ИначеЕсли ЗначениеЗаполнено(ПараметрыЗапроса.ДатаОкончания)
				И ПараметрыЗапроса.ДатаОкончания > ПараметрыЗапроса.ДатаСинхронизации Тогда
				
				РегистрыСведений.СинхронизацияСтатусовДокументовСАТУРН.УстановитьДатуВыполненияСинхронизации(
					ЭлементОчереди.ОрганизацияСАТУРН,
					Перечисления.ВидыНастроекОбменаСАТУРН.ЗагрузкаИмпортируемыхПартий,
					ПараметрыЗапроса.ДатаОкончания);
				
			КонецЕсли;
			
		Иначе
			
			// Обновить дату выполнения обмена
			РегистрыСведений.СинхронизацияСтатусовДокументовСАТУРН.УстановитьДатуВыполненияСинхронизации(
				ЭлементОчереди.ОрганизацияСАТУРН,
				Перечисления.ВидыНастроекОбменаСАТУРН.ЗагрузкаИмпортируемыхПартий);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.ИмпортируемаяПартияЗапросКлассификатора;
КонецФункции

#КонецОбласти

#Область ПоискСсылок

Функция ЗагрузитьОбъект(ДанныеИмпортируемойПартии, ПараметрыОбмена, ДокументОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Если ДанныеИмпортируемойПартии = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового = Ложь;
	ИмяТаблицы   = Метаданные.Справочники.ИмпортируемаяПартияСАТУРН.ПолноеИмя();
	
	Если ДокументОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(
				ПараметрыОбмена, ИмяТаблицы, ДанныеИмпортируемойПартии.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникСсылка = ПолучитьСсылку(ИдентификаторОбъекта);
			СправочникОбъект.УстановитьСсылкуНового(СправочникСсылка);
			
			ЗаписьНового = Истина;
			
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
		
	Иначе
		СправочникСсылка = ДокументОбъект.Ссылка;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Идентификатор             = Формат(ДанныеИмпортируемойПартии.Идентификатор, "ЧРГ=; ЧГ=0;");
	СправочникОбъект.ДатаПолучения             = ДанныеИмпортируемойПартии.ДатаПолучения;
	СправочникОбъект.ДатаВвоза                 = ДанныеИмпортируемойПартии.ДатаВвоза;
	СправочникОбъект.Наименование              = ДанныеИмпортируемойПартии.Наименование;
	СправочникОбъект.Статус                    = ДанныеИмпортируемойПартии.Статус;
	СправочникОбъект.НомерИсходногоДокумента   = ДанныеИмпортируемойПартии.НомерИсходногоДокумента;
	СправочникОбъект.ДатаИсходногоДокумента    = ДанныеИмпортируемойПартии.ДатаИсходногоДокумента;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторМестаХранения) Тогда
		
		МестоХранения = Справочники.МестаХраненияСАТУРН.МестоХранения(ДанныеИмпортируемойПартии.ИдентификаторМестаХранения, ПараметрыОбмена);
		СправочникОбъект.МестоХранения = МестоХранения;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторСтраныЭкспортера) Тогда
		
		СтранаЭкспортер = Справочники.КлассификаторСтранСАТУРН.КлассификаторСтраны(ДанныеИмпортируемойПартии.ИдентификаторСтраныЭкспортера, ПараметрыОбмена);
		СправочникОбъект.СтранаЭкспортер = СтранаЭкспортер;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторТаможенногоПункта) Тогда
		
		ТаможенныйПункт = Справочники.КлассификаторТаможенныхПунктовСАТУРН.КлассификаторТаможенногоПункта(ДанныеИмпортируемойПартии.ИдентификаторТаможенногоПункта, ПараметрыОбмена);
		СправочникОбъект.ТаможенныйПункт = ТаможенныйПункт;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторТерриториальногоУправления) Тогда
		
		ТерриториальноеУправление = Справочники.КлассификаторТерриториальныхУправленийСАТУРН.КлассификаторТУ(ДанныеИмпортируемойПартии.ИдентификаторТерриториальногоУправления, ПараметрыОбмена);
		СправочникОбъект.ТерриториальноеУправление = ТерриториальноеУправление;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторТипаТранспортногоСредстваПеревозчика) Тогда
		
		ТипТранспортногоСредства = Справочники.КлассификаторТранспортныхСредствСАТУРН.КлассификаторТС(ДанныеИмпортируемойПартии.ИдентификаторТипаТранспортногоСредстваПеревозчика, ПараметрыОбмена);
		СправочникОбъект.ТипТранспортногоСредства = ТипТранспортногоСредства;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеИмпортируемойПартии.ИдентификаторОрганизации) Тогда
		
		Организация = Справочники.КлассификаторОрганизацийСАТУРН.Организация(ДанныеИмпортируемойПартии.ИдентификаторОрганизации, ПараметрыОбмена);
		СправочникОбъект.ОрганизацияСАТУРН = Организация;
		
	Иначе
		СправочникОбъект.ОрганизацияСАТУРН  = ОрганизацияСАТУРН;
	КонецЕсли;
	
	СправочникОбъект.Товары.Очистить();
	
	Если ДанныеИмпортируемойПартии.Товары.Количество() Тогда
		
		Для Каждого ДанныеИмпорта Из ДанныеИмпортируемойПартии.Товары Цикл
			
			НоваяСтрокаТоваров = СправочникОбъект.Товары.Добавить();
			
			НоваяСтрокаТоваров.ИдентификаторСтроки         = ДанныеИмпорта.ИдентификаторСтроки;
			НоваяСтрокаТоваров.ПАТ                         = Справочники.КлассификаторПАТСАТУРН.ПАТ(ДанныеИмпорта.ИдентификаторПА, ПараметрыОбмена, ОрганизацияСАТУРН);
			НоваяСтрокаТоваров.Количество                  = ДанныеИмпорта.КоличествоУпаковок * ДанныеИмпорта.КоличествоВУпаковкеСАТУРН;
			НоваяСтрокаТоваров.УпаковочнаяЕдиница          = Справочники.КлючиЕдиницИзмеренияСАТУРН.КлючЕдиницы(ДанныеИмпорта.Упаковка.Идентификатор,
				ПараметрыОбмена,
				ДанныеИмпорта.Упаковка);
			НоваяСтрокаТоваров.ТипИзмеряемойВеличиныСАТУРН = ДанныеИмпорта.ТипИзмеряемойВеличиныСАТУРН;
			НоваяСтрокаТоваров.КоличествоВУпаковкеСАТУРН   = ДанныеИмпорта.КоличествоВУпаковкеСАТУРН;
			НоваяСтрокаТоваров.КоличествоУпаковок          = ДанныеИмпорта.КоличествоУпаковок;
			
			НоваяСтрокаТоваров.СрокГодности                = ДанныеИмпорта.СрокГодности;
			НоваяСтрокаТоваров.КодТНВЭД                    = ДанныеИмпорта.КодТНВЭД;
			НоваяСтрокаТоваров.ИдентификаторПартии         = ДанныеИмпорта.ИдентификаторПартии;
			
		КонецЦикла;
		
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеИмпортируемойПартии.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - ДокументСсылка.ИмпортПродукцииСАТУРН - Ссылка на документ итмпорта.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	СообщенияJSON = Новый Массив;
	Идентификатор = "";
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПараметрыОбработкиДокумента")
		И ТипЗнч(ДополнительныеПараметры.ПараметрыОбработкиДокумента) = Тип("Структура")
		И ДополнительныеПараметры.ПараметрыОбработкиДокумента.Свойство("ДополнительныеПараметры") Тогда
		
		ПараметрыОбработки = ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры;
		
		Если ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
			
			Если ПараметрыОбработки.Свойство("Идентификатор") Тогда
				Идентификатор = ПараметрыОбработки.Идентификатор;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ВыполнитеОбмен Тогда
		
		Возврат ЗагрузкаИмпортаПродукцииСАТУРН(ДополнительныеПараметры);
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаИмпортируемойПартииПАТJSON(Идентификатор, ДополнительныеПараметры);
		
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗагрузкаИмпортаПродукцииСАТУРН(ДополнительныеПараметры)
	
	ПараметрыОбработкиДокумента = ДополнительныеПараметры.ПараметрыОбработкиДокумента;
	
	Операция      = ОперацияЗагрузкиКлассификатора();
	СообщенияJSON = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = Операция;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	СообщениеJSON.ОрганизацияСАТУРН   = ПараметрыОбработкиДокумента.ОрганизацияСАТУРН;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ОбновитьДатуСинхронизации", Ложь);
	ПараметрыЗапроса.Вставить("ДатаСинхронизации",         Дата(1, 1, 1));
	ПараметрыЗапроса.Вставить("ДатаОкончания",             Дата(1, 1, 1));
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	Если ТипЗнч(ПараметрыОбработкиДокумента.ДополнительныеПараметры) = Тип("Структура") Тогда 
		
		ПараметрыЗапроса.ДатаСинхронизации = РегистрыСведений.СинхронизацияСтатусовДокументовСАТУРН.ДатаСинхронизации(
			ПараметрыОбработкиДокумента.ОрганизацияСАТУРН,
			Перечисления.ВидыНастроекОбменаСАТУРН.ЗагрузкаИмпортируемыхПартий);
		
		ПараметрыФормирования = Новый Структура;
		ПараметрыФормирования.Вставить("ДатаНачала",          Дата(1, 1, 1));
		ПараметрыФормирования.Вставить("ДатаОкончания",       Дата(1, 1, 1));
		ПараметрыФормирования.Вставить("ИдентификаторОтбора", Неопределено);
		ПараметрыФормирования.Вставить("Статус",              Неопределено);
		ЗаполнитьЗначенияСвойств(ПараметрыФормирования, ПараметрыОбработкиДокумента.ДополнительныеПараметры);
		
		ПараметрыЗапроса.ДатаОкончания = ПараметрыФормирования.ДатаОкончания;
		
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
			АргументыОперации, "type",
			"IM", "=");
		
		Если Не ЗначениеЗаполнено(ПараметрыЗапроса.ДатаСинхронизации)
			И Не ЗначениеЗаполнено(ПараметрыФормирования.ДатаНачала) Тогда
			Сутки = 86400;
			ПараметрыФормирования.ДатаНачала = НачалоДня(ТекущаяДатаСеанса()) - 30 * Сутки;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ПараметрыФормирования.ИдентификаторОтбора)
			И Не ЗначениеЗаполнено(ПараметрыФормирования.Статус)
			И (ЗначениеЗаполнено(ПараметрыЗапроса.ДатаСинхронизации)
				И (Не ЗначениеЗаполнено(ПараметрыФормирования.ДатаНачала)
					Или ПараметрыЗапроса.ДатаСинхронизации > ПараметрыФормирования.ДатаНачала)
				Или Не ЗначениеЗаполнено(ПараметрыЗапроса.ДатаСинхронизации)
					И Не ЗначениеЗаполнено(ПараметрыФормирования.ДатаНачала)
					И Не ЗначениеЗаполнено(ПараметрыФормирования.ДатаОкончания)) Тогда
			ПараметрыЗапроса.ОбновитьДатуСинхронизации = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыФормирования.ДатаНачала) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "sysChangeDat",
				Формат(ОбщегоНазначенияИС.ДатаВСтрокуUNIX(ПараметрыФормирования.ДатаНачала), "ЧГ=0;"), ">=");
		ИначеЕсли Не ЗначениеЗаполнено(ПараметрыФормирования.ИдентификаторОтбора)
			И ЗначениеЗаполнено(ПараметрыЗапроса.ДатаСинхронизации) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "sysChangeDat",
				Формат(ОбщегоНазначенияИС.ДатаВСтрокуUNIX(ПараметрыФормирования.ДатаСинхронизации), "ЧГ=0;"), ">=");
		КонецЕсли;
		Если ЗначениеЗаполнено(ПараметрыФормирования.ДатаОкончания) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "sysChangeDat",
				Формат(ОбщегоНазначенияИС.ДатаВСтрокуUNIX(ПараметрыФормирования.ДатаОкончания), "ЧГ=0;"), "<=");
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыФормирования.ИдентификаторОтбора) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "id",
				ПараметрыФормирования.ИдентификаторОтбора);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыФормирования.Статус) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "lcState",
				ИнтерфейсСАТУРН.СтатусИмпортируемойПродукции(ПараметрыФормирования.Статус));
		ИначеЕсли Не ЗначениеЗаполнено(ПараметрыФормирования.ДатаНачала)
			И Не ЗначениеЗаполнено(ПараметрыФормирования.ИдентификаторОтбора)
			И Не ЗначениеЗаполнено(ПараметрыЗапроса.ДатаСинхронизации) Тогда
			ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(
				АргументыОперации, "lcState",
				ИнтерфейсСАТУРН.СтатусИмпортируемойПродукции(Перечисления.СтатусыОбъектовСАТУРН.Черновик));
		КонецЕсли;
		
	КонецЕсли;
	
	СообщениеJSON.ПараметрыЗапроса = ПараметрыЗапроса;
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗагрузкаИмпортируемойПартииПАТJSON(Идентификатор, ПараметрыОбмена)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации      = СообщениеJSON.АргументыОперации;
	АргументыОперации.pos  = 0;
	АргументыОперации.size = 100;
	
	ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "id", Формат(Идентификатор, "ЧГ=;"));
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли