
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	УправлениеЭлементамиФормы();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	МодификацияКонфигурацииКлиентПереопределяемый.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МодификацияКонфигурацииПереопределяемый.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	МодификацияКонфигурацииПереопределяемый.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	МодификацияКонфигурацииПереопределяемый.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Валюта.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТипЗапасов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыЗапасов.КомиссионныйТовар;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Валюта");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ВладелецТовара.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТипЗапасов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ТипыЗапасов.КомиссионныйТовар);
	СписокЗначений.Добавить(Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ВладелецТовара");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//++ НЕ УТ
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодразделениеПроизводства.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ОблагаетсяАкцизом");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ПодразделениеПроизводства");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	//-- НЕ УТ
	
КонецПроцедуры

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	ЭтоКомиссионныйТовар	= Объект.ТипЗапасов = Перечисления.ТипыЗапасов.КомиссионныйТовар;
	ЭтоОтветхранение		= Объект.ТипЗапасов = Перечисления.ТипыЗапасов.ТоварНаХраненииСПравомПродажи;
	ЭтоПередачаНаКомиссию	= Объект.СпособПередачиТоваров = Перечисления.СпособыПередачиТоваров.ПередачаНаКомиссию;
	
	Элементы.ВладелецТовара.Видимость		= ЭтоКомиссионныйТовар
												Или ЭтоПередачаНаКомиссию
												Или ЭтоОтветхранение;
	Элементы.Контрагент.Видимость			= ЗначениеЗаполнено(Объект.Контрагент)
												Или ЭтоКомиссионныйТовар
												Или ЭтоПередачаНаКомиссию
												Или ЭтоОтветхранение;
	Элементы.Соглашение.Видимость			= ЭтоКомиссионныйТовар
												Или ЭтоОтветхранение;
	Элементы.Договор.Видимость				= ЭтоКомиссионныйТовар
												Или ЭтоОтветхранение;
	Элементы.Валюта.Видимость				= ЭтоКомиссионныйТовар
												Или ЭтоПередачаНаКомиссию
												Или ЭтоОтветхранение;
	Элементы.ВидЦены.Видимость				= ЭтоОтветхранение;
	Элементы.ГруппаПродукции.Видимость		= Ложь;
	Элементы.ГруппаДанныеАкциза.Видимость	= Ложь;
	
	//++ НЕ УТ
	Элементы.ГруппаПродукции.Видимость		= ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции");
	Элементы.ГруппаДанныеАкциза.Видимость	= ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары");
	
	//++ НЕ УТКА
	ТипыЗапасовДавальца = Новый Массив;
	ТипыЗапасовДавальца.Добавить(Перечисления.ТипыЗапасов.ПродукцияДавальца);
	ТипыЗапасовДавальца.Добавить(Перечисления.ТипыЗапасов.ПолуфабрикатДавальца);
	ТипыЗапасовДавальца.Добавить(Перечисления.ТипыЗапасов.МатериалДавальца);
	
	Если ТипыЗапасовДавальца.Найти(Объект.ТипЗапасов) <> Неопределено Тогда
		Элементы.ВладелецТовара.Видимость	= Истина;
		Элементы.Контрагент.Видимость		= Истина;
		Элементы.Договор.Видимость			= Истина;
	КонецЕсли;
	//-- НЕ УТКА
	
	//-- НЕ УТ

КонецПроцедуры

#КонецОбласти

#КонецОбласти
