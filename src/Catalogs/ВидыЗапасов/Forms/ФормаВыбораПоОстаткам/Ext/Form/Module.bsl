
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Отбор") Тогда
		
		ИспользуетсяСкладОтгрузки = Неопределено;
		Если Параметры.Отбор.Свойство("ИспользуетсяСкладОтгрузки", ИспользуетсяСкладОтгрузки) Тогда
			
			Если Не ИспользуетсяСкладОтгрузки Тогда
				Параметры.Отбор.Удалить("СкладОтгрузки");
			КонецЕсли;
			Параметры.Отбор.Удалить("ИспользуетсяСкладОтгрузки");
			
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("СкладОтгрузки") Тогда
			Параметры.Отбор.Склад = Параметры.Отбор.СкладОтгрузки;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(Параметры.Отбор.Склад) Тогда
			Параметры.Отбор.Удалить("Склад");
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("ДокументРеализации") Тогда
			Если Параметры.Отбор.ДокументРеализации = Неопределено Тогда
				Параметры.Отбор.Удалить("ДокументРеализации");
				ПоДокументуРеализации = Ложь;
			Иначе
				ПоДокументуРеализации = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	//++ НЕ УТ
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПодакцизныеТовары") 
		И Параметры.ПодакцизныйТовар Тогда
			СтрокаДанных = Неопределено;
			Параметры.Свойство("ТекущаяСтрока", СтрокаДанных);
		Если ЗначениеЗаполнено(СтрокаДанных) Тогда
			ОблагаетсяАкцизом = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаДанных, "ОблагаетсяАкцизом");
		КонецЕсли;
		Элементы.УстановитьОблагаетсяАкцизом.Видимость = Истина;
		Элементы.УстановитьОблагаетсяАкцизом.Пометка = ОблагаетсяАкцизом;
	КонецЕсли;
	//-- НЕ УТ
	
	Элементы.ТаблицаВидовЗапасовОстаток.Видимость = Не ПоДокументуРеализации;
	Элементы.ТаблицаВидовЗапасовПродано.Видимость = ПоДокументуРеализации;
	
	ЗаполнитьТаблицаВидовЗапасов();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаВидовЗапасов

&НаКлиенте
Процедура ТаблицаВидовЗапасовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		ОповеститьОВыборе(СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор(Команда)
	
	Закрыть(КодВозвратаДиалога.OK);
	
	СтрокаТаблицы = Элементы.ТаблицаВидовЗапасов.ТекущиеДанные;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		ОповеститьОВыборе(СтрокаТаблицы.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьТаблицаВидовЗапасов();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоОстаткам(Команда)
	
	БезОстатков = Не БезОстатков;
	Элементы.УстановитьОтборПоОстаткам.Пометка = БезОстатков;
	ЗаполнитьТаблицаВидовЗапасов();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОблагаетсяАкцизом(Команда)
	
	ОблагаетсяАкцизом = Не ОблагаетсяАкцизом;
	Элементы.УстановитьОблагаетсяАкцизом.Пометка = ОблагаетсяАкцизом;
	ЗаполнитьТаблицаВидовЗапасов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Прочее

&НаСервере
Процедура ЗаполнитьТаблицаВидовЗапасов()
	
	Запрос = Новый Запрос();
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.РеализацияЗапасовДругойОрганизации КАК РеализацияЗапасовДругойОрганизации,
	|	ВидыЗапасов.ПометкаУдаления КАК ПометкаУдаления,
	|	ВидыЗапасов.Предопределенный КАК Предопределенный,
	|	ВидыЗапасов.Наименование КАК Наименование,
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВидыЗапасов.ВладелецТовара КАК ВладелецТовара,
	|	ВидыЗапасов.Соглашение КАК Соглашение,
	|	ВидыЗапасов.Валюта КАК Валюта,
	|	ВидыЗапасов.Организация КАК Организация,
	|	ЕСТЬNULL(ВидыЗапасов.ОблагаетсяАкцизом, ЛОЖЬ) КАК ОблагаетсяАкцизом,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия КАК Серия,
	|	ТоварыОрганизаций.АналитикаУчетаНоменклатуры.МестоХранения КАК Склад
	|ПОМЕСТИТЬ ВТВидыЗапасов
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.ОстаткиИОбороты(&МоментВремени, &МоментВремени, Период, , ) КАК ТоварыОрганизаций
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО (ВидыЗапасов.Ссылка = ТоварыОрганизаций.ВидЗапасов)
	|
	|ГДЕ &УсловияВТВидыЗапасов
	|
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализованныеТовары.ВидЗапасов КАК ВидЗапасов,
	|	РеализованныеТовары.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	РеализованныеТовары.Регистратор КАК ДокументРеализации,
	|	ЕСТЬNULL(РеализованныеТовары.КоличествоРасход, 0) КАК Продано
	|ПОМЕСТИТЬ ВТРеализованныеТовары
	|ИЗ
	|	РегистрНакопления.ТоварыОрганизаций.Обороты(
	|			,
	|			,
	|			Регистратор,
	|			&ПоДокументуРеализации
	|				И (ВидЗапасов, АналитикаУчетаНоменклатуры) В
	|					(ВЫБРАТЬ
	|						Таблица.Ссылка КАК ВидЗапасов,
	|						Таблица.АналитикаУчетаНоменклатуры
	|					ИЗ
	|						ВТВидыЗапасов КАК Таблица)) КАК РеализованныеТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОстатков.ВидЗапасов					КАК ВидЗапасов,
	|	ВременнаяТаблицаОстатков.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|	СУММА(ВременнаяТаблицаОстатков.Остаток)				КАК Остаток
	|ПОМЕСТИТЬ ВТОстатки
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаОстатков.ВидЗапасов					КАК ВидЗапасов,
	|		ТаблицаОстатков.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ТаблицаОстатков.КоличествоОстаток			КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций.Остатки(
	|			,
	|			(ВидЗапасов, АналитикаУчетаНоменклатуры) В
	|				(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|					ТаблицаЗапасов.Ссылка КАК ВидЗапасов,
	|					ТаблицаЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|				ИЗ
	|					ВТВидыЗапасов КАК ТаблицаЗапасов)) КАК ТаблицаОстатков
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОстатков.ВидЗапасов					КАК ВидЗапасов,
	|		ТаблицаОстатков.АналитикаУчетаНоменклатуры	КАК АналитикаУчетаНоменклатуры,
	|		ВЫБОР
	|			КОГДА ТаблицаОстатков.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ТаблицаОстатков.Количество
	|			ИНАЧЕ ТаблицаОстатков.Количество
	|		КОНЕЦ										КАК Остаток
	|	ИЗ
	|		РегистрНакопления.ТоварыОрганизаций КАК ТаблицаОстатков
	|	ГДЕ
	|		ТаблицаОстатков.Активность
	|		И ТаблицаОстатков.Регистратор = &Регистратор
	|		И (ТаблицаОстатков.ВидЗапасов, ТаблицаОстатков.АналитикаУчетаНоменклатуры) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицаЗапасов.Ссылка КАК ВидЗапасов,
	|				ТаблицаЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|			ИЗ
	|				ВТВидыЗапасов КАК ТаблицаЗапасов)
	|	
	|	) КАК ВременнаяТаблицаОстатков
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаОстатков.ВидЗапасов,
	|	ВременнаяТаблицаОстатков.АналитикаУчетаНоменклатуры
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВременнаяТаблицаОстатков.Остаток) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	АналитикаУчетаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.ПометкаУдаления КАК ПометкаУдаления,
	|	ВидыЗапасов.Предопределенный КАК Предопределенный,
	|	ВидыЗапасов.Наименование КАК Наименование,
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВидыЗапасов.ВладелецТовара КАК ВладелецТовара,
	|	ВидыЗапасов.Соглашение КАК Соглашение,
	|	ВидыЗапасов.Валюта КАК Валюта,
	|	ВидыЗапасов.Организация КАК Организация,
	|	ВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ВидыЗапасов.Характеристика КАК Характеристика,
	|	ВидыЗапасов.Серия КАК Серия,
	|	ВидыЗапасов.Склад КАК Склад,
	|	РеализованныеТовары.ДокументРеализации КАК ДокументРеализации,
	|	ЕСТЬNULL(ТаблицаОстатков.Остаток, 0) КАК Остаток,
	|	ЕСТЬNULL(РеализованныеТовары.Продано, 0) КАК Продано,
	|	ЕСТЬNULL(СправочникНоменклатура.ЕдиницаИзмерения, НЕОПРЕДЕЛЕНО) КАК ЕдиницаИзмерения
	|ИЗ
	|	ВТВидыЗапасов КАК ВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ВидыЗапасов.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРеализованныеТовары КАК РеализованныеТовары
	|		ПО ВидыЗапасов.Ссылка = РеализованныеТовары.ВидЗапасов
	|			И ВидыЗапасов.АналитикаУчетаНоменклатуры = РеализованныеТовары.АналитикаУчетаНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОстатки КАК ТаблицаОстатков
	|		ПО ВидыЗапасов.Ссылка = ТаблицаОстатков.ВидЗапасов
	|			И ВидыЗапасов.АналитикаУчетаНоменклатуры = ТаблицаОстатков.АналитикаУчетаНоменклатуры
	|ГДЕ
	|	НЕ ВидыЗапасов.РеализацияЗапасовДругойОрганизации
	|	И НЕ ВидыЗапасов.ПометкаУдаления
	|	И ВидыЗапасов.ОблагаетсяАкцизом = &ОблагаетсяАкцизом
	|	И (&ПоДокументуРеализации
	|		ИЛИ НЕ ТаблицаОстатков.Остаток ЕСТЬ NULL)
	|	И НЕ &БезОстатков
	|	И &УсловияТаблицаВыборки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВидыЗапасов.Ссылка КАК Ссылка,
	|	ВидыЗапасов.ПометкаУдаления КАК ПометкаУдаления,
	|	ВидыЗапасов.Предопределенный КАК Предопределенный,
	|	ВидыЗапасов.Наименование КАК Наименование,
	|	ВидыЗапасов.ТипЗапасов КАК ТипЗапасов,
	|	ВидыЗапасов.НалогообложениеНДС КАК НалогообложениеНДС,
	|	ВидыЗапасов.ВладелецТовара КАК ВладелецТовара,
	|	ВидыЗапасов.Соглашение КАК Соглашение,
	|	ВидыЗапасов.Валюта КАК Валюта,
	|	ВидыЗапасов.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО КАК Серия,
	|	НЕОПРЕДЕЛЕНО КАК Склад,
	|	НЕОПРЕДЕЛЕНО КАК ДокументРеализации,
	|	0 КАК Остаток,
	|	0 КАК Продано,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмерения
	|ИЗ
	|	Справочник.ВидыЗапасов КАК ВидыЗапасов
	|ГДЕ
	|	НЕ ВидыЗапасов.ПометкаУдаления
	|	И ВидыЗапасов.ОблагаетсяАкцизом = &ОблагаетсяАкцизом
	|	И &БезОстатков
	|	И ВидыЗапасов.Организация = &Организация
	|	И НЕ ВидыЗапасов.Ссылка В (ВЫБРАТЬ ВТОстатки.ВидЗапасов ИЗ ВТОстатки)
	|	И &УсловияТаблицаВыборки";
	
	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("Номенклатура",          Справочники.Номенклатура.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Характеристика",        Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Серия",                 Справочники.СерииНоменклатуры.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Склад",                 Справочники.Склады.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("Организация",           Справочники.Организации.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("ПоДокументуРеализации", Ложь);
	ПараметрыЗапроса.Вставить("ДокументРеализации",    Неопределено);
	ПараметрыЗапроса.Вставить("МоментВремени",         Дата(1, 1, 1));
	ПараметрыЗапроса.Вставить("Регистратор",           Неопределено);
	ПараметрыЗапроса.Вставить("ОблагаетсяАкцизом",     ОблагаетсяАкцизом);
	ПараметрыЗапроса.Вставить("БезОстатков",           БезОстатков);
	
	УсловияЗапросаВТВидыЗапасов = Новый Структура();
	УсловияЗапросаВТВидыЗапасов.Вставить("Номенклатура",   "ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Номенклатура = &Номенклатура");
	УсловияЗапросаВТВидыЗапасов.Вставить("Характеристика", "ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Характеристика = &Характеристика");
	УсловияЗапросаВТВидыЗапасов.Вставить("Серия",          "ТоварыОрганизаций.АналитикаУчетаНоменклатуры.Серия = &Серия");
	УсловияЗапросаВТВидыЗапасов.Вставить("Склад",          "ТоварыОрганизаций.АналитикаУчетаНоменклатуры.МестоХранения = &Склад");
	УсловияЗапросаВТВидыЗапасов.Вставить("Организация",    "ВидыЗапасов.Организация = &Организация");
	
	УсловияЗапросаТаблицаВыборки = Новый Структура();
	УсловияЗапросаТаблицаВыборки.Вставить("ДокументРеализации", "РеализованныеТовары.ДокументРеализации = &ДокументРеализации");
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		Если Параметры.Отбор.Свойство(ПараметрЗапроса.Ключ)
			И ЗначениеЗаполнено(Параметры.Отбор[ПараметрЗапроса.Ключ]) Тогда
				
			ПараметрыЗапроса[ПараметрЗапроса.Ключ] = Параметры.Отбор[ПараметрЗапроса.Ключ];
			
		КонецЕсли;
	КонецЦикла;
	
	Если Параметры.Отбор.Свойство("ИспользуетсяСкладОтгрузки")
		И Параметры.Отбор.ИспользуетсяСкладОтгрузки
		И Параметры.Отбор.Свойство("СкладОтгрузки")
		И ЗначениеЗаполнено(Параметры.Отбор.СкладОтгрузки) Тогда
		ПараметрыЗапроса.Склад = Параметры.Отбор.СкладОтгрузки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.ДокументРеализации) Тогда
		ПараметрыЗапроса.МоментВремени = ПараметрыЗапроса.ДокументРеализации.МоментВремени();
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ДокументРеализации")
		И ЗначениеЗаполнено(Параметры.Отбор.ДокументРеализации) Тогда
		ПараметрыЗапроса.ПоДокументуРеализации = Истина;
	КонецЕсли;
	
	МассивУсловийВТВидыЗапасов = Новый Массив();
	МассивУсловийТаблицаВыборка = Новый Массив();
	
	Для Каждого ПараметрЗапроса Из ПараметрыЗапроса Цикл
		
		Если УсловияЗапросаВТВидыЗапасов.Свойство(ПараметрЗапроса.Ключ)
			И ЗначениеЗаполнено(ПараметрЗапроса.Значение) Тогда
			МассивУсловийВТВидыЗапасов.Добавить(УсловияЗапросаВТВидыЗапасов[ПараметрЗапроса.Ключ]);
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		ИначеЕсли УсловияЗапросаТаблицаВыборки.Свойство(ПараметрЗапроса.Ключ)
			И ЗначениеЗаполнено(ПараметрЗапроса.Значение) Тогда
			МассивУсловийТаблицаВыборка.Добавить(УсловияЗапросаТаблицаВыборки[ПараметрЗапроса.Ключ]);
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		Иначе
			Запрос.УстановитьПараметр(ПараметрЗапроса.Ключ, ПараметрЗапроса.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ТекстУсловийВТВидыЗапасов  = СтрСоединить(МассивУсловийВТВидыЗапасов, " И ");
	ТекстУсловийТаблицаВыборки = СтрСоединить(МассивУсловийТаблицаВыборка, " И ");
	
	Если ТекстУсловийВТВидыЗапасов = "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ГДЕ &УсловияВТВидыЗапасов", "");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ГДЕ &УсловияВТВидыЗапасов", "ГДЕ " + ТекстУсловийВТВидыЗапасов);
	КонецЕсли;
	
	Если ТекстУсловийТаблицаВыборки = "" Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловияТаблицаВыборки", "");
	Иначе	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &УсловияТаблицаВыборки", "И " + ТекстУсловийТаблицаВыборки);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ТаблицаВидовЗапасов.Загрузить(Результат);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
