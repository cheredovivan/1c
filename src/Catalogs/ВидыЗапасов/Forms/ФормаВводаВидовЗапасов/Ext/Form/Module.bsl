#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРедактированияВидовЗапасов = Новый ФиксированнаяСтруктура(Параметры.ПараметрыРедактированияВидовЗапасов);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыРедактированияВидовЗапасов);
	
	ТекущаяДата = ?(ПараметрыРедактированияВидовЗапасов.Свойство("ДатаДокумента")
						И ЗначениеЗаполнено(ПараметрыРедактированияВидовЗапасов.ДатаДокумента),
					ПараметрыРедактированияВидовЗапасов.ДатаДокумента,
					ТекущаяДатаСеанса());
	
	ИспользоватьУчетПрослеживаемыхИмпортныхТоваров =
		УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(ТекущаяДата);
	ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров =
		УчетПрослеживаемыхТоваровЛокализация.ДатаНачалаУчетаПрослеживаемыхИмпортныхТоваров();
	
	ЗаполнитьДеревоВидовЗапасов();
	УправлениеЭлементамиФормы();
	УчетПрослеживаемыхТоваровЛокализация.УстановитьВидимостьКоличестваРНПТ(ЭтотОбъект,
																			ТекущаяДата,
																			"ТоварыГруппаКоличествоПоРНПТ");
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыДерева = ДеревоВидовЗапасов.ПолучитьЭлементы();
	Для Каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если ЭлементДерева.ПолучитьРодителя() = Неопределено Тогда
			Элементы.ДеревоВидовЗапасов.Развернуть(ЭлементДерева.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	Если ЗакупкиКлиент.ЭтоУказаниеНомераГТД(Источник) Тогда
		ТекущиеДанные = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			ТекущиеДанные.НомерГТД = НовыйОбъект;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоВидовЗапасов

&НаКлиенте
Процедура ДеревоВидовЗапасовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущиеДанные.Свойство(Поле.Имя) Тогда
		Значение = Неопределено;
		Если Поле.Имя = "НомерСтроки" Тогда
			Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
				Значение = Элемент.ТекущиеДанные.Номенклатура;
			Иначе
				Значение = Элемент.ТекущиеДанные.НоменклатураВидыЗапасов;
			КонецЕсли;
			СтандартнаяОбработка = Ложь;
		ИначеЕсли Элементы.ДеревоВидовЗапасов.ТолькоПросмотр Тогда
			Значение = Элемент.ТекущиеДанные[Поле.Имя];
		КонецЕсли;
		Если ЗначениеЗаполнено(Значение) Тогда
			ПоказатьЗначение(Неопределено, Значение);
		КонецЕсли;
	ИначеЕсли Элементы.ДеревоВидовЗапасов.ТолькоПросмотр Тогда
		Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидЗапасов) Тогда
			ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.ВидЗапасов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	СтрокаРодитель = Элемент.ТекущиеДанные.ПолучитьРодителя();
	Если Не Копирование
	 И СтрокаРодитель <> Неопределено Тогда
	 
		Отказ = Истина;
		Элемент.ТекущаяСтрока = СтрокаРодитель.ПолучитьИдентификатор();
		Элемент.ДобавитьСтроку();
		
	ИначеЕсли Копирование
	 И СтрокаРодитель = Неопределено Тогда
	 
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовПередУдалением(Элемент, Отказ)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если СтрокаТаблицы.ПолучитьРодителя() = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Корневую строку дерева удалять нельзя';
													|en = 'Cannot delete a root line of a tree'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
		СтрокаРодитель.КоличествоВидыЗапасов = 0;
		СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= 0;
		//++ НЕ УТ
		СтрокаРодитель.СуммаАкциза = 0;
		СтрокаРодитель.НалоговаяБазаАкциза = 0;
		//-- НЕ УТ
		Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
			СтрокаРодитель.КоличествоВидыЗапасов = СтрокаРодитель.КоличествоВидыЗапасов + Строка.Количество;
			СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= СтрокаРодитель.КоличествоПоРНПТВидыЗапасов
															+ Строка.КоличествоПоРНПТ;
			//++ НЕ УТ
			СтрокаРодитель.СуммаАкциза = СтрокаРодитель.СуммаАкциза + Строка.СуммаАкциза;
			СтрокаРодитель.НалоговаяБазаАкциза = СтрокаРодитель.НалоговаяБазаАкциза + Строка.НалоговаяБазаАкциза;
			//-- НЕ УТ
		КонецЦикла;
		СтрокаРодитель.КоличествоВидыЗапасов = СтрокаРодитель.КоличествоВидыЗапасов - СтрокаТаблицы.Количество;
		СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= СтрокаРодитель.КоличествоПоРНПТВидыЗапасов
														- СтрокаТаблицы.КоличествоПоРНПТ;
		//++ НЕ УТ
		СтрокаРодитель.СуммаАкциза = СтрокаРодитель.СуммаАкциза - СтрокаТаблицы.СуммаАкциза;
		СтрокаРодитель.НалоговаяБазаАкциза = СтрокаРодитель.НалоговаяБазаАкциза - СтрокаТаблицы.НалоговаяБазаАкциза;
		//-- НЕ УТ
		ЗаполнитьПолеИнформации(СтрокаРодитель);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И Не Копирование Тогда
		
		СтрокаТаблицы = Элемент.ТекущиеДанные;
		СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
		
		СуммаВидыЗапасов = 0;
		СуммаНДСВидыЗапасов = 0;
		КоличествоВидыЗапасов = 0;
		КоличествоПоРНПТВидыЗапасов	= 0;
		СтрокиДерева = СтрокаРодитель.ПолучитьЭлементы();
		Для Каждого Строка Из СтрокиДерева Цикл
			КоличествоВидыЗапасов = КоличествоВидыЗапасов + Строка.Количество;
			КоличествоПоРНПТВидыЗапасов	= КоличествоПоРНПТВидыЗапасов + Строка.КоличествоПоРНПТ;
			СуммаВидыЗапасов = СуммаВидыЗапасов + Строка.СуммаСНДС;
			СуммаНДСВидыЗапасов = СуммаНДСВидыЗапасов + Строка.СуммаНДС;
		КонецЦикла;
		СтрокаРодитель.КоличествоВидыЗапасов = СтрокаРодитель.Количество;
		СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= СтрокаРодитель.КоличествоПоРНПТ;
		ЗаполнитьПолеИнформации(СтрокаРодитель);
		
		СтрокаТаблицы.НоменклатураВидыЗапасов = СтрокаРодитель.Номенклатура;
		СтрокаТаблицы.ХарактеристикаВидыЗапасов = СтрокаРодитель.Характеристика;
		СтрокаТаблицы.НазначениеВидыЗапасов = СтрокаРодитель.Назначение;
		СтрокаТаблицы.СерияВидыЗапасов = СтрокаРодитель.Серия;
		СтрокаТаблицы.СтавкаНДС = СтрокаРодитель.СтавкаНДС;
		СтрокаТаблицы.Количество = СтрокаРодитель.Количество - КоличествоВидыЗапасов;
		СтрокаТаблицы.КоличествоПоРНПТ			= СтрокаРодитель.КоличествоПоРНПТ - КоличествоПоРНПТВидыЗапасов;
		СтрокаТаблицы.СуммаСНДС = СтрокаРодитель.СуммаСНДС - СуммаВидыЗапасов;
		СтрокаТаблицы.СуммаНДС = СтрокаРодитель.СуммаНДС - СуммаНДСВидыЗапасов;
		
		СтрокаТаблицы.ДокументРеализации = СтрокаРодитель.ДокументРеализации;
		Если ЗначениеЗаполнено(СтрокаРодитель.ДокументРеализации) Тогда
			СтрокаТаблицы.СкладОтгрузки = Склад;
		КонецЕсли;
		
		СтрокаТаблицы.ВедетсяУчетПоГТД = СтрокаРодитель.ВедетсяУчетПоГТД;
		СтрокаТаблицы.ТипНоменклатуры = СтрокаРодитель.ТипНоменклатуры;
		СтрокаТаблицы.ПрослеживаемыйТовар = СтрокаРодитель.ПрослеживаемыйТовар;
		//++ НЕ УТ
		СтрокаТаблицы.ПодакцизныйТовар = СтрокаРодитель.ПодакцизныйТовар;
		СтрокаТаблицы.ОблагаетсяАкцизом = СтрокаРодитель.ОблагаетсяАкцизом;
		СтрокаТаблицы.ЕдиницаИзмеренияАкциза = СтрокаРодитель.ЕдиницаИзмеренияАкциза;
		СтрокаТаблицы.КоэффициентПересчетаАкциза = СтрокаРодитель.КоэффициентПересчетаАкциза;
		СтрокаТаблицы.КатегорияПодакцизногоТовара = СтрокаРодитель.КатегорияПодакцизногоТовара;
		СтрокаТаблицы.СтавкаАкциза = СтрокаРодитель.СтавкаАкциза;
		СтрокаТаблицы.СтавкаАкцизаРегл = СтрокаРодитель.СтавкаАкцизаРегл;
		СтрокаТаблицы.ВалютаАкциза = СтрокаРодитель.ВалютаАкциза;
		УстановитьДоступностьОписанияПодакцизнойКатегорииТовара(СтрокаРодитель.ПодакцизныйТовар);
		//-- НЕ УТ
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовПриАктивизацииСтроки(Элемент)
	
	//++ НЕ УТ
	СтрокаТаблицы = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	Если ОтображатьСуммуАкциза И СтрокаТаблицы <> Неопределено Тогда
		УстановитьДоступностьОписанияПодакцизнойКатегорииТовара(СтрокаТаблицы.ПодакцизныйТовар)
	КонецЕсли;
	//-- НЕ УТ
	
	Возврат; // Чтобы в УТ не был пустым обработчик
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовКоличествоПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
	
	Если СтрокаРодитель.Количество <> 0 Тогда
		СтрокаТаблицы.СуммаСНДС = Окр(СтрокаТаблицы.Количество * СтрокаРодитель.СуммаСНДС / СтрокаРодитель.Количество, 2, РежимОкругления.Окр15как20);
	Иначе
		СтрокаТаблицы.СуммаСНДС = 0;
	КонецЕсли;
	
	Если СтрокаРодитель.СуммаСНДС <> 0 Тогда
		СтрокаТаблицы.СуммаНДС = Окр(СтрокаТаблицы.СуммаСНДС * СтрокаРодитель.СуммаНДС / СтрокаРодитель.СуммаСНДС, 2, РежимОкругления.Окр15как20);
	Иначе
		СтрокаТаблицы.СуммаНДС = 0;
	КонецЕсли;
	
	//++ НЕ УТ
	Если СтрокаТаблицы.ОблагаетсяАкцизом Тогда
		СтрокаТаблицы.НалоговаяБазаАкциза =
			Окр(СтрокаТаблицы.Количество * СтрокаТаблицы.КоэффициентПересчетаАкциза, 2, РежимОкругления.Окр15как20);
		СтрокаТаблицы.СуммаАкциза =
			Окр(СтрокаТаблицы.НалоговаяБазаАкциза * СтрокаТаблицы.СтавкаАкцизаРегл, 2, РежимОкругления.Окр15как20);
	Иначе
		СтрокаТаблицы.НалоговаяБазаАкциза = 0;
		СтрокаТаблицы.СуммаАкциза = 0;
	КонецЕсли;
	//-- НЕ УТ
	
	СтрокаРодитель.КоличествоВидыЗапасов = 0;
	СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= 0;
	//++ НЕ УТ
	СтрокаРодитель.СуммаАкциза = 0;
	СтрокаРодитель.НалоговаяБазаАкциза = 0;
	//-- НЕ УТ
	Для Каждого Строка Из СтрокаРодитель.ПолучитьЭлементы() Цикл
		Строка.КоличествоПоРНПТ = Строка.Количество * Строка.КоэффициентРНПТ;
		СтрокаРодитель.КоличествоВидыЗапасов = СтрокаРодитель.КоличествоВидыЗапасов + Строка.Количество;
		СтрокаРодитель.КоличествоПоРНПТВидыЗапасов	= СтрокаРодитель.КоличествоПоРНПТВидыЗапасов
														+ Строка.КоличествоПоРНПТ;
		//++ НЕ УТ
		СтрокаРодитель.СуммаАкциза = СтрокаРодитель.СуммаАкциза + Строка.СуммаАкциза;
		СтрокаРодитель.НалоговаяБазаАкциза = СтрокаРодитель.НалоговаяБазаАкциза + Строка.НалоговаяБазаАкциза;
		//-- НЕ УТ
	КонецЦикла;
	ЗаполнитьПолеИнформации(СтрокаРодитель);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерГТДПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	Если ЗначениеЗаполнено(СтрокаТаблицы.НомерГТД) Тогда
		СтрокаТаблицы.ПроверитьНомерГТД = ПроверитьДанныеПрослеживаемостиНомераГТДНаСервере(СтрокаТаблицы.НомерГТД);
	Иначе
		СтрокаТаблицы.ПроверитьНомерГТД = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерГТДСоздание(Элемент, СтандартнаяОбработка)
	
	Если Не ИспользоватьУчетПрослеживаемыхИмпортныхТоваров Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	
	ТипНомераГТД = ?(ТекущиеДанные.ПрослеживаемыйТовар,
					ПредопределенноеЗначение("Перечисление.ТипыНомеровГТД.НомерРНПТ"),
					ПредопределенноеЗначение("Перечисление.ТипыНомеровГТД.НомерГТД"));
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Код",				Элемент.ТекстРедактирования);
	ПараметрыЗаполнения.Вставить("Номенклатура",	ТекущиеДанные.НоменклатураВидыЗапасов);
	ПараметрыЗаполнения.Вставить("ТипНомераГТД",	ТипНомераГТД);
	
	ПараметрыФормы		= Новый Структура("ЗначенияЗаполнения", ПараметрыЗаполнения);
	ОповещениеЗакрытия	= Новый ОписаниеОповещения("НомерГТДСозданиеЗавершение", ЭтотОбъект, ТекущиеДанные.НомерГТД);
	
	ОткрытьФорму("Справочник.НомераГТД.Форма.ФормаЭлемента",
				ПараметрыФормы,
				ЭтотОбъект,
				УникальныйИдентификатор,
				,
				,
				ОповещениеЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоВидовЗапасовСуммаАкцизаПриИзменении(Элемент)
	
	//++ НЕ УТ
	СтрокаТаблицы = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
	ЗаполнитьДанныеПоАкцизамВГруппе(СтрокаРодитель);
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЗапасовПриИзменении(Элемент)
	
	//++ НЕ УТ
	Если ОтображатьВидЗапасовПолучателя Тогда
		Возврат;
	КонецЕсли;
	
	ПриИзмененииВидаЗапасов(Элемент);
	//-- НЕ УТ
	Возврат;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереключитьРедактированиеВидовЗапасов(Команда)
	
	НастроитьРедактированиеВидовЗапасов(Не ВидыЗапасовУказаныВручную);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПоместитьВидыЗапасовВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
	
	Результат = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыРедактированияВидовЗапасов);
	Результат.ВидыЗапасовУказаныВручную = ВидыЗапасовУказаныВручную;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьКомплектыРНПТ(Команда)
	
	Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка = Не Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка;
	ОтобратьКомплектыРНПТ = Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка;
	
	Если Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка Тогда
		
		ИдентификаторСтроки = 0;
		ОбщегоНазначенияКлиентСервер.ПолучитьИдентификаторСтрокиДереваПоЗначениюПоля(
			"ПроверитьНомерГТД",ИдентификаторСтроки, ДеревоВидовЗапасов.ПолучитьЭлементы(), 1, Ложь);
			
		Элементы.ДеревоВидовЗапасов.ТекущаяСтрока = ИдентификаторСтроки;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьВидыЗапасов(Команда)
	
	ОчиститьСообщения();
	
	ЗаполнитьВидыЗапасовНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ReportGroup1BackColor);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасов.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.НоменклатураВидыЗапасов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ВидЗапасов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныеДанныеЦвет);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовКоличество.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовИнформация.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.Количество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.КоличествоВидыЗапасов");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовСуммаСНДС.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовИнформация.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.СуммаСНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.СуммаСНДСВидыЗапасов");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовСуммаНДС.Имя);

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовИнформация.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.СуммаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.СуммаНДСВидыЗапасов");

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Истина);

	//

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НомерГТД.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.НомерГТД");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ВедетсяУчетПоГТД");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.НоменклатураВидыЗапасов");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<ГТД не используются>';
																|en = '<CCD numbers are not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Для Каждого КорневойЭлемент Из Элементы.ДеревоВидовЗапасов.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(КорневойЭлемент) = Тип("ПолеФормы") Тогда
			ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
			ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(КорневойЭлемент.Имя);
		ИначеЕсли ТипЗнч(КорневойЭлемент) = Тип("ГруппаФормы") Тогда
			Для Каждого ПодчиненныйЭлемент Из КорневойЭлемент.ПодчиненныеЭлементы Цикл
				Если ТипЗнч(ПодчиненныйЭлемент) = Тип("ПолеФормы") Тогда
					ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
					ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ПодчиненныйЭлемент.Имя);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ПроверитьНомерГТД");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтобратьКомплектыРНПТ");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//++ НЕ УТ
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовСуммаАкциза.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ПодакцизныйТовар");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Неподакцизный>';
																|en = '<Non-excisable>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоВидовЗапасовСуммаАкциза.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ПодакцизныйТовар");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоВидовЗапасов.ОблагаетсяАкцизом");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Непроизведенный>';
																|en = '<Unproduced>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	//-- НЕ УТ

КонецПроцедуры

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура НастроитьРедактированиеВидовЗапасов(РедактированиеВключено, ЗаполнятьДеревоВидовЗапасов = Истина)
	
	ВидыЗапасовУказаныВручную = РедактированиеВключено;
	Элементы.ПереключитьРедактированиеВидовЗапасов.Пометка = ВидыЗапасовУказаныВручную;
	Элементы.ЗаполнитьВидыЗапасов.Доступность = РедактированиеВключено;
	Элементы.ДеревоВидовЗапасов.ТолькоПросмотр = Не РедактированиеВключено;
	Элементы.ГруппаВидыЗапасовУказаныВручную.Видимость = РедактированиеВключено;
	
	Если ЗаполнятьДеревоВидовЗапасов Тогда
		ЗаполнитьДеревоВидовЗапасов();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ПереключитьРедактированиеВидовЗапасов.Видимость = РедактироватьВидыЗапасов;
	Элементы.ДеревоВидовЗапасов.ТолькоПросмотр = Не РедактироватьВидыЗапасов;
	Элементы.ГруппаДерево.Видимость = РедактироватьВидыЗапасов;
	Элементы.ГруппаФормы.Видимость = Не РедактироватьВидыЗапасов;
	Элементы.ПеренестиВДокумент.Видимость = РедактироватьВидыЗапасов;
	Элементы.ЗаполнитьВидыЗапасов.Видимость = РедактироватьВидыЗапасов;
	
	Элементы.КартинкаВнимание.Видимость = ДокументМодифицирован;
	Элементы.НадписьДокументМодифицирован.Видимость = ДокументМодифицирован;
	
	Элементы.ДеревоВидовЗапасовДокументРеализации.Видимость = ПараметрыРедактированияВидовЗапасов.ОтображатьДокументыРеализации;
	
	Элементы.ГруппаДанныеПодакцизногоТовара.Видимость = ОтображатьСуммуАкциза;
	
	Если ПараметрыРедактированияВидовЗапасов.ОтображатьДокументыРеализации Тогда
		Элементы.ВидЗапасов.ФормаВыбора = "Справочник.ВидыЗапасов.Форма.ФормаВыбора";
	Иначе
		Элементы.ВидЗапасов.ФормаВыбора = "Справочник.ВидыЗапасов.Форма.ФормаВыбораПоОстаткам";
	КонецЕсли;	
	
	Элементы.ДеревоВидовЗапасовСуммаСНДС.Заголовок = 
		?(ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"), НСтр("ru = 'Сумма с НДС';
																	|en = 'Amount including VAT'"), НСтр("ru = 'Сумма';
																								|en = 'Amount'"));
	
	Элементы.ГруппаВидыЗапасовУказаныВручную.Видимость = ВидыЗапасовУказаныВручную;
	
	Если РедактироватьВидыЗапасов Тогда
		НастроитьРедактированиеВидовЗапасов(ВидыЗапасовУказаныВручную, Ложь);
	КонецЕсли;
	
	Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Видимость = Ложь;
	//++ Локализация
	Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Видимость = ИспользоватьУчетПрослеживаемыхИмпортныхТоваров;
	//-- Локализация
	
	Элементы.ДеревоВидовЗапасовСуммаАкциза.Видимость = ОтображатьСуммуАкциза;
	
	Элементы.ВидЗапасов.Заголовок = ?(ОтображатьВидЗапасовПолучателя,
										НСтр("ru = 'Вид запасов отправителя';
											|en = 'Sender inventory owner attributes'"),
										НСтр("ru = 'Вид запасов';
											|en = 'Inventory owner attributes'"));
	Элементы.ВидЗапасовПолучателя.Видимость = ОтображатьВидЗапасовПолучателя;
	Элементы.ДеревоВидовЗапасовВидЗапасовПолучателяОрганизация.Видимость = ОтображатьВидЗапасовПолучателя;
	
	УчетПрослеживаемыхТоваровЛокализация.УстановитьЗаголовокНомерГТД(Элементы, "НомерГТД");
	
	//++ НЕ УТ
	ВалютаРегламентированногоУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	Если ЗначениеЗаполнено(ВалютаРегламентированногоУчета) Тогда
		НаименованиеВалютыРеглУчета =
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВалютаРегламентированногоУчета, "Наименование");
		ТекстЗаголовка = СтрШаблон(НСтр("ru = 'Сумма акциза (%1)';
										|en = 'Excise amount (%1)'"), НаименованиеВалютыРеглУчета);
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Сумма акциза';
								|en = 'Excise amount'");
	КонецЕсли;
	Элементы.ДеревоВидовЗапасовСуммаАкциза.Заголовок = ТекстЗаголовка;
	//-- НЕ УТ
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПоместитьВидыЗапасовВХранилище() 
	
	ТаблицаВидыЗапасов = ПолучитьИзВременногоХранилища(АдресВидовЗапасовВХранилище).СкопироватьКолонки();
	
	ЕстьКоличествоПоРНПТ = (ТаблицаВидыЗапасов.Колонки.Найти("КоличествоПоРНПТ") <> Неопределено);
	ЕстьСуммаСНДС = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаСНДС") <> Неопределено);
	ЕстьСуммаНДС = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаНДС") <> Неопределено);
	ЕстьСтавкаНДС = (ТаблицаВидыЗапасов.Колонки.Найти("СтавкаНДС") <> Неопределено);
	ЕстьСкладОтгрузки = (ТаблицаВидыЗапасов.Колонки.Найти("СкладОтгрузки") <> Неопределено);
	ЕстьДокументРеализации = (ТаблицаВидыЗапасов.Колонки.Найти("ДокументРеализации") <> Неопределено);
	ЕстьСпособОпределенияСебестоимости = (ТаблицаВидыЗапасов.Колонки.Найти("СпособОпределенияСебестоимости") <> Неопределено);
	//++ НЕ УТ
	ЕстьСуммаАкциза = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаАкциза") <> Неопределено);
	//-- НЕ УТ
	ЕстьВидЗапасовПолучателя = (ТаблицаВидыЗапасов.Колонки.Найти("ВидЗапасовПолучателя") <> Неопределено);
	
	Дерево = РеквизитФормыВЗначение("ДеревоВидовЗапасов");
	Для Каждого СтрокаНоменклатуры Из Дерево.Строки Цикл
		
		Для Каждого Строка Из СтрокаНоменклатуры.Строки Цикл
		
			НоваяСтрока = ТаблицаВидыЗапасов.Добавить();
			НоваяСтрока.АналитикаУчетаНоменклатуры = СтрокаНоменклатуры.АналитикаУчетаНоменклатуры;
			НоваяСтрока.ВидЗапасов = Строка.ВидЗапасов;
			НоваяСтрока.НомерГТД = Строка.НомерГТД;
			НоваяСтрока.Количество = Строка.Количество;
			Если ЕстьКоличествоПоРНПТ Тогда
				НоваяСтрока.КоличествоПоРНПТ = Строка.КоличествоПоРНПТ;
			КонецЕсли;
			Если ЕстьСуммаСНДС Тогда
				НоваяСтрока.СуммаСНДС = Строка.СуммаСНДС;
			КонецЕсли;
			Если ЕстьСуммаНДС Тогда
				НоваяСтрока.СуммаНДС = Строка.СуммаНДС;
			КонецЕсли;
			Если ЕстьСтавкаНДС Тогда
				НоваяСтрока.СтавкаНДС = Строка.СтавкаНДС;
			КонецЕсли;
			Если ЕстьСкладОтгрузки Тогда
				НоваяСтрока.СкладОтгрузки = Строка.СкладОтгрузки;
			КонецЕсли;
			Если ЕстьДокументРеализации Тогда
				НоваяСтрока.ДокументРеализации = Строка.ДокументРеализации;
			КонецЕсли;
			Если ЕстьСпособОпределенияСебестоимости Тогда
				НоваяСтрока.СпособОпределенияСебестоимости = Строка.СпособОпределенияСебестоимости;
			КонецЕсли;
			//++ НЕ УТ
			Если ЕстьСуммаАкциза Тогда
				НоваяСтрока.СуммаАкциза = Строка.СуммаАкциза;
			КонецЕсли;
			//-- НЕ УТ
			Если ЕстьВидЗапасовПолучателя Тогда
				НоваяСтрока.ВидЗапасовПолучателя = Строка.ВидЗапасовПолучателя;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

	ПоместитьВоВременноеХранилище(ТаблицаВидыЗапасов, АдресВидовЗапасовВХранилище);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоВидовЗапасов(ТаблицаВидыЗапасов = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ИсходнаяТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ИсходнаяТаблицаДокумента.Склад КАК Склад,
	|	ИсходнаяТаблицаДокумента.Подразделение КАК Подразделение,
	|	ИсходнаяТаблицаДокумента.ДокументРеализации КАК ДокументРеализации,
	|	ИсходнаяТаблицаДокумента.СпособОпределенияСебестоимости КАК СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ИсходнаяТаблицаДокумента.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ИсходнаяТаблицаДокумента.СтатусУказанияСерий В (14, 18)
	|			ТОГДА ИсходнаяТаблицаДокумента.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ИсходнаяТаблицаДокумента.Назначение КАК Назначение,
	|	ИсходнаяТаблицаДокумента.Количество КАК Количество,
	|	ИсходнаяТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	(ВЫБОР
	|		КОГДА ИсходнаяТаблицаДокумента.Количество < 0 ТОГДА -1
	|		ИНАЧЕ 1 КОНЕЦ) КАК Знак,
	|	ВЫБОР КОГДА ИсходнаяТаблицаДокумента.СуммаПродажи <> 0 ТОГДА
	|		ИсходнаяТаблицаДокумента.СуммаПродажи
	|	ИНАЧЕ
	|		ИсходнаяТаблицаДокумента.Сумма
	|	КОНЕЦ КАК Сумма,
	|	ВЫБОР КОГДА ИсходнаяТаблицаДокумента.СуммаПродажи <> 0 ТОГДА
	|		0
	|	ИНАЧЕ
	|		ИсходнаяТаблицаДокумента.СуммаНДС
	|	КОНЕЦ КАК СуммаНДС,
	|	ИсходнаяТаблицаДокумента.СтавкаНДС КАК СтавкаНДС
	|	
	|ПОМЕСТИТЬ ИсходнаяТаблицаДокумента
	|ИЗ
	|	&ИсходнаяТаблицаДокумента КАК ИсходнаяТаблицаДокумента
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ИсходнаяТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры) КАК АналитикаУчетаНоменклатуры,
	|	ИсходнаяТаблицаДокумента.Склад КАК МестоХранения,
	|	ВЫБОР
	|		КОГДА &ОтображатьДокументыРеализации
	|		ТОГДА ИсходнаяТаблицаДокумента.ДокументРеализации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ ДокументРеализации,
	|	ИсходнаяТаблицаДокумента.СпособОпределенияСебестоимости КАК СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	СправочникНоменклатура.ВестиУчетПоГТД КАК ВестиУчетПоГТД,
	|	ИсходнаяТаблицаДокумента.Характеристика КАК Характеристика,
	|	ИсходнаяТаблицаДокумента.Серия КАК Серия,
	|	ИсходнаяТаблицаДокумента.Знак КАК Знак,
	//++ НЕ УТ
	|	&Страна КАК Страна,
	//-- НЕ УТ
	|	СУММА(ИсходнаяТаблицаДокумента.Количество) КАК Количество,
	|	СУММА(ИсходнаяТаблицаДокумента.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	СУММА(
	|		ИсходнаяТаблицаДокумента.Сумма
	|		+ &ЦенаНеВключаетНДС * ИсходнаяТаблицаДокумента.СуммаНДС
	|	) КАК СуммаСНДС,
	|	МАКСИМУМ(ИсходнаяТаблицаДокумента.СтавкаНДС) КАК СтавкаНДС,
	|	СУММА(ИсходнаяТаблицаДокумента.СуммаНДС) КАК СуммаНДС
	|	
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ИсходнаяТаблицаДокумента КАК ИсходнаяТаблицаДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО
	|		ИсходнаяТаблицаДокумента.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ИсходнаяТаблицаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И ИсходнаяТаблицаДокумента.Характеристика = Аналитика.Характеристика
	|		И ИсходнаяТаблицаДокумента.Серия = Аналитика.Серия
	|		И ИсходнаяТаблицаДокумента.Склад = Аналитика.МестоХранения
	|		И ИсходнаяТаблицаДокумента.Назначение = Аналитика.Назначение
	|		И ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	//++ НЕ УТ 
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|ГДЕ
	|	СправочникНоменклатура.ТипНоменклатуры В (
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры),
	|	ИсходнаяТаблицаДокумента.Склад,
	|	ИсходнаяТаблицаДокумента.ДокументРеализации,
	|	ИсходнаяТаблицаДокумента.СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаДокумента.Номенклатура,
	|	ИсходнаяТаблицаДокумента.Характеристика,
	|	ИсходнаяТаблицаДокумента.Серия,
	|	ИсходнаяТаблицаДокумента.Знак,
	|	СправочникНоменклатура.ВестиУчетПоГТД
	|
	// Прослеживаемые работы
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ИсходнаяТаблицаДокумента.НомерСтроки),
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры),
	|	ВЫБОР
	|		КОГДА &ПодразделениеВШапке
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ИсходнаяТаблицаДокумента.Подразделение
	|	КОНЕЦ КАК МестоХранения,
	|	ВЫБОР
	|		КОГДА &ОтображатьДокументыРеализации
	|		ТОГДА ИсходнаяТаблицаДокумента.ДокументРеализации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ИсходнаяТаблицаДокумента.СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаДокумента.Номенклатура,
	|	СправочникНоменклатура.ВестиУчетПоГТД,
	|	ИсходнаяТаблицаДокумента.Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ИсходнаяТаблицаДокумента.Знак,
	//++ НЕ УТ
	|	&Страна КАК Страна,
	//-- НЕ УТ
	|	СУММА(ИсходнаяТаблицаДокумента.Количество),
	|	СУММА(ИсходнаяТаблицаДокумента.КоличествоПоРНПТ),
	|	СУММА(
	|		ИсходнаяТаблицаДокумента.Сумма
	|		+ &ЦенаНеВключаетНДС * ИсходнаяТаблицаДокумента.СуммаНДС
	|	),
	|	МАКСИМУМ(ИсходнаяТаблицаДокумента.СтавкаНДС),
	|	СУММА(ИсходнаяТаблицаДокумента.СуммаНДС)
	|ИЗ
	|	ИсходнаяТаблицаДокумента КАК ИсходнаяТаблицаДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО
	|		ИсходнаяТаблицаДокумента.Номенклатура = СправочникНоменклатура.Ссылка
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		РегистрСведений.АналитикаУчетаНоменклатуры КАК Аналитика
	|	ПО
	|		ИсходнаяТаблицаДокумента.Номенклатура = Аналитика.Номенклатура
	|		И ИсходнаяТаблицаДокумента.Характеристика = Аналитика.Характеристика
	|		И ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка) = Аналитика.Серия
	|		И ИсходнаяТаблицаДокумента.Подразделение = Аналитика.МестоХранения
	|		И ИсходнаяТаблицаДокумента.Назначение = Аналитика.Назначение
	|		И ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры = ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаНоменклатуры.ПустаяСсылка)
	//++ НЕ УТ
	|		И ЗНАЧЕНИЕ(Справочник.СтатьиКалькуляции.ПустаяСсылка) = Аналитика.СтатьяКалькуляции
	//-- НЕ УТ
	|ГДЕ
	|	&ИспользоватьУчетПрослеживаемыхИмпортныхТоваров
	|	И СправочникНоменклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
	|	И СправочникНоменклатура.ПрослеживаемыйТовар
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(Аналитика.КлючАналитики, ИсходнаяТаблицаДокумента.АналитикаУчетаНоменклатуры),
	|	ВЫБОР
	|		КОГДА &ПодразделениеВШапке
	|			ТОГДА &Подразделение
	|		ИНАЧЕ ИсходнаяТаблицаДокумента.Подразделение
	|	КОНЕЦ,
	|	ИсходнаяТаблицаДокумента.ДокументРеализации,
	|	ИсходнаяТаблицаДокумента.СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаДокумента.Номенклатура,
	|	ИсходнаяТаблицаДокумента.Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка),
	|	ИсходнаяТаблицаДокумента.Знак,
	|	СправочникНоменклатура.ВестиУчетПоГТД
	//++ НЕ УТ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Страна
	//-- НЕ УТ
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	ИсходнаяТаблицаВидыЗапасов.ВидЗапасов,
	|	ИсходнаяТаблицаВидыЗапасов.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	ИсходнаяТаблицаВидыЗапасов.НомерГТД,
	|	ИсходнаяТаблицаВидыЗапасов.СтавкаНДС,
	|	ИсходнаяТаблицаВидыЗапасов.Количество,
	|	ИсходнаяТаблицаВидыЗапасов.КоличествоПоРНПТ,
	|	ИсходнаяТаблицаВидыЗапасов.СуммаСНДС,
	|	ИсходнаяТаблицаВидыЗапасов.СуммаНДС,
	//++ НЕ УТ
	|	ИсходнаяТаблицаВидыЗапасов.СуммаАкциза,
	//-- НЕ УТ
	|	(ВЫБОР
	|		КОГДА ИсходнаяТаблицаВидыЗапасов.Количество < 0 ТОГДА -1
	|		ИНАЧЕ 1 КОНЕЦ) КАК Знак,
	|	ВЫБОР 
	|		КОГДА &ОтображатьДокументыРеализации
	|		ТОГДА ИсходнаяТаблицаВидыЗапасов.ДокументРеализации
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ ДокументРеализации,
	|	ИсходнаяТаблицаВидыЗапасов.СпособОпределенияСебестоимости,
	|	ИсходнаяТаблицаВидыЗапасов.СкладОтгрузки
	|
	|ПОМЕСТИТЬ ВтТаблицаВидыЗапасов
	|ИЗ
	|	&ИсходнаяТаблицаВидыЗапасов КАК ИсходнаяТаблицаВидыЗапасов
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	Ключи.Назначение КАК Назначение,
	|	Ключи.МестоХранения КАК МестоХранения,
	|	Ключи.Номенклатура,
	|	Ключи.Характеристика,
	|	Ключи.Серия,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.СтавкаНДС,
	|	ВидыЗапасов.Знак,
	|	СУММА(ВидыЗапасов.Количество),
	|	СУММА(ВидыЗапасов.КоличествоПоРНПТ) КАК КоличествоПоРНПТ,
	|	СУММА(ВидыЗапасов.СуммаСНДС),
	|	СУММА(ВидыЗапасов.СуммаНДС),
	//++ НЕ УТ
	|	СУММА(ВидыЗапасов.СуммаАкциза) КАК СуммаАкциза,
	//-- НЕ УТ
	|	ВидыЗапасов.ДокументРеализации,
	|	ВидыЗапасов.СпособОпределенияСебестоимости,
	|	ВидыЗапасов.СкладОтгрузки
	|
	|ПОМЕСТИТЬ ТаблицаВидыЗапасов
	|ИЗ
	|	ВтТаблицаВидыЗапасов КАК ВидыЗапасов
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.КлючиАналитикиУчетаНоменклатуры КАК Ключи
	|	ПО
	|		ВидыЗапасов.АналитикаУчетаНоменклатуры = Ключи.Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ВидыЗапасов.АналитикаУчетаНоменклатуры,
	|	Ключи.Назначение,
	|	Ключи.МестоХранения,
	|	Ключи.Номенклатура,
	|	Ключи.Характеристика,
	|	Ключи.Серия,
	|	ВидыЗапасов.ВидЗапасов,
	|	ВидыЗапасов.ВидЗапасовПолучателя,
	|	ВидыЗапасов.НомерГТД,
	|	ВидыЗапасов.СтавкаНДС,
	|	ВидыЗапасов.СпособОпределенияСебестоимости,
	|	ВидыЗапасов.ДокументРеализации,
	|	ВидыЗапасов.СкладОтгрузки,
	|	ВидыЗапасов.Знак
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	//++ НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КатегорииПодакцизныхТоваров.Номенклатура КАК Номенклатура,
	|	КатегорииПодакцизныхТоваров.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	КатегорииПодакцизныхТоваров.КоэффициентПересчета КАК КоэффициентПересчета,
	|	КатегорииПодакцизныхТоваров.ПодакцизныйТовар КАК ПодакцизныйТовар
	|ПОМЕСТИТЬ ТаблицаПодакцизныхТоваров
	|ИЗ
	|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&ДатаДокумента, (Номенклатура, Страна) В
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|			ТаблицаДокумента.Страна КАК Страна
	|		ИЗ
	|			ТаблицаДокумента КАК ТаблицаДокумента)) КАК КатегорииПодакцизныхТоваров
	|
	|ГДЕ
	|	КатегорииПодакцизныхТоваров.ПодакцизныйТовар
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КатегорияПодакцизногоТовара
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПодакцизныеТовары.Номенклатура КАК Номенклатура,
	|	ПодакцизныеТовары.КоэффициентПересчета КАК КоэффициентПересчета,
	|	ПодакцизныеТовары.ПодакцизныйТовар КАК ПодакцизныйТовар,
	|	КатегорииПодакцизныхТоваров.Валюта КАК ВалютаАкциза,
	|	КатегорииПодакцизныхТоваров.ЕдиницаИзмерения КАК ЕдиницаИзмеренияАкциза,
	|	КатегорииПодакцизныхТоваров.Ссылка КАК КатегорияПодакцизногоТовара
	|ПОМЕСТИТЬ ТаблицаАкцизов
	|ИЗ
	|	Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПодакцизныхТоваров КАК ПодакцизныеТовары
	|	ПО КатегорииПодакцизныхТоваров.Ссылка = ПодакцизныеТовары.КатегорияПодакцизногоТовара
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтавкиКатегорийПодакцизныхТоваровСрезПоследних.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	СтавкиКатегорийПодакцизныхТоваровСрезПоследних.Ставка КАК СтавкаАкциза
	|ПОМЕСТИТЬ ТаблицаСтавокАкцизов
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&ДатаДокумента, КатегорияПодакцизногоТовара В
	|		(ВЫБРАТЬ
	|			ТаблицаПодакцизныхТоваров.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара
	|		ИЗ
	|			ТаблицаПодакцизныхТоваров КАК ТаблицаПодакцизныхТоваров)) КАК СтавкиКатегорийПодакцизныхТоваровСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсВалюты.Валюта КАК Валюта,
	|	КурсВалюты.КурсЧислитель / КурсВалюты.КурсЗнаменатель КАК КоэффициентПересчета
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ДатаДокумента, Валюта В
	|		(ВЫБРАТЬ
	|			ТаблицаАкцизов.ВалютаАкциза КАК ВалютаАкциза
	|		ИЗ
	|			ТаблицаАкцизов)
	|	И БазоваяВалюта = &БазоваяВалюта) КАК КурсВалюты
	|ГДЕ
	|	КурсВалюты.КурсЗнаменатель <> 0
	|;
	//-- НЕ УТ
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаДокумента.АналитикаУчетаНоменклатуры.Назначение КАК Назначение,
	|	ВЫБОР КОГДА ТаблицаДокумента.МестоХранения = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка) ТОГДА
	|		&Склад
	|	ИНАЧЕ
	|		ТаблицаДокумента.МестоХранения
	|	КОНЕЦ КАК МестоХранения,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.ВестиУчетПоГТД КАК ВедетсяУчетПоГТД,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СправочникНоменклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.Серия КАК Серия,
	|	ТаблицаДокумента.Количество КАК Количество,
	|	ТаблицаДокумента.КоличествоПоРНПТ КАК КоличествоПоРНПТ,
	|	ТаблицаДокумента.СуммаСНДС КАК СуммаСНДС,
	|	ТаблицаДокумента.СуммаНДС КАК СуммаНДС,
	|	ТаблицаДокумента.СтавкаНДС КАК СтавкаНДС,
	|
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.Количество, 0) КАК КоличествоВидыЗапасов,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.КоличествоПоРНПТ, 0) КАК КоличествоПоРНПТВидыЗапасов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ТаблицаВидыЗапасов.Количество, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ЕСТЬNULL(ТаблицаВидыЗапасов.КоличествоПоРНПТ, 0) / ТаблицаВидыЗапасов.Количество
	|	КОНЕЦ КАК КоэффициентРНПТ,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.СуммаСНДС, 0) КАК СуммаСНДСВидыЗапасов,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.СуммаНДС, 0) КАК СуммаНДСВидыЗапасов,
	|	ЕСТЬNULL(ТаблицаВидыЗапасов.СтавкаНДС, Неопределено) КАК СтавкаНДСВидыЗапасов,
	//++ НЕ УТ
	|	ЕСТЬNULL(ТаблицаАкцизов.ПодакцизныйТовар, ЛОЖЬ) КАК ПодакцизныйТовар,
	|	ВЫБОР КОГДА &ОблагаетсяАкцизомВидЗапасовПолучателя ТОГДА
	|		ЕСТЬNULL(ВидыЗапасовПолучателя.ОблагаетсяАкцизом, ЛОЖЬ)
	|	ИНАЧЕ
	|		ЕСТЬNULL(ВидыЗапасов.ОблагаетсяАкцизом, ЛОЖЬ)
	|	КОНЕЦ КАК ОблагаетсяАкцизом,
	|	ВЫБОР КОГДА ЕСТЬNULL(ВидыЗапасов.ОблагаетсяАкцизом, ЛОЖЬ)
	|		ИЛИ &ОблагаетсяАкцизомВидЗапасовПолучателя И ЕСТЬNULL(ВидыЗапасовПолучателя.ОблагаетсяАкцизом, ЛОЖЬ) ТОГДА
	|		ЕСТЬNULL(ТаблицаВидыЗапасов.СуммаАкциза, 0)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК СуммаАкциза,
	|	ВЫБОР КОГДА ЕСТЬNULL(ВидыЗапасов.ОблагаетсяАкцизом, ЛОЖЬ)
	|		ИЛИ &ОблагаетсяАкцизомВидЗапасовПолучателя И ЕСТЬNULL(ВидыЗапасовПолучателя.ОблагаетсяАкцизом, ЛОЖЬ) ТОГДА
	|		ЕСТЬNULL(ТаблицаВидыЗапасов.Количество, 0) * ЕСТЬNULL(ТаблицаАкцизов.КоэффициентПересчета, 0)
	|	ИНАЧЕ
	|		0
	|	КОНЕЦ КАК НалоговаяБазаАкциза,
	|	ТаблицаАкцизов.ЕдиницаИзмеренияАкциза КАК ЕдиницаИзмеренияАкциза,
	|	ЕСТЬNULL(ТаблицаАкцизов.КоэффициентПересчета, 0) КАК КоэффициентПересчетаАкциза,
	|	ТаблицаАкцизов.КатегорияПодакцизногоТовара КАК КатегорияПодакцизногоТовара,
	|	ЕСТЬNULL(ТаблицаСтавокАкцизов.СтавкаАкциза, 0) КАК СтавкаАкциза,
	|	ЕСТЬNULL(ТаблицаСтавокАкцизов.СтавкаАкциза, 0)
	|		* ЕСТЬNULL(КурсыВалют.КоэффициентПересчета, 0) КАК СтавкаАкцизаРегл,
	|	ТаблицаАкцизов.ВалютаАкциза КАК ВалютаАкциза,
	//-- НЕ УТ
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|	ИНАЧЕ
	|		ВидыЗапасов.ТипЗапасов
	|	КОНЕЦ КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.Организация
	|	ИНАЧЕ
	|		ВидыЗапасов.Организация
	|	КОНЕЦ КАК Организация,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.ВладелецТовара
	|	ИНАЧЕ
	|		ВидыЗапасов.ВладелецТовара
	|	КОНЕЦ КАК ВладелецТовара,
	|
	|	ТаблицаВидыЗапасов.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.СпособОпределенияСебестоимости КАК СпособОпределенияСебестоимости,
	|	ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки
	|ИЗ
	|	ТаблицаДокумента КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|			И ТаблицаДокумента.СпособОпределенияСебестоимости = ТаблицаВидыЗапасов.СпособОпределенияСебестоимости
	|			И ТаблицаДокумента.Знак = ТаблицаВидыЗапасов.Знак
	|			И (ТаблицаДокумента.МестоХранения = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ИЛИ ТаблицаДокумента.МестоХранения = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				ИЛИ ТаблицаДокумента.МестоХранения = ТаблицаВидыЗапасов.МестоХранения)
	|			И (ТаблицаДокумента.ДокументРеализации = ТаблицаВидыЗапасов.ДокументРеализации
	|				ИЛИ (&ОтображатьДокументыРеализации
	|					И (ТаблицаДокумента.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|						ИЛИ ТаблицаДокумента.ДокументРеализации = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|						ИЛИ ТаблицаДокумента.ДокументРеализации = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка))))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ТаблицаВидыЗапасов.ВидЗапасов = ВидыЗапасов.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаДокумента.Номенклатура = СправочникНоменклатура.Ссылка
	//++ НЕ УТ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасовПолучателя
	|			ПО ТаблицаВидыЗапасов.ВидЗапасовПолучателя = ВидыЗапасовПолучателя.Ссылка
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаАкцизов КАК ТаблицаАкцизов
	|			ПО ТаблицаАкцизов.Номенклатура = ТаблицаДокумента.Номенклатура
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСтавокАкцизов КАК ТаблицаСтавокАкцизов
	|			ПО ТаблицаАкцизов.КатегорияПодакцизногоТовара = ТаблицаСтавокАкцизов.КатегорияПодакцизногоТовара
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|			ПО ТаблицаАкцизов.ВалютаАкциза = КурсыВалют.Валюта
	//-- НЕ УТ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	0 КАК НомерСтроки,
	|	ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
	|	ТаблицаВидыЗапасов.Назначение КАК Назначение,
	|	ТаблицаВидыЗапасов.МестоХранения КАК МестоХранения,
	|	ТаблицаВидыЗапасов.Номенклатура КАК Номенклатура,
	|	ТаблицаВидыЗапасов.Номенклатура.ВестиУчетПоГТД КАК ВедетсяУчетПоГТД,
	|	СправочникНоменклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	СправочникНоменклатура.ПрослеживаемыйТовар КАК ПрослеживаемыйТовар,
	|	ТаблицаВидыЗапасов.Характеристика КАК Характеристика,
	|	ТаблицаВидыЗапасов.Серия КАК Серия,
	|	0 КАК Количество,
	|	0 КАК КоличествоПоРНПТ,
	|	0 КАК СуммаСНДС,
	|	0 КАК СуммаНДС,
	|	ТаблицаВидыЗапасов.СтавкаНДС КАК СтавкаНДС,
	|
	|	ТаблицаВидыЗапасов.ВидЗапасов КАК ВидЗапасов,
	|	ТаблицаВидыЗапасов.ВидЗапасовПолучателя КАК ВидЗапасовПолучателя,
	|	ТаблицаВидыЗапасов.НомерГТД КАК НомерГТД,
	|	ТаблицаВидыЗапасов.Количество КАК КоличествоВидыЗапасов,
	|	ТаблицаВидыЗапасов.КоличествоПоРНПТ КАК КоличествоПоРНПТВидыЗапасов,
	|	ВЫБОР
	|		КОГДА ТаблицаВидыЗапасов.Количество = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаВидыЗапасов.КоличествоПоРНПТ / ТаблицаВидыЗапасов.Количество
	|	КОНЕЦ КАК КоэффициентРНПТ,
	|	ТаблицаВидыЗапасов.СуммаСНДС КАК СуммаСНДСВидыЗапасов,
	|	ТаблицаВидыЗапасов.СуммаНДС КАК СуммаНДСВидыЗапасов,
	|	ТаблицаВидыЗапасов.СтавкаНДС КАК СтавкаНДСВидыЗапасов,
	//++ НЕ УТ
	|	ЛОЖЬ КАК ПодакцизныйТовар,
	|	ЛОЖЬ КАК ОблагаетсяАкцизом,
	|	0 КАК СуммаАкциза,
	|	0 КАК НалоговаяБазаАкциза,
	|	НЕОПРЕДЕЛЕНО КАК ЕдиницаИзмеренияАкциза,
	|	0 КАК КоэффициентПересчетаАкциза,
	|	НЕОПРЕДЕЛЕНО КАК КатегорияПодакцизныхТоваров,
	|	0 КАК СтавкаАкциза,
	|	0 КАК СтавкаАкцизаРегл,
	|	НЕОПРЕДЕЛЕНО КАК ВалютаАкциза,
	//-- НЕ УТ
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.ТипЗапасов
	|	ИНАЧЕ
	|		ВидыЗапасов.ТипЗапасов
	|	КОНЕЦ КАК ТипЗапасов,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.Организация
	|	ИНАЧЕ
	|		ВидыЗапасов.Организация
	|	КОНЕЦ КАК Организация,
	|
	|	ВЫБОР КОГДА ВидыЗапасов.РеализацияЗапасовДругойОрганизации ТОГДА
	|		ВидыЗапасов.ВидЗапасовВладельца.ВладелецТовара
	|	ИНАЧЕ
	|		ВидыЗапасов.ВладелецТовара
	|	КОНЕЦ КАК ВладелецТовара,
	|
	|	ТаблицаВидыЗапасов.ДокументРеализации КАК ДокументРеализации,
	|	ТаблицаВидыЗапасов.СпособОпределенияСебестоимости КАК СпособОпределенияСебестоимости,
	|	ТаблицаВидыЗапасов.СкладОтгрузки КАК СкладОтгрузки
	|
	|ИЗ
	|	ТаблицаВидыЗапасов КАК ТаблицаВидыЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДокумента КАК ТаблицаДокумента
	|		ПО ТаблицаДокумента.АналитикаУчетаНоменклатуры = ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры
	|			И ТаблицаДокумента.Знак = ТаблицаВидыЗапасов.Знак
	|			И (ТаблицаДокумента.МестоХранения = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|				ИЛИ ТаблицаДокумента.МестоХранения = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
	|				ИЛИ ТаблицаДокумента.МестоХранения = ТаблицаВидыЗапасов.МестоХранения)
	|			И (ТаблицаДокумента.ДокументРеализации = ТаблицаВидыЗапасов.ДокументРеализации
	|				ИЛИ (&ОтображатьДокументыРеализации
	|					И (ТаблицаДокумента.ДокументРеализации = НЕОПРЕДЕЛЕНО
	|						ИЛИ ТаблицаДокумента.ДокументРеализации = ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)
	|						ИЛИ ТаблицаДокумента.ДокументРеализации = ЗНАЧЕНИЕ(Документ.ОтчетОРозничныхПродажах.ПустаяСсылка))))
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЗапасов КАК ВидыЗапасов
	|		ПО ТаблицаВидыЗапасов.ВидЗапасов = ВидыЗапасов.Ссылка
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО ТаблицаДокумента.Номенклатура = СправочникНоменклатура.Ссылка
	|ГДЕ
	|	ТаблицаДокумента.Номенклатура ЕСТЬ NULL
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	ВидЗапасов,
	|	НомерГТД
	|
	|ИТОГИ
	|	СУММА(Количество),
	|	СУММА(КоличествоПоРНПТ),
	|	СУММА(СуммаСНДС),
	|	СУММА(СуммаНДС),
	|	МАКСИМУМ(СтавкаНДС),
	|	СУММА(КоличествоВидыЗапасов),
	|	СУММА(КоличествоПоРНПТВидыЗапасов),
	//++ НЕ УТ
	|	МАКСИМУМ(ПодакцизныйТовар),
	|	МАКСИМУМ(ОблагаетсяАкцизом),
	|	СУММА(СуммаАкциза),
	|	СУММА(НалоговаяБазаАкциза),
	|	МАКСИМУМ(КатегорияПодакцизногоТовара),
	|	МАКСИМУМ(СтавкаАкциза),
	|	МАКСИМУМ(ЕдиницаИзмеренияАкциза),
	|	МАКСИМУМ(КоэффициентПересчетаАкциза),
	|	МАКСИМУМ(СтавкаАкцизаРегл),
	|	МАКСИМУМ(ВалютаАкциза),
	//-- НЕ УТ
	|	СУММА(СуммаСНДСВидыЗапасов),
	|	СУММА(СуммаНДСВидыЗапасов),
	|	МАКСИМУМ(СтавкаНДСВидыЗапасов)
	|ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ЦенаНеВключаетНДС",				?(ЦенаВключаетНДС, 0, 1));
	Запрос.УстановитьПараметр("ОтображатьДокументыРеализации",	ОтображатьДокументыРеализации);
	Запрос.УстановитьПараметр("Склад",							Склад);
	Запрос.УстановитьПараметр("ПодразделениеВШапке",			ПараметрыРедактированияВидовЗапасов.ПодразделениеВШапке);
	Запрос.УстановитьПараметр("Подразделение",					ПараметрыРедактированияВидовЗапасов.Подразделение);
	Запрос.УстановитьПараметр("ДатаДокумента",					ПараметрыРедактированияВидовЗапасов.ДатаДокумента);
	Запрос.УстановитьПараметр("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров",
								ИспользоватьУчетПрослеживаемыхИмпортныхТоваров);
	//++ НЕ УТ
	БазоваяВалюта = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(Организация);
	СтранаРегистрации = ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(Организация);
	Запрос.УстановитьПараметр("Страна",							СтранаРегистрации);
	Запрос.УстановитьПараметр("БазоваяВалюта",					БазоваяВалюта);
	ОблагаетсяАкцизомВидЗапасовПолучателя = Ложь;
	Если ТипЗнч(Регистратор) = Тип("ДокументСсылка.ВозвратТоваровМеждуОрганизациями") Тогда
		ОблагаетсяАкцизомВидЗапасовПолучателя = Истина;
	КонецЕсли;
	Запрос.УстановитьПараметр("ОблагаетсяАкцизомВидЗапасовПолучателя", ОблагаетсяАкцизомВидЗапасовПолучателя);
	//-- НЕ УТ
	
	// Формирование данных таблицы Товары.
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(АдресТоваровВХранилище); // ТаблицаЗначений
	
	КолонкаНомерСтроки = ТаблицаТоваров.Колонки.Найти("LineNumber");
	Если КолонкаНомерСтроки <> Неопределено Тогда
		КолонкаНомерСтроки.Имя = "НомерСтроки";
	КонецЕсли;
	
	ЕстьКоличествоПоРНПТВТЧТовары = (ТаблицаТоваров.Колонки.Найти("КоличествоПоРНПТ") <> Неопределено);
	Если Не ЕстьКоличествоПоРНПТВТЧТовары Тогда
		ТаблицаТоваров.Колонки.Добавить("КоличествоПоРНПТ", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьСумма = (ТаблицаТоваров.Колонки.Найти("Сумма") <> Неопределено);
	Если Не ЕстьСумма Тогда
		ТаблицаТоваров.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьСуммаНДС = (ТаблицаТоваров.Колонки.Найти("СуммаНДС") <> Неопределено);
	Если Не ЕстьСуммаНДС Тогда
		ТаблицаТоваров.Колонки.Добавить("СуммаНДС", Новый ОписаниеТипов("Число"));
		ТаблицаТоваров.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	КонецЕсли;
	
	ЕстьСуммаПродажи = (ТаблицаТоваров.Колонки.Найти("СуммаПродажи") <> Неопределено);
	Если Не ЕстьСуммаПродажи Тогда
		ТаблицаТоваров.Колонки.Добавить("СуммаПродажи", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьСклад = (ТаблицаТоваров.Колонки.Найти("Склад") <> Неопределено);
	Если Не ЕстьСклад Тогда
		ТаблицаТоваров.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	КонецЕсли;
	
	ЕстьПодразделение = (ТаблицаТоваров.Колонки.Найти("Подразделение") <> Неопределено);
	Если Не ЕстьПодразделение Тогда
		ТаблицаТоваров.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.СтруктураПредприятия"));
	КонецЕсли;
	
	ЕстьДокументРеализации = (ТаблицаТоваров.Колонки.Найти("ДокументРеализации") <> Неопределено);
	Если Не ЕстьДокументРеализации Тогда
		ТаблицаТоваров.Колонки.Добавить("ДокументРеализации", Документы.ТипВсеСсылки());
	КонецЕсли;
	
	ЕстьСерия = (ТаблицаТоваров.Колонки.Найти("Серия") <> Неопределено);
	Если Не ЕстьСерия Тогда
		ТаблицаТоваров.Колонки.Добавить("Серия", Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	КонецЕсли;

	ЕстьСтатусУказанияСерий = (ТаблицаТоваров.Колонки.Найти("СтатусУказанияСерий") <> Неопределено);
	Если Не ЕстьСтатусУказанияСерий Тогда
		ТаблицаТоваров.Колонки.Добавить("СтатусУказанияСерий", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьНазначение = (ТаблицаТоваров.Колонки.Найти("Назначение") <> Неопределено);
	Если Не ЕстьНазначение Тогда
		ТаблицаТоваров.Колонки.Добавить("Назначение", Новый ОписаниеТипов("СправочникСсылка.Назначения"));
	КонецЕсли;
	
	ЕстьСпособОпределенияСебестоимости = (ТаблицаТоваров.Колонки.Найти("СпособОпределенияСебестоимости") <> Неопределено);
	Если Не ЕстьСпособОпределенияСебестоимости Тогда
		ТаблицаТоваров.Колонки.Добавить("СпособОпределенияСебестоимости", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыОпределенияСебестоимостиВозврата"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИсходнаяТаблицаДокумента", ТаблицаТоваров);
	
	// Формирование данных таблицы ВидыЗапасов.
	Если ТаблицаВидыЗапасов = Неопределено Тогда
		ТаблицаВидыЗапасов = ПолучитьИзВременногоХранилища(АдресВидовЗапасовВХранилище); // ТаблицаЗначений - 
	КонецЕсли;
	
	ЕстьКоличествоПоРНПТ = (ТаблицаВидыЗапасов.Колонки.Найти("КоличествоПоРНПТ") <> Неопределено);
	Если Не ЕстьКоличествоПоРНПТ Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("КоличествоПоРНПТ", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьСуммаНДС = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаНДС") <> Неопределено);
	Если Не ЕстьСуммаНДС Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("СуммаНДС",  Новый ОписаниеТипов("Число"));
		ТаблицаВидыЗапасов.Колонки.Добавить("СтавкаНДС", Новый ОписаниеТипов("СправочникСсылка.СтавкиНДС"));
	КонецЕсли;
	
	ЕстьСуммаСНДС = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаСНДС") <> Неопределено);
	Если Не ЕстьСуммаСНДС Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("СуммаСНДС",  Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьСкладОтгрузки = (ТаблицаВидыЗапасов.Колонки.Найти("СкладОтгрузки") <> Неопределено);
	Если Не ЕстьСкладОтгрузки Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("СкладОтгрузки", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	КонецЕсли;
	
	ЕстьДокументРеализации = (ТаблицаВидыЗапасов.Колонки.Найти("ДокументРеализации") <> Неопределено);
	Если Не ЕстьДокументРеализации Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("ДокументРеализации", Документы.ТипВсеСсылки());
	КонецЕсли;
	
	ЕстьНомерГТД = (ТаблицаВидыЗапасов.Колонки.Найти("НомерГТД") <> Неопределено);
	Если Не ЕстьНомерГТД Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	КонецЕсли;
	
	ЕстьСпособОпределенияСебестоимости = (ТаблицаВидыЗапасов.Колонки.Найти("СпособОпределенияСебестоимости") <> Неопределено);
	Если Не ЕстьСпособОпределенияСебестоимости Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("СпособОпределенияСебестоимости", Новый ОписаниеТипов("ПеречислениеСсылка.СпособыОпределенияСебестоимостиВозврата"));
	КонецЕсли;
	
	ЕстьСуммаАкциза = (ТаблицаВидыЗапасов.Колонки.Найти("СуммаАкциза") <> Неопределено);
	Если Не ЕстьСуммаАкциза Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("СуммаАкциза", Новый ОписаниеТипов("Число"));
	КонецЕсли;
	
	ЕстьВидЗапасовПолучателя = (ТаблицаВидыЗапасов.Колонки.Найти("ВидЗапасовПолучателя") <> Неопределено);
	Если Не ЕстьВидЗапасовПолучателя Тогда
		ТаблицаВидыЗапасов.Колонки.Добавить("ВидЗапасовПолучателя", Новый ОписаниеТипов("СправочникСсылка.ВидыЗапасов"));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ИсходнаяТаблицаВидыЗапасов", ТаблицаВидыЗапасов);
	
	Дерево = РеквизитФормыВЗначение("ДеревоВидовЗапасов");
	Дерево.Строки.Очистить();
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		РедактироватьВидыЗапасов = Ложь;
	Иначе
		ВыборкаПоСтрокам = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоСтрокам.Следующий() Цикл
			
			ДобавитьСтрокуНоменклатуры = Истина;
			
			Выборка = ВыборкаПоСтрокам.Выбрать();
			КоличествоЗаписей = Выборка.Количество();
			
			Если КоличествоЗаписей > 1
				Или ВидыЗапасовУказаныВручную
				Или ВыборкаПоСтрокам.Количество = 0 Тогда
				
				ДобавлятьПодчиненныеСтроки = Истина;
				
			ИначеЕсли ВыборкаПоСтрокам.Количество <> ВыборкаПоСтрокам.КоличествоВидыЗапасов
				Или (ЕстьКоличествоПоРНПТВТЧТовары
					И ВыборкаПоСтрокам.КоличествоПоРНПТ <> ВыборкаПоСтрокам.КоличествоПоРНПТВидыЗапасов)
				Или ВыборкаПоСтрокам.СуммаСНДС <> ВыборкаПоСтрокам.СуммаСНДСВидыЗапасов
				Или ВыборкаПоСтрокам.СуммаНДС <> ВыборкаПоСтрокам.СуммаНДСВидыЗапасов
				Или ВыборкаПоСтрокам.СтавкаНДС <> ВыборкаПоСтрокам.СтавкаНДСВидыЗапасов Тогда
				
				ДобавлятьПодчиненныеСтроки = Истина;
				
			Иначе
				ДобавлятьПодчиненныеСтроки = Ложь;
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				
				Если ДобавитьСтрокуНоменклатуры Тогда
				
					СтрокаГруппы = Дерево.Строки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаГруппы, Выборка);
					
					Если Не Выборка.КоличествоПоРНПТ
						И Выборка.КоличествоПоРНПТВидыЗапасов Тогда
						
						СтрокаГруппы.КоличествоПоРНПТ = Выборка.Количество * Выборка.КоэффициентРНПТ;
						
					КонецЕсли;
					
					Если ДобавлятьПодчиненныеСтроки Тогда
						СтрокаГруппы.ВидЗапасов = Неопределено;
						СтрокаГруппы.ВидЗапасовПолучателя = Неопределено;
						СтрокаГруппы.НомерГТД = Неопределено;
						СтрокаГруппы.ТипЗапасов = Неопределено;
						СтрокаГруппы.ВладелецТовара = Неопределено;
						СтрокаГруппы.Организация = Неопределено;
						СтрокаГруппы.СкладОтгрузки = Неопределено;
						СтрокаГруппы.КоличествоВидыЗапасов = 0;
						СтрокаГруппы.КоличествоПоРНПТВидыЗапасов = 0;
						СтрокаГруппы.СуммаСНДСВидыЗапасов = 0;
						СтрокаГруппы.СуммаНДСВидыЗапасов = 0;
						СтрокаГруппы.СпособОпределенияСебестоимости = Неопределено;
						//++ НЕ УТ
						СтрокаГруппы.СуммаАкциза = 0;
						СтрокаГруппы.НалоговаяБазаАкциза = 0;
						СтрокаГруппы.ОблагаетсяАкцизом = Ложь;
						//-- НЕ УТ
					КонецЕсли;
					
					ДобавитьСтрокуНоменклатуры = Ложь;
				
				КонецЕсли;
			
				Если Выборка.КоличествоВидыЗапасов <> 0 И ДобавлятьПодчиненныеСтроки Тогда
					
					СтрокаВидыЗапасов = СтрокаГруппы.Строки.Добавить();
					СтрокаВидыЗапасов.НоменклатураВидыЗапасов = Выборка.Номенклатура;
					СтрокаВидыЗапасов.ХарактеристикаВидыЗапасов = Выборка.Характеристика;
					СтрокаВидыЗапасов.НазначениеВидыЗапасов = Выборка.Назначение;
					СтрокаВидыЗапасов.СерияВидыЗапасов = Выборка.Серия;
					СтрокаВидыЗапасов.ВидЗапасов = Выборка.ВидЗапасов;
					СтрокаВидыЗапасов.ВидЗапасовПолучателя = Выборка.ВидЗапасовПолучателя;
					СтрокаВидыЗапасов.ТипЗапасов = Выборка.ТипЗапасов;
					СтрокаВидыЗапасов.ВладелецТовара = Выборка.ВладелецТовара;
					СтрокаВидыЗапасов.Организация = Выборка.Организация;
					СтрокаВидыЗапасов.ВедетсяУчетПоГТД = Выборка.ВедетсяУчетПоГТД;
					СтрокаВидыЗапасов.ТипНоменклатуры = Выборка.ТипНоменклатуры;
					СтрокаВидыЗапасов.ПрослеживаемыйТовар = Выборка.ПрослеживаемыйТовар;
					СтрокаВидыЗапасов.НомерГТД = Выборка.НомерГТД;
					СтрокаВидыЗапасов.ДокументРеализации = Выборка.ДокументРеализации;
					СтрокаВидыЗапасов.СпособОпределенияСебестоимости = Выборка.СпособОпределенияСебестоимости;
					СтрокаВидыЗапасов.СкладОтгрузки = Выборка.СкладОтгрузки;
					СтрокаВидыЗапасов.Количество = Выборка.КоличествоВидыЗапасов;
					СтрокаВидыЗапасов.КоличествоПоРНПТ = Выборка.КоличествоПоРНПТВидыЗапасов;
					СтрокаВидыЗапасов.КоэффициентРНПТ = Выборка.КоэффициентРНПТ;
					СтрокаВидыЗапасов.СуммаСНДС = Выборка.СуммаСНДСВидыЗапасов;
					СтрокаВидыЗапасов.СуммаНДС = Выборка.СуммаНДСВидыЗапасов;
					СтрокаВидыЗапасов.СтавкаНДС = Выборка.СтавкаНДСВидыЗапасов;
					//++ НЕ УТ
					СтрокаВидыЗапасов.ПодакцизныйТовар = Выборка.ПодакцизныйТовар;
					СтрокаВидыЗапасов.ОблагаетсяАкцизом = Выборка.ОблагаетсяАкцизом;
					СтрокаВидыЗапасов.СуммаАкциза = Выборка.СуммаАкциза;
					СтрокаВидыЗапасов.НалоговаяБазаАкциза = Выборка.НалоговаяБазаАкциза;
					СтрокаВидыЗапасов.ЕдиницаИзмеренияАкциза = Выборка.ЕдиницаИзмеренияАкциза;
					СтрокаВидыЗапасов.КоэффициентПересчетаАкциза = Выборка.КоэффициентПересчетаАкциза;
					СтрокаВидыЗапасов.КатегорияПодакцизногоТовара = Выборка.КатегорияПодакцизногоТовара;
					СтрокаВидыЗапасов.СтавкаАкциза = Выборка.СтавкаАкциза;
					СтрокаВидыЗапасов.СтавкаАкцизаРегл = Выборка.СтавкаАкцизаРегл;
					СтрокаВидыЗапасов.ВалютаАкциза = Выборка.ВалютаАкциза;
					//-- НЕ УТ
					
					СтрокаГруппы.КоличествоВидыЗапасов = СтрокаГруппы.КоличествоВидыЗапасов + Выборка.КоличествоВидыЗапасов;
					СтрокаГруппы.КоличествоПоРНПТВидыЗапасов = СтрокаГруппы.КоличествоПоРНПТВидыЗапасов + Выборка.КоличествоПоРНПТВидыЗапасов;
					СтрокаГруппы.СуммаСНДСВидыЗапасов = СтрокаГруппы.СуммаСНДСВидыЗапасов + Выборка.СуммаСНДСВидыЗапасов;
					СтрокаГруппы.СуммаНДСВидыЗапасов = СтрокаГруппы.СуммаНДСВидыЗапасов + Выборка.СуммаНДСВидыЗапасов;
					//++ НЕ УТ
					СтрокаГруппы.СуммаАкциза = СтрокаГруппы.СуммаАкциза + Выборка.СуммаАкциза;
					СтрокаГруппы.НалоговаяБазаАкциза = СтрокаГруппы.НалоговаяБазаАкциза + Выборка.НалоговаяБазаАкциза;
					СтрокаГруппы.ОблагаетсяАкцизом = Макс(СтрокаГруппы.ОблагаетсяАкцизом, Выборка.ОблагаетсяАкцизом);
					СтрокаГруппы.ПодакцизныйТовар = Макс(СтрокаГруппы.ПодакцизныйТовар, Выборка.ПодакцизныйТовар);
					//-- НЕ УТ
					
					Если СтрокаГруппы.Количество > СтрокаГруппы.КоличествоВидыЗапасов Тогда
						Недостача	= Формат(СтрокаГруппы.Количество - СтрокаГруппы.КоличествоВидыЗапасов, "ЧДЦ=3");
						Информация	= НСтр("ru = 'Недостаточно: %Количество%';
											|en = 'Insufficient: %Количество%'");
						
						СтрокаГруппы.Информация = СтрЗаменить(Информация, "%Количество%", Недостача);
					ИначеЕсли СтрокаГруппы.Количество < СтрокаГруппы.КоличествоВидыЗапасов Тогда
						Излишек		= Формат(СтрокаГруппы.КоличествоВидыЗапасов - СтрокаГруппы.Количество, "ЧДЦ=3");
						Информация	= НСтр("ru = 'Превышение: %Количество%';
											|en = 'Exceeding: %Количество%'");
						
						СтрокаГруппы.Информация = СтрЗаменить(Информация, "%Количество%", Излишек);
					Иначе
						СтрокаГруппы.Информация = "";
					КонецЕсли;
					
				КонецЕсли;
			
			КонецЦикла;
			
		КонецЦикла;
		
		ЗначениеВРеквизитФормы(Дерево, "ДеревоВидовЗапасов");
	КонецЕсли;
	
	Если Не ВидимостьУстановлена Тогда
		ИспользуетсяСкладОтгрузки = ЕстьСкладОтгрузки;
		Элементы.ДеревоВидовЗапасовСуммаСНДС.Видимость     = (ЕстьСумма ИЛИ ЕстьСуммаПродажи);
		Элементы.ДеревоВидовЗапасовСтавкаНДС.Видимость     = ЕстьСуммаНДС;
		Элементы.ДеревоВидовЗапасовСуммаНДС.Видимость      = ЕстьСуммаНДС;
		Элементы.МестоХранения.Видимость                   = ЕстьСклад Или ЕстьПодразделение;
		Элементы.ДеревоВидовЗапасовСкладОтгрузки.Видимость = ЕстьСкладОтгрузки;
		ВидимостьУстановлена = Истина;
	КонецЕсли;
	
	ЗаполнитьПолеПроверитьНомерГТДНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПолеИнформации(СтрокаТаблицы)
	
	Если СтрокаТаблицы.Количество > СтрокаТаблицы.КоличествоВидыЗапасов Тогда
		СтрокаТаблицы.Информация = "Недостаточно: " + Формат(СтрокаТаблицы.Количество - СтрокаТаблицы.КоличествоВидыЗапасов, "ЧДЦ=3");
	ИначеЕсли СтрокаТаблицы.Количество < СтрокаТаблицы.КоличествоВидыЗапасов Тогда
		СтрокаТаблицы.Информация = "Превышение: " + Формат(СтрокаТаблицы.КоличествоВидыЗапасов - СтрокаТаблицы.Количество, "ЧДЦ=3");
	Иначе
		СтрокаТаблицы.Информация = "";
	КонецЕсли;
	
КонецПроцедуры

#Область НомераГТД

&НаСервере
Процедура ЗаполнитьПолеПроверитьНомерГТДНаСервере()
	
	Если Не ИспользоватьУчетПрослеживаемыхИмпортныхТоваров Тогда
		Возврат;
	КонецЕсли;
	
	УчетПрослеживаемыхТоваровЛокализация.ЗаполнитьПолеПроверитьНомерГТДВДереве(ЭтотОбъект);
	Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Доступность = ЕстьРНПТБезСтоимости;
	
	Если Не ЕстьРНПТБезСтоимости
		И Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка Тогда
		
		Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка = Ложь;
		ОтобратьКомплектыРНПТ = Элементы.ДеревоВидовЗапасовОтобратьКомплектыРНПТ.Пометка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьДанныеПрослеживаемостиНомераГТДНаСервере(НомерГТД)
	
	Возврат УчетПрослеживаемыхТоваровЛокализация.ПроверитьДанныеПрослеживаемостиНомераГТД(НомерГТД);
	
КонецФункции

&НаКлиенте
Процедура НомерГТДСозданиеЗавершение(Результат, ТекущийНомерГТД) Экспорт
	
	ТекущиеДанные = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущийНомерГТД)
		И ТекущиеДанные.НомерГТД = ТекущийНомерГТД Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОбработатьУказаниеНомераГТД(ТекущиеДанные, ТекущийНомерГТД);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьУказаниеНомераГТД(ТекущиеДанные, ТекущийНомерГТД)
	
	ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
	
	Если ИдентификаторСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщенияОбОшибке = "";
	
	ОбработатьУказаниеНомераГТДСервер(ИдентификаторСтроки, ТекстСообщенияОбОшибке);
	
	Если Не ПустаяСтрока(ТекстСообщенияОбОшибке) Тогда
		ТекущиеДанные.НомерГТД = ТекущийНомерГТД;
		
		ПоказатьПредупреждение(, ТекстСообщенияОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьУказаниеНомераГТДСервер(ИдентификаторСтроки, ТекстСообщенияОбОшибке)
	
	ТекущаяСтрока = ДеревоВидовЗапасов.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НомерГТДУказанКорректно = НомерГТДУказанКорректно(ТекущаяСтрока, ТекстСообщенияОбОшибке);
	
	Если НомерГТДУказанКорректно Тогда
		ТекущаяСтрока.ПроверитьНомерГТД = ПроверитьДанныеПрослеживаемостиНомераГТДНаСервере(ТекущаяСтрока.НомерГТД);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НомерГТДУказанКорректно(ТекущаяСтрока, ТекстСообщенияОбОшибке)
	
	НомерГТДУказанКорректно	= Истина;
	ТипНомераГТД			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.НомерГТД, "ТипНомераГТД");
	СписокТиповГТД			= УчетПрослеживаемыхТоваровКлиентСерверЛокализация.ТипыНомеровГТД(Истина);
	
	Если СписокТиповГТД.Найти(ТипНомераГТД) <> Неопределено Тогда
		Возврат НомерГТДУказанКорректно;
	КонецЕсли;
	
	Если ТекущаяСтрока.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
		НомерГТДУказанКорректно = Ложь;
		
		ТекстСообщенияОбОшибке = НСтр("ru = '""%1"" не соответствует условиям выбора';
										|en = '""%1"" does not meet the selection criteria'");
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									ТекстСообщенияОбОшибке,
									СокрЛП(Строка(ТекущаяСтрока.НомерГТД)));
	КонецЕсли;
	
	Возврат НомерГТДУказанКорректно;
	
КонецФункции

#КонецОбласти

//++ НЕ УТ

&НаКлиенте
Процедура ЗаполнитьДанныеПоАкцизамВГруппе(СтрокаГруппы)
	
	СтрокаГруппы.СуммаАкциза = 0;
	СтрокаГруппы.НалоговаяБазаАкциза = 0;
	СтрокаГруппы.ОблагаетсяАкцизом = Ложь;
	СтрокаГруппы.ПодакцизныйТовар = Ложь;
	Для Каждого Строка Из СтрокаГруппы.ПолучитьЭлементы() Цикл
		СтрокаГруппы.СуммаАкциза = СтрокаГруппы.СуммаАкциза + Строка.СуммаАкциза;
		СтрокаГруппы.НалоговаяБазаАкциза = СтрокаГруппы.НалоговаяБазаАкциза + Строка.НалоговаяБазаАкциза;
		СтрокаГруппы.ОблагаетсяАкцизом = Макс(СтрокаГруппы.ОблагаетсяАкцизом, Строка.ОблагаетсяАкцизом);
		СтрокаГруппы.ПодакцизныйТовар = Макс(СтрокаГруппы.ПодакцизныйТовар, Строка.ПодакцизныйТовар);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииВидаЗапасов(Элемент)
	
	СтрокаТаблицы = Элементы.ДеревоВидовЗапасов.ТекущиеДанные;
	СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
	Если ЗначениеЗаполнено(СтрокаРодитель.КатегорияПодакцизногоТовара) Тогда
		СтрокаТаблицы.ОблагаетсяАкцизом = ОблагаетсяАкцизом(СтрокаТаблицы[Элемент.Имя]);
		Если СтрокаТаблицы.ОблагаетсяАкцизом Тогда
			СтрокаТаблицы.НалоговаяБазаАкциза = СтрокаТаблицы.Количество * СтрокаТаблицы.КоэффициентПересчетаАкциза;
			СтрокаТаблицы.СуммаАкциза = СтрокаТаблицы.НалоговаяБазаАкциза * СтрокаРодитель.СтавкаАкцизаРегл;
		Иначе
			СтрокаТаблицы.НалоговаяБазаАкциза = 0;
			СтрокаТаблицы.СуммаАкциза = 0;
		КонецЕсли;
		ЗаполнитьДанныеПоАкцизамВГруппе(СтрокаРодитель);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОблагаетсяАкцизом(ВидЗапасов)
	
	Возврат Справочники.ВидыЗапасов.ОблагаетсяАкцизом(ВидЗапасов);
	
КонецФункции

&НаКлиенте
Процедура УстановитьДоступностьОписанияПодакцизнойКатегорииТовара(ПодакцизныйТовар)
	
	Элементы.ГруппаДанныеПодакцизногоТовара.Доступность = ПодакцизныйТовар;
	
КонецПроцедуры

//-- НЕ УТ

&НаСервере
Процедура ЗаполнитьВидыЗапасовНаСервере()
	
	НачатьТранзакцию();
	
	Попытка
		
		ПричинаИсключения = 1;
		
		МетаданныеДокумента = Регистратор.Метаданные();
		
		Если ЗначениеЗаполнено(Регистратор) Тогда
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(МетаданныеДокумента.ПолноеИмя());
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Регистратор);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			
			Блокировка.Заблокировать();
			
			ДокументОбъект = Регистратор.ПолучитьОбъект();
		Иначе
			ДокументОбъект = Документы[МетаданныеДокумента.Имя].СоздатьДокумент();
		КонецЕсли;
		
		Если ДокументОбъект <> Неопределено Тогда
			ПричинаИсключения = 2;
			
			ДанныеДокумента = ПолучитьИзВременногоХранилища(АдресДанныхДокументаВХранилище);
			
			ЗаполнитьЗначенияСвойств(ДокументОбъект, ДанныеДокумента);
			
			ИмяТЧТовары			= ПараметрыРедактированияВидовЗапасов.ИмяТЧТовары;
			ИмяТЧВидыЗапасов	= ПараметрыРедактированияВидовЗапасов.ИмяТЧВидыЗапасов;
			
			ДокументОбъект[ИмяТЧТовары].Загрузить(ДанныеДокумента[ИмяТЧТовары]);
			ДокументОбъект[ИмяТЧВидыЗапасов].Загрузить(ПолучитьИзВременногоХранилища(АдресВидовЗапасовВХранилище));
			
			ПараметрыЗаполненияВидовЗапасовВручную = ЗапасыСервер.ПараметрыЗаполненияВидовЗапасовВручную();
			ПараметрыЗаполненияВидовЗапасовВручную.ИгнорироватьОшибкиКонтроляОстатков		= Истина;
			ПараметрыЗаполненияВидовЗапасовВручную.НеЗаполнятьВидыЗапасовПодакцизныхТоваров	= Истина;
			
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПерезаполнитьВидыЗапасов", Истина);
			ДокументОбъект.ДополнительныеСвойства.Вставить("ПараметрыЗаполненияВидовЗапасовВручную",
															ПараметрыЗаполненияВидовЗапасовВручную);
			
			ДокументОбъект.ЗаполнитьВидыЗапасовПриОбмене(Ложь, Неопределено);
			ЗаполнитьДеревоВидовЗапасов(ДокументОбъект[ИмяТЧВидыЗапасов].Выгрузить());
		Иначе
			ТекстСообщения	= НСтр("ru = 'Не удалось заполнить виды запасов.';
									|en = 'Cannot fill inventory owner attributes.'");
			КлючДанных		= ?(ЗначениеЗаполнено(Регистратор), Регистратор, Неопределено);
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстСообщения	= "";
		КлючДанных		= ?(ЗначениеЗаполнено(Регистратор), Регистратор, Неопределено);
		
		Если ПричинаИсключения = 1 Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось заблокировать %1. %2';
									|en = 'Cannot block %1. %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения,
										Строка(Регистратор),
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ИначеЕсли ПричинаИсключения = 2 Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось заполнить виды запасов по причине: %1';
									|en = 'Cannot fill inventory owner attributes. Reason: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		
		Если Не ПустаяСтрока(ТекстСообщения) Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных);
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
