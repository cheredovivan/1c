#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ОбработатьПереданныеПараметры(Отказ);
	УправлениеЭлементамиФормы();
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнформацияСостояниеЗагрузкиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьМестоХранения" Тогда
		ПоказатьЗначение(, МестоХранения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьОрганизацию" Тогда
		ПоказатьЗначение(, ОрганизацияСАТУРН);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ЗагрузитьОрганизацию" Тогда
		
		ОповещениеПослеВопроса = Новый ОписаниеОповещения("ПослеЗагрузкиОрганизации", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Загрузить владельца места хранения?';
							|en = 'Загрузить владельца места хранения?'");
		
		ПоказатьВопрос(ОповещениеПослеВопроса, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Загрузить(Команда)
	
	ЗагрузитьМестоХранения_ДлительнаяОперация();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗагрузитьМестоХранения_ДлительнаяОперация()
	
	ПараметрыОбработкиДокументов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыОбработкиДокументов();
	ПараметрыОбработкиДокументов.Ссылка             = ПредопределенноеЗначение("Справочник.МестаХраненияСАТУРН.ПустаяСсылка");
	ПараметрыОбработкиДокументов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор");
	
	ПараметрыОбработкиДокументов.ДополнительныеПараметры = Новый Структура();
	ПараметрыОбработкиДокументов.ДополнительныеПараметры.Вставить("Идентификатор", Идентификатор);
	
	ОписаниеПриЗавершении = Новый ОписаниеОповещения(
		"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиДокументов);
	
	ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
		ЭтотОбъект,
		ПараметрыОбработкиДокументов,
		ОписаниеПриЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементДанных Из Результат Цикл
		
		Если ЗначениеЗаполнено(ЭлементДанных.ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ЭлементДанных.ТекстОшибки);
			Возврат;
		КонецЕсли;
		
		Для Каждого ОбъектЗагрузки Из ЭлементДанных.Объект Цикл
			
			Если Не ТипЗнч(ОбъектЗагрузки) = Тип("СправочникСсылка.МестаХраненияСАТУРН") Тогда
				Продолжить;
			КонецЕсли;
			
			МестоХранения = ОбъектЗагрузки;
			Прервать;
			
		КонецЦикла;
		
		Если ЗначениеЗаполнено(МестоХранения) Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(МестоХранения) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = 'Загрузка из классификатора';
							|en = 'Загрузка из классификатора'");
	
	ТекстСообщения = СтрШаблон(НСтр("ru = 'Выполнена загрузка места хранения %1.';
									|en = 'Выполнена загрузка места хранения %1.'"), МестоХранения);
	ПоказатьОповещениеПользователя(ТекстЗаголовка,, ТекстСообщения, БиблиотекаКартинок.Информация32ГосИС);
	
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(МестоХранения);
	
	Закрыть(МестоХранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗагрузкиОрганизации(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьОрганизациюНаСервере();
	
	Если ЗначениеЗаполнено(ОрганизацияСАТУРН) Тогда
		
		ПараметрОповещения = ОрганизацияСАТУРН;
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(ОрганизацияСАТУРН, ПараметрОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьОрганизациюНаСервере()
	
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполненияЗапроса = ИнтерфейсСАТУРНВызовСервера.ОрганизацияПоИдентификатору(ИдентификаторОрганизации);
	
	Если Не ПустаяСтрока(РезультатВыполненияЗапроса.ТекстОшибки) Тогда
		
		ОбщегоНазначения.СообщитьПользователю(РезультатВыполненияЗапроса.ТекстОшибки);
		
	Иначе
		
		ДанныеОрганизации = ИнтерфейсСАТУРН.ДанныеОрганизации(РезультатВыполненияЗапроса.Элемент);
		ОрганизацияСАТУРН = ИнтеграцияСАТУРН.ЗагрузитьОрганизацию(ДанныеОрганизации);
		
	КонецЕсли;
	
	УстановитьОтображениеОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПереданныеПараметры(Отказ)

	Если Не ЗначениеЗаполнено(Параметры.Идентификатор) Тогда
		ВызватьИсключение НСтр("ru = 'Форма не предназначена для открытия без передачи в неё идентификатора места хранения.';
								|en = 'Форма не предназначена для открытия без передачи в неё идентификатора места хранения.'");
	Иначе
		
		Идентификатор = Параметры.Идентификатор;
		НеПоказыватьСостояниеЗагрузки = Параметры.НеПоказыватьСостояниеЗагрузки;
		Результат = ИнтерфейсСАТУРНВызовСервера.МестоХраненияПоИдентификатору(Идентификатор);
		
		Если Не ПустаяСтрока(Результат.ТекстОшибки) Тогда
			
			ЕстьОшибка  = Истина;
			
			Строки = Новый Массив;
			Строки.Добавить(
				Новый ФорматированнаяСтрока(
				НСтр("ru = 'Ошибка:';
					|en = 'Ошибка:'")));
			Строки.Добавить(" ");
			Строки.Добавить(Новый ФорматированнаяСтрока(Результат.ТекстОшибки,, ЦветаСтиля.ЦветТекстаПроблемаГосИС));

			ТекстОшибки = Новый ФорматированнаяСтрока(Строки);
			Элементы.ФормаЗагрузить.Доступность = Ложь;
			
		Иначе
			
			ЕстьОшибка  = Ложь;
			ДанныеМестаХранения = ИнтерфейсСАТУРН.ДанныеМестаХранения(Результат.Элемент);
			АдресХраненияПараметрыОбмена = ПоместитьВоВременноеХранилище(Результат.ПараметрыОбмена, УникальныйИдентификатор);
			
			ЗаполнитьДанныеМестаХранения(ДанныеМестаХранения);
			Элементы.ФормаЗагрузить.Доступность = Истина;
			
			ОпределитьНаличиеВИБ();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьНаличиеВИБ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	МестаХраненияСАТУРН.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.МестаХраненияСАТУРН КАК МестаХраненияСАТУРН
	|ГДЕ
	|	МестаХраненияСАТУРН.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		МестоХранения = Выборка.Ссылка;
		
		Строки = Новый Массив;
		Строки.Добавить(НСтр("ru = 'Место хранения уже загружено';
							|en = 'Место хранения уже загружено'"));
		Строки.Добавить(" (");
		Строки.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'открыть';
														|en = 'открыть'"),, ЦветаСтиля.ЦветГиперссылкиГосИС,, "ОткрытьМестоХранения"));
		Строки.Добавить(").");
		
		ИнформацияСостояниеЗагрузки = Новый ФорматированнаяСтрока(Строки);
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КлассификаторОрганизацийСАТУРН.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
	|ГДЕ
	|	КлассификаторОрганизацийСАТУРН.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторОрганизации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		ОрганизацияСАТУРН = Выборка.Ссылка;

	КонецЕсли;
	
	УстановитьОтображениеОрганизации();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеМестаХранения(ДанныеМестаХранения)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеМестаХранения);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеОрганизации()
	
	Если ЗначениеЗаполнено(ОрганизацияСАТУРН) Тогда
		
		ОписаниеОрганизации = Новый ФорматированнаяСтрока(
			Строка(ОрганизацияСАТУРН),,,,
			"ОткрытьОрганизацию");
		
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		
		ОписаниеОрганизации = Новый ФорматированнаяСтрока(
			НСтр("ru = '<не загружено>';
				|en = '<не загружено>'"),,
			ЦветаСтиля.ЦветГиперссылкиГосИС,,
			"ЗагрузитьОрганизацию");
		
	Иначе
		
		ОписаниеОрганизации = Новый ФорматированнаяСтрока(
			НСтр("ru = '<не указан>';
				|en = '<не указан>'"),,
			ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Если ЕстьОшибка Тогда
		
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаОшибка;
		Элементы.ФормаОтмена.Видимость         = Ложь;
		Элементы.ФормаЗакрыть.Видимость        = Истина;
		Элементы.ФормаЗагрузить.Видимость      = Истина;
		
	Иначе

		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаДанныеКлассификатора;
		
		Если ЗначениеЗаполнено(МестоХранения) Тогда
			
			Если НеПоказыватьСостояниеЗагрузки Тогда
				Элементы.ИнформацияСостояниеЗагрузки.Видимость = Ложь;
			КонецЕсли;
			
			Элементы.ФормаОтмена.Видимость    = Ложь;
			Элементы.ФормаЗакрыть.Видимость   = Истина;
			Элементы.ФормаЗагрузить.Видимость = Ложь;
			
		Иначе
			
			Элементы.ИнформацияСостояниеЗагрузки.Видимость = Ложь;
			
			Элементы.ФормаОтмена.Видимость    = Истина;
			Элементы.ФормаЗакрыть.Видимость   = Ложь;
			Элементы.ФормаЗагрузить.Видимость = Истина;
			
		КонецЕсли;
		
		Если Статус = Перечисления.СтатусыОбъектовСАТУРН.НаСогласовании Тогда
			
			Элементы.ИнформацияОбъектНаСогласовании.Видимость = Истина;
			ИнформацияОбъектНаСогласовании = НСтр("ru = 'Объект находится на согласовании. Ожидайте подтверждения Инспектора ТУ РСХН.';
													|en = 'Объект находится на согласовании. Ожидайте подтверждения Инспектора ТУ РСХН.'");
			
		Иначе
			
			Элементы.ИнформацияОбъектНаСогласовании.Видимость = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
