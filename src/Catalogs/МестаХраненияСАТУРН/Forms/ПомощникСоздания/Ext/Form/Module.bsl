#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	МестоХранения   = Параметры.МестоХранения;
	ЦветГиперссылки = ЦветаСтиля.ЦветГиперссылкиГосИС;
	ЦветПроблема    = ЦветаСтиля.ЦветТекстаПроблемаГосИС;
	ЖирныйШрифт     = Новый Шрифт(,, Истина);
	
	ВидОтношений = Перечисления.ВидыИспользуемыхТерриторийСАТУРН.ВСобственности;
	
	// СтандартныеПодсистемы.КонтактнаяИнформация
	ИнициализироватьПоляКонтактнойИнформации();
	// Конец СтандартныеПодсистемы.КонтактнаяИнформация
	
	Если ЗначениеЗаполнено(МестоХранения) Тогда
		
		РежимИзменения = Истина;
		МестоХраненияПриИзмененииНаСервере();
		
	Иначе
		РежимИзменения = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("ЗначенияЗаполнения")
		И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ЗначенияЗаполнения);
		
	КонецЕсли;
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
	СтандартныеПодсистемыСервер.СброситьРазмерыИПоложениеОкна(ЭтотОбъект);
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрНачинаетсяС(НавигационнаяСсылкаФорматированнойСтроки, "ОткрытьРезультат") Тогда
		
		ПоказатьЗначение(, МестоХранения);
		
	КонецЕсли;
	
КонецПроцедуры

#Область РедактированиеАдреса

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	
	Текст = Элемент.ТекстРедактирования;
	Если ПустаяСтрока(Текст) Тогда
		// Очистка данных, сбрасываем как представления, так и внутренние значения полей.
		АдресЗначение = "";
		Возврат;
	КонецЕсли;
	
	АдресСтрокой = Текст;
	// Формируем внутренние значения полей по тексту и параметрам формирования из
	// реквизита ВидКонтактнойИнформацииАдресаДоставки.
	ТекстСообщенияОбОшибке = АдресПриИзмененииНаСервере(Текст);
	
	ЗаполнитьОКТМОПриИзмененииАдреса();
	
	Если ЗначениеЗаполнено(ТекстСообщенияОбОшибке) Тогда
		ОчиститьСообщения();
		ИмяПоля = "АдресСтрокой";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщенияОбОшибке,, ИмяПоля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Если представление было изменено в поле и сразу нажата кнопка выбора, то необходимо 
	// привести данные в соответствие и сбросить внутренние поля для повторного разбора.
	Если Элемент.ТекстРедактирования <> АдресСтрокой Тогда
		АдресСтрокой   = Элемент.ТекстРедактирования;
		АдресЗначение  = "";
	КонецЕсли;
	
	// Данные для редактирования
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ВидКонтактнойИнформации", ВидКонтактнойИнформацииАдреса);
	ПараметрыОткрытия.Вставить("ЗначенияПолей",           АдресЗначение);
	ПараметрыОткрытия.Вставить("Представление",           АдресСтрокой);
	
	// Переопределямый заголовок формы, по умолчанию отобразятся данные по ВидКонтактнойИнформации.
	ПараметрыОткрытия.Вставить("Заголовок", НСтр("ru = 'Адрес места хранения';
												|en = 'Адрес места хранения'"));
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОчистка(Элемент, СтандартнаяОбработка)
	
	// Сбрасываем как представления, так и внутренние значения полей.
	АдресСтрокой  = "";
	АдресЗначение = "";
	ДанныеАдреса  = ДанныеАдресаКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
		// Отказ от выбора, данные неизменны.
		Возврат;
	КонецЕсли;
	
	АдресСтрокой  = ВыбранноеЗначение.Представление;
	АдресЗначение = ВыбранноеЗначение.КонтактнаяИнформация;
	ДанныеАдреса  = ДанныеАдресаКлиент();
	
	ЗаполнитьОКТМОПриИзмененииАдреса();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.СтраницаМестаХранения
		И ПроверитьЗаполнение() Тогда
		
		ДанныеМестаХранения = ИнтеграцияСАТУРНКлиентСервер.СтруктураДанныхМестаХранения();
		
		ДанныеМестаХранения.Наименование      = Наименование;
		ДанныеМестаХранения.Ссылка            = МестоХранения;
		ДанныеМестаХранения.Идентификатор     = Идентификатор;
		ДанныеМестаХранения.Широта            = Формат(Широта, "ЧГ=;");
		ДанныеМестаХранения.Долгота           = Формат(Долгота, "ЧГ=;");
		ДанныеМестаХранения.ОКТМО             = ОКТМО;
		ДанныеМестаХранения.ВидОтношений      = ВидОтношений;
		ДанныеМестаХранения.Адрес             = АдресСтрокой;
		
		ПараметрыОбработкиКлассификаторов = ИнтеграцияСАТУРНСлужебныйКлиентСервер.ПараметрыПередачиДанныхКлассификаторов();
		ПараметрыОбработкиКлассификаторов.Ссылка             = МестоХранения;
		
		ПараметрыОбработкиКлассификаторов.ДальнейшееДействие = ПредопределенноеЗначение("Перечисление.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные");
		
		ПараметрыОбработкиКлассификаторов.ДополнительныеПараметры = Новый Структура();
		ПараметрыОбработкиКлассификаторов.ДополнительныеПараметры.Вставить("ДанныеМестаХранения", ДанныеМестаХранения);
		
		ОписаниеПриЗавершении = Новый ОписаниеОповещения(
			"Подключаемый_ПриЗавершенииОперации", ЭтотОбъект, ПараметрыОбработкиКлассификаторов);
		
		ОжиданиеРезультата = Истина;
		ИнтеграцияСАТУРНКлиент.ПодготовитьКПередаче(
			ЭтотОбъект,
			ПараметрыОбработкиКлассификаторов,
			ОписаниеПриЗавершении);

		ОбработатьРезультатОбменаСАТУРН();
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМестаХранения;
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РедактированиеАдреса

&НаКлиенте
Функция ДанныеАдресаКлиент(ОчищатьСообщения = Истина, Отказ = Ложь)
	
	Попытка
		ДанныеАдресаСтруктурой = ИнтеграцияСАТУРНВызовСервера.ДанныеАдресаПоАдресуXML(АдресЗначение, АдресСтрокой);
	Исключение
		ТекстСообщения = НСтр("ru = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.';
								|en = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.'");
		Если ОчищатьСообщения Тогда
			ОчиститьСообщения();
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, "ПредставлениеАдреса");
		ДанныеАдресаСтруктурой = Неопределено;
		
		Отказ = Истина;
	КонецПопытки;
	
	Возврат ДанныеАдресаСтруктурой;
	
КонецФункции

&НаСервере
Процедура ИнициализироватьПоляКонтактнойИнформации()
	
	// Реквизит формы, контролирующий работу с адресом доставки.
	// Используемые поля аналогичны полям справочника ВидыКонтактнойИнформации.
	ВидКонтактнойИнформацииАдреса = Новый Структура;
	ВидКонтактнойИнформацииАдреса.Вставить("Тип",                          Перечисления.ТипыКонтактнойИнформации.Адрес);
	ВидКонтактнойИнформацииАдреса.Вставить("АдресТолькоРоссийский",        Истина);
	ВидКонтактнойИнформацииАдреса.Вставить("ВключатьСтрануВПредставление", Ложь);
	ВидКонтактнойИнформацииАдреса.Вставить("СкрыватьНеактуальныеАдреса",   Ложь);
	
	// Считываем данные из полей адреса в реквизиты для редактирования.
	АдресСтрокой = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(АдресЗначение);
	
КонецПроцедуры

&НаСервере
Функция АдресПриИзмененииНаСервере(Знач Представление)
	
	ТекстСообщенияОбОшибке = "";
	
	АдресЗначение = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияXMLПоПредставлению(Представление, ВидКонтактнойИнформацииАдреса);
	
	Попытка
		ДанныеАдресаСтруктурой = ИнтеграцияСАТУРНВызовСервера.ДанныеАдресаПоАдресуXML(АдресЗначение, АдресСтрокой);
	Исключение
		ТекстСообщенияОбОшибке = НСтр("ru = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.';
										|en = 'Не удалось прочитать данные адреса. Возможно не корректно введен регион. Повторите ввод.'");
		ДанныеАдресаСтруктурой = Неопределено;
	КонецПопытки;
	
	ДанныеАдреса = ДанныеАдресаСтруктурой;
	
	Возврат ТекстСообщенияОбОшибке;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьОКТМОПриИзмененииАдреса()
	
	Если Не ТипЗнч(ДанныеАдреса) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ДанныеАдреса.ДополнительныеКоды, "ОКТМО") Тогда
		Возврат;
	КонецЕсли;
	
	ОКТМО = ДанныеАдреса.ДополнительныеКоды.ОКТМО;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Подключаемый_ПриЗавершенииОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	ОжиданиеРезультата = Ложь;
	
	Если Результат.Количество() = 0 Тогда
		
		УправлениеЭлементамиФормы(ЭтотОбъект);
		Возврат;

	КонецЕсли;
	
	Для Каждого ЭлементОбработки Из Результат Цикл
		
		Если ЗначениеЗаполнено(ЭлементОбработки.ТекстОшибки) Тогда
			
			ВывестиИнформациюОбОшибке(ЭлементОбработки.ТекстОшибки);
			УправлениеЭлементамиФормы(ЭтотОбъект);
			
			Возврат;
			
		КонецЕсли;
		
		Если ЭлементОбработки.Объект.Количество() Тогда
			
			МестоХранения = ЭлементОбработки.Объект[0];
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВывестиИнформациюОУспешномРезультатеОбмена();
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
	ОповеститьОбЗаписиОбъектов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатОбменаСАТУРН()
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиИнформациюОбОшибке(ТекстОшибки)
	
	Строки = Новый Массив;
	Строки.Добавить(
		Новый ФорматированнаяСтрока(
			НСтр("ru = 'Ошибка:';
				|en = 'Ошибка:'")));
	Строки.Добавить(" ");
	Строки.Добавить(Новый ФорматированнаяСтрока(ТекстОшибки,, ЦветПроблема));
		
	ТекстОшибка = Новый ФорматированнаяСтрока(Строки);
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросОшибка;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиИнформациюОУспешномРезультатеОбмена()
	
	Строки = Новый Массив;
	Строки.Добавить(
		Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = 'На запрос о %1 места хранения %2 получен ответ.';
							|en = 'На запрос о %1 места хранения %2 получен ответ.'"), СтрокаТипОперации("Получение"), Наименование)));
			
	Строки.Добавить(" ");
	Строки.Добавить(
		Новый ФорматированнаяСтрока(
			СтрШаблон(НСтр("ru = '%1 место хранения';
							|en = '%1 место хранения'"), СтрокаТипОперации("Завершено"))));
	Строки.Добавить(" ");
	Строки.Добавить(Новый ФорматированнаяСтрока(Строка(МестоХранения),, ЦветГиперссылки,, "ОткрытьРезультат"));
	Строки.Добавить(".");
	
	ТекстРезультат = Новый ФорматированнаяСтрока(Строки);
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросРезультат;
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаТипОперации(СтадияОперации)

	ТипОперацииСтрокой = "";
	Если РежимИзменения Тогда
		Если СтадияОперации = "Завершено" Тогда
			ТипОперацииСтрокой = НСтр("ru = 'Изменено';
										|en = 'Изменено'");
		ИначеЕсли СтадияОперации = "Получение" Тогда
			ТипОперацииСтрокой = НСтр("ru = 'изменении';
										|en = 'изменении'");
		КонецЕсли;
	Иначе
		Если СтадияОперации = "Завершено" Тогда
			ТипОперацииСтрокой = НСтр("ru = 'Добавлено';
										|en = 'Добавлено'");
		ИначеЕсли СтадияОперации = "Получение" Тогда
			ТипОперацииСтрокой = НСтр("ru = 'добавлении';
										|en = 'добавлении'");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТипОперацииСтрокой;

КонецФункции

&НаКлиенте
Процедура ОповеститьОбЗаписиОбъектов()
	
	ПараметрОповещения = МестоХранения;
	ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(МестоХранения, ПараметрОповещения);
	
КонецПроцедуры

&НаСервере
Процедура МестоХраненияПриИзмененииНаСервере()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МестаХраненияСАТУРН.Идентификатор КАК Идентификатор
	|ИЗ
	|	Справочник.МестаХраненияСАТУРН КАК МестаХраненияСАТУРН
	|ГДЕ
	|	МестаХраненияСАТУРН.Ссылка = &Ссылка
	|");
	Запрос.УстановитьПараметр("Ссылка", МестоХранения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		РезультатВыполненияЗапроса = ИнтерфейсСАТУРНВызовСервера.МестоХраненияПоИдентификатору(Выборка.Идентификатор);
		Если Не ПустаяСтрока(РезультатВыполненияЗапроса.ТекстОшибки) Тогда
			
			Строки = Новый Массив;
			Строки.Добавить(
				Новый ФорматированнаяСтрока(
					НСтр("ru = 'Ошибка:';
						|en = 'Ошибка:'")));
			Строки.Добавить(" ");
			Строки.Добавить(Новый ФорматированнаяСтрока(РезультатВыполненияЗапроса.ТекстОшибки,, ЦветПроблема));
			
			ТекстОшибка = Новый ФорматированнаяСтрока(Строки);
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросОшибка;
			
		Иначе
			
			ЭлементДанных = РезультатВыполненияЗапроса.Элемент;
			ДанныеМестаХранения = ИнтерфейсСАТУРН.ДанныеМестаХранения(ЭлементДанных);
			
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеМестаХранения);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#Область УправлениеЭлементамиФормы

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)
	
	Если Форма.РежимИзменения Тогда
		
		Форма.Заголовок = НСтр("ru = 'Изменение места хранения';
								|en = 'Изменение места хранения'");
		
	Иначе
		
		Форма.Заголовок = НСтр("ru = 'Создание места хранения';
								|en = 'Создание места хранения'");
		
	КонецЕсли;
	
	УправлениеЭлементамиНавигацииПомощника(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭлементамиНавигацииПомощника(Форма)

	Элементы = Форма.Элементы;
	Элементы.Закрыть.Заголовок = НСтр("ru = 'Закрыть';
										|en = 'Закрыть'");
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаМестаХранения Тогда
		
		Если Форма.ОжиданиеРезультата Тогда
			
			Форма.Доступность = Ложь;
			
		Иначе
		
			Форма.Доступность = Истина;
		
			Элементы.Назад.Видимость   = Ложь;
			Элементы.Далее.Видимость   = Истина;
			Элементы.Закрыть.Видимость = Истина;
			Элементы.Далее.Заголовок   = НСтр("ru = 'Далее';
												|en = 'Далее'");
			
			Элементы.МестоХранения.Видимость = Форма.РежимИзменения;
			
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросОшибка Тогда
		
		Форма.Доступность = Истина;
		
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Ложь;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаЗапросРезультат Тогда
		
		Форма.Доступность = Истина;
		
		Элементы.Назад.Видимость = Ложь;
		Элементы.Далее.Видимость = Ложь;
		Элементы.Далее.Заголовок = НСтр("ru = 'Готово';
										|en = 'Готово'");
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаИнформация Тогда
		
		Форма.Доступность = Истина;
		
		Элементы.Назад.Видимость = Ложь;
		Элементы.Далее.Видимость = Ложь;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти