#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
	
		Если Параметры = Неопределено Тогда
			Параметры = Новый Структура();
		КонецЕсли;
		
		Параметры.Вставить("РежимВыбора", Истина);
		
		ВыбраннаяФорма       = "Справочник.МестаХраненияСАТУРН.Форма.ФормаСписка";
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяТаблицаСопоставленияМестХранения() Экспорт
	
	ВозвращаемоеЗначение = Новый ТаблицаЗначений();
	ВозвращаемоеЗначение.Колонки.Добавить("ОрганизацияСАТУРН", Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийСАТУРН"));
	ВозвращаемоеЗначение.Колонки.Добавить("МестоХранения",     Новый ОписаниеТипов("СправочникСсылка.МестаХраненияСАТУРН"));
	ВозвращаемоеЗначение.Колонки.Добавить("ТорговыйОбъект");
	ВозвращаемоеЗначение.Колонки.Добавить("ПроизводственныйОбъект");
	
	ВозвращаемоеЗначение.Индексы.Добавить("ОрганизацияСАТУРН, МестоХранения");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ЗаполнитьТаблицуСопоставлений(ТаблицаСопоставлений) Экспорт
	
	ВозвращаемоеЗначение  = Новый Соответствие();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.ОрганизацияСАТУРН,
		|	ИсходнаяТаблица.МестоХранения
		|ПОМЕСТИТЬ ИсходнаяТаблица
		|ИЗ
		|	&ИсходнаяТаблица КАК ИсходнаяТаблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИсходнаяТаблица.ОрганизацияСАТУРН                                  КАК ОрганизацияСАТУРН,
		|	ИсходнаяТаблица.МестоХранения                                      КАК МестоХранения,
		|	КлассификаторОрганизацийСАТУРНМестаХранения.ТорговыйОбъект         КАК ТорговыйОбъект,
		|	КлассификаторОрганизацийСАТУРНМестаХранения.ПроизводственныйОбъект КАК ПроизводственныйОбъект
		|ИЗ
		|	ИсходнаяТаблица КАК ИсходнаяТаблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК
		|			КлассификаторОрганизацийСАТУРНМестаХранения
		|		ПО ИсходнаяТаблица.ОрганизацияСАТУРН = КлассификаторОрганизацийСАТУРНМестаХранения.Ссылка
		|		И ИсходнаяТаблица.МестоХранения = КлассификаторОрганизацийСАТУРНМестаХранения.МестоХранения";
	
	Запрос.УстановитьПараметр("ИсходнаяТаблица", ТаблицаСопоставлений);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("ОрганизацияСАТУРН, МестоХранения");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаДетальныеЗаписи);
		
		ПоискСтрок = ТаблицаСопоставлений.НайтиСтроки(СтруктураПоиска);
		Если ПоискСтрок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = ПоискСтрок[0];
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ТорговыйОбъект) Тогда
			
			Если СтрокаТаблицы.ТорговыйОбъект = Неопределено Тогда
				СтрокаТаблицы.ТорговыйОбъект = ВыборкаДетальныеЗаписи.ТорговыйОбъект
			ИначеЕсли ТипЗнч(СтрокаТаблицы.ТорговыйОбъект) = Тип("Массив") Тогда
				СтрокаТаблицы.ТорговыйОбъект.Добавить(ВыборкаДетальныеЗаписи.ТорговыйОбъект);
			Иначе
				Значения = Новый Массив();
				Значения.Добавить(СтрокаТаблицы.ТорговыйОбъект);
				Значения.Добавить(ВыборкаДетальныеЗаписи.ТорговыйОбъект);
				СтрокаТаблицы.ТорговыйОбъект = Значения;
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ПроизводственныйОбъект) Тогда
			
			Если СтрокаТаблицы.ПроизводственныйОбъект = Неопределено Тогда
				СтрокаТаблицы.ПроизводственныйОбъект = ВыборкаДетальныеЗаписи.ПроизводственныйОбъект
			ИначеЕсли ТипЗнч(СтрокаТаблицы.ПроизводственныйОбъект) = Тип("Массив") Тогда
				СтрокаТаблицы.ПроизводственныйОбъект.Добавить(ВыборкаДетальныеЗаписи.ПроизводственныйОбъект);
			Иначе
				Значения = Новый Массив();
				Значения.Добавить(СтрокаТаблицы.ПроизводственныйОбъект);
				Значения.Добавить(ВыборкаДетальныеЗаписи.ПроизводственныйОбъект);
				СтрокаТаблицы.ПроизводственныйОбъект = Значения;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * GUID - Строка
// * Идентификатор - Строка
// * Статус - ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка - 
// * ДатаСоздания - Дата - 
// * ДатаИзменения - Дата - 
// * Наименование - Строка
// * Адрес - Строка
// * НаименованиеСубъектаРФ - Строка
// * ЭтоПроизводственнаяПлощадка - Булево - 
// * ЭтоСкладВременногоХранения - Булево - 
// * ГеографическиеКоординаты - Строка
// * ГеографическаяФорма - Строка
// * Комментарий - Строка
// * ДанныеОрганизации - Неопределено - 
// * ИдентификаторОрганизации - Строка - 
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеМестаХранения             = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеМестаХранения.Вставить("Идентификатор",               Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеМестаХранения.Вставить("Статус",                      ИнтерфейсСАТУРН.Статус(ЭлементДанных.lcState));
	ДанныеМестаХранения.Вставить("Наименование",                ЭлементДанных.name);
	ДанныеМестаХранения.Вставить("Адрес",                       ЭлементДанных.addr);
	ДанныеМестаХранения.Вставить("НаименованиеСубъектаРФ",      ЭлементДанных.subName);
	ДанныеМестаХранения.Вставить("ЭтоПроизводственнаяПлощадка", ЭлементДанных.isProducingStoreArea = Истина Или ЭлементДанных.isProducingStoreArea = "true");
	ДанныеМестаХранения.Вставить("ЭтоСкладВременногоХранения",  ЭлементДанных.isTempStoringArea = Истина Или ЭлементДанных.isTempStoringArea = "true");
	ДанныеМестаХранения.Вставить("ГеографическиеКоординаты",    ЭлементДанных.location);
	ДанныеМестаХранения.Вставить("ГеографическаяФорма",         ЭлементДанных.geoForm);
	ДанныеМестаХранения.Вставить("Комментарий",                 ЭлементДанных.description);
	
	ИнтеграцияСАТУРНСлужебный.ДополнитьДанныеОбъектаОбщимиДанными(ДанныеМестаХранения, ЭлементДанных);
	
	// инициализация значений, которые могут отсутствовать в структуре данных
	ДанныеМестаХранения.Вставить("ДанныеОрганизации",        Неопределено);
	ДанныеМестаХранения.Вставить("ИдентификаторОрганизации", Формат(ЭлементДанных.ownerId, ФорматОтображенияИдентификатора));
	
	Возврат ДанныеМестаХранения;
	
КонецФункции

Функция ОрганизацииВладельцыМестаХранения(МестоХранения) Экспорт
	
	МассивВладельцев = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
		|	КлассификаторОрганизацийСАТУРНМестаХранения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК КлассификаторОрганизацийСАТУРНМестаХранения
		|ГДЕ
		|	НЕ КлассификаторОрганизацийСАТУРНМестаХранения.Ссылка.ПометкаУдаления
		|	И КлассификаторОрганизацийСАТУРНМестаХранения.МестоХранения = &МестоХранения";
	
	Запрос.УстановитьПараметр("МестоХранения", МестоХранения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивВладельцев.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	Возврат МассивВладельцев;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					Продолжить;
				КонецЕсли;
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Если ТипЗнч(ПараметрыИсходящегоСообщенияОснования) = Тип("Структура") И ПараметрыИсходящегоСообщенияОснования.Свойство("ПорционнаяЗагрузка")
			И ПараметрыИсходящегоСообщенияОснования.ПорционнаяЗагрузка Тогда
			
			НомерТекущейСтраницы = ПараметрыИсходящегоСообщенияОснования.НомерСтраницы;
			РазмерПорции         = ПараметрыИсходящегоСообщенияОснования.РазмерПорции;
			
			Если ВходящиеДанные.Количество() < РазмерПорции Тогда
				Возврат;
			КонецЕсли;
			
			НомерТекущейСтраницы = НомерТекущейСтраницы + 1;
			
			СообщенияJSON = ЗагрузкаКлассификатораМестаХраненияJSON("", ПараметрыИсходящегоСообщенияОснования.ИдентификаторОрганизации, НомерТекущейСтраницы, РазмерПорции);
			ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
			
		КонецЕсли;
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.МестоХраненияСозданиеКлассификатора Тогда
		
		СообщенияJSON = Новый Массив;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Идентификатор = ПолученныеДанные.id;
		
		СправочникОбъект               = СоздатьЭлемент();
		СправочникОбъект.Идентификатор = Идентификатор;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта,, "Идентификатор");
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ИзмененныеОбъекты.Добавить(СправочникОбъект.Ссылка);
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ,, Истина);
		СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		
		СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеJSON.АргументыОперации, "id", Идентификатор);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.МестоХраненияЗапросКлассификатора;
КонецФункции

#Область ПоискСсылок

Функция МестоХранения(ЗначениеИдентификатора, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Идентификатор = Формат(ЗначениеИдентификатора, "ЧГ=0;");
	
	Если Не ЗначениеЗаполнено(Идентификатор)
		Или ЗначениеИдентификатора = -1 Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ИмяТаблицы       = Метаданные.Справочники.МестаХраненияСАТУРН.ПолноеИмя();
	СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				ДанныеОбъекта = ИнтеграцияСАТУРНСлужебный.ДанныеОбъекта(
					Идентификатор,
					Метаданные.Справочники.МестаХраненияСАТУРН, ПараметрыОбмена);
				Если ДанныеОбъекта = Неопределено Тогда
					СправочникСсылка = СоздатьМестоХранения(Идентификатор, ПараметрыОбмена);
					ИнтеграцияСАТУРНСлужебный.ДобавитьКЗагрузке(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка, ОрганизацияСАТУРН);
				Иначе
					СправочникСсылка = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,, Ложь, ОрганизацияСАТУРН);
				КонецЕсли;
				
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Места хранения с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Места хранения с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Места хранения с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Места хранения с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с Местами хранения';
					|en = 'Работа с Местами хранения'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьОбъект(ДанныеМестаХранения, ПараметрыОбмена, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ДанныеМестаХранения = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового = Ложь;
	ИмяТаблицы   = Метаданные.Справочники.МестаХраненияСАТУРН.ПолноеИмя();
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, ДанныеМестаХранения.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникСсылка = ПолучитьСсылку(ИдентификаторОбъекта);
			СправочникОбъект.УстановитьСсылкуНового(СправочникСсылка);
			ЗаписьНового = Истина;
			
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
		
	Иначе
		СправочникСсылка = СправочникОбъект.Ссылка;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Наименование                = ДанныеМестаХранения.Наименование;
	СправочникОбъект.Идентификатор               = ДанныеМестаХранения.Идентификатор;
	СправочникОбъект.Статус                      = ДанныеМестаХранения.Статус;
	СправочникОбъект.Адрес                       = ДанныеМестаХранения.Адрес;
	СправочникОбъект.ЭтоПроизводственнаяПлощадка = ДанныеМестаХранения.ЭтоПроизводственнаяПлощадка;
	СправочникОбъект.ЭтоСкладВременногоХранения  = ДанныеМестаХранения.ЭтоСкладВременногоХранения;
	СправочникОбъект.Комментарий                 = ДанныеМестаХранения.Комментарий;
	
	Если ЗначениеЗаполнено(ДанныеМестаХранения.ИдентификаторОрганизации) Тогда
		
		Организация = Справочники.КлассификаторОрганизацийСАТУРН.Организация(
			ДанныеМестаХранения.ИдентификаторОрганизации,
			ПараметрыОбмена,
			ОрганизацияСАТУРН);
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		
		СтруктураОтбора = Новый Структура("МестоХранения", СправочникСсылка);
		СтрокаСвязкиОрганизацииСМестомХранения = ОрганизацияОбъект.МестаХранения.НайтиСтроки(СтруктураОтбора);
		
		Если СтрокаСвязкиОрганизацииСМестомХранения.Количество() = 0 Тогда
			
			НоваяСтрокаПривязки = ОрганизацияОбъект.МестаХранения.Добавить();
			НоваяСтрокаПривязки.МестоХранения = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СправочникОбъект.ТребуетсяЗагрузка = Ложь;
	СправочникОбъект.Записать();
	
	ОрганизацияОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеМестаХранения.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция СоздатьМестоХранения(Идентификатор, ПараметрыОбмена)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор     = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка = Истина;
	СправочникОбъект.Наименование      = НСтр("ru = '<Требуется загрузка>';
												|en = '<Требуется загрузка>'");
	
	РеквизитыСоздания = ИнтеграцияСАТУРН.РеквизитыСозданияОбъекта(
		Идентификатор, СправочникОбъект.Метаданные(), ПараметрыОбмена);
	
	Если РеквизитыСоздания <> Неопределено Тогда
		Для Каждого КлючИЗначение Из РеквизитыСоздания Цикл
			СправочникОбъект[КлючИЗначение.Ключ] = КлючИЗначение.Значение;
		КонецЦикла;
	КонецЕсли;
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбработки       = Неопределено;
	ДанныеОбъекта            = Неопределено;
	Идентификатор            = "";
	ИдентификаторОрганизации = "";
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПараметрыОбработкиДокумента")
		И ТипЗнч(ДополнительныеПараметры.ПараметрыОбработкиДокумента) = Тип("Структура")
		И ДополнительныеПараметры.ПараметрыОбработкиДокумента.Свойство("ДополнительныеПараметры") Тогда
		
		ПараметрыОбработки = ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры;
		
		Если ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
			
			Если ПараметрыОбработки.Свойство("ДанныеМестаХранения") Тогда
				ДанныеОбъекта = ПараметрыОбработки.ДанныеМестаХранения;
			ИначеЕсли ПараметрыОбработки.Свойство("Идентификатор") Тогда
				Идентификатор = ПараметрыОбработки.Идентификатор;
			ИначеЕсли ПараметрыОбработки.Свойство("ИдентификаторОрганизации") Тогда
				ИдентификаторОрганизации = ПараметрыОбработки.ИдентификаторОрганизации;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СообщенияJSON = Новый Массив;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные Тогда
		
		Если ДанныеОбъекта <> Неопределено Тогда
			
			Возврат СозданиеКлассификатораМестаХраненияJSON(СправочникСсылка, ДанныеОбъекта);
			
		КонецЕсли;
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаКлассификатораМестаХраненияJSON(Идентификатор, ИдентификаторОрганизации);
		
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

// Формирует JSON сообщения для создания/изменения места хранения.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.МестаХраненияСАТУРН - Ссылка на организацию.
//  ДанныеОбъекта - Структура из См. ИнтеграцияСАТУРНКлиентСервер.СтруктураДанныхМестаХранения - описание справочника
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
//
Функция СозданиеКлассификатораМестаХраненияJSON(СправочникСсылка, ДанныеОбъекта)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = СправочникСсылка;
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.МестоХраненияСозданиеКлассификатора;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СправочникСсылка);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
	СообщениеJSON.ПараметрыЗапроса.Вставить("ДанныеОбъекта", ДанныеОбъекта);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "name",         ДанныеОбъекта.Наименование);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "oktmo",        ДанныеОбъекта.ОКТМО);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "locationLon",  ДанныеОбъекта.Долгота);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "locationLat",  ДанныеОбъекта.Широта);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "addr",         ДанныеОбъекта.Адрес);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "relationType", ИнтерфейсСАТУРН.ВидИспользуемойТерритории(ДанныеОбъекта.ВидОтношений));
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗагрузкаКлассификатораМестаХраненияJSON(Идентификатор, ИдентификаторОрганизации = "", НомерСтраницы = 0, РазмерПорции = 100) Экспорт
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации      = СообщениеJSON.АргументыОперации;
	
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		
		АргументыОперации.pos  = 0;
		АргументыОперации.size = 100;
		
		Если Не ТипЗнч(Идентификатор) = Тип("Массив") Тогда
			Идентификатор = Формат(Идентификатор, "ЧГ=;");
		КонецЕсли;
		
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "id", Идентификатор);
		
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		
		АргументыОперации.pos  = НомерСтраницы * РазмерПорции;
		АргументыОперации.size = РазмерПорции;
		
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "ownerId", Формат(ИдентификаторОрганизации, "ЧГ=;"));
		
		СообщениеJSON.ПараметрыЗапроса = Новый Структура();
		СообщениеJSON.ПараметрыЗапроса.Вставить("ПорционнаяЗагрузка",       Истина);
		СообщениеJSON.ПараметрыЗапроса.Вставить("НомерСтраницы",            АргументыОперации.pos);
		СообщениеJSON.ПараметрыЗапроса.Вставить("РазмерПорции",             АргументыОперации.size);
		СообщениеJSON.ПараметрыЗапроса.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		
	Иначе
		СообщениеJSON.ТекстОшибки = 
			НСтр("ru = 'Некорректный вызов метода загрузки:
			|Для загрузки места хранения должен быть передан идентификатор объекта или идентификатор организации-владельца.';
			|en = 'Некорректный вызов метода загрузки:
			|Для загрузки места хранения должен быть передан идентификатор объекта или идентификатор организации-владельца.'");
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
