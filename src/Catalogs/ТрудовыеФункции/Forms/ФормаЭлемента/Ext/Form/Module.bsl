#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Параметры.ЗначенияЗаполнения.Свойство("ОКЗКод")
			И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ОКЗКод) Тогда
			
			Объект.КодПоОКЗ = Справочники.КлассификаторЗанятий.НайтиПоКоду(Параметры.ЗначенияЗаполнения.ОКЗКод);
		КонецЕсли;
		
		РабочаяДата = ОбщегоНазначения.РабочаяДатаПользователя();
		УстановитьОтображениеЭлементов(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	РабочаяДата = ОбщегоНазначения.РабочаяДатаПользователя();
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы
		И ОписаниеОповещенияОЗакрытии <> Неопределено
		И Не Модифицированность
		И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияОЗакрытии, Объект.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОКПДТР2026КодНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПослеДобавленияКодовИзКлассификатора", ЭтотОбъект);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВерсияКлассификатора", ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026());
	СтруктураПараметров.Вставить("ОКПДТРКод", Объект.ОКПДТР2026Код);
	
	ОткрытьФорму("Справочник.ТрудовыеФункции.Форма.ВыборОКПДТР", СтруктураПараметров, ЭтотОбъект, , , , Оповещение, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОКПДТР2026КодОчистка(Элемент, СтандартнаяОбработка)
	Объект.ОКПДТР2026Категория = "";
	Объект.КодПоОКЗ2026 = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ОКПДТРКодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПослеДобавленияКодовИзКлассификатора", ЭтотОбъект);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОКПДТРКод", Объект.ОКПДТРКод + Объект.ОКПДТРКЧ);
	
	ОткрытьФорму("Справочник.ТрудовыеФункции.Форма.ВыборОКПДТР", СтруктураПараметров, ЭтотОбъект, , , , Оповещение, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОКПДТРКодСтатистикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ПослеДобавленияКодовИзКлассификатораСтатистика", ЭтотОбъект);
	КодДляОткрытия = ?(Объект.ОКПДТРКодСтатистика="",Объект.ОКПДТРКод,Объект.ОКПДТРКодСтатистика);
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ОКПДТРКодСтатистика", КодДляОткрытия);
	
	ОткрытьФорму("Справочник.ТрудовыеФункции.Форма.ВыборОКПДТРСтатистика", СтруктураПараметров, 
		ЭтотОбъект, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОКПДТРКодОчистка(Элемент, СтандартнаяОбработка)
	Объект.ОКПДТРКЧ = "";
	Объект.ОКПДТРКатегория = "";
	Объект.КодПоОКЗ = Неопределено;
КонецПроцедуры

&НаКлиенте
Процедура ОКПДТРКодСтатистикаОчистка(Элемент, СтандартнаяОбработка)
	Объект.КодПоОКЗСтатистика = "";
	Объект.ОКПДТРКатегорияСтатистика = "";
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	УстановитьОтображениеЭлементов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеДобавленияКодовИзКлассификатора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Объект, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеДобавленияКодовИзКлассификатораСтатистика(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Объект.ОКПДТРКодСтатистика	= Результат.ОКПДТРКодСтатистика;
		Объект.КодПоОКЗСтатистика 	= Результат.ОКЗКодСтатистика;
		Объект.ОКПДТРКатегорияСтатистика 	= Результат.ОКПДТРКатегорияСтатистика;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЭлементов(УправляемаяФорма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ПредупрежедниеОДлинеОписания",
		"Видимость",
		СтрДлина(УправляемаяФорма.Объект.Описание) > 255);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"ГруппаОКПДТР",
		"ТолькоПросмотр",
		УправляемаяФорма.РабочаяДата >= ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026());
	
	Если Не ПустаяСтрока(УправляемаяФорма.Объект.ИмяПредопределенныхДанных) Тогда
		
		УправляемаяФорма.ТолькоПросмотр = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"ГруппаОКПДТР",
			"Видимость",
			Ложь);
			
		УправляемаяФорма.ТолькоПросмотр = Истина;
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"ГруппаСтатистика",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"Описание",
			"Видимость",
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			УправляемаяФорма.Элементы,
			"КодПрофессиональнойДеятельности",
			"Маска",
			"NNNNNNNNNN");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
