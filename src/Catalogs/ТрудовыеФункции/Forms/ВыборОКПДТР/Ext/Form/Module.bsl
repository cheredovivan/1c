#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ОКПДТРКод", ОКПДТРКодДолжности);
	Параметры.Свойство("ВерсияКлассификатора", ВерсияКлассификатора);
	Если ВерсияКлассификатора < ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026() Тогда
		ЗаполнитьКлассификатор("КлассификаторОКПДТРДолжности");
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйУчет.ОбразованияФизическихЛиц") Тогда
			ЗаполнитьКлассификатор("КлассификаторОКПДТРПрофессии");
		КонецЕсли;
	Иначе
		ЗаполнитьКлассификатор("КлассификаторОКПДТР2026Должности");
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйУчет.ОбразованияФизическихЛиц") Тогда
			ЗаполнитьКлассификатор("КлассификаторОКПДТР2026Профессии");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(ОКПДТРКодДолжности) Тогда
		Если Не НайтиИПозиционировать(КлассификаторОКПДТРДолжности, "КлассификаторОКПДТРДолжности", ТекущаяСтрокаДолжностей,
			ОКПДТРКодДолжности, "Code") Тогда
			НайтиИПозиционировать(КлассификаторОКПДТРПрофессии, "КлассификаторОКПДТРПрофессии", ТекущаяСтрокаПрофессий,
				ОКПДТРКодДолжности, "Code");
		КонецЕсли;
	КонецЕсли;
	ТекущаяСтрокаДолжностей = Элементы.КлассификаторОКПДТРДолжности.ТекущаяСтрока;
	ТекущаяСтрокаПрофессий = Элементы.КлассификаторОКПДТРПрофессии.ТекущаяСтрока;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификаторОКПДТРДолжности

&НаКлиенте
Процедура КлассификаторДолжностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьИЗакрыть("КлассификаторОКПДТРДолжности");
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторОКПДТРДолжностиПриАктивизацииПоля(Элемент)
	ТекущаяСтрокаДолжностей = Элементы.КлассификаторОКПДТРДолжности.ТекущаяСтрока;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКлассификаторОКПДТРПрофессии

&НаКлиенте
Процедура КлассификаторПрофессииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьИЗакрыть("КлассификаторОКПДТРПрофессии");
КонецПроцедуры

&НаКлиенте
Процедура КлассификаторОКПДТРПрофессииПриАктивизацииПоля(Элемент)
	ТекущаяСтрокаПрофессий = Элементы.КлассификаторОКПДТРПрофессии.ТекущаяСтрока;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИскатьДолжность(Команда)
	НайтиИПозиционировать(КлассификаторОКПДТРДолжности, "КлассификаторОКПДТРДолжности", 
		ТекущаяСтрокаДолжностей, ТекстПоискаДолжности);
КонецПроцедуры

&НаКлиенте
Процедура ИскатьПрофессию(Команда)
	НайтиИПозиционировать(КлассификаторОКПДТРПрофессии, "КлассификаторОКПДТРПрофессии", 
		ТекущаяСтрокаПрофессий, ТекстПоискаПрофессии);
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	Если ЭтотОбъект.ТекущийЭлемент = Элементы.КлассификаторОКПДТРДолжности Тогда
		ВыбратьИЗакрыть("КлассификаторОКПДТРДолжности");
	ИначеЕсли ЭтотОбъект.ТекущийЭлемент = Элементы.КлассификаторОКПДТРПрофессии Тогда
		ВыбратьИЗакрыть("КлассификаторОКПДТРПрофессии");
	Иначе 
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана строка классификатора.';
														|en = 'No classifier line selected.'"));
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Загружает классификатор из макета в дерево значений.
//
&НаСервере
Процедура ЗаполнитьКлассификатор(ИмяКлассификатора)
	
	КлассификаторXML = ПолучитьОбщийМакет(ИмяКлассификатора).ПолучитьТекст();
	КлассификаторОКПДТР = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	КлассификаторДерево = Новый ДеревоЗначений;
	КлассификаторДерево.Колонки.Добавить("Code");
	КлассификаторДерево.Колонки.Добавить("Name");
	КлассификаторДерево.Колонки.Добавить("OKZ");
	КлассификаторДерево.Колонки.Добавить("Категория");
		
	ТекущийУровень = 0;
	ТекущаяСтрока = Неопределено;
	Для Каждого СтрокаКлассификатора Из КлассификаторОКПДТР Цикл
		Уровень = Число(СтрокаКлассификатора.level);
		Если Уровень > ТекущийУровень Тогда
			Строка = ТекущаяСтрока.Строки.Добавить();
		Иначе
			Строка = КлассификаторДерево.Строки.Добавить();
			ТекущийУровень = Уровень;
			ТекущаяСтрока  = Строка;
		КонецЕсли;
		Строка.Code = СтрокаКлассификатора.Code;
		Строка.Name = СтрокаКлассификатора.Name;
		Строка.OKZ  = СтрокаКлассификатора.OKZ;
		Если ИмяКлассификатора = "КлассификаторОКПДТРДолжности"
			Или ИмяКлассификатора = "КлассификаторОКПДТР2026Должности" Тогда
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СтрокаКлассификатора, "Категория") Тогда
				Строка.Категория = СтрокаКлассификатора.Категория;
			Иначе
				Строка.Категория = СтрокаКлассификатора.Category;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ИмяКлассификатора = "КлассификаторОКПДТРДолжности"
		Или ИмяКлассификатора = "КлассификаторОКПДТР2026Должности" Тогда
		
		ИмяРеквизита = "КлассификаторОКПДТРДолжности";
	Иначе
		ИмяРеквизита = "КлассификаторОКПДТРПрофессии";
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(КлассификаторДерево, ИмяРеквизита);
	
КонецПроцедуры

&НаКлиенте
Функция НайтиИПозиционировать(Классификатор, ИмяТаблицыДерева, ТекущаяСтрокаКлассификатора, ТекстПоиска, ПолеПоиска = Неопределено)
	
	Результат = Ложь;
	
	Если ПолеПоиска = Неопределено Тогда
		ПолеПоиска= ?(СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ТекстПоиска), "Code", "Name");
	КонецЕсли;
		
	ИдентификаторСтрокиПоиска = ТекущаяСтрокаКлассификатора;
	
	// если не найдено от текущей строки, то поиск происходит с начала дерева
	Если Не ОбщегоНазначенияБЗККлиентСервер.СледующийИдентификаторСтрокиДереваПоЗначениюПоля(
			ИдентификаторСтрокиПоиска,
			Классификатор, 
			ПолеПоиска, 
			ТекстПоиска) Тогда
		ИдентификаторСтрокиПоиска = ОбщегоНазначенияБЗККлиентСервер.ИдентификаторСтрокиДереваПоЗначениюПоля(
				Классификатор, 
				ПолеПоиска, 
				ТекстПоиска);
	КонецЕсли;
	
	Если ИдентификаторСтрокиПоиска <> Неопределено Тогда
		ТекущаяСтрокаКлассификатора = ИдентификаторСтрокиПоиска;
		Результат = Истина;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(ЭтотОбъект.Элементы, ИмяТаблицыДерева, 
		"ТекущаяСтрока", ТекущаяСтрокаКлассификатора);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыбратьИЗакрыть(ИмяКлассификатора)
	
	Результат = Новый Структура;
	Если ВерсияКлассификатора = ЭлектронныеТрудовыеКнижкиКлиентСервер.ДатаВсутпленияВСилуОКПДТР2026() Тогда
		Результат.Вставить("ОКПДТР2026Код", Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.Code);
		Если ИмяКлассификатора = "КлассификаторОКПДТРДолжности" Тогда
			Результат.Вставить("ОКПДТР2026Категория", Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.Категория);
		Иначе
			Результат.Вставить("ОКПДТР2026Категория", 4);
		КонецЕсли;
		Если ЗначениеЗаполнено(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.OKZ) Тогда
			Результат.Вставить("КодПоОКЗ2026", ЗначениеОКЗПоКоду(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.OKZ));
		Иначе
			Результат.Вставить("КодПоОКЗ2026", Неопределено);
		КонецЕсли;
	Иначе
		Результат.Вставить("ОКПДТРКод", Лев(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.Code, 5));
		Результат.Вставить("ОКПДТРКЧ", Прав(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.Code, 1));
		Если ИмяКлассификатора = "КлассификаторОКПДТРДолжности" Тогда
			Результат.Вставить("ОКПДТРКатегория", Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.Категория);
		Иначе
			Результат.Вставить("ОКПДТРКатегория", 4);
		КонецЕсли;
		Если ЗначениеЗаполнено(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.OKZ) Тогда
			Результат.Вставить("КодПоОКЗ", ЗначениеОКЗПоКоду(Элементы.Найти(ИмяКлассификатора).ТекущиеДанные.OKZ));
		Иначе
			Результат.Вставить("КодПоОКЗ", Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Закрыть(Результат);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеОКЗПоКоду(КодОКЗ)
	
	Возврат Справочники.КлассификаторЗанятий.НайтиПоКоду(КодОКЗ);
	
КонецФункции

#КонецОбласти