///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" Или ВидФормы = "ФормаВыбора" Тогда
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "Обработка.НастройкиПлатежныхСистем.Форма.Форма";
		НастройкиПлатежныхСистемСлужебный.ОбработкаПолученияФормыСписка(
			"ИнтернетЭквайринг",
			ВидФормы,
			Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Определяет наличие настроек подключения к Интернет-эквайрингу.
//
// Возвращаемое значение:
//  Булево - Истина, если есть настройки подключения.
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК Признак
		|ИЗ
		|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК Настройки
		|ГДЕ
		|	НЕ ЭтоГруппа";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Сохраняет настройки подключения в базу.
// 
// Параметры:
//  ПараметрыЗаполнения - Структура - заполненные параметры создания нового подключения:
//    * Наименование - Строка - Наименование новой настройки.
//    * ПараметрыУчастника - см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника.
//    * Настройки - см. ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов.
//    * ТокенСервиса - Структура - токен аутентификации сервиса онлайн заказов:
//      ** Идентификатор - УникальныйИдентификатор - идентификатор токена. Если не заполнен, то значение токена не
//         меняется. В противном случае - токен перезаписывается, даже если в его значении указана пустая строка.
//      ** Токен - Строка - значение токена.
// 
// Возвращаемое значение:
//  Структура - результат создания настройки:
//    * Ошибка - Булево - Истина, если в процессе создания возникла ошибка.
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//    * Ссылка - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу, Неопределено - созданная настройка;
//        Неопределено в случае ошибки.
//    
Функция НоваяНастройкаПодключения(ПараметрыЗаполнения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("Ссылка", Неопределено);
	
	НачатьТранзакцию();
	
	Попытка

		НоваяНастройка = СоздатьЭлемент();
		НоваяНастройка.Родитель = ГруппаНастроек(
			ПараметрыЗаполнения.ПараметрыУчастника.Идентификатор,
			ПараметрыЗаполнения.ПараметрыУчастника.Представление);
		НоваяНастройка.Наименование            = ПараметрыЗаполнения.Наименование;
		НоваяНастройка.ИдентификаторМерчанта   = ПараметрыЗаполнения.Настройки.ИдентификаторМерчанта;
		НоваяНастройка.ИдентификаторУчастника  = ПараметрыЗаполнения.ПараметрыУчастника.Идентификатор;
		НоваяНастройка.Используется            = Истина;
		
		НоваяНастройка.ДополнительныеПараметры = Новый ХранилищеЗначения(
			ПараметрыЗаполнения.Настройки.ДополнительныеПараметры);
		
		НоваяНастройка.Заполнить(Неопределено);
		НоваяНастройка.Записать();
		
		ПараметрыАутентификации = ИнтернетЭквайрингСлужебный.ПодготовитьПараметрыАутентификацииДляЗаписи(
			ПараметрыЗаполнения.ПараметрыУчастника.ПоляАутентификации,
			ПараметрыЗаполнения.Настройки.НастройкиАутентификации);
		
		ДанныеАутентификации = ИнтернетЭквайрингСлужебный.НовыйДанныеАутентификации(
			ПараметрыЗаполнения.ПараметрыУчастника.СпособАутентификации,
			ПараметрыАутентификации);
		
		ТокеныАутентификации = Новый Соответствие;
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.ТокенСервиса.Идентификатор) Тогда
			ИдентификаторСервиса = НастройкиПлатежныхСистемСлужебный.ИдентификаторСервисаПоИмени(
				ИнтернетЭквайрингСлужебный.ИдентификаторСервисаОнлайнЗаказы());
			Если ЗначениеЗаполнено(ИдентификаторСервиса) Тогда
				ТокеныАутентификации.Вставить(
					ИдентификаторСервиса,
					ПараметрыЗаполнения.ТокенСервиса);
			КонецЕсли;
		КонецЕсли;
		
		РезультатЗаписи = ИнтернетЭквайрингСлужебный.СохранитьНастройкиАутентификации(
			НоваяНастройка.Ссылка,
			ДанныеАутентификации,
			ТокеныАутентификации);
		
		Если РезультатЗаписи.Отказ Тогда
			Результат.Ошибка            = РезультатЗаписи.Отказ;
			Результат.СообщениеОбОшибке = РезультатЗаписи.СообщениеОбОшибке;
			ОтменитьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		РезультатЗаписи = ИнтернетЭквайрингСлужебный.СохранитьПрикладныеНастройки(
			НоваяНастройка.Ссылка,
			ПараметрыЗаполнения.Настройки.ПрикладныеНастройки);
		
		Если РезультатЗаписи.Отказ Тогда
			Результат.Ошибка            = РезультатЗаписи.Отказ;
			Результат.СообщениеОбОшибке = РезультатЗаписи.СообщениеОбОшибке;
			ОтменитьТранзакцию();
			Возврат Результат;
		КонецЕсли;
		
		Результат.Ссылка = НоваяНастройка.Ссылка;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		// Вызов во вложенной транзакции не предполагается,
		// вместо вызова исключения обработка ошибки делегируется
		// вызывающему методу.
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать настройку подключения по причине:
				|%1';
				|en = 'Cannot save the connection setting. Reason:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Создает группу с наименованием банка, если он отсутствует.
// 
// Параметры:
//  ИдентификаторУчастника - Строка - идентификатор банка
//  Представление - Строка - представление банка
// 
// Возвращаемое значение:
//  СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - созданная или найденная группа.
//  
Функция ГруппаНастроек(ИдентификаторУчастника, Представление)
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиПодключенияКИнтернетЭквайрингу");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Настройки.Ссылка КАК Ссылка,
			|	Настройки.ПометкаУдаления КАК ПометкаУдаления
			|ИЗ
			|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК Настройки
			|ГДЕ
			|	Настройки.ЭтоГруппа
			|	И Настройки.ИдентификаторУчастника = &ИдентификаторУчастника";
		
		Запрос.УстановитьПараметр("ИдентификаторУчастника", ИдентификаторУчастника);
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			
			РодительНастройкиОбъект = СоздатьГруппу();
			РодительНастройкиОбъект.Наименование = Представление;
			РодительНастройкиОбъект.ИдентификаторУчастника = ИдентификаторУчастника;
			РодительНастройкиОбъект.Записать();
			РодительНастройки = РодительНастройкиОбъект.Ссылка;
			
		Иначе
			ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
			ВыборкаДетальныеЗаписи.Следующий();
			Если ВыборкаДетальныеЗаписи.ПометкаУдаления Тогда
				РодительНастройкиОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
				РодительНастройкиОбъект.УстановитьПометкуУдаления(Ложь);
			КонецЕсли;
			РодительНастройки = ВыборкаДетальныеЗаписи.Ссылка;
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать корневой узел настройки:
				|%1';
				|en = 'Cannot save the setting root node:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Возврат РодительНастройки;
	
КонецФункции

// Получает данные настройки подключения по ссылке
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, для которой необходимо
//    получить информацию.
//
// Возвращаемое значение:
//  см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//
Функция ДанныеНастройкиПодключения(НастройкаПодключения) Экспорт

	ДанныеНастройки = ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения();

	Если НастройкаПодключения = Неопределено Тогда
		Возврат ДанныеНастройки;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Настройки.Ссылка КАК НастройкаПодключения,
		|	Настройки.Наименование КАК Наименование,
		|	Настройки.Используется КАК Используется,
		|	Настройки.ИдентификаторУчастника КАК ИдентификаторУчастника,
		|	Настройки.ИдентификаторМерчанта КАК ИдентификаторМерчанта
		|ИЗ
		|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК Настройки
		|ГДЕ
		|	Настройки.Ссылка = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ЗаполнитьЗначенияСвойств(
		ДанныеНастройки,
		ВыборкаДетальныеЗаписи);
	
	Возврат ДанныеНастройки;
	
КонецФункции

// Определяет данные настройки подключения к Интернет-эквайрингу.
//
// Параметры:
//  ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//
// Возвращаемое значение:
//  Структура - параметры настройки:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//
Функция ДанныеНастройкиПодключенияПоИдентификаторуМерчанта(ИдентификаторМерчанта) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("НастройкаПодключения",   ПустаяСсылка());
	Результат.Вставить("ИдентификаторУчастника", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НастройкиПодключения.Ссылка КАК Ссылка,
		|	НастройкиПодключения.ИдентификаторУчастника КАК ИдентификаторУчастника
		|ИЗ
		|	Справочник.НастройкиПодключенияКИнтернетЭквайрингу КАК НастройкиПодключения
		|ГДЕ
		|	НастройкиПодключения.ИдентификаторМерчанта = &ИдентификаторМерчанта
		|
		|УПОРЯДОЧИТЬ ПО
		|	НастройкиПодключения.Используется УБЫВ";
	
	Запрос.УстановитьПараметр("ИдентификаторМерчанта", ИдентификаторМерчанта);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат.НастройкаПодключения   = ВыборкаДетальныеЗаписи.Ссылка;
		Результат.ИдентификаторУчастника = ВыборкаДетальныеЗаписи.ИдентификаторУчастника;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает дополнительные параметры настройки подключения.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, для которой необходимо
//    получить информацию.
//  ВернутьСоответствие - Булево - если Истина, то значение настроек будет преобразовано к соответствию. Если Ложь,
//    то будет возвращено в том виде, в котором сохранено.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - если ВернутьСоответствие = Истина. Дополнительные параметры подключения:
//   * Ключ - Строка - идентификатор параметра.
//   * Значение - Произвольный - значение параметра.
//  Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации - подробное описание дополнительных
//    параметров.
//
Функция ДополнительныеПараметрыНастройки(НастройкаПодключения, ВернутьСоответствие = Истина) Экспорт
	
	Результат = ?(ВернутьСоответствие, Новый Соответствие, Новый Массив);
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Возврат Результат;
	КонецЕсли;
	
	СохраненныеДанные = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		НастройкаПодключения,
		"ДополнительныеПараметры");
	
	ЗначениеСохраненныхДанных = СохраненныеДанные.Получить();
	Если ТипЗнч(ЗначениеСохраненныхДанных) = Тип("Массив") Тогда
		
		Если ВернутьСоответствие Тогда
		
			Для Каждого ОписаниеНастройки Из ЗначениеСохраненныхДанных Цикл
				Если ЗначениеЗаполнено(ОписаниеНастройки.Значение) Тогда
					Результат.Вставить(
						ОписаниеНастройки.Идентификатор,
						ОписаниеНастройки.Значение);
				КонецЕсли;
			КонецЦикла;
		
			Возврат Результат;
			
		Иначе
			
			Возврат ЗначениеСохраненныхДанных;
			
		КонецЕсли;
	
	Иначе
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли
