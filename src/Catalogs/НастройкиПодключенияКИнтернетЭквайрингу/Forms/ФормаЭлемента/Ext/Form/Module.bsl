///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПодсистемаПодключаемыеКоманды = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды");
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
		МодульПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Отказ = Истина;
		ВызватьИсключение НСтр("ru = 'Создание объекта через форму элемента запрещено.';
								|en = 'Cannot create an object via the item form.'");
	КонецЕсли;

	ИдентификаторТокена = ИдентификаторСохраненногоТокена(Объект.Ссылка);
	Если Не ЗначениеЗаполнено(ИдентификаторТокена) Тогда
		ИдентификаторТокена = Строка(УникальныйИдентификатор);
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
	НастройкаПодключения = ИнтернетЭквайрингСлужебный.НастройкиПодключенияСкрытые(Объект.Ссылка);
	
	СпособАутентификации = НастройкаПодключения.СпособАутентификации;
	
	ИнтернетЭквайрингСлужебный.ВывестиПараметрыАутентификации(
		ЭтотОбъект,
		Элементы.ГруппаНастройкиАутентификации,
		НастройкаПодключения.ПараметрыАутентификации,
		Истина);
	
	ВывестиДополнительныеНастройкиАутентификации();
	
	ИнтернетЭквайрингСлужебный.ВывестиПрикладныеНастройки(
		ЭтотОбъект,
		ПараметрыВыводаПрикладныхНастроек(Ложь));

	Элементы.ГруппаПредупреждениеНастройкиПодключения.Видимость =
		ДинамическиеРеквизиты.НастройкиАутентификации.Количество() = 0;
	
	ЗаполнитьДекорацияНастройкиШаблонов();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
		МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	КешПараметров = Новый Структура("ЗначенияДополнительныхПараметров", Новый Массив);
	
	ПрочитатьСохраненныеЗначенияДополнительныхПараметров(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	ПроверкаАутентификации = ИнтернетЭквайрингСлужебный.ДинамическиеРеквизитыЗаполнены(
		ЭтотОбъект,
		"НастройкиАутентификации");
	
	ПроверкаПрикладныхНастроек = ИнтернетЭквайрингСлужебный.ДинамическиеРеквизитыЗаполнены(
		ЭтотОбъект,
		"ПрикладныеНастройки");
	
	Если Не (ПроверкаАутентификации И ПроверкаПрикладныхНастроек) Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОбновитьТокен", Ложь);
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыЗаписи);
	
	ОбновитьТокен = ДополнительныеПараметры.ОбновитьТокен;
	
	ЗначенияДинамическихРеквизитов = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект);
	ТекущийОбъект.ИдентификаторМерчанта = ЗначенияДинамическихРеквизитов.ИдентификаторМерчанта;
	
	ТекущийОбъект.ДополнительныеПараметры = Новый ХранилищеЗначения(
		ЗначенияДинамическихРеквизитов.ДополнительныеПараметры);
	
	Если Не ДополнительныеПараметры.ОбновитьТокен Тогда
	
		ОбновитьТокен = ИнтернетЭквайрингСлужебный.НеобходимоОбновитьТокен(
			ТекущийОбъект.Ссылка,
			ЗначенияДинамическихРеквизитов,
			ТекущийОбъект.Используется);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)

	ЗначенияДинамическихРеквизитов = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект);

	РезультатЗаписи = ИнтернетЭквайрингСлужебный.СохранитьПрикладныеНастройки(
		Объект.Ссылка,
		ЗначенияДинамическихРеквизитов.ПрикладныеНастройки);
	
	Если РезультатЗаписи.Отказ Тогда
		ОбщегоНазначения.СообщитьПользователю(
			РезультатЗаписи.СообщениеОбОшибке,
			,
			,
			,
			Отказ);
		ВызватьИсключение НСтр("ru = 'Не удалось сохранить настройку.';
								|en = 'Cannot save the settings.'");
	КонецЕсли;

	ПараметрыАутентификации = ИнтернетЭквайрингСлужебный.ПодготовитьПараметрыАутентификацииДляЗаписи(
			ДинамическиеРеквизиты.НастройкиАутентификации,
			ЗначенияДинамическихРеквизитов.НастройкиАутентификации);

	ДанныеАутентификации = ИнтернетЭквайрингСлужебный.НовыйДанныеАутентификации(
		СпособАутентификации,
		ПараметрыАутентификации);

	РезультатЗаписи = ИнтернетЭквайрингСлужебный.СохранитьНастройкиАутентификации(
		Объект.Ссылка,
		ДанныеАутентификации,
		Новый Соответствие);

	Если РезультатЗаписи.Отказ Тогда
		ОбщегоНазначения.СообщитьПользователю(
			РезультатЗаписи.СообщениеОбОшибке,
			,
			,
			,
			Отказ);
		ВызватьИсключение НСтр("ru = 'Не удалось сохранить настройку.';
								|en = 'Cannot save the settings.'");
	КонецЕсли;

	Если РезультатЗаписи.ПараметрыАутентификацииИзменены Тогда
		ОбновитьТокен = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи",             Ложь);
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыЗаписи);
	
	Если Не ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда
	
		ТекущиеЗначения = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект).НастройкиАутентификации;
	
		СтрокаСекретныхДанных = ИнтернетЭквайрингСлужебный.СтрокаСекретныхДанныхПоУмолчанию();
		Для Каждого ОписаниеПоля Из ДинамическиеРеквизиты.НастройкиАутентификации Цикл
			Если ОписаниеПоля.РежимПароля Тогда
				ЭтотОбъект[ОписаниеПоля.ИмяРеквизита] = СтрокаСекретныхДанных;
				Элементы[ОписаниеПоля.ИмяЭлемента].КнопкаВыбора = Неопределено;
				Элементы[ОписаниеПоля.ИмяЭлемента].РежимПароля = Истина;
			Иначе
				// Перед сохранением из значения удаляются нечитаемые символы, поэтому значение может отличаться от
				// того, что пользователь видит в поле.
				ЗначениеПослеСохранения = ТекущиеЗначения.Получить(ОписаниеПоля.Идентификатор);
				Если ЭтотОбъект[ОписаниеПоля.ИмяРеквизита] <> ЗначениеПослеСохранения Тогда
					ЭтотОбъект[ОписаниеПоля.ИмяРеквизита] = ЗначениеПослеСохранения;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	
	КонецЕсли;
	
	ПрочитатьСохраненныеЗначенияДополнительныхПараметров(ТекущийОбъект);
	ВывестиДополнительныеНастройкиАутентификации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи",             Ложь);
	ДополнительныеПараметры.Вставить("ОбновитьНастройкуОнлайнЗаказов", Ложь);
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыЗаписи);

	Если ОбновитьТокен Тогда
		ОбновлениеТокена(ПараметрыЗаписи);
	ИначеЕсли ДополнительныеПараметры.ОбновитьНастройкуОнлайнЗаказов Тогда
		ОбновитьНастройкуОнлайнЗаказов(ДополнительныеПараметры.ЗакрытьПослеЗаписи);
	ИначеЕсли ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда
		Закрыть(Истина);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиОплаты(Элемент)
	
	ПриИзмененииПрикладныхНастроекНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиАутентификации(
		Элемент,
		Текст,
		СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбораНастройкиАутентификации(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	ИмяРеквизита = ДинамическиеРеквизиты.РеквизитПоИмениЭлемента.Получить(
			Элемент.Имя);
	Если Не ЗначениеЗаполнено(ИмяРеквизита) Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		ИмяРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДополнительнаяИнформацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("ПрикладныеНастройки", ЗначенияПрикладныхНастроек());
	ДанныеФормы.Вставить("НастройкаПодключения", Неопределено);
	ДанныеФормы.Вставить("Модифицированность", Модифицированность);
	
	ИнтернетЭквайрингКлиентПереопределяемый.ПриОбработкеНавигационнойСсылкиНастроекПодключения(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ДанныеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиШаблоновОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "open:paymentPurpose" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Перед настройкой шаблонов назначения необходимо записать изменения.';
				|en = 'Save the changes before setting up assignment templates.'"));
		Возврат;
	КонецЕсли;
	
	Если Не Объект.Используется Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для отключенной настройки подключения недоступно изменение шаблонов назначения.';
				|en = 'Assignment template change is unavailable for a disabled connection setting.'"));
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДекорацияНастройкиШаблоновЗавершение",
		ЭтотОбъект);
	
	НастройкиПлатежныхСистемКлиент.ОткрытьНастройкуШаблоновНазначения(
		Объект.Ссылка,
		ЭтотОбъект,
		ОписаниеОповещения);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Записать(Новый Структура("ЗакрытьПослеЗаписи", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	
	Если Модифицированность Тогда
		ОбработкаОтвета = Новый ОписаниеОповещения(
			"ПроверитьПодключениеОбработкаОтвета",
			ЭтотОбъект);
		
		ПоказатьВопрос(
			ОбработкаОтвета,
			НСтр("ru = 'Необходимо сохранить все изменения. Продолжить?';
				|en = 'All changes must be saved. Continue?'"),
			РежимДиалогаВопрос.ДаНет);
		
	ИначеЕсли Не Объект.Используется Тогда
		
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Проверка подключения доступна только для используемой настройки.';
				|en = 'Connection check is only available for the used settings.'"));
		
	Иначе
		
		ОчиститьСообщения();
		ПроверкаПараметров = Истина;
		ОбновлениеТокена(Новый Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыАутентификации(Команда)

	Если Не ЗначениеЗаполнено(Объект.ИдентификаторУчастника) Тогда
		ПоказатьПредупреждение(
			Неопределено,
			НСтр("ru = 'Не заполнен идентификатор участника. Обновление невозможно.';
				|en = 'Participant ID is not filled in. Cannot update.'"));
	КонецЕсли;

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Проверка актуальности параметров аутентификации.';
										|en = 'Check the relevance of authentication parameters.'");
	
	РезультатВыполнения = ОбновитьПараметрыАутентификацииЗапускЗадания();
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновитьПараметрыАутентификацииЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОбновитьПараметрыАутентификацииЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НастройкиПодключения

&НаСервере
Функция ОбновитьПараметрыАутентификацииЗапускЗадания()
	
	ПараметрыПроцедуры = Новый Структура(
		"ИдентификаторУчастника", Объект.ИдентификаторУчастника);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Проверка актуальности параметров аутентификации.';
				|en = 'Check the relevance of authentication parameters.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ПараметрыУчастникаПоИдентификаторуВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПараметрыАутентификацииЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = "";
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ТекстПредупреждения = ИнтернетЭквайрингКлиент.ТекстСообщенияПоПравамПользователя(
				РезультатОперации.СообщениеОбОшибке,
				РезультатОперации.ИнформацияОбОшибке);
		Иначе
			
			Если ТипЗнч(РезультатОперации.ПараметрыУчастника) <> Тип("Структура") Тогда
				ШаблонСообщения = НСтр("ru = 'Ошибка при получении информации об участнике Интернет-эквайринга. %1';
										|en = 'An error occurred when receiving information about an Online acquiring participant. %1'");
				ТекстПредупреждения = ИнтернетЭквайрингКлиент.ТекстСообщенияПоПравамПользователя(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонСообщения,
						НСтр("ru = 'Обратитесь к администратору.';
							|en = 'Please contact the administrator.'")),
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ШаблонСообщения,
						НСтр("ru = 'Метод ИнтернетЭквайрингСлужебный.ПараметрыУчастникаПоИдентификаторуВФоне вернул недопустимое значение.';
							|en = 'The ИнтернетЭквайрингСлужебный.ПараметрыУчастникаПоИдентификаторуВФоне method returned an invalid value.'")));
				
				ПоказатьПредупреждение(
					Неопределено,
					ТекстПредупреждения);
				Возврат;
				
			КонецЕсли;
				
			Эталон = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника();
			ЗаполнитьЗначенияСвойств(Эталон, РезультатОперации.ПараметрыУчастника);
			
			Если СпособАутентификации <> Эталон.СпособАутентификации
				Или ПараметрыАутентификацииОтличаются(Эталон.ПоляАутентификации) Тогда
				
				СпособАутентификации = Эталон.СпособАутентификации;
				ОбновитьДинамическиеРеквизитыАутентификации(Эталон.ПоляАутентификации);
				
				ПоказатьОповещениеПользователя(
					НСтр("ru = 'Успешно';
						|en = 'Success'"),
					,
					НСтр("ru = 'Параметры аутентификации успешно обновлены.';
						|en = 'Authentication parameters are updated successfully.'"),
					БиблиотекаКартинок.Информация32,
					СтатусОповещенияПользователя.Информация);
				
				Модифицированность = Истина;
				
				Возврат;
				
			КонецЕсли;
			
			ТекстПредупреждения = НСтр("ru = 'Параметры аутентификации соответствуют данным сервиса. Обновление не требуется.';
										|en = 'Authentication parameters correspond to the service data. No update is required.'");
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстПредупреждения =  ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(
			Результат.ИнформацияОбОшибке);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыАутентификацииОтличаются(ПоляАутентификации)
	
	ПоляДляСравнения = "Представление, Тип, РежимПароля, ЭтоИдентификаторМерчанта";
	ТекущиеПараметры = Новый Соответствие;
	Для Каждого ОписаниеРеквизита Из ДинамическиеРеквизиты.НастройкиАутентификации Цикл
		
		ДанныеПараметра = Новый Структура(ПоляДляСравнения);
		ЗаполнитьЗначенияСвойств(ДанныеПараметра, ОписаниеРеквизита);
		
		ТекущиеПараметры.Вставить(
			ОписаниеРеквизита.Идентификатор,
			ДанныеПараметра);
			
	КонецЦикла;
	
	НовыеПараметры = Новый Соответствие;
	Для Каждого ОписаниеРеквизита Из ПоляАутентификации Цикл
		
		ДанныеПараметра = Новый Структура(ПоляДляСравнения);
		ЗаполнитьЗначенияСвойств(ДанныеПараметра, ОписаниеРеквизита);
		ДанныеПараметра.Тип = Новый ОписаниеТипов(
			"Строка",
			,
			Новый КвалификаторыСтроки(ОписаниеРеквизита.ДлинаСтроки));
		
		НовыеПараметры.Вставить(
			ОписаниеРеквизита.Идентификатор,
			ДанныеПараметра);
			
	КонецЦикла;
	
	Возврат Не ИнтернетЭквайрингКлиентСервер.ДанныеСовпадают(ТекущиеПараметры, НовыеПараметры);

КонецФункции

&НаСервере
Процедура ОбновитьДинамическиеРеквизитыАутентификации(Знач ПоляАутентификации)
	
	Модифицированность = Истина;
	
	ТекущиеЗначения = Новый Соответствие;
	Для Каждого ОписаниеРеквизита Из ДинамическиеРеквизиты.НастройкиАутентификации Цикл
		
		ДанныеПараметра = Новый Структура;
		ДанныеПараметра.Вставить("Значение",    ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита]);
		ДанныеПараметра.Вставить("Тип",         ОписаниеРеквизита.Тип);
		ДанныеПараметра.Вставить("РежимПароля", ОписаниеРеквизита.РежимПароля);

		ТекущиеЗначения.Вставить(
			ОписаниеРеквизита.Идентификатор,
			ДанныеПараметра);
			
	КонецЦикла;
	
	ИнтернетЭквайрингСлужебный.ВывестиПараметрыАутентификации(
			ЭтотОбъект,
			Элементы.ГруппаНастройкиАутентификации,
			ПоляАутентификации);
	
	Для Каждого ОписаниеРеквизита Из ДинамическиеРеквизиты.НастройкиАутентификации Цикл
		
		ДанныеПараметра = ТекущиеЗначения.Получить(ОписаниеРеквизита.Идентификатор);
		Если ДанныеПараметра = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НовоеЗначение = "";
		Если ДанныеПараметра.Тип = ОписаниеРеквизита.Тип Тогда
			НовоеЗначение = ДанныеПараметра.Значение;
		КонецЕсли;
		
		Если ДанныеПараметра.РежимПароля
			И НовоеЗначение <> ИнтернетЭквайрингСлужебный.СтрокаСекретныхДанныхПоУмолчанию() Тогда
			
			Элементы[ОписаниеРеквизита.ИмяЭлемента].КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
			Элементы[ОписаниеРеквизита.ИмяЭлемента].КнопкаВыбора = Истина;
		
		КонецЕсли;
		
		ЭтотОбъект[ОписаниеРеквизита.ИмяРеквизита] = НовоеЗначение;
		
	КонецЦикла;
	
	Элементы.ГруппаПредупреждениеНастройкиПодключения.Видимость =
		ДинамическиеРеквизиты.НастройкиАутентификации.Количество() = 0;
		
КонецПроцедуры

&НаСервере
Процедура ВывестиДополнительныеНастройкиАутентификации()
	
	ПараметрыМетода = ИнтернетЭквайрингСлужебный.НовыйПараметрыВыводаДополнительныхНастроек(
		ЭтотОбъект,
		Истина,
		ДинамическиеРеквизиты.ДополнительныеПараметры,
		КешПараметров.ЗначенияДополнительныхПараметров,
		"ГруппаДополнительныеНастройкиАутентификации",
		Истина,
		Истина);
	
	ИнтернетЭквайрингСлужебный.ВывестиДополнительныеНастройкиАутентификации(ПараметрыМетода);
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеНастройкиАутентификации(
		Знач ДополнительныеПараметры,
		Знач СоздаватьРеквизиты,
		Знач НовыеЗначения)

	ПараметрыМетода = ИнтернетЭквайрингСлужебный.НовыйПараметрыВыводаДополнительныхНастроек(
		ЭтотОбъект,
		СоздаватьРеквизиты,
		ДополнительныеПараметры,
		НовыеЗначения,
		"ГруппаДополнительныеНастройкиАутентификации",
		,
		Истина);
	
	ИнтернетЭквайрингСлужебный.ВывестиДополнительныеНастройкиАутентификации(ПараметрыМетода);
	
	Результат = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыМетода.ДополнительныеПараметры);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПрикладныеНастройки

&НаСервере
Функция ПараметрыВыводаПрикладныхНастроек(ПрикладныеПараметрыЗагружены = Истина)
	
	ПараметрыВывода = ИнтернетЭквайрингСлужебный.НовыйПараметрыВывода();
	ПараметрыВывода.Настройка                    = Объект.Ссылка;
	ПараметрыВывода.Родитель                     = Элементы.ГруппаНастройкиОплаты;
	ПараметрыВывода.ИдентификаторУчастника       = Объект.ИдентификаторУчастника;
	ПараметрыВывода.ИмяРеквизитаНаименование     = "Объект.Наименование";
	ПараметрыВывода.ПрикладныеПараметрыЗагружены = ПрикладныеПараметрыЗагружены;
	ПараметрыВывода.СохраняемыеДанные            = Истина;
	
	Возврат ПараметрыВывода;
	
КонецФункции

&НаСервере
Процедура ПриИзмененииПрикладныхНастроекНаСервере()
	
	ИнтернетЭквайрингСлужебный.НастроитьЭлементыФормыПодключения(
		ЭтотОбъект,
		ПараметрыВыводаПрикладныхНастроек());
	
КонецПроцедуры

&НаСервере
Функция ЗначенияПрикладныхНастроек()

	Возврат ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект).ПрикладныеНастройки;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеТокена

&НаКлиенте
Процедура ОбновлениеТокена(ДополнительныеПараметры)
	
	Если Объект.Используется Тогда
		ДополнительныеПараметры.Вставить("ТипОбновления", "Запрос");
		ЗапроситьТокен(ДополнительныеПараметры);
	Иначе
		ДополнительныеПараметры.Вставить("ТипОбновления", "Отзыв");
		ОтозватьТокен(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтозватьТокен(ДополнительныеПараметры)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	РезультатВыполнения = ОтозватьТокенЗапускЗадания(
		Объект.Ссылка,
		УникальныйИдентификатор);

	ПараметрыОжидания.Заголовок = НСтр("ru = 'Отзыв токена аутентификации в сервисе онлайн-заказов.';
										|en = 'Revoke an authentication token in the service of online orders.'");
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновлениеТокенаЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОбновлениеТокенаЗавершение(
			РезультатВыполнения,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьТокен(ДополнительныеПараметры)
	
	ПараметрыЗапроса = Новый Структура(
		"ДополнительныеПараметры",
		Неопределено);
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ДополнительныеПараметры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	
	РезультатВыполнения = ЗапроситьТокенЗапускЗадания(
		Объект.Ссылка,
		УникальныйИдентификатор,
		ИдентификаторТокена,
		ПараметрыЗапроса.ДополнительныеПараметры);

	ПараметрыОжидания.Заголовок = НСтр("ru = 'Запрос токена аутентификации в сервисе онлайн-заказов.';
										|en = 'Request an authentication token in the service of online orders.'");
	
	Если РезультатВыполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновлениеТокенаЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОбновлениеТокенаЗавершение(
			РезультатВыполнения,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтозватьТокенЗапускЗадания(
		Знач Ссылка,
		Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отзыв токена аутентификации в сервисе онлайн-заказов.';
															|en = 'Revoke an authentication token in the service of online orders.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ОтозватьТокенПриОтключенииНастройкиВФоне",
		Ссылка,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗапроситьТокенЗапускЗадания(
		Знач Ссылка,
		Знач УникальныйИдентификатор,
		Знач ИдентификаторТокена,
		Знач ДополнительныеПараметры = Неопределено)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Запрос токена аутентификации в сервисе онлайн-заказов.';
															|en = 'Request an authentication token in the service of online orders.'");
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("НастройкаПодключения",    Ссылка);
	ПараметрыМетода.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ПараметрыМетода.Вставить("ИдентификаторТокена",     ИдентификаторТокена);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ЗапроситьТокенПриЗаписиНастройкиВФоне",
		ПараметрыМетода,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеТокенаЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ТекстПредупреждения = ИнтернетЭквайрингКлиент.ТекстСообщенияПоПравамПользователя(
				РезультатОперации.СообщениеОбОшибке,
				РезультатОперации.ИнформацияОбОшибке);
		Иначе
			
			ДопПараметры = Новый Структура("ТипОбновления, ЗакрытьПослеЗаписи", "", Ложь);
			ЗаполнитьЗначенияСвойств(ДопПараметры, ДополнительныеПараметры);
			
			Если ДопПараметры.ТипОбновления = "Отзыв" Тогда
				ОбновитьНастройкуОнлайнЗаказов(ДопПараметры.ЗакрытьПослеЗаписи);
			Иначе
				ОбработатьРезультатПолученияТокена(
					РезультатОперации,
					ДополнительныеПараметры);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ТекстПредупреждения =  ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(
			Результат.ИнформацияОбОшибке);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ВопросПовторить(ТекстПредупреждения, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолученияТокена(
		РезультатОперации,
		ДополнительныеПараметры)
	
	Эталон = Новый Структура();
	Эталон.Вставить("ТребуетсяНастройка",            Ложь);
	Эталон.Вставить("Токен",                         Неопределено);
	Эталон.Вставить("ТребуемыеНастройки",            Неопределено);
	Эталон.Вставить("СохранитьПослеПолученияТокена", Ложь);
	
	ЗаполнитьЗначенияСвойств(Эталон, РезультатОперации);
	
	ТребуетсяЗапись = Эталон.СохранитьПослеПолученияТокена;
	
	ДопПараметры = Новый Структура();
	ДопПараметры.Вставить("ЗакрытьПослеЗаписи", Ложь);
	ЗаполнитьЗначенияСвойств(ДопПараметры, ДополнительныеПараметры);
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ЗакрытьПослеЗаписи", ДопПараметры.ЗакрытьПослеЗаписи);
	ПараметрыЗаписи.Вставить("ОбновитьНастройкуОнлайнЗаказов", Истина);
	ПараметрыЗаписи.Вставить("СохранитьПослеПолученияТокена",  ТребуетсяЗапись);
	
	Если Эталон.ТребуетсяНастройка Тогда
		
		НовыеЗначения = ИнтернетЭквайрингКлиент.ОписаниеДополнительныхПараметров(
			РезультатОперации,
			ДинамическиеРеквизиты.ДополнительныеПараметры);
		
		ИдентификаторыЗаполнить = Новый Массив;
		Для Каждого ОписаниеНастройки Из НовыеЗначения Цикл
			Если ОписаниеНастройки.ТребуемыйПараметр Тогда
				ИдентификаторыЗаполнить.Добавить(ОписаниеНастройки.Идентификатор);
			КонецЕсли;
		КонецЦикла;
		
		ОбновлениеНастроек = ДополнительныеНастройкиАутентификации(
			ДинамическиеРеквизиты.ДополнительныеПараметры,
			Истина,
			НовыеЗначения);
		
		ДинамическиеРеквизиты.Вставить("ДополнительныеПараметры", ОбновлениеНастроек);
		
		Если ИдентификаторыЗаполнить.Количество() = 0 Тогда
			
			ДополнительныеПараметры.Вставить("НетТребуемыхНастроек", Истина);
			ОбработкаОшибки = Новый ОписаниеОповещения(
				"ОшибкаОбработкиТокенаЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметры);
			
			ПоказатьПредупреждение(
				ОбработкаОшибки,
				НСтр("ru = 'Ошибка получения токена авторизации сервиса. Нет требуемых настроек.';
					|en = 'Error receiving the service authorization token. There are no required settings.'"));
				
		КонецЕсли;
		
		Для Каждого ОписаниеРеквизита Из ДинамическиеРеквизиты.ДополнительныеПараметры Цикл
			Если ИдентификаторыЗаполнить.Найти(ОписаниеРеквизита.Идентификатор) <> Неопределено Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Поле %1 не заполнено или заполнено не верно';
							|en = 'The %1 field is not filled in or is filled in incorrectly'"),
						ОписаниеРеквизита.Представление),
					,
					,ОписаниеРеквизита.ИмяРеквизита);
			КонецЕсли;
		КонецЦикла;

		Возврат;
		
	ИначеЕсли ЗначениеЗаполнено(Эталон.Токен) Тогда
		
		РезультатСохраненияТокена = ЗаписатьТокенСервиса(
			Объект.Ссылка,
			ИдентификаторТокена,
			Эталон.Токен);
		
		Если ЗначениеЗаполнено(РезультатСохраненияТокена.КодОшибки) Тогда
			
			ДополнительныеПараметры.Вставить("ОшибкаСохраненияТокена", Истина);
			
			ОбработкаОшибки = Новый ОписаниеОповещения(
				"ОшибкаОбработкиТокенаЗавершение",
				ЭтотОбъект,
				ДополнительныеПараметры);
			
			ИнтернетЭквайрингКлиент.ТекстСообщенияПоПравамПользователя(
				РезультатСохраненияТокена.СообщениеОбОшибке,
				РезультатСохраненияТокена.ИнформацияОбОшибке);
			
			ТекстПредупреждения = ИнтернетЭквайрингКлиент.ТекстСообщенияПоПравамПользователя(
				РезультатСохраненияТокена.СообщениеОбОшибке,
				РезультатСохраненияТокена.ИнформацияОбОшибке);
			
			ПоказатьПредупреждение(
				ОбработкаОшибки,
				ТекстПредупреждения);
			
			ПроверкаПараметров = Ложь;
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначенияДинамическихРеквизитов = ДополнительныеПараметрыНаСервере();
	
	Если ДополнительныеПараметрыОтличаются(Объект.Ссылка, ЗначенияДинамическихРеквизитов) Тогда
		ТребуетсяЗапись = Истина;
	КонецЕсли;
	
	Если ТребуетсяЗапись Тогда
		Записать(ПараметрыЗаписи);
		Возврат;
	КонецЕсли;
	
	ОбновитьНастройкуОнлайнЗаказов(ДопПараметры.ЗакрытьПослеЗаписи);
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПараметрыНаСервере()
	
	Результат = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект);
	Возврат Результат.ДополнительныеПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьНастройкуОнлайнЗаказов(ЗакрытьПослеЗаписи)
	
	Если ПроверкаПараметров Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Проверка параметров подключения выполнена успешно.';
				|en = 'Connection parameters are checked successfully.'"));
		ПроверкаПараметров = Ложь;
	КонецЕсли;
	
	ЗапущеноОбновление = Ложь;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы")
		И ЕстьНастройкиОнлайнЗаказов(Объект.Ссылка) Тогда
			
		МодульОнлайнЗаказыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОнлайнЗаказыКлиент");
		МодульОнлайнЗаказыКлиент.ПоказатьВопросОбновленияНастроекПодключенияПоНастройкеОплаты(
			Объект.Ссылка,
			ЭтотОбъект,
			ЗакрытьПослеЗаписи);
			
		ЗапущеноОбновление = Истина;
		
	КонецЕсли;
	
	Если Не ЗапущеноОбновление И ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьНастройкиОнлайнЗаказов(Знач НастройкаПодключения)
	
	МодульОнлайнЗаказыСлужебный = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСлужебный");
	Возврат МодульОнлайнЗаказыСлужебный.СуществуютНастройкиПоНастройкеОплаты(
		НастройкаПодключения);
	
КонецФункции

&НаКлиенте
Процедура ОшибкаОбработкиТокенаЗавершение(ДополнительныеПараметры) Экспорт

	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("НетТребуемыхНастроек",          Ложь);
	ПараметрыОбработки.Вставить("ОшибкаСохраненияТокена",        Ложь);
	ПараметрыОбработки.Вставить("ЗакрытьПослеЗаписи",            Ложь);
	ПараметрыОбработки.Вставить("ПользовательНеУказалПараметры", Ложь);

	ЗаполнитьЗначенияСвойств(ПараметрыОбработки, ДополнительныеПараметры);
	
	Если ПараметрыОбработки.НетТребуемыхНастроек 
		Или ПараметрыОбработки.ПользовательНеУказалПараметры Тогда
		
		Объект.Используется = Ложь;
		Записать(ДополнительныеПараметры);
		
	КонецЕсли;
	
	Если ПараметрыОбработки.ЗакрытьПослеЗаписи Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПовторить(ТекстОшибки, ДополнительныеПараметры)
	
	ПараметрыЗаписи = Новый Структура(
		"ЗакрытьПослеЗаписи, НеЗадаватьВопрос",
		Ложь,
		Ложь);
	ЗаполнитьЗначенияСвойств(ПараметрыЗаписи, ДополнительныеПараметры);
	Если ПараметрыЗаписи.НеЗадаватьВопрос Тогда
		Если ПараметрыЗаписи.ЗакрытьПослеЗаписи Тогда
			Закрыть();
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'При обновлении токена аутентификации в сервисе Онлайн-заказов возникли ошибки.
		|
		|Повторить запрос?
		|
		|Информация об ошибке:
		|%1';
		|en = 'Errors occurred when updating the authentication token in the service of online orders.
		|
		|Repeat the request?
		|
		|Error details:
		|%1'"),
		ТекстОшибки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбработкаОтветаПовторить",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	ПоказатьВопрос(
		ОписаниеОповещения,
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНетОтмена,
		30,
		,
		,
		КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаПовторить(Ответ, ДополнительныеПараметры) Экспорт
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗакрытьПослеЗаписи", Ложь);
	ПараметрыМетода.Вставить("ТипОбновления",      "");
	ПараметрыМетода.Вставить("НеЗадаватьВопрос",   Ложь);

	ЗаполнитьЗначенияСвойств(ПараметрыМетода, ДополнительныеПараметры);
	
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		ПроверкаПараметров = Ложь;
		Если Объект.Используется Тогда
			Объект.Используется = Ложь;
			ПараметрыМетода.ЗакрытьПослеЗаписи = Ложь;
			ПараметрыМетода.НеЗадаватьВопрос   = Истина;
			Записать(ПараметрыМетода);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		ПроверкаПараметров = Ложь;
		
		Если Объект.Используется Тогда
			Объект.Используется = Ложь;
			ПараметрыМетода.НеЗадаватьВопрос = Истина;
			Записать(ПараметрыМетода);
			Возврат;
		КонецЕсли;
		
		Если ПараметрыМетода.ЗакрытьПослеЗаписи Тогда
			Закрыть();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	Если ПараметрыМетода.ТипОбновления = "Отзыв" Тогда
		ОтозватьТокен(ДополнительныеПараметры);
	ИначеЕсли ПараметрыМетода.ТипОбновления = "Запрос" Тогда
		ЗапроситьТокен(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДополнительныеПараметрыОтличаются(
		Знач Ссылка,
		Знач НовыеПараметры)
	
	ТекущиеПараметры = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДополнительныеПараметрыНастройки(
		Ссылка,
		Ложь);
	Если НовыеПараметры = Неопределено И ТекущиеПараметры.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Не ОбщегоНазначения.ДанныеСовпадают(НовыеПараметры, ТекущиеПараметры);
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаписатьТокенСервиса(
		Знач НастройкаПодключения,
		Знач Идентификатор,
		Знач Токен)
	
	ИмяСервиса = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСобытия = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСобытия");
		ИмяСервиса = МодульОнлайнЗаказыСобытия.ИдентификаторСервиса();
	КонецЕсли;
	
	Возврат ИнтернетЭквайрингСлужебный.ЗаписатьТокенСервиса(
		НастройкаПодключения,
		Идентификатор,
		Токен,
		ИмяСервиса);
	
КонецФункции

#КонецОбласти

#Область ПроверкаПодключения

&НаКлиенте
Процедура ПроверитьПодключениеОбработкаОтвета(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПроверкаПараметров = Истина;
	ПараметрыЗаписи = Новый Структура("ОбновитьТокен", Истина);
	
	Записать(ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИдентификаторСохраненногоТокена(Знач НастройкаПодключения)
	
	ИмяСервиса = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОнлайнЗаказы") Тогда
		МодульОнлайнЗаказыСобытия = ОбщегоНазначения.ОбщийМодуль("ОнлайнЗаказыСобытия");
		ИмяСервиса = МодульОнлайнЗаказыСобытия.ИдентификаторСервиса();
	КонецЕсли;
	
	Возврат ИнтернетЭквайрингСлужебный.ИдентификаторСохраненногоТокена(
		НастройкаПодключения,
		ИмяСервиса);
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()

	// Видимость должна быть включена в переопределении.
	Элементы.ДекорацияДополнительнаяИнформация.Видимость = Ложь;
	
	ТолькоПросмотр = Не ИнтернетЭквайринг.НастройкаПодключенияДоступна();
	Элементы.ПроверитьПодключение.Доступность = Не ТолькоПросмотр;
	Элементы.ФормаОбновитьПараметрыАутентификации.Доступность = Не ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиШаблоновЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Число") И Результат > 0 Тогда
		ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблоны назначения (%1)';
				|en = 'Assignment templates (%1)'"),
			Результат);
	Иначе
		ПредставлениеСсылки = НСтр("ru = 'Шаблоны назначения';
									|en = 'Assignment templates'");
	КонецЕсли;
	
	Элементы.ДекорацияНастройкиШаблонов.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '<a href = ""open:paymentPurpose"">%1</a>';
				|en = '<a href = ""open:paymentPurpose"">%1</a>'"),
			ПредставлениеСсылки))
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДекорацияНастройкиШаблонов()
	
	ПредопределенныеШаблоны = ИнтернетЭквайрингСлужебный.ПрикладныеНастройкиПодключения().ШаблоныНазначений;
	
	Если ПредопределенныеШаблоны = Неопределено Или ПредопределенныеШаблоны.Количество() = 0 Тогда
		Элементы.ДекорацияНастройкиШаблонов.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	СохраненныеШаблоны = РегистрыСведений.ШаблоныНазначенийПлатежей.ШаблоныНазначенийПлатежей(
		Объект.Ссылка);
	
	Если СохраненныеШаблоны.Количество() <> 0 Тогда
		ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблоны назначения (%1)';
				|en = 'Assignment templates (%1)'"),
			СохраненныеШаблоны.Количество());
	Иначе
		ПредставлениеСсылки = НСтр("ru = 'Шаблоны назначения';
									|en = 'Assignment templates'")
	КонецЕсли;
		
	Элементы.ДекорацияНастройкиШаблонов.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '<a href = ""open:paymentPurpose"">%1</a>';
				|en = '<a href = ""open:paymentPurpose"">%1</a>'"),
			ПредставлениеСсылки));
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСохраненныеЗначенияДополнительныхПараметров(ТекущийОбъект)
	
	ТекущиеЗначения = ТекущийОбъект.ДополнительныеПараметры.Получить();
	Если ТекущиеЗначения = Неопределено Тогда
		ТекущиеЗначения = Новый Массив;
	КонецЕсли;
	
	НовыеЗначения = Новый Массив;
	Для Каждого ОписаниеНастройки Из ТекущиеЗначения Цикл
		
		Эталон = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
		ЗаполнитьЗначенияСвойств(Эталон, ОписаниеНастройки);
		
		Эталон.ТребуемыйПараметр = Истина;
		Если ЗначениеЗаполнено(Эталон.Значение) Тогда
			Эталон.ДоступныеЗначения.Вставить(Эталон.Значение, Эталон.ПредставлениеЗначения);
		КонецЕсли;
		
		НовыеЗначения.Добавить(Эталон);
		
	КонецЦикла;
	
	КешПараметров = Новый Структура(
		"ЗначенияДополнительныхПараметров",
		НовыеЗначения);
	
КонецПроцедуры

#КонецОбласти

#Область ДругиеПодсистемы

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
	МодульПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(Знач ПараметрыВыполнения)
	
	МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
	МодульПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
	МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
