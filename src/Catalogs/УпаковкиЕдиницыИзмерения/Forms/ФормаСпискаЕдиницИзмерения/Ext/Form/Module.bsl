#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Владелец = Неопределено;
	
	Если Параметры.Свойство("Отбор")
		И Параметры.Отбор.Свойство("Владелец") Тогда
		
		Владелец = Параметры.Отбор.Владелец;
		
	КонецЕсли;
	
	Если Владелец <> Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения Тогда
		ТекстИсключения = НСтр("ru = 'Форма предназначена для отображения только списка единиц упаковок.';
								|en = 'The form is supposed to contain only packaging units.'");
		
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Элементы.ПодобратьИзКлассификатора.Видимость = ПравоДоступа("Добавление", Метаданные.Справочники.УпаковкиЕдиницыИзмерения);
	// СтандартныеПодсистемы.БазоваяФункциональность
	МультиязычностьСервер.ИзменениеТекстаЗапросаСпискаДляТекущегоЯзыка(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.БазоваяФункциональность
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодобратьИзКлассификатора(Команда)
	
	//++ Локализация
	ЛокализацияРФ = ОбщегоНазначенияУТВызовСервера.ЗначениеФункциональнойОпции("ЛокализацияРФ");
	Если ЛокализацияРФ Тогда
		ПараметрыПодбора = ОблачныеКлассификаторыКлиент.ПараметрыПодбораЭлементовОКЕИ();
		ПараметрыПодбора.Владелец              = ЭтотОбъект;
		ПараметрыПодбора.ОповещениеОЗавершении = Новый ОписаниеОповещения("ПодобратьИзКлассификатораЗавершение", ЭтотОбъект);
		
		ОблачныеКлассификаторыКлиент.ПодобратьИзСервисаЭлементыОКЕИ(ПараметрыПодбора);
		Возврат;
	КонецЕсли;
	//-- Локализация
	
	ОткрытьВстроенныйПодбор(Неопределено, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьВстроенныйПодбор(Результат, ДополнительныеПараметры) Экспорт
	
	ОткрытьФорму("Справочник.УпаковкиЕдиницыИзмерения.Форма.КлассификаторЕдиницИзмерения",
		,
		ЭтотОбъект,
		,
		,
		,
		Новый ОписаниеОповещения("ПодобратьИзКлассификатораЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодобратьИзКлассификатораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("КоличествоНовыхЭлементов") Тогда
		
		НоменклатураКлиент.ОповеститьОбИзмененииСпискаВыбораЕдиницИзмерения(
			Результат.КоличествоНовыхЭлементов,
			Результат.НовыеЭлементы);
		
	КонецЕсли;
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти
