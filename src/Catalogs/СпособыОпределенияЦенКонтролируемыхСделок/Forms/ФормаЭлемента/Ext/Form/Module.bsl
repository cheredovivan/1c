#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокКодов = КонтролируемыеСделки.ПолучитьКодыОпределенияЦеныСделки();
	Элементы.КодОпределенияЦеныСделки.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодов Цикл
		НовыйКод = Элементы.КодОпределенияЦеныСделки.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;

	СписокКодов = КонтролируемыеСделки.ПолучитьКодыМетодовЦенообразования();
	Элементы.КодМетодаЦенообразования.СписокВыбора.Очистить();
	Для Каждого Код Из СписокКодов Цикл
		НовыйКод = Элементы.КодМетодаЦенообразования.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйКод, Код);
	КонецЦикла;
	
	ЗаполнитьСписокВыбораТестируемойСтороны();
	
	НастроитьФормуПоМетодуЦенообразования();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ЗаполнитьОписанияСтрокСвойствЦенообразованияМетода();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_СпособыОпределенияЦенКонтролируемыхСделок", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодМетодаЦенообразованияПриИзменении(Элемент)
	НастроитьФормуПоМетодуЦенообразования();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвойстваЦенообразования

&НаКлиенте
Процедура СвойстваЦенообразованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьСвойстваМетодаЦенообразования();
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЦенообразованияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	ОткрытьСвойстваМетодаЦенообразования(Истина, Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваЦенообразованияПередНачаломИзменения(Элемент, Отказ)
	
	ОткрытьСвойстваМетодаЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФормуПоМетодуЦенообразования()
	
	ЗаполнитьОписанияСтрокСвойствЦенообразованияМетода();
	
	УстановитьСтрокиСвойстваЦенообразования();
	
	КодМетодаЦенообразования = Объект.КодМетодаЦенообразования;
	
	КлючСохраненияПоложенияОкна = "КодМетодаЦенообразования:" + КодМетодаЦенообразования;
	
	Элементы.ГруппаМетодСопоставимыхЦен.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимыхРыночныхЦен();
	Элементы.ГруппаИнформацияПоМетодам02_04.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЦеныПоследующейРеализации()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаЗатратного()
		Или КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаСопоставимойРентабельности();
	Элементы.ГруппаМетодРаспределенияПрибыли.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаРаспределенияПрибыли();
	Элементы.ГруппаКомбинацияМетодов.Видимость = КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтрокиСвойстваЦенообразования()
	
	КодМетодаЦенообразования = Объект.КодМетодаЦенообразования;
	
	Если КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.СвойстваЦенообразования.Количество() = 0 Тогда
		ПерваяСтрока = Объект.СвойстваЦенообразования.Добавить();
		ПерваяСтрока.КодМетодаЦенообразования = КодМетодаЦенообразования;
	Иначе
		СтрокиМетодаЦенообразования = Объект.СвойстваЦенообразования.НайтиСтроки(
			Новый Структура("КодМетодаЦенообразования", КодМетодаЦенообразования));
		Если СтрокиМетодаЦенообразования.Количество() = 0 Тогда
			// С таким методом ценообразования строки нет.
			// Поэтому вставим на 1 место строку с нужным методом.
			ПерваяСтрока = Объект.СвойстваЦенообразования.Вставить(0);
			ПерваяСтрока.КодМетодаЦенообразования = КодМетодаЦенообразования;
		Иначе
			// Передвинем эту строку на первое место.
			ПерваяСтрока = СтрокиМетодаЦенообразования[0];
			ИндексСтроки = Объект.СвойстваЦенообразования.Индекс(ПерваяСтрока);
			Если ИндексСтроки <> 0 Тогда
				Объект.СвойстваЦенообразования.Сдвинуть(ИндексСтроки, -ИндексСтроки);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписанияСтрокСвойствЦенообразованияМетода()
	
	Для Каждого Строка Из Объект.СвойстваЦенообразования Цикл
		Строка.ОписаниеМетода = ОписаниеМетодаСтрокиСвойствЦенообразования(Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораТестируемойСтороны()
	
	СписокВыбораТестируемойСтороны = Документы.ДокументацияПоКонтролируемымСделкам.СписокВыбораТестируемойСтороны();
	Элементы.ТестируемаяСторона.СписокВыбора.Очистить();
	Для Каждого ЭлементСписка Из СписокВыбораТестируемойСтороны Цикл
		Элементы.ТестируемаяСторона.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеМетодаСтроки(ИдентификаторТекущейСтроки)
	
	ТекущаяСтрока = Объект.СвойстваЦенообразования.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	ТекущаяСтрока.ОписаниеМетода = ОписаниеМетодаСтрокиСвойствЦенообразования(ТекущаяСтрока);
	
КонецПроцедуры

&НаСервере
Функция ОписаниеМетодаСтрокиСвойствЦенообразования(Строка)
	
	ПоляМетода = КонтролируемыеСделкиЦенообразование.ПоляСвойствМетодаЦенообразования(
		Строка.КодМетодаЦенообразования);
	
	ОписаниеСвойствПоМетоду = КонтролируемыеСделкиЦенообразование.ОписаниеМетодаСтрокиСвойствЦенообразования(Строка, ПоляМетода);
	
	Если ЗначениеЗаполнено(ОписаниеСвойствПоМетоду) Тогда
		Возврат ОписаниеСвойствПоМетоду;
	Иначе
		Возврат ТекстЗаполнить();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ТекстЗаполнить()
	Возврат НСтр("ru = 'Заполнить';
				|en = 'Fill'");
КонецФункции

&НаКлиенте
Процедура ОткрытьСвойстваМетодаЦенообразования(ЭтоНовый = Ложь, Копирование = Ложь)
	
	ДанныеЗаполнения = ?(Не ЭтоНовый ИЛИ Копирование, СтруктураСтрокиСвойстваМетода(), Новый Структура);
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ЭтоНовый", ЭтоНовый);
	СтруктураПараметров.Вставить("Копирование", Копирование);
	СтруктураПараметров.Вставить("ДанныеЗаполнения", ДанныеЗаполнения);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтруктураПараметров", СтруктураПараметров);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОткрытьСвойстваМетодаЦенообразованияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Документ.ДокументацияПоКонтролируемымСделкам.Форма.ИзменениеСвойствМетодаЦенообразования",
		СтруктураПараметров, ЭтотОбъект,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Функция СтруктураСтрокиСвойстваМетода()
	
	СтрокаТаблицы = Элементы.СвойстваЦенообразования.ТекущиеДанные;
	Если СтрокаТаблицы = Неопределено Тогда
		Возврат Новый Структура;
	КонецЕсли;
	
	СтруктураТабличнойЧастиСвойстваЦенообразования = Новый Структура(
		"КодМетодаЦенообразования,
		|ИсточникИнформацииСопоставимыхЦен,
		|НаименованиеКотировкиСопоставимыхЦен,
		|ПорядокПримененияКотировкиСопоставимыхЦен,
		|НаименованиеДокументаЦеныСопоставимыхЦен,
		|НаименованиеКорректировкиДляСопоставимостиСопоставимыхЦен,
		|ПоказательРентабельности,
		|ТестируемаяСторона,
		|Рентабельность,
		|РыночнаяРентабельность,
		|ОписаниеМетодаРаспределенияПрибыли");
	
	ЗаполнитьЗначенияСвойств(СтруктураТабличнойЧастиСвойстваЦенообразования, СтрокаТаблицы);
	
	Возврат СтруктураТабличнойЧастиСвойстваЦенообразования;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСвойстваМетодаЦенообразованияЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтруктураДанныхСтроки = РезультатЗакрытия;
	
	Если СтруктураДанныхСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = ДополнительныеПараметры.СтруктураПараметров.ЭтоНовый;
	Если ЭтоНовый Тогда
		СтрокаТаблицы = Объект.СвойстваЦенообразования.Добавить();
		ИдентификаторТекущейСтроки = СтрокаТаблицы.ПолучитьИдентификатор();
	Иначе
		ИдентификаторТекущейСтроки = Элементы.СвойстваЦенообразования.ТекущаяСтрока;
		СтрокаТаблицы = Объект.СвойстваЦенообразования.НайтиПоИдентификатору(ИдентификаторТекущейСтроки);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтруктураДанныхСтроки);
	
	ЗаполнитьОписаниеМетодаСтроки(ИдентификаторТекущейСтроки);
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти