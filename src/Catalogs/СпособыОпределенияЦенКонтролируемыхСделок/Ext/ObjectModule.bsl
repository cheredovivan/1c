#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьЛишниеДанныеМетодовЦенообразования();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОчиститьЛишниеДанныеМетодовЦенообразования()
	
	Если Не ЗначениеЗаполнено(КодМетодаЦенообразования) Тогда
		Возврат;
	КонецЕсли;
	
	Если КодМетодаЦенообразования = КонтролируемыеСделкиЦенообразование.КодМетодаКомбинацияМетодов() Тогда
		Возврат;
	КонецЕсли;
	
	КодыМетодовЦенообразования = Документы.ДокументацияПоКонтролируемымСделкам.КодыМетодовЦенообразования();
	
	Если КодыМетодовЦенообразования.НайтиПоЗначению(КодМетодаЦенообразования) = Неопределено Тогда
		// Этот код метода ценообразования не подходит для заполнения документации.
		СвойстваЦенообразования.Очистить();
	КонецЕсли;
	
	// Для всех вариантов, кроме комплексного, в таблице свойств должна быть только одна строка в СвойстваЦенообразования.
	// И код метода ценообразования в ней должен совпадать с кодом в строке МетодыЦенообразования.
	// Все несовпадающие строки нужно удалить.
	СтрокаКодаМетодаНайдена = Ложь;
	СтрокиКУдалению = Новый Массив;
	Для Каждого СтрокаСвойств Из СвойстваЦенообразования Цикл
		Если Не СтрокаКодаМетодаНайдена
			И КодМетодаЦенообразования = СтрокаСвойств.КодМетодаЦенообразования Тогда
			СтрокаКодаМетодаНайдена = Истина;
			Продолжить;
		КонецЕсли;
		
		СтрокиКУдалению.Добавить(СтрокаСвойств);
		
	КонецЦикла;
	Для Каждого СтрокаКУдалению Из СтрокиКУдалению Цикл
		СвойстваЦенообразования.Удалить(СвойстваЦенообразования.Индекс(СтрокаКУдалению));
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли