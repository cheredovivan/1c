
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ПодключитьКЭтапу.ТолькоПросмотр = НЕ Параметры.ДоступностьРеквизитов
		ИЛИ НЕ Параметры.МожноПодключитьКЭтапу;
	
	Элементы.МатериалыИУслуги.ТолькоПросмотр = НЕ Параметры.Свойство("МатериалыИУслуги");
		
	Элементы.Трудозатраты.ТолькоПросмотр = НЕ Параметры.Свойство("Трудозатраты");
		
	Если Параметры.МожноПодключитьКЭтапу
		И Параметры.Свойство("НаименованиеЭтапа", НаименованиеЭтапа) Тогда
		Элементы.ПодключитьКЭтапу.Заголовок =
			Элементы.ПодключитьКЭтапу.Заголовок + СтрШаблон(" ""%1""", НаименованиеЭтапа);
	КонецЕсли;
	
	Перенумеровать = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		Результат = НастройкиКонструктор();
		ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
		Результат.СвязанныеРесурсы = МатериалыИУслуги ИЛИ Трудозатраты;
		
		Закрыть(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьСнятьОтметкиЭлементов(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьСнятьОтметкиЭлементов(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция НастройкиКонструктор(ТолькоПереключатели = Ложь)
	
	Результат = Новый Структура;
	
	Результат.Вставить("Перенумеровать");
	Результат.Вставить("ПодключитьКЭтапу");
	Результат.Вставить("МатериалыИУслуги");
	Результат.Вставить("Трудозатраты");
	
	Если НЕ ТолькоПереключатели Тогда
		Результат.Вставить("НаименованиеТехпроцесса");
		Результат.Вставить("СвязанныеРесурсы");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСнятьОтметкиЭлементов(Отметка)
	
	Для каждого КлючИЗначение Из НастройкиКонструктор(Истина) Цикл
		Если НЕ Элементы[КлючИЗначение.Ключ].ТолькоПросмотр Тогда
			ЭтотОбъект[КлючИЗначение.Ключ] = Отметка;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти