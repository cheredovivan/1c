#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет первоначальное заполнение статей ТК - оснований увольнения.
Процедура НачальноеЗаполнение() Экспорт
	
	ЗагрузитьКлассификатор();
	
КонецПроцедуры

Процедура ЗагрузитьКлассификатор() Экспорт
	Классификатор = ЭлементыКлассификатора();
	ДанныеМакетаКлассификатора = ТаблицаИзМакетаКлассификатора();
	МассивРодителей = Новый Массив();
	Для Каждого СтрокаКлассификатора Из ДанныеМакетаКлассификатора Цикл
		ЭтоНовыйЭлемент = Ложь;
		Если МассивРодителей.Количество() > СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаКлассификатора.Level) Тогда
			МассивРодителей.Удалить(МассивРодителей.Количество() - 1);
		КонецЕсли;
		ЭтоГруппаКлассификатора = ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора);
		Элемент = Неопределено;
		Если ЗначениеЗаполнено(МассивРодителей) Тогда
			Родитель = МассивРодителей[МассивРодителей.Количество() - 1];
		Иначе
			Родитель = Неопределено;
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
			Попытка
				Элемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + СтрокаКлассификатора.Id);
			Исключение
				ЭтоНовыйЭлемент = Истина;
				ОбъектСсылки = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСсылки);
				Элемент = ОбъектСсылки.Ссылка;
			КонецПопытки;
		Иначе
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
			СтруктураПоиска.Вставить("ЭтоГруппа", ЭтоГруппаКлассификатора);
			НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
			Если Не ЗначениеЗаполнено(НайденныеСтроки) Тогда
				ЭтоНовыйЭлемент = Истина;
				ОбъектСсылки = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
				ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ОбъектСсылки);
				Элемент = ОбъектСсылки.Ссылка;
			Иначе
				Элемент = НайденныеСтроки[0].Ссылка;
			КонецЕсли;
		КонецЕсли;
		Если ЭтоНовыйЭлемент Тогда
			НоваяСтрокаКлассификатора = Классификатор.Добавить();
			НоваяСтрокаКлассификатора.Наименование = СтрокаКлассификатора.Name;
			НоваяСтрокаКлассификатора.ЭтоГруппа = ЭтоГруппаКлассификатора;
			НоваяСтрокаКлассификатора.Ссылка = Элемент;
		КонецЕсли;
		Если ЭтоГруппаКлассификатора Тогда
			МассивРодителей.Добавить(Элемент);
		КонецЕсли;
		УстановитьВидДокументаПечатнымФормам(Элемент, СтрокаКлассификатора.PrintedFormIds);
		УстановитьВидДокументаШаблонуДокумента(Элемент, СтрокаКлассификатора.TemplateIds);
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьВидДокументаПечатнымФормам(ВидДокумента, ИдентификаторыПечатныхФорм)
	Если Не ЗначениеЗаполнено(ИдентификаторыПечатныхФорм) Тогда
		Возврат;
	КонецЕсли;
	Идентификаторы = СтрРазделить(ИдентификаторыПечатныхФорм, ", ", Ложь);
	Для Каждого ИдентификаторПечатнойФормы Из Идентификаторы Цикл
		ИдентификаторПечатнойФормы = СокрЛП(ИдентификаторПечатнойФормы);
		НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить(ИдентификаторПечатнойФормы);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			Если Не ЗначениеЗаполнено(НаборЗаписей[0].ВидДокумента) Тогда
				НаборЗаписей[0].ВидДокумента = ВидДокумента;
				НаборЗаписей.Записать();
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьВидДокументаШаблонуДокумента(ВидДокумента, ИдентификаторыШаблонов)
	Если Не ЗначениеЗаполнено(ИдентификаторыШаблонов) Тогда
		Возврат;
	КонецЕсли;
	Идентификаторы = СтрРазделить(ИдентификаторыШаблонов, ", ", Ложь);
	Для Каждого Идентификатор Из Идентификаторы Цикл
		Попытка
			ШаблонДокументаСсылка = ПредопределенноеЗначение("Справочник.ШаблоныДокументов." + Идентификатор);
		Исключение
			Продолжить;;
		КонецПопытки;
		ШаблонДокументаОбъект = ШаблонДокументаСсылка.ПолучитьОбъект();
		Если Не ЗначениеЗаполнено(ШаблонДокументаОбъект.ВидДокумента) Тогда
			ШаблонДокументаОбъект.ВидДокумента = ВидДокумента;
			ОбновлениеИнформационнойБазы.ЗаписатьОбъект(ШаблонДокументаОбъект);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Функция ПредопределенныйВидДокумента(ИмяПредопределенныхДанных) Экспорт
	
	ПредопределенныйЭлемент = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ВидыДокументовЭДОБЗК." + ИмяПредопределенныхДанных);
	Если ПредопределенныйЭлемент <> Неопределено Тогда
		Возврат ПредопределенныйЭлемент;
	КонецЕсли;
	
	Если Метаданные.Справочники.ВидыДокументовЭДОБЗК.ПолучитьИменаПредопределенных().Найти(ИмяПредопределенныхДанных) Тогда
		Попытка
			ПредопределенныйЭлемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + ИмяПредопределенныхДанных);
		Исключение
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
			ОбъектСсылки.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных;
			ОбъектСсылки.Наименование = ИмяПредопределенныхДанных;
			ОбъектСсылки.Записать();
			ПредопределенныйЭлемент = ОбъектСсылки.Ссылка;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ПредопределенныйЭлемент;
	
КонецФункции

Функция ВидДокументаПоПричинеОтсутствия(ПричинаОтсутствия) Экспорт
	Если ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Опоздание Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОбОпоздании");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Болезнь Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОБолезни");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отпуск Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпуск");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.УчебныйОтпуск Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаУчебныйОтпуск");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускЗаСвойСчет Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускЗаСвойСчет");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоБеременностиИРодам Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускПоБеременностиИРодам");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ОтпускПоУходуЗаРебенком Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтпускПоУходуЗаРебенком");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Отгул Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаОтгул");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.Командировка Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаКомандировку");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ЛичныеДела Тогда
		Возврат ПредопределенныйВидДокумента("СообщениеОбОтсутствии");
	ИначеЕсли ПричинаОтсутствия = Перечисления.ПричиныОтсутствийЗаявокКабинетСотрудника.ДниУходаЗаДетьмиИнвалидами Тогда
		Возврат ПредопределенныйВидДокумента("ЗаявлениеНаДниУходаЗаДетьмиИнвалидами");
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ВидПоКодуКадровогоДокумент(КодКадровогоДокумента) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодКадровогоДокумента", КодКадровогоДокумента);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ВидыДокументовЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|ГДЕ
		|	ВидыДокументовЭДОБЗК.КодКадровогоДокумента = &КодКадровогоДокумента
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыДокументовЭДОБЗК.Ссылка,
		|	ВидыДокументовЭДОБЗК.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		Выборка.Следующий();
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Если ДобавитьВидыКадровыхДокументовПоКодам(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КодКадровогоДокумента)) > 0 Тогда
		Возврат ВидПоКодуКадровогоДокумент(КодКадровогоДокумента);
	КонецЕсли;
	
	Возврат ПредопределенныйВидДокумента("ПрочиеКЭДО");
	
КонецФункции

Функция ДобавитьВидыКадровыхДокументовПоКодам(КодыКадровыхДокументов) Экспорт
	
	ДобавленныеРодители = Новый Соответствие;
	ДобавленныеЭлементы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("КодыКадровыхДокументов", КодыКадровыхДокументов);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КодыДокументовКадровыхМероприятий.Ссылка КАК КодКадровогоДокумента,
		|	КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК Наименование,
		|	КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК НаименованиеРодителя,
		|	ЕСТЬNULL(ВидыДокументовЭДОБЗКРодители.Ссылка, НЕОПРЕДЕЛЕНО) КАК Родитель
		|ИЗ
		|	Справочник.КодыДокументовКадровыхМероприятий КАК КодыДокументовКадровыхМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|		ПО (НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа)
		|			И КодыДокументовКадровыхМероприятий.Ссылка = ВидыДокументовЭДОБЗК.КодКадровогоДокумента
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗК.Наименование)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗКРодители
		|		ПО (ВидыДокументовЭДОБЗКРодители.ЭтоГруппа)
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК СТРОКА(150))) = (ВЫРАЗИТЬ(ВидыДокументовЭДОБЗКРодители.Наименование КАК СТРОКА(150))))
		|ГДЕ
		|	КодыДокументовКадровыхМероприятий.Ссылка В(&КодыКадровыхДокументов)
		|	И НЕ КодыДокументовКадровыхМероприятий.ЭтоГруппа
		|	И ВидыДокументовЭДОБЗК.Ссылка ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	КодыДокументовКадровыхМероприятий.Ссылка,
		|	КодыДокументовКадровыхМероприятий.НаименованиеПолное,
		|	КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное,
		|	ЕСТЬNULL(ВидыДокументовЭДОБЗКРодители.Ссылка, НЕОПРЕДЕЛЕНО)
		|ИЗ
		|	Справочник.КодыДокументовКадровыхМероприятий КАК КодыДокументовКадровыхМероприятий
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗК
		|		ПО (НЕ ВидыДокументовЭДОБЗК.ЭтоГруппа)
		|			И КодыДокументовКадровыхМероприятий.Ссылка = ВидыДокументовЭДОБЗК.КодКадровогоДокумента
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗК.Наименование)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыДокументовЭДОБЗК КАК ВидыДокументовЭДОБЗКРодители
		|		ПО (ВидыДокументовЭДОБЗКРодители.ЭтоГруппа)
		|			И ((ВЫРАЗИТЬ(КодыДокументовКадровыхМероприятий.Родитель.НаименованиеПолное КАК СТРОКА(150))) = ВидыДокументовЭДОБЗКРодители.Наименование)
		|ГДЕ
		|	КодыДокументовКадровыхМероприятий.Родитель В(&КодыКадровыхДокументов)
		|	И НЕ КодыДокументовКадровыхМероприятий.ЭтоГруппа
		|	И ВидыДокументовЭДОБЗК.Ссылка ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ДобавленныеЭлементы.Найти(Выборка.КодКадровогоДокумента) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Родитель = Выборка.Родитель;
		Если Не ЗначениеЗаполнено(Родитель) Тогда
			
			Если ЗначениеЗаполнено(Выборка.НаименованиеРодителя) Тогда
				Родитель = ДобавленныеРодители.Получить(Выборка.НаименованиеРодителя);
				Если Родитель = Неопределено Тогда
					РодительОбъект = Справочники.ВидыДокументовЭДОБЗК.СоздатьГруппу();
					РодительОбъект.Наименование = Выборка.НаименованиеРодителя;
					РодительОбъект.Записать();
					Родитель = РодительОбъект.Ссылка;
					ДобавленныеРодители.Вставить(Выборка.НаименованиеРодителя, Родитель);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		ЭлементОбъект = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		ЭлементОбъект.Родитель = Родитель;
		ЭлементОбъект.Наименование = Выборка.Наименование;
		ЭлементОбъект.ДокументСвязанныйСРаботой = Истина;
		ЭлементОбъект.КодКадровогоДокумента = Выборка.КодКадровогоДокумента;
		ЭлементОбъект.Записать();
		
		ДобавленныеЭлементы.Добавить(Выборка.КодКадровогоДокумента);
		
	КонецЦикла;
	
	Возврат ДобавленныеЭлементы.Количество();
	
КонецФункции

Функция ВидПоИдентификаторуШаблона(ИдентификаторШаблона) Экспорт
	Возврат ВидПоИдентификаторуОбъекта(ИдентификаторШаблона, "TemplateIds")
КонецФункции

Функция ВидПоИдентификаторуПечатнойФормы(ИдентификаторПечатнойФормы) Экспорт
	Возврат ВидПоИдентификаторуОбъекта(ИдентификаторПечатнойФормы, "PrintedFormIds")
КонецФункции

Функция ВидПоИдентификаторуОбъекта(ИдентификаторОбъекта, ПолеИдентификатора)

	ДанныеМакетаКлассификатора = ТаблицаИзМакетаКлассификатора();
	МассивРодителей = Новый Массив();
	Для Каждого СтрокаКлассификатора Из ДанныеМакетаКлассификатора Цикл
		Если МассивРодителей.Количество() > СтроковыеФункцииКлиентСервер.СтрокаВЧисло(СтрокаКлассификатора.Level) Тогда
			МассивРодителей.Удалить(МассивРодителей.Количество() - 1);
		КонецЕсли;
		Если ЗначениеЗаполнено(СтрокаКлассификатора[ПолеИдентификатора]) Тогда
			Идентификаторы = СтрРазделить(СтрокаКлассификатора[ПолеИдентификатора], ", ", Ложь);
			Если Идентификаторы.Найти(ИдентификаторОбъекта) <> Неопределено Тогда
				Родитель = РодительПоСтрокамИдентификатора(МассивРодителей);
				Возврат ЭлементКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Родитель);
			КонецЕсли;
		КонецЕсли;
		Если ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора) Тогда
			МассивРодителей.Добавить(СтрокаКлассификатора);
		КонецЕсли;
	КонецЦикла;
	Возврат ПредопределенныйВидДокумента("Прочие");
КонецФункции

Функция ЭлементКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Родитель)
	Элемент = Неопределено;
	Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
		Попытка
			Элемент = ПредопределенноеЗначение("Справочник.ВидыДокументовЭДОБЗК." + СтрокаКлассификатора.Id);
		Исключение
			Элемент = Неопределено
		КонецПопытки;
	Иначе
		Классификатор = ЭлементыКлассификатора();
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
		СтруктураПоиска.Вставить("ЭтоГруппа", Ложь);
		НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
		Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
			Элемент = НайденныеСтроки[0].Ссылка;
		КонецЕсли;
	КонецЕсли;
	Если Элемент = Неопределено Тогда
		ЭлементОбъект = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
		ЭлементОбъект.Записать();
		Элемент = ЭлементОбъект.Ссылка;
	КонецЕсли;
	Возврат Элемент;
КонецФункции

Функция ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель)
	
	ЭтоГруппаКлассификатора = ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора);
	Если ЗначениеЗаполнено(СтрокаКлассификатора.Id) Тогда
		ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		ОбъектСсылки.ИмяПредопределенныхДанных = СтрокаКлассификатора.Id;
	Иначе
		Если ЭтоГруппаКлассификатора Тогда
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьГруппу();
		Иначе
			ОбъектСсылки = Справочники.ВидыДокументовЭДОБЗК.СоздатьЭлемент();
		КонецЕсли;
	КонецЕсли;
	ОбъектСсылки.Наименование = СтрокаКлассификатора.Name;
	ОбъектСсылки.Родитель = Родитель;
	Если Не ЭтоГруппаКлассификатора Тогда
		Если ЗначениеЗаполнено(СтрокаКлассификатора.Code) Тогда
			ОбъектСсылки.КодКадровогоДокумента = ЭДОБЗКПовтИсп.СсылкаНаКодДокументаКадровогоМероприятия(СтрокаКлассификатора.Code);
		Иначе
			ОбъектСсылки.КодКадровогоДокумента = Неопределено;
		КонецЕсли;
		ОбъектСсылки.ДокументСвязанныйСРаботой = ЗначениеЗаполнено(ОбъектСсылки.КодКадровогоДокумента);
	КонецЕсли;
	Возврат ОбъектСсылки;
	
КонецФункции

Функция РодительПоСтрокамИдентификатора(МассивРодителей);
	Родитель = Неопределено;
	Если ЗначениеЗаполнено(МассивРодителей) Тогда
		Классификатор = ЭлементыКлассификатора();
		Родитель = ГруппаКлассификатораПоДаннымСтрокиМакета(МассивРодителей[МассивРодителей.Количество() - 1], Классификатор);
		Если Родитель = Неопределено Тогда
			Для Каждого СтрокаКлассификатора Из МассивРодителей Цикл
				Родитель = ГруппаКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Классификатор);
				Если Родитель = Неопределено Тогда
					ЭлементОбъект = ДобавитьЭлементПоСтрокеКлассификатора(СтрокаКлассификатора, Родитель);
					ЭлементОбъект.Записать();
					Родитель = ЭлементОбъект.Ссылка;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Возврат Родитель;
КонецФункции

Функция ГруппаКлассификатораПоДаннымСтрокиМакета(СтрокаКлассификатора, Классификатор)
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Наименование", СтрокаКлассификатора.Name);
	СтруктураПоиска.Вставить("ЭтоГруппа", Истина);
	НайденныеСтроки = Классификатор.НайтиСтроки(СтруктураПоиска);
	Если ЗначениеЗаполнено(НайденныеСтроки) Тогда
		Возврат НайденныеСтроки[0].Ссылка;
	КонецЕсли;
	Возврат Неопределено;
КонецФункции

Функция ЭлементыКлассификатора()
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДанных.Ссылка КАК Ссылка,
		|	ТаблицаДанных.Наименование КАК Наименование,
		|	ТаблицаДанных.ЭтоГруппа КАК ЭтоГруппа,
		|	ТаблицаДанных.ДокументСвязанныйСРаботой КАК ДокументСвязанныйСРаботой,
		|	ТаблицаДанных.КодКадровогоДокумента КАК КодКадровогоДокумента
		|ИЗ
		|	Справочник.ВидыДокументовЭДОБЗК КАК ТаблицаДанных
		|ГДЕ
		|	НЕ ТаблицаДанных.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка";
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Функция ТаблицаИзМакетаКлассификатора()
	Возврат ОбщегоНазначения.ПрочитатьXMLВТаблицу(
		ПолучитьМакет("Классификатор").ПолучитьТекст()).Данные;
КонецФункции

Функция ЭтоГруппаМакетаКлассификатора(СтрокаКлассификатора)
	Возврат СтрокаКлассификатора.IsGroup = "True";
КонецФункции

#КонецОбласти

#КонецЕсли