#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьИзКлассификатораКадровыхМероприятий(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Истина);
	ПараметрыОткрытия.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.ГруппыИЭлементы);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьВыборКлассификатора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.КодыДокументовКадровыхМероприятий.ФормаВыбора",
		ПараметрыОткрытия, Список, Истина, , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборКлассификатора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ДобавитьВидыКадровыхДокументовПоКодам(Результат) > 0 Тогда
			Элементы.Список.Обновить();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДобавитьВидыКадровыхДокументовПоКодам(ВыбранныеЗначения)
	Возврат Справочники.ВидыДокументовЭДОБЗК.ДобавитьВидыКадровыхДокументовПоКодам(ВыбранныеЗначения);
КонецФункции

#КонецОбласти
