#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Определяется действующая на заданную дату расценка на вид работ.
// Если дата не указана, возвращается последнее заданное значение.
//
// Параметры:
//	ВидРабот - СправочникСсылка.ВидыРаботСотрудников - вид работ,
//	ДатаАктуальности - Дата - дата сведений,
//	КвалификационныйРазряд - СправочникСсылка.РазрядыКатегорииДолжностей, Неопределено - квалификационный разряд.
//	Подразделение - СправочникСсылка.СтруктураПредприятия, Неопределено -
//	Организация - СправочникСсылка.Организации, Неопределено -
//
// Возвращаемое значение:
//  Число, Неопределено - действующая расценка или Неопределено, если ни одного значения не задано.
// 
Функция ДействующаяРасценкаВидаРабот(ВидРабот, ДатаАктуальности, КвалификационныйРазряд = Неопределено, Подразделение = Неопределено, Организация = Неопределено) Экспорт
	
	//++ Локализация
	ИспользоватьТарифныеСеткиПриРасчетеЗарплаты = ПолучитьФункциональнуюОпцию("ИспользоватьТарифныеСеткиПриРасчетеЗарплаты");
	//-- Локализация
	
	Если НЕ ЗначениеЗаполнено(ВидРабот) Тогда
		Возврат 0;
	КонецЕсли;
	
	ПараметрыВидыРабот = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидРабот, "РасценкаПоТарифнойСетке, РасценкаЗаЧасРаботы, Трудоемкость, КратностьТрудоемкости");
	
	//++ НЕ УТКА
	
	//++ Локализация
	Если ПараметрыВидыРабот.РасценкаПоТарифнойСетке И ПолучитьФункциональнуюОпцию("НормированиеИРасценкаРаботПоТарифнымСеткам") И НЕ ИспользоватьТарифныеСеткиПриРасчетеЗарплаты
		Тогда
		
		ДополнительныйРазрез = Константы.ДополнительныйРазрезТарифнойСетки.Получить();
		
		Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ТарифныеСтавки.Тариф, 0) * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости КАК Расценка
		|ИЗ
		|	Справочник.ВидыРаботСотрудников КАК ВидыРабот
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифныеСтавки.СрезПоследних(&Период) КАК ТарифныеСтавки
		|	ПО ВидыРабот.Должность = ТарифныеСтавки.Должность
		|	И ТарифныеСтавки.Разряд = &Разряд
		|	И ((ТарифныеСтавки.Подразделение = &Подразделение И ТарифныеСтавки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	ИЛИ ТарифныеСтавки.Организация = &Организация И ТарифныеСтавки.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|	ИЛИ (ТарифныеСтавки.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	И ТарифныеСтавки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)))
		|	И ТарифныеСтавки.Тариф <> 0
		|ГДЕ
		|	ВидыРабот.Ссылка = &ВидРабот
		|УПОРЯДОЧИТЬ ПО
		|	ТарифныеСтавки.Подразделение УБЫВ,
		|	ТарифныеСтавки.Организация УБЫВ");
		
		Запрос.УстановитьПараметр("ВидРабот", ВидРабот);
		Запрос.УстановитьПараметр("Период", ДатаАктуальности);
		
		Если ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Подразделение Тогда
			Запрос.УстановитьПараметр("Подразделение", Подразделение);
			Запрос.УстановитьПараметр("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
		ИначеЕсли ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Организация Тогда
			Запрос.УстановитьПараметр("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
			Запрос.УстановитьПараметр("Организация", Организация);
		Иначе
			Запрос.УстановитьПараметр("Организация", ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"));
			Запрос.УстановитьПараметр("Подразделение", ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка"));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(КвалификационныйРазряд) Тогда
			Запрос.УстановитьПараметр("Разряд", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРабот, "КвалификационныйРазряд"));
		Иначе
			Запрос.УстановитьПараметр("Разряд", КвалификационныйРазряд);
		КонецЕсли;
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			Возврат Результат.Расценка;
		КонецЕсли;
		
	ИначеЕсли ПараметрыВидыРабот.РасценкаПоТарифнойСетке И ИспользоватьТарифныеСеткиПриРасчетеЗарплаты Тогда
		
		Если НЕ ЗначениеЗаполнено(КвалификационныйРазряд) Тогда
			Возврат 0;
		КонецЕсли;
		
		Запрос = Новый Запрос("
			|ВЫБРАТЬ
			|	МАКСИМУМ(ЗначенияТарифов.Тариф * ВидыРабот.Трудоемкость / ВидыРабот.КратностьТрудоемкости) КАК Расценка
			|ИЗ
			|	Справочник.ВидыРаботСотрудников КАК ВидыРабот
			|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
			|	ПО ВидыРабот.Должность = Должности.Ссылка
			|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|	ПО Должности.Ссылка = ШтатноеРасписание.Должность
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗначенияТарифов.СрезПоследних(&Период) КАК ЗначенияТарифов
			|	ПО ЗначенияТарифов.ТарифнаяСетка = ШтатноеРасписание.ТарифнаяСетка
			|	И ЗначенияТарифов.РазрядКатегория = &Разряд
			|ГДЕ
			|	ВидыРабот.Ссылка = &ВидРабот");
		
		Запрос.УстановитьПараметр("ВидРабот", ВидРабот);
		Запрос.УстановитьПараметр("Период", ДатаАктуальности);
		Запрос.УстановитьПараметр("Разряд", КвалификационныйРазряд);
		
		Результат = Запрос.Выполнить().Выбрать();
		Если Результат.Следующий() Тогда
			Возврат Результат.Расценка;
		КонецЕсли;
	КонецЕсли;
	//-- Локализация
	
	//-- НЕ УТКА
	
	Отбор = Новый Структура("ВидРабот", ВидРабот);
	
	ИспользоватьРазрядыРасценокРаботСотрудников = Истина;
	//++ НЕ УТКА
	ИспользоватьРазрядыРасценокРаботСотрудников = ПолучитьФункциональнуюОпцию("ИспользоватьРазрядыРасценокРаботСотрудников");
	//-- НЕ УТКА
	
	Если ИспользоватьРазрядыРасценокРаботСотрудников И Не ЗначениеЗаполнено(КвалификационныйРазряд) Тогда
		Отбор.Вставить("КвалификационныйРазряд", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидРабот, "КвалификационныйРазряд"));
	ИначеЕсли ИспользоватьРазрядыРасценокРаботСотрудников Тогда
		Отбор.Вставить("КвалификационныйРазряд", КвалификационныйРазряд);
	КонецЕсли;
	
	СтруктураРасценки = РегистрыСведений.РасценкиРаботСотрудников.ПолучитьПоследнее(ДатаАктуальности, Отбор);
	
	Расценка = СтруктураРасценки.Расценка;
	
	Если ПараметрыВидыРабот.РасценкаЗаЧасРаботы Тогда
		Расценка = СтруктураРасценки.Расценка * ПараметрыВидыРабот.Трудоемкость / ПараметрыВидыРабот.КратностьТрудоемкости;
	КонецЕсли;
	
	Возврат Расценка;
	
КонецФункции

// Определяется настройки отражения в бух. учете по умолчанию для указанного вида работ.
//
// Параметры:
//   ВидРабот - СправочникСсылка.ВидыРаботСотрудников - Вид работ.
//
// Возвращаемое значение:
//   Структура - Описание полей см. в ресурсах регистра сведений БухучетРаботСотрудников.
//
Функция БухучетВидаРабот(ВидРабот) Экспорт
	ДанныеРегистра = Неопределено;
	ИнтеграцияБЗК.ЗаполнитьПоДаннымРегистраБухучетВидаРабот(ДанныеРегистра, ВидРабот);
	Возврат ДанныеРегистра;
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Параметры автозаполения трудоемкости.
// 
// Параметры:
//  Объект - СправочникОбъект.ВидыРаботСотрудников
// 
// Возвращаемое значение:
//  Неопределено, Структура - Параметры автозаполения трудоемкости:
// * Трудоемкость - Число
// * КратностьТрудоемкости - Число
Функция ПараметрыАвтозаполенияТрудоемкости(Объект) Экспорт
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.ЕдиницаИзмерения, "ТипИзмеряемойВеличины,Числитель,Знаменатель");
	
	Если ЗначенияРеквизитов.ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Время
		И ЗначениеЗаполнено(ЗначенияРеквизитов.Числитель)
		И ЗначениеЗаполнено(ЗначенияРеквизитов.Знаменатель) Тогда
		
		Дробь = ПроизводствоСервер.Дробь(ЗначенияРеквизитов.Числитель, 3600 * ЗначенияРеквизитов.Знаменатель);
		ПроизводствоСервер.СократитьДробь(Дробь);
		
		Возврат Новый Структура("Трудоемкость,КратностьТрудоемкости", Дробь.Числитель, Дробь.Знаменатель);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли