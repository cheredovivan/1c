#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция ОписаниеЗапрашиваемыхДанных() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТипыДанных.Ссылка КАК Ссылка,
	|	НЕ ТипыДанных.НеЗапрашивать КАК Запрашивать,
	|	ТипыДанных.ЗапросФайлов КАК ЗапросФайлов,
	|	ТипыДанных.ЗапросДанных КАК ЗапросДанных
	|ИЗ
	|	Справочник.ТипыДанныхДляОформленияНаРаботу КАК ТипыДанных";
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ИменаСвойств = "Запрашивать,ЗапросФайлов,ЗапросДанных";
	Описание = Новый Соответствие;
	Для каждого СтрокаТЗ Из ТаблицаДанных Цикл
		СвойстваДанных = Новый Структура(ИменаСвойств);
		ЗаполнитьЗначенияСвойств(СвойстваДанных, СтрокаТЗ);
		Описание.Вставить(СтрокаТЗ.Ссылка, Новый ФиксированнаяСтруктура(СвойстваДанных));
	КонецЦикла;
	
	Возврат Новый ФиксированноеСоответствие(Описание);

КонецФункции

Функция НачальноеЗаполнение(ПараметрыОбновления = Неопределено) Экспорт

	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
	КонецЕсли;
	
	// Поля
	// PredefinedDataName - Имя предопределенных данных
	// Name - Наименование
	// DataRequest - запрос данных
	// QueryingFiles - Запрос файлов
	ОписаниеНачальныхДанных = Справочники.ТипыДанныхДляОформленияНаРаботу.ПолучитьМакет("ЗапрашиваемыеДанные").ПолучитьТекст();
	ОписаниеТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(ОписаниеНачальныхДанных).Данные;
	
	ОбработкаЗавершена = Истина;
	Для каждого СтрокаТЗ Из ОписаниеТаблица Цикл
		ИмяПредопределенныхДанных = СтрокаТЗ.PredefinedDataName;
		Если МассивОбновленных.Найти(ИмяПредопределенныхДанных) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Описание = Новый Структура("Наименование,ЗапросДанных,ЗапросФайлов");
		Описание.Наименование = СтрокаТЗ.Name;
		Описание.ЗапросДанных = ?(СтрокаТЗ.DataRequest = "0", Ложь, Истина);
		Описание.ЗапросФайлов = ?(СтрокаТЗ.QueryingFiles = "0", Ложь, Истина);
		ОбработкаЗавершена = Ложь;
		ОбновитьПредопределенныйЭлемент(ИмяПредопределенныхДанных, Описание, ПараметрыОбновления);
	КонецЦикла;
		
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьПредопределенныйЭлемент(ИмяПредопределенныхДанных, Описание, ПараметрыОбновления)
	
	ПолноеИмяПредопределенныхДанных = СтрШаблон("%1.%2", "Справочник.ТипыДанныхДляОформленияНаРаботу", ИмяПредопределенныхДанных);
	СсылкаПредопределенного = ОбщегоНазначения.ПредопределенныйЭлемент(ПолноеИмяПредопределенныхДанных);
	НовыйЭлемент = Ложь;
	Если ЗначениеЗаполнено(СсылкаПредопределенного) Тогда
		СправочникОбъект = СсылкаПредопределенного.ПолучитьОбъект();
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "Справочник.ТипыДанныхДляОформленияНаРаботу", "Ссылка", СсылкаПредопределенного) Тогда
			Возврат;
		КонецЕсли;
	Иначе
		НовыйЭлемент = Истина;
		СправочникОбъект = Справочники.ТипыДанныхДляОформленияНаРаботу.СоздатьЭлемент();
		СправочникОбъект.ИмяПредопределенныхДанных = ИмяПредопределенныхДанных;
	КонецЕсли;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
	КонецЕсли;
	
	СправочникОбъект.Наименование = Описание.Наименование;
	СправочникОбъект.ЗапросФайлов = Описание.ЗапросФайлов;
	СправочникОбъект.ЗапросДанных = Описание.ЗапросДанных;
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СправочникОбъект);
	МассивОбновленных.Добавить(ИмяПредопределенныхДанных);
	
	Если Не НовыйЭлемент Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
