///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Создает новую настройку подключения к онлайн-заказам.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация для которой создается новая настройка.
//
// Возвращаемое значение:
//  Структура - результат создания торговой точки:
//    * Ссылка - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам - новая настройка;
//    * Ошибка - Булево - признак ошибки сохранения;
//    * ОшибкаОтправкиВСервис - Булево - признак ошибки отправки в сервис;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//
Функция НоваяНастройкаПодключения(Организация) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("ОшибкаОтправкиВСервис", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	Результат.Вставить("Ссылка", Неопределено);
	
	НастройкаПодключения = СоздатьЭлемент();
	НастройкаПодключения.Заполнить(Неопределено);
	
	НастройкаПодключения.Организация = Организация;
	НастройкаПодключения.Наименование = Строка(Организация);
	НастройкаПодключения.Используется = Истина;
	НастройкаПодключения.ПоРеквизитамОтФизЛиц = Истина;
	НастройкаПодключения.ПоРеквизитамОтЮрЛиц = Истина;
	
	НастройкаПодключения.Записать();
	
	Результат.Ссылка = НастройкаПодключения.Ссылка;
	
	РезультатОперации = ОнлайнЗаказыСлужебный.ОбновитьНастройку(
		НастройкаПодключения.Ссылка);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		
		Результат.ОшибкаОтправкиВСервис = Истина;
		
		РезультатУстановки = УстановитьПризнакОшибкиСинхронизации(НастройкаПодключения);
		
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка отправки настройки в сервис. Подробности:
				|%1';
				|en = 'An error occurred when sending the setting to the service. Details:
				|%1'"),
			РезультатОперации.СообщениеОбОшибке);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Сохраняет настройки подключения к СБП в информационную базу.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//  настройка для которой выполняется операция включения.
//
// Возвращаемое значение:
//  Структура - результат создания торговой точки:
//    * Ошибка - Булево - признак ошибки сохранения;
//    * ОшибкаОтправкиВСервис - Булево - признак ошибки отправки в сервис;
//    * СообщениеОбОшибке  - Строка, ФорматированнаяСтрока - сообщение об ошибке для пользователя.
//
Функция ВключитьНастройку(НастройкаПодключения) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("ОшибкаОтправкиВСервис", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиПодключенияКОнлайнЗаказам");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", НастройкаПодключения);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НастройкаПодключенияОбъект = НастройкаПодключения.ПолучитьОбъект();
		НастройкаПодключенияОбъект.Заблокировать();
		НастройкаПодключенияОбъект.Используется = Истина;
		НастройкаПодключенияОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		// Вызов во вложенной транзакции не предполагается,
		// вместо вызова исключения обработка ошибки делегируется
		// вызывающему методу.
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось включить настройку подключения по причине:
				|%1';
				|en = 'Cannot enable the connection setting. Reason:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Результат;
		
	КонецПопытки;
	
	РезультатОперации = ОнлайнЗаказыСлужебный.ОбновитьНастройку(
		НастройкаПодключения);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		
		Результат.ОшибкаОтправкиВСервис = Истина;
		
		РезультатУстановки = УстановитьПризнакОшибкиСинхронизации(НастройкаПодключенияОбъект);
		
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Ошибка отправки настройки в сервис. Подробности:
				|%1';
				|en = 'An error occurred when sending the setting to the service. Details:
				|%1'"),
			РезультатОперации.СообщениеОбОшибке);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует настройки подключения для выполнения запросов в сервисе.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка выполнения операции.
//
// Возвращаемое значение:
//  Неопределено - Настройки не найдены.
//  Структура - Содержит параметры настройки:
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка подключения к сервису;
//    * ПредставлениеОрганизации - Строка - Представление организации на странице заказа;
//    * Используется - Булево - Признак использования настройки подключения;
//    * НастройкаОблачнойКассы - СправочникСсылка.НастройкиПодключенияКОблачнымКассам -
//      настройка подключения к облачной кассе.
//    * НастройкиОплат - Соответствие Из КлючИЗначение - Содержит данные по способам оплат:
//      ** Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов - Допустимый способ оплаты заказа;
//      ** Значение - Произвольный - Настройка подключения по способу оплаты.
//    * НастройкиПриемаОплатПоРеквизитам - Структура - настройки приема оплат от физ. лиц:
//      ** ИспользуетсяПриемОплатОтФизЛиц - Булево - настроен прием оплат от физ. лиц;
//      ** ИспользуетсяПриемОплатОтЮрЛиц - Булево - настроен прием оплат от юр. лиц.
//
Функция ПараметрыНастройки(НастройкаПодключения) Экспорт
	
	// Заполнение настроек проведения оплаты.
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказам.Используется КАК Используется,
	|	НастройкиПодключенияКОнлайнЗаказам.Наименование КАК Наименование,
	|	НастройкиПодключенияКОнлайнЗаказам.ПоРеквизитамОтФизЛиц КАК ПоРеквизитамОтФизЛиц,
	|	НастройкиПодключенияКОнлайнЗаказам.ПоРеквизитамОтЮрЛиц КАК ПоРеквизитамОтЮрЛиц,
	|	НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе КАК НастройкаПодключенияКОблачнойКассе
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка = &НастройкаПодключения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиОплаты.СпособОплаты КАК СпособОплаты,
	|	НастройкиОплаты.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиОплаты
	|ГДЕ
	|	НастройкиОплаты.Ссылка = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаПараметров = РезультатЗапроса[0].Выбрать();
	
	Если Не ВыборкаПараметров.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить("НастройкаПодключения",     НастройкаПодключения);
	ПараметрыНастройки.Вставить("ПредставлениеОрганизации", ВыборкаПараметров.Наименование);
	ПараметрыНастройки.Вставить("Используется",             ВыборкаПараметров.Используется);
	ПараметрыНастройки.Вставить("НастройкаОблачнойКассы",   ВыборкаПараметров.НастройкаПодключенияКОблачнойКассе);
	
	НастройкиПриемаОплатПоРеквизитам = Новый Структура;
	НастройкиПриемаОплатПоРеквизитам.Вставить(
		"ИспользуетсяПриемОплатОтФизЛиц",
		ВыборкаПараметров.ПоРеквизитамОтФизЛиц);
	НастройкиПриемаОплатПоРеквизитам.Вставить(
		"ИспользуетсяПриемОплатОтЮрЛиц",
		ВыборкаПараметров.ПоРеквизитамОтЮрЛиц);
		
	ПараметрыНастройки.Вставить("НастройкиПриемаОплатПоРеквизитам", НастройкиПриемаОплатПоРеквизитам);
	
	НастройкиОплат = Новый Соответствие;
	
	ВыборкаСпособовОплат = РезультатЗапроса[1].Выбрать();
	
	Пока ВыборкаСпособовОплат.Следующий() Цикл
		НастройкиОплат.Вставить(ВыборкаСпособовОплат.СпособОплаты, ВыборкаСпособовОплат.НастройкаПодключения);
	КонецЦикла;
	
	ПараметрыНастройки.Вставить("НастройкиОплат", НастройкиОплат);
	
	Возврат ПараметрыНастройки;
	
КонецФункции

// Определяет настройки подключений онлайн-заказов.
//
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация по которой выполняется запрос.
//
// Возвращаемое значение:
//  Массив Из СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам - используемые настройки.
//
Функция НастройкиОрганизации(Организация) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ИспользуемыеНастройки", Новый Массив);
	Результат.Вставить("ОтключенныеНастройки", Новый Массив);
	
	ИспользуемыеНастройки = Новый Массив;
	ОтключенныеНастройки = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка КАК Ссылка,
	|	НастройкиПодключенияКОнлайнЗаказам.Используется КАК Используется
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Если ВыборкаДетальныеЗаписи.Используется Тогда
			ИспользуемыеНастройки.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		Иначе
			ОтключенныеНастройки.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Результат.ИспользуемыеНастройки = ИспользуемыеНастройки;
	Результат.ОтключенныеНастройки = ОтключенныеНастройки;
	
	Возврат Результат;
	
КонецФункции

// Определяет настройку подключения оплаты по ее способу.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка выполнения операции.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов -
//    способ оплаты.
//
// Возвращаемое значение:
//  Произвольный - настройка подключения по способу оплаты.
//
Функция НастройкаОплаты(НастройкаПодключения, СпособОплаты) Экспорт
	
	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка = &НастройкаПодключения
	|	И НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.СпособОплаты = &СпособОплаты";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Запрос.УстановитьПараметр("СпособОплаты", СпособОплаты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.НастройкаПодключения;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Определяет перечень настроек оплат для переданной настройки онлайн-заказов.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка выполнения операции.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов -
//    способ оплаты.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - настройки подключения по способу оплаты:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов- Способ оплаты;
//    * Значение - Произвольный- Ссылка на настройку подключения по способу оплаты
//
Функция НастройкиОплат(НастройкаПодключения) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.СпособОплаты КАК СпособОплаты,
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.СпособОплаты, Выборка.НастройкаПодключения);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция НастройкаОблачнойКассы(НастройкаПодключения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе КАК НастройкаПодключенияКОблачнойКассе
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", НастройкаПодключения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.НастройкаПодключенияКОблачнойКассе;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Определяет перечень неиспользуемых настроек оплат и облачных касс по переданной настройки онлайн-заказов.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка выполнения операции.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов -
//    способ оплаты.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - настройки подключения по способу оплаты,
//    используемые только в переданной настройке:
//    * Ключ - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов- Способ оплаты;
//    * Значение - Произвольный- Ссылка на настройку подключения по способу оплаты
//
Функция НеиспользуемыеНастройки(НастройкаПодключения) Экспорт
	
	Результат = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.СпособОплаты КАК СпособОплаты,
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения КАК НастройкаПодключения
	|ПОМЕСТИТЬ ВТ_НастройкиОплат
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка = &НастройкаПодключения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка) КАК ЧислоИспользований,
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения КАК НастройкаПодключения
	|ПОМЕСТИТЬ ВТ_ИспользованиеНастроекОплат
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка <> &НастройкаПодключения
	|	И НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения В
	|			(ВЫБРАТЬ
	|				ВТ_НастройкиОплат.НастройкаПодключения КАК НастройкаПодключения
	|			ИЗ
	|				ВТ_НастройкиОплат КАК ВТ_НастройкиОплат)
	|
	|СГРУППИРОВАТЬ ПО
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_НастройкиОплат.СпособОплаты КАК СпособОплаты,
	|	ВТ_НастройкиОплат.НастройкаПодключения КАК НастройкаПодключения
	|ИЗ
	|	ВТ_НастройкиОплат КАК ВТ_НастройкиОплат
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИспользованиеНастроекОплат КАК ВТ_ИспользованиеНастроекОплат
	|		ПО ВТ_НастройкиОплат.НастройкаПодключения = ВТ_ИспользованиеНастроекОплат.НастройкаПодключения
	|ГДЕ
	|	ЕСТЬNULL(ВТ_ИспользованиеНастроекОплат.ЧислоИспользований, 0) = 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.НастройкаПодключенияКОблачнойКассе КАК НастройкаПодключения
	|ИЗ
	|	(ВЫБРАТЬ
	|		НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе КАК НастройкаПодключенияКОблачнойКассе
	|	ИЗ
	|		Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|	ГДЕ
	|		НастройкиПодключенияКОнлайнЗаказам.Ссылка = &НастройкаПодключения
	|		И НЕ НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе.Ссылка ЕСТЬ NULL) КАК ВложенныйЗапрос
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|		ПО ВложенныйЗапрос.НастройкаПодключенияКОблачнойКассе = НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе
	|			И (НастройкиПодключенияКОнлайнЗаказам.Ссылка <> &НастройкаПодключения)
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	НеиспользуемыеНастройкиОплат = Новый Соответствие;
	ВыборкаНастроекОплат = РезультатыЗапроса[2].Выбрать();
	Пока ВыборкаНастроекОплат.Следующий() Цикл
		НеиспользуемыеНастройкиОплат.Вставить(ВыборкаНастроекОплат.СпособОплаты, ВыборкаНастроекОплат.НастройкаПодключения);
	КонецЦикла;
	
	НеиспользуемыеНастройкиОблачныхКасс = Новый Соответствие;
	ВыборкаНастроекОблачныхКасс = РезультатыЗапроса[3].Выбрать();
	Пока ВыборкаНастроекОблачныхКасс.Следующий() Цикл
		НеиспользуемыеНастройкиОблачныхКасс.Вставить(
			Перечисления.СпособыОплатыОнлайнЗаказов.ПустаяСсылка(),
			ВыборкаНастроекОблачныхКасс.НастройкаПодключения);
	КонецЦикла;
	
	Результат.Вставить(
		ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат(),
		НеиспользуемыеНастройкиОплат);
	Результат.Вставить(
		ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса(),
		НеиспользуемыеНастройкиОблачныхКасс);
	
	Возврат Результат;
	
КонецФункции

// Определяет наличие введенных настроек онлайн-заказов по переданной настройке оплаты.
//
// Параметры:
//  НастройкаОплаты - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей,
//    СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - искомая настройка подключения.
//  ИсключаемаяНастройка - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    Настройка подключения не учитываемая в результатах запроса.
//
// Возвращаемое значение:
//  Булево - Истина, если настройки введены.
//
Функция СуществуютНастройкиПоНастройкеОплаты(НастройкаОплаты, ИсключаемаяНастройка = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения = &НастройкаОплаты
	|	И НастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка <> &ИсключаемаяНастройка";
	
	Запрос.УстановитьПараметр("НастройкаОплаты",      НастройкаОплаты);
	Запрос.УстановитьПараметр("ИсключаемаяНастройка", ИсключаемаяНастройка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Определяет наличие введенных настроек онлайн-заказов по переданной настройке оплаты.
//
// Параметры:
//  НастройкаОблачнойКассы - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам - искомая настройка подключения.
//  ИсключаемаяНастройка - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    Настройка подключения не учитываемая в результатах запроса.
//
// Возвращаемое значение:
//  Булево - Истина, если настройки введены.
//
Функция СуществуютНастройкиПоОблачнойКассе(НастройкаОблачнойКассы, ИсключаемаяНастройка = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам
	|ГДЕ
	|	НастройкиПодключенияКОнлайнЗаказам.Ссылка <> &ИсключаемаяНастройка
	|	И НастройкиПодключенияКОнлайнЗаказам.НастройкаПодключенияКОблачнойКассе = &НастройкаОблачнойКассы";
	
	Запрос.УстановитьПараметр("НастройкаОблачнойКассы", НастройкаОблачнойКассы);
	Запрос.УстановитьПараметр("ИсключаемаяНастройка", ИсключаемаяНастройка);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

// Определяет перечень введенных настроек онлайн-заказов по переданной настройке оплаты.
//
// Параметры:
//  НастройкаОплаты - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка подключения к СБП.
//  СпособОплаты - ПеречислениеСсылка.СпособыОплатыОнлайнЗаказов -
//    способ оплаты.
//
// Возвращаемое значение:
//  Массив Из Справочник.НастройкиПодключенияКОнлайнЗаказам - перечень настроек.
//
Функция НастройкиПодключенияПоНастройкеОплаты(НастройкаОплаты) Экспорт
	
	НастройкиПодключения = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ННастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам.НастройкиОплаты КАК ННастройкиПодключенияКОнлайнЗаказамНастройкиОплаты
	|ГДЕ
	|	ННастройкиПодключенияКОнлайнЗаказамНастройкиОплаты.НастройкаПодключения = &НастройкаОплаты";
	
	Запрос.УстановитьПараметр("НастройкаОплаты", НастройкаОплаты);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НастройкиПодключения.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	Возврат НастройкиПодключения;
	
КонецФункции

// Определяет наличие настроек подключения онлайн-заказов.
//
// Возвращаемое значение:
//  Булево - если Истина, есть настройки подключения.
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ИСТИНА КАК Признак
	|ИЗ
	|	Справочник.НастройкиПодключенияКОнлайнЗаказам КАК НастройкиПодключенияКОнлайнЗаказам";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Устанавливает признак ошибки синхронизации настройки с сервисом.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКОнлайнЗаказам -
//    настройка подключения к онлайн-заказам.
//
Процедура УстановитьОшибкуСинхронизации(НастройкаПодключения) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.НастройкиПодключенияКОнлайнЗаказам");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", НастройкаПодключения);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НастройкаПодключенияОбъект = НастройкаПодключения.ПолучитьОбъект();
		НастройкаПодключенияОбъект.ОшибкаСинхронизации = Истина;
		НастройкаПодключенияОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
	КонецПопытки;
	
КонецПроцедуры

Функция УстановитьПризнакОшибкиСинхронизации(НастройкаПодключенияОбъект)
	
	Результат = Новый Структура;
	Результат.Вставить("Ошибка", Ложь);
	Результат.Вставить("СообщениеОбОшибке", "");
	
	НачатьТранзакцию();
	
	Попытка
		
		НастройкаПодключенияОбъект.ОшибкаСинхронизации = Истина;
		НастройкаПодключенияОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		// Вызов во вложенной транзакции не предполагается,
		// вместо вызова исключения обработка ошибки делегируется
		// вызывающему методу.
		Результат.Ошибка = Истина;
		Результат.СообщениеОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось включить настройку подключения по причине:
				|%1';
				|en = 'Cannot enable the connection setting. Reason:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Результат;
		
	КонецПопытки;
	
КонецФункции

#КонецОбласти

#КонецЕсли