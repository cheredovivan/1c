///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	Для Каждого НастройкаОплаты Из Объект.НастройкиОплаты Цикл
		Если НастройкаОплаты.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПc2b Тогда
			НастройкаПодключенияКСБПc2b = НастройкаОплаты.НастройкаПодключения;
		ИначеЕсли НастройкаОплаты.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.СБПb2b Тогда
			НастройкаПодключенияКСБПb2b = НастройкаОплаты.НастройкаПодключения;
		ИначеЕсли НастройкаОплаты.СпособОплаты = Перечисления.СпособыОплатыОнлайнЗаказов.Эквайринг Тогда
			НастройкаПодключенияКИнтернетЭквайрингу = НастройкаОплаты.НастройкаПодключения;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Объект.Используется = Истина;
		Объект.ПоРеквизитамОтФизЛиц = Истина;
		Объект.ПоРеквизитамОтЮрЛиц = Истина;
		
		Если ЗначениеЗаполнено(Объект.Организация)
			И ПустаяСтрока(Объект.Наименование) Тогда
			Объект.Наименование = Строка(Объект.Организация);
		КонецЕсли;
		
	Иначе
		
		УстановитьПривилегированныйРежим(Истина);
		ДанныеХранилища = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(
			Объект.Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
	НастройкаОнлайнЗаказовДоступна = ОнлайнЗаказыСлужебный.НастройкаОнлайнЗаказовДоступна();
	
	УстановитьВидимостьДоступность();
	УправлениеЭлементамиФормыПоДанным();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НеобходимОтзывТокенов = Ложь;
	
	Если Не ИзменениеАктуальности
		И Не ТекущийОбъект.Используется
		И ЗначениеЗаполнено(ТекущийОбъект.Ссылка) Тогда
		
		НеобходимОтзывТокенов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			ТекущийОбъект.Ссылка,
			"Используется");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	РезультатПроверкиШаблонов = Неопределено;
	ИспользоватьШаблоныСообщений = ОнлайнЗаказыСлужебный.ИспользоватьШаблоныСообщений();
	
	Если ИспользоватьШаблоныСообщений Тогда
		РезультатПроверкиШаблонов = ОнлайнЗаказыСлужебный.ВсеШаблоныСозданы();
	КонецЕсли;
	
	Если ИзменениеАктуальности Тогда
		ИзменениеАктуальности = Ложь;
		УправлениеЭлементамиФормыПоДанным();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи", Ложь);
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, ПараметрыЗаписи);
	
	Если ОписаниеОповещенияОЗакрытии <> Неопределено
		И ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры <> Неопределено Тогда
		ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить(
			"НастройкаПодключенияКОнлайнЗаказам",
			Объект.Ссылка);
		ОписаниеОповещенияОЗакрытии.ДополнительныеПараметры.Вставить(
			"ИзмененыНастройкиСтраницы",
			ИзмененыНастройкиСтраницы);
	КонецЕсли;
	
	Если НеобходимОтзывТокенов Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Выполняется отзыв неиспользуемых токенов.';
												|en = 'Unused tokens are being revoked.'");
		
		РезультатВыполнения = ОтозватьТокеныЗапускЗадания();
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения(
			"ОтозватьТокеныЗавершение",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		Если РезультатВыполнения = Неопределено
			Или РезультатВыполнения.Статус = "Выполнено"
			Или РезультатВыполнения.Статус = "Ошибка" Тогда
			
			ОтозватьТокеныЗавершение(РезультатВыполнения, ДополнительныеПараметры);
			Возврат;
			
		КонецЕсли;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатВыполнения,
			ОповещениеОЗавершении,
			ПараметрыОжидания);
		
	Иначе
		ПослеЗавершенияОперацийСТокенами(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ПредставлениеНеРедактировалось = Объект.Наименование = Строка(Объект.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ПредставлениеНеРедактировалось Тогда
		Объект.Наименование = Строка(Объект.Организация);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПредупреждениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "operation:pageSettings" Тогда
		ИнициализацияОправкиВСервис(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодключениеКСБПc2bОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("СБПc2b", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюСсылкуСБПc2b(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодключениеКСБПb2bОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("СБПb2b", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюСсылкуСБПb2b(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодключениеИнтернетЭквайрингОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("ИнтернетЭквайринг", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюСсылкуИнтернетЭквайринг(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодключениеКОблачнойКассеОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("ОблачнаяКасса", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюСсылкуОблачнаяКасса(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПоБанковскимРеквизитамФизЛицаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("БанковскиеРеквизитыФизЛица", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюПоБанковскимРеквизитамФизЛица(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПоБанковскимРеквизитамЮрЛицаОбработкаНавигационнойСсылки(
	Элемент,
	НавигационнаяСсылкаФорматированнойСтроки,
	СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("БанковскиеРеквизитыЮрЛица", НавигационнаяСсылкаФорматированнойСтроки);
	Иначе
		ОбработатьНавигационнуюПоБанковскимРеквизитамЮрЛица(НавигационнаяСсылкаФорматированнойСтроки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Записать(Новый Структура("ЗакрытьПослеЗаписи", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСтраницуЗаказа(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЗадатьВопросОЗаписи("ВнешнийВидСтраницы", Неопределено);
	Иначе
		НастроитьВнешнийВидСтраницы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьВСервис(Команда)
	
	Если Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Перед обновлением настроек в сервисе необходимо записать изменения.';
				|en = 'Save the changes before updating the settings in the service.'"));
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Необходимо записать настройку подключения.';
				|en = 'It is necessary to save the connection setting.'"));
		Возврат;
	КонецЕсли;
	
	ИнициализацияОправкиВСервис(Ложь, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВыборНастройки

&НаКлиенте
Процедура ЗадатьВопросОЗаписи(ВидНастройки, НавигационнаяСсылка)
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтветаНаВопросОЗаписи",
		ЭтотОбъект,
		Новый Структура(
			"ВидОбрабатываемойНастройки, НавигационнаяСсылка",
			ВидНастройки,
			НавигационнаяСсылка));
			
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, "Записать");
	Кнопки.Добавить(КодВозвратаДиалога.Отмена, "Отмена");
	
	ПоказатьВопрос(
		Оповещение,
		НСтр("ru = 'Для продолжения необходимо записать настройку подключения.';
			|en = 'To continue, save the connection setting.'"),
		Кнопки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОЗаписи(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Записать();
		
		Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
			Возврат;
		КонецЕсли;
		
		Если Параметры.ВидОбрабатываемойНастройки = "СБПc2b" Тогда
			ОбработатьНавигационнуюСсылкуСБПc2b(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "СБПb2b" Тогда
			ОбработатьНавигационнуюСсылкуСБПb2b(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "ИнтернетЭквайринг" Тогда
			ОбработатьНавигационнуюСсылкуИнтернетЭквайринг(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "ОблачнаяКасса" Тогда
			ОбработатьНавигационнуюСсылкуОблачнаяКасса(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "БанковскиеРеквизитыФизЛица" Тогда
			ОбработатьНавигационнуюПоБанковскимРеквизитамФизЛица(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "БанковскиеРеквизитыЮрЛица" Тогда
			ОбработатьНавигационнуюПоБанковскимРеквизитамЮрЛица(Параметры.НавигационнаяСсылка);
		ИначеЕсли Параметры.ВидОбрабатываемойНастройки = "ВнешнийВидСтраницы" Тогда
			НастроитьВнешнийВидСтраницы();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеВыбораНастройки(
	Знач НастройкаПодключения,
	Знач ДекорацияПодключения,
	Знач ДекорацияДействия)
	
	ИзмененыНастройкиСтраницы = Истина;
	
	Записать();
	УстановитьПредставлениеПодключенияСДействием(
		НастройкаПодключения,
		ДекорацияПодключения,
		ДекорацияДействия);
	
КонецПроцедуры

#КонецОбласти

#Область СпособыОплаты

&НаКлиенте
Процедура УдалитьСпособОплаты(СпособОплаты)
	
	Модифицированность = Истина;
	
	Отбор = Новый Структура;
	Отбор.Вставить("СпособОплаты", СпособОплаты);
	
	УдаленнаяНастройкаОплаты = Новый Структура;
	
	НайденныеЗначения = Объект.НастройкиОплаты.НайтиСтроки(Отбор);
	
	Для Каждого НайденноеЗначение Из НайденныеЗначения Цикл
		УдаленнаяНастройкаОплаты.Вставить("СпособОплаты", НайденноеЗначение.СпособОплаты);
		УдаленнаяНастройкаОплаты.Вставить("НастройкаПодключения", НайденноеЗначение.НастройкаПодключения);
		Объект.НастройкиОплаты.Удалить(НайденноеЗначение);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УдаленнаяНастройкаОплаты) Тогда
		ОтозватьТокен(
			ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат(),
			УдаленнаяНастройкаОплаты.НастройкаПодключения,
			УдаленнаяНастройкаОплаты.СпособОплаты);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылкуСБПc2b(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "chose:setting" Тогда
		ОнлайнЗаказыКлиентСобытия.ПриВыбореНастройкиПодключения(
			ЭтотОбъект,
			Новый ОписаниеОповещения("ПослеВыбораНастройкиПодключенияСБПc2b", ЭтотОбъект),
			ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b"));
	ИначеЕсли НавигационнаяСсылка = "disable:setting" Тогда
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b"));
		НастройкаПодключенияКСБПc2b = Неопределено;
		ПослеВыбораНастройки(
			НастройкаПодключенияКСБПc2b,
			"ДекорацияПодключениеКСБПc2b",
			"ДекорацияПодключениеКСБПc2bДействие");
		ПослеЗаписи(Новый Структура);
	ИначеЕсли НавигационнаяСсылка = "open:setting" Тогда
		ОткрытьНастройкуПодключения(
			НастройкаПодключенияКСБПc2b,
			"ДекорацияПодключениеКСБПc2b");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНастройкиПодключенияСБПc2b(
		НастройкаПодключения,
		ДополнительныеПараметры) Экспорт
	
	Если НастройкаПодключения <> Неопределено Тогда
		
		НастройкаПодключенияКСБПc2b = НастройкаПодключения;
		
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b"));
		
		СпособОплаты = Объект.НастройкиОплаты.Добавить();
		СпособОплаты.СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПc2b");
		СпособОплаты.НастройкаПодключения = НастройкаПодключенияКСБПc2b;
		
		ПослеВыбораНастройки(
			НастройкаПодключенияКСБПc2b,
			"ДекорацияПодключениеКСБПc2b",
			"ДекорацияПодключениеКСБПc2bДействие");
		ПослеЗаписи(Новый Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылкуСБПb2b(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "chose:setting" Тогда
		ОнлайнЗаказыКлиентСобытия.ПриВыбореНастройкиПодключения(
			ЭтотОбъект,
			Новый ОписаниеОповещения("ПослеВыбораНастройкиПодключенияСБПb2b", ЭтотОбъект),
			ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПb2b"));
	ИначеЕсли НавигационнаяСсылка = "disable:setting" Тогда
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПb2b"));
		НастройкаПодключенияКСБПb2b = Неопределено;
		ПослеВыбораНастройки(
			НастройкаПодключенияКСБПb2b,
			"ДекорацияПодключениеКСБПb2b",
			"ДекорацияПодключениеКСБПb2bДействие");
		ПослеЗаписи(Новый Структура);
	ИначеЕсли НавигационнаяСсылка = "open:setting" Тогда
		ОткрытьНастройкуПодключения(
			НастройкаПодключенияКСБПb2b,
			"ДекорацияПодключениеКСБПb2b");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНастройкиПодключенияСБПb2b(
		НастройкаПодключения,
		ДополнительныеПараметры) Экспорт
	
	Если НастройкаПодключения <> Неопределено Тогда
		
		НастройкаПодключенияКСБПb2b = НастройкаПодключения;
		
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПb2b"));
		
		СпособОплаты = Объект.НастройкиОплаты.Добавить();
		СпособОплаты.СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.СБПb2b");
		СпособОплаты.НастройкаПодключения = НастройкаПодключенияКСБПb2b;
		
		ПослеВыбораНастройки(
			НастройкаПодключенияКСБПb2b,
			"ДекорацияПодключениеКСБПb2b",
			"ДекорацияПодключениеКСБПb2bДействие");
		ПослеЗаписи(Новый Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылкуИнтернетЭквайринг(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "chose:setting" Тогда
		ОнлайнЗаказыКлиентСобытия.ПриВыбореНастройкиПодключения(
			ЭтотОбъект,
			Новый ОписаниеОповещения("ПослеВыбораНастройкиПодключенияИнтернетЭквайринг", ЭтотОбъект),
			ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.Эквайринг"));
	ИначеЕсли НавигационнаяСсылка = "disable:setting" Тогда
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.Эквайринг"));
		НастройкаПодключенияКИнтернетЭквайрингу = Неопределено;
		ПослеВыбораНастройки(
			НастройкаПодключенияКИнтернетЭквайрингу,
			"ДекорацияПодключениеИнтернетЭквайринг",
			"ДекорацияПодключениеИнтернетЭквайрингДействие");
		ПослеЗаписи(Новый Структура);
	ИначеЕсли НавигационнаяСсылка = "open:setting" Тогда
		ОткрытьНастройкуПодключения(
			НастройкаПодключенияКИнтернетЭквайрингу,
			"ДекорацияПодключениеИнтернетЭквайринг");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНастройкиПодключенияИнтернетЭквайринг(
		НастройкаПодключения,
		ДополнительныеПараметры) Экспорт
	
	Если НастройкаПодключения <> Неопределено Тогда
		
		НастройкаПодключенияКИнтернетЭквайрингу = НастройкаПодключения;
		
		УдалитьСпособОплаты(ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.Эквайринг"));
		
		СпособОплаты = Объект.НастройкиОплаты.Добавить();
		СпособОплаты.СпособОплаты = ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.Эквайринг");
		СпособОплаты.НастройкаПодключения = НастройкаПодключенияКИнтернетЭквайрингу;
		
		ПослеВыбораНастройки(
			НастройкаПодключенияКИнтернетЭквайрингу,
			"ДекорацияПодключениеИнтернетЭквайринг",
			"ДекорацияПодключениеИнтернетЭквайрингДействие");
		ПослеЗаписи(Новый Структура);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюПоБанковскимРеквизитамФизЛица(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "action:enable" Тогда
		Объект.ПоРеквизитамОтФизЛиц = Истина;
	ИначеЕсли НавигационнаяСсылка = "action:disable" Тогда
		Объект.ПоРеквизитамОтФизЛиц = Ложь;
	КонецЕсли;
	
	ПослеВыбораИспользованияОплатыПоРеквизитамФизЛица();
	ПослеЗаписи(Новый Структура);
	
КонецПроцедуры

&НаСервере
Процедура ПослеВыбораИспользованияОплатыПоРеквизитамФизЛица()
	
	ИзмененыНастройкиСтраницы = Истина;
	
	Записать();
	УстановитьПредставлениеДекорацииПоБанковскимРеквизитамФизЛица();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюПоБанковскимРеквизитамЮрЛица(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "action:enable" Тогда
		Объект.ПоРеквизитамОтЮрЛиц = Истина;
	ИначеЕсли НавигационнаяСсылка = "action:disable" Тогда
		Объект.ПоРеквизитамОтЮрЛиц = Ложь;
	КонецЕсли;
	
	ПослеВыбораИспользованияОплатыПоРеквизитамЮрЛица();
	ПослеЗаписи(Новый Структура);
	
КонецПроцедуры

&НаСервере
Процедура ПослеВыбораИспользованияОплатыПоРеквизитамЮрЛица()
	
	ИзмененыНастройкиСтраницы = Истина;
	
	Записать();
	УстановитьПредставлениеДекорацииПоБанковскимРеквизитамЮрЛица();
	
КонецПроцедуры

#КонецОбласти

#Область ОблачнаяКасса

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылкуОблачнаяКасса(НавигационнаяСсылка)
	
	Если НавигационнаяСсылка = "chose:setting" Тогда
		ОнлайнЗаказыКлиентСобытия.ПриВыбореОблачнойКассы(
			ЭтотОбъект,
			Новый ОписаниеОповещения("ПослеВыбораНастройкиПодключенияКОблачнойКассе", ЭтотОбъект),
			Объект.Организация);
	ИначеЕсли НавигационнаяСсылка = "disable:setting" Тогда
		Если ЗначениеЗаполнено(Объект.НастройкаПодключенияКОблачнойКассе) Тогда
			НастройкаПодключенияКОблачнойКассе = Объект.НастройкаПодключенияКОблачнойКассе;
			Объект.НастройкаПодключенияКОблачнойКассе = Неопределено;
			ОтозватьТокен(
				ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса(),
				НастройкаПодключенияКОблачнойКассе,
				ПредопределенноеЗначение("Перечисление.СпособыОплатыОнлайнЗаказов.ПустаяСсылка"));
		КонецЕсли;
		ПослеВыбораНастройки(
			Объект.НастройкаПодключенияКОблачнойКассе,
			"ДекорацияПодключениеКОблачнойКассе",
			"ДекорацияПодключениеКОблачнойКассеДействие");
		ПослеЗаписи(Новый Структура);
	ИначеЕсли НавигационнаяСсылка = "open:setting" Тогда
		ОткрытьНастройкуПодключения(
			Объект.НастройкаПодключенияКОблачнойКассе,
			"ДекорацияПодключениеКОблачнойКассе");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораНастройкиПодключенияКОблачнойКассе(
		НастройкаПодключения,
		ДополнительныеПараметры) Экспорт
	
	Если НастройкаПодключения <> Неопределено Тогда
		
		Объект.НастройкаПодключенияКОблачнойКассе = НастройкаПодключения;
		
		ПослеВыбораНастройки(
			Объект.НастройкаПодключенияКОблачнойКассе,
			"ДекорацияПодключениеКОблачнойКассе",
			"ДекорацияПодключениеКОблачнойКассеДействие");
		
		ПослеЗаписи(Новый Структура);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

&НаКлиенте
Процедура ПослеОтветаНаВопросОСозданииШаблонов(Результат, Параметры) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи", Ложь);
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Параметры);
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		СозданныеШаблоны = СоздатьПредопределенныеШаблоныСообщений();
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			РезультатПроверкиШаблонов.Шаблоны,
			СозданныеШаблоны);
		РезультатПроверкиШаблонов.ВсеШаблоны = Истина;
		
		Если Не ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Предопределенные шаблоны созданы.';
					|en = 'Predefined templates have been created.'"));
		КонецЕсли;
		
	КонецЕсли;
	
	ИнициализацияОправкиВСервис(
		ДополнительныеПараметры.ЗакрытьПослеЗаписи,
		Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьПредопределенныеШаблоныСообщений()
	
	ОнлайнЗаказыСлужебный.УстановитьИспользованиеШаблоновСообщенийПроверкаПодсистем();
	Возврат ОнлайнЗаказыСлужебный.СоздатьПредопределенныеШаблоныСообщенийПроверкаПодсистем();
	
КонецФункции

#КонецОбласти

#Область ОтзывТокенаНастройкиОплаты

&НаКлиенте
Процедура ОтозватьТокен(
	ВидТокена,
	НастройкаПодключения,
	СпособОплаты)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Проверка использования и отзыв токена по настройке оплаты.';
											|en = 'Checking token usage and revocation on payment settings.'");
	
	РезультатВыполнения = ОтозватьТокенЗапускЗадания(ВидТокена, НастройкаПодключения, СпособОплаты);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОтозватьТокенЗавершение",
		ЭтотОбъект,
		Новый Структура(
			"ВидТокена, НастройкаПодключения, СпособОплаты",
			ВидТокена,
			НастройкаПодключения,
			СпособОплаты));
	
	Если РезультатВыполнения = Неопределено
		Или РезультатВыполнения.Статус = "Выполнено"
		Или РезультатВыполнения.Статус = "Ошибка" Тогда
		
		ОтозватьТокенЗавершение(
			РезультатВыполнения,
			Новый Структура(
				"ВидТокена, НастройкаПодключения, СпособОплаты",
				ВидТокена,
				НастройкаПодключения,
				СпособОплаты));
		
		Возврат;
		
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОтозватьТокенЗапускЗадания(Знач ВидТокена, Знач НастройкаПодключения, Знач СпособОплаты)
	
	Если СуществуютНастройки(ВидТокена, НастройкаПодключения) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Отзыв токена настройки оплаты.';
				|en = 'Revocation of payment setting token.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОнлайнЗаказыСлужебный.ОтозватьТокенНастройкиПодключения",
		ВидТокена,
		НастройкаПодключения,
		СпособОплаты);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция СуществуютНастройки(ВидТокена, НастройкаПодключения)
	
	Если ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат() Тогда
		Возврат Справочники.НастройкиПодключенияКОнлайнЗаказам.СуществуютНастройкиПоНастройкеОплаты(
			НастройкаПодключения,
			Объект.Ссылка)
	ИначеЕсли ВидТокена = ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса() Тогда
		Возврат Справочники.НастройкиПодключенияКОнлайнЗаказам.СуществуютНастройкиПоОблачнойКассе(
			НастройкаПодключения,
			Объект.Ссылка)
	КонецЕсли
	
КонецФункции

&НаКлиенте
Процедура ОтозватьТокенЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(
			Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПослеОтветаНаВопросОПовторномОтзывеТокена",
				ЭтотОбъект,
				Новый Структура(
					"ВидТокена, НастройкаОплаты",
					ДополнительныеПараметры.ВидТокена,
					ДополнительныеПараметры.НастройкаОплаты));
			ПоказатьВопрос(
				Оповещение,
				НСтр("ru = 'При отзыве токена возникла ошибка. Повторить попытку?';
					|en = 'An error occurred while revoking the token. Retry?'"),
				РежимДиалогаВопрос.ДаНетОтмена);
		Иначе
			Записать();
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОПовторномОтзывеТокена",
			ЭтотОбъект,
			Новый Структура(
				"ВидТокена, НастройкаОплаты",
				ДополнительныеПараметры.ВидТокена,
				ДополнительныеПараметры.НастройкаОплаты));
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'При отзыве токена возникла ошибка. Повторить попытку?';
				|en = 'An error occurred while revoking the token. Retry?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОПовторномОтзывеТокена(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтозватьТокен(
			Параметры.ВидТокена,
			Параметры.НастройкаПодключения,
			Параметры.СпособОплаты);
		Возврат;
	КонецЕсли;
	
	Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОтзывТокенов

&НаСервере
Функция ОтозватьТокеныЗапускЗадания()
	
	НеиспользуемыеНастройки = Справочники.НастройкиПодключенияКОнлайнЗаказам.НеиспользуемыеНастройки(
		Объект.Ссылка);
	
	Если Не ЗначениеЗаполнено(НеиспользуемыеНастройки[ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаПриемОплат()])
		И Не ЗначениеЗаполнено(НеиспользуемыеНастройки[ОнлайнЗаказыКлиентСервер.ИдентификаторВидаТокенаОблачнаяКасса()]) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отзыв неиспользуемых токенов онлайн-заказов.';
															|en = 'Revoke unused online order tokens.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОнлайнЗаказыСлужебный.ОтозватьНеиспользуемыеТокеныНастройкиПодключения",
		НеиспользуемыеНастройки);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОтозватьТокеныЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ПослеЗавершенияОперацийСТокенами(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Оповещение = Новый ОписаниеОповещения(
				"ПослеОтветаНаВопросОПовторномОтзывеТокенов",
				ЭтотОбъект,
				ДополнительныеПараметры);
			ПоказатьВопрос(
				Оповещение,
				НСтр("ru = 'При отзыве токенов возникла ошибка. Повторить попытку?';
					|en = 'An error occurred while revoking the tokens. Retry?'"),
				РежимДиалогаВопрос.ДаНетОтмена);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОПовторномОтзывеТокенов",
			ЭтотОбъект,
			ДополнительныеПараметры);
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'При отзыве токенов возникла ошибка. Повторить попытку?';
				|en = 'An error occurred while revoking the tokens. Retry?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
	ПослеЗавершенияОперацийСТокенами(ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтветаНаВопросОПовторномОтзывеТокенов(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОтозватьТокеныЗапускЗадания();
		Возврат;
	КонецЕсли;
	
	ПослеЗавершенияОперацийСТокенами(Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияОперацийСТокенами(ДополнительныеПараметры)
	
	Если РезультатПроверкиШаблонов <> Неопределено
		И Не РезультатПроверкиШаблонов.ВсеШаблоны Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтветаНаВопросОСозданииШаблонов",
			ЭтотОбъект,
			ДополнительныеПараметры);
		
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Создать шаблоны сообщений для автоматического заполнения на основании данных документов?';
				|en = 'Do you want to create message templates for automatic population from document data?'"),
			РежимДиалогаВопрос.ДаНет);
			
	Иначе
		
		Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
			ИнициализацияОправкиВСервис(
				ДополнительныеПараметры.ЗакрытьПослеЗаписи,
				Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтправкаВСервис

&НаКлиенте
Процедура ИнициализацияОправкиВСервис(ЗакрытьПослеЗаписи, ПоказыватьПредупреждение)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЗакрытьПослеЗаписи", ЗакрытьПослеЗаписи);
	ДополнительныеПараметры.Вставить("ПоказыватьПредупреждение", ПоказыватьПредупреждение);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = НСтр("ru = 'Обновление настроек онлайн-заказов в сервисе.';
											|en = 'Updating online orders settings in the service.'");
	
	РезультатВыполнения = ОтправитьВСервисЗапускЗадания();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОтправитьВСервисЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОтправитьВСервисЗавершение(
			РезультатВыполнения,
			ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОтправитьВСервисЗапускЗадания()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Обновление настроек онлайн-заказов в сервисе.';
				|en = 'Updating online orders settings in the service.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"ОнлайнЗаказыСлужебный.ОбновитьНастройку",
		Объект.Ссылка);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОтправитьВСервисЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			
			ПоказатьПредупреждение(
				Неопределено,
				РезультатОперации.СообщениеОбОшибке);
			
			Если Объект.ОшибкаСинхронизации <> Истина Тогда
				УстановитьОшибкуСинхронизации(Истина);
			КонецЕсли;
			
		Иначе
			
			Если Объект.ОшибкаСинхронизации <> Ложь Тогда
				УстановитьОшибкуСинхронизации(Ложь);
			КонецЕсли;
			
			Если ДополнительныеПараметры.ПоказыватьПредупреждение Тогда
				
				ПоказатьПредупреждение(
					Неопределено,
					НСтр("ru = 'Настройки успешно обновлены.';
						|en = 'Settings are successfully updated.'"));
					
			ИначеЕсли ДополнительныеПараметры.ЗакрытьПослеЗаписи Тогда
				Закрыть(Истина);
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка обновления настройки онлайн-заказов в сервисе.';
				|en = 'An error occurred when updating online order setting in the service.'"));
		
		ПоказатьПредупреждение(
			Неопределено,
			ДанныеОшибки.Текст);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаВнешнегоВидаСтраницы

&НаКлиенте
Процедура НастроитьВнешнийВидСтраницы()
	
	Результат = URLНастройкиПодключения(Объект.Ссылка);
	
	Если Результат.КодОшибки = ОнлайнЗаказыКлиент.КодОшибкиДанныеАутентификацииИППНеЗаполнены() Тогда
		Если ИнтернетПоддержкаПользователейКлиент.ДоступноПодключениеИнтернетПоддержки() Тогда
			ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
				Новый ОписаниеОповещения("ПриЗавершенииПодключенияИнтернетПоддержки", ЭтотОбъект),
				ЭтотОбъект);
		Иначе
			ПоказатьПредупреждение(
				,
				НСтр("ru = 'Необходимо подключить Интернет-поддержку пользователей.
				|Обратитесь к администратору системы.';
				|en = 'Please enable online support.
				|Contact system administrator.'"));
		КонецЕсли;
	ИначеЕсли ПустаяСтрока(Результат.КодОшибки) Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Результат.URLНастройкиПодключения);
	Иначе
		ПоказатьПредупреждение(
			,
			Результат.СообщениеОбОшибке);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция URLНастройкиПодключения(Знач НастройкаПодключения)
	
	Возврат ОнлайнЗаказыСлужебный.URLНастройкиПодключения(НастройкаПодключения);
	
КонецФункции

#КонецОбласти

#Область ОткрытиеНастройки

&НаКлиенте
Процедура ОткрытьНастройкуПодключения(НастройкаПодключения, ДекорацияФормы)
	
	ИмяОткрываемойФормы = "";
	
	Если ДекорацияФормы = "ДекорацияПодключениеКСБПc2b" Тогда
		ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаОбъекта";
		ДекорацияДействия = "ДекорацияПодключениеКСБПc2bДействие";
	ИначеЕсли ДекорацияФормы = "ДекорацияПодключениеКСБПb2b" Тогда
		ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКСистемеБыстрыхПлатежей.ФормаОбъекта";
		ДекорацияДействия = "ДекорацияПодключениеКСБПb2bДействие";
	ИначеЕсли ДекорацияФормы = "ДекорацияПодключениеИнтернетЭквайринг" Тогда
		ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКИнтернетЭквайрингу.ФормаОбъекта";
		ДекорацияДействия = "ДекорацияПодключениеИнтернетЭквайрингДействие";
	ИначеЕсли ДекорацияФормы = "ДекорацияПодключениеКОблачнойКассе" Тогда
		ИмяОткрываемойФормы = "Справочник.НастройкиПодключенияКОблачнымКассам.ФормаОбъекта";
		ДекорацияДействия = "ДекорацияПодключениеКОблачнойКассеДействие";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИмяОткрываемойФормы) Тогда
		ПоказатьЗначение(, НастройкаПодключения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура(
		"НастройкаПодключения, ДекорацияПодключения, ДекорацияДействия",
		НастройкаПодключения,
		ДекорацияФормы,
		ДекорацияДействия);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения(
		"ОбработкаЗакрытияФормыЭлемента",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ПараметрыОткрытия = Новый Структура
		("Ключ", НастройкаПодключения);
	
	ОткрытьФорму(
		ИмяОткрываемойФормы,
		ПараметрыОткрытия,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияФормыЭлемента(Результат, ДополнительныеПараметры) Экспорт
	
	УстановитьПредставлениеПодключенияСДействием(
		ДополнительныеПараметры.НастройкаПодключения,
		ДополнительныеПараметры.ДекорацияПодключения,
		ДополнительныеПараметры.ДекорацияДействия);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Элементы.Организация.Доступность = Не Параметры.ЗапретИзмененияОрганизации;
	
	Элементы.НастроитьСтраницуЗаказа.Доступность = НастройкаОнлайнЗаказовДоступна;
	Элементы.ФормаОбновитьНастройки.Доступность = НастройкаОнлайнЗаказовДоступна;
	Элементы.ФормаЗаписатьИЗакрыть.Доступность = НастройкаОнлайнЗаказовДоступна;
	
	УстановитьВидимостьПредставлениеСБП();
	УстановитьВидимостьПредставлениеИнтернетЭквайринга();
	УстановитьВидимостьПредставлениеОблачнойФискализации();
	УстановитьПредставлениеДекорацииПоБанковскимРеквизитамФизЛица();
	УстановитьПредставлениеДекорацииПоБанковскимРеквизитамЮрЛица();
	УстановитьПредставлениеДекорацииПредупреждение();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПредставлениеСБП()
	
	ЧтениеНастроекСБПДоступно = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		ЧтениеНастроекСБПДоступно = МодульСистемаБыстрыхПлатежейСлужебный.ЧтениеНастроекДоступно();
	КонецЕсли;
	
	Если Не ЧтениеНастроекСБПДоступно
		Или Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b") Тогда
		Элементы.ГруппаПодключениеКСБПc2b.Видимость = Ложь;
	Иначе
		УстановитьПредставлениеПодключенияСДействием(
			НастройкаПодключенияКСБПc2b,
			"ДекорацияПодключениеКСБПc2b",
			"ДекорацияПодключениеКСБПc2bДействие");
	КонецЕсли;
	
	Если Не ЧтениеНастроекСБПДоступно
		Или Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПb2b") Тогда
		Элементы.ГруппаПодключениеКСБПb2b.Видимость = Ложь;
	Иначе
		УстановитьПредставлениеПодключенияСДействием(
			НастройкаПодключенияКСБПb2b,
			"ДекорацияПодключениеКСБПb2b",
			"ДекорацияПодключениеКСБПb2bДействие");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПредставлениеИнтернетЭквайринга()
	
	ЧтениеНастроекИнтернетЭквайрингаДоступно = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		ЧтениеНастроекИнтернетЭквайрингаДоступно = МодульИнтернетЭквайрингСлужебный.ЧтениеНастроекДоступно();
	КонецЕсли;
	
	Элементы.ГруппаПодключениеЭквайринг.Видимость = ЧтениеНастроекИнтернетЭквайрингаДоступно;
	
	Если ЧтениеНастроекИнтернетЭквайрингаДоступно Тогда
		УстановитьПредставлениеПодключенияСДействием(
			НастройкаПодключенияКИнтернетЭквайрингу,
			"ДекорацияПодключениеИнтернетЭквайринг",
			"ДекорацияПодключениеИнтернетЭквайрингДействие");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПредставлениеОблачнойФискализации()
	
	ЧтениеНастроекОблачныхКассДоступно = Ложь;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ОблачныеКассы") Тогда
		МодульОблачныеКассы = ОбщегоНазначения.ОбщийМодуль("ОблачныеКассы");
		ЧтениеНастроекОблачныхКассДоступно = МодульОблачныеКассы.ЧтениеНастроекПодключенияДоступно();
	КонецЕсли;
	
	Элементы.ГруппаПодключениеКОблачнойКассе.Видимость = ЧтениеНастроекОблачныхКассДоступно;
	
	Если ЧтениеНастроекОблачныхКассДоступно Тогда
		УстановитьПредставлениеПодключенияСДействием(
			Объект.НастройкаПодключенияКОблачнойКассе,
			"ДекорацияПодключениеКОблачнойКассе",
			"ДекорацияПодключениеКОблачнойКассеДействие");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеДекорацииПоБанковскимРеквизитамФизЛица()
	
	Если Объект.ПоРеквизитамОтФизЛиц Тогда
		Если НастройкаОнлайнЗаказовДоступна Тогда
			ПредставлениеЗаголовка = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Отключить';
					|en = 'Disconnect'"),
				,
				,
				,
				"action:disable");
		Иначе
			ПредставлениеЗаголовка = НСтр("ru = 'Включен';
											|en = 'Enabled'");
		КонецЕсли;
	Иначе
		Если НастройкаОнлайнЗаказовДоступна Тогда
			ПредставлениеЗаголовка = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Включить';
					|en = 'Enable'"),
				,
				,
				,
				"action:enable");
		Иначе
			ПредставлениеЗаголовка = НСтр("ru = 'Отключен';
											|en = 'Disconnected'");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДекорацияПоБанковскимРеквизитамФизЛица.Заголовок = ПредставлениеЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеДекорацииПоБанковскимРеквизитамЮрЛица()
	
	Если Объект.ПоРеквизитамОтЮрЛиц Тогда
		Если НастройкаОнлайнЗаказовДоступна Тогда
			ПредставлениеЗаголовка = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Отключить';
					|en = 'Disconnect'"),
				,
				,
				,
				"action:disable");
		Иначе
			ПредставлениеЗаголовка = НСтр("ru = 'Включен';
											|en = 'Enabled'");
		КонецЕсли;
	Иначе
		Если НастройкаОнлайнЗаказовДоступна Тогда
			ПредставлениеЗаголовка = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Включить';
					|en = 'Enable'"),
				,
				,
				,
				"action:enable");
		Иначе
			ПредставлениеЗаголовка = НСтр("ru = 'Отключен';
											|en = 'Disconnected'");
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ДекорацияПоБанковскимРеквизитамЮрЛица.Заголовок = ПредставлениеЗаголовка;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеДекорацииПредупреждение()
	
	ЧастиСтрок = Новый Массив;
	ЧастиСтрок.Добавить(НСтр("ru = 'Не удалось обновить данные в сервисе.';
							|en = 'Cannot update data in the service.'"));
	
	Если НастройкаОнлайнЗаказовДоступна Тогда
		ЧастиСтрок.Добавить(" ");
		ЧастиСтрок.Добавить(
			Новый ФорматированнаяСтрока(
				НСтр("ru = 'Повторить отправку';
					|en = 'Retry'")
				,
				,
				,
				,
				"operation:pageSettings"));
	КонецЕсли;
	
	Элементы.ДекорацияПредупреждение.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтрок);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставлениеПодключенияСДействием(
	Знач НастройкаПодключения,
	Знач ДекорацияПодключения,
	Знач ДекорацияДействия)
	
	Если Не ЗначениеЗаполнено(НастройкаПодключения) Тогда
		Элементы[ДекорацияПодключения].Видимость = Ложь;
		Если НастройкаОнлайнЗаказовДоступна Тогда
			Элементы[ДекорацияДействия].Заголовок = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Выбрать';
					|en = 'Select'")
				,
				,
				,
				,
				"chose:setting");
		Иначе
			Элементы[ДекорацияДействия].Заголовок = "";
		КонецЕсли;
	Иначе
		Элементы[ДекорацияПодключения].Видимость = Истина;
		Элементы[ДекорацияПодключения].Заголовок = Новый ФорматированнаяСтрока(
			Строка(НастройкаПодключения),
			,
			,
			,
			"open:setting");
		Если НастройкаОнлайнЗаказовДоступна Тогда
			Элементы[ДекорацияДействия].Заголовок = Новый ФорматированнаяСтрока(
				НСтр("ru = 'Отключить';
					|en = 'Disconnect'")
				,
				,
				,
				,
				"disable:setting");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗавершенииПодключенияИнтернетПоддержки(ДанныеАутентификации, ДопПараметры) Экспорт
	
	Если ДанныеАутентификации <> Неопределено Тогда
		НастроитьВнешнийВидСтраницы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОшибкуСинхронизации(Знач ЗначениеОшибки)
	
	Объект.ОшибкаСинхронизации = ЗначениеОшибки;
	ИзменениеАктуальности = Истина;
	Записать();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормыПоДанным()
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Элементы.ГруппаПредупреждение.Видимость = Объект.ОшибкаСинхронизации;
	Иначе
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
