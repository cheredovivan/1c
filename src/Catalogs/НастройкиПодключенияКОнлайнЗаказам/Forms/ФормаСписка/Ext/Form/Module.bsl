///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////


#Область ОбработчикиСобытийФормы
 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.СозданиеНастройки.Видимость = ОнлайнЗаказы.НастройкаОнлайнЗаказовДоступна();
	ПользовательскоеСоглашениеПринято = Справочники.НастройкиПодключенияКОнлайнЗаказам.ЕстьНастройкиПодключения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ОбработкаПродолжения = Новый ОписаниеОповещения(
		"СоздатьНастройкуПодключенияПродолжение",
		ЭтотОбъект);
	
	ИнтернетПоддержкаПользователейКлиент.НачатьПроверкуИПодключениеИнтернетПоддержкиПользователей(
		ОбработкаПродолжения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроблемаСЗаказом(Команда)
	
	ОнлайнЗаказыКлиент.ОткрытьСтраницуПорталаПроблемыЗаказов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СоздатьНастройкуПодключенияПродолжение(ПодключенаИПП, Параметры) Экспорт
	
	Если Не ПодключенаИПП Тогда
		Возврат;
	КонецЕсли;
	
	Если ПользовательскоеСоглашениеПринято Тогда
		ОткрытьФормуНовогоЭлемента();
	Иначе
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеЗакрытияФормыПользовательскогоСоглашения",
			ЭтотОбъект);
		ОткрытьФорму(
			"Справочник.НастройкиПодключенияКОнлайнЗаказам.Форма.ПользовательскоеСоглашение",
			,
			ЭтотОбъект,
			,
			,
			,
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНовогоЭлемента()
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	ОнлайнЗаказыКлиент.СлужебнаяПодключитьОнлайнЗаказы(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыПользовательскогоСоглашения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ПользовательскоеСоглашениеПринято = Результат.ПользовательскоеСоглашениеПринято;
	КонецЕсли;
	
	Если ПользовательскоеСоглашениеПринято Тогда
		ОткрытьФормуНовогоЭлемента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Список.Обновить();
	
КонецПроцедуры

#КонецОбласти