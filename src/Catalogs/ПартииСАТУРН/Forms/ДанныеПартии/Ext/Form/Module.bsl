#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("ДанныеПартии") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеПартии);
	КонецЕсли;
	
	Если Параметры.Свойство("НередактируемыеРеквизиты") Тогда
		Для Каждого ИмяРеквизита Из Параметры.НередактируемыеРеквизиты Цикл
			Элементы[ИмяРеквизита].ТолькоПросмотр = Истина;
		КонецЦикла;
	КонецЕсли;
	
	Если Параметры.Свойство("ОбязательныеРеквизиты") Тогда
		
		ОбязательныеРеквизиты.Очистить();
		
		Для Каждого ИмяРеквизита Из Параметры.ОбязательныеРеквизиты Цикл
			
			Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, ИмяРеквизита) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрокаОбязательныхРеквизитов = ОбязательныеРеквизиты.Добавить();
			НоваяСтрокаОбязательныхРеквизитов.Значение = ИмяРеквизита;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Для Каждого ОбязательныйРеквизит Из ОбязательныеРеквизиты Цикл
		ПроверяемыеРеквизиты.Добавить(ОбязательныйРеквизит.Значение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПартии = Новый Структура;
	ДанныеПартии.Вставить("НомерПартии",          НомерПартии);
	ДанныеПартии.Вставить("ТорговоеНаименование", ТорговоеНаименование);
	ДанныеПартии.Вставить("АртикулПроизводителя", АртикулПроизводителя);
	ДанныеПартии.Вставить("ДатаПроизводства",     ДатаПроизводства);
	ДанныеПартии.Вставить("СрокГодности",         СрокГодности);
	
	Закрыть(ДанныеПартии);
	
КонецПроцедуры

#КонецОбласти
