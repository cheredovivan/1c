#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет таблицу сопоставлений см. ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент
// 
// Параметры:
//  ТаблицаСопоставлений - см. ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент
// 
Процедура ЗаполнитьТаблицуСопоставленийКлассификаторамиСАТУРН(ТаблицаСопоставлений) Экспорт
	
	ИнтеграцияСАТУРНВызовСервера.РеквизитыОрганизацийКонтрагентов(ТаблицаСопоставлений);
	
	Запрос = Новый Запрос;
	Запрос.Текст  = "ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки            КАК НомерСтроки,
	|	ИсходнаяТаблица.ИНН                    КАК ИНН,
	|	ИсходнаяТаблица.ОрганизацияКонтрагент  КАК ОрганизацияКонтрагент,
	|	ИсходнаяТаблица.ТорговыйОбъект         КАК ТорговыйОбъект,
	|	ИсходнаяТаблица.ПроизводственныйОбъект КАК ПроизводственныйОбъект
	|ПОМЕСТИТЬ ИсходнаяТаблица
	|ИЗ
	|	&ИсходнаяТаблица КАК ИсходнаяТаблица
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки            КАК НомерСтроки,
	|	КлассификаторОрганизацийСАТУРН.Ссылка  КАК ОрганизацияСАТУРН,
	|	ИсходнаяТаблица.ТорговыйОбъект         КАК ТорговыйОбъект,
	|	ИсходнаяТаблица.ПроизводственныйОбъект КАК ПроизводственныйОбъект,
	|	1                                      КАК Приоритет
	|ПОМЕСТИТЬ НайденныеОрганизации
	|ИЗ
	|	ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
	|		ПО ИсходнаяТаблица.ОрганизацияКонтрагент = КлассификаторОрганизацийСАТУРН.Контрагент
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ИсходнаяТаблица.НомерСтроки            КАК НомерСтроки,
	|	КлассификаторОрганизацийСАТУРН.Ссылка  КАК ОрганизацияСАТУРН,
	|	ИсходнаяТаблица.ТорговыйОбъект         КАК ТорговыйОбъект,
	|	ИсходнаяТаблица.ПроизводственныйОбъект КАК ПроизводственныйОбъект,
	|	2                                      КАК Приоритет
	|ИЗ
	|	ИсходнаяТаблица КАК ИсходнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
	|		ПО ИсходнаяТаблица.ИНН = КлассификаторОрганизацийСАТУРН.ИНН
	|		И ИсходнаяТаблица.ИНН <> """"
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеОрганизации.НомерСтроки                  КАК НомерСтроки,
	|	НайденныеОрганизации.ОрганизацияСАТУРН            КАК ОрганизацияСАТУРН,
	|	МестаХраненияПроизводственныйОбъект.МестоХранения КАК МестоХраненияПроизводственныйОбъект,
	|	МестаХраненияТорговыйОбъект.МестоХранения         КАК МестоХраненияТорговыйОбъект,
	|	НайденныеОрганизации.Приоритет                    КАК Приоритет,
	|	1                                                 КАК ПриоритетОрганизацияКонтрагент
	|ИЗ
	|	НайденныеОрганизации КАК НайденныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК МестаХраненияТорговыйОбъект
	|		ПО НайденныеОрганизации.ОрганизацияСАТУРН = МестаХраненияТорговыйОбъект.Ссылка
	|		И НайденныеОрганизации.ТорговыйОбъект = МестаХраненияТорговыйОбъект.ТорговыйОбъект
	|		И НайденныеОрганизации.ТорговыйОбъект <> &ПустойТорговыйОбъект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК МестаХраненияПроизводственныйОбъект
	|		ПО НайденныеОрганизации.ОрганизацияСАТУРН = МестаХраненияПроизводственныйОбъект.Ссылка
	|		И НайденныеОрганизации.ПроизводственныйОбъект = МестаХраненияПроизводственныйОбъект.ПроизводственныйОбъект
	|		И НайденныеОрганизации.ПроизводственныйОбъект <> &ПустойПроизводственныйОбъект
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	НайденныеОрганизации.НомерСтроки,
	|	НайденныеОрганизации.ОрганизацияСАТУРН,
	|	МестаХраненияПроизводственныйОбъект.МестоХранения,
	|	МестаХраненияТорговыйОбъект.МестоХранения,
	|	НайденныеОрганизации.Приоритет,
	|	2
	|ИЗ
	|	НайденныеОрганизации КАК НайденныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК МестаХраненияТорговыйОбъект
	|		ПО НайденныеОрганизации.ТорговыйОбъект = МестаХраненияТорговыйОбъект.ТорговыйОбъект
	|		И НайденныеОрганизации.ТорговыйОбъект <> &ПустойТорговыйОбъект
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлассификаторОрганизацийСАТУРН.МестаХранения КАК МестаХраненияПроизводственныйОбъект
	|		ПО НайденныеОрганизации.ПроизводственныйОбъект = МестаХраненияПроизводственныйОбъект.ПроизводственныйОбъект
	|		И НайденныеОрганизации.ПроизводственныйОбъект <> &ПустойПроизводственныйОбъект
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ПриоритетОрганизацияКонтрагент";
	
	Запрос.УстановитьПараметр("ИсходнаяТаблица",              ТаблицаСопоставлений);
	Запрос.УстановитьПараметр("ПустойТорговыйОбъект",         ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ТорговыйОбъектСАТУРН"));
	Запрос.УстановитьПараметр("ПустойПроизводственныйОбъект", ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("ПроизводственныйОбъектИС"));
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаИзРезультатаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("НомерСтроки", ВыборкаИзРезультатаЗапроса.НомерСтроки);
		СтрокаТаблицы   = ТаблицаСопоставлений.НайтиСтроки(СтруктураПоиска)[0];
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.ОрганизацияСАТУРН) Тогда
			СтрокаТаблицы.ОрганизацияСАТУРН = ВыборкаИзРезультатаЗапроса.ОрганизацияСАТУРН;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.МестоХраненияТорговыйОбъект) Тогда
			СтрокаТаблицы.МестоХраненияТорговыйОбъект = ВыборкаИзРезультатаЗапроса.МестоХраненияТорговыйОбъект;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.МестоХраненияПроизводственныйОбъект) Тогда
			СтрокаТаблицы.МестоХраненияПроизводственныйОбъект = ВыборкаИзРезультатаЗапроса.МестоХраненияПроизводственныйОбъект;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Организации контрагенты по классификаторам САТУРН.
// 
// Параметры:
//  КлассификаторыСАТУРН - СправочникСсылка.КлассификаторОрганизацийСАТУРН, Массив Из СправочникСсылка.КлассификаторОрганизацийСАТУРН -
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//   * Ключ - СправочникСсылка.КлассификаторОрганизацийСАТУРН
//   * Значение - СтрокаТаблицыЗначений: см. НоваяТаблицаРеквизитовКлассификатораОрганизаций
Функция ОрганизацииКонтрагентыПоКлассификаторамСАТУРН(КлассификаторыСАТУРН) Экспорт
	
	ВозвращаемоеЗначение   = Новый Соответствие();
	СсылкиНаКлассификаторы = Новый Массив;
	
	ТаблицаДанных = НоваяТаблицаРеквизитовКлассификатораОрганизаций();
	ТаблицаДанных.Индексы.Добавить("ОрганизацияСАТУРН");
	
	Если ТипЗнч(КлассификаторыСАТУРН) = Тип("Массив") Тогда
		СсылкиНаКлассификаторы = КлассификаторыСАТУРН;
	Иначе
		СсылкиНаКлассификаторы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(КлассификаторыСАТУРН);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	КлассификаторОрганизацийСАТУРН.Ссылка     КАК ОрганизацияСАТУРН,
		|	КлассификаторОрганизацийСАТУРН.ИНН        КАК ИНН,
		|	КлассификаторОрганизацийСАТУРН.Контрагент КАК ОрганизацияКонтрагент
		|ИЗ
		|	Справочник.КлассификаторОрганизацийСАТУРН КАК КлассификаторОрганизацийСАТУРН
		|ГДЕ
		|	КлассификаторОрганизацийСАТУРН.Ссылка В (&Ссылки)";
	
	Запрос.УстановитьПараметр("Ссылки", СсылкиНаКлассификаторы);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса       = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	ТипЗначенияПустойОрганизации = ТипЗнч(ОбщегоНазначенияИС.ПустоеЗначениеОпределяемогоТипа("Организация"));
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		ЗначениеПоКлючу = Новый Структура("Организация, Контрагент");
		ВозвращаемоеЗначение.Вставить(ВыборкаДетальныеЗаписи.ОрганизацияСАТУРН, ЗначениеПоКлючу);
		
		Если ЗначениеЗаполнено(ВыборкаДетальныеЗаписи.ОрганизацияКонтрагент) Тогда
			
			Если ТипЗнч(ВыборкаДетальныеЗаписи.ОрганизацияКонтрагент) = ТипЗначенияПустойОрганизации Тогда
				ЗначениеПоКлючу.Организация = ВыборкаДетальныеЗаписи.ОрганизацияКонтрагент;
			Иначе
				ЗначениеПоКлючу.Контрагент = ВыборкаДетальныеЗаписи.ОрганизацияКонтрагент;
			КонецЕсли;
		
		Иначе
			НоваяСтрока = ТаблицаДанных.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаДанных.Количество() Тогда
		ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииКонтрагентовОрганизацийПоРеквизитам(ТаблицаДанных);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанных Цикл
		ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение[СтрокаТаблицы.ОрганизацияСАТУРН], СтрокаТаблицы);
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Записывает в справочник "Классификатор организаций САТУРН" соответствие прикладных реквизитов
//   * Не изменяет существующие сопоставления (поле Контрагент)
//
// Параметры:
//   Ключ       - СправочникСсылка.КлассификаторОрганизацийСАТУРН - данные классификатора САТУРН
//   Контрагент - ОпределяемыйТип.ОрганизацияКонтрагентГосИС      - прикладной справочник
//
Процедура СопоставитьСПрикладнымиРеквизитами(Ключ, Контрагент) Экспорт
	
	Если Не ЗначениеЗаполнено(Ключ)
		Или Не ЗначениеЗаполнено(Контрагент)
		Или ЗначениеЗаполнено(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ключ, "Контрагент")) Тогда
		Возврат;
	КонецЕсли;
	
	КлючОбъект = Ключ.ПолучитьОбъект();
	КлючОбъект.Контрагент = Контрагент;
	
	УстановитьПривилегированныйРежим(Истина);
	КлючОбъект.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
	
		Если Параметры = Неопределено Тогда
			Параметры = Новый Структура();
		КонецЕсли;
		
		Параметры.Вставить("РежимВыбора", Истина);
		
		ВыбраннаяФорма       = "Справочник.КлассификаторОрганизацийСАТУРН.Форма.ФормаСписка";
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Обмен

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * GUID - Строка
// * Идентификатор - Строка
// * Статус - ПеречислениеСсылка.СтатусыОбъектовСАТУРН, Строка - 
// * ДатаСозданияЦЕРБЕР - Дата - 
// * ДатаИзмененияЦЕРБЕР - Дата - 
// * ДатаСоздания - Дата - 
// * ДатаИзменения - Дата - 
// * Тип - ПеречислениеСсылка.ТипыОрганизацийСАТУРН, Неопределено - 
// * ИНН - Строка
// * КПП - Строка
// * ОГРН - Строка
// * Наименование - Строка
// * НаименованиеПолное - Строка
// * ФИО - Строка
// * ФактическийАдрес - Строка
// * ЮридическийАдрес - Строка
// * Телефон - Строка
// * Email - Строка
// * ОтветственноеЛицо- Строка 
// * ЭтоВладелецСклада - Булево - 
// * Роли - Массив из Строка- 
// * Комментарий - Строка
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеОрганизации               = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеОрганизации.Вставить("Идентификатор",       Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеОрганизации.Вставить("Статус",              ИнтерфейсСАТУРН.Статус(ЭлементДанных.lcState));
	ДанныеОрганизации.Вставить("ДатаСозданияЦЕРБЕР",  ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементДанных.dateRegistration));
	ДанныеОрганизации.Вставить("ДатаИзмененияЦЕРБЕР", ИнтерфейсСАТУРН.ДатаИзСтрокиISO(ЭлементДанных.dateModified));
	
	ДанныеОрганизации.Вставить("Тип",  ИнтерфейсСАТУРН.ТипХозяйствующегоСубъекта(ЭлементДанных.vetis_type));
	ДанныеОрганизации.Вставить("ИНН",  ЭлементДанных.inn);
	ДанныеОрганизации.Вставить("КПП",  ЭлементДанных.kpp);
	ДанныеОрганизации.Вставить("ОГРН", ЭлементДанных.ogrn);
	
	ДанныеОрганизации.Вставить("Наименование",       ЭлементДанных.name);
	ДанныеОрганизации.Вставить("НаименованиеПолное", ЭлементДанных.fullName);
	ДанныеОрганизации.Вставить("ФИО",                ЭлементДанных.personFio);
	ДанныеОрганизации.Вставить("ФактическийАдрес",   ЭлементДанных.factualAddress);
	ДанныеОрганизации.Вставить("ЮридическийАдрес",   ЭлементДанных.legalAddress);
	ДанныеОрганизации.Вставить("Телефон",            ЭлементДанных.phoneNumber);
	ДанныеОрганизации.Вставить("Email",              ЭлементДанных.email);
	ДанныеОрганизации.Вставить("ОтветственноеЛицо",  ЭлементДанных.responsiblePerson);
	ДанныеОрганизации.Вставить("ЭтоВладелецСклада",  ЭлементДанных.isWarehouseOwner = "true");
	
	ИнтеграцияСАТУРНСлужебный.ДополнитьДанныеОбъектаОбщимиДанными(ДанныеОрганизации, ЭлементДанных);
	
	ДанныеОрганизации.Вставить("Роли", Новый Массив);
	Если ЭлементДанных.isAgriculturalManufacturer = "true" Или ЭлементДанных.isAgriculturalManufacturer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.ПроизводительСельхозпродукции);
	КонецЕсли;
	Если ЭлементДанных.isAnother = "true" Или ЭлементДанных.isAnother = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Прочий);
	КонецЕсли;
	Если ЭлементДанных.isDistributor = "true" Или ЭлементДанных.isDistributor = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Дистрибьютор);
	КонецЕсли;
	Если ЭлементДанных.isForeignConsumer = "true" Или ЭлементДанных.isForeignConsumer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.ИностраннаяОрганизацияПотребитель);
	КонецЕсли;
	Если ЭлементДанных.isImporter = "true" Или ЭлементДанных.isImporter = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Импортер);
	КонецЕсли;
	Если ЭлементДанных.isIndustrialConsumer = "true" Или ЭлементДанных.isIndustrialConsumer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.ПромышленноеХимическоеПроизводство);
	КонецЕсли;
	Если ЭлементДанных.isManufacturer = "true" Или ЭлементДанных.isManufacturer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.ПроизводительПАТ);
	КонецЕсли;
	Если ЭлементДанных.isMeasuring = "true" Или ЭлементДанных.isMeasuring = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Лаборатория);
	КонецЕсли;
	Если ЭлементДанных.isRegulator = "true" Или ЭлементДанных.isRegulator = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Регулятор);
	КонецЕсли;
	Если ЭлементДанных.isRetailTrader = "true" Или ЭлементДанных.isRetailTrader = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Ритейлер);
	КонецЕсли;
	Если ЭлементДанных.isTransporter = "true" Или ЭлементДанных.isTransporter = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.Перевозчик);
	КонецЕсли;
	Если ЭлементДанных.isUnregisteredConsumer = "true" Или ЭлементДанных.isUnregisteredConsumer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.НезарегистрированныйУчастникОборота);
	КонецЕсли;
	Если ЭлементДанных.isUtilizer = "true" Или ЭлементДанных.isUtilizer = Истина Тогда
		ДанныеОрганизации.Роли.Добавить(Перечисления.РолиКонтрагентовСАТУРН.ОрганизацияУтилизатор);
	КонецЕсли;
	
	ДанныеОрганизации.Вставить("Комментарий", ЭлементДанных.description);
	
	Возврат ДанныеОрганизации;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					Продолжить;
				КонецЕсли;
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Если ТипЗнч(ПараметрыИсходящегоСообщенияОснования) = Тип("Структура")
			И ПараметрыИсходящегоСообщенияОснования.Свойство("ИдентификаторыМестХранения")
			И ПараметрыИсходящегоСообщенияОснования.ИдентификаторыМестХранения.Количество() Тогда
			
			СообщенияJSON = Справочники.МестаХраненияСАТУРН.ЗагрузкаКлассификатораМестаХраненияJSON(ПараметрыИсходящегоСообщенияОснования.ИдентификаторыМестХранения);
			ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
			
		КонецЕсли;
		
		Если ТипЗнч(ПараметрыИсходящегоСообщенияОснования) = Тип("Структура")
			И ПараметрыИсходящегоСообщенияОснования.Свойство("ИдентификаторыМестПрименения")
			И ПараметрыИсходящегоСообщенияОснования.ИдентификаторыМестПрименения.Количество() Тогда
			
			СообщенияJSON = Справочники.МестаПримененияСАТУРН.ЗагрузкаКлассификатораМестаПримененияJSON(ПараметрыИсходящегоСообщенияОснования.ИдентификаторыМестПрименения);
			ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
			
		КонецЕсли;
	
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСозданиеКлассификатора Тогда
		
		СообщенияJSON = Новый Массив;
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Идентификатор = ПолученныеДанные.id;
		
		СправочникОбъект               = СоздатьЭлемент();
		СправочникОбъект.Идентификатор = Идентификатор;
		
		ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта,, "Идентификатор");
		
		Попытка
			СправочникОбъект.Записать();
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
		ИзмененныеОбъекты.Добавить(СправочникОбъект.Ссылка);
		
		СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
		
		ИнтеграцияСАТУРНСлужебный.ЗаполнитьСообщениеПоИсточнику(СообщениеJSON, РеквизитыИсходящегоСообщения);
		
		СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
		СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СообщениеJSON.Документ,, Истина);
		СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
		
		СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
		ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(СообщениеJSON.АргументыОперации, "id", Идентификатор);
		
		СообщенияJSON.Добавить(СообщениеJSON);
		
		ИнтеграцияСАТУРНСлужебный.ПодготовитьКПередачеИсходныеСообщения(СообщенияJSON, ПараметрыОбмена);
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияИзменениеКлассификатора Тогда
		
		РеквизитыИсходящегоСообщения          = ЭлементОчереди.РеквизитыИсходящегоСообщения;
		ПараметрыИсходящегоСообщенияОснования = РеквизитыИсходящегоСообщения.ПараметрыЗапроса;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.КлассификаторОрганизацийСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникОбъект               = ЭлементОчереди.Документ.ПолучитьОбъект();
			РеквизитыИсключения            = "";
			
			Если ЗначениеЗаполнено(СправочникОбъект.Статус) Тогда
				РеквизитыИсключения = "Статус";
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(СправочникОбъект, ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта,, РеквизитыИсключения);
			
			СправочникОбъект.Роли.Очистить();
			
			Для Каждого РольКонтрагента Из ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта.МассивРолей Цикл
				
				НоваяРоль = СправочникОбъект.Роли.Добавить();
				НоваяРоль.Роль = РольКонтрагента;
				
			КонецЦикла;
			
			СправочникОбъект.АдресЭлектроннойПочты = ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта.Email;
			СправочникОбъект.КонтактныйТелефон     = ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта.Телефон;
			СправочникОбъект.ЮридическийАдрес      = ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта.ЮридическийАдрес;
			СправочникОбъект.ФактическийАдрес      = ПараметрыИсходящегоСообщенияОснования.ДанныеОбъекта.ФактическийАдрес;
			
			СправочникОбъект.Записать();
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
	
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВОтменено Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.КлассификаторОрганизацийСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникОбъект        = ЭлементОчереди.Документ.ПолучитьОбъект();
			СправочникОбъект.Статус = Перечисления.СтатусыОбъектовСАТУРН.Отменен;
			СправочникОбъект.Записать();
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
	ИначеЕсли ЭлементОчереди.Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВАктуально Тогда
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Справочники.КлассификаторОрганизацийСАТУРН.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Ссылка", ЭлементОчереди.Документ);
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникОбъект        = ЭлементОчереди.Документ.ПолучитьОбъект();
			СправочникОбъект.Статус = Перечисления.СтатусыОбъектовСАТУРН.Актуально;
			СправочникОбъект.Записать();
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.ОрганизацияЗапросКлассификатора;
КонецФункции

// Таблица реквизитов организаций САТУРН.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица реквизитов ключей реквизитов организаций:
// * ОрганизацияСАТУРН - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию САТУРН
// * ИНН            - Строка                                           - ИНН
// * Организация    - ОпределяемыйТип.Организация                      - Организация
// * Контрагент     - ОпределяемыйТип.КонтрагентГосИС                  - Контрагент
Функция НоваяТаблицаРеквизитовКлассификатораОрганизаций() Экспорт
	
	ТаблицаДанных = Новый ТаблицаЗначений();
	ТаблицаДанных.Колонки.Добавить("ОрганизацияСАТУРН", Новый ОписаниеТипов("СправочникСсылка.КлассификаторОрганизацийСАТУРН"));
	ТаблицаДанных.Колонки.Добавить("ИНН",            ОбщегоНазначения.ОписаниеТипаСтрока(50));
	ТаблицаДанных.Колонки.Добавить("Организация",    Метаданные.ОпределяемыеТипы.Организация.Тип);
	ТаблицаДанных.Колонки.Добавить("Контрагент",     Метаданные.ОпределяемыеТипы.КонтрагентГосИС.Тип);
	
	ТаблицаДанных.Колонки.Добавить("ОрганизацияКонтрагент", Метаданные.ОпределяемыеТипы.ОрганизацияКонтрагентГосИС.Тип);
	
	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ПоискСсылок

Функция Организация(ЗначениеИдентификатора, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Идентификатор = Формат(ЗначениеИдентификатора, "ЧГ=0;");
	Если Не ЗначениеЗаполнено(Идентификатор)
		Или ЗначениеИдентификатора = -1 Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ИмяТаблицы       = Метаданные.Справочники.КлассификаторОрганизацийСАТУРН.ПолноеИмя();
	СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				ДанныеОбъекта = ИнтеграцияСАТУРНСлужебный.ДанныеОбъекта(
					Идентификатор,
					Метаданные.Справочники.КлассификаторОрганизацийСАТУРН, ПараметрыОбмена);
				Если ДанныеОбъекта = Неопределено Тогда
					СправочникСсылка = СоздатьОрганизацию(Идентификатор, ПараметрыОбмена);
					ИнтеграцияСАТУРНСлужебный.ДобавитьКЗагрузке(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка, ОрганизацияСАТУРН);
				Иначе
					СправочникСсылка = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,, Ложь, ОрганизацияСАТУРН);
				КонецЕсли;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Организации САТУРН с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Организации САТУРН с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании Организации САТУРН с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании Организации САТУРН с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с Организациями САТУРН';
					|en = 'Работа с Организациями САТУРН'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьОбъект(ДанныеОрганизации, ПараметрыОбмена, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Если ДанныеОрганизации = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового = Ложь;
	ИмяТаблицы   = Метаданные.Справочники.КлассификаторОрганизацийСАТУРН.ПолноеИмя();
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(
				ПараметрыОбмена, ИмяТаблицы, ДанныеОрганизации.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникОбъект.УстановитьСсылкуНового(ПолучитьСсылку(ИдентификаторОбъекта));
			ЗаписьНового = Истина;
			
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Наименование  = ДанныеОрганизации.Наименование;
	СправочникОбъект.Идентификатор = ДанныеОрганизации.Идентификатор;
	СправочникОбъект.Статус        = ДанныеОрганизации.Статус;
	СправочникОбъект.ИНН           = ДанныеОрганизации.ИНН;
	СправочникОбъект.КПП           = ДанныеОрганизации.КПП;
	СправочникОбъект.ОГРН          = ДанныеОрганизации.ОГРН;
	СправочникОбъект.Комментарий   = ДанныеОрганизации.Комментарий;
	
	СправочникОбъект.Роли.Очистить();
	Для Каждого Роль Из ДанныеОрганизации.Роли Цикл
		НоваяСтрока = СправочникОбъект.Роли.Добавить();
		НоваяСтрока.Роль = Роль;
	КонецЦикла;
	
	СправочникОбъект.ЮридическийАдрес      = ДанныеОрганизации.ЮридическийАдрес;
	СправочникОбъект.ФактическийАдрес      = ДанныеОрганизации.ФактическийАдрес;
	СправочникОбъект.АдресЭлектроннойПочты = ДанныеОрганизации.Email;
	СправочникОбъект.КонтактныйТелефон     = ДанныеОрганизации.Телефон;
	
	СправочникОбъект.ТребуетсяЗагрузка = Ложь;
	СправочникОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеОрганизации.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция СоздатьОрганизацию(Идентификатор, ПараметрыОбмена)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор     = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка = Истина;
	СправочникОбъект.Наименование      = НСтр("ru = '<Требуется загрузка>';
												|en = '<Требуется загрузка>'");
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыОбработки           = Неопределено;
	НовыйСтатус                  = Неопределено;
	ДанныеОбъекта                = Неопределено;
	Идентификатор                = "";
	ИдентификаторыМестХранения   = Новый Массив;
	ИдентификаторыМестПрименения = Новый Массив;
	
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("ПараметрыОбработкиДокумента")
		И ТипЗнч(ДополнительныеПараметры.ПараметрыОбработкиДокумента) = Тип("Структура")
		И ДополнительныеПараметры.ПараметрыОбработкиДокумента.Свойство("ДополнительныеПараметры") Тогда
		
		ПараметрыОбработки = ДополнительныеПараметры.ПараметрыОбработкиДокумента.ДополнительныеПараметры;
		
		Если ТипЗнч(ПараметрыОбработки) = Тип("Структура") Тогда
			
			Если ПараметрыОбработки.Свойство("НовыйСтатус") Тогда
				НовыйСтатус = ПараметрыОбработки.НовыйСтатус;
			КонецЕсли;
			
			Если ПараметрыОбработки.Свойство("ДанныеОрганизации") Тогда
				ДанныеОбъекта = ПараметрыОбработки.ДанныеОрганизации;
			КонецЕсли;
			
			Если ПараметрыОбработки.Свойство("Идентификатор") Тогда
				Идентификатор = ПараметрыОбработки.Идентификатор;
			КонецЕсли;
			
			Если ПараметрыОбработки.Свойство("ИдентификаторыМестХранения") Тогда
				ИдентификаторыМестХранения = ПараметрыОбработки.ИдентификаторыМестХранения;
			КонецЕсли;
			
			Если ПараметрыОбработки.Свойство("ИдентификаторыМестПрименения") Тогда
				ИдентификаторыМестПрименения = ПараметрыОбработки.ИдентификаторыМестПрименения;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СообщенияJSON = Новый Массив;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ПередайтеДанные Тогда
		
		Если ДанныеОбъекта <> Неопределено Тогда
			
			ДобавитьСообщенияJSON = СозданиеИзменениеКлассификатораОрганизацииJSON(СправочникСсылка, ДанныеОбъекта);
			
			Если ДобавитьСообщенияJSON <> Неопределено Тогда
				
				Для Каждого СообщениеJSON Из ДобавитьСообщенияJSON Цикл
					СообщенияJSON.Добавить(СообщениеJSON);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НовыйСтатус <> Неопределено Тогда
			
			ДобавитьСообщенияJSON = СменаСтатусаКлассификатораОрганизацииJSON(СправочникСсылка, НовыйСтатус);
			
			Если ДобавитьСообщенияJSON <> Неопределено Тогда
				
				Для Каждого СообщениеJSON Из ДобавитьСообщенияJSON Цикл
					СообщенияJSON.Добавить(СообщениеJSON);
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаКлассификатораОрганизацииJSON(Идентификатор, ИдентификаторыМестХранения, ИдентификаторыМестПрименения, ДополнительныеПараметры);
		
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

// Формирует JSON сообщения для создания/изменения организации.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию.
//  ДанныеОбъекта - Структура из См. ИнтеграцияСАТУРНКлиентСервер.СтруктураДанныхОрганизации - описание справочника
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
//
Функция СозданиеИзменениеКлассификатораОрганизацииJSON(СправочникСсылка, ДанныеОбъекта)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = СправочникСсылка;
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СправочникСсылка);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ПараметрыЗапроса    = Новый Структура;
	СообщениеJSON.ПараметрыЗапроса.Вставить("ДанныеОбъекта", ДанныеОбъекта);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		
		СообщениеJSON.Операция            = Перечисления.ВидыОперацийСАТУРН.ОрганизацияИзменениеКлассификатора;
		РеквизитыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникСсылка, "Идентификатор");
		
		СообщениеJSON.ИдентификаторЗаявки = РеквизитыОрганизации.Идентификатор;
		
	Иначе
		СообщениеJSON.Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСозданиеКлассификатора;
	КонецЕсли;
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации = СообщениеJSON.АргументыОперации;
	
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "id",                ДанныеОбъекта.Идентификатор);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "lcState",           ИнтерфейсСАТУРН.Статус(ДанныеОбъекта.Статус));
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "inn",               ДанныеОбъекта.ИНН);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "ogrn",              ДанныеОбъекта.ОГРН);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "responsiblePerson", ДанныеОбъекта.ОтветственноеЛицо);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "phoneNumber",       ДанныеОбъекта.Телефон);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "email",             ДанныеОбъекта.Email);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "factualAddress",    ДанныеОбъекта.ФактическийАдрес);
	ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "legalAddress",      ДанныеОбъекта.ЮридическийАдрес);
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ПроизводительСельхозпродукции) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isAgriculturalManufacturer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Прочий) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isAnother", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Дистрибьютор) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isDistributor", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ИностраннаяОрганизацияПотребитель) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isForeignConsumer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Импортер) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isImporter", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ПромышленноеХимическоеПроизводство) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isIndustrialConsumer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ПроизводительПАТ) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isManufacturer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ПроизводительПАТ) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isManufacturer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Лаборатория) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isMeasuring", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Регулятор) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isRegulator", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Ритейлер) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isRetailTrader", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.Перевозчик) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isTransporter", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.НезарегистрированныйУчастникОборота) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isUnregisteredConsumer", Истина);
	КонецЕсли;
	
	Если ДанныеОбъекта.МассивРолей.Найти(Перечисления.РолиКонтрагентовСАТУРН.ОрганизацияУтилизатор) <> Неопределено Тогда
		ИнтерфейсСАТУРН.ДобавитьАргументtheCard(АргументыОперации, "isUtilizer", Истина);
	КонецЕсли;
	
	Возврат СообщенияJSON;
	
КонецФункции

// Формирует JSON сообщения для смены статуса организации.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторОрганизацийСАТУРН - Ссылка на организацию.
//  НовыйСтатус - ПеречислениеСсылка.СтатусыОбъектовСАТУРН - статус к установке
//
// Возвращаемое значение:
//  Массив из Структура - см. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче
//
Функция СменаСтатусаКлассификатораОрганизацииJSON(СправочникСсылка, НовыйСтатус)
	
	Операция      = Неопределено;
	СообщенияJSON = Новый Массив;
	
	Если НовыйСтатус = Перечисления.СтатусыОбъектовСАТУРН.Актуально Тогда
		Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВАктуально;
	ИначеЕсли НовыйСтатус = Перечисления.СтатусыОбъектовСАТУРН.Отменен Тогда
		Операция = Перечисления.ВидыОперацийСАТУРН.ОрганизацияСменаСтатусаИзЧерновикаВОтменено;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = СправочникСсылка;
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция, СправочникСсылка);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	СообщениеJSON.ИдентификаторЗаявки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СправочникСсылка, "Идентификатор");
	СообщениеJSON.Операция            = Операция;
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

Функция ЗагрузкаКлассификатораОрганизацииJSON(Идентификатор, ИдентификаторыМестХранения, ИдентификаторыМестПрименения, ПараметрыОбмена)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации(Истина, Истина);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	АргументыОперации      = СообщениеJSON.АргументыОперации;
	АргументыОперации.pos  = 0;
	АргументыОперации.size = 100;
	
	ИнтерфейсСАТУРН.ДобавитьУсловиеОтбора(АргументыОперации, "id", Формат(Идентификатор, "ЧГ=;"));
	
	СообщениеJSON.ПараметрыЗапроса = Новый Структура();
	
	СообщениеJSON.ПараметрыЗапроса.Вставить("ИдентификаторыМестХранения",   ИдентификаторыМестХранения);
	СообщениеJSON.ПараметрыЗапроса.Вставить("ИдентификаторыМестПрименения", ИдентификаторыМестПрименения);
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
