
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.РежимВыбора Тогда
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список");
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.КоманднаяПанельФормы;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если ПравоДоступа("ИнтерактивноеДобавление", Метаданные.Справочники.Награды) Тогда
		Группа = Элементы.Найти("ГруппаПодборИзКлассификатора");
		ИмяКоманды = "КомандаПодборИзКлассификатора";
		Кнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), Группа);
		Кнопка.ИмяКоманды = ИмяКоманды;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, Настройки);
КонецПроцедуры

&НаСервере
Процедура СписокПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	
	ЗарплатаКадры.ПроверитьПользовательскиеНастройкиДинамическогоСписка(ЭтотОбъект, , СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПодборИзКлассификатора(Команда)

	СоответствиеПолей = Новый Соответствие;
	СоответствиеПолей.Вставить("ClassifierCode", "КодОКИН");
	СоответствиеПолей.Вставить("Name", "Наименование");
	СоответствиеПолей.Вставить("Code", "ИдентификаторКлассификатора");
	СоответствиеПолей.Вставить("Type", "ТипНаграды");
	СоответствиеПолей.Вставить("Сategory", "КатегорияНаграды");
	
	ЗаголовкиПолей = Новый Соответствие;
	ЗаголовкиПолей.Вставить("ClassifierCode", "Код ОКИН");
	ЗаголовкиПолей.Вставить("Name", "Наименование");
	ЗаголовкиПолей.Вставить("Code", "Идентификатор классификатора");
	ЗаголовкиПолей.Вставить("Type", "Тип награды");
	ЗаголовкиПолей.Вставить("Сategory", "Категория награды");
	
	ЗначенияЗамены = Новый Соответствие;
	ЗначенияЗамены.Вставить("КатегорияНаграды", ЗначенияЗаменыКатегорийНаград());
	
	ПараметрыПодбора = КадровыйУчетКлиент.ОписаниеПараметровПодбораИзКлассификатора();
	ПараметрыПодбора.ИмяСправочника = "Награды";
	ПараметрыПодбора.Заголовок = НСтр("ru = 'Классификатор наград по ОКИН';
										|en = 'RNCIP award classifier'");
	ПараметрыПодбора.ПолныйПутьКМакету = "Справочник.Награды.КлассификаторНаградОКИН";
	ПараметрыПодбора.ЗаголовкиПолей = ЗаголовкиПолей;
	ПараметрыПодбора.ЗагружатьИерархию = Истина;
	ПараметрыПодбора.СоответствиеПолей = СоответствиеПолей;
	ПараметрыПодбора.ЗначенияЗамены = ЗначенияЗамены;
	
	КадровыйУчетКлиент.ОткрытьФормуПодбораИзКлассификатора(ЭтотОбъект, ПараметрыПодбора);
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЗначенияЗаменыКатегорийНаград()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КатегорииНаград.ИмяПредопределенныхДанных КАК ЗначениеПоля,
	|	КатегорииНаград.Ссылка КАК ЗначениеЗамены
	|ИЗ
	|	Справочник.КатегорииНаград КАК КатегорииНаград
	|ГДЕ
	|	НЕ КатегорииНаград.ПометкаУдаления
	|	И КатегорииНаград.Предопределенный";

	ЗначенияСправочникаКатегорий = Новый Соответствие;

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗначенияСправочникаКатегорий.Вставить(Выборка.ЗначениеПоля, Выборка.ЗначениеЗамены);
	КонецЦикла;
	
	Возврат ЗначенияСправочникаКатегорий;
КонецФункции

#КонецОбласти
