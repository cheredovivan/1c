#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения реквизитов варианта наладки
// 
// Параметры:
// 	ВариантНаладки - СправочникСсылка.ВариантыНаладки
// Возвращаемое значение:
// 	См. СтруктураРеквизитовВариантаНаладки
Функция ЗначенияРеквизитовВариантаНаладки(ВариантНаладки) Экспорт
	
	Результат = СтруктураРеквизитовВариантаНаладки();
	
	Если ЗначениеЗаполнено(ВариантНаладки) Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ВариантыНаладки.ВремяРаботы      КАК ВремяРаботы,
		|	ВариантыНаладки.ЕдиницаИзмерения КАК ЕдиницаИзмерения
		|ИЗ
		|	Справочник.ВариантыНаладки КАК ВариантыНаладки
		|ГДЕ
		|	ВариантыНаладки.Ссылка = &ВариантНаладки";
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ВариантНаладки", ВариантНаладки);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Выборка.Следующий() Тогда
			ЗаполнитьЗначенияСвойств(Результат, Выборка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру реквизитов варианта наладки.
//
// Возвращаемое значение:
//  Структура - содержит:
//   * ВремяРаботы - Число
//   * ЕдиницаИзмерения - ПеречислениеСсылка.ЕдиницыИзмеренияВремени
Функция СтруктураРеквизитовВариантаНаладки()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ВремяРаботы", 0);
	Результат.Вставить("ЕдиницаИзмерения", Перечисления.ЕдиницыИзмеренияВремени.ПустаяСсылка());
	
	Возврат Результат;
	
КонецФункции

// СтандартныеПодсистемы.ВерсионированиеОбъектов
// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
// Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры
// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Отбор.Свойство("Владелец")
		И Параметры.Свойство("РабочийЦентр") Тогда
		
		Параметры.Отбор.Вставить("Владелец", РедакторПроизводственногоПроцессаКлиентСервер.ВидРабочегоЦентра(Параметры.РабочийЦентр));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли