///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.НастройкиОблачныхКасс) Тогда
		
		Для Каждого НастройкаОблачнойКассы Из Параметры.НастройкиОблачныхКасс Цикл
			НоваяСтрока = НастройкиОблачныхКасс.Добавить();
			НоваяСтрока.ОблачнаяКасса = НастройкаОблачнойКассы.Ключ;
			НоваяСтрока.НастройкаПодключения = НастройкаОблачнойКассы.Значение;
		КонецЦикла;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.НастройкиОблачныхКасс.ТекущаяСтрока = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не выбрана облачная касса.';
				|en = 'Cloud cash account is not selected.'"),
			,
			,
			"НастройкиОблачныхКасс");
	Иначе
		ОбработатьВыборСтроки(Элементы.НастройкиОблачныхКасс.ТекущаяСтрока, "");
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Создать(Команда)
	
	Закрыть(НовыйРезультатВыбора());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыНастройкиОблачныхКасс

&НаКлиенте
Процедура НастройкиОблачныхКассВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыборСтроки(ВыбраннаяСтрока, Поле.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	// Оформляемые поля
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкиОблачныхКассНастройкаПодключения.Имя);
	
	// Отбор
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("НастройкиОблачныхКасс.НастройкаПодключения");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Подключить';
																|en = 'Connect'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);
	Элемент.Использование = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборСтроки(ИдентификаторСтроки, ИмяПоля)
	
	ВыбраннаяСтрока = НастройкиОблачныхКасс.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ЗначениеЗаполнено(ВыбраннаяСтрока.НастройкаПодключения)
		И ИмяПоля = "НастройкиОблачныхКассНастройкаПодключения" Тогда
		ПоказатьЗначение(Неопределено, ВыбраннаяСтрока.НастройкаПодключения);
	ИначеЕсли ЗначениеЗаполнено(ВыбраннаяСтрока.НастройкаПодключения) Тогда
		ОбработатьПодтверждениеВыбора(ВыбраннаяСтрока);
	ИначеЕсли ЗначениеЗаполнено(ВыбраннаяСтрока.ОблачнаяКасса) Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ВыбраннаяСтрока", ВыбраннаяСтрока);
		
		ОбработкаОтвета = Новый ОписаниеОповещения(
			"ПоказатьВопросСозданияНастройкиПодключенияЗавершение",
			ЭтотОбъект,
			ПараметрыОповещения);
		
		ТекстВопроса = НСтр("ru = 'Настройка подключения для кассы не найдена.
			|Создать сейчас?';
			|en = 'Cash account connection settings are not found.
			|Create now?'");
		
		ПоказатьВопрос(
			ОбработкаОтвета,
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросСозданияНастройкиПодключенияЗавершение(
		Знач Ответ,
		Знач Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ОбработатьПодтверждениеВыбора(Параметры.ВыбраннаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПодтверждениеВыбора(ВыбраннаяСтрока)
	
	РезультатВыбора = НовыйРезультатВыбора();
	РезультатВыбора.ОблачнаяКасса = ВыбраннаяСтрока.ОблачнаяКасса;
	РезультатВыбора.НастройкаПодключения = ВыбраннаяСтрока.НастройкаПодключения;
	
	Закрыть(РезультатВыбора);
	
КонецПроцедуры

&НаКлиенте
Функция НовыйРезультатВыбора()
	
	Результат = Новый Структура;
	Результат.Вставить("ОблачнаяКасса", Неопределено);
	Результат.Вставить(
		"НастройкаПодключения",
		ПредопределенноеЗначение("Справочник.НастройкиПодключенияКОблачнымКассам.ПустаяСсылка"));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти