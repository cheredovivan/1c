///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Определяет наличие настроек подключения к Облачной кассе.
//
// Возвращаемое значение:
//  Булево - если Истина, есть настройки подключения.
//
Функция ЕстьНастройкиПодключения() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК Признак
	|ИЗ
	|	Справочник.НастройкиПодключенияКОблачнымКассам КАК НастройкиПодключенияКОблачнымКассам";
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Выполняет поиск настройки подключения к Облачной кассе.
//
// Параметры:
//  ЗначениеПоиска - ОпределяемыйТип.ОблачнаяКасса - ссылка на элемент справочника для которого настроена интеграция с
//    Облачной кассой.
//                 - СправочникСсылка.НастройкиПодключенияКОблачнымКассам
//
// Возвращаемое значение:
//  Неопределено - настройка не найдена.
//  Структура:
//    * Ссылка - СправочникСсылка.НастройкиПодключенияКОблачнымКассам
//    * Идентификатор - Строка
//
Функция НастройкаПодключения(ЗначениеПоиска) Экспорт
	
	Результат = Неопределено;
	
	Если Не ЗначениеЗаполнено(ЗначениеПоиска) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ЗначениеПоиска", ЗначениеПоиска);
	
	Если ТипЗнч(ЗначениеПоиска) = Тип("СправочникСсылка.НастройкиПодключенияКОблачнымКассам") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиПодключенияКОблачнымКассам.Ссылка КАК Ссылка,
		|	НастройкиПодключенияКОблачнымКассам.Идентификатор КАК Идентификатор
		|ИЗ
		|	Справочник.НастройкиПодключенияКОблачнымКассам КАК НастройкиПодключенияКОблачнымКассам
		|ГДЕ
		|	НастройкиПодключенияКОблачнымКассам.Ссылка = &ЗначениеПоиска";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиПодключенияКОблачнымКассам.Ссылка КАК Ссылка,
		|	НастройкиПодключенияКОблачнымКассам.Идентификатор КАК Идентификатор
		|ИЗ
		|	Справочник.НастройкиПодключенияКОблачнымКассам КАК НастройкиПодключенияКОблачнымКассам
		|ГДЕ
		|	НастройкиПодключенияКОблачнымКассам.ОблачнаяКасса = &ЗначениеПоиска";
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Результат = Новый Структура();
		Результат.Вставить("Ссылка"       , Выборка.Ссылка);
		Результат.Вставить("Идентификатор", Выборка.Идентификатор);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет поиск настройки подключения по облачным кассам.
//
// Параметры:
//  ОблачныеКассы - Массив Из ОпределяемыйТип.ОблачнаяКасса - ссылки на элемент справочника для которого настроена интеграция с
//    Облачной кассой.
//
// Возвращаемое значение:
//  Массив Из СправочникСсылка.НастройкиПодключенияКОблачнымКассам - найденные настройки подключения
//
Функция НастройкиПодключенияПоОблачнымКассам(ОблачныеКассы) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОблачнымКассам.Ссылка КАК НастройкаПодключения,
	|	НастройкиПодключенияКОблачнымКассам.ОблачнаяКасса КАК ОблачнаяКасса
	|ИЗ
	|	Справочник.НастройкиПодключенияКОблачнымКассам КАК НастройкиПодключенияКОблачнымКассам
	|ГДЕ
	|	НастройкиПодключенияКОблачнымКассам.ОблачнаяКасса В(&ОблачныеКассы)";
	
	Запрос.УстановитьПараметр("ОблачныеКассы", ОблачныеКассы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого ОблачнаяКасса Из ОблачныеКассы Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("ОблачнаяКасса", ОблачнаяКасса);
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Отбор) Тогда
			Результат.Вставить(ОблачнаяКасса, Выборка.НастройкаПодключения);
		Иначе
			Результат.Вставить(ОблачнаяКасса, Неопределено);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ОблачнаяКасса(НастройкаПодключения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиПодключенияКОблачнымКассам.ОблачнаяКасса КАК ОблачнаяКасса
	|ИЗ
	|	Справочник.НастройкиПодключенияКОблачнымКассам КАК НастройкиПодключенияКОблачнымКассам
	|ГДЕ
	|	НастройкиПодключенияКОблачнымКассам.Ссылка = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОблачнаяКасса;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли