#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ОбъектыНоменклатуры") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НоменклатураИСтроки.ЗагрузитьЗначения(Параметры.ОбъектыНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Сохранить изменения?';
							|en = 'Save changes?'");
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьПередЗакрытием", ЭтотОбъект), 
				ТекстВопроса, 
				РежимДиалогаВопрос.ДаНет,
				,
				,
				НСтр("ru = 'Сохранение изменений';
					|en = 'Save the changes'")); 
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура Операции0ПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Операции0НачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОповещениеОВыборе = Новый ОписаниеОповещения("ПослеВыбораОперации0", ЭтотОбъект, Новый Структура);
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Операции0", Операции0);
	ОткрытьФорму("Справочник.Операции0.Форма.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, Истина, , , ОповещениеОВыборе);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере(); 
	Модифицированность = Ложь;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	Модифицированность = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьПередЗакрытием(Ответ, ДополнительныеПараметры) Экспорт
	Модифицированность = Ложь;
	Если Ответ = КодВозвратаДиалога.Да Тогда 
		ЗаписатьНаСервере();
		Закрыть();
	Иначе
		Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	Для Каждого Объект Из НоменклатураИСтроки Цикл
		НачатьТранзакцию();
		Попытка
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.Номенклатура");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", Объект.Значение);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
		
			НоменклатураОбъект = Объект.Значение.ПолучитьОбъект();
			НоменклатураОбъект.Операции0 = Операции0;
			НоменклатураОбъект.Записать();
		
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
	
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Не удалось записать элемент справочника';
											|en = 'Cannot save the catalog item'"),
				УровеньЖурналаРегистрации.Ошибка,
					,
				,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОперации0(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Операции0 = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти