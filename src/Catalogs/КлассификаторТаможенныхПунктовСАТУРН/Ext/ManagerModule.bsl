#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаВыбора" Тогда
	
		Если Параметры = Неопределено Тогда
			Параметры = Новый Структура();
		КонецЕсли;
		
		Параметры.Вставить("РежимВыбора", Истина);
		
		ВыбраннаяФорма       = "Справочник.КлассификаторТаможенныхПунктовСАТУРН.Форма.ФормаСписка";
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;

КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Сообщения

// Сообщение к передаче JSON.
//
// Параметры:
//  СправочникСсылка - СправочникСсылка.КлассификаторТаможенныхПунктовСАТУРН - Ссылка на страну.
//  ДальнейшееДействие - ПеречислениеСсылка.ДальнейшиеДействияПоВзаимодействиюСАТУРН - Дальнейшее действие.
//  ДополнительныеПараметры - Структура, Неопределено - Дополнительные параметры.
//
// Возвращаемое значение:
//  Массив Из См. ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON - Сообщения к передаче.
//
Функция СообщениеКПередачеJSON(СправочникСсылка, ДальнейшееДействие, ДополнительныеПараметры = Неопределено) Экспорт
	
	СообщенияJSON = Новый Массив;
	
	Если ДальнейшееДействие = Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.ЗагрузитеКлассификатор Тогда
		
		Возврат ЗагрузкаКлассификатораТаможенныхПунктовJSON(ДополнительныеПараметры);
		
	КонецЕсли;

	Возврат СообщенияJSON;
	
КонецФункции

Процедура ОбработкаЗагрузкиПолученныхДанных(ЭлементОчереди, ПараметрыОбмена, ПолученныеДанные, ИзмененныеОбъекты) Экспорт
	
	Если ЭлементОчереди.Операция = ОперацияЗагрузкиКлассификатора() Тогда
		
		ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
			ПустаяСсылка().Метаданные(), ПолученныеДанные, ПараметрыОбмена);
		ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
		
		Попытка
			
			Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
				
				ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
				СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,,, ЭлементОчереди.ОрганизацияСАТУРН);
				
				Если Не ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
					Продолжить;
				КонецЕсли;
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
				
			КонецЦикла;
			
		Исключение
			ВызватьИсключение;
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Обмен

// Данные объекта.
// 
// Параметры:
//  ЭлементДанных - Произвольный - Элемент данных
// 
// Возвращаемое значение:
//  Структура - Данные объекта:
// * Идентификатор - Строка
// * Наименование - Строка - наименование территориального управления
// * КодТаможни - Строка - Код ТУ
Функция ДанныеОбъекта(ЭлементДанных) Экспорт
	
	ДанныеТаможенногоПункта         = Новый Структура;
	ФорматОтображенияИдентификатора = "ЧРД=; ЧРГ=; ЧГ=;";
	
	ДанныеТаможенногоПункта.Вставить("Идентификатор", Формат(ЭлементДанных.id, ФорматОтображенияИдентификатора));
	ДанныеТаможенногоПункта.Вставить("Наименование",  ЭлементДанных.borderPpName);
	ДанныеТаможенногоПункта.Вставить("КодТаможни",    ЭлементДанных.borderPpCode);
	
	Возврат ДанныеТаможенногоПункта;
	
КонецФункции

Функция ОперацияЗагрузкиКлассификатора() Экспорт
	Возврат Перечисления.ВидыОперацийСАТУРН.ЗагрузкаКлассификаторов;
КонецФункции

#КонецОбласти

#Область ПоискСсылок

Функция КлассификаторТаможенногоПункта(ЗначениеИдентификатора, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Идентификатор = Формат(ЗначениеИдентификатора, "ЧГ=0;");
	Если Не ЗначениеЗаполнено(Идентификатор)
		Или ЗначениеИдентификатора = -1 Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ИмяТаблицы       = Метаданные.Справочники.КлассификаторТаможенныхПунктовСАТУРН.ПолноеИмя();
	СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
	Иначе
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить(ИмяТаблицы);
		ЭлементБлокировки.УстановитьЗначение("Идентификатор", Идентификатор);
		
		ТранзакцияЗафиксирована = Истина;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка.Заблокировать();
			
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(ПараметрыОбмена, ИмяТаблицы, Идентификатор);
			
			Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
				ДанныеОбъекта = ИнтеграцияСАТУРНСлужебный.ДанныеОбъекта(
					Идентификатор,
					Метаданные.Справочники.КлассификаторТаможенныхПунктовСАТУРН, ПараметрыОбмена);
				Если ДанныеОбъекта = Неопределено Тогда
					СправочникСсылка = СоздатьТаможенныйПункт(Идентификатор, ПараметрыОбмена);
					ИнтеграцияСАТУРНСлужебный.ДобавитьКЗагрузке(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка, ОрганизацияСАТУРН);
				Иначе
					СправочникСсылка = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена,, Ложь, ОрганизацияСАТУРН);
				КонецЕсли;
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТранзакцияЗафиксирована = Ложь;
			
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Ошибка при создании таможенного пункта с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании таможенного пункта с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ТекстОшибкиПодробно = СтрШаблон(
				НСтр("ru = 'Ошибка при создании таможенного пункта с идентификатором %1:
				           |%2';
				           |en = 'Ошибка при создании таможенного пункта с идентификатором %1:
				           |%2'"),
				Идентификатор,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ОбщегоНазначенияИСВызовСервера.ЗаписатьОшибкуВЖурналРегистрации(
				ТекстОшибкиПодробно,
				НСтр("ru = 'Работа с классификатором таможенных пунктов';
					|en = 'Работа с классификатором таможенных пунктов'", ОбщегоНазначения.КодОсновногоЯзыка()));
			
			ВызватьИсключение ТекстОшибки;
			
		КонецПопытки;
		
		Если ТранзакцияЗафиксирована Тогда
			ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(ПараметрыОбмена, ИмяТаблицы, Идентификатор, СправочникСсылка);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СправочникСсылка;
	
КонецФункции

Функция ЗагрузитьОбъекты(СписокКлассификатораТаможенныхПунктов, ПараметрыОбмена, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	ИзмененныеОбъекты = Новый Массив;
	
	Если СписокКлассификатораТаможенныхПунктов.Количество() = 0 Тогда
		Возврат ИзмененныеОбъекты;
	КонецЕсли;
	
	ВходящиеДанные = ИнтеграцияСАТУРНСлужебный.ОбработатьРезультатЗапросаСпискаОбъектов(
		ПустаяСсылка().Метаданные(), СписокКлассификатораТаможенныхПунктов, ПараметрыОбмена);
	ИнтеграцияСАТУРНСлужебный.СсылкиПоИдентификаторам(ПараметрыОбмена, ИзмененныеОбъекты);
	
	Попытка
		
		Для Каждого ЭлементДанных Из ВходящиеДанные Цикл
			
			ДанныеОбъекта   = ДанныеОбъекта(ЭлементДанных);
			СсылкаНаЭлемент = ЗагрузитьОбъект(ДанныеОбъекта, ПараметрыОбмена);
			
			Если ЗначениеЗаполнено(СсылкаНаЭлемент) Тогда
				ИзмененныеОбъекты.Добавить(СсылкаНаЭлемент);
			КонецЕсли;
			
		КонецЦикла;
		
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат ИзмененныеОбъекты;
	
КонецФункции

Функция ЗагрузитьОбъект(ДанныеТаможенногоПункта, ПараметрыОбмена, СправочникОбъект = Неопределено, ТребуетсяПоиск = Истина, ОрганизацияСАТУРН = Неопределено) Экспорт
	
	Если ДанныеТаможенногоПункта = Неопределено Тогда
		Возврат ПустаяСсылка();
	КонецЕсли;
	
	ЗаписьНового = Ложь;
	ИмяТаблицы   = Метаданные.Справочники.КлассификаторТаможенныхПунктовСАТУРН.ПолноеИмя();
	
	Если СправочникОбъект = Неопределено Тогда
		
		СправочникСсылка = Неопределено;
		Если ТребуетсяПоиск Тогда
			СправочникСсылка = ИнтеграцияСАТУРНСлужебный.СсылкаПоИдентификатору(
				ПараметрыОбмена, ИмяТаблицы, ДанныеТаможенногоПункта.Идентификатор);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникОбъект = СоздатьЭлемент();
			СправочникОбъект.Заполнить(Неопределено);
			
			ИдентификаторОбъекта = Новый УникальныйИдентификатор();
			СправочникОбъект.УстановитьСсылкуНового(ПолучитьСсылку(ИдентификаторОбъекта));
			ЗаписьНового = Истина;
			
		Иначе
			СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗаписьНового Тогда
		СправочникОбъект.Заблокировать();
	КонецЕсли;
	
	СправочникОбъект.Наименование  = ДанныеТаможенногоПункта.Наименование;
	СправочникОбъект.Идентификатор = ДанныеТаможенногоПункта.Идентификатор;
	СправочникОбъект.КодТаможни    = ДанныеТаможенногоПункта.КодТаможни;
	
	СправочникОбъект.ТребуетсяЗагрузка = Ложь;
	СправочникОбъект.Записать();
	
	ИнтеграцияСАТУРНСлужебный.ОбновитьСсылку(
		ПараметрыОбмена,
		ИмяТаблицы,
		ДанныеТаможенногоПункта.Идентификатор,
		СправочникОбъект.Ссылка);
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

Функция СоздатьТаможенныйПункт(Идентификатор, ПараметрыОбмена)
	
	СправочникОбъект = СоздатьЭлемент();
	СправочникОбъект.Идентификатор     = Идентификатор;
	СправочникОбъект.ТребуетсяЗагрузка = Истина;
	СправочникОбъект.Наименование      = НСтр("ru = '<Требуется загрузка>';
												|en = '<Требуется загрузка>'");
	
	СправочникОбъект.Записать();
	
	Возврат СправочникОбъект.Ссылка;
	
КонецФункции

#КонецОбласти

#Область Сообщения

Функция ЗагрузкаКлассификатораТаможенныхПунктовJSON(ПараметрыОбмена)
	
	СообщенияJSON  = Новый Массив;
	
	СообщениеJSON = ИнтеграцияСАТУРНСлужебный.СтруктураСообщенияJSON();
	СообщениеJSON.Документ            = ПустаяСсылка();
	СообщениеJSON.Версия              = 1;
	СообщениеJSON.Операция            = ОперацияЗагрузкиКлассификатора();
	СообщениеJSON.Описание            = ИнтеграцияСАТУРНСлужебный.ОписаниеОперацииПередачиДанных(СообщениеJSON.Операция);
	СообщениеJSON.АргументыОперации   = ИнтерфейсСАТУРН.АргументыОперации();
	
	АргументыЗапроса = СообщениеJSON.АргументыОперации;
	ИмяСловаря = "border";
	
	ИнтерфейсСАТУРН.ДобавитьАргумент(АргументыЗапроса, "dictionary", ИмяСловаря);
	
	СообщенияJSON.Добавить(СообщениеJSON);
	
	Возврат СообщенияJSON;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли