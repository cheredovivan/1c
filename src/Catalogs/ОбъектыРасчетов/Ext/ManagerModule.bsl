#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	(Объект <> Неопределено
	|		И ЗначениеРазрешено(Объект)
	|	ИЛИ Объект = Неопределено
	|		И ЗначениеРазрешено(Организация)
	|		И ЗначениеРазрешено(Партнер)
	|	)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	(Объект <> Неопределено
	|		И ЗначениеРазрешено(Объект)
	|	ИЛИ Объект = Неопределено
	|		И ЗначениеРазрешено(Организация)
	|		И ЗначениеРазрешено(Партнер)
	|	)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Поля.Добавить("Ссылка");
	Поля.Добавить("Объект");
	Поля.Добавить("Наименование");
	Поля.Добавить("ТипСсылки");
	Поля.Добавить("ТипРасчетов");
	Поля.Добавить("ТипОбъектаРасчетов");
	Поля.Добавить("НаименованиеПервичногоДокумента");
	Поля.Добавить("НомерВходящегоДокумента");
	Поля.Добавить("ДатаВходящегоДокумента");
	Поля.Добавить("Дата");
	Поля.Добавить("Номер");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Данные.Объект) И ЗначениеЗаполнено(Данные.Ссылка) Тогда
		Представление = НСтр("ru = '<Пустой>';
							|en = '<Empty>'");
		СтандартнаяОбработка = Ложь;
	ИначеЕсли Данные.ТипРасчетов = ПредопределенноеЗначение("Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком")
		И Данные.ТипОбъектаРасчетов = ПредопределенноеЗначение("Перечисление.ТипыОбъектовРасчетов.Накладная") Тогда
		
		Данные.Вставить("НаименованиеВходящегоДокумента", Данные.НаименованиеПервичногоДокумента);
		ОбщегоНазначенияУТКлиентСервер.ОбработкаПолученияПредставленияВходящегоДокумента(
		Данные, Представление, СтандартнаяОбработка, Данные.ТипСсылки);
		
	Иначе
		Представление = Данные.Наименование;   
		СтандартнаяОбработка = Ложь;
		#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		Если НЕ МультиязычностьПовтИсп.КонфигурацияИспользуетТолькоОдинЯзык(Ложь) Тогда
			
			Если ЗначениеЗаполнено(Данные.Объект) И Данные.ТипОбъектаРасчетов <> ПредопределенноеЗначение("Перечисление.ТипыОбъектовРасчетов.Договор") Тогда
				Представление = ОбщегоНазначенияУТКлиентСервер.СтандартноеПредставлениеДокумента(Данные.ТипСсылки, Данные.Номер, Данные.Дата);
			КонецЕсли;
			
		КонецЕсли;
		#КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" Тогда
		#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
			РежимОтлажки = ОбщегоНазначения.РежимОтладки();
		#Иначе
			РежимОтлажки = ОбщегоНазначенияКлиент.РежимОтладки();
		#КонецЕсли
		
		Если НЕ РежимОтлажки Тогда
			Если Параметры.Свойство("Ключ") И ЗначениеЗаполнено(Параметры.Ключ) Тогда
				Результат= ВзаиморасчетыВызовСервера.ФормаОбъектаРасчетов(Параметры.Ключ);
				Если Результат.Форма <> "" Тогда
					СтандартнаяОбработка = Ложь;
					ВыбраннаяФорма = Результат.Форма;
					Параметры.Ключ = Результат.Ключ;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "Справочники.ОбъектыРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.39";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("51119c20-5762-5c1b-bdf7-2f9a8da27bec");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "Справочники.ОбъектыРасчетов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр("ru = 'Обновление справочника ""Объекты расчетов"":';
								|en = 'Update the ""AR/AP objects"" catalog:'"));
	СписокОписаний.Добавить(НСтр("ru = '- Заполнение признака ""Регистры взаиморасчетов делятся по направлениям деятельности""';
								|en = '- Fill in the ""AR/AP accounting registers are divided by lines of business"" flag'"));
	
	Обработчик.Комментарий = СтрСоединить(СписокОписаний, Символы.ПС);
	// Влияет на проведение по взаиморасчетам
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Критичный;
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.Справочники.ОбъектыРасчетов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПустаяСсылка().Метаданные().ПолноеИмя();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Справочник.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК Справочник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК ЗаписиРегистраСНД
	|			ПО ЗаписиРегистраСНД.ОбъектРасчетов = Справочник.Ссылка
	|				И ЗаписиРегистраСНД.АналитикаУчетаПоПартнерам.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|ГДЕ
	|	Справочник.РегистрыВзаиморасчетовНеДелятсяПоНаправлениямДеятельности <> (ЗаписиРегистраСНД.ОбъектРасчетов ЕСТЬ NULL)
	|	И Справочник.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Справочник.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК Справочник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК ЗаписиРегистраСНД
	|			ПО ЗаписиРегистраСНД.ОбъектРасчетов = Справочник.Ссылка
	|				И ЗаписиРегистраСНД.АналитикаУчетаПоПартнерам.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|ГДЕ
	|	Справочник.РегистрыВзаиморасчетовНеДелятсяПоНаправлениямДеятельности <> (ЗаписиРегистраСНД.ОбъектРасчетов ЕСТЬ NULL)
	|	И Справочник.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Справочник.Ссылка КАК ОбъектРасчетов
	|ИЗ
	|	Справочник.ОбъектыРасчетов КАК Справочник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НаправленияДеятельности КАК Направление
	|			ПО Направление.Ссылка = Справочник.НаправлениеДеятельности
	|ГДЕ
	|	НЕ Направление.УчетДоходов И Справочник.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|	ИЛИ НЕ Направление.УчетРасчетовСПоставщиками И Справочник.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
	|	
	|";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ОбъектРасчетов"));
	
КонецПроцедуры

// Обработчик обновления
// 
// Параметры:
// 	Параметры - См. ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляМногопоточнойОбработки 
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	ПолноеИмяОбъекта = ПустаяСсылка().Метаданные().ПолноеИмя();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	ИсключенияПриОбновлении = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДанныеДляОбновления.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ТаблицаЭлементов
		|ИЗ
		|	&ДанныеДляОбновления КАК ДанныеДляОбновления
		|ИНДЕКСИРОВАТЬ ПО Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ВложенныйЗапрос.ЕстьЗаписиСНД) КАК ЕстьЗаписиСНД
		|ИЗ (
		|	ВЫБРАТЬ
		|		ТаблицаЭлементов.Ссылка КАК Ссылка,
		|		ЕСТЬNULL(РасчетыСКлиентами.АналитикаУчетаПоПартнерам.НаправлениеДеятельности,ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				 <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК ЕстьЗаписиСНД
		|	ИЗ
		|		ТаблицаЭлементов КАК ТаблицаЭлементов
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
		|				ПО ТаблицаЭлементов.Ссылка = РасчетыСКлиентами.ОбъектРасчетов
		|	ГДЕ
		|		РасчетыСКлиентами.ОбъектРасчетов В (ВЫБРАТЬ Ссылка ИЗ ТаблицаЭлементов)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаЭлементов.Ссылка КАК Ссылка,
		|		ЕСТЬNULL(РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.НаправлениеДеятельности,ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка))
		|				 <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК ЕстьЗаписиСНД
		|	ИЗ
		|		ТаблицаЭлементов КАК ТаблицаЭлементов
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
		|				ПО ТаблицаЭлементов.Ссылка = РасчетыСПоставщиками.ОбъектРасчетов) КАК ВложенныйЗапрос
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Ссылка 
		|";
	
	Запрос.УстановитьПараметр("ДанныеДляОбновления", ОбновляемыеДанные);
	
	Справочник = Запрос.Выполнить().Выбрать();
	
	Пока Справочник.Следующий() Цикл
		
		ПричинаИсключения = "";
		Рекомендация = "";
		
		НачатьТранзакцию();
		
		Попытка
			
			ПричинаИсключения = "Блокировка";
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Справочник.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			СправочникОбъект = Справочник.Ссылка.ПолучитьОбъект(); // СправочникОбъект
			
			ПричинаИсключения = "ПлохиеДанные";
			Рекомендация = НСтр("ru = 'Проверьте данные справочника вручную.';
								|en = 'Check the catalog data manually.'");
			
			Если СправочникОбъект <> Неопределено Тогда
				
				// Заполнение РегистрыВзаиморасчетовНеДелятсяПоНаправлениямДеятельности
				Если СправочникОбъект.РегистрыВзаиморасчетовНеДелятсяПоНаправлениямДеятельности <> (НЕ Справочник.ЕстьЗаписиСНД) Тогда
					СправочникОбъект.РегистрыВзаиморасчетовНеДелятсяПоНаправлениямДеятельности = НЕ Справочник.ЕстьЗаписиСНД;
				КонецЕсли;
				
				// Очистка неправильных НД
				Если ЗначениеЗаполнено(СправочникОбъект.НаправлениеДеятельности) Тогда
					ЗначенияРеквизитовНД = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникОбъект.НаправлениеДеятельности, "УчетДоходов,УчетРасчетовСПоставщиками");
					Если СправочникОбъект.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом И НЕ ЗначенияРеквизитовНД.УчетДоходов
						ИЛИ СправочникОбъект.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком И НЕ ЗначенияРеквизитовНД.УчетРасчетовСПоставщиками Тогда
							СправочникОбъект.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ПустаяСсылка();
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ПричинаИсключения = "Запись";
			
			Если СправочникОбъект.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(СправочникОбъект);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(Справочник.Ссылка);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Справочник.Ссылка);
			
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				
				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
					Справочник.Ссылка, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				
			ИначеЕсли ПричинаИсключения = "Запись" Тогда
				
				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
					ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(НСтр("ru = 'Не удалось обработать элемент справочника ""Объекты расчетов"".';
													|en = 'Cannot process the ""AR/AP objects"" catalog item.'"), Символы.ПС);
				
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбъектовОбработано > 0 Тогда
		ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена =
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли