
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.ДанныеПанелей) <> Тип("Массив") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для каждого Панель Из Параметры.ДанныеПанелей Цикл
		ЗаполнитьЗначенияСвойств(ПанелиИнтернетСервиса.Добавить(), Панель);
	КонецЦикла;
	
	Структура = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"Обработка.УправлениеПанелями1САналитики", "АктуализацияПанелейИзИнтернетСервиса");
	
	Если ТипЗнч(Структура) = Тип("Структура") Тогда
		Для каждого Строка Из ПанелиИнтернетСервиса Цикл
			Структура.Свойство(Строка.Идентификатор, Строка.Пометка)
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		ПередЗакрытиемНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПанелиИнтернетСервиса

&НаКлиенте
Процедура ПанелиИнтернетСервисаПриАктивизацииПоля(Элемент)
	
	ИмяТекущейКолонки = Элемент.ТекущийЭлемент.Имя;
	Если ИмяТекущейКолонки = "ПанелиИнтернетСервисаПодробнееНаИТС" Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Элемент.ТекущиеДанные.ПодробнееНаИТС);
		Элемент.ТекущийЭлемент = Элементы[ИмяТекущегоЭлементаФормы];
	Иначе
		ИмяТекущегоЭлементаФормы = ИмяТекущейКолонки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьВыбранныеПанели(Команда)
	
	Результат = Новый Структура;
	
	Для каждого Строка Из ПанелиИнтернетСервиса Цикл
		Если Строка.Пометка Тогда
			Результат.Вставить(Строка.ИдентификаторВерсииПанели, СтрРазделить(Строка.ИдентификаторыВерсийДопИсточников, ",", Ложь));
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	ОтметитьЭлементыТаблицы(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
	ОтметитьЭлементыТаблицы(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПередЗакрытиемНаСервере()
	
	Структура = Новый Структура;
	Для каждого Строка Из ПанелиИнтернетСервиса Цикл
		Структура.Вставить(Строка.Идентификатор, Строка.Пометка);
	КонецЦикла;
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"Обработка.УправлениеПанелями1САналитики", "АктуализацияПанелейИзИнтернетСервиса", Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьЭлементыТаблицы(Пометка) 
	Для Каждого Строка Из ПанелиИнтернетСервиса Цикл
		Строка.Пометка = Пометка;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
