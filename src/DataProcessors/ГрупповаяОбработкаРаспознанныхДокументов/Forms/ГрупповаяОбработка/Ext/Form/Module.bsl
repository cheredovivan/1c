
#Область ОписаниеПеременных

&НаКлиенте
Перем КоординатыВыделения;

&НаКлиенте
Перем СтрокВИзображенииВыделения;

&НаКлиенте
Перем ДанныеДублей;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	ИспользоватьСоглашенияСПоставщиками = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками");
	ИспользоватьИмпортныеТовары = ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеТовары");
	
	ЗаполнитьВидимостьПодсказок();
	ПолнаяКартинкаВидна = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(
		"Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.ГрупповаяОбработка", "ПолнаяКартинкаВидна");
	
	СтруктураКнопок = Новый Структура;
	СтруктураКнопок.Вставить("setmain", НСтр("ru = 'Сделать основным';
											|en = 'Set as main'"));
	СтруктураКнопок.Вставить("duplicate", НСтр("ru = 'Дубль';
												|en = 'Duplicate'"));
	СтруктураКнопок.Вставить("notDuplicate", НСтр("ru = 'Другой документ';
													|en = 'Another document'"));
	СтруктураКнопок.Вставить("main", НСтр("ru = 'Основной экземпляр';
											|en = 'Main instance'"));
	СтруктураКнопок.Вставить("removeDuplicate", НСтр("ru = 'Убрать из дублей';
													|en = 'Remove from duplicates'"));
	СтруктураКнопок.Вставить("returnDuplicate", НСтр("ru = 'Вернуть в дубли';
													|en = 'Return to duplicates'"));
	СтрокаКнопокДляДублей = РаспознаваниеДокументовHTTP.ОбъектВJson(СтруктураКнопок);
	
	ДанныеФоновыхОпераций = Новый Структура;
	ДанныеФоновыхОпераций.Вставить("ИзмененияРеквизитовШапки", Новый Массив);
	ДанныеФоновыхОпераций.Вставить("ИзмененияСписковВыбораШапки", Новый Массив);
	ДанныеФоновыхОпераций.Вставить("ИзмененияНоменклатуры", Новый Массив);
	ДанныеФоновыхОпераций.Вставить("ИзмененияТаблицыСумм", Новый Массив);
	
	РаспознаваниеДокументовПраваДоступа.ЗаполнитьСправочникиБезПравНаСоздание(СправочникиБезПравНаСоздание, Истина);
	
	НастроитьКолонкиТаблицыШапки();
	
	Если Параметры.Свойство("ОтборИзСписка") Тогда
		ЗаполнитьДокументыПоОтбору(Параметры.ОтборИзСписка);
	Иначе
		ОтборИзСписка = Новый Структура("РаспознанныеДокументы, ТекущаяДата, ТекущаяДатаПериод, ТекущаяОрганизация, ТекущийКонтрагент", Новый Массив);
		ЗаполнитьДокументыПоОтбору(ОтборИзСписка);
	КонецЕсли;
	
	// Нажатые кнопки
	СкрытьЗаполненныеСтрокиШапки = Истина;
	Элементы.РеквизитыШапки.ОтборСтрок = Новый ФиксированнаяСтруктура("ДанныеЗаполнены", Ложь);
	СкрытьЗаполненныеСтрокиДопРеквизитов = Истина;
	Элементы.ТаблицаДопРеквизитов.ОтборСтрок = Новый ФиксированнаяСтруктура("ДанныеЗаполнены", Ложь);
	СкрытьЗаполненнуюНоменклатуру = Истина;
	СкрытьЗаполненныеСтрокиСумм = Истина;
	Элементы.ТаблицаСумм.ОтборСтрок = Новый ФиксированнаяСтруктура("ЕстьОшибка", Истина);
	
	ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML(УникальныйИдентификатор);
	
	ТекущийШаг = "Шапка";
	ВыделитьШагТекущейСтраницы();
	УстановитьУсловноеОформление();
	УстановитьСвойстваЭлементовФормы();
	
	РаспознаваниеДокументовПереопределяемый.ГрупповаяОбработкаПриСозданииНаСервере(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("Подключаемый_АктивироватьПервуюСтроку", 0.3, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения("ОтправитьОбратнуюСвязьПоГрупповойОбработкеКлиент", ЭтотОбъект);
	
	ТекстПредупреждения = НСтр("ru = 'Закрыть групповую обработку распознанных документов?';
								|en = 'Close bulk processing of recognized documents?'");
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
		ЭтотОбъект, Отказ, ЗавершениеРаботы, ТекстПредупреждения, "ЗакрытьФормуБезПодтверждения", Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АктивироватьПервуюСтроку() Экспорт
	
	ВыделитьНезаполненнуюЯчейкуНаШаге();
	Подключаемый_ПриАктивизацииЯчейкиШапки();
	ОбновитьВидимостьНадписиПроЗаполненность();
	СменитьВидимостьПолнойКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.СозданиеКонтрагентов" Тогда
		СозданиеКонтрагентов(ВыбранноеЗначение);
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.СозданиеДоговоров" Тогда
		СозданиеДоговоров(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ВыбранРеквизитФормыСоздания" Тогда
		ПриблизитьПоКоординатам(Параметр.Координаты, Параметр.ВысотаКартинки);
	ИначеЕсли ИмяСобытия = "РаспознанныйДокумент_ОбновитьОтборФормыСписка"
		Или ИмяСобытия = "РаспознанныйДокумент_СтатусОбработан" Тогда
		ОбновитьДанныеДокументаВКомплекте(Параметр);
		Если Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаИтого Тогда
			ЗаполнитьТаблицыИтого();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеПросмотраДокументСформирован(Элемент)
	
	Если Элемент.Имя = "ПолеПросмотра" Тогда
		HTMLДокументСформирован = Истина;
	Иначе
		НомерПоля = СтрЗаменить(Элемент.Имя, "ПолеПросмотра", "");
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерПоля) Тогда
			НомерПоля = Число(НомерПоля);
			Пока HTMLДублейСформирован.Количество() - 1 < НомерПоля Цикл
				HTMLДублейСформирован.Добавить(Ложь);
			КонецЦикла;
			HTMLДублейСформирован[НомерПоля].Значение = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеПросмотраПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ДанныеСобытия.Element.id = "image_load_button" Тогда
		КартинкаЗагруженаВHTML = Истина;
	ИначеЕсли ДанныеСобытия.Element.id = "setMain" ИЛИ ДанныеСобытия.Element.id = "" Тогда
		
		Если ТекущаяСтрокаДублей <> Элементы.ТаблицаДублей.ТекущаяСтрока Тогда
			Возврат;
		КонецЕсли;
		
		НомерПоля = СтрЗаменить(Элемент.Имя, "ПолеПросмотра", "");
		Если НомерПоля <> "" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерПоля) Тогда
			НомерПоля = Число(НомерПоля);
			
			Если Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы[НомерПоля].УбратьИзДублей = Ложь Тогда
				
				Элементы.ТаблицаДублей.ТекущиеДанные.ЕстьОсновной = Истина;
				Для Каждого СтрокаСписка Из Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы Цикл
					
					ИндексСтроки = Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы.Индекс(СтрокаСписка);
					Если СтрокаСписка.УбратьИзДублей Тогда
						Продолжить;
					КонецЕсли;
					
					ДокументHTML = ДокументHTML(Элементы["ПолеПросмотра" + ИндексСтроки]);
					Если ДокументHTML = Неопределено Тогда
						Продолжить;
					КонецЕсли;
					СтрокаСписка.Основной = (НомерПоля = ИндексСтроки);
					ДокументHTML.setMain(СтрокаСписка.Основной);
					
				КонецЦикла;
				
				ОбновитьВидимостьНадписиПроЗаполненность();
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ДанныеСобытия.Element.id = "setDuplicate" Тогда
		
		Если ТекущаяСтрокаДублей <> Элементы.ТаблицаДублей.ТекущаяСтрока Тогда
			Возврат;
		КонецЕсли;
		
		НомерПоля = СтрЗаменить(Элемент.Имя, "ПолеПросмотра", "");
		Если НомерПоля <> "" И СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(НомерПоля) Тогда
			
			НомерПоля = Число(НомерПоля);
			ТекущееПоле = Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы[НомерПоля];
			ТекущееПоле.Основной = Ложь;
			ТекущееПоле.УбратьИзДублей = Не ТекущееПоле.УбратьИзДублей;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолнаяКартинкаВиднаПриИзменении(Элемент)
	СменитьВидимостьПолнойКартинки();
КонецПроцедуры

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиШапкиПриИзменении(Элемент)
	
	СкрытьЗаполненныеСтрокиШапки();
	ПроверитьИЗагрузитьПустыеКартинки();
	Если ВсеСтрокиНаШагеЗаполнены И Не СкрытьЗаполненныеСтрокиШапки Тогда
		ТекущаяСтрокаДляСпискаВыбора = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовКомплектов

&НаКлиенте
Процедура ПредложениеПеретащитьНажатие(Элемент)
	
	СтрокиДляКомплекта = Элементы.ДанныеДокументов.ВыделенныеСтроки;
	КоличествоКомплектов = КоличествоКомплектов + 1;
	
	РезультатОбратнойСвязи.Комплекты.Вставить(КоличествоКомплектов, Новый Соответствие);
	НовыйКомплектНаСервере(Истина, КоличествоКомплектов);
	
	Если СтрокиДляКомплекта.Количество() > 1 Тогда
		Для Каждого ИдСтроки Из СтрокиДляКомплекта Цикл
			СтрокаТаблицы = ДанныеДокументов.НайтиПоИдентификатору(ИдСтроки);
			СтрокаТаблицы.НомерКомплекта = КоличествоКомплектов;
		КонецЦикла;
		НастроитьПанельИОшибкиКомплекта(КоличествоКомплектов);
	КонецЕсли;
	ПодключитьОбработчикОжидания("Подключаемый_АктивизироватьПоследнийКомплект", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредложениеПеретащитьПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоКомплектов = КоличествоКомплектов + 1;
	РезультатОбратнойСвязи.Комплекты.Вставить(КоличествоКомплектов, Новый Соответствие);
	ТекущиеДействия = РезультатОбратнойСвязи.Комплекты.Получить(КоличествоКомплектов);
	
	Для Каждого СтрокаТаблицы Из ПараметрыПеретаскивания.Значение Цикл
		ТекущиеДействия.Вставить(СтрокаТаблицы.Ссылка, "ДобавленВручную");
		СтрокаТаблицы.НомерКомплекта = КоличествоКомплектов;
	КонецЦикла;
	
	#Если ВебКлиент Тогда
		ИзменяемыеКомплекты = Новый Массив;
		ИзменяемыеКомплекты.Добавить(КоличествоКомплектов);
		ДокументыДляПересчетаОшибок.ЗагрузитьЗначения(ИзменяемыеКомплекты);
		ПодключитьОбработчикОжидания("Подключаемый_НовыйКомплектНаСервере", 0.1, Истина);
		ПодключитьОбработчикОжидания("Подключаемый_НастроитьПанельИОшибкиКомплекта", 0.1, Истина);
	#Иначе
		НовыйКомплектНаСервере(Истина, КоличествоКомплектов);
		НастроитьПанельИОшибкиКомплекта(КоличествоКомплектов);
		Подключаемый_АктивизироватьПоследнийКомплект();
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастроитьПанельИОшибкиКомплекта()
	
	НастроитьПанельИОшибкиКомплектаНесколько();
	Если ДокументыДляПересчетаОшибок.Количество() = 1 И ДокументыДляПересчетаОшибок[0].Значение = КоличествоКомплектов Тогда
		ПодключитьОбработчикОжидания("Подключаемый_АктивизироватьПоследнийКомплект", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПанельИОшибкиКомплектаНесколько()
	
	ВсеНомераКомплектов = ДокументыДляПересчетаОшибок.ВыгрузитьЗначения();
	Для Каждого НомерКомплекта Из ВсеНомераКомплектов Цикл
		НастроитьПанельИОшибкиКомплекта(НомерКомплекта);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НовыйКомплектНаСервере()
	
	ВсеНомераКомплектов = ДокументыДляПересчетаОшибок.ВыгрузитьЗначения();
	Для Каждого НомерКомплекта Из ВсеНомераКомплектов Цикл
		НовыйКомплектНаСервере(Истина, НомерКомплекта);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДопРеквизитов

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиДопРеквизитовПриИзменении(Элемент)
	
	СкрытьЗаполненныеСтрокиДопРеквизитов();
	ПроверитьИЗагрузитьПустыеКартинки();
	Если ВсеСтрокиНаШагеЗаполнены И Не СкрытьЗаполненныеСтрокиДопРеквизитов Тогда
		ТекущаяСтрокаДляСпискаВыбора = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовНоменклатуры

&НаКлиенте
Процедура СкрытьЗаполненнуюНоменклатуруПриИзменении(Элемент)
	
	СкрытьЗаполненнуюНоменклатуруНаСервере(СкрытьЗаполненнуюНоменклатуру);
	ПроверитьИЗагрузитьПустыеКартинки();
	Если ВсеСтрокиНаШагеЗаполнены И Не СкрытьЗаполненнуюНоменклатуру Тогда
		// Отбор в дереве возможен только через усл. оформление, поэтому при отключении отбора (Не СкрытьЗаполненнуюНоменклатуру), если
		// не было видно строк (ВсеСтрокиНаШагеЗаполнены), то нужно обновить ТекущаяСтрокаДляСпискаВыбора и подгрузить картинки текущей строки
		ТекущаяСтрокаДляСпискаВыбора = Неопределено;
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиНоменклатуры", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовСумм

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиСуммПриИзменении(Элемент)
	
	СкрытьЗаполненныеСтрокиСумм();
	ПроверитьИЗагрузитьПустыеКартинки();
	Если ВсеСтрокиНаШагеЗаполнены И Не СкрытьЗаполненныеСтрокиСумм Тогда
		ТекущаяСтрокаДляСпискаВыбора = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРеквизитыШапки

&НаКлиенте
Процедура РеквизитыШапкиПриИзменении(Элемент)
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	Если ДанныеЭтойСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизитаШапки = СтрЗаменить(ТекущийЭлемент.ТекущийЭлемент.Имя, "Шапка", "");
	
	ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(ДанныеЭтойСтроки);
	
	СтрокиТаблицы = Новый Массив;
	СтрокиТаблицы.Добавить(ТекущийЭлемент.ТекущаяСтрока);
	
	ПараметрыЗаписиРеквизитов = Новый Структура;
	ПараметрыЗаписиРеквизитов.Вставить("ТаблицаФормы", Элементы.РеквизитыШапки);
	ПараметрыЗаписиРеквизитов.Вставить("СтрокиТаблицы", СтрокиТаблицы);
	ПараметрыЗаписиРеквизитов.Вставить("ИмяРеквизитаШапки", ИмяРеквизитаШапки);
	ПараметрыЗаписиРеквизитов.Вставить("ЗначениеЗаполнения", ДанныеЭтойСтроки[ИмяРеквизитаШапки]);
	ПараметрыЗаписиРеквизитов.Вставить("ОбновитьЗначениеВЯчейке", Ложь);
	
	ЗаписатьНовоеЗначениеРеквизита(ПараметрыЗаписиРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыШапкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеЭтойСтроки = Элемент.ТекущиеДанные;
	Если Поле.Имя = "ШапкаНаправлениеДокумента" Тогда
		СтандартнаяОбработка = Ложь;
		
		НовыеЗначения = ИзменитьНаправлениеНаСервере(ДанныеЭтойСтроки.Ссылка, ДанныеЭтойСтроки.ВидДокумента);
		ЗаполнитьЗначенияСвойств(ДанныеЭтойСтроки, НовыеЗначения); 
		ЗаполнитьПредупрежденияПоСкидкам();
		ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(ДанныеЭтойСтроки);
	ИначеЕсли Поле.Имя = "ШапкаВидДокумента" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если Не ДанныеЭтойСтроки.ТипДокумента = ПредопределенноеЗначение(
			"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") И Не ДанныеЭтойСтроки.ТипДокумента 
			= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.НеопределенныйДокумент")
			И Не ДанныеЭтойСтроки.ТипДокумента = ПредопределенноеЗначение(
			"Перечисление.ТипыДокументовРаспознаваниеДокументов.КассовыйЧек") Тогда
			
			НовыеЗначения = ИзменитьВидДокументаНаСервере(ДанныеЭтойСтроки.Ссылка);
			ЗаполнитьЗначенияСвойств(ДанныеЭтойСтроки, НовыеЗначения);
			
		КонецЕсли;
		
		ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(ДанныеЭтойСтроки);
		
	ИначеЕсли Поле.Имя = "ШапкаКартинкаОткрытия" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Ссылка);
	ИначеЕсли Поле.Имя = "ШапкаОшибка" Тогда
		
		Если Не ЗначениеЗаполнено(ДанныеЭтойСтроки.Ошибка) Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура("Направление", ДанныеЭтойСтроки.Направление);
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПриПолученииРезультатаОВключенииСкидок", ЭтотОбъект, ДанныеЭтойСтроки);
		ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.ВопросУстановкаФункциональныхОпцийПоСкидкам", ПараметрыФормы, ЭтотОбъект, , , , ОповещениеОЗавершении);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПолученииРезультатаОВключенииСкидок(Результат, ДанныеЭтойСтроки) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да И Результат <> КодВозвратаДиалога.Пропустить Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		РаспознаваниеДокументовСлужебныйВызовСервера.УстановитьЗначениеКонстантыОпцииПоСкидкам(ДанныеЭтойСтроки.Направление);
		ЗаполнитьПредупрежденияПоСкидкам();
		
	Иначе
		ДанныеЭтойСтроки.ЕстьСкидка = Ложь;
		ДанныеЭтойСтроки.Ошибка = "";
	КонецЕсли;
	
	ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(ДанныеЭтойСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыШапкиПриАктивизацииЯчейки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиШапки", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыШапкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	// Нельзя убрать ИзменятьСоставСтрок, т.к. строки можно удалять, а добавлять запрещено
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыШапкиПередНачаломИзменения(Элемент, Отказ)
	
	Если ТекущаяСтрокаДляСпискаВыбора <> Элементы.РеквизитыШапки.ТекущаяСтрока
		Или ТекущееИмяРеквизита <> СтрЗаменить(Элементы.РеквизитыШапки.ТекущийЭлемент.Имя, "Шапка", "") Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиШапки");
		Подключаемый_ПриАктивизацииЯчейкиШапки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыШапкиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПослеЗаполненияСтрокШапки();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДублей

&НаКлиенте
Процедура ТаблицаДублейПриАктивизацииСтроки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииСтрокиДублей", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДублейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеДокументовКомплектов

&НаКлиенте
Процедура ВсеТаблицыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыделеннойСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеТаблицыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьФормуВыделеннойСтроки();
	
КонецПроцедуры

&НаКлиенте
Процедура ВсеТаблицыПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВсеТаблицыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыПеретаскивания.Значение.Количество() <> 0
		И ТипЗнч(ПараметрыПеретаскивания.Значение[0]) = Тип("Число") Тогда
		
		// При перетаскивании строк внутри одной таблицы на форме
		Возврат;
	КонецЕсли;
	
	Если Элементы.НадписьВсеЗаполнено.Видимость Тогда
		Элементы.НадписьВсеЗаполнено.Видимость = Ложь;
	КонецЕсли;
	
	ИзменяемыеКомплекты = Новый Массив;
	Если Элемент.Имя = "ДанныеДокументов" Тогда
		Для Каждого СтрокаТаблицы Из ПараметрыПеретаскивания.Значение Цикл
			Если СтрокаТаблицы = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИзменяемыеКомплекты.Найти(СтрокаТаблицы.НомерКомплекта) = Неопределено Тогда
				ИзменяемыеКомплекты.Добавить(СтрокаТаблицы.НомерКомплекта);
			КонецЕсли;
			
			ТекущиеДействия = РезультатОбратнойСвязи.Комплекты.Получить(СтрокаТаблицы.НомерКомплекта);
			ДействиеДокумента = ТекущиеДействия.Получить(СтрокаТаблицы.Ссылка);
			Если ДействиеДокумента = "ДобавленАвтоматически" Тогда
				ТекущиеДействия.Вставить(СтрокаТаблицы.Ссылка, "Удален");
			Иначе
				ТекущиеДействия.Удалить(СтрокаТаблицы.Ссылка);
			КонецЕсли;
			
			НоваяСтрока = ДанныеДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НомерКомплекта = 0;
			НоваяСтрока.ТипКомплекта = "";
			ДанныеДокументов.Удалить(СтрокаТаблицы);
		КонецЦикла;
		
	Иначе
		
		НомерКомплекта = Число(СтрЗаменить(Элемент.Имя, "Комплект", ""));
		ИзменяемыеКомплекты.Добавить(НомерКомплекта);
		Для Каждого СтрокаТаблицы Из ПараметрыПеретаскивания.Значение Цикл
			Если ИзменяемыеКомплекты.Найти(СтрокаТаблицы.НомерКомплекта) = Неопределено Тогда
				ИзменяемыеКомплекты.Добавить(СтрокаТаблицы.НомерКомплекта);
			КонецЕсли;
			
			ТекущиеДействия = РезультатОбратнойСвязи.Комплекты.Получить(НомерКомплекта);
			ДействиеПрошлогоДобавления = ТекущиеДействия.Получить(СтрокаТаблицы.Ссылка);
			Если ДействиеПрошлогоДобавления <> Неопределено И ДействиеПрошлогоДобавления = "Удален" Тогда
				// Действие "Удален" может быть только у автоматически добавленных документов
				ТекущиеДействия.Вставить(СтрокаТаблицы.Ссылка, "ДобавленАвтоматически");
			Иначе
				ТекущиеДействия.Вставить(СтрокаТаблицы.Ссылка, "ДобавленВручную");
			КонецЕсли;
			
			НоваяСтрока = ДанныеДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НомерКомплекта = НомерКомплекта;
			ДанныеДокументов.Удалить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	#Если ВебКлиент Тогда
		ДокументыДляПересчетаОшибок.ЗагрузитьЗначения(ИзменяемыеКомплекты);
		ПодключитьОбработчикОжидания("Подключаемый_НастроитьПанельИОшибкиКомплекта", 0.1, Истина);
	#Иначе
		Для Каждого НомерКомплекта Из ИзменяемыеКомплекты Цикл
			НастроитьПанельИОшибкиКомплекта(НомерКомплекта);
		КонецЦикла;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВсеТаблицыОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Массив") И ЗначениеЗаполнено(ПараметрыПеретаскивания.Значение) Тогда
		
		НомерКомплекта = ПараметрыПеретаскивания.Значение[0].НомерКомплекта;
		СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
		Если СтрокиКомплекта.Количество() = 0 Тогда
			ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
			Элементы["ДекорацияТекст" + ИмяТаблицы].Заголовок = НСтр("ru = 'Перетащите документы в таблицу';
																	|en = 'Drag documents to the table'");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаДопРеквизитов

&НаКлиенте
Процедура ТаблицаДопРеквизитовПриИзменении(Элемент)
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ИмяРеквизитаШапки = СтрЗаменить(ТекущийЭлемент.ТекущийЭлемент.Имя, "ТаблицаДопРеквизитов", "");

	НастройкаСлужебныхПолейТаблицыДопРеквизитов(ДанныеЭтойСтроки, ИмяРеквизитаШапки);

	ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыДопРеквизиты(ДанныеЭтойСтроки);
	
	ИмяРеквизита = ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, ИмяРеквизитаШапки);
	
	СтрокаИзменений = Новый Структура;
	СтрокаИзменений.Вставить("Ссылка", ДанныеЭтойСтроки.Ссылка);
	СтрокаИзменений.Вставить("ИмяРеквизита", ИмяРеквизита);
	СтрокаИзменений.Вставить("Значение", ДанныеЭтойСтроки[ИмяРеквизитаШапки]);
	СтрокаИзменений.Вставить("ТипДокумента", ДанныеЭтойСтроки.ТипДокумента);
	
	ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
	
	Если ИмяРеквизитаШапки = "ПродавецОрганизация" Тогда
		ДанныеЭтойСтроки["БанковскийСчет"] = ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка");
		ИмяРеквизитаСчет = ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, "БанковскийСчет");
		
		СтрокаИзменений = Новый Структура;
		СтрокаИзменений.Вставить("Ссылка", ДанныеЭтойСтроки.Ссылка);
		СтрокаИзменений.Вставить("ИмяРеквизита", ИмяРеквизитаСчет);
		СтрокаИзменений.Вставить("Значение", ПредопределенноеЗначение("Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка"));
		СтрокаИзменений.Вставить("ТипДокумента", ДанныеЭтойСтроки.ТипДокумента);
	
		ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
	КонецЕсли;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаДопРеквизитовКартинкаОткрытия" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовПриАктивизацииЯчейки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовПередНачаломИзменения(Элемент, Отказ)
	
	Если ТекущаяСтрокаДляСпискаВыбора <> Элементы.ТаблицаДопРеквизитов.ТекущаяСтрока
		Или ТекущееИмяРеквизита <> СтрЗаменить(Элементы.ТаблицаДопРеквизитов.ТекущийЭлемент.Имя, "ТаблицаДопРеквизитов", "") Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов");
		Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПослеЗаполненияСтрокДопРеквизитов();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовДоговорОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораПоляТаблицаДопРеквизитов(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовБанковскийСчетОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораПоляТаблицаДопРеквизитов(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПоляТаблицаДопРеквизитов(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "ТаблицаДопРеквизитов", "");
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")
			И ВыбранноеЗначение.Свойство("СозданВФормеБРД") Тогда
			
			ВыбранноеЗначение = ВыбранноеЗначение.Ссылка;
			СозданНовыйЭлемент = Истина;
		Иначе
			// Выбрали значение из выпадающего списка
			СозданНовыйЭлемент = Ложь;
		КонецЕсли;
		
		Если СозданНовыйЭлемент Тогда
			Если ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Найти(ИмяРеквизита) = Неопределено Тогда
				ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Добавить(ИмяРеквизита);
			КонецЕсли;
			ЗаполнениеСписковВыбораШапки();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено Тогда 
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ЗначениеПодходящейСтроки = ДанныеЭтойСтроки[ИмяРеквизита];
	
	ТипРеквизита = ТипЗнч(ЗначениеПодходящейСтроки);
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипРеквизита) Тогда
		Если ТипРеквизита = Тип("Строка") Тогда
			ВыбранноеЗначение = СокрЛП(ВыбранноеЗначение);
		Иначе
			ВыбранноеЗначение = РаспознаваниеДокументовСериализацияСлужебныйКлиентСервер.ПривестиТип(ВыбранноеЗначение, ТипРеквизита);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеДляСозданияЭлемента = ДанныеДляСозданияЭлемента(ДанныеЭтойСтроки.Ссылка, "РеквизитыДокумента");
	
	ИменаРеквизитов = ИменаРеквизитовБанковскийСчетИСоглашение(ДанныеЭтойСтроки);
	Если ИменаРеквизитов.Свойство(ИмяРеквизита) Тогда
		ИмяРеквизита = ИменаРеквизитов[ИмяРеквизита];
	КонецЕсли;
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыСозданияОбъекта(ИмяРеквизита, ДанныеДляСозданияЭлемента.НаборДанных,
		ДанныеДляСозданияЭлемента.ДополнительныеДанные, ДанныеЭтойСтроки.ТипДокумента, ДанныеЭтойСтроки.Направление,
		ЗначениеПодходящейСтроки);
	
	ИмяФормыСоздания = "";
	РаспознаваниеДокументовКлиент.ИмяФормыСозданияЭлемента(ИмяРеквизита, ИмяФормыСоздания, ПараметрыОткрытия);
	
	ОткрытьФорму(ИмяФормыСоздания, ПараметрыОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДопРеквизитовСоглашениеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТаблицаДопРеквизитов.ТекущиеДанные;
	Если ТекущиеДанные.Направление
		= ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
		
		Операция = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
		ПараметрыВыбораСоглашения = ПродажиКлиент.ПараметрыНачалаВыбораСоглашенияСКлиентом();
		ПараметрыВыбораСоглашения.Элемент = Элемент;
		ПараметрыВыбораСоглашения.Партнер  = ТекущиеДанные.Партнер;
		ПараметрыВыбораСоглашения.Документ = ТекущиеДанные.Соглашение;
		ПараметрыВыбораСоглашения.ХозяйственнаяОперация = Операция;
		ПродажиКлиент.НачалоВыбораСоглашенияСКлиентом(ПараметрыВыбораСоглашения, СтандартнаяОбработка);
		
	Иначе
		
		Операция = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
		ЗакупкиКлиент.НачалоВыбораСоглашенияСПоставщиком(
			Элемент,
			СтандартнаяОбработка,
			ТекущиеДанные.Партнер,
			ТекущиеДанные.Соглашение);
			
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаНоменклатуры

&НаКлиенте
Процедура ТаблицаНоменклатурыПриИзменении(Элемент)
	
	Если Не ТекущийЭлемент.ТекущийЭлемент.Имя = "ТаблицаНоменклатурыНоменклатура" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ИмяРеквизита = СтрЗаменить(ТекущийЭлемент.ТекущийЭлемент.Имя, "ТаблицаНоменклатуры", "");
	
	СтрокиТаблицы = Новый Массив;
	СтрокиТаблицы.Добавить(ТекущийЭлемент.ТекущаяСтрока);
	
	ПараметрыЗаписиРеквизитов = Новый Структура;
	ПараметрыЗаписиРеквизитов.Вставить("ТаблицаФормы", Элементы.ТаблицаНоменклатуры);
	ПараметрыЗаписиРеквизитов.Вставить("ДанныеИзменений", ДанныеФоновыхОпераций.ИзмененияНоменклатуры);
	ПараметрыЗаписиРеквизитов.Вставить("СтрокиТаблицы", СтрокиТаблицы);
	ПараметрыЗаписиРеквизитов.Вставить("ИмяРеквизита", ИмяРеквизита);
	ПараметрыЗаписиРеквизитов.Вставить("ЗначениеЗаполнения", ДанныеЭтойСтроки[ИмяРеквизита]);
	ПараметрыЗаписиРеквизитов.Вставить("ОбновитьЗначениеВЯчейке", Ложь);
	
	ЗаписатьНовоеЗначениеРеквизитаТаблицы(ПараметрыЗаписиРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаНоменклатурыКартинкаОткрытия" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыПриАктивизацииЯчейки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиНоменклатуры", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыПередНачаломИзменения(Элемент, Отказ)
	
	Если ТекущаяСтрокаДляСпискаВыбора <> Элементы.ТаблицаНоменклатуры.ТекущаяСтрока
		Или ТекущееИмяРеквизита <> СтрЗаменить(Элементы.ТаблицаНоменклатуры.ТекущийЭлемент.Имя, "ТаблицаНоменклатуры", "") Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиНоменклатуры");
		Подключаемый_ПриАктивизацииЯчейкиНоменклатуры();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ПроверкаДанныеЗаполненыНоменклатура(ДанныеЭтойСтроки);
	
	ПослеЗаполненияСтрокНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаНоменклатурыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаВыбораПоляТаблицаНоменклатуры(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораПоляТаблицаНоменклатуры(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "ТаблицаНоменклатуры", "");
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")
			И ВыбранноеЗначение.Свойство("СозданВФормеБРД") Тогда
			
			ВыбранноеЗначение = ВыбранноеЗначение.Ссылка;
			СозданНовыйЭлемент = Истина;
		Иначе
			// Выбрали значение из выпадающего списка
			СозданНовыйЭлемент = Ложь;
		КонецЕсли;
		
		Если СозданНовыйЭлемент Тогда
			Если ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Найти(ИмяРеквизита) = Неопределено Тогда
				ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Добавить(ИмяРеквизита);
			КонецЕсли;
			ЗаполнениеСписковВыбораШапки();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено Тогда 
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ЗначениеПодходящейСтроки = ДанныеЭтойСтроки[ИмяРеквизита];
	
	ТипРеквизита = ТипЗнч(ЗначениеПодходящейСтроки);
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипРеквизита) Тогда
		Если ТипРеквизита = Тип("Строка") Тогда
			ВыбранноеЗначение = СокрЛП(ВыбранноеЗначение);
		Иначе
			ВыбранноеЗначение = РаспознаваниеДокументовСериализацияСлужебныйКлиентСервер.ПривестиТип(ВыбранноеЗначение, ТипРеквизита);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
		
	ДанныеДляСозданияЭлемента = ДанныеДляСозданияЭлемента(ДанныеЭтойСтроки.Ссылка, "РеквизитыТабличныхЧастей");
	
	ДополнительныеДанные = ДанныеДляСозданияЭлемента.ДополнительныеДанные;
	ДополнительныеДанные.Вставить("НомерСтрокиТЧ", ДанныеЭтойСтроки.НомерСтрокиТЧ);
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыСозданияОбъекта(ИмяРеквизита, ДанныеДляСозданияЭлемента.НаборДанных,
		ДополнительныеДанные, ДополнительныеДанные.ТипДокумента, ДополнительныеДанные.Направление,
		ЗначениеПодходящейСтроки);
	
	ИмяФормыСоздания = "";
	РаспознаваниеДокументовКлиент.ИмяФормыСозданияЭлемента(ИмяРеквизита, ИмяФормыСоздания, ПараметрыОткрытия);
	
	ОткрытьФорму(ИмяФормыСоздания, ПараметрыОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСумм

&НаКлиенте
Процедура ТаблицаСуммПриИзменении(Элемент)
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ИмяРеквизита = СтрЗаменить(ТекущийЭлемент.ТекущийЭлемент.Имя, "ТаблицаСумм", "");
	
	СтрокиТаблицы = Новый Массив;
	СтрокиТаблицы.Добавить(ТекущийЭлемент.ТекущаяСтрока);
	
	Если ИмяРеквизита = "СтатьяРасходов" Тогда
		
		Если Не ДанныеЭтойСтроки.СписатьНаРасходы Тогда
			ДанныеЭтойСтроки.СтатьяРасходов = Неопределено;
		КонецЕсли;
		
		ПараметрыЗаписиРеквизитов = НовыеПараметрыЗаписиСтрокиТаблицыСумм(СтрокиТаблицы, "СписатьНаРасходы",
			ДанныеЭтойСтроки.СписатьНаРасходы);
		ЗаписатьНовоеЗначениеРеквизитаТаблицы(ПараметрыЗаписиРеквизитов);
		
	КонецЕсли;
	
	ЗаполнитьОшибкиСуммВСтроке(ДанныеЭтойСтроки);
	ОбновитьВидимостьИСуммыДляОшибкиТЧ(ДанныеЭтойСтроки, ИмяРеквизита);
	
	ПараметрыЗаписиРеквизитов = НовыеПараметрыЗаписиСтрокиТаблицыСумм(СтрокиТаблицы, ИмяРеквизита,
		ДанныеЭтойСтроки[ИмяРеквизита]);
	ЗаписатьНовоеЗначениеРеквизитаТаблицы(ПараметрыЗаписиРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаСуммКартинкаОткрытия" Тогда
		ПоказатьЗначение(, Элемент.ТекущиеДанные.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммПриАктивизацииЯчейки(Элемент)
	ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиСумм", 0.1, Истина);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммПередНачаломИзменения(Элемент, Отказ)
	
	Если ТекущаяСтрокаДляСпискаВыбора <> Элементы.ТаблицаСумм.ТекущаяСтрока
		Или ТекущееИмяРеквизита <> СтрЗаменить(Элементы.ТаблицаСумм.ТекущийЭлемент.Имя, "ТаблицаСумм", "") Тогда
		
		ОтключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиСумм");
		Подключаемый_ПриАктивизацииЯчейкиСумм();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ПослеЗаполненияСтрокСумм();
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммСтатьяНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТаблицаСуммСтатьиДоходовРасходовНачалоВыбора(Элементы.ТаблицаСумм.ТекущиеДанные.Статья, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммСтатьяДоходовНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТаблицаСуммСтатьиДоходовРасходовНачалоВыбора(Элементы.ТаблицаСумм.ТекущиеДанные.СтатьяДоходов, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаСуммСтатьяРасходовНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТаблицаСуммСтатьиДоходовРасходовНачалоВыбора(Элементы.ТаблицаСумм.ТекущиеДанные.СтатьяРасходов, Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИтого

&НаКлиенте
Процедура ТаблицаИтогоОдиночныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДокументИзСтроки(Элемент.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИтогоКомплектыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДокументИзСтроки(Элемент.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИтогоПрикрепитьСканыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДокументИзСтроки(Элемент.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаИтогоБезОбработкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьДокументИзСтроки(Элемент.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьПодсказку(Команда)
	
	Элементы.ГруппаПодсказки.Видимость = Не Элементы.ГруппаПодсказки.Видимость;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьПодсказкуНажатие(Элемент)
	
	ЗакрытьПодсказкуНажатиеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗакрытьПодсказкуНажатиеНаСервере()
	
	Элементы.ГруппаПодсказки.Видимость = Ложь;
	ВидимостьПодсказок.Вставить(ТекущийШаг, Ложь);
	СохранитьВидимостьПодсказок();
	
КонецПроцедуры

&НаКлиенте
Процедура Далее(Команда)
	ПерейтиНаШаг(Истина);
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	ПерейтиНаШаг(Ложь);
КонецПроцедуры

#Область ОбработчикиКомандФормыШапки

&НаСервере
Процедура СкрытьЗаполненнуюНоменклатуруНаСервере(Скрыть)
	
	Для Каждого ЭлементОформления Из УсловноеОформление.Элементы Цикл
		Если ЭлементОформления.Представление = "ВидимостьЗаполненнойНоменклатуры" Тогда
			ЭлементОформления.Использование = Скрыть;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ШапкаУстановитьВВыделенные(Команда)
	
	ВыделенныеСтроки = Элементы.РеквизитыШапки.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед заполнением реквизитов необходимо выделить нужные строки в таблице.';
									|en = 'Before filling in the attributes, select the required rows in the table.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеЭтойСтроки = Элементы.РеквизитыШапки.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если ОтметкаВШапкеДатаДокумента Тогда
			ДанныеЭтойСтроки.ДатаДокумента = ВШапкеДатаДокумента;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				"ДатаДокумента",
				ВШапкеДатаДокумента
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		Если ОтметкаВШапкеКонтрагент Тогда
			ДанныеЭтойСтроки.Контрагент = ВШапкеКонтрагент;
			ИмяРеквизита = ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, "Контрагент");
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				ИмяРеквизита,
				ВШапкеКонтрагент
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		Если ОтметкаВШапкеОрганизация Тогда
			ДанныеЭтойСтроки.Организация = ВШапкеОрганизация;
			ИмяРеквизита = ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, "Организация");
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				ИмяРеквизита,
				ВШапкеОрганизация
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		
		ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(ДанныеЭтойСтроки);
	КонецЦикла;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	
	ОтметкаВШапкеДатаДокумента = Ложь;
	ОтметкаВШапкеКонтрагент = Ложь;
	ОтметкаВШапкеОрганизация = Ложь;
	Элементы.ГруппаЗаполнениеШапки.Скрыть();
	ПослеЗаполненияСтрокШапки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗаполнениеШапки(Команда)
	
	Элементы.ГруппаЗаполнениеШапки.Показать();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормыКомплекты

&НаКлиенте
Процедура ПерепроверитьКомплекты(Команда)
	ПерепроверитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьКомплект(Команда)
	
	ИмяТаблицы = СтрЗаменить(Команда.Имя, "УдалитьКомплект", "");
	НомерКомплекта = Число(СтрЗаменить(ИмяТаблицы, "Комплект", ""));
	
	СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	Если СтрокиКомплекта.Количество() = 0 Тогда
		УдалитьКомплектНаСервере(НомерКомплекта, Ложь, Истина);
	Иначе
		ДополнительныеПараметры = Новый Структура("НомерКомплекта", НомерКомплекта);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВопросаУдаленияКомплекта", ЭтотОбъект, ДополнительныеПараметры);
		
		ТекстВопроса = НСтр("ru = 'Удалить комплект?';
							|en = 'Delete the kit?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопросаУдаленияКомплекта(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		УдалитьКомплектНаСервере(ДополнительныеПараметры.НомерКомплекта, Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыКомплекта(Команда)
	
	Позиция_ = СтрНайти(Команда.Имя, "_", НаправлениеПоиска.СКонца);
	ИмяТаблицы = Сред(Команда.Имя, Позиция_ + 1);
	
	СтрокаПоступленияРеализации = Сред(Команда.Имя, 1, Позиция_ - 1);
	СтрокаПоступленияРеализации = СтрЗаменить(СтрокаПоступленияРеализации, "СчетНаОплату", "");
	СтрокаПоступленияРеализации = СтрЗаменить(СтрокаПоступленияРеализации, "СчетФактура", "");
	
	ПараметрыОперации = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперации(СтрокаПоступленияРеализации);
	ДанныеОбработки = РаспознаваниеДокументовКомплектыКлиентСервер.НовыеДанныеОбработкиКомплектов();
	ДанныеОбработки.РезультатОбратнойСвязи = РезультатОбратнойСвязи;
	ОткрытиеФормыДокумента(ЭтотОбъект, ПараметрыОперации, ИмяТаблицы, ДанныеОбработки);
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьЛишниеИзКомплекта(Команда)
	ИмяТаблицы = СтрЗаменить(Команда.Имя, "УбратьЛишниеИзКомплекта", "");
	УбратьЛишниеИзКомплектаНаСервере(ИмяТаблицы);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормыДопРеквизитов

&НаКлиенте
Процедура СоздатьДоговоры(Команда)
	
	ВыделенныеСтроки = Элементы.ТаблицаДопРеквизитов.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед созданием договоров необходимо выделить нужные строки в таблице.';
									|en = 'Before creating contracts, select the required rows in the table.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = "Договор";
	
	ПараметрыСоздания = Новый Структура("ИдСтрокДокументов, РаспознанныеДокументы, ДополнительныеДанные, ИмяРеквизитаШапки",
		Новый Массив, Новый Массив, Новый Массив, ИмяРеквизита);
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеВыделеннойСтроки = Элементы.ТаблицаДопРеквизитов.ДанныеСтроки(ВыделеннаяСтрока);
		Если Не ЗначениеЗаполнено(ДанныеВыделеннойСтроки[ИмяРеквизита]) Тогда
			ДанныеСтроки = Новый Структура("ТипДокумента, Направление", ДанныеВыделеннойСтроки.ТипДокумента, ДанныеВыделеннойСтроки.Направление);
			
			// Добавляем одновременно, поэтому индексы массивов будут совпадать
			ПараметрыСоздания.ИдСтрокДокументов.Добавить(ВыделеннаяСтрока);
			ПараметрыСоздания.РаспознанныеДокументы.Добавить(ДанныеВыделеннойСтроки.Ссылка);
			ПараметрыСоздания.ДополнительныеДанные.Добавить(ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	СоздатьДоговорыПоПараметрам(ПараметрыСоздания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьЗаполнениеДопРеквизитов(Команда)
	
	Элементы.ГруппаЗаполнениеДопРеквизитов.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыУстановитьВВыделенные(Команда)
	
	ВыделенныеСтроки = Элементы.ТаблицаДопРеквизитов.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед заполнением реквизитов необходимо выделить нужные строки в таблице.';
									|en = 'Before filling in the attributes, select the required rows in the table.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СообщатьОбОграниченииРедактированияМетодаИсчисленияНДС = Ложь;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеЭтойСтроки = Элементы.ТаблицаДопРеквизитов.ДанныеСтроки(ВыделеннаяСтрока);
		
		Если ОтметкаДопРеквизитыСклад Тогда
			ДанныеЭтойСтроки.Склад = ДопРеквизитыСклад;
			СтрокаИзменений = Новый Структура;
			СтрокаИзменений.Вставить("Ссылка", ДанныеЭтойСтроки.Ссылка);
			СтрокаИзменений.Вставить("ИмяРеквизита", "Склад");
			СтрокаИзменений.Вставить("Значение", ДопРеквизитыСклад);
			СтрокаИзменений.Вставить("ТипДокумента", ДанныеЭтойСтроки.ТипДокумента);
			СтрокаИзменений.Вставить("ИдентификаторСтроки", ВыделеннаяСтрока);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		
		Если ОтметкаДопРеквизитыСуммаВключаетНДС Тогда
			
			РедактированиеМетодаИсчисленияНДСРазрешено = (Не ДанныеЭтойСтроки.ТипДокумента 
				= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД")
				И Не ДанныеЭтойСтроки.ТипДокумента
				= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12")
				И Не ДанныеЭтойСтроки.ТипДокумента
				= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура"));
			
			Если РедактированиеМетодаИсчисленияНДСРазрешено Тогда
				ДанныеЭтойСтроки.СуммаВключаетНДС = ДопРеквизитыСуммаВключаетНДС;
				СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				"СуммаВключаетНДС",
				ДопРеквизитыСуммаВключаетНДС);
				ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
			Иначе
				СообщатьОбОграниченииРедактированияМетодаИсчисленияНДС = Истина;
			КонецЕсли;
				
		КонецЕсли;
		
		Если ОтметкаДопРеквизитыСрокОплаты
			И ДанныеЭтойСтроки.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату")
			Тогда
			ДанныеЭтойСтроки.СрокОплаты = ДопРеквизитыСрокОплаты;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				"СрокОплаты",
				ДопРеквизитыСрокОплаты
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		Если ОтметкаДопРеквизитыПродавецОрганизация
			И ДанныеЭтойСтроки.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату")
			И ДанныеЭтойСтроки.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий")
			Тогда
			ДанныеЭтойСтроки.ПродавецОрганизация = ДопРеквизитыПродавецОрганизация;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				"ПродавецОрганизация",
				ДопРеквизитыПродавецОрганизация
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		Если ОтметкаДопРеквизитыВидСкидки
			И ДанныеЭтойСтроки.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату")
			Тогда
			ДанныеЭтойСтроки.ВидСкидки = ДопРеквизитыВидСкидки;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				ДанныеЭтойСтроки.Ссылка,
				"ВидСкидки",
				ДопРеквизитыВидСкидки
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		
		ДанныеЭтойСтроки.ДанныеЗаполнены = ПроверкаДанныеЗаполненыДопРеквизиты(ДанныеЭтойСтроки);
	КонецЦикла;
	
	ОчиститьСообщения();
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	
	ОтметкаДопРеквизитыСклад = Ложь;
	ОтметкаДопРеквизитыСуммаВключаетНДС = Ложь;
	ОтметкаДопРеквизитыСрокОплаты = Ложь;
	ОтметкаДопРеквизитыПродавецОрганизация = Ложь;
	ОтметкаДопРеквизитыВидСкидки = Ложь;
	Элементы.ГруппаЗаполнениеДопРеквизитов.Скрыть();
	ПослеЗаполненияСтрокДопРеквизитов();
	
	Если СообщатьОбОграниченииРедактированияМетодаИсчисленияНДС Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Редактирование метода исчисления НДС недоступно для документов с типом: ""УПД"", ""ТОРГ-12"", ""Счет-фактура"".';
				|en = 'Editing the VAT calculation method is not available for documents of the following types: ""UTD"", ""TORG-12"", and ""Tax invoice"".'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормыНоменклатура

&НаКлиенте
Процедура ПоказатьЗаполнениеРеквизитовНоменклатуры(Команда)
	
	Элементы.ГруппаЗаполнениеРеквизитовНоменклатуры.Показать();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВыделеннуюНоменклатуру(Команда)
	
	ИнтерактивноеГрупповоеСозданиеНоменклатуры();
		
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураУстановитьВВыделенные(Команда)
	
	ВыделенныеСтроки = Элементы.ТаблицаНоменклатуры.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед заполнением реквизитов необходимо выделить нужные строки в таблице.';
									|en = 'Before filling in the attributes, select the required rows in the table.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	СтрокиТаблицы = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеВыделеннойСтроки = Элементы.ТаблицаНоменклатуры.ДанныеСтроки(ВыделеннаяСтрока);
		Если ЗначениеЗаполнено(ДанныеВыделеннойСтроки.Номенклатура)
			Или Не ЗначениеЗаполнено(ДанныеВыделеннойСтроки.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеВыделеннойСтроки.Номенклатура = НоменклатураНоменклатура;
		СтрокиТаблицы.Добавить(ВыделеннаяСтрока);
		
		ПроверкаДанныеЗаполненыНоменклатура(ДанныеВыделеннойСтроки);
		
	КонецЦикла;
	
	Элементы.ГруппаЗаполнениеРеквизитовНоменклатуры.Скрыть();
	ПослеЗаполненияСтрокНоменклатуры();
	
	Если СтрокиТаблицы.Количество() > 0 Тогда
		ПараметрыЗаписиРеквизитов = Новый Структура;
		ПараметрыЗаписиРеквизитов.Вставить("ТаблицаФормы", Элементы.ТаблицаНоменклатуры);
		ПараметрыЗаписиРеквизитов.Вставить("ДанныеИзменений", ДанныеФоновыхОпераций.ИзмененияНоменклатуры);
		ПараметрыЗаписиРеквизитов.Вставить("СтрокиТаблицы", СтрокиТаблицы);
		ПараметрыЗаписиРеквизитов.Вставить("ИмяРеквизита", "Номенклатура");
		ПараметрыЗаписиРеквизитов.Вставить("ЗначениеЗаполнения", НоменклатураНоменклатура);
		ПараметрыЗаписиРеквизитов.Вставить("ОбновитьЗначениеВЯчейке", Ложь);
		
		ЗаписатьНовоеЗначениеРеквизитаТаблицы(ПараметрыЗаписиРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормыТаблицыСумм

#КонецОбласти

#Область ОбработчикиКомандФормыИтого

&НаКлиенте
Процедура ОбработатьДокументы(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВопросаСозданияДокументов", ЭтотОбъект);
	
	ТекстВопроса = НСтр("ru = 'Обработать документы?';
						|en = 'Process the documents?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыделитьНезаполненнуюЯчейкуНаШаге()
	
	Если ТекущийШаг = "Шапка" Тогда
		
		ТекущийЭлемент = Элементы.РеквизитыШапки;
		Если Не СкрытьЗаполненныеСтрокиШапки Тогда
			Возврат;
		КонецЕсли;
		
		ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
		Если ДанныеЭтойСтроки <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(ДанныеЭтойСтроки.НомерДокумента) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ШапкаНомерДокумента;
			ИначеЕсли Не ЗначениеЗаполнено(ДанныеЭтойСтроки.ДатаДокумента) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ШапкаДатаДокумента;
			ИначеЕсли Не ЗначениеЗаполнено(ДанныеЭтойСтроки.Контрагент) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ШапкаКонтрагент;
			ИначеЕсли Не ЗначениеЗаполнено(ДанныеЭтойСтроки.Организация) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ШапкаОрганизация;
			Иначе
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ШапкаИтогоВсего;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = "ДопРеквизиты" Тогда
		
		ТекущийЭлемент = Элементы.ТаблицаДопРеквизитов;
		Если Не СкрытьЗаполненныеСтрокиДопРеквизитов Тогда
			Возврат;
		КонецЕсли;
		
		НезаполненнаяСтрока = Неопределено;
		Для Каждого СтрокаТаблицы Из ТаблицаДопРеквизитов Цикл
			Если Не СтрокаТаблицы.ДанныеЗаполнены Тогда
				НезаполненнаяСтрока = СтрокаТаблицы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НезаполненнаяСтрока <> Неопределено Тогда
			ИменаРеквизитов = ОбязательныеРеквизитыТаблицаДопРеквизитов(НезаполненнаяСтрока);
			Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
				Если Не ЗначениеЗаполнено(НезаполненнаяСтрока[ИмяРеквизита]) Тогда
					ТекущийЭлемент.ТекущийЭлемент = Элементы["ТаблицаДопРеквизитов"+ИмяРеквизита];
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = "Суммы" Тогда
		
		ТекущийЭлемент = Элементы.ТаблицаСумм;
		Если Не СкрытьЗаполненныеСтрокиСумм Тогда
			Возврат;
		КонецЕсли;
		
		НезаполненнаяСтрока = Неопределено;
		Для Каждого СтрокаТаблицы Из ТаблицаСумм Цикл
			Если СтрокаТаблицы.ЕстьОшибка Тогда
				НезаполненнаяСтрока = СтрокаТаблицы;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если НезаполненнаяСтрока <> Неопределено Тогда
			Если Не ЗначениеЗаполнено(НезаполненнаяСтрока.Номенклатура) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ТаблицаСуммНоменклатура;
			ИначеЕсли НезаполненнаяСтрока.ОшибкаЦены Или НезаполненнаяСтрока.Количество = 0 Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ТаблицаСуммКоличество;
			ИначеЕсли НезаполненнаяСтрока.Цена = 0 Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ТаблицаСуммЦена;
			ИначеЕсли НезаполненнаяСтрока.ОшибкаВсего Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ТаблицаСуммСумма;
			ИначеЕсли НезаполненнаяСтрока.ОшибкаНДС Или Не ЗначениеЗаполнено(НезаполненнаяСтрока.СтавкаНДС) Тогда
				ТекущийЭлемент.ТекущийЭлемент = Элементы.ТаблицаСуммСтавкаНДС;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОчиститьСообщения()
	
	ОчиститьСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИЗагрузитьПустыеКартинки()
	
	Если Не ВсеСтрокиНаШагеЗаполнены Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущийШаг = "Шапка" Тогда
		Если РеквизитыШапки.Количество() <> 0 И Не СкрытьЗаполненныеСтрокиШапки Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТекущийШаг = "ДопРеквизиты" Тогда
		Если ТаблицаДопРеквизитов.Количество() <> 0 И Не СкрытьЗаполненныеСтрокиДопРеквизитов Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТекущийШаг = "Номенклатура" Тогда
		Если ТаблицаНоменклатуры.ПолучитьЭлементы().Количество() <> 0 И Не СкрытьЗаполненнуюНоменклатуру Тогда
			Возврат;
		КонецЕсли;
	ИначеЕсли ТекущийШаг = "Суммы" Тогда
		Если ТаблицаСумм.Количество() <> 0 И Не СкрытьЗаполненныеСтрокиСумм Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗагрузитьПустыеКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьПустыеКартинки()
	
	КартинкаРеквизита = КартинкаКакТекстHTML("");
	
	ДокументHTML = ДокументHTML(Элементы.ПолеПросмотра);
	Если ДокументHTML = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ДокументHTML.clear_image();
	КартинкаЗагруженаВHTML = Ложь;
	
КонецПроцедуры

&НаКлиенте
Функция ДокументHTML(ЭлементФормы)
	
	ПолеHTML = ЭлементФормы.Документ;
	Если ПолеHTML = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументСкан = Неопределено;
	#Если ВебКлиент Тогда
		ДокументСкан = ПолеHTML.defaultView.app;
	#Иначе
		ДокументСкан = ПолеHTML.app;
	#КонецЕсли
	
	Возврат ДокументСкан;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьВидимостьПодсказок()
	
	СохраненныеНастройки = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(
		"Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.ГрупповаяОбработка", "ВидимостьПодсказок");
	
	Если ТипЗнч(СохраненныеНастройки) = Тип("Структура") Тогда
		ВидимостьПодсказок = СохраненныеНастройки;
	Иначе
		ВидимостьПодсказок = Новый Структура;
		ИменаШагов = ПолучитьИменаШагов();
		Для Каждого Имя Из ИменаШагов Цикл
			ВидимостьПодсказок.Вставить(Имя, Истина);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьВидимостьПодсказок()
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(
		"Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.ГрупповаяОбработка", "ВидимостьПодсказок", ВидимостьПодсказок);
	
КонецПроцедуры

&НаКлиенте
Процедура СменитьВидимостьПолнойКартинки()
	
	Если ПолнаяКартинкаВидна Тогда
		Элементы.ГруппаСтраницыПолнойКартинки.ТекущаяСтраница = Элементы.СтраницаПолнойКартинки;
	Иначе
		Элементы.ГруппаСтраницыПолнойКартинки.ТекущаяСтраница = Элементы.СтраницаБезКартинки;
	КонецЕсли;
	СохранитьВидимостьПолнойКартинки(ПолнаяКартинкаВидна);
	
	Подключаемый_СменитьВидимостьПолнойКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СменитьВидимостьПолнойКартинки() Экспорт
	
	Если ПолнаяКартинкаВидна И Не HTMLДокументСформирован Тогда
		ПодключитьОбработчикОжидания("Подключаемый_СменитьВидимостьПолнойКартинки", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	ЗагрузитьПолнуюКартинку();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьВидимостьПолнойКартинки(ПолнаяКартинкаВидна)
	
	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(
		"Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.ГрупповаяОбработка", "ПолнаяКартинкаВидна", ПолнаяКартинкаВидна);
	
КонецПроцедуры

&НаСервере
Процедура ВыделитьШагТекущейСтраницы()
	
	ПараметрыПодсказки = ПараметрыПодсказкиШага(ТекущийШаг, ВидимостьПодсказок);
	Элементы.ГруппаПодсказки.Видимость = ПараметрыПодсказки.Видимость;
	Элементы.ДекорацияТекстПодсказкиШага.Заголовок = ПараметрыПодсказки.Текст;
	
	Для Каждого ДекорацияФормы Из Элементы.ГруппаШаги.ПодчиненныеЭлементы Цикл
		ИмяШага = СтрЗаменить(ДекорацияФормы.Имя, "КартинкаШаг", "");
		ДекорацияФормы.Видимость = (ИмяШага = ТекущийШаг);
	КонецЦикла;
	
	Элементы.НадписьОшибкаТЧ.Видимость = Ложь;
	Если ТекущийШаг <> "Итого" Тогда
		Элементы.ДекорацияИтогоНеОбработано.Видимость = Ложь;
	КонецЕсли;
	ОбновитьВидимостьНадписиПроЗаполненность();
	
	Если ОбщегоНазначения.ЭтоВебКлиент() Тогда
		
		// Обход проблемы с переходом на вкладках из-за HTML просмотра.
		
		ИмяСтраницы = "Страница" + ТекущийШаг;
		Элементы.СтраницаДляТаблиц.Видимость = Ложь;
		Для Каждого ВерхняяСтраница Из Элементы.СтраницыШаги.ПодчиненныеЭлементы Цикл
			Если ВерхняяСтраница.Имя = "СтраницаДляТаблиц" Тогда
				Для Каждого ПодчиненнаяСтраница Из Элементы.СтраницыТаблиц.ПодчиненныеЭлементы Цикл
					Если ПодчиненнаяСтраница.Имя = ИмяСтраницы Тогда
						ПодчиненнаяСтраница.Видимость = Истина;
						Элементы.СтраницаДляТаблиц.Видимость = Истина;
					Иначе
						ПодчиненнаяСтраница.Видимость = Ложь;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ВерхняяСтраница.Имя = ИмяСтраницы Тогда
					ВерхняяСтраница.Видимость = Истина;
				Иначе
					ВерхняяСтраница.Видимость = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПодсказкиШага(ИмяШага, ВидимостьПодсказок)
	
	Результат = Новый Структура("Видимость, Текст", Истина, "");
	
	Если ВидимостьПодсказок.Свойство(ИмяШага) Тогда
		Результат.Видимость = ВидимостьПодсказок[ИмяШага];
	КонецЕсли;
	
	ШрифтПодсказок = Новый Шрифт(, 11);
	Если ИмяШага = "Шапка" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверьте и заполните основные реквизиты документов. Заполните пустые значения и сопоставьте подсвеченные красным.
			|Создать новых контрагентов и заполнить реквизиты можно сразу для нескольких распознанных документов с помощью действия ""Заполнить для выделенных строк"".';
			|en = 'Check and fill in the main document attributes. Fill in the blank values and match the values highlighted in red.
			|You can create new counterparties and fill in attributes for multiple recognized documents at once by clicking Fill for the selected lines.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "Дубли" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Посмотрите документы в каждой группе и выберите экземпляры с наилучшим качеством в каждой группе. Остальные дубли в данной группе будут помечены обработанными.
			|Если документ в группе не является дублем, нажмите ""Убрать из дублей"".
			|Удалите всю группу дублей, если она была сформирована ошибочно.';
			|en = 'View the documents in each group and select the best quality copies. The remaining duplicates in the group will be marked as processed.
			|If a document in the group is not a duplicate, click Remove from duplicates.
			|Delete the entire duplicate group if it was created by error.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "Комплекты" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Комплект документов – это группа документов, отражающих одну хозяйственную операцию.
			|Объедините в комплект документы, которые относятся к одной операции, для сокращения числа правок номенклатуры и других реквизитов на следующих шагах группового ввода документов.
			|Учетные документы могут быть созданы для каждого отдельного комплекта, либо для всех сразу на последнем шаге обработки.';
			|en = 'A document kit is a group of documents recording one business transaction.
			|Merge the documents that belong to one transaction into a kit to reduce the number of changes to items and other attributes at the next steps of bulk document input.
			|You can create accounting documents either for each kit or for all kits at once at the last step.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "ДопРеквизиты" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверьте и заполните дополнительные реквизиты документов. Заполните пустые значения и сопоставьте подсвеченные красным.
			|Заполнить реквизиты можно сразу для нескольких распознанных документов с помощью действия ""Заполнить для выделенных строк"".
			|Серые поля в таблице недоступны для редактирования.';
			|en = 'Check and fill in the additional document attributes. Fill in blank values and match the values highlighted in red.
			|You can fill in the attributes for multiple recognized documents at once by clicking Fill for the selected lines.
			|Gray fields in the table are not editable.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "Номенклатура" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверьте и заполните номенклатуру. Заполняйте одинаковые значения номенклатуры сразу для нескольких выделенных строк.
			|По кнопке ""Создать выделенные"" можно быстро добавить новую номенклатуру для строк, где нет сопоставления с номенклатурой из базы.';
			|en = 'Check and fill in the items. Fill in the same attribute values for several selected lines at once.
			|Click Create selected items to quickly add a new item for lines that are not mapped to items from the database.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "Суммы" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверьте и заполните суммы из таблиц документов, перепроверьте выделенные красным значения.';
				|en = 'Check and fill in the amounts from the document tables, check the values ​​highlighted in red again.'"),
			ШрифтПодсказок
		);
	ИначеЕсли ИмяШага = "Итого" Тогда
		Результат.Текст = Новый ФорматированнаяСтрока(
			НСтр("ru = 'Проверьте изменения, которые будут внесены в базу данных. При необходимости возможно вернуться на предыдущие шаги обработки.';
				|en = 'Check the changes that will be made to the database. If required, you can return to the previous steps.'"),
			ШрифтПодсказок
		);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИменаШагов()
	
	ИменаШагов = Новый Массив;
	ИменаШагов.Добавить("Шапка");
	ИменаШагов.Добавить("Дубли");
	ИменаШагов.Добавить("Комплекты");
	ИменаШагов.Добавить("ДопРеквизиты");
	ИменаШагов.Добавить("Номенклатура");
	ИменаШагов.Добавить("Суммы");
	ИменаШагов.Добавить("Итого");
	
	Возврат ИменаШагов;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиНаШаг(Вперед)
	
	ИменаШагов = ПолучитьИменаШагов();
	ИдШага = ИменаШагов.Найти(ТекущийШаг);
	Если Вперед Тогда
		Направление = "Вперед";
		ИдШага = ИдШага + 1;
	Иначе
		Направление = "Назад";
		ИдШага = ИдШага - 1;
	КонецЕсли;
	
	ИмяШага = ИменаШагов[ИдШага];
	
	КлючеваяОперация = 
		СтрШаблон("ГрупповаяОбработкаРаспознанныхДокументов.Форма.ГрупповаяОбработка.Навигация.%1.%2", ИмяШага, Направление);
	ОценкаПроизводительностиКлиент.ЗамерВремени(КлючеваяОперация);
	
	ПерейтиНаСтраницу(ИмяШага, Вперед);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницу(ИмяШага, Вперед)
	
	ТекущаяСтрокаДляСпискаВыбора = Неопределено;
	ТекущееИмяРеквизита = Неопределено;
	Если Вперед Тогда
		Если ИмяШага = "Шапка" Тогда
			
			ТекущийШаг = ИмяШага;
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаШапка;
			ВыделитьШагТекущейСтраницы();
			#Если ВебКлиент Тогда
				ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиШапки", 0.1, Истина);
			#КонецЕсли
			
		ИначеЕсли ИмяШага = "Дубли" Тогда
			
			КоличествоНеЗаполнено = ПодсчетНезаполненныхСтрокШапки();
			Оповещение = Новый ОписаниеОповещения("ПерейтиНаСтраницуДублиЗавершение", ЭтотОбъект);
			Если КоличествоНеЗаполнено > 0 Тогда
				
				ЖирныйШрифт = Новый Шрифт(, , Истина);
				
				Если КоличествоНеЗаполнено = 1 Тогда
					ТекстДокумент = НСтр("ru = ' распознанном документе ';
										|en = ' recognized document'");
				Иначе
					ТекстДокумент = НСтр("ru = ' распознанных документах ';
										|en = ' recognized documents'");
				КонецЕсли;
				
				ТекстВопроса = Новый ФорматированнаяСтрока(НСтр("ru = 'В ';
																|en = 'To'"),
					Новый ФорматированнаяСтрока(Строка(КоличествоНеЗаполнено), ЖирныйШрифт),
					ТекстДокумент + НСтр("ru = 'есть незаполненные поля, поэтому они не будут обработаны. Продолжить?';
										|en = 'there are blank fields, so they will not be processed. Continue?'")
				);
				
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Иначе
				ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
			КонецЕсли;
			
		ИначеЕсли ИмяШага = "Комплекты" Тогда
			
			КоличествоНеЗаполнено = ПодсчетНезаполненныхСтрокДублей();
			ПараметрыОповещения = Новый Структура("ИмяШага", ИмяШага);
			Оповещение = Новый ОписаниеОповещения("ПерейтиНаСтраницуКомплектыЗавершение", ЭтотОбъект, ПараметрыОповещения);
			Если КоличествоНеЗаполнено > 0 Тогда
				
				ЖирныйШрифт = Новый Шрифт(, , Истина);
				
				Если КоличествоНеЗаполнено = 1 Тогда
					ТекстДубль = НСтр("ru = ' дубля ';
										|en = ' duplicate'");
				Иначе
					ТекстДубль = НСтр("ru = ' дублей ';
										|en = ' duplicates'");
				КонецЕсли;
				
				ТекстВопроса = Новый ФорматированнаяСтрока(НСтр("ru = 'Для ';
																|en = 'For'"),
					Новый ФорматированнаяСтрока(Строка(КоличествоНеЗаполнено), ЖирныйШрифт),
					ТекстДубль + НСтр("ru = 'не выбран основной экземпляр. Их можно выбрать автоматически. Продолжить?';
										|en = 'no main instance is selected. It can be selected automatically. Continue?'")
				);
				
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
			Иначе
				ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
			КонецЕсли;
			
		ИначеЕсли ИмяШага = "ДопРеквизиты" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаДопРеквизиты;
			
			ПерейтиНаСтраницуДопРеквизиты();
		ИначеЕсли ИмяШага = "Номенклатура" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаНоменклатура;
			
			ПерейтиНаСтраницуНоменклатура(ИмяШага);
		ИначеЕсли ИмяШага = "Суммы" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаСуммы;
			
			ПерейтиНаСтраницуСумм();
		ИначеЕсли ИмяШага = "Итого" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаИтого;
			
			ПерейтиНаСтраницуИтого();
		КонецЕсли;
		
	Иначе
		
		// при шаге назад с последней страницы сделать видимость у кнопки Далее
		ТекущийШаг = ИмяШага;
		Если ИмяШага = "Шапка" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаШапка;
			Элементы.КнопкаНазад.Видимость = Ложь;
			#Если ВебКлиент Тогда
				ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиШапки", 0.1, Истина);
			#КонецЕсли
		ИначеЕсли ИмяШага = "Дубли" Тогда
			ТекущаяСтрокаДублей = -1;
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДубли;
			ПерейтиНаСтраницуДублиНаСервере();
		ИначеЕсли ИмяШага = "Комплекты" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаКомплекты;
		ИначеЕсли ИмяШага = "ДопРеквизиты" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаДопРеквизиты;
			
			ПерейтиНаСтраницуДопРеквизиты();
		ИначеЕсли ИмяШага = "Номенклатура" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаНоменклатура;
			
			ПерейтиНаСтраницуНоменклатура(ИмяШага);
		ИначеЕсли ИмяШага = "Суммы" Тогда
			Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц;
			Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаСуммы;
			
			ПерейтиНаСтраницуСумм();
		КонецЕсли;
		
		ВыделитьШагТекущейСтраницы();
	КонецЕсли;
	
	ПроверитьИЗагрузитьПустыеКартинки();
	ПодключитьОбработчикОжидания("Подключаемый_ОчиститьСообщения", 0.1, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьНадписиПроЗаполненность()
	
	Если Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаКомплекты Тогда
		// Для этой страницы плашка выводится 1 раз и показывается до изменения комплектов
		Возврат;
	КонецЕсли;
	
	ВсеСтрокиНаШагеЗаполнены = Ложь;
	ТекстНадписи = "";
	Если Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДляТаблиц Тогда
		Если ТекущийШаг = "Шапка" Тогда
			ВсеСтрокиНаШагеЗаполнены = Истина;
			ТекстНадписи = НСтр("ru = 'Все основные реквизиты заполнены корректно. Нажмите кнопку ""Далее""';
								|en = 'All the main attributes are filled in correctly. Click Next'");
			Для Каждого СтрокаТаблицы Из РеквизитыШапки Цикл
				Если Не СтрокаТаблицы.ДанныеЗаполнены Тогда
					ВсеСтрокиНаШагеЗаполнены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТекущийШаг = "ДопРеквизиты" Тогда
			ВсеСтрокиНаШагеЗаполнены = Истина;
			ТекстНадписи = НСтр("ru = 'Все дополнительные реквизиты заполнены корректно. Нажмите кнопку ""Далее""';
								|en = 'All the additional attributes are filled in correctly. Click Next'");
			Для Каждого СтрокаТаблицы Из ТаблицаДопРеквизитов Цикл
				Если Не СтрокаТаблицы.ДанныеЗаполнены Тогда
					ВсеСтрокиНаШагеЗаполнены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТекущийШаг = "Номенклатура" Тогда
			ВсеСтрокиНаШагеЗаполнены = Истина;
			ТекстНадписи = НСтр("ru = 'Вся номенклатура заполнена корректно. Нажмите кнопку ""Далее""';
								|en = 'All items are filled in correctly. Click Next'");
			Для Каждого СтрокаСКонтрагентом Из ТаблицаНоменклатуры.ПолучитьЭлементы() Цикл
				Если Не СтрокаСКонтрагентом.ЕстьНоменклатура Тогда
					ВсеСтрокиНаШагеЗаполнены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТекущийШаг = "Суммы" Тогда
			ВсеСтрокиНаШагеЗаполнены = Истина;
			ТекстНадписи = НСтр("ru = 'Все данные таблиц документов заполнены корректно. Нажмите кнопку ""Далее""';
								|en = 'All document table data is filled in correctly. Click Next'");
			Для Каждого СтрокаТаблицы Из ТаблицаСумм Цикл
				Если СтрокаТаблицы.ЕстьОшибка Тогда
					ВсеСтрокиНаШагеЗаполнены = Ложь;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли ТекущийШаг = "Дубли" Тогда
		
		ВсеСтрокиНаШагеЗаполнены = Ложь;
		ТекстНадписи = НСтр("ru = 'Все дубли проверены. Нажмите кнопку ""Далее""';
							|en = 'All duplicates are checked. Click Next'");
		Для Каждого СтрокаТаблицы Из ТаблицаДублей Цикл
			Если Не ВсеСтрокиНаШагеЗаполнены Тогда
				ВсеСтрокиНаШагеЗаполнены = Истина;
			КонецЕсли;
			Если Не СтрокаТаблицы.ЕстьОсновной Тогда
				ВсеСтрокиНаШагеЗаполнены = Ложь;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	// для шагов "Комплекты" и "Итого" не нужно
	
	НадписьВсеЗаполнено = ТекстНадписи;
	Элементы.НадписьВсеЗаполнено.Видимость = ВсеСтрокиНаШагеЗаполнены;
	
КонецПроцедуры

#Область СлужебныеПроцедурыШапки

#Область МассовоеСозданиеДляШапки

&НаКлиенте
Процедура СоздатьДоговорыПоПараметрам(ПараметрыСоздания)
	
	ДанныеСоздания = ДанныеДляСозданияРеквизитовПоПараметрам(ПараметрыСоздания);
	
	ПараметрыСоздания = Новый Структура;
	ПараметрыСоздания.Вставить("ДанныеСоздания", ДанныеСоздания);
	ОткрытьФорму("Обработка.ГрупповаяОбработкаРаспознанныхДокументов.Форма.СозданиеДоговоров", ПараметрыСоздания, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеКонтрагентов(ПараметрыСоздания)
	
	ТекущийИдСоздания = 0;
	ПараметрыСоздания.Вставить("ИдСтрокиСоздания", ТекущийИдСоздания);
	КоличествоСозданий = ПараметрыСоздания.ДанныеСоздания.Количество();
	
	РезультатСоздания = СоздатьОставшихсяКонтрагентовНаСервере(ПараметрыСоздания);
	
	ОчиститьСообщения();
	Для Каждого НовоеСообщение Из РезультатСоздания.Сообщения Цикл
		НовоеСообщение.Сообщить();
	КонецЦикла;
	
	Пока КоличествоСозданий > ТекущийИдСоздания Цикл
		
		ДанныеКонтрагента = ПараметрыСоздания.ДанныеСоздания[ТекущийИдСоздания];
		КонтрагентСсылка = РезультатСоздания.Контрагенты[ТекущийИдСоздания];
		
		Для Каждого ИдСтроки Из ДанныеКонтрагента.ИдСтрокДокументов Цикл
			СтрокаТаблицы = РеквизитыШапки.НайтиПоИдентификатору(ИдСтроки);
			СтрокаТаблицы.Контрагент = КонтрагентСсылка;
			СтрокаТаблицы.ДанныеЗаполнены = ПроверкаДанныеЗаполненыШапка(СтрокаТаблицы);
			
			ИменаРеквизитов = РаспознаваниеДокументовСлужебныйКлиентСервер.ИменаРеквизитовКонтрагентИОрганизация(СтрокаТаблицы);
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				СтрокаТаблицы.Ссылка,
				ИменаРеквизитов.Контрагент,
				КонтрагентСсылка
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЦикла;
		
		ТекущийИдСоздания = ТекущийИдСоздания + 1;
	КонецЦикла;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	ПослеЗаполненияСтрокШапки();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеДоговоров(ПараметрыСоздания)
	
	ОчиститьСообщения();
	ТекущийИдСоздания = 0;
	ПараметрыСоздания.Вставить("ИдСтрокиСоздания", ТекущийИдСоздания);
	КоличествоСозданий = ПараметрыСоздания.ДанныеСоздания.Количество();
	
	РезультатСоздания = СоздатьОставшиесяДоговорыНаСервере(ПараметрыСоздания);
	
	Для Каждого НовоеСообщение Из РезультатСоздания.Сообщения Цикл
		НовоеСообщение.Сообщить();
	КонецЦикла;
	
	Пока КоличествоСозданий > ТекущийИдСоздания Цикл
		
		ДанныеДоговора = ПараметрыСоздания.ДанныеСоздания[ТекущийИдСоздания];
		ДоговорСсылка = РезультатСоздания.Договоры[ТекущийИдСоздания];
		
		Для Каждого ИдСтроки Из ДанныеДоговора.ИдСтрокДокументов Цикл
			СтрокаТаблицы = ТаблицаДопРеквизитов.НайтиПоИдентификатору(ИдСтроки);
			СтрокаТаблицы.Договор = ДоговорСсылка;
			СтрокаТаблицы.ДанныеЗаполнены = ПроверкаДанныеЗаполненыДопРеквизиты(СтрокаТаблицы);
			
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				СтрокаТаблицы.Ссылка,
				"Договор",
				ДоговорСсылка
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЦикла;
		
		ТекущийИдСоздания = ТекущийИдСоздания + 1;
	КонецЦикла;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	ПослеЗаполненияСтрокДопРеквизитов();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеДляСозданияРеквизитовПоПараметрам(ПараметрыСоздания)
	
	ТаблицаОбхода = Новый ТаблицаЗначений;
	ТаблицаОбхода.Колонки.Добавить("Ссылка");
	ТаблицаОбхода.Колонки.Добавить("Реквизит", Новый ОписаниеТипов("Строка"));
	ТаблицаОбхода.Колонки.Добавить("ИмяРеквизита", Новый ОписаниеТипов("Строка"));
	
	МаксИдДокумента = ПараметрыСоздания.РаспознанныеДокументы.Количество() - 1;
	Ид = -1;
	Пока Ид < МаксИдДокумента Цикл
		Ид = Ид + 1;
		
		ДанныеДокумента = ПараметрыСоздания.ДополнительныеДанные[Ид];
		
		Если ПараметрыСоздания.ИмяРеквизитаШапки = "Контрагент" Тогда
			ИменаРеквизитов = РаспознаваниеДокументовСлужебныйКлиентСервер.ИменаРеквизитовКонтрагентИОрганизация(ДанныеДокумента);
			ИмяОсновногоРеквизита = ИменаРеквизитов[ПараметрыСоздания.ИмяРеквизитаШапки];
		Иначе
			ИмяОсновногоРеквизита = ПараметрыСоздания.ИмяРеквизитаШапки;
		КонецЕсли;
		
		СвязанныеКолонки = РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента(
			ИмяОсновногоРеквизита,
			ДанныеДокумента.ТипДокумента,
			ДанныеДокумента.Направление
		);
		
		Для Каждого ПараметрыКолонки Из СвязанныеКолонки Цикл
			Если Не ПараметрыКолонки.Свойство("ИмяРеквизита") Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаОбхода = ТаблицаОбхода.Добавить();
			СтрокаОбхода.Ссылка = ПараметрыСоздания.РаспознанныеДокументы[Ид];
			СтрокаОбхода.Реквизит = ПараметрыКолонки.Реквизит;
			СтрокаОбхода.ИмяРеквизита = ПараметрыКолонки.ИмяРеквизита;
		КонецЦикла;
	КонецЦикла;
	
	Если ТаблицаОбхода.Количество() = 0 Тогда
		Возврат Новый Массив;
	КонецЕсли;
	
	ТаблицаОбхода.Сортировать("Реквизит, ИмяРеквизита");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", ПараметрыСоздания.РаспознанныеДокументы);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Ссылка КАК Ссылка,
	|	РаспознанныйДокумент.Направление КАК Направление,
	|	РаспознанныйДокумент.ИдентификаторРезультата КАК ИдентификаторРезультата
	|ПОМЕСТИТЬ ОсновныеДанные
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка В(&РаспознанныеДокументы)
	|
	|";
	
	ИтоговаяЧастьЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеДанные.Ссылка КАК Ссылка,
	|	ОсновныеДанные.Направление КАК Направление,
	|	ОсновныеДанные.ИдентификаторРезультата КАК ИдентификаторРезультата,
	|	&РаспознанныйТекстШаблон
	|ИЗ
	|	ОсновныеДанные КАК ОсновныеДанные";
	
	ЭтотРеквизит = "";
	ЭтоИмяРеквизита = "";
	Ид = 0;
	
	ОбъединитьЗапросы = Ложь;
	
	ПоследняяСтрокаПройдена = Ложь;
	КоличествоСтрок = ТаблицаОбхода.Количество();
	ИдСтроки = -1;
	Пока ИдСтроки < КоличествоСтрок Цикл
		ИдСтроки = ИдСтроки + 1;
		Если ИдСтроки = КоличествоСтрок Тогда
			ПоследняяСтрокаПройдена = Истина;
		Иначе
			СтрокаОбхода = ТаблицаОбхода[ИдСтроки];
		КонецЕсли;
		
		Если ПоследняяСтрокаПройдена
			Или ЭтотРеквизит <> СтрокаОбхода.Реквизит
			Или ЭтоИмяРеквизита <> СтрокаОбхода.ИмяРеквизита Тогда
			
			Если ЭтотРеквизит = "" Тогда
				ПодходящиеСсылки = Новый Массив;
			Иначе
				
				ШаблонЗапроса = 
				"ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	РаспознанныйДокументРеквизитыДокумента.Ссылка КАК Ссылка,
				|	РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита КАК ИмяРеквизита,
				|	РаспознанныйДокументРеквизитыДокумента.Значение КАК Значение,
				|	РаспознанныйДокументРеквизитыДокумента.РаспознанныйТекст КАК РаспознанныйТекст
				|ПОМЕСТИТЬ ДанныеШаблон
				|ИЗ
				|	Документ.РаспознанныйДокумент.РеквизитыДокумента КАК РаспознанныйДокументРеквизитыДокумента
				|ГДЕ
				|	РаспознанныйДокументРеквизитыДокумента.Ссылка В (&СсылкиШаблон)
				|	И РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита = &ИмяРеквизитаШаблон
				|
				|";
				
				СсылкиШаблон = "Ссылки" + XMLСтрока(Ид);
				ДанныеШаблон = "Данные" + ЭтотРеквизит;
				Запрос.УстановитьПараметр(СсылкиШаблон, ПодходящиеСсылки);
				
				ЧастьЗапроса = СтрЗаменить(ШаблонЗапроса, "СсылкиШаблон", СсылкиШаблон);
				ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "&ИмяРеквизитаШаблон", """"+ЭтоИмяРеквизита+"""");
				
				Если ОбъединитьЗапросы Тогда
					ЧастьЗапроса = "ОБЪЕДИНИТЬ ВСЕ
					|
					|" + ЧастьЗапроса;
					ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, " РАЗРЕШЕННЫЕ", "");
					ЧастьЗапроса = СтрЗаменить(ЧастьЗапроса, "ПОМЕСТИТЬ ДанныеШаблон", "");
				Иначе
					ЧастьЗапроса = ";
					|"
					+ СтрЗаменить(ЧастьЗапроса, "ДанныеШаблон", ДанныеШаблон);
					
					ИтоговаяЧастьЗапроса = СтрЗаменить(ИтоговаяЧастьЗапроса, "&РаспознанныйТекстШаблон",
					ДанныеШаблон+".Значение КАК "+ЭтотРеквизит+"Значение,
					|	"+ДанныеШаблон+".РаспознанныйТекст КАК "+ЭтотРеквизит+",
					|	&РаспознанныйТекстШаблон")
					+
					"
					|		ЛЕВОЕ СОЕДИНЕНИЕ "+ДанныеШаблон+" КАК "+ДанныеШаблон+"
					|		ПО ОсновныеДанные.Ссылка = "+ДанныеШаблон+".Ссылка";
				КонецЕсли;
				
				Запрос.Текст = Запрос.Текст + ЧастьЗапроса;
				Если ПоследняяСтрокаПройдена Тогда
					Прервать;
				КонецЕсли;
				
				ОбъединитьЗапросы = (ЭтотРеквизит = СтрокаОбхода.Реквизит); // Следующий Запрос по такому же реквизиту (Название, ИНН, КПП)
				
				Ид = Ид + 1;
				ПодходящиеСсылки = Новый Массив;
			КонецЕсли;
			
			ЭтотРеквизит = СтрокаОбхода.Реквизит;
			ЭтоИмяРеквизита = СтрокаОбхода.ИмяРеквизита;
		КонецЕсли;
		
		ПодходящиеСсылки.Добавить(СтрокаОбхода.Ссылка);
		
	КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + ";
	|"
	+ СтрЗаменить(
		ИтоговаяЧастьЗапроса,
		",
		|	&РаспознанныйТекстШаблон",
		""
	);
	
	Если ПараметрыСоздания.ИмяРеквизитаШапки = "Контрагент" Тогда
		ИтоговаяТаблица = ТаблицаРеквизитовДляКонтрагентов(ПараметрыСоздания, Запрос);
	ИначеЕсли ПараметрыСоздания.ИмяРеквизитаШапки = "Договор" Тогда
		ИтоговаяТаблица = ТаблицаРеквизитовДляДоговоров(ПараметрыСоздания, Запрос);
	Иначе
		Возврат Новый Массив;
	КонецЕсли;
	
	Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(ИтоговаяТаблица);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТаблицаРеквизитовДляКонтрагентов(ПараметрыСоздания, Запрос)
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИтоговаяТаблица = Новый ТаблицаЗначений;
	ИтоговаяТаблица.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("ИНН", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("КПП", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("ИдентификаторРезультата", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("ЮридическоеФизическоеЛицо");
	ИтоговаяТаблица.Колонки.Добавить("РаспознанныеДокументы");
	ИтоговаяТаблица.Колонки.Добавить("ИдСтрокДокументов");
	
	// Соответствие для избавления от дублей
	// Ключ - Наименование или ИНН+КПП, значение - Ид строки из ИтоговаяТаблица
	КлючиНазваний = Новый Соответствие;
	КлючиИННКПП = Новый Соответствие;
	
	ИдСтроки = 0;
	Пока Выборка.Следующий() Цикл
		ХэшНаименования = РаспознаваниеДокументовНечеткийПоискСлужебный.ОчиститьСтроку(Выборка.Наименование);
		ХэшНаименования = РаспознаваниеДокументовНечеткийПоискСлужебный.ОптическийХэш(ХэшНаименования);
		
		ИдНайденнойСтроки = КлючиНазваний.Получить(ХэшНаименования);
		Если ИдНайденнойСтроки = Неопределено Тогда
			
			Если СтрНайти(Выборка.ИНН, "/") <> 0 Тогда
				ЭтотИНН = РаспознаваниеДокументовСлужебный.ИзвлечьИННКППИзСтроки(Выборка.ИНН, "ИНН");
			Иначе
				ЭтотИНН = Выборка.ИНН;
			КонецЕсли;
			
			Если СтрНайти(Выборка.КПП, "/") <> 0 Тогда
				ЭтотКПП = РаспознаваниеДокументовСлужебный.ИзвлечьИННКППИзСтроки(Выборка.КПП, "КПП");
			Иначе
				ЭтотКПП = Выборка.КПП;
			КонецЕсли;
			// оставляем только цифры в строке с КПП
			Если ТипЗнч(ЭтотКПП) = Тип("Строка") И Не СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЭтотКПП) Тогда
				ЭтотКПП = СтрЗаменитьПоРегулярномуВыражению(ЭтотКПП, "[^0-9]", "");
			КонецЕсли;
			
			ИдНайденнойСтроки = КлючиИННКПП.Получить(ЭтотИНН + "+" + ЭтотКПП);
			Если ИдНайденнойСтроки = Неопределено Тогда
				КлючиНазваний.Вставить(ХэшНаименования, ИдСтроки);
				КлючиИННКПП.Вставить(ЭтотИНН + "+" + ЭтотКПП, ИдСтроки);
				
				ИдНайденнойСтроки = ИдСтроки;
				ИдСтроки = ИдСтроки + 1;
				
				СтрокаИтога = ИтоговаяТаблица.Добавить();
				СтрокаИтога.Наименование = Выборка.Наименование;
				СтрокаИтога.ИНН = ЭтотИНН;
				СтрокаИтога.КПП = ЭтотКПП;
				СтрокаИтога.ИдентификаторРезультата = Выборка.ИдентификаторРезультата;
				СтрокаИтога.ЮридическоеФизическоеЛицо =
					РаспознаваниеДокументовСлужебныйКлиентСервер.ОпределитьЮридическоеФизическоеЛицо(ЭтотИНН);
				
				СтрокаИтога.РаспознанныеДокументы = Новый Массив;
				СтрокаИтога.ИдСтрокДокументов = Новый Массив;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаИтога = ИтоговаяТаблица[ИдНайденнойСтроки];
		СтрокаИтога.РаспознанныеДокументы.Добавить(Выборка.Ссылка);
		
		// Индексы массивов ИдСтрокДокументов и РаспознанныеДокументы совпадают
		ИдВМассиве = ПараметрыСоздания.РаспознанныеДокументы.Найти(Выборка.Ссылка);
		СтрокаИтога.ИдСтрокДокументов.Добавить(ПараметрыСоздания.ИдСтрокДокументов[ИдВМассиве]);
	КонецЦикла;
	
	Возврат ИтоговаяТаблица;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТаблицаРеквизитовДляДоговоров(ПараметрыСоздания, Запрос)
	
	ИтоговаяТаблица = Новый ТаблицаЗначений;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	ИмяРеквизитаНомер = "Номер";
	ИмяРеквизитаДата  = "Дата";
	
	РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(ИмяРеквизитаНомер, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(ИмяРеквизитаДата, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
	
	ИтоговаяТаблица.Колонки.Добавить("Направление", Новый ОписаниеТипов("ПеречислениеСсылка.НаправленияРаспознанногоДокумента"));
	ИтоговаяТаблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ИтоговаяТаблица.Колонки.Добавить("Контрагент", Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ИтоговаяТаблица.Колонки.Добавить("ВидДоговора", Метаданные.ОпределяемыеТипы.ВидыДоговоровБРД.Тип);
	ИтоговаяТаблица.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("ХэшНаименования", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить(ИмяРеквизитаНомер, Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить(ИмяРеквизитаДата, Новый ОписаниеТипов("Дата"));
	ИтоговаяТаблица.Колонки.Добавить("ИдентификаторРезультата", Новый ОписаниеТипов("Строка"));
	ИтоговаяТаблица.Колонки.Добавить("РаспознанныеДокументы");
	ИтоговаяТаблица.Колонки.Добавить("ИдСтрокДокументов");
	
	Пока Выборка.Следующий() Цикл
		ПараметрыВыбораДоговора = Новый Структура;
		РаспознаваниеДокументовКлиентСерверПереопределяемый.ПриЗаполненииПараметровВыбораДоговора(Выборка, ПараметрыВыбораДоговора);
		
		ХэшНаименования = РаспознаваниеДокументовНечеткийПоискСлужебный.ОчиститьСтроку(Выборка.Наименование);
		ХэшНаименования = РаспознаваниеДокументовНечеткийПоискСлужебный.ОптическийХэш(ХэшНаименования);
		
		ПараметрыПоиска = Новый Структура;
		ПараметрыПоиска.Вставить("Организация", Выборка.ОрганизацияЗначение);
		ПараметрыПоиска.Вставить("Контрагент", Выборка.КонтрагентЗначение);
		ПараметрыПоиска.Вставить("ВидДоговора", ПараметрыВыбораДоговора.ВидыДоговоров[0]);
		ПараметрыПоиска.Вставить("ХэшНаименования", ХэшНаименования);
		
		НайденныеСтроки = ИтоговаяТаблица.НайтиСтроки(ПараметрыПоиска);
		Если НайденныеСтроки.Количество() <> 0 Тогда
			СтрокаИтога = НайденныеСтроки[0];
		Иначе
			СтрокаИтога = ИтоговаяТаблица.Добавить();
			СтрокаИтога.Направление = Выборка.Направление;
			СтрокаИтога.Организация = Выборка.ОрганизацияЗначение;
			СтрокаИтога.Контрагент = Выборка.КонтрагентЗначение;
			СтрокаИтога.ВидДоговора = ПараметрыВыбораДоговора.ВидыДоговоров[0];
			СтрокаИтога.Наименование = Выборка.Наименование;
			СтрокаИтога.ХэшНаименования = ХэшНаименования;
			СтрокаИтога[ИмяРеквизитаНомер] = Выборка[ИмяРеквизитаНомер];
			СтрокаИтога[ИмяРеквизитаДата] = Выборка[ИмяРеквизитаДата + "Значение"];
			СтрокаИтога.ИдентификаторРезультата = Выборка.ИдентификаторРезультата;
			СтрокаИтога.РаспознанныеДокументы = Новый Массив;
			СтрокаИтога.ИдСтрокДокументов = Новый Массив;
		КонецЕсли;
		
		СтрокаИтога.РаспознанныеДокументы.Добавить(Выборка.Ссылка);
		
		// Индексы массивов ИдСтрокДокументов и РаспознанныеДокументы совпадают
		ИдВМассиве = ПараметрыСоздания.РаспознанныеДокументы.Найти(Выборка.Ссылка);
		СтрокаИтога.ИдСтрокДокументов.Добавить(ПараметрыСоздания.ИдСтрокДокументов[ИдВМассиве]);
	КонецЦикла;
	
	ИтоговаяТаблица.Колонки.Удалить("ХэшНаименования");
	
	Возврат ИтоговаяТаблица;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьНовыйЭлементСправочника(ПараметрыСоздания, ДанныеЭлемента)

	Если ПараметрыСоздания.ИмяСправочника = "Контрагенты" Тогда  
		
		РеквизитыСоздаваемогоОбъекта = РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента(
			ПараметрыСоздания.ИмяСправочника, Неопределено, Неопределено);
		
		Для Каждого Реквизит Из РеквизитыСоздаваемогоОбъекта Цикл
			Если Не ДанныеЭлемента.Свойство(Реквизит.Реквизит) Тогда
				ДанныеЭлемента.Вставить(Реквизит.Реквизит, Реквизит.ПустаяСсылка);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	НовыйЭлементСправочника = Неопределено;
	Если ПараметрыСоздания.Использовать1СКонтрагент Тогда
		РаспознаваниеДокументовПереопределяемый.СоздатьКонтрагентаПоРеквизитам(ДанныеЭлемента, НовыйЭлементСправочника);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НовыйЭлементСправочника) Тогда
		МенеджерСправочника = Справочники[ПараметрыСоздания.ИмяСправочника];
		СохраняемыйОбъект = МенеджерСправочника.СоздатьЭлемент();
		
		ДанныеДляЗаполнения = Новый Структура;
		Для Каждого СохраняемыйРеквизит Из ДанныеЭлемента Цикл
			Если СохраняемыйРеквизит.Ключ = "РаспознанныеДокументы"
				Или СохраняемыйРеквизит.Ключ = "Направление"
				Или СохраняемыйРеквизит.Ключ = "ИдентификаторРезультата"
				Или СохраняемыйРеквизит.Ключ = "ИдСтрокДокументов" Тогда
				
				Продолжить;
			КонецЕсли;
			
			СохраняемыйОбъект[СохраняемыйРеквизит.Ключ] = СохраняемыйРеквизит.Значение;
			ДанныеДляЗаполнения.Вставить(СохраняемыйРеквизит.Ключ, СохраняемыйОбъект[СохраняемыйРеквизит.Ключ]);
			Если СохраняемыйРеквизит.Ключ = "Наименование" Тогда
				ТекущееИмя = "НаименованиеПолное";
				Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(СохраняемыйОбъект, ТекущееИмя) Тогда
					СохраняемыйОбъект[ТекущееИмя] = СохраняемыйРеквизит.Значение;
					ДанныеДляЗаполнения.Вставить(ТекущееИмя, СохраняемыйОбъект[ТекущееИмя]);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Попытка
			РаспознаваниеДокументовПереопределяемый.ПриЗаполненииНовогоЭлементаСправочника(СохраняемыйОбъект, ДанныеДляЗаполнения);
			СохраняемыйОбъект.Записать();
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Описание ошибки:';
									|en = 'Error details:'") + Символы.ПС
			+ ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Сообщение.Сообщить();
		КонецПопытки;
		
		// Избегаем Неопределено: если Объект записан, то ссылка реальная, если нет - то в ней ПустаяСсылка
		НовыйЭлементСправочника = СохраняемыйОбъект.Ссылка;
		
	КонецЕсли;
	
	Возврат НовыйЭлементСправочника;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьОставшихсяКонтрагентовНаСервере(ПараметрыСоздания)
	
	ТекущийИдСоздания = ПараметрыСоздания.ИдСтрокиСоздания;
	КоличествоСозданий = ПараметрыСоздания.ДанныеСоздания.Количество();
	
	Результат = Новый Структура;
	Результат.Вставить("Сообщения", Новый Массив);
	Результат.Вставить("Контрагенты");
	Если КоличествоСозданий = 0 Тогда
		Результат.Контрагенты = Новый Массив;
		Возврат Результат;
	Иначе
		Результат.Контрагенты = Новый Массив(КоличествоСозданий);
	КонецЕсли;
	
	Пока КоличествоСозданий > ТекущийИдСоздания Цикл
		ДанныеКонтрагента = ПараметрыСоздания.ДанныеСоздания[ТекущийИдСоздания];
		
		// Нечеткий поиск
		ОбъединяемыеРеквизиты = Новый Массив;
		ОбъединяемыеРеквизиты.Добавить("Наименование");
		ОбъединяемыеРеквизиты.Добавить("НаименованиеПолное");
		ПараметрыОбъединенияРеквизитов = Новый Структура("Наименование", ОбъединяемыеРеквизиты);
		
		РеквизитыПоиска = Новый Структура;
		РеквизитыПоиска.Вставить("Наименование", ДанныеКонтрагента.Наименование);
		РеквизитыПоиска.Вставить("ИНН", ДанныеКонтрагента.ИНН);
		РеквизитыПоиска.Вставить("КПП", ДанныеКонтрагента.КПП);
		Тип = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
		Кандидаты = РаспознаваниеДокументов.ПолучитьКандидатов(
			РеквизитыПоиска,
			Тип,
			ПараметрыОбъединенияРеквизитов,
			,
			ДанныеКонтрагента.ИдентификаторРезультата
		);
		Если Кандидаты.Количество() <> 0 
			И Кандидаты[0].Коэффициент * 100 >= РаспознаваниеДокументовСлужебныйКлиентСервер.ГраницаУверенныхЗначений() Тогда
			
			Результат.Контрагенты[ТекущийИдСоздания] = Кандидаты[0].Ссылка;
			СуществующийКонтрагент = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Кандидаты[0].Ссылка, "Наименование, ИНН, КПП", Истина);
			
			КонтрагентКСозданию = ДанныеКонтрагента.Наименование;
			Если ЗначениеЗаполнено(ДанныеКонтрагента.ИНН) Тогда
				КонтрагентКСозданию = КонтрагентКСозданию + ", " + ДанныеКонтрагента.ИНН;
				Если ЗначениеЗаполнено(ДанныеКонтрагента.КПП) Тогда
					КонтрагентКСозданию = КонтрагентКСозданию + " / " + ДанныеКонтрагента.КПП;
				КонецЕсли;
			КонецЕсли;
			
			КонтрагентВБазе = СуществующийКонтрагент.Наименование;
			Если ЗначениеЗаполнено(СуществующийКонтрагент.ИНН) Тогда
				КонтрагентВБазе = КонтрагентВБазе + ", " + СуществующийКонтрагент.ИНН;
				Если ЗначениеЗаполнено(СуществующийКонтрагент.КПП) Тогда
					КонтрагентВБазе = КонтрагентВБазе + " / " + СуществующийКонтрагент.КПП;
				КонецЕсли;
			КонецЕсли;
			
			НовоеСообщение = Новый СообщениеПользователю;
			НовоеСообщение.Текст = СтрШаблон(НСтр("ru = 'Новый контрагент ""%1"" не создан. В информационной базе уже есть контрагент ""%2""';
													|en = 'A new ""%1"" counterparty is not created. The infobase already contains the ""%2"" counterparty'"),
				КонтрагентКСозданию, КонтрагентВБазе
			);
			Результат.Сообщения.Добавить(НовоеСообщение);
		Иначе
			ПараметрыСправочника = Новый Структура("ИмяСправочника, Использовать1СКонтрагент", "Контрагенты", ПараметрыСоздания.Использовать1СКонтрагент);
			Результат.Контрагенты[ТекущийИдСоздания] = СоздатьНовыйЭлементСправочника(ПараметрыСправочника, ДанныеКонтрагента);
		КонецЕсли;
		
		ТекущийИдСоздания = ТекущийИдСоздания + 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СоздатьОставшиесяДоговорыНаСервере(ПараметрыСоздания)
	
	ТекущийИдСоздания = ПараметрыСоздания.ИдСтрокиСоздания;
	КоличествоСозданий = ПараметрыСоздания.ДанныеСоздания.Количество();
	
	Результат = Новый Структура;
	Результат.Вставить("Сообщения", Новый Массив);
	Результат.Вставить("Договоры");
	Если КоличествоСозданий = 0 Тогда
		Результат.Договоры = Новый Массив;
		Возврат Результат;
	Иначе
		Результат.Договоры = Новый Массив(КоличествоСозданий);
	КонецЕсли;
	
	Пока КоличествоСозданий > ТекущийИдСоздания Цикл
		ДанныеДоговора = ПараметрыСоздания.ДанныеСоздания[ТекущийИдСоздания];
		
		// Нечеткий поиск
		ПараметрыВыбораДоговора = Новый Структура;
		ПараметрыВыбораДоговора.Вставить("Контрагент", ДанныеДоговора.Контрагент);
		ПараметрыВыбораДоговора.Вставить("Организация", ДанныеДоговора.Организация);
		РаспознаваниеДокументовКлиентСерверПереопределяемый.ПриЗаполненииПараметровВыбораДоговора(ДанныеДоговора, ПараметрыВыбораДоговора);
		
		ОтборМетаданных = Новый Массив;
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Контрагент", "=", ПараметрыВыбораДоговора.Контрагент));
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Организация", "=", ПараметрыВыбораДоговора.Организация));
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"ТипДоговора", "В", ПараметрыВыбораДоговора.ВидыДоговоров));
		
		РеквизитыПоиска = Новый Структура;
		
		РаспознаваниеДокументовПереопределяемый.ЗаполнитьРеквизитыПоискаДоговора(РеквизитыПоиска, ДанныеДоговора);
		
		Тип = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
		Кандидаты = РаспознаваниеДокументов.ПолучитьКандидатов(
			РеквизитыПоиска,
			Тип,
			,
			ОтборМетаданных,
			ДанныеДоговора.ИдентификаторРезультата
		);
		Если Кандидаты.Количество() <> 0 
			И Кандидаты[0].Коэффициент * 100 >= РаспознаваниеДокументовСлужебныйКлиентСервер.ГраницаУверенныхЗначений() Тогда
			
			Результат.Договоры[ТекущийИдСоздания] = Кандидаты[0].Ссылка;
			
			НовоеСообщение = Новый СообщениеПользователю;
			НовоеСообщение.Текст = СтрШаблон(НСтр("ru = 'Новый договор ""%1"" не создан. В информационной базе уже есть договор ""%2""';
													|en = 'A new ""%1"" contract is not created. The infobase already contains the ""%2"" contract'"),
				ДанныеДоговора.Наименование, Кандидаты[0].Ссылка
			);
			Результат.Сообщения.Добавить(НовоеСообщение);
		Иначе
			ПараметрыСправочника = Новый Структура("ИмяСправочника, Использовать1СКонтрагент", "ДоговорыКонтрагентов", Ложь);
			Результат.Договоры[ТекущийИдСоздания] = СоздатьНовыйЭлементСправочника(ПараметрыСправочника, ДанныеДоговора);
		КонецЕсли;
		
		ТекущийИдСоздания = ТекущийИдСоздания + 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиШапки()
	
	Если СкрытьЗаполненныеСтрокиШапки Тогда
		Элементы.РеквизитыШапки.ОтборСтрок = Новый ФиксированнаяСтруктура("ДанныеЗаполнены", Ложь);
	Иначе
		Элементы.РеквизитыШапки.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаполненияСтрокШапки()
	
	СкрытьЗаполненныеСтрокиШапки();
	ОбновитьВидимостьНадписиПроЗаполненность();
	ПроверитьИЗагрузитьПустыеКартинки();
	
КонецПроцедуры

&НаКлиенте
Функция ПодсчетНезаполненныхСтрокШапки()
	
	КоличествоНеЗаполнено = 0;
	Для Каждого СтрокаТаблицы Из РеквизитыШапки Цикл
		Если Не СтрокаТаблицы.ДанныеЗаполнены Тогда
			КоличествоНеЗаполнено = КоличествоНеЗаполнено + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоличествоНеЗаполнено;
	
КонецФункции

&НаКлиенте
Функция ПроверкаДанныеЗаполненыШапка(СтрокаТаблицы)
	
	Возврат ЗначениеЗаполнено(СтрокаТаблицы.ДатаДокумента)
		И ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента)
		И ЗначениеЗаполнено(СтрокаТаблицы.Контрагент)
		И ЗначениеЗаполнено(СтрокаТаблицы.Организация)
		И ЗначениеЗаполнено(СтрокаТаблицы.ИтогоВсего)
		И Не ЗначениеЗаполнено(СтрокаТаблицы.Ошибка)
		И СтрокаТаблицы.ЕстьПравоСоздания;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьПолнуюКартинку()
	
	Если Не ПолнаяКартинкаВидна
		Или (ТекущийШаг = "Номенклатура" И СкрытьЗаполненнуюНоменклатуру И ВсеСтрокиНаШагеЗаполнены) Тогда
		
		Возврат;
	КонецЕсли;
	
	Если Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаШапка Тогда
		ТекущиеДанные = Элементы.РеквизитыШапки.ТекущиеДанные;
	ИначеЕсли Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаДопРеквизиты Тогда
		ТекущиеДанные = Элементы.ТаблицаДопРеквизитов.ТекущиеДанные;
	ИначеЕсли Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаНоменклатура Тогда
		ТекущиеДанные = Элементы.ТаблицаНоменклатуры.ТекущиеДанные;
	ИначеЕсли Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаСуммы Тогда
		ТекущиеДанные = Элементы.ТаблицаСумм.ТекущиеДанные;
	КонецЕсли;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("Ссылка", ТекущиеДанные.Ссылка);
	ПараметрыПолучения.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыПолучения.Вставить("ОбновитьПолнуюКартинку", Истина);
	ПараметрыПолучения.Вставить("ИмяРеквизита", ТекущееИмяРеквизита);
	
	Если Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаШапка
		Или Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаДопРеквизиты Тогда
		ПараметрыПолучения.Вставить("Направление", ТекущиеДанные.Направление);
		ПараметрыПолучения.Вставить("ТипДокумента", ТекущиеДанные.ТипДокумента);
		ПараметрыПолучения.Вставить("ИсточникПоляБанковскийСчетВладелец", ИсточникПоляБанковскийСчетВладелец);
		ПараметрыПолучения.Вставить("ИсточникПоляДоговорВидДоговора", ИсточникПоляДоговорВидДоговора.ВыгрузитьЗначения());
		ПараметрыПолучения.Вставить("ИсточникПоляДоговорКонтрагент", ИсточникПоляДоговорКонтрагент);
		ПараметрыПолучения.Вставить("ИсточникПоляДоговорОрганизация", ИсточникПоляДоговорОрганизация);
		ДанныеЯчейки = РаспознаваниеДокументовКомплектыВызовСервера.ДанныеЯчейкиШапкиНаСервере(ПараметрыПолучения);
	Иначе
		ПараметрыПолучения.Вставить("НомерСтрокиТЧ", ТекущиеДанные.НомерСтрокиТЧ);
		ДанныеЯчейки = РаспознаваниеДокументовКомплектыВызовСервера.ДанныеЯчейкиТаблицыНаСервере(ПараметрыПолучения);
	КонецЕсли;
	
	Если ДанныеЯчейки.Свойство("АдресПолнойКартинки") Тогда
		АдресПолнойКартинки = ДанныеЯчейки.АдресПолнойКартинки;
		Если HTMLДокументСформирован Тогда
			РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресПолнойКартинки);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЯчейки.Свойство("КоординатыКартинки") Тогда
		КоординатыВыделения = ДанныеЯчейки.КоординатыКартинки;
		СтрокВИзображенииВыделения = ДанныеЯчейки.СтрокВИзображении;
		ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИзменитьНаправлениеНаСервере(ДокументСсылка, ВидДокумента)
	
	Результат = Новый Структура;
	Результат.Вставить("Направление");
	Результат.Вставить("НаправлениеДокумента");
	Результат.Вставить("Контрагент");
	Результат.Вставить("Организация");
	Результат.Вставить("ЕстьПравоСоздания");
	Результат.Вставить("ВидДокумента");
	
	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
		
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
		Результат.НаправлениеДокумента = 1;
	Иначе
		ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
		Результат.НаправлениеДокумента = 2;
	КонецЕсли;
	
	Если ВидДокумента = "ПТИУ" Тогда
		ВидДокумента = "РТИУ"	
	ИначеЕсли ВидДокумента = "РТИУ" Тогда
		ВидДокумента = "ПТИУ"
	ИначеЕсли ВидДокумента = "РУИПА" Тогда
		ВидДокумента = "ПУИПА"
	ИначеЕсли ВидДокумента = "ПУИПА" Тогда
		ВидДокумента = "РУИПА"
	ИначеЕсли Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ВидДокумента = ?(ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий, "РТИУ",
			"ПТИУ");
	КонецЕсли;
	
	Результат.ВидДокумента = ВидДокумента;
	
	ДокументОбъект.ВариантОбработки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВариантОбработкиПоТипуИНаправлению(
		ДокументОбъект.ТипДокумента, ДокументОбъект.Направление);
	
	РаспознаваниеДокументовПереопределяемый.ПередЗаписьюРаспознанногоДокумента(ДокументОбъект);
	ДокументОбъект.Записать();
	
	ЗаполнитьЗначенияСвойств(Результат, ДокументОбъект, "Направление, Контрагент, Организация");
	
	ИменаРеквизитов = РаспознаваниеДокументовСлужебныйКлиентСервер.ИменаРеквизитовКонтрагентИОрганизация(ДокументОбъект);
	
	Для Каждого КлючЗначение Из ИменаРеквизитов Цикл
		ДанныеРеквизита = РаспознаваниеДокументовСлужебныйКлиентСервер.РеквизитДокумента(ДокументОбъект, КлючЗначение.Значение);
		Результат.Вставить(КлючЗначение.Ключ + "Распознано", НСтр("ru = 'Не сопоставлен: ';
																	|en = 'Not mapped:'") + ДанныеРеквизита.РаспознанныйТекст);
	КонецЦикла;
	
	Результат.ЕстьПравоСоздания = РаспознаваниеДокументовПраваДоступа.ЕстьПравоСозданияДокументаИзРаспознанного(
		ДокументОбъект.Направление, ДокументОбъект.ТипДокумента); 
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИзменитьВидДокументаНаСервере(ДокументСсылка)
	
	Результат = Новый Структура("ВидДокумента, ЕстьПравоСоздания");

	ДокументОбъект = ДокументСсылка.ПолучитьОбъект();
	ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ДокументОбъект, "ВидДокумента");
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		ВидДокумента = ?(ВидДокумента = "РТИУ", "РУИПА", "РТИУ");	
	Иначе
        ВидДокумента = ?(ВидДокумента = "ПТИУ", "ПУИПА", "ПТИУ");
	КонецЕсли;
	РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ВидДокумента",
		ВидДокумента);
	ДокументОбъект.Записать();
	
	Результат.ВидДокумента = ВидДокумента;
	Результат.ЕстьПравоСоздания = РаспознаваниеДокументовПраваДоступа.ЕстьПравоСозданияДокументаИзРаспознанного(
		ДокументОбъект.Направление, ДокументОбъект.ТипДокумента, ВидДокумента);	
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДокументыПоОтбору(ОтборИзСписка)
	
	Таблица = Документы.РаспознанныйДокумент.ПолучитьДокументыПоОтбору(ОтборИзСписка);
	РеквизитыШапки.Загрузить(Таблица);
	
	ЗаполнитьПраваНаСозданиеДокументовШапки(РеквизитыШапки);
	ЗаполнитьПредупрежденияПоСкидкам();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПраваНаСозданиеДокументовШапки(РеквизитыШапки)
	
	Для Каждого Строка Из РеквизитыШапки Цикл
		
		Если ЗначениеЗаполнено(Строка.ВидДокумента) Тогда
			ВидДокумента = Строка.ВидДокумента;
		Иначе
			ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(Строка.Ссылка,
				"ВидДокумента", "");
		КонецЕсли;
		
		Строка.ЕстьПравоСоздания = РаспознаваниеДокументовПраваДоступа.ЕстьПравоСозданияДокументаИзРаспознанного(
			Строка.Направление, Строка.ТипДокумента, ВидДокумента);
		
		Если Не Строка.ЕстьПравоСоздания Тогда
			Строка.ДанныеЗаполнены = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьКолонкиТаблицыШапки()
	
	ДобавляемыеРеквизиты = Новый Массив;
	Для Каждого ЭлементКолонка Из Элементы.РеквизитыШапки.ПодчиненныеЭлементы Цикл
		Если ЭлементКолонка.Имя <> "ШапкаНаправлениеДокумента"
			И ЭлементКолонка.Имя <> "ШапкаКартинкаОткрытия" 
			И ЭлементКолонка.Имя <> "ШапкаОшибка"
			И ЭлементКолонка.Имя <> "ШапкаВидДокумента" Тогда
			
			ЭлементКолонка.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ОбработкаВыбораПоля");
			
			НовыйРеквизит = Новый РеквизитФормы(СтрЗаменить(ЭлементКолонка.Имя, "Шапка", "") + "Распознано", Новый ОписаниеТипов("Строка"));
			НовыйРеквизит.Путь = "РеквизитыШапки";
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
		КонецЕсли;
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	СоздатьЭлементыДобавленныхРеквизитов(ДобавляемыеРеквизиты, Элементы.РеквизитыШапки);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыДобавленныхРеквизитов(ДобавляемыеРеквизиты, ЭлементРодитель)
	
	Для Каждого Реквизит Из ДобавляемыеРеквизиты Цикл 
		НовыйЭлемент = Элементы.Добавить("Шапка" + Реквизит.Имя, Тип("ПолеФормы"), ЭлементРодитель);
		НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
		НовыйЭлемент.ПутьКДанным = "РеквизитыШапки." + Реквизит.Имя;
		НовыйЭлемент.Видимость = Истина;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеЗаполненияВыделенныхСтрок(ЗначениеЗаполнения, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаписиРеквизитов = Новый Структура;
	ПараметрыЗаписиРеквизитов.Вставить("ТаблицаФормы", ДополнительныеПараметры.ТаблицаФормы);
	ПараметрыЗаписиРеквизитов.Вставить("СтрокиТаблицы", Элементы.РеквизитыШапки.ВыделенныеСтроки);
	ПараметрыЗаписиРеквизитов.Вставить("ИмяРеквизитаШапки", ДополнительныеПараметры.ИмяРеквизитаШапки);
	ПараметрыЗаписиРеквизитов.Вставить("ЗначениеЗаполнения", ЗначениеЗаполнения);
	ПараметрыЗаписиРеквизитов.Вставить("ОбновитьЗначениеВЯчейке", Истина);
	
	ЗаписатьНовоеЗначениеРеквизита(ПараметрыЗаписиРеквизитов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНовоеЗначениеРеквизита(Параметры)
	
	Для Каждого СтрокаДляЗаполнения Из Параметры.СтрокиТаблицы Цикл
		ДанныеЭтойСтроки = Параметры.ТаблицаФормы.ДанныеСтроки(СтрокаДляЗаполнения);
		
		Если Параметры.ОбновитьЗначениеВЯчейке
			И ДанныеЭтойСтроки[Параметры.ИмяРеквизитаШапки] = Параметры.ЗначениеЗаполнения Тогда
			
			// Выделили строку, в которой уже стоит правильное значение
			Продолжить;
		КонецЕсли;
		
		ИмяРеквизита =  ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, Параметры.ИмяРеквизитаШапки);
		СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
			ДанныеЭтойСтроки.Ссылка,
			ИмяРеквизита,
			Параметры.ЗначениеЗаполнения
		);
		ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
	КонецЦикла;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	
КонецПроцедуры

&НаКлиенте
Функция ИмяРеквизитаДокументаПоИмениВШапке(ДанныеЭтойСтроки, ИмяРеквизитаШапки)
	
	ИменаРеквизитов = РаспознаваниеДокументовСлужебныйКлиентСервер.ИменаРеквизитовКонтрагентИОрганизация(ДанныеЭтойСтроки);
	Если ИменаРеквизитов.Свойство(ИмяРеквизитаШапки) Тогда
		ИмяРеквизита = ИменаРеквизитов[ИмяРеквизитаШапки];
	Иначе
		ИмяРеквизита = ИмяРеквизитаШапки;
	КонецЕсли;
	
	ИменаРеквизитов = ИменаРеквизитовБанковскийСчетИСоглашение(ДанныеЭтойСтроки);
	Если ИменаРеквизитов.Свойство(ИмяРеквизитаШапки) Тогда
		ИмяРеквизита = ИменаРеквизитов[ИмяРеквизитаШапки];
	КонецЕсли;
	
	Возврат ИмяРеквизита;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИменаРеквизитовБанковскийСчетИСоглашение(ДанныеДокумента)
	
	// Ключ - БанковскийСчет
	// Значение - имя реквизита в документе
	Результат = Новый Структура;
	
	Если ДанныеДокумента.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий") Тогда
		Результат.Вставить("БанковскийСчет", "БанковскийСчетКонтрагента");
		Результат.Вставить("Соглашение", "СоглашениеСПоставщиком");
	Иначе
		Результат.Вставить("БанковскийСчет", "БанковскийСчетОрганизации");
		Результат.Вставить("Соглашение", "СоглашениеСКлиентом");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НаборЦветов = Новый Структура;
	НаборЦветов.Вставить("КрасныйЦвет", Новый Цвет(255, 0, 0));
	НаборЦветов.Вставить("РозовыйЦвет", Новый Цвет(251, 212, 212));
	НаборЦветов.Вставить("СалатовыйЦвет", Новый Цвет(240, 255, 240));
	НаборЦветов.Вставить("СерыйЦвет", Новый Цвет(220, 220, 220));
	
	УстановитьУсловноеОформлениеШагШапка(НаборЦветов);
	УстановитьУсловноеОформлениеШагДубли(НаборЦветов);
	УстановитьУсловноеОформлениеШагДопРеквизиты(НаборЦветов);
	УстановитьУсловноеОформлениеШагНоменклатура(НаборЦветов);
	УстановитьУсловноеОформлениеШагСуммы(НаборЦветов);
	УстановитьУсловноеОформлениеШагИтого(НаборЦветов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваЭлементовФормы()
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками")
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
		
		Элементы.СоздатьДоговоры.Видимость = Ложь;
		Элементы.ТаблицаДопРеквизитовДоговор.Видимость = Ложь;
		
	Иначе
		
		Элементы.СоздатьДоговоры.Видимость = ПравоДоступа("ИнтерактивноеДобавление",
			Метаданные.Справочники.ДоговорыКонтрагентов, Пользователи.ТекущийПользователь());
		
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками")
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		
		Элементы.ТаблицаДопРеквизитовСоглашение.Видимость = Ложь;
		
	КонецЕсли;
	
	Если Не ПравоДоступа("ИнтерактивноеДобавление",
		Метаданные.Справочники.Номенклатура, Пользователи.ТекущийПользователь()) Тогда
		
		Элементы.ТаблицаНоменклатурыСоздатьВыделеннуюНоменклатуру.Доступность = Ложь;
		Элементы.ТаблицаНоменклатурыКонтекстноеМенюСоздатьВыделеннуюНоменклатуру.Доступность = Ложь;
		
	КонецЕсли;
	
	Если Не ИспользоватьИмпортныеТовары Тогда
		Элементы.ТаблицаСуммНомерГТД.Видимость = Ложь;
		Элементы.ТаблицаСуммСтранаПроисхождения.Видимость = Ложь;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагШапка(НаборЦветов)
	
	ВсеКолонки = Новый Массив;
	ВсеКолонки.Добавить("НомерДокумента");
	ВсеКолонки.Добавить("ДатаДокумента");
	ВсеКолонки.Добавить("Контрагент");
	ВсеКолонки.Добавить("Организация");
	ВсеКолонки.Добавить("ИтогоВсего");
	ВсеКолонки.Добавить("Ошибка");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Истина;
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Для Каждого Колонка Из ВсеКолонки Цикл  
		
		Если Колонка = "Ошибка" Тогда
			Продолжить;
		КонецЕсли;
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Шапка" + Колонка + "Распознано");
		
	КонецЦикла;
	
	Для Каждого Колонка Из ВсеКолонки Цикл
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
		Если Колонка = "Контрагент" Или Колонка = "Организация" Тогда
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
			ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("РеквизитыШапки." + Колонка + "Распознано"));
		КонецЕсли;
		
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РеквизитыШапки." + Колонка); 
		
		Если Колонка = "Ошибка" Тогда
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено; 
		Иначе
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
		КонецЕсли;
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("Шапка" + Колонка);
	КонецЦикла;
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("РеквизитыШапки.ЕстьПравоСоздания"); 
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ШапкаВидДокумента");
	
	// Вид документа (текст)
	ВидыДокументов = Новый СписокЗначений;
	ВидыДокументов.Добавить("ПТИУ");
	ВидыДокументов.Добавить("РТИУ");
	ВидыДокументов.Добавить("");
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Товары и услуги';
																			|en = 'Goods and services'"));
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"РеквизитыШапки.ВидДокумента", ВидСравненияКомпоновкиДанных.ВСписке, ВидыДокументов);
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ШапкаВидДокумента");
	
	ВидыДокументов = Новый СписокЗначений;
	ВидыДокументов.Добавить("ПУИПА");
	ВидыДокументов.Добавить("РУИПА");
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Услуги и пр. активы';
																			|en = 'Services and other assets'"));
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"РеквизитыШапки.ВидДокумента", ВидСравненияКомпоновкиДанных.ВСписке, ВидыДокументов);
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ШапкаВидДокумента");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагДубли(НаборЦветов)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СалатовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДублей.ЕстьОсновной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДублейИменаРеквизитов");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДублейЗначенияРеквизитов");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагДопРеквизиты(НаборЦветов)
	
	// оформление по спискам выбора
	Для Каждого ЗначениеСписка Из Элементы.ТаблицаДопРеквизитовСуммаВключаетНДС.СписокВыбора Цикл
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ЗначениеСписка.Представление);
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.СуммаВключаетНДС");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = ЗначениеСписка.Значение;
		
		ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаДопРеквизитовСуммаВключаетНДС.Имя);
	КонецЦикла;
	
	Для Каждого ЗначениеСписка Из Элементы.ТаблицаДопРеквизитовВидСкидки.СписокВыбора Цикл
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", ЗначениеСписка.Представление);
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату;
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ВидСкидки");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = ЗначениеСписка.Значение;
		
		ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТаблицаДопРеквизитовВидСкидки.Имя);
	КонецЦикла;
	// оформление по спискам выбора
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовКонтрагент");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовТипДокумента");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокОтбора = Новый СписокЗначений;
	СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД);
	СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12);
	СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура);
	ОтборЭлемента.ПравоеЗначение = СписокОтбора;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСуммаВключаетНДС");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСрокОплаты");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПродавецОрганизация");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовБанковскийСчет");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовВидСкидки");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСрокОплаты");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПродавецОрганизация");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовБанковскийСчет");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовВидСкидки");
		
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.Направление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПродавецОрганизация");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ПродавецОрганизацияРаспознано"));
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ПродавецОрганизация");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.Направление");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПродавецОрганизация");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.БанковскийСчетРаспознано"));
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.БанковскийСчет");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ТипДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовБанковскийСчет"); 
	
	РаспознаваниеДокументовСлужебный.УстановитьУсловноеОформлениеДопРеквизитовГрупповаяОбработка(
		ЭтотОбъект, НаборЦветов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагНоменклатура(НаборЦветов)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = "ВидимостьЗаполненнойНоменклатуры";
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.ЕстьНоменклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры");
	
	// Номенклатура (ТолькоПросмотр, Отображать)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	ВидыДокумента = Новый СписокЗначений;
	ВидыДокумента.Добавить("РУИПА");
	ВидыДокумента.Добавить("ПУИПА");
		
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаНоменклатуры.ТипДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаНоменклатуры.ВидДокумента",
		ВидСравненияКомпоновкиДанных.ВСписке, ВидыДокумента);
		
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатурыНоменклатура");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.НоменклатураРаспознано"));
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено; 
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.ВидДокумента");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеВСписке;
	ВидыДокумента = Новый СписокЗначений;
	ВидыДокумента.Добавить("РУИПА");
	ВидыДокумента.Добавить("ПУИПА");
	ОтборЭлемента.ПравоеЗначение = ВидыДокумента;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатурыНоменклатура");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СалатовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаНоменклатуры.Номенклатура", ВидСравненияКомпоновкиДанных.Заполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаНоменклатуры.Ссылка", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры");
	
	// номенклатура - видимость колонок
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатурыНоменклатура");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатурыКонтрагент");
	
	СоответствиеСтавок = Новый Соответствие();
	
	РаспознаваниеДокументовПереопределяемый.ПолучитьСоответствиеСтавокНДС(СоответствиеСтавок);

	// Только для отображения в списке без возможности редактировать
	Для Каждого ЗначениеСоответствия Из СоответствиеСтавок Цикл
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Строка(ЗначениеСоответствия.Значение));
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатуры.СтавкаНДС");
		ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = ЗначениеСоответствия.Ключ;
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаНоменклатурыНоменклатураСтавкаНДС");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагСуммы(НаборЦветов)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ТаблицаСумм.НоменклатураРаспознано"));
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммНоменклатура");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.Номенклатура");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммНоменклатура");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.СтавкаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтавкаНДС");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.Количество");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммКоличество");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.Цена");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммЦена");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.Сумма");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСумма");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ОшибкаЦены");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммКоличество");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммЦена");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСумма");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ОшибкаНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтавкаНДС");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСуммаНДС");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ОшибкаВсего");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.СуммаВключаетНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСумма");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСуммаНДС");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммВсего");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ОшибкаВсего");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.СуммаВключаетНДС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСумма");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммВсего");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ОшибкаЦены");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ВидСкидки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСуммаСкидки");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаСумм.ВидСкидки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСуммаСкидки");
	
	РаспознаваниеДокументовСлужебный.УстановитьУсловноеОформлениеШагСуммыГрупповаяОбработка(
		ЭтотОбъект, НаборЦветов);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШагИтого(НаборЦветов)
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплекты.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплектыНаправлениеДокумента");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплектыДата");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплектыТипДокумента");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплекты.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаИтогоКомплектыНазваниеКомплекта");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииЯчейкиШапки() Экспорт
	
	ТаблицаЭлемент = Элементы.РеквизитыШапки;
	ДанныеЭтойСтроки = ТаблицаЭлемент.ТекущиеДанные;
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДляТаблиц
		Или Элементы.СтраницыТаблиц.ТекущаяСтраница <> Элементы.СтраницаШапка
		Или ДанныеЭтойСтроки = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	Если ПолнаяКартинкаВидна И ТипЗнч(Элементы.ПолеПросмотра.Документ) <> Тип("ВнешнийОбъект") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиШапки", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(ТаблицаЭлемент.ТекущийЭлемент.Имя, "Шапка", "");
	
	ОбновитьПолнуюКартинку = (ПолнаяКартинкаВидна И ТекущаяСтрокаДляСпискаВыбора <> ТаблицаЭлемент.ТекущаяСтрока);
	ТекущаяСтрокаДляСпискаВыбора = ТаблицаЭлемент.ТекущаяСтрока;
	ТекущееИмяРеквизита = ИмяРеквизита;
	
	ПриАктивизацииЯчейкиШапки(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора)
	
	РаспознаваниеДокументовКлиентСерверПереопределяемый.ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора);

КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииЯчейкиШапки(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку)
	
	ИмяКолонки = ТаблицаЭлемент.ТекущийЭлемент.Имя;
	ТекущиеДанные = ТаблицаЭлемент.ТекущиеДанные;
	
	Если ИмяРеквизита = "НаправлениеДокумента"
		Или ИмяРеквизита = "КартинкаОткрытия"
		Или ИмяРеквизита = "ТипДокумента"
		Или ИмяРеквизита = "СуммаВключаетНДС"
		Или ИмяРеквизита = "ВидСкидки"
		Или ИмяРеквизита = "Склад" 
		Или ИмяРеквизита = "Ошибка"
		Или ИмяРеквизита = "ВидДокумента" 
		Или ИмяРеквизита = "Соглашение" 
		Или ИмяРеквизита = "Подразделение" Тогда
		
		Если ОбновитьПолнуюКартинку И HTMLДокументСформирован Тогда
			АдресПолнойКартинки = ПолучитьАдресПолнойКартинки(ТекущиеДанные.Ссылка, УникальныйИдентификатор);
			РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресПолнойКартинки);
		КонецЕсли;
		
		Если ИмяРеквизита <> "НаправлениеДокумента"
			И ИмяРеквизита <> "КартинкаОткрытия" Тогда
			
			КартинкаРеквизита = КартинкаКакТекстHTML("");
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("Ссылка", ТекущиеДанные.Ссылка);
	ПараметрыПолучения.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыПолучения.Вставить("ОбновитьПолнуюКартинку", ОбновитьПолнуюКартинку);
	ПараметрыПолучения.Вставить("Направление", ТекущиеДанные.Направление);
	ПараметрыПолучения.Вставить("ТипДокумента", ТекущиеДанные.ТипДокумента);
	ПараметрыПолучения.Вставить("ИмяРеквизита", ИмяРеквизита);
	ПараметрыПолучения.Вставить("ИсточникПоляБанковскийСчетВладелец", ИсточникПоляБанковскийСчетВладелец);
	ПараметрыПолучения.Вставить("ИсточникПоляДоговорВидДоговора", ИсточникПоляДоговорВидДоговора.ВыгрузитьЗначения());
	ПараметрыПолучения.Вставить("ИсточникПоляДоговорКонтрагент", ИсточникПоляДоговорКонтрагент);
	ПараметрыПолучения.Вставить("ИсточникПоляДоговорОрганизация", ИсточникПоляДоговорОрганизация);
	ДанныеЯчейки = РаспознаваниеДокументовКомплектыВызовСервера.ДанныеЯчейкиШапкиНаСервере(ПараметрыПолучения);
	
	Если ОбновитьПолнуюКартинку И ДанныеЯчейки.Свойство("АдресПолнойКартинки") Тогда
		АдресПолнойКартинки = ДанныеЯчейки.АдресПолнойКартинки;
		Если HTMLДокументСформирован Тогда
			РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресПолнойКартинки);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЯчейки.Свойство("КартинкаВнутриHTML") Тогда
		КартинкаРеквизита = ДанныеЯчейки.КартинкаВнутриHTML;
	КонецЕсли;
	
	ЭлементКолонка = Элементы[ИмяКолонки];
	ЭлементКолонка.СписокВыбора.Очистить();
	ТипЗначения = ТипЗнч(ТекущиеДанные[ТекущееИмяРеквизита]);
	
	Если ИмяРеквизита = "Организация" Или СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено Тогда
		КартинкаСоздание = 2;
	Иначе
		КартинкаСоздание = Неопределено;
	КонецЕсли;
	
	Если ДанныеЯчейки.ЗначенияВыбора.Количество() Тогда
		Если КартинкаСоздание = Неопределено Тогда
			СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ПолучитьЗначенияСпискаВыбора(
				ДанныеЯчейки.РаспознанныйТекст, ТипЗначения, ДанныеЯчейки.ЗначенияВыбора);
		Иначе
			СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(
				ДанныеЯчейки.РаспознанныйТекст, ДанныеЯчейки.ЗначенияВыбора, КартинкаСоздание);
		КонецЕсли;
	Иначе
		Если КартинкаСоздание = Неопределено Тогда
			СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ПолучитьЗначенияСпискаВыбора(
				ДанныеЯчейки.РаспознанныйТекст, ТипЗначения, Неопределено);
		Иначе
			СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьСписокДляВыбораПользователем(
				ДанныеЯчейки.РаспознанныйТекст, Неопределено, КартинкаСоздание);
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
		ЭлементКолонка.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, , ДанныеВыбора.Картинка);
	КонецЦикла;
	
	Если ПолнаяКартинкаВидна Тогда
		КоординатыВыделения = ДанныеЯчейки.КоординатыКартинки;
		СтрокВИзображенииВыделения = ДанныеЯчейки.СтрокВИзображении;
		ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриблизитьПоКоординатам() Экспорт
	
	ПриблизитьПоКоординатам(КоординатыВыделения, СтрокВИзображенииВыделения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбораПоля(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ИмяРеквизита = СтрЗаменить(Элемент.Имя, "Шапка", "");
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура")
			И ВыбранноеЗначение.Свойство("СозданВФормеБРД") Тогда
			
			ВыбранноеЗначение = ВыбранноеЗначение.Ссылка;
			СозданНовыйЭлемент = Истина;
		Иначе
			// Выбрали значение из выпадающего списка
			СозданНовыйЭлемент = Ложь;
		КонецЕсли;
		
		Если СозданНовыйЭлемент Тогда
			Если ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Найти(ИмяРеквизита) = Неопределено Тогда
				ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Добавить(ИмяРеквизита);
			КонецЕсли;
			ЗаполнениеСписковВыбораШапки();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) <> Неопределено Тогда 
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	ДанныеЭтойСтроки = ТекущийЭлемент.ТекущиеДанные;
	ЗначениеПодходящейСтроки = ДанныеЭтойСтроки[ИмяРеквизита];
	
	ТипРеквизита = ТипЗнч(ЗначениеПодходящейСтроки);
	Если РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипРеквизита) Тогда
		Если ТипРеквизита = Тип("Строка") Тогда
			ВыбранноеЗначение = СокрЛП(ВыбранноеЗначение);
		Иначе
			ВыбранноеЗначение = РаспознаваниеДокументовСериализацияСлужебныйКлиентСервер.ПривестиТип(ВыбранноеЗначение, ТипРеквизита);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Если ИмяРеквизита = "Организация" Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляСозданияЭлемента = ДанныеДляСозданияЭлемента(ДанныеЭтойСтроки.Ссылка, "РеквизитыДокумента");
		
	ИменаРеквизитов = РаспознаваниеДокументовСлужебныйКлиентСервер.ИменаРеквизитовКонтрагентИОрганизация(ДанныеЭтойСтроки);
	Если ИменаРеквизитов.Свойство(ИмяРеквизита) Тогда
		ИмяРеквизита = ИменаРеквизитов[ИмяРеквизита];
	КонецЕсли;
	
	ПараметрыОткрытия = ПараметрыОткрытияФормыСозданияОбъекта(ИмяРеквизита, ДанныеДляСозданияЭлемента.НаборДанных,
		ДанныеДляСозданияЭлемента.ДополнительныеДанные, ДанныеЭтойСтроки.ТипДокумента, ДанныеЭтойСтроки.Направление,
		ЗначениеПодходящейСтроки);
		
	ИмяФормыСоздания = "";
	РаспознаваниеДокументовКлиент.ИмяФормыСозданияЭлемента(ИмяРеквизита, ИмяФормыСоздания, ПараметрыОткрытия);
	
	ОткрытьФорму(ИмяФормыСоздания, ПараметрыОткрытия, Элемент, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Функция возвращает данные для создания элемента на основе распознанного документа.
//
// Параметры:
//  РаспознанныйДокумент - ДокументСсылка.РаспознанныйДокумент - Ссылка на распознанный документ.
//  ИмяТаблицы - Строка - Имя таблицы распознанного документа ("РеквизитыДокумента" или "РеквизитыТабличныхЧастей").
//
// Возвращаемое значение:
//  Структура - Структура с двумя элементами:
//   * НаборДанных - ФиксированноеСоответствие - Набор данных для создания элемента.
//   * ДополнительныеДанные - Структура - Дополнительные данные для создания элемента.
&НаСервереБезКонтекста
Функция ДанныеДляСозданияЭлемента(РаспознанныйДокумент, ИмяТаблицы)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныйДокумент", РаспознанныйДокумент);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита КАК ИмяРеквизита,
	|	РаспознанныйДокументРеквизитыДокумента.НомерСтроки КАК НомерСтроки,
	|	РаспознанныйДокументРеквизитыДокумента.РаспознанныйТекст КАК РаспознанныйТекст,
	|	РаспознанныйДокументРеквизитыДокумента.СтрокВИзображении КАК СтрокВИзображении,
	|	РаспознанныйДокументРеквизитыДокумента.Значение КАК Значение,
	|	РаспознанныйДокументРеквизитыДокумента.КоординатаX0 КАК КоординатаX0,
	|	РаспознанныйДокументРеквизитыДокумента.КоординатаY0 КАК КоординатаY0,
	|	РаспознанныйДокументРеквизитыДокумента.КоординатаX1 КАК КоординатаX1,
	|	РаспознанныйДокументРеквизитыДокумента.КоординатаY1 КАК КоординатаY1
	|ИЗ
	|	Документ.РаспознанныйДокумент.РеквизитыДокумента КАК РаспознанныйДокументРеквизитыДокумента
	|ГДЕ
	|	РаспознанныйДокументРеквизитыДокумента.Ссылка = &РаспознанныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Дата КАК Дата,
	|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
	|	РаспознанныйДокумент.Направление КАК Направление
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка = &РаспознанныйДокумент";
	
	Если ИмяТаблицы = "РеквизитыТабличныхЧастей" Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "РеквизитыДокумента", "РеквизитыТабличныхЧастей");	
	КонецЕсли;
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	НаборДанных = РаспознаваниеДокументовСлужебныйКлиентСервер.НаборДанныхСозданияЭлемента(
		РезультатЗапроса[0].Выгрузить());
	
	ПараметрыВыбораДоговора = РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора(РаспознанныйДокумент);
	
	ВидыДоговоров = Новый СписокЗначений;
	ВидыДоговоров.ЗагрузитьЗначения(ПараметрыВыбораДоговора.ВидыДоговоров);
	
	ДанныеДокумента = РезультатЗапроса[1].Выбрать();
	ДанныеДокумента.Следующий();
	ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РаспознанныйДокумент, 
		"Дата, Направление, ТипДокумента", Истина);
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("РаспознанныйДокумент", РаспознанныйДокумент);
	ДополнительныеДанные.Вставить("ИмяТаблицы", ИмяТаблицы);
	ДополнительныеДанные.Вставить("ЮрФизЛицоПоОрганизационнойФорме", 
		РаспознаваниеДокументовСлужебный.ЮрФизЛицоПоОрганизационнойФорме());
	ДополнительныеДанные.Вставить("ТипДоговора", ВидыДоговоров);
	ДополнительныеДанные.Вставить("ИспользоватьПартнеровИКонтрагентов", 
		ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов"));
	ДополнительныеДанные.Вставить("ДатаДокумента", ДанныеДокумента.Дата);
	ДополнительныеДанные.Вставить("ТипДокумента", ДанныеДокумента.ТипДокумента);
	ДополнительныеДанные.Вставить("Направление", ДанныеДокумента.Направление);
	ДополнительныеДанные.Вставить("ПараметрыВыбораНоменклатуры", 
		ПараметрыВыбораНоменклатуры(ДанныеДокумента.ТипДокумента));
		
	НаправлениеИсходящее = (ДанныеДокумента.Направление = ПредопределенноеЗначение(
		"Перечисление.НаправленияРаспознанногоДокумента.Исходящий"));
	ИННКПП = "";
	
	Если ДанныеДокумента.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД") 
		Или ДанныеДокумента.ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура") Тогда
		
		Ключ = ?(НаправлениеИсходящее, "ИННКПППокупателя", "ИННКПППродавца");
		ИННКПП = НаборДанных.Получить(Ключ);
		
		Если ИННКПП = Неопределено Тогда
			ИННКПП = "";
		Иначе
			ИННКПП = ИННКПП.Значение;
		КонецЕсли;
		
	КонецЕсли;
	
	ИННКонтрагента = РаспознаваниеДокументовСлужебный.ИзвлечьИННКППИзСтроки(ИННКПП, "ИНН");
    КППКонтрагента = РаспознаваниеДокументовСлужебный.ИзвлечьИННКППИзСтроки(ИННКПП, "КПП");
	
	ДополнительныеДанные.Вставить("ИННКонтрагента", ИННКонтрагента);
	ДополнительныеДанные.Вставить("КППКонтрагента", КППКонтрагента);
	
	Возврат Новый Структура("НаборДанных, ДополнительныеДанные", НаборДанных, ДополнительныеДанные);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыОткрытияФормыСозданияОбъекта(ИмяЭлемента, 
											НаборДанных, 
											ДополнительныеДанные, 
											ТипДокумента, 
											Направление, 
											ЗначениеСтроки)

	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ЗапретОткрытияСозданныхЭлементов", Истина);
	ПараметрыОткрытия.Вставить("ИмяЭлемента", ИмяЭлемента);
	ПараметрыОткрытия.Вставить("НаборДанных", НаборДанных);
	ПараметрыОткрытия.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	ПараметрыОткрытия.Вставить("ТипДокумента", ТипДокумента);
	ПараметрыОткрытия.Вставить("Направление", Направление);
	ПараметрыОткрытия.Вставить("СоздаваемыйОбъект", ЗначениеСтроки);
	
	Возврат ПараметрыОткрытия;

КонецФункции // ПараметрыОткрытияФормыСозданияОбъекта()

&НаСервереБезКонтекста
Функция ПараметрыВыбораНоменклатуры(ТипДокумента)  
		
	ПараметрыВыбора = Новый Массив;	 
		
	Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.Работа);
	Иначе 
		
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.Работа);
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
		ПараметрыВыбора.Добавить(Перечисления.ТипыНоменклатуры.Товар); 
		
	КонецЕсли; 
		
	Возврат Новый ФиксированныйМассив(ПараметрыВыбора);

КонецФункции

&НаКлиенте
Процедура ПриблизитьПоКоординатам(Координаты, СтрокВИзображении)
	
	Если HTMLДокументСформирован И КартинкаЗагруженаВHTML И ТипЗнч(Элементы.ПолеПросмотра.Документ) = Тип("ВнешнийОбъект") Тогда
		Если РаспознаваниеДокументовКлиентСервер.ВсеКоординатыНулевые(Координаты) Тогда
			Элементы.ПолеПросмотра.Документ.defaultView.clean_bbox();
		Иначе
			Элементы.ПолеПросмотра.Документ.defaultView.zoom_to_bbox(Координаты[0], Координаты[1], Координаты[2], Координаты[3], СтрокВИзображении);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьАдресПолнойКартинки(Ссылка, ИдентификаторФормы)
	
	ИсходноеИзображение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ИсходноеИзображение");
	Если ЗначениеЗаполнено(ИсходноеИзображение) Тогда
		АдресПолнойКартинки = ПоместитьВоВременноеХранилище(ИсходноеИзображение.Получить(), ИдентификаторФормы);
	Иначе
		АдресПолнойКартинки = ПоместитьВоВременноеХранилище(Неопределено, ИдентификаторФормы);
	КонецЕсли;
	
	Возврат АдресПолнойКартинки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КартинкаКакТекстHTML(ДвоичныеДанные, Позиция = "Сбоку")
	
	Если ЗначениеЗаполнено(ДвоичныеДанные) Тогда
		DataImage = Base64Строка(ДвоичныеДанные);
		DataImage = СтрЗаменить(DataImage, Символы.ВК, "");
		DataImage = СтрЗаменить(DataImage, Символы.ПС, "");
	Иначе
		DataImage = "";
	КонецЕсли;
	DataImage = "data:image/jpg;base64," + DataImage;
	
	ШаблонHTML = 
		"<html>
		|<head>
		|  <style type=""text/css"">
		|    html, body { width: 100%; height:100%; margin: 0; padding: 0; }
		|    body {
		|      background-image: url('{{ DataImage }}');
		|      background-size: contain;
		|      background-repeat: no-repeat;
		|      background-position: left {{ Position }};
		|      scrollbar-width: none;
		|    }
		|    ::-webkit-scrollbar { display: none; }
		|  </style>
		|</head>
		|<body>
		|</body>
		|<html>";
	
	Результат = СтрЗаменить(ШаблонHTML, "{{ DataImage }}", DataImage);
	Результат = СтрЗаменить(Результат, "{{ Position }}", ?(Позиция = "Сбоку", "center", "bottom"));
	
	Возврат Результат;
	
КонецФункции

#Область ЗаписьШапкиВФоне

&НаКлиенте
Процедура ЗаполнениеСписковВыбораШапки()
	
	Результат = ЗаполнениеСписковВыбораШапкиНаСервере(ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки, УникальныйИдентификатор);
	Если Результат.ОперацияЗапущена Тогда
		
		ДанныеФоновыхОпераций.ИзмененияСписковВыбораШапки.Очистить();
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнениеСписковВыбораШапкиЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		ПовторноЗапуститьЗаполнениеСписковВыбораШапки = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнениеСписковВыбораШапкиНаСервере(ИменаРеквизитов, ИдентификаторФормы)
	
	Результат = Новый Структура("ОперацияЗапущена, ДлительнаяОперация", Ложь);
	
	КлючЗадания = "ГрупповаяОбработкаРаспознанныхДокументовСпискиВыбораШапки";
	
	ОтборЗаданий = Новый Структура;
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ОтборЗаданий.Вставить("Ключ", КлючЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	Отказ = (ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий).Количество() > 0);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не Отказ Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Групповой ввод: обновление списков выбора шапки';
																|en = 'Bulk input: header selection list update'");
		
		ИмяМетода = "РаспознаваниеДокументовСлужебный.ЗаполнитьСпискиВыбораРеквизитаШапкиДокументов";
		
		РаспознанныеДокументы = Новый Массив;
		ТаблицаСортировки = РеквизитыШапки.Выгрузить(, "Ссылка, Контрагент");
		ТаблицаСортировки.Сортировать("Контрагент"); // В начале будут строки с незаполненным Контрагентом
		Для Каждого СтрокаТаблицы Из ТаблицаСортировки Цикл
			РаспознанныеДокументы.Добавить(СтрокаТаблицы.Ссылка);
		КонецЦикла;
		
		ПараметрыОперации = Новый Структура;
		ПараметрыОперации.Вставить("ИменаРеквизитов", ИменаРеквизитов);
		ПараметрыОперации.Вставить("РаспознанныеДокументы", РаспознанныеДокументы);
		
		Результат.ОперацияЗапущена = Истина;
		Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыОперации, ПараметрыВыполнения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнениеСписковВыбораШапкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // отменено пользователем
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Результат.ПодробноеПредставлениеОшибки;
		Сообщение.Сообщить();
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		Если ПовторноЗапуститьЗаполнениеСписковВыбораШапки Тогда
			ПовторноЗапуститьЗаполнениеСписковВыбораШапки = Ложь;
			ЗаполнениеСписковВыбораШапки();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки()
	
	ЕстьНезаписанныеРеквизитыШапки = Истина;
	Результат = ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапкиНаСервере(ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки, УникальныйИдентификатор);
	Если Результат.ОперацияЗапущена Тогда
		
		ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Очистить();
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапкиЗавершение", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		ПовторноЗапуститьЗаполнениеРеквизитовШапки = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапкиНаСервере(Знач ПараметрыОперации, Знач ИдентификаторФормы)
	
	ВыборГруппыСкладаЗапрещен = Ложь;
	КорректировкаВыбораГруппыСкладов(ПараметрыОперации, ВыборГруппыСкладаЗапрещен);
	Если ВыборГруппыСкладаЗапрещен Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Данная группа складов может быть выбрана только для документов с типом ""Счет на оплату.""';
				|en = 'This warehouse group can only be selected for the ""Commercial invoice"" documents.'"));
	КонецЕсли;
	
	Результат = Новый Структура("ОперацияЗапущена, ДлительнаяОперация", Ложь);
	
	КлючЗадания = "ГрупповаяОбработкаРаспознанныхДокументовРеквизитовШапки";
	
	ОтборЗаданий = Новый Структура;
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ОтборЗаданий.Вставить("Ключ", КлючЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	Отказ = (ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий).Количество() > 0);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не Отказ Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Групповой ввод реквизитов шапки';
																|en = 'Bulk input of header attributes'");
		
		ИмяМетода = "Документы.РаспознанныйДокумент.ЗаписатьИзмененияРеквизитов";
		
		Результат.ОперацияЗапущена = Истина;
		Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыОперации, ПараметрыВыполнения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // отменено пользователем
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Результат.ПодробноеПредставлениеОшибки;
		Сообщение.Сообщить();
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		Если ПовторноЗапуститьЗаполнениеРеквизитовШапки Тогда
			ПовторноЗапуститьЗаполнениеРеквизитовШапки = Ложь;
			ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
		Иначе
			ЕстьНезаписанныеРеквизитыШапки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ЗаполнитьПредупрежденияПоСкидкам()
	
	Для Каждого Строка Из РеквизитыШапки Цикл
		
		Если Не Строка.ЕстьСкидка Тогда
			Продолжить;
		КонецЕсли;
		
		ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции;
		Действие = "ПостОбработкаНеТребуется";
		РаспознаваниеДокументовПереопределяемый.ПриОбработкеДокументовСоСкидками(ВидСкидки, Строка.Направление, Действие);
		
		Если Действие = "ПотребоватьВключениеФункциональныхОпцийПоСкидкам" Тогда
			Строка.Ошибка = НСтр("ru = 'Скидки не поддерживаются';
								|en = 'Discounts are not supported'");
			Строка.ДанныеЗаполнены = Ложь;
		Иначе
			Строка.Ошибка = "";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура КорректировкаВыбораГруппыСкладов(ПараметрыОперации, ВыборГруппыСкладаЗапрещен)
	
	ДанныеСклада = Неопределено;
	МаксимальныйИндексОперации = ПараметрыОперации.ВГраница();
	Для Сч = 0 По МаксимальныйИндексОперации Цикл
		ИндексТекущейОперации = МаксимальныйИндексОперации - Сч;
		ПараметрОперации = ПараметрыОперации[ИндексТекущейОперации];
		Если ПараметрОперации.ИмяРеквизита = "Склад" И ЗначениеЗаполнено(ПараметрОперации.Значение) Тогда
			Если Не ПараметрОперации.ТипДокумента = ПредопределенноеЗначение(
				"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") Тогда
				
				Если ДанныеСклада = Неопределено Тогда
					ДанныеСклада = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрОперации.Значение,
						"ЭтоГруппа, ВыборГруппы", Истина);
				КонецЕсли;
				
				Если ДанныеСклада.ЭтоГруппа И ДанныеСклада.ВыборГруппы = ПредопределенноеЗначение(
					"Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах") Тогда
					ВыборГруппыСкладаЗапрещен = Истина;
					ТекущаяСтрока = ТаблицаДопРеквизитов.НайтиПоИдентификатору(ПараметрОперации.ИдентификаторСтроки);
					ТекущаяСтрока.Склад = Справочники.Склады.ПустаяСсылка();
					ПараметрыОперации.Удалить(ИндексТекущейОперации);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыДублей

&НаКлиенте
Функция ПодсчетНезаполненныхСтрокДублей()
	
	КоличествоНеЗаполнено = 0;
	Для Каждого СтрокаТаблицы Из ТаблицаДублей Цикл
		Если Не СтрокаТаблицы.ЕстьОсновной Тогда
			КоличествоНеЗаполнено = КоличествоНеЗаполнено + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоличествоНеЗаполнено;
	
КонецФункции

&НаКлиенте
Процедура ПерейтиНаСтраницуДублиЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПерейтиНаСтраницуДубли();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуДубли()
	
	Если ЕстьНезаписанныеРеквизитыШапки Тогда
		ПодключитьОбработчикОжидания("ПерейтиНаСтраницуДубли", 0.5, Истина);
		Возврат;
	КонецЕсли;
	
	ПерейтиНаСтраницуДублиНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуДублиНаСервере()
	
	ТекущийШаг = "Дубли";
	
	ТекущаяСтрокаДублей = -1;
	Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаДубли;
	Элементы.КнопкаНазад.Видимость = Истина;
	
	ЗаполнитьТаблицуДублей();
	СтрокаТаблицы = ПоляПросмотраДублей.Добавить();
	СтрокаТаблицы.ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML(УникальныйИдентификатор);
	СтрокаТаблицы = ПоляПросмотраДублей.Добавить();
	СтрокаТаблицы.ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML(УникальныйИдентификатор);
	
	ВыделитьШагТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДублей()
	
	// Первым должен стать наилучший из дублей. А лучшим является дубль:
	//	1. С бОльшим количеством печатей
	//	2. С бОльшим количество строк в табличной части
	//	3. С меньшим количеством ошибок в табличной части
	//	4. С меньшим количеством ошибок по документу в целом
	
	РаспознанныеДокументы = ВыбранныеДокументы();
	ТаблицаОшибок = РаспознаваниеДокументовКомплекты.ОшибкиПоДокументам(РаспознанныеДокументы);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", РаспознанныеДокументы);
	Запрос.УстановитьПараметр("ТаблицаОшибок", ТаблицаОшибок);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаОшибок.Ссылка КАК Ссылка,
	|	ТаблицаОшибок.КоличествоОшибок КАК КоличествоОшибок,
	|	ТаблицаОшибок.КоличествоОшибокВТаблицах КАК КоличествоОшибокВТаблицах
	|ПОМЕСТИТЬ ВТ_ТаблицаОшибок
	|ИЗ
	|	&ТаблицаОшибок КАК ТаблицаОшибок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокументПометкиНаДокументе.Ссылка КАК Ссылка,
	|	КОЛИЧЕСТВО(РаспознанныйДокументПометкиНаДокументе.ТипПометки) КАК Печатей
	|ПОМЕСТИТЬ КоличествоПечатей
	|ИЗ
	|	Документ.РаспознанныйДокумент.ПометкиНаДокументе КАК РаспознанныйДокументПометкиНаДокументе
	|ГДЕ
	|	РаспознанныйДокументПометкиНаДокументе.Ссылка В(&РаспознанныеДокументы)
	|	И РаспознанныйДокументПометкиНаДокументе.ТипПометки = ЗНАЧЕНИЕ(Перечисление.ТипыПометокРаспознанногоДокумента.Печать)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспознанныйДокументПометкиНаДокументе.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка КАК Ссылка,
	|	МАКСИМУМ(РаспознанныйДокументРеквизитыТабличныхЧастей.НомерСтрокиТЧ) КАК СтрокТЧ
	|ПОМЕСТИТЬ КоличествоСтрокТЧ
	|ИЗ
	|	Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК РаспознанныйДокументРеквизитыТабличныхЧастей
	|ГДЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка В(&РаспознанныеДокументы)
	|	И НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.СтрокаУдалена
	|
	|СГРУППИРОВАТЬ ПО
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Ссылка КАК Ссылка,
	|	РаспознанныйДокумент.Статус КАК Статус,
	|	РаспознанныйДокумент.Направление КАК Направление,
	|	ВЫБОР
	|		КОГДА РаспознанныйДокумент.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияРаспознанногоДокумента.Входящий)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК НаправлениеДокумента,
	|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
	|	РаспознанныйДокумент.НомерДокумента КАК НомерДокумента,
	|	РаспознанныйДокумент.ДатаДокумента КАК ДатаДокумента,
	|	РаспознанныйДокумент.СуммаДокумента КАК ИтогоВсего,
	|	РаспознанныйДокумент.Контрагент КАК Контрагент,
	|	РаспознанныйДокумент.Организация КАК Организация,
	|	РаспознанныйДокумент.Наименование КАК Наименование,
	|	ВТ_ТаблицаОшибок.КоличествоОшибокВТаблицах КАК КоличествоОшибокВТаблицах,
	|	ВТ_ТаблицаОшибок.КоличествоОшибок КАК КоличествоОшибок
	|ПОМЕСТИТЬ ОбщийСписок
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаОшибок КАК ВТ_ТаблицаОшибок
	|		ПО РаспознанныйДокумент.Ссылка = ВТ_ТаблицаОшибок.Ссылка
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка В(&РаспознанныеДокументы)
	|	И РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|	И РаспознанныйДокумент.НомерДокумента <> """"
	|	И РаспознанныйДокумент.ДатаДокумента <> ДАТАВРЕМЯ(1, 1, 1)
	|	И РаспознанныйДокумент.СуммаДокумента <> 0
	|	И РаспознанныйДокумент.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И РаспознанныйДокумент.Организация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбщийСписок.Ссылка КАК Ссылка,
	|	ОбщийСписок.Статус КАК Статус,
	|	ОбщийСписок.Направление КАК Направление,
	|	ОбщийСписок.НаправлениеДокумента КАК НаправлениеДокумента,
	|	ОбщийСписок.ТипДокумента КАК ТипДокумента,
	|	ОбщийСписок.НомерДокумента КАК НомерДокумента,
	|	ОбщийСписок.ДатаДокумента КАК ДатаДокумента,
	|	ОбщийСписок.ИтогоВсего КАК ИтогоВсего,
	|	ОбщийСписок.Контрагент КАК Контрагент,
	|	ОбщийСписок.Организация КАК Организация,
	|	ОбщийСписок.Наименование КАК Наименование,
	|	ЕСТЬNULL(КоличествоПечатей.Печатей, 0) КАК Печатей,
	|	ЕСТЬNULL(КоличествоСтрокТЧ.СтрокТЧ, 0) КАК СтрокТЧ,
	|	ОбщийСписок.КоличествоОшибокВТаблицах КАК ОшибокТЧ,
	|	ОбщийСписок.КоличествоОшибок КАК ОшибокВсего
	|ИЗ
	|	ОбщийСписок КАК ОбщийСписок
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоПечатей КАК КоличествоПечатей
	|		ПО ОбщийСписок.Ссылка = КоличествоПечатей.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ КоличествоСтрокТЧ КАК КоличествоСтрокТЧ
	|		ПО ОбщийСписок.Ссылка = КоличествоСтрокТЧ.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбщийСписок КАК ОбщийСписокДубли
	|		ПО ОбщийСписок.НомерДокумента = ОбщийСписокДубли.НомерДокумента
	|			И ОбщийСписок.ДатаДокумента = ОбщийСписокДубли.ДатаДокумента
	|			И ОбщийСписок.ИтогоВсего = ОбщийСписокДубли.ИтогоВсего
	|			И ОбщийСписок.Контрагент = ОбщийСписокДубли.Контрагент
	|			И ОбщийСписок.Организация = ОбщийСписокДубли.Организация
	|			И ОбщийСписок.Направление = ОбщийСписокДубли.Направление
	|			И ОбщийСписок.ТипДокумента = ОбщийСписокДубли.ТипДокумента
	|			И ОбщийСписок.Ссылка <> ОбщийСписокДубли.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаДокумента,
	|	Контрагент,
	|	Организация,
	|	ИтогоВсего,
	|	НомерДокумента,
	|	Направление,
	|	ТипДокумента,
	|	Печатей УБЫВ,
	|	СтрокТЧ УБЫВ,
	|	ОшибокТЧ,
	|	ОшибокВсего";
	
	ПараметрыСравнения = "НомерДокумента, ДатаДокумента, ИтогоВсего, Контрагент, Организация, Направление, ТипДокумента";
	ТекущаяГруппа = Новый Структура(ПараметрыСравнения);
	
	ТаблицаДублей.Очистить();
	
	СтрокаТаблицы = Неопределено;
	КоличествоДублей = 0;
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Элементы.СтраницыЕстьНетДублей.ТекущаяСтраница = Элементы.СтраницаНетДублей;
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		Если ЭтоНоваяГруппаДублей(ТекущаяГруппа, Выборка) Тогда
			ЗаполнитьЗначенияСвойств(ТекущаяГруппа, Выборка);
			
			Если СтрокаТаблицы <> Неопределено Тогда
				СтрокаТаблицы.ЗначенияРеквизитов = СтрокаТаблицы.ЗначенияРеквизитов + КоличествоДублей;
			КонецЕсли;
			КоличествоДублей = 1;
			
			СтрокаТаблицы = ТаблицаДублей.Добавить();
			СтрокаТаблицы.ЗначенияРеквизитов =
				СтрШаблон(НСтр("ru = 'Тип документа: %1
				|Номер: %2
				|Дата: %3
				|Контрагент: %4
				|Организация: %5
				|Сумма: %6
				|Дублей: ';
				|en = 'Document type: %1
				|Number: %2
				|Date: %3
				|Counterparty: %4
				|Company: %5
				|Amount: %6
				|Duplicates:'"),
				Выборка.ТипДокумента,
				Выборка.НомерДокумента,
				Формат(Выборка.ДатаДокумента, "ДЛФ=D"),
				Выборка.Контрагент,
				Выборка.Организация,
				Формат(Выборка.ИтогоВсего, "ЧДЦ=2")
			);
		Иначе
			КоличествоДублей = КоличествоДублей + 1;
		КонецЕсли;
		
		НоваяСтрока = СтрокаТаблицы.РаспознанныеДокументы.Добавить();
		НоваяСтрока.Ссылка = Выборка.Ссылка;
	КонецЦикла;
	
	Если СтрокаТаблицы <> Неопределено Тогда
		СтрокаТаблицы.ЗначенияРеквизитов = СтрокаТаблицы.ЗначенияРеквизитов + КоличествоДублей;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоНоваяГруппаДублей(ТекущаяГруппа, НоваяГруппа)
	
	Для Каждого КлючЗначение Из ТекущаяГруппа Цикл
		Если КлючЗначение.Значение <> НоваяГруппа[КлючЗначение.Ключ] Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииСтрокиДублей() Экспорт
	
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДубли
		Или ТекущаяСтрокаДублей = Элементы.ТаблицаДублей.ТекущаяСтрока Тогда
		Возврат;
	КонецЕсли;
	ТекущаяСтрокаДублей = Элементы.ТаблицаДублей.ТекущаяСтрока;
	ТекущиеДанные = Элементы.ТаблицаДублей.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		// Дублей нет
		Возврат;
	КонецЕсли;
	
	ДанныеДублей = ДанныеДублейНаСервере(ТаблицаДублей.Индекс(ТекущиеДанные));
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьКартинкиДублей", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗагрузитьКартинкиДублей() Экспорт
	
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДубли
		Или Элементы.ТаблицаДублей.ТекущиеДанные = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	СколькоHTMLДолжноБытьВидно = Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы.Количество();
	СколькоHTMLСформировано = 0;
	Для Каждого СтрокаHTMLСформирован Из HTMLДублейСформирован Цикл
		Если СтрокаHTMLСформирован.Значение Тогда
			СколькоHTMLСформировано = СколькоHTMLСформировано + 1;
		Иначе
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если СколькоHTMLДолжноБытьВидно > СколькоHTMLСформировано Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьКартинкиДублей", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	Если ПолученыДанныеСтрокиДублей <> Элементы.ТаблицаДублей.ТекущаяСтрока Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьКартинкиДублей", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаДублей Из Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы Цикл
		Ид = Элементы.ТаблицаДублей.ТекущиеДанные.РаспознанныеДокументы.Индекс(СтрокаДублей);
		Если Элементы["ПолеПросмотра" + Ид].Документ = Неопределено Тогда
			ПодключитьОбработчикОжидания("Подключаемый_ЗагрузитьКартинкиДублей", 0.3, Истина);
			Возврат;
		КонецЕсли;
		ДвоичныеДанные = ДанныеДублей.Получить(СтрокаДублей.Ссылка);
		ЗагрузитьКартинкуПоАдресу(Элементы["ПолеПросмотра" + Ид], ДвоичныеДанные);
		
		ДокументHTML = ДокументHTML(Элементы["ПолеПросмотра" + Ид]);
		Если ДокументHTML = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ДокументHTML.showStatusStrip(СтрокаКнопокДляДублей);
		ДокументHTML.setMain(СтрокаДублей.Основной);
		ДокументHTML.setDuplicate(Не СтрокаДублей.УбратьИзДублей);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКартинкуПоАдресу(ЭлементФормы, ДвоичныеДанные)
	
	Если ДвоичныеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	DataImage = Base64Строка(ДвоичныеДанные);
	DataImage = СтрЗаменить(DataImage, Символы.ВК, "");
	DataImage = СтрЗаменить(DataImage, Символы.ПС, "");
	
	Если ЭлементФормы.Документ <> Неопределено Тогда
		ЭлементКартинкиПоляHTML = ЭлементФормы.Документ.getElementById("image_setter");
		Если ЭлементКартинкиПоляHTML <> Неопределено Тогда
			ЭлементКартинкиПоляHTML.setAttribute("data-img", DataImage);
			ЭлементКартинкиПоляHTML.click();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДанныеДублейНаСервере(ИндексСтроки)
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("РаспознанныеДокументы", ТаблицаДублей[ИндексСтроки].РаспознанныеДокументы.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
	ПолейПросмотра = 2 * Элементы.ГруппаКартинкиДублей.ПодчиненныеЭлементы.Количество();
	КоличествоДокументов = ПараметрыПолучения.РаспознанныеДокументы.Количество();
	Если ПолейПросмотра < КоличествоДокументов Тогда
		
		Пока ПолейПросмотра < КоличествоДокументов Цикл
			ПолейПросмотра = ПолейПросмотра + 2;
			
			СтрокаТаблицы = ПоляПросмотраДублей.Добавить();
			СтрокаТаблицы.ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML(УникальныйИдентификатор);
			СтрокаТаблицы = ПоляПросмотраДублей.Добавить();
			СтрокаТаблицы.ПолеПросмотра = РаспознаваниеДокументовСлужебный.МакетОтображенияКартинкиДокументаHTML(УникальныйИдентификатор);
			
			НоваяГруппа = Элементы.Добавить("ГруппаКартинкиДублей" + ПолейПросмотра, Тип("ГруппаФормы"), Элементы.ГруппаКартинкиДублей);
			НоваяГруппа.Вид = Элементы.ГруппаКартинкиДублей2.Вид;
			ЗаполнитьЗначенияСвойств(НоваяГруппа, Элементы.ГруппаКартинкиДублей2, "Отображение, Группировка, ОтображатьЗаголовок");
			
			Для НомерПоля = ПолейПросмотра - 2 По ПолейПросмотра - 1 Цикл
				ГруппаСтраницы = Элементы.Добавить("СтраницыПолеПросмотра" + НомерПоля, Тип("ГруппаФормы"), НоваяГруппа);
				ГруппаСтраницы.Вид = Элементы.СтраницыПолеПросмотра1.Вид;
				ЗаполнитьЗначенияСвойств(ГруппаСтраницы, Элементы.СтраницыПолеПросмотра1, "ОтображениеСтраниц, Подсказка");
				
				ГруппаПоля = Элементы.Добавить("ГруппаПолеПросмотра" + НомерПоля, Тип("ГруппаФормы"), ГруппаСтраницы);
				ГруппаПоля.Вид = Элементы.ГруппаПолеПросмотра1.Вид;
				ЗаполнитьЗначенияСвойств(ГруппаПоля, Элементы.ГруппаПолеПросмотра1, "Группировка, ОтображатьЗаголовок, ЦветФона");
				
				НовыйЭлемент = Элементы.Добавить("ПолеПросмотра" + НомерПоля, Тип("ПолеФормы"), ГруппаПоля);
				НовыйЭлемент.Вид = Элементы.ПолеПросмотра1.Вид;
				НовыйЭлемент.ПутьКДанным = СтрЗаменить(Элементы.ПолеПросмотра1.ПутьКДанным, "1", НомерПоля);
				НовыйЭлемент.УстановитьДействие("ДокументСформирован", "ПолеПросмотраДокументСформирован");
				НовыйЭлемент.УстановитьДействие("ПриНажатии", "ПолеПросмотраПриНажатии");
				
				ЗаполнитьЗначенияСвойств(
					НовыйЭлемент,
					Элементы.ПолеПросмотра1,
					"АвтоМаксимальнаяВысота,
					|АвтоМаксимальнаяШирина,
					|Высота,
					|МаксимальнаяВысота,
					|МаксимальнаяШирина,
					|РастягиватьПоВертикали,
					|РастягиватьПоГоризонтали,
					|Ширина,
					|ВажностьПриОтображении,
					|ВертикальноеПоложение,
					|ВертикальноеПоложениеВГруппе,
					|Видимость,
					|ВысотаЗаголовка,
					|ГоризонтальноеПоложение,
					|ГоризонтальноеПоложениеВГруппе,
					|Доступность,
					|Заголовок,
					|ОтображениеПодсказки,
					|Подсказка,
					|ПоложениеЗаголовка,
					|ПропускатьПриВводе,
					|ТолькоПросмотр,
					|ЦветРамки,
					|ЦветТекстаЗаголовка,
					|ШрифтЗаголовка"
				);
				
				ГруппаПоля = Элементы.Добавить("ГруппаДекорацияПустаяДубль" + НомерПоля, Тип("ГруппаФормы"), ГруппаСтраницы);
				ГруппаПоля.Вид = Элементы.ГруппаДекорацияПустаяДубль1.Вид;
				ЗаполнитьЗначенияСвойств(ГруппаПоля, Элементы.ГруппаДекорацияПустаяДубль1, "Группировка, ОтображатьЗаголовок");
				
				НовыйЭлемент = Элементы.Добавить("ДекорацияПустаяДубль" + НомерПоля, Тип("ДекорацияФормы"), ГруппаПоля);
				НовыйЭлемент.Вид = Элементы.ДекорацияПустаяДубль1.Вид;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Для Ид = 0 По ПолейПросмотра - 1 Цикл
		ГруппаСтраницы = Элементы["СтраницыПолеПросмотра" + Ид];
		Если Ид < КоличествоДокументов Тогда
			ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаПолеПросмотра" + Ид];
		Иначе
			ГруппаСтраницы.ТекущаяСтраница = Элементы["ГруппаДекорацияПустаяДубль" + Ид];
		КонецЕсли;
	КонецЦикла;
	
	ИдВидимойСдвоеннойГруппы = 2 * Окр(КоличествоДокументов / 2);
	Ид = ПолейПросмотра;
	Пока Ид > 2 Цикл
		СдвоеннаяГруппа = Элементы["ГруппаКартинкиДублей" + Ид];
		Если Не СдвоеннаяГруппа.Видимость И Ид <= ИдВидимойСдвоеннойГруппы Тогда
			Пока HTMLДублейСформирован.Количество() < Ид Цикл
				HTMLДублейСформирован.Добавить(Ложь);
			КонецЦикла;
			HTMLДублейСформирован[Ид - 1].Значение = Ложь;
			HTMLДублейСформирован[Ид - 2].Значение = Ложь;
		КонецЕсли;
		Элементы["ГруппаКартинкиДублей" + Ид].Видимость = (Ид <= ИдВидимойСдвоеннойГруппы);
		Ид = Ид - 2;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", ПараметрыПолучения.РаспознанныеДокументы);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокументРеквизитыДокумента.Ссылка КАК Ссылка,
	|	РаспознанныйДокументРеквизитыДокумента.ИсходноеИзображение КАК ИсходноеИзображение
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокументРеквизитыДокумента
	|ГДЕ
	|	РаспознанныйДокументРеквизитыДокумента.Ссылка В(&РаспознанныеДокументы)";
	
	Результат = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.Ссылка, Выборка.ИсходноеИзображение.Получить());
	КонецЦикла;
	
	ПолученыДанныеСтрокиДублей = Элементы.ТаблицаДублей.ТекущаяСтрока;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыКомплектов

&НаКлиенте
Процедура Подключаемый_АктивизироватьПоследнийКомплект()
	
	ИмяТаблицы = "Комплект" + Формат(КоличествоКомплектов, "ЧГ=");
	ТекущийЭлемент = Элементы[ИмяТаблицы];
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуКомплектыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ТекущийШаг = ДополнительныеПараметры.ИмяШага;
		НужноОбновитьКомплекты = Истина;
		Элементы.СтраницыДляКомплектов.ТекущаяСтраница = Элементы.СтраницаКомплектыЗагрузка;
		Элементы.СтраницыШаги.ТекущаяСтраница = Элементы.СтраницаКомплекты;
		ПодключитьОбработчикОжидания("Подключаемый_ПодключаемыйЗагрузитьКомплекты", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПодключаемыйЗагрузитьКомплекты()
	
	Обработчик = Новый ОписаниеОповещения("ПослеФормированияКомплектов", ЭтотОбъект);
	
	ДлительнаяОперация = НачатьФормированиеКомплектовВФоне();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		ДлительнаяОперация,
		Обработчик,
		ПараметрыОжидания
	);
	
КонецПроцедуры

&НаСервере
Функция НачатьФормированиеКомплектовВФоне()
	
	ВыделитьШагТекущейСтраницы();
	
	// Комплекты могут формироваться несколько раз – нужно удалить все предыдущие
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		УдалитьКомплектНаСервере(НомерКомплекта, Ложь, Ложь);
	КонецЦикла;
	КоличествоКомплектов = 0;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"РаспознаваниеДокументовКомплекты.ДанныеКомплектов",
		ВыбранныеДокументы()
	);
	
КонецФункции

&НаКлиенте
Процедура ПослеФормированияКомплектов(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресРезультата = "";
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("АдресРезультата", АдресРезультата);
	КонецЕсли;
	
	ПерейтиНаСтраницуКомплектыНаСервере(АдресРезультата);
	ОбновитьВидимостьНадписиПроЗаполненность();
	Элементы.СтраницыДляКомплектов.ТекущаяСтраница = Элементы.СтраницаКомплектыОсновная;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьДублиОбработанными()
	
	Для Каждого СтрокаДублей Из ТаблицаДублей Цикл
		ОсновнойДокумент = Неопределено;
		ДокументыДубли = Новый Массив;
		ДокументыНеДубли = Новый Массив;
		Для Каждого СтрокаСписка Из СтрокаДублей.РаспознанныеДокументы Цикл
			Если СтрокаСписка.УбратьИзДублей Тогда
				ДокументыНеДубли.Добавить(СтрокаСписка.Ссылка);
				Продолжить;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(ОсновнойДокумент) И СтрокаСписка.Основной Тогда
				ДокументыНеДубли.Добавить(СтрокаСписка.Ссылка);
				ОсновнойДокумент = СтрокаСписка.Ссылка;
				Продолжить;
			КонецЕсли;
			
			ДокументыДубли.Добавить(СтрокаСписка.Ссылка);
		КонецЦикла;
		
		Если Не ЗначениеЗаполнено(ОсновнойДокумент) И ДокументыДубли.Количество() > 0 Тогда
			// Основным считаем первый документ, т.к. они расположены, чтобы первым был наилучший из дублей
			ДокументыНеДубли.Добавить(ДокументыДубли[0]);
			ОсновнойДокумент = ДокументыДубли[0];
			ДокументыДубли.Удалить(0);
		КонецЕсли;
		
		Для Каждого ДокументНеДубль Из ДокументыНеДубли Цикл
			ДокументОбъект = ДокументНеДубль.ПолучитьОбъект();
			ДокументОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый;
			ДокументОбъект.Записать();
		КонецЦикла;
		
		Для Каждого ДокументДубль Из ДокументыДубли Цикл
			ДокументОбъект = ДокументДубль.ПолучитьОбъект();
			ДокументОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
			ДокументОбъект.Записать();
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуКомплектыНаСервере(АдресДанныеКомплектов)
	
	// Нужно проверить и отметить обработанными дубли. Это сделано тут для единого серверного вызова
	ОтметитьДублиОбработанными();
	
	Если НужноОбновитьКомплекты Тогда
		НужноОбновитьКомплекты = Ложь;
		
		Элементы.ГруппаКнопкиТаблицы.Видимость = Ложь;
		Элементы.ДанныеДокументов.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерКомплекта, Статус",
			0, Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый);
		
		Результат = ПолучитьИзВременногоХранилища(АдресДанныеКомплектов);
		ВсеДанныеДокументов = Результат.ВсеДанныеДокументов;
		ДанныеКомплектов    = Результат.ДанныеКомплектов;
		
		КоличествоКомплектов = ДанныеКомплектов.КоличествоКомплектов;
		
		ВсеДанныеДокументов.ОбщаяТаблицаДокументов.Сортировать("Контрагент Убыв, Организация Убыв, ДатаДокумента Убыв");
		ДанныеДокументов.Загрузить(ВсеДанныеДокументов.ОбщаяТаблицаДокументов);
		
		Для Каждого СтрокаТаблицы Из ДанныеДокументов Цикл
			СтрокаТаблицы.ОшибокДляКомплекта = РаспознаваниеДокументовСлужебный.ПроблемныеРеквизиты(
					СтрокаТаблицы.Ссылка.ПолучитьОбъект()
				).Количество();
			Если СтрокаТаблицы.НомерКомплекта = 0 Тогда
				// На форме в списке "не комплектов" отображается количество ошибок, где значение совсем не заполнено
				// (заполненные поля, подсвеченные красным не считаются). Ошибки в комплектах заполняются процедурой ОбновитьОшибкиКомплекта
				СтрокаТаблицы.Ошибок = СтрокаТаблицы.ОшибокДляКомплекта;
			КонецЕсли;
		КонецЦикла;
		
		РезультатОбратнойСвязи = Новый Структура("Комплекты, Отправить", Новый Соответствие, Новый Структура);
		Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
			РезультатОбратнойСвязи.Комплекты.Вставить(НомерКомплекта, Новый Соответствие);
			НовыйКомплектНаСервере(Ложь, НомерКомплекта);
		КонецЦикла;
		НастроитьПанельИОшибкиКомплекта();
		
		ОбратнаяСвязьКомплектов = РезультатОбратнойСвязи.Комплекты;
		Для Каждого СтрокаКомплекта Из ДанныеКомплектов.ТаблицаКомплектов Цикл
			ОбратнаяСвязьКомплектов[СтрокаКомплекта.НомерКомплекта].Вставить(СтрокаКомплекта.Ссылка, "ДобавленАвтоматически");
		КонецЦикла;
		
		ОбновитьКоличествоДокументов();
		
		НадписьВсеЗаполнено = НСтр("ru = 'Сформированы все комплекты, которые можно определить автоматически. Нажмите кнопку ""Далее""';
									|en = 'All kits that can be automatically defined are generated. Click Next'");
		Элементы.НадписьВсеЗаполнено.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОшибкиКомплекта(НомерКомплекта)
	
	СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	Если СтрокиКомплекта.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
	Если СтрокиКомплекта.Количество() > 1 Тогда
		ВысотаТаблицы = 3 + 2 * СтрокиКомплекта.Количество();
	Иначе
		ВысотаТаблицы = 7;
	КонецЕсли;
	Элементы[ИмяТаблицы].Высота = ВысотаТаблицы;
	Элементы[ИмяТаблицы].МаксимальнаяВысота = ВысотаТаблицы;
	
	ТипКомплекта = СтрокиКомплекта[0].ТипКомплекта;
	Если РаспознаваниеДокументовКомплектыКлиентСервер.ЭтоТипКомплектаМожноОбработать(ТипКомплекта) Тогда
		
		// Определяем основной документ
		Направление = СтрокиКомплекта[0].Направление;
		ПараметрыДляТипов = Новый Структура("ТипКомплекта, НаправлениеДокумента", ТипКомплекта,
			Направление);
		ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(СтрокиКомплекта[0].Ссылка,
			"ВидДокумента", "");
		Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
			ВидДокумента = ?(Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий, "ПТИУ", "РТИУ");	
		КонецЕсли;
		ПараметрыДляТипов.Вставить("ВидДокумента", ВидДокумента);
		
		ТипыДокументов1С = РаспознаваниеДокументовКомплектыКлиентСервер.СоздаваемыеДокументыКомплекта(ПараметрыДляТипов);
		
		СтрокаОсновногоДокумента = Неопределено;
		ТипыРаспознанныхДокументов = РаспознаваниеДокументовКомплектыКлиентСервер.ПодходящиеТипыРаспознанногоДокумента(ТипыДокументов1С[0]);
		Для Каждого ТипРаспознанного Из ТипыРаспознанныхДокументов Цикл
			Для Каждого СтрокаКомплекта Из СтрокиКомплекта Цикл
				Если ТипРаспознанного = СтрокаКомплекта.ТипДокумента Тогда
					СтрокаОсновногоДокумента = СтрокаКомплекта;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если СтрокаОсновногоДокумента <> Неопределено Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		СтрокаОсновногоДокумента.Ошибок = РаспознаваниеДокументовСлужебный.ПроблемныеРеквизиты(
				СтрокаОсновногоДокумента.Ссылка.ПолучитьОбъект()
			).Количество();
		
		Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
			Если СтрокаТаблицы = СтрокаОсновногоДокумента Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТаблицы.Ошибок = 0;
			
			// Заполнение ошибок для подчиненных документов
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.НомерДокумента) Тогда
				СтрокаТаблицы.Ошибок = СтрокаТаблицы.Ошибок + 1;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.ДатаДокумента) Тогда
				СтрокаТаблицы.Ошибок = СтрокаТаблицы.Ошибок + 1;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
			СтрокаТаблицы.Ошибок = РаспознаваниеДокументовСлужебный.ПроблемныеРеквизиты(
					СтрокаТаблицы.Ссылка.ПолучитьОбъект()
				).Количество();
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВыбранныеДокументы()
	
	Возврат РеквизитыШапки.Выгрузить(Новый Структура("ДанныеЗаполнены", Истина), "Ссылка").ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаСервере
Процедура НовыйКомплектНаСервере(ОбновитьТекущийЭлемент, НомерКомплекта)
	
	Если Элементы.НадписьВсеЗаполнено.Видимость Тогда
		Элементы.НадписьВсеЗаполнено.Видимость = Ложь;
	КонецЕсли;
	
	Если НомерКомплекта = 0 Тогда
		КоличествоКомплектов = КоличествоКомплектов + 1;
		НомерКомплекта = КоличествоКомплектов;
	КонецЕсли;
	ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
	
	// Просто убрать Автозаполнение у КоманднаяПанель нельзя, нужно полностью скрыть
	// КоманднаяПанель и сделать ее аналог
	КнопкиТаблицы = Элементы.Добавить("ГруппаКнопкиТаблицы" + ИмяТаблицы, Тип("ГруппаФормы"), Элементы.ГруппаКомплекты);
	КнопкиТаблицы.Вид = Элементы.ГруппаКнопкиТаблицы.Вид;
	ЗаполнитьЗначенияСвойств(КнопкиТаблицы, Элементы.ГруппаКнопкиТаблицы, "Отображение, Группировка, ОтображатьЗаголовок");
	
	// Заполнение КнопкиТаблицы начало
	ПанельКомплекта = Элементы.Добавить("ГруппаКоманднаяПанель" + ИмяТаблицы, Тип("ГруппаФормы"), КнопкиТаблицы);
	ПанельКомплекта.Вид = Элементы.ГруппаКоманднаяПанель.Вид;
	ЗаполнитьЗначенияСвойств(ПанельКомплекта, Элементы.ГруппаКоманднаяПанель, "Видимость, РастягиватьПоГоризонтали");
	
	ДекорацияКомплекта = Элементы.Добавить("ДекорацияТекст" + ИмяТаблицы, Тип("ДекорацияФормы"), КнопкиТаблицы);
	ДекорацияКомплекта.Вид = Элементы.ДекорацияТекст.Вид;
	ЗаполнитьЗначенияСвойств(
		ДекорацияКомплекта,
		Элементы.ДекорацияТекст,
		"АвтоМаксимальнаяШирина,
		|ВертикальноеПоложениеВГруппе,
		|ГоризонтальноеПоложение,
		|Заголовок,
		|РастягиватьПоГоризонтали,
		|Шрифт"
	);
	
	ДекорацияРаздвижная = Элементы.Добавить("ДекорацияРаздвижная" + ИмяТаблицы, Тип("ДекорацияФормы"), КнопкиТаблицы);
	ДекорацияРаздвижная.Вид = Элементы.ДекорацияРаздвижная.Вид;
	ЗаполнитьЗначенияСвойств(
		ДекорацияРаздвижная,
		Элементы.ДекорацияРаздвижная,
		"АвтоМаксимальнаяШирина,
		|ГоризонтальноеПоложение,
		|Заголовок,
		|РастягиватьПоГоризонтали"
	);
	
	НовоеИмяКоманды = Элементы.УбратьЛишниеИзКомплекта.Имя + ИмяТаблицы;
	НоваяКоманда = Команды.Добавить(НовоеИмяКоманды);
	НоваяКоманда.Действие = "УбратьЛишниеИзКомплекта";
	НоваяКоманда.Подсказка = Команды.УбратьЛишниеИзКомплекта.Подсказка;
	
	НоваяКнопка = Элементы.Добавить("УбратьЛишниеИзКомплекта" + ИмяТаблицы, Тип("КнопкаФормы"), КнопкиТаблицы);
	НоваяКнопка.ИмяКоманды = НовоеИмяКоманды;
	НоваяКнопка.Заголовок = Элементы.УбратьЛишниеИзКомплекта.Заголовок;
	НоваяКнопка.Доступность = Ложь;
	
	НовоеИмяКоманды = Элементы.УдалитьКомплект.Имя + ИмяТаблицы;
	НоваяКоманда = Команды.Добавить(НовоеИмяКоманды);
	ЗаполнитьЗначенияСвойств(НоваяКоманда, Команды.УдалитьКомплект, "Заголовок, Действие, Картинка, Подсказка, Отображение");
	
	КнопкаУдалить = Элементы.Добавить("УдалитьКомплект" + ИмяТаблицы, Тип("КнопкаФормы"), КнопкиТаблицы);
	КнопкаУдалить.ИмяКоманды = НовоеИмяКоманды;
	КнопкаУдалить.Заголовок = Элементы.УдалитьКомплект.Заголовок;
	// Заполнение КнопкиТаблицы конец
	
	ТаблицаНаФорме = Элементы.Добавить(ИмяТаблицы, Тип("ТаблицаФормы"), Элементы.ГруппаКомплекты);
	ТаблицаНаФорме.ПутьКДанным = "ДанныеДокументов";
	ТаблицаНаФорме.Отображение = ОтображениеТаблицы.Список;
	ТаблицаНаФорме.ОтборСтрок = Новый ФиксированнаяСтруктура("НомерКомплекта", НомерКомплекта);
	ТаблицаНаФорме.ИзменятьСоставСтрок = Ложь;
	ТаблицаНаФорме.РазрешитьНачалоПеретаскивания = Истина;
	ТаблицаНаФорме.РазрешитьПеретаскивание = Истина;
	
	ТаблицаНаФорме.УстановитьДействие("Выбор", "ВсеТаблицыВыбор");
	ТаблицаНаФорме.УстановитьДействие("ПередНачаломИзменения", "ВсеТаблицыПередНачаломИзменения");
	ТаблицаНаФорме.УстановитьДействие("ПередУдалением", "ВсеТаблицыПередУдалением");
	ТаблицаНаФорме.УстановитьДействие("Перетаскивание", "ВсеТаблицыПеретаскивание");
	ТаблицаНаФорме.УстановитьДействие("ОкончаниеПеретаскивания", "Подключаемый_ВсеТаблицыОкончаниеПеретаскивания");
	ТаблицаНаФорме.Высота = 7;
	ТаблицаНаФорме.АвтоМаксимальнаяВысота = Ложь;
	ТаблицаНаФорме.МаксимальнаяВысота = 7;
	ТаблицаНаФорме.КоманднаяПанель.Видимость = Ложь;
	
	ЗаполнитьКолонкиРекурсивно(ИмяТаблицы, Элементы.ДанныеДокументов.ПодчиненныеЭлементы, ТаблицаНаФорме);
	ОбновитьКоличествоДокументов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКолонкиРекурсивно(ИмяТаблицы, ЭлементыПеребора, РодительЭлемента)
	
	Для Каждого КолонкаИлиГруппа Из ЭлементыПеребора Цикл
		Если ТипЗнч(КолонкаИлиГруппа) = Тип("ГруппаФормы") Тогда
			НовыйЭлемент = Элементы.Добавить(ИмяТаблицы + КолонкаИлиГруппа.Имя, Тип("ГруппаФормы"), РодительЭлемента);
			НовыйЭлемент.Вид = ВидГруппыФормы.ГруппаКолонок;
			ЗаполнитьКолонкиРекурсивно(ИмяТаблицы, КолонкаИлиГруппа.ПодчиненныеЭлементы, НовыйЭлемент);
		Иначе
			НовыйЭлемент = Элементы.Добавить(ИмяТаблицы + КолонкаИлиГруппа.Имя, Тип("ПолеФормы"), РодительЭлемента);
			НовыйЭлемент.Заголовок = КолонкаИлиГруппа.Заголовок;
			НовыйЭлемент.Вид = КолонкаИлиГруппа.Вид;
			НовыйЭлемент.ПутьКДанным = "ДанныеДокументов." + КолонкаИлиГруппа.Имя;
			НовыйЭлемент.ПоложениеЗаголовка = КолонкаИлиГруппа.ПоложениеЗаголовка;
			Если КолонкаИлиГруппа.Имя = "Ошибок" Тогда
				НовыйЭлемент.Формат = КолонкаИлиГруппа.Формат;
			КонецЕсли;
			НовыйЭлемент.Подсказка = КолонкаИлиГруппа.Подсказка;
			НовыйЭлемент.КартинкаШапки = КолонкаИлиГруппа.КартинкаШапки;
			НовыйЭлемент.Ширина = КолонкаИлиГруппа.Ширина;
			
			Если КолонкаИлиГруппа.Имя = "СтатусРаспознавания" Тогда
				НовыйЭлемент.КартинкаЗначений = БиблиотекаКартинок.КоллекцияСтатусыРаспознаванияДокумента;
			ИначеЕсли КолонкаИлиГруппа.Имя = "НаправлениеДокумента" Тогда
				НовыйЭлемент.КартинкаЗначений = БиблиотекаКартинок.НаправлениеРаспознанногоДокумента;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКоличествоДокументов()
	
	БезКомплектовКоличество = ДанныеДокументов.Выгрузить(Новый Структура("НомерКомплекта", 0), "Ссылка").Количество();
	Элементы.ДекорацияДокументыБезКомплектов.Заголовок = Элементы.ДекорацияДокументыБезКомплектов.Подсказка
		+ СтрШаблон(" (%1)", БезКомплектовКоличество);
	СобранноКомплектов = Элементы.ГруппаКомплекты.ПодчиненныеЭлементы.Количество() / 2;
	// по 2 элемента на комплект: командная панель + таблица
	Элементы.ДекорацияСобранныеКомплекты.Заголовок = Элементы.ДекорацияСобранныеКомплекты.Подсказка
		+ СтрШаблон(" (%1)", СобранноКомплектов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеДокументаВКомплекте(РаспознанныйДокумент)
	
	СтрокиТаблицы = ДанныеДокументов.НайтиСтроки(Новый Структура("Ссылка", РаспознанныйДокумент));
	КоличествоСтрок = СтрокиТаблицы.Количество();
	Если КоличествоСтрок = 0 Тогда
		Возврат;
	ИначеЕсли СтрокиТаблицы.Количество() > 1 Тогда
		// Сюда не должны попадать. Дополнительная проверка
		ВызватьИсключение НСтр("ru = 'В основной таблице появились дубли документов.';
								|en = 'Duplicate documents are found in the main table.'");
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			РаспознанныйДокумент,
			"ПометкаУдаления, Статус, Направление, ТипДокумента, Наименование, Организация, Контрагент, СуммаДокумента, НомерДокумента, ДатаДокумента",
			Истина);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеДокумента);
		РаспознаваниеДокументовКомплекты.ДополнитьДанныеДокументовПоСтроке(СтрокаТаблицы);
		СтрокаТаблицы.Ошибок = РаспознаваниеДокументовСлужебный.ПроблемныеРеквизиты(
				РаспознанныйДокумент.ПолучитьОбъект()
			).Количество();
		НастроитьПанельИОшибкиКомплекта(СтрокаТаблицы.НомерКомплекта);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьПанельИОшибкиКомплекта(НастраиваемыйКомплект = Неопределено)
	
	Если НастраиваемыйКомплект = Неопределено Тогда
		СтрокиДляОбхода = ДанныеДокументов;
	Иначе
		Если НастраиваемыйКомплект = 0 Тогда
			// Т.к. комплекты нумеруются с 1
			Возврат;
		КонецЕсли;
		СтрокиДляОбхода = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НастраиваемыйКомплект));
		
		// Обновим ТипКомплекта для всех строк этого комплекта
		ТипКомплекта = РаспознаваниеДокументовКомплекты.СформироватьТипКомплекта(СтрокиДляОбхода, "ПоТаблице");
		Для Каждого СтрокаДанных Из СтрокиДляОбхода Цикл
			СтрокаДанных.ТипКомплекта = ТипКомплекта;
		КонецЦикла;
		
		
		ИмяТаблицы = "Комплект" + Формат(НастраиваемыйКомплект, "ЧГ=");
		Если СтрокиДляОбхода.Количество() = 0 Тогда
			Элементы["УбратьЛишниеИзКомплекта" + ИмяТаблицы].Доступность = Ложь;
		Иначе
			
			// Кнопка УбратьЛишние появляется, если:
			// 1 - это комплект неизвестного типа
			// 2 - есть документы разного направления
			
			Если Не Элементы["УбратьЛишниеИзКомплекта" + ИмяТаблицы].Доступность Тогда
				Если Не РаспознаваниеДокументовКомплектыКлиентСервер.ЭтоТипКомплектаМожноОбработать(СтрокиДляОбхода[0].ТипКомплекта) Тогда
					Элементы["УбратьЛишниеИзКомплекта" + ИмяТаблицы].Доступность = Истина;
				Иначе
					Для Каждого СтрокаДанных Из СтрокиДляОбхода Цикл
						Если СтрокаДанных.НаправлениеДокумента <> СтрокиДляОбхода[0].НаправлениеДокумента Тогда // НаправлениеДокумента - это число
							Элементы["УбратьЛишниеИзКомплекта" + ИмяТаблицы].Доступность = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПройденныеКомплекты = Новый Массив;
	
	Для Каждого СтрокаДанных Из СтрокиДляОбхода Цикл
		Если СтрокаДанных.НомерКомплекта = 0 Тогда
			// Т.к. комплекты нумеруются с 1
			Продолжить;
		КонецЕсли;
		
		Если НастраиваемыйКомплект <> Неопределено И СтрокаДанных.НомерКомплекта <> НастраиваемыйКомплект Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяТаблицы = "Комплект" + Формат(СтрокаДанных.НомерКомплекта, "ЧГ=");
		Если Не ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяТаблицы) Тогда
			// Для удаленных комплектов
			Продолжить;
		КонецЕсли;
		
		Если ПройденныеКомплекты.Найти(СтрокаДанных.НомерКомплекта) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ОбновитьОшибкиКомплекта(СтрокаДанных.НомерКомплекта);
		
		Элементы["ДекорацияТекст" + ИмяТаблицы].Заголовок = НазваниеКомплекта(СтрокаДанных);
		ПройденныеКомплекты.Добавить(СтрокаДанных.НомерКомплекта);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НазваниеКомплекта(СтрокаДанных)
	
	Если СтрокаДанных.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		СтрокаТипДокумента = НСтр("ru = 'Поступление';
									|en = 'Inflow'");
		Если ЗначениеЗаполнено(СтрокаДанных.Контрагент) Тогда
			ВОтКонтрагента = СтрШаблон(НСтр("ru = ' от ""%1""';
											|en = ' from ""%1""'"), СокрЛП(СтрокаДанных.Контрагент));
		Иначе
			ВОтКонтрагента = "";
		КонецЕсли;
	ИначеЕсли СтрокаДанных.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		СтрокаТипДокумента = НСтр("ru = 'Реализация';
									|en = 'Sales'");
		Если ЗначениеЗаполнено(СтрокаДанных.Контрагент) Тогда
			ВОтКонтрагента = СтрШаблон(НСтр("ru = ' в ""%1""';
											|en = ' to ""%1""'"), СокрЛП(СтрокаДанных.Контрагент));
		Иначе
			ВОтКонтрагента = "";
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаДанных.ДатаДокумента) Тогда
		ДатаВДекорации = НСтр("ru = ' от ';
								|en = ' from'") + Формат(СтрокаДанных.ДатаДокумента, "ДЛФ=D");
	Иначе
		ДатаВДекорации = "";
	КонецЕсли;
	Результат = СтрШаблон(НСтр("ru = '%1%2%3';
								|en = '%1%2%3'"), СтрокаТипДокумента, ДатаВДекорации, ВОтКонтрагента);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УдалитьКомплектНаСервере(НомерКомплекта, УдалятьСтроки, ОтправитьОбратнуюСвязь = Ложь)
	
	Если Элементы.НадписьВсеЗаполнено.Видимость Тогда
		Элементы.НадписьВсеЗаполнено.Видимость = Ложь;
	КонецЕсли;
	
	СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
		Если Не УдалятьСтроки Тогда
			НоваяСтрока = ДанныеДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.НомерКомплекта = 0;
			НоваяСтрока.ТипКомплекта = "";
			НоваяСтрока.Ошибок = РаспознаваниеДокументовСлужебный.ПроблемныеРеквизиты(
					СтрокаТаблицы.Ссылка.ПолучитьОбъект()
				).Количество();
		КонецЕсли;
		ДанныеДокументов.Удалить(СтрокаТаблицы);
	КонецЦикла;
	
	ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяТаблицы) Тогда
		Элементы.Удалить(Элементы[ИмяТаблицы]);
		Элементы.Удалить(Элементы["ГруппаКнопкиТаблицы" + ИмяТаблицы]);
		Команды.Удалить(Команды[Элементы.УбратьЛишниеИзКомплекта.Имя + ИмяТаблицы]);
		Команды.Удалить(Команды[Элементы.УдалитьКомплект.Имя + ИмяТаблицы]);
	КонецЕсли;
	
	ТекущиеДействия = РезультатОбратнойСвязи.Комплекты.Получить(НомерКомплекта);
	НовыеДействия = Новый Соответствие;
	Для Каждого КлючЗначение Из ТекущиеДействия Цикл
		Если КлючЗначение.Значение = "ДобавленАвтоматически"
			Или КлючЗначение.Значение = "Удален"
			Или КлючЗначение.Значение = "ОтказОтСоздания" Тогда
			// Действие "Удален" может быть только у автоматически добавленных документов
			
			НовыеДействия.Вставить(КлючЗначение.Ключ, "ОтказОтСоздания");
		КонецЕсли;
	КонецЦикла;
	
	Если ОтправитьОбратнуюСвязь И НовыеДействия.Количество() <> 0 Тогда
		ДанныеПакета = Новый Массив;
		
		ВсеРаспознанные = Новый Массив;
		Для Каждого КлючЗначение Из НовыеДействия Цикл
			ВсеРаспознанные.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		
		ТипыДокументовВСервисе = РаспознаваниеДокументовСлужебныйКлиентСервер.ПолучитьОбратноеСоответствие(
			Документы.РаспознанныйДокумент.СоответствиеТиповДокументовВСервисеИБРД());
		
		set_id = Строка(Новый УникальныйИдентификатор);
		РаспознанныеДанные = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ВсеРаспознанные,
			"Номер, Направление, ТипДокумента, НомерДокумента, ДатаДокумента, СуммаДокумента, Контрагент, Организация, ИдентификаторРезультата");
		
		Для Каждого КлючЗначение Из РаспознанныеДанные Цикл
			ДанныеДокумента = КлючЗначение.Значение;
			ДопДанныеПакета = РаспознаваниеДокументовКомплектыВызовСервера.ПолучитьОбратнуюСвязьДляСозданногоДокумента(ДанныеДокумента);
			ДопДанныеПакета.Удалить("Статус");
			ДопДанныеПакета.Вставить("set_id", set_id);
			ДопДанныеПакета.Вставить("doc_uuid", ДанныеДокумента.ИдентификаторРезультата);
			ДопДанныеПакета.Вставить("ТипДокумента", ТипыДокументовВСервисе.Получить(ДанныеДокумента.ТипДокумента));
			ДопДанныеПакета.Вставить("ОсновнойДокумент", Ложь);
			ДопДанныеПакета.Вставить("Действие", "ОтказОтСоздания");
			
			ДанныеПакета.Добавить(ДопДанныеПакета);
		КонецЦикла;
		
		Пакет = Новый Структура;
		Пакет.Вставить("set_creation", Новый Структура("set_data", ДанныеПакета));
		РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(КлючЗначение.Значение.ИдентификаторРезультата, Пакет);
	КонецЕсли;
	ОбновитьКоличествоДокументов();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьИСоздатьДокументыПоКомплекту(Форма, ПараметрыСоздания)
	
	#Если Сервер Тогда
		ЗаполнитьПараметрыСозданияНаСервере(ПараметрыСоздания);
		ПолученВариантСозданияКомплекта(Форма, ПараметрыСоздания);
	#Иначе
		//@skip-check notify-description-to-server-procedure
		//@skip-check bsl-legacy-check-string-literal
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолученВариантСозданияКомплектаНаКлиенте", Форма);
		ПараметрыФормы = Новый Структура("ПараметрыСоздания", ПараметрыСоздания);
		ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.СозданиеКомплекта", ПараметрыФормы, Форма,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыСозданияНаСервере(ПараметрыСоздания)
	
	РаспознаваниеДокументовКомплекты.ОбновитьИЗаполнитьСвязанныеИСозданныеДокументы(ПараметрыСоздания);
	Для Каждого ЭтотПараметр Из ПараметрыСоздания.СозданныеДокументы Цикл
		Если ЭтотПараметр.Значение.СканУжеПрикреплен Тогда
			ЭтотПараметр.Значение.ПрикрепитьСкан = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#Область СлужебныеИзФормыТорг12

&НаКлиентеНаСервереБезКонтекста
Процедура ОткрытиеФормыДокумента(Форма, ПараметрыОперации, ИмяТаблицы, ДанныеОбработки = Неопределено)
	
	НомерКомплекта = Число(СтрЗаменить(ИмяТаблицы, "Комплект", ""));
	РезультатПроверки = ПроверитьПравилаКомплекта(Форма.ДанныеДокументов, НомерКомплекта);
	
	Если Не РезультатПроверки.ПроверкиПройдены Тогда
		Если ДанныеОбработки <> Неопределено Тогда
			ДанныеОбработки.КомплектовНеУдалосьОбработать = ДанныеОбработки.КомплектовНеУдалосьОбработать + 1;
		КонецЕсли;
		
		#Если Не Сервер Тогда
			ПоказатьПредупреждение(, РезультатПроверки.ТекстСообщения);
		#КонецЕсли
		
		Возврат;
	КонецЕсли;
	
	ДокументыПоТипам = Новый Соответствие;
	СтрокиКомплекта = Форма.ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
		ДокументыПоТипам.Вставить(СтрокаТаблицы.ТипДокумента, СтрокаТаблицы.Ссылка);
	КонецЦикла;
	
	ПараметрыСоздания = РаспознаваниеДокументовКомплектыКлиентСервер.НовыеПараметрыСозданияКомплекта();
	ПараметрыСоздания.ПараметрыОперации    = ПараметрыОперации;
	ПараметрыСоздания.НаправлениеДокумента = СтрокаТаблицы.Направление;
	ПараметрыСоздания.ТипКомплекта         = РезультатПроверки.ТипКомплекта;
	ПараметрыСоздания.РаспознанныеДокументыПоТипам = ДокументыПоТипам;
	ПараметрыСоздания.ДанныеОбработки      = ДанныеОбработки;
	
	ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(СтрокиКомплекта[0].Ссылка,
		"ВидДокумента", "");
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ВидДокумента = ?(СтрокаТаблицы.Направление = ПредопределенноеЗначение(
			"Перечисление.НаправленияРаспознанногоДокумента.Входящий"), "ПТИУ", "РТИУ");
				
	КонецЕсли;
	ПараметрыСоздания.Вставить("ВидДокумента", ВидДокумента);
	
	ПараметрыСоздания.Вставить("НомерКомплекта", НомерКомплекта); // Необязательный параметр
	ПроверитьИСоздатьДокументыПоКомплекту(Форма, ПараметрыСоздания);
	
	Если ДанныеОбработки <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ДанныеОбработки, ПараметрыСоздания.ДанныеОбработки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученВариантСозданияКомплектаНаКлиенте(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеНаКлиенте = Новый ОписаниеОповещения("ПолученРезультатСозданияКомплекта", ЭтотОбъект);
	ПолученВариантСозданияКомплекта(ЭтотОбъект, Результат, ОповещениеНаКлиенте);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолученРезультатСозданияКомплекта(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ДанныеОбработки <> Неопределено Тогда
		Для Каждого НомерКомплекта Из Результат.ДанныеОбработки.НомераСозданныхКомплектов Цикл
			УдалитьКомплектНаСервере(НомерКомплекта, Истина);
		КонецЦикла;
		
		Если Результат.ДанныеОбработки.КомплектовНеУдалосьОбработать <> 0 Тогда
			ТекстПредупреждения = НСтр("ru = 'Не удалось обработать комплект. Повторите операцию после исправления ошибок:';
										|en = 'Cannot process the kit. Correct the following errors and try again:'");
			ПараметрыФормы = Новый Структура(
				"ОшибкиПроведения, ТекстПредупреждения",
				Результат.ДанныеОбработки.ОшибкиПроведения,
				ТекстПредупреждения
			);
			ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.ОтчетПоОшибкам",
				ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПолученВариантСозданияКомплекта(Форма, Результат, ОповещениеНаКлиенте = Неопределено)
	
	РаспознаваниеДокументовКомплектыКлиентСервер.ОбработатьДокументыКомплекта(Форма, Результат, ОповещениеНаКлиенте);
	
КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьПравилаКомплекта(ДанныеДокументов, НомерКомплекта)
	
	РезультатПроверки = Новый Структура("ПроверкиПройдены, ТипКомплекта, ТекстСообщения",
		Ложь, "", НСтр("ru = 'Выбранные типы документов не могут входить в один комплект. Уберите лишние документы из комплекта.';
						|en = 'The selected document types cannot be included in one kit. Remove unnecessary documents from the kit.'"));
	СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	
	Если СтрокиКомплекта.Количество() = 0 Тогда
		РезультатПроверки.ПроверкиПройдены = Ложь;
		РезультатПроверки.ТекстСообщения = НСтр("ru = 'Нельзя создать комплект без документов';
												|en = 'Cannot create a kit without documents'");
		Возврат РезультатПроверки;
	КонецЕсли;
	
	ТипКомплекта = СтрокиКомплекта[0].ТипКомплекта;
	РезультатПроверки.ТипКомплекта = ТипКомплекта;
	Если РаспознаваниеДокументовКомплектыКлиентСервер.ЭтоТипКомплектаМожноОбработать(ТипКомплекта) Тогда
		ПроверитьКомплектАктОбОказанииУслугСчетНаОплатуСчетФактураТОРГ12(СтрокиКомплекта, ТипКомплекта, РезультатПроверки);
	КонецЕсли;
	
	Возврат РезультатПроверки;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьКомплектАктОбОказанииУслугСчетНаОплатуСчетФактураТОРГ12(СтрокиКомплекта, ТипКомплекта, РезультатПроверки)
	
	РезультатПроверки.ПроверкиПройдены = Истина;
	ПервыйДокументКомплекта = СтрокиКомплекта[0];
	
	Для Каждого ЭтотДокументКомплекта Из СтрокиКомплекта Цикл
		Если ПервыйДокументКомплекта = ЭтотДокументКомплекта Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПервыйДокументКомплекта.Направление <> ЭтотДокументКомплекта.Направление Тогда
			РезультатПроверки.ПроверкиПройдены = Ложь;
			РезультатПроверки.ТекстСообщения = НСтр("ru = 'Комплект не может одновременно содержать документы поступления и списания. Убедитесь, что либо все документы комплекта относятся к списанию, либо все – к поступлению.';
													|en = 'A kit cannot contain both receipt and write-off documents. Make sure all documents in the kit are write-off documents or receipt documents.'");
			Возврат;
		КонецЕсли;
		
		Если ПервыйДокументКомплекта.Организация <> ЭтотДокументКомплекта.Организация Тогда
			РезультатПроверки.ПроверкиПройдены = Ложь;
			РезультатПроверки.ТекстСообщения = НСтр("ru = 'Данные документы принадлежат разным организациям и не могут быть обработаны в комплекте. Проверьте корректность заполнения данных документов или обработайте их по отдельности.';
													|en = 'The documents belong to different companies and cannot be processed together. Check if the documents are filled correctly or process them separately.'");
			Возврат;
		КонецЕсли;
		
		Если ПервыйДокументКомплекта.Контрагент <> ЭтотДокументКомплекта.Контрагент Тогда
			РезультатПроверки.ПроверкиПройдены = Ложь;
			РезультатПроверки.ТекстСообщения = НСтр("ru = 'Данные документы принадлежат разным контрагентам и не могут быть обработаны в комплекте. Проверьте корректность заполнения данных документов или обработайте их по отдельности.';
													|en = 'The documents belong to different counterparties and cannot be processed together. Check if the documents are filled correctly or process them separately.'");
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПерепроверитьНаСервере()
	
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
		УбратьЛишниеИзКомплектаНаСервере(ИмяТаблицы);
	КонецЦикла;
	
	ТаблицаДокументов = ДанныеДокументов.Выгрузить(Новый Структура("НомерКомплекта", 0));
	ВсеДанныеДокументов = РаспознаваниеДокументовКомплекты.ДополнитьДанныеДокументов(ТаблицаДокументов.ВыгрузитьКолонку("Ссылка"));
	
	ДанныеКомплектов = РаспознаваниеДокументовКомплекты.НайтиКомплекты(ВсеДанныеДокументов);
	УдалитьДублиКомплектов(ДанныеКомплектов);
	
	БылоКомплектов = КоличествоКомплектов;
	КоличествоКомплектов = КоличествоКомплектов + ДанныеКомплектов.КоличествоКомплектов;
	
	Для Каждого СтрокаКомплекта Из ДанныеКомплектов.ТаблицаКомплектов Цикл
		СтрокиТаблицы = ДанныеДокументов.НайтиСтроки(Новый Структура("Ссылка", СтрокаКомплекта.Ссылка));
		Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
			СтрокаТаблицы.НомерКомплекта = СтрокаКомплекта.НомерКомплекта + БылоКомплектов;
			СтрокаТаблицы.ТипКомплекта = СтрокаКомплекта.ТипКомплекта;
		КонецЦикла;
	КонецЦикла;
	
	Для НомерКомплекта = БылоКомплектов + 1 По КоличествоКомплектов Цикл
		РезультатОбратнойСвязи.Комплекты.Вставить(НомерКомплекта, Новый Соответствие);
		НовыйКомплектНаСервере(Ложь, НомерКомплекта);
	КонецЦикла;
	НастроитьПанельИОшибкиКомплекта();
	
	ОбратнаяСвязьКомплектов = РезультатОбратнойСвязи.Комплекты;
	Для Каждого СтрокаКомплекта Из ДанныеКомплектов.ТаблицаКомплектов Цикл
		ОбратнаяСвязьКомплектов[СтрокаКомплекта.НомерКомплекта + БылоКомплектов].Вставить(СтрокаКомплекта.Ссылка, "ДобавленАвтоматически");
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьДублиКомплектов(ДанныеКомплектов)
	
	ДублиКомплектов = Новый Массив;
	
	ТаблицаПоискаДублей = ДанныеДокументов.Выгрузить();
	ИдПоиска = ТаблицаПоискаДублей.Количество();
	Пока ИдПоиска > 0 Цикл
		ИдПоиска = ИдПоиска - 1;
		Если ТаблицаПоискаДублей[ИдПоиска].НомерКомплекта = 0 Тогда
			ТаблицаПоискаДублей.Удалить(ИдПоиска);
		КонецЕсли;
	КонецЦикла;
	
	Для НомерКомплекта = 1 По ДанныеКомплектов.КоличествоКомплектов Цикл
		СтрокиКомплекта = ДанныеКомплектов.ТаблицаКомплектов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
		Если СтрокиКомплекта.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого СтрокаКомплекта Из СтрокиКомплекта Цикл
			ЭтоДубль = РаспознаваниеДокументовКомплекты.ЕстьДубльДокументаИзКомплекта(СтрокаКомплекта, ТаблицаПоискаДублей);
			Если ЭтоДубль Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЭтоДубль Тогда
			ДублиКомплектов.Добавить(НомерКомплекта);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ДублиКомплектов.Количество() <> 0 Тогда
		
		ИдСтрокиКомплектов = ДанныеКомплектов.ТаблицаКомплектов.Количество();
		Пока ИдСтрокиКомплектов > 0 Цикл
			ИдСтрокиКомплектов = ИдСтрокиКомплектов - 1;
			Если ДублиКомплектов.Найти(ДанныеКомплектов.ТаблицаКомплектов[ИдСтрокиКомплектов].НомерКомплекта)
				<> Неопределено Тогда
				
				ДанныеКомплектов.ТаблицаКомплектов.Удалить(ИдСтрокиКомплектов);
			КонецЕсли;
		КонецЦикла;
		
		// Исправление нумерации
		МассивНомеров = Новый Массив;
		МассивНомеров.Добавить(0); // для выравнивания
		Для НомерКомплекта = 1 По ДанныеКомплектов.КоличествоКомплектов Цикл
			МассивНомеров.Добавить(НомерКомплекта);
		КонецЦикла;
		
		Для Каждого НомерДубля Из ДублиКомплектов Цикл
			Для ПозицияУменьшения = НомерДубля + 1 По ДанныеКомплектов.КоличествоКомплектов Цикл
				МассивНомеров[ПозицияУменьшения] = МассивНомеров[ПозицияУменьшения] - 1;
			КонецЦикла;
		КонецЦикла;
		
		Для Каждого СтрокаКомплекта Из ДанныеКомплектов.ТаблицаКомплектов Цикл
			СтрокаКомплекта.НомерКомплекта = МассивНомеров[СтрокаКомплекта.НомерКомплекта];
		КонецЦикла;
		
		ДанныеКомплектов.КоличествоКомплектов = ДанныеКомплектов.КоличествоКомплектов - ДублиКомплектов.Количество();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьКомплектыНаСервере(ДанныеОбработки)
	
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
		ИмяГруппыШапкиКомплекта = "ГруппаКнопкиТаблицы" + ИмяТаблицы;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяГруппыШапкиКомплекта) Тогда
			
			// Удаление пустых комплектов
			СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
			Если СтрокиКомплекта.Количество() = 0 Тогда
				УдалитьКомплектНаСервере(НомерКомплекта, Ложь);
				Продолжить;
			КонецЕсли;
			
			ДанныеОбработки.КомплектовВсего = ДанныеОбработки.КомплектовВсего + 1;
			// На случай, если в подменю нет Доступных Команд
			ДанныеОбработки.КомплектовНеУдалосьОбработать = ДанныеОбработки.КомплектовНеУдалосьОбработать + 1;
			
			ДоступныеКоманды = РаспознаваниеДокументовКомплектыКлиентСервер.ДоступныеКомандыПоТипуКомплекта(
				СтрокиКомплекта[0].Направление, СтрокиКомплекта[0].ТипКомплекта);
			
			Если ДоступныеКоманды.Количество() > 0 Тогда
				ДанныеОбработки.КомплектовНеУдалосьОбработать = ДанныеОбработки.КомплектовНеУдалосьОбработать - 1;
				
				СтрокаПоступленияРеализации = СтрЗаменить(ДоступныеКоманды[0], "СчетНаОплату", "");
				СтрокаПоступленияРеализации = СтрЗаменить(СтрокаПоступленияРеализации, "СчетФактура", "");
				
				ПараметрыОперации = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперации(СтрокаПоступленияРеализации);
				ОткрытиеФормыДокумента(ЭтотОбъект, ПараметрыОперации, ИмяТаблицы, ДанныеОбработки);
				Прервать;
			КонецЕсли;
			// Тут можно Прервать; при необходимости
		КонецЕсли;
	КонецЦикла;
	
	ОбратнаяСвязь = ДанныеОбработки.РезультатОбратнойСвязи.Отправить;
	Если ЗначениеЗаполнено(ОбратнаяСвязь) Тогда
		Пакет = Новый Структура;
		Пакет.Вставить("set_creation", Новый Структура("set_data", ОбратнаяСвязь.ДанныеПакета));
		РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(ОбратнаяСвязь.ИдентификаторРезультата, Пакет);
	КонецЕсли;
	
	Для Каждого НомерКомплекта Из ДанныеОбработки.НомераСозданныхКомплектов Цикл
		УдалитьКомплектНаСервере(НомерКомплекта, Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыделеннойСтроки()
	
	ЧастьКомплекта = (ТекущийЭлемент.ТекущиеДанные.НомерКомплекта <> 0);
	ПараметрыФормы = Новый Структура("Ключ, ЧастьКомплекта", ТекущийЭлемент.ТекущиеДанные.Ссылка, ЧастьКомплекта);
	
	ИмяФормыОбработчика = РаспознаваниеДокументовСлужебныйКлиентПовтИсп.ПолучитьИмяОткрываемойФормыПоТипу(ТекущийЭлемент.ТекущиеДанные.ТипДокумента, ТекущийЭлемент.ТекущиеДанные.ВариантОбработки);
	ОткрытьФорму(ИмяФормыОбработчика, ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Процедура УбратьЛишниеИзКомплектаНаСервере(ИмяТаблицы)
	
	НомерКомплекта = Число(СтрЗаменить(ИмяТаблицы, "Комплект", ""));
	СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
	
	Если СтрокиКомплекта.Количество() < 2 Тогда
		УдалитьКомплектНаСервере(НомерКомплекта, Ложь);
		Возврат;
	КонецЕсли;
	
	Элементы["УбратьЛишниеИзКомплекта" + ИмяТаблицы].Доступность = Ложь;
	ТаблицаПоиска = ДанныеДокументов.Выгрузить(Новый Структура("НомерКомплекта", НомерКомплекта));
	
	ДанныеКомплекта = РаспознаваниеДокументовКомплекты.ПоследнийПеренесенныйКомплект(ТаблицаПоиска);
	Если ДанныеКомплекта.ДокументыКомплекта.Количество() = 0 Тогда
		УдалитьКомплектНаСервере(НомерКомплекта, Ложь);
	Иначе
		Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
			Если ДанныеКомплекта.ДокументыКомплекта.Найти(СтрокаТаблицы.Ссылка) = Неопределено Тогда
				ТекущиеДействия = РезультатОбратнойСвязи.Комплекты.Получить(СтрокаТаблицы.НомерКомплекта);
				ДействиеДокумента = ТекущиеДействия.Получить(СтрокаТаблицы.Ссылка);
				Если ДействиеДокумента = "ДобавленАвтоматически" Тогда
					ТекущиеДействия.Вставить(СтрокаТаблицы.Ссылка, "Удален");
				Иначе
					ТекущиеДействия.Удалить(СтрокаТаблицы.Ссылка);
				КонецЕсли;
				
				СтрокаТаблицы.НомерКомплекта = 0;
				СтрокаТаблицы.ТипКомплекта = "";
			Иначе
				СтрокаТаблицы.ТипКомплекта = ДанныеКомплекта.ТипКомплекта;
			КонецЕсли;
		КонецЦикла;
		
		НастроитьПанельИОшибкиКомплекта(НомерКомплекта);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыДопРеквизиты

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиДопРеквизитов()
	
	Если СкрытьЗаполненныеСтрокиДопРеквизитов Тогда
		Элементы.ТаблицаДопРеквизитов.ОтборСтрок = Новый ФиксированнаяСтруктура("ДанныеЗаполнены", Ложь);
	Иначе
		Элементы.ТаблицаДопРеквизитов.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаполненияСтрокДопРеквизитов()
	
	СкрытьЗаполненныеСтрокиДопРеквизитов();
	ОбновитьВидимостьНадписиПроЗаполненность();
	ПроверитьИЗагрузитьПустыеКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов() Экспорт
	
	ТаблицаЭлемент = Элементы.ТаблицаДопРеквизитов;
	ДанныеЭтойСтроки = ТаблицаЭлемент.ТекущиеДанные;
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДляТаблиц
		Или Элементы.СтраницыТаблиц.ТекущаяСтраница <> Элементы.СтраницаДопРеквизиты
		Или ДанныеЭтойСтроки = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	Если ПолнаяКартинкаВидна И ТипЗнч(Элементы.ПолеПросмотра.Документ) <> Тип("ВнешнийОбъект") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЭлемент.ТекущийЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаДопРеквизитов", "");
	
	ОбновитьПолнуюКартинку = (ПолнаяКартинкаВидна И ТекущаяСтрокаДляСпискаВыбора <> ТаблицаЭлемент.ТекущаяСтрока);
	ТекущаяСтрокаДляСпискаВыбора = ТаблицаЭлемент.ТекущаяСтрока;
	ТекущееИмяРеквизита = ИмяРеквизита;
	
	ИсточникПоляДоговорКонтрагент = ДанныеЭтойСтроки.Контрагент;
	ИсточникПоляДоговорОрганизация = ДанныеЭтойСтроки.Организация;
	ПараметрыВыбораДоговора = Новый Структура;
	ПриЗаполненииПараметровВыбораДоговора(ДанныеЭтойСтроки, ПараметрыВыбораДоговора);
	ИсточникПоляДоговорВидДоговора.ЗагрузитьЗначения(ПараметрыВыбораДоговора.ВидыДоговоров);
	
	Если ДанныеЭтойСтроки.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда	
		ИсточникПоляБанковскийСчетВладелец = ДанныеЭтойСтроки.ПродавецОрганизация;
	Иначе	
		ИсточникПоляБанковскийСчетВладелец = ДанныеЭтойСтроки.Контрагент;
	КонецЕсли;
	
	ОграничитьТипЭлементаДопРеквизита(ДанныеЭтойСтроки, ИмяРеквизита, ТаблицаЭлемент.ТекущийЭлемент);
	УстановитьПараметрыВыбораЭлемента(ДанныеЭтойСтроки, ИмяРеквизита, ТаблицаЭлемент.ТекущийЭлемент);
	
	ПриАктивизацииЯчейкиШапки(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку);
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьТипЭлементаДопРеквизита(ТекущиеДанные, ИмяРеквизита, Элемент)
	
	Если Не ИмяРеквизита = "Соглашение" И Не ИмяРеквизита = "БанковскийСчет" Тогда
		Возврат;
	КонецЕсли;
	
	ИсходящееНаправление = (ТекущиеДанные.Направление = ПредопределенноеЗначение(
		"Перечисление.НаправленияРаспознанногоДокумента.Исходящий"));
	
	Если ИмяРеквизита = "Соглашение" Тогда
		ИмяТипа = ?(ИсходящееНаправление, "СправочникСсылка.СоглашенияСКлиентами", 
			"СправочникСсылка.СоглашенияСПоставщиками"); 	
	ИначеЕсли ИмяРеквизита = "БанковскийСчет" Тогда
		ИмяТипа = ?(ИсходящееНаправление, "СправочникСсылка.БанковскиеСчетаОрганизаций", 
			"СправочникСсылка.БанковскиеСчетаКонтрагентов"); 
	КонецЕсли;
	
	Элемент.ОграничениеТипа = Новый ОписаниеТипов(ИмяТипа);
    ТекущиеДанные[ИмяРеквизита] = Элемент.ОграничениеТипа.ПривестиЗначение(ТекущиеДанные[ИмяРеквизита]);

КонецПроцедуры // ОграничитьТипДополнительногоЭлемента()

&НаКлиенте
Процедура УстановитьПараметрыВыбораЭлемента(ТекущиеДанные, ИмяРеквизита, Элемент)

	Если ИмяРеквизита = "Склад" Тогда
		
		ГруппыСкладов = Новый Массив;
		ГруппыСкладов.Добавить(ПредопределенноеЗначение("Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных"));
	
		Если ТекущиеДанные.ТипДокумента = ПредопределенноеЗначение(
			"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") Тогда
			ГруппыСкладов.Добавить(ПредопределенноеЗначение("Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах"));
		КонецЕсли;
		
		ПараметрыВыбора = Новый Массив;
		ВыборГруппыСкладов = Новый ФиксированныйМассив(ГруппыСкладов);
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ВыборГруппыСкладов", ВыборГруппыСкладов));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ДокументЗакупки", Истина));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ДокументПродажи", Истина));
		
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	КонецЕсли;
	
	Если ИмяРеквизита = "Соглашение" Тогда
		
		ПараметрыВыбора = Новый Массив;
		Если ТекущиеДанные.Направление = ПредопределенноеЗначение(
			"Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
			ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
		Иначе
			ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
			ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ДокументЗакупки", Истина));
		КонецЕсли;
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ХозяйственнаяОперация", ХозяйственнаяОперация));
		
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуДопРеквизиты()
	
	Если ЕстьНезаписанныеРеквизитыШапки Или ЕстьНезаписанныеРеквизитыТаблицы Тогда
		ПодключитьОбработчикОжидания("ПерейтиНаСтраницуДопРеквизиты", 0.5, Истина);
		Возврат;
	КонецЕсли;
	
	ПерейтиНаСтраницуДопРеквизитыНаСервере();
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовШапки();
	ВыделитьНезаполненнуюЯчейкуНаШаге();
	#Если ВебКлиент Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиДопРеквизитов", 0.1, Истина);
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуДопРеквизитыНаСервере()
	
	ТекущийШаг = "ДопРеквизиты";
	ЗаполнитьДокументыКомплектные();
	СобратьДокументыНеТребующиеЗаполнения();
	ЗаполнитьТаблицуДопРеквизиты();
	ВыделитьШагТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДокументыКомплектные()
	
	ДокументыКомплектные = Новый Массив;
	Для Каждого СтрокаКомплекта Из ДанныеДокументов Цикл
		Если СтрокаКомплекта.НомерКомплекта = 0 Тогда
			Продолжить;
		КонецЕсли;
		ДокументыКомплектные.Добавить(СтрокаКомплекта.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СобратьДокументыНеТребующиеЗаполнения()
	
	ДокументыПропуститьЗаполнение.Очистить();
	ДокументыПропуститьТаблицу.Очистить();
	
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
		ИмяГруппыШапкиКомплекта = "ГруппаКнопкиТаблицы" + ИмяТаблицы;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяГруппыШапкиКомплекта) Тогда
			СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
			Если СтрокиКомплекта.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыПоиска = Новый Структура("ДокументыПоТипам, ТипКомплекта, НаправлениеДокумента", Новый Соответствие);
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				ПараметрыПоиска.ДокументыПоТипам.Вставить(СтрокаТаблицы.ТипДокумента, СтрокаТаблицы.Ссылка);
			КонецЦикла;
			
			ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(СтрокиКомплекта[0].Ссылка,
				"ВидДокумента", "");
			Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
				ВидДокумента = ?(СтрокаТаблицы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий,
					"ПТИУ", "РТИУ");	
			КонецЕсли;
			ПараметрыПоиска.Вставить("ВидДокумента", ВидДокумента);
		
			ПараметрыПоиска.ТипКомплекта = СтрокаТаблицы.ТипКомплекта;
			ПараметрыПоиска.НаправлениеДокумента = СтрокаТаблицы.Направление;
			
			ОсновнойДокумент = РаспознаваниеДокументовКомплектыКлиентСервер.НайтиОсновнойДокументКомплекта(ПараметрыПоиска);
			
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				Если СтрокаТаблицы.Ссылка = ОсновнойДокумент Тогда
					Продолжить;
				КонецЕсли;
				ПараметрыПоиска.Вставить("ТипПодчиненного", СтрокаТаблицы.ТипДокумента);
				Реквизиты = РаспознаваниеДокументовКомплектыКлиентСервер.ОбязательныеРеквизитыПодчиненногоДокументаКомплекта(ПараметрыПоиска);
				
				Если Реквизиты = Неопределено Тогда
					Продолжить;
				ИначеЕсли Реквизиты.Количество() = 0 Тогда
					ДокументыПропуститьЗаполнение.Добавить(СтрокаТаблицы.Ссылка);
				Иначе
					// Тут 2 варианта:
					// 1. Сохранить список реквизитов, которые нужно заполнить - тогда нужно будет как-то блокировать реквизиты, которые будут заполнены из комплекта.
					// 2. Фактически заполнить реквизиты, которые могут быть заполнены из комплекта - тогда доп. блокировок делать не нужно.
					// Второй вариант кажется лучше.
					
					ПараметрыДополнения = Новый Структура;
					ПараметрыДополнения.Вставить("ОсновнойДокумент", ОсновнойДокумент);
					ПараметрыДополнения.Вставить("ДокументДляДополнения", СтрокаТаблицы.Ссылка);
					ПараметрыДополнения.Вставить("ОбязательныеРеквизиты", Реквизиты);
					
					ДополнитьИзКомплекта(ПараметрыДополнения);
					
					Если Реквизиты.Найти("ВсяТаблица") = Неопределено Тогда
						ДокументыПропуститьТаблицу.Добавить(СтрокаТаблицы.Ссылка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДополнитьИзКомплекта(ПараметрыДополнения)
	
	ОсновнойОбъект = ПараметрыДополнения.ОсновнойДокумент.ПолучитьОбъект();
	ОбъектДляДополнения = ПараметрыДополнения.ДокументДляДополнения.ПолучитьОбъект();
	Если ОсновнойОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату
		Или ОсновнойОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
		
		ДоговорВОсновном = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ОсновнойОбъект, "Договор");
		СтрокаДоговора = РаспознаваниеДокументовСлужебныйКлиентСервер.РеквизитДокумента(ОбъектДляДополнения, "Договор");
		Если ЗначениеЗаполнено(ДоговорВОсновном) И Не ЗначениеЗаполнено(СтрокаДоговора.Значение) Тогда
			СтрокаДоговора.Значение = ДоговорВОсновном;
			ОбъектДляДополнения.Записать();
		КонецЕсли;
		
	ИначеЕсли ОсновнойОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		Или ОсновнойОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
		
		НужноЗаписать = Ложь;
		
		ДоговорВОсновном = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ОсновнойОбъект, "Договор");
		СтрокаДоговора = РаспознаваниеДокументовСлужебныйКлиентСервер.РеквизитДокумента(ОбъектДляДополнения, "Договор");
		Если ЗначениеЗаполнено(ДоговорВОсновном) И Не ЗначениеЗаполнено(СтрокаДоговора.Значение) Тогда
			НужноЗаписать = Истина;
			СтрокаДоговора.Значение = ДоговорВОсновном;
		КонецЕсли;
		
		Если ОбъектДляДополнения.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			Или ОбъектДляДополнения.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
			Или ОбъектДляДополнения.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			
			СкладВОсновном = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ОсновнойОбъект, "Склад");
			СтрокаСклада = РаспознаваниеДокументовСлужебныйКлиентСервер.РеквизитДокумента(ОбъектДляДополнения, "Склад");
			Если ЗначениеЗаполнено(СкладВОсновном) И Не ЗначениеЗаполнено(СтрокаСклада.Значение) Тогда
				НужноЗаписать = Истина;
				СтрокаСклада.Значение = СкладВОсновном;
			КонецЕсли;
		КонецЕсли;
		
		Если НужноЗаписать Тогда
			ОбъектДляДополнения.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуДопРеквизиты()
	
	ИспользоватьПартнеровИКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	ИспользоватьПодразделения = ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам");
	
	РаспознанныеДокументы = ВыбранныеДокументы();
	ПропуститьЗаполнение = ДокументыПропуститьЗаполнение.ВыгрузитьЗначения();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", РаспознанныеДокументы);
	Запрос.УстановитьПараметр("ПропуститьЗаполнение", ПропуститьЗаполнение);
	Запрос.УстановитьПараметр("ПустойКонтрагент", Метаданные.ОпределяемыеТипы.КонтрагентБРД.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("ПустаяОрганизация", Метаданные.ОпределяемыеТипы.ОрганизацияБРД.Тип.ПривестиЗначение());
	Запрос.УстановитьПараметр("НеСопоставлен", НСтр("ru = 'Не сопоставлен: ';
													|en = 'Not mapped:'"));
	Запрос.УстановитьПараметр("НеСопоставлена", НСтр("ru = 'Не сопоставлена: ';
													|en = 'Not mapped: '"));
	Запрос.УстановитьПараметр("ИспользоватьПартнеров", ИспользоватьПартнеровИКонтрагентов);
	Запрос.УстановитьПараметр("ИспользоватьПодразделения", ИспользоватьПодразделения);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Ссылка КАК Ссылка,
	|	РаспознанныйДокумент.Дата КАК Дата,
	|	РаспознанныйДокумент.Направление КАК Направление,
	|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
	|	ВЫБОР
	|		КОГДА РаспознанныйДокумент.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияРаспознанногоДокумента.Входящий)
	|			ТОГДА ""БанковскийСчетКонтрагента""
	|		ИНАЧЕ ""БанковскийСчетОрганизации""
	|	КОНЕЦ КАК ИмяРеквизитаБанковскийСчет,
	|	ВЫБОР
	|		КОГДА РаспознанныйДокумент.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияРаспознанногоДокумента.Входящий)
	|			ТОГДА ""СоглашениеСПоставщиком""
	|		ИНАЧЕ ""СоглашениеСКлиентом""
	|	КОНЕЦ КАК ИмяРеквизитаСоглашение,
	|	РаспознанныйДокумент.Контрагент КАК Контрагент,
	|	РаспознанныйДокумент.Организация КАК Организация
	|ПОМЕСТИТЬ ОсновнаяТаблица
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка В(&РаспознанныеДокументы)
	|	И НЕ РаспознанныйДокумент.Ссылка В (&ПропуститьЗаполнение)
	|	И РаспознанныйДокумент.Контрагент <> &ПустойКонтрагент
	|	И РаспознанныйДокумент.Организация <> &ПустаяОрганизация
	|	И РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Справочник.Номенклатура).ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Товар)
	|					ИЛИ ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Справочник.Номенклатура).ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьТовары
	|ПОМЕСТИТЬ втПризнакиТовара
	|ИЗ
	|	ОсновнаяТаблица КАК ОсновнаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК РаспознанныйДокументРеквизитыТабличныхЧастей
	|		ПО ОсновнаяТаблица.Ссылка = РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка
	|			И (РаспознанныйДокументРеквизитыТабличныхЧастей.ИмяРеквизита = ""Номенклатура"")
	|			И (РаспознанныйДокументРеквизитыТабличныхЧастей.Значение ССЫЛКА Справочник.Номенклатура)
	|			И (НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.СтрокаУдалена)
	|
	|СГРУППИРОВАТЬ ПО
	|	ОсновнаяТаблица.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	ИСТИНА КАК КартинкаОткрытия,
	|	ЛОЖЬ КАК ДанныеЗаполнены,
	|	ОсновнаяТаблица.Направление КАК Направление,
	|	ОсновнаяТаблица.ТипДокумента КАК ТипДокумента,
	|	ОсновнаяТаблица.Контрагент КАК Контрагент,
	|	ОсновнаяТаблица.Организация КАК Организация,
	|	СтрокаДоговор.Значение КАК Договор,
	|	ВЫРАЗИТЬ(СтрокаДоговор.Значение КАК Справочник.ДоговорыКонтрагентов).ТипДоговора КАК ВидДоговора,
	|	ВЫРАЗИТЬ(СтрокаДоговор.Значение КАК Справочник.ДоговорыКонтрагентов).Контрагент КАК ДоговорКонтрагент,
	|	ВЫРАЗИТЬ(СтрокаДоговор.Значение КАК Справочник.ДоговорыКонтрагентов).Организация КАК ДоговорОрганизация,
	|	&НеСопоставлен + (ВЫРАЗИТЬ(СтрокаДоговор.РаспознанныйТекст КАК СТРОКА(1024))) КАК ДоговорРаспознано,
	|	СтрокаСклад.Значение КАК Склад,
	|	СтрокаСуммаВключаетНДС.Значение КАК СуммаВключаетНДС,
	|	СтрокаСрокОплаты.Значение КАК СрокОплаты,
	|	&НеСопоставлен + (ВЫРАЗИТЬ(СтрокаСрокОплаты.РаспознанныйТекст КАК СТРОКА(1024))) КАК СрокОплатыРаспознано,
	|	СтрокаПолучательОплаты.Значение КАК ПродавецОрганизация,
	|	&НеСопоставлена + (ВЫРАЗИТЬ(СтрокаПолучательОплаты.РаспознанныйТекст КАК СТРОКА(1024))) КАК ПродавецОрганизацияРаспознано,
	|	СтрокаБанковскийСчет.Значение КАК БанковскийСчет,
	|	СтрокаБанковскийСчет.Значение.Владелец КАК БанковскийСчетВладелец,
	|	&НеСопоставлен + (ВЫРАЗИТЬ(СтрокаБанковскийСчет.РаспознанныйТекст КАК СТРОКА(1024))) КАК БанковскийСчетРаспознано,
	|	СтрокаВидСкидки.Значение КАК ВидСкидки,
	|	СтрокаСоглашение.Значение КАК Соглашение,
	|	ЕСТЬNULL(СтрокаСоглашение.Значение.ИспользуютсяДоговорыКонтрагентов, ЛОЖЬ) КАК ВСоглашенииИспользуютсяДоговорыКонтрагентов,
	|	СтрокаПартнер.Значение КАК Партнер,
	|	СтрокаПодразделение.Значение КАК Подразделение,
	|	&ИспользоватьПартнеров КАК ИспользоватьПартнеров,
	|	СтрокаВидДокумента.Значение КАК ВидДокумента,
	|	&ИспользоватьПодразделения КАК ИспользоватьПодразделения,
	|	втПризнакиТовара.ЕстьТовары КАК ЕстьТовары
	|ИЗ
	|	ОсновнаяТаблица КАК ОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаДоговор
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаДоговор.Ссылка
	|			И (СтрокаДоговор.ИмяРеквизита = ""Договор"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаСклад
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСклад.Ссылка
	|			И (СтрокаСклад.ИмяРеквизита = ""Склад"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаСуммаВключаетНДС
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСуммаВключаетНДС.Ссылка
	|			И (СтрокаСуммаВключаетНДС.ИмяРеквизита = ""СуммаВключаетНДС"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаСрокОплаты
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСрокОплаты.Ссылка
	|			И (СтрокаСрокОплаты.ИмяРеквизита = ""СрокОплаты"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаПолучательОплаты
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаПолучательОплаты.Ссылка
	|			И (ОсновнаяТаблица.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату))
	|			И (ОсновнаяТаблица.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияРаспознанногоДокумента.Исходящий))
	|			И (СтрокаПолучательОплаты.ИмяРеквизита = ""ПродавецОрганизация"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаБанковскийСчет
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаБанковскийСчет.Ссылка
	|			И ОсновнаяТаблица.ИмяРеквизитаБанковскийСчет = СтрокаБанковскийСчет.ИмяРеквизита
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаВидСкидки
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВидСкидки.Ссылка
	|			И (СтрокаВидСкидки.ИмяРеквизита = ""ВидСкидки"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаСоглашение
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСоглашение.Ссылка
	|			И ОсновнаяТаблица.ИмяРеквизитаСоглашение = СтрокаСоглашение.ИмяРеквизита
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаПартнер
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаПартнер.Ссылка
	|			И (СтрокаПартнер.ИмяРеквизита = ""Партнер"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаПодразделение
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаПодразделение.Ссылка
	|			И (СтрокаПодразделение.ИмяРеквизита = ""Подразделение"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаВидДокумента
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВидДокумента.Ссылка
	|			И (СтрокаВидДокумента.ИмяРеквизита = ""ВидДокумента"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ втПризнакиТовара КАК втПризнакиТовара
	|		ПО ОсновнаяТаблица.Ссылка = втПризнакиТовара.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновнаяТаблица.Контрагент.Наименование,
	|	ОсновнаяТаблица.ТипДокумента.Порядок,
	|	ОсновнаяТаблица.Дата";
	
	ТаблицаДопРеквизитов.Очистить();
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	ДокументыБезДоговора = Новый Массив;
	СкладПоУмолчанию = РаспознаваниеДокументовСлужебный.ПолучитьСкладПоУмолчанию();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаДопРеквизитов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Выборка);
		
		ЭтоВходящееНаправление = (Выборка.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
		
		Если ЗначениеЗаполнено(СкладПоУмолчанию)
			И Не ЗначениеЗаполнено(СтрокаТаблицы.Склад)
			И  (Выборка.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			Или Выборка.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
			Или Выборка.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура) Тогда
			
			СтрокаТаблицы.Склад = СкладПоУмолчанию;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				СтрокаТаблицы.Ссылка,
				"Склад",
				СкладПоУмолчанию
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.БанковскийСчет) Тогда
			Если ЭтоВходящееНаправление Тогда
				Если Выборка.Контрагент <> Выборка.БанковскийСчетВладелец Тогда
					СтрокаТаблицы.БанковскийСчет = Неопределено;
				КонецЕсли;
			Иначе
				Если Выборка.ПродавецОрганизация <> Выборка.БанковскийСчетВладелец Тогда
					СтрокаТаблицы.БанковскийСчет = Неопределено;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыВыбораДоговора = Новый Структура;
		ПараметрыВыбораДоговора.Вставить("Контрагент", Выборка.Контрагент);
		ПараметрыВыбораДоговора.Вставить("Организация", Выборка.Организация);
		Если ПараметрыВыбораДоговора.Контрагент <> Выборка.ДоговорКонтрагент
			Или ПараметрыВыбораДоговора.Организация <> Выборка.ДоговорОрганизация Тогда
			
			СтрокаТаблицы.Договор = Неопределено;
		Иначе
			РаспознаваниеДокументовКлиентСерверПереопределяемый.ПриЗаполненииПараметровВыбораДоговора(Выборка, ПараметрыВыбораДоговора);
			Если ПараметрыВыбораДоговора.Свойство("ВидыДоговоров") <> Неопределено
				И ПараметрыВыбораДоговора.ВидыДоговоров.Найти(Выборка.ВидДоговора) = Неопределено Тогда
				
				СтрокаТаблицы.Договор = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Договор) Тогда
			ДокументыБезДоговора.Добавить(СтрокаТаблицы.Ссылка);
		КонецЕсли;
		
		СтрокаТаблицы.ИспользуютсяДоговорыКонтрагентов = ИспользуютсяДоговорыКонтрагентов(Выборка, ЭтоВходящееНаправление, 
			ИспользоватьСоглашенияСПоставщиками, ИспользоватьСоглашенияСКлиентами);
		
		СтрокаТаблицы.ВидДокумента = ?(ЗначениеЗаполнено(Выборка.ВидДокумента), Выборка.ВидДокумента, 
			ВидДокументаПоУмолчанию(Выборка.Направление));
			
		Если ЭтоВходящееНаправление Тогда
			Если ИспользоватьСоглашенияСПоставщиками И ЗначениеЗаполнено(СтрокаТаблицы.Партнер) Тогда
				КоличествоСоглашений = ЗакупкиВызовСервера.ПолучитьКоличествоСоглашенийСПоставщиком(СтрокаТаблицы.Партнер);
				СтрокаТаблицы.ИспользоватьСоглашение = КоличествоСоглашений > 0;
			Иначе
				СтрокаТаблицы.ИспользоватьСоглашение = ИспользоватьСоглашенияСПоставщиками;
			КонецЕсли;
		Иначе
			СтрокаТаблицы.ИспользоватьСоглашение = ИспользоватьСоглашенияСКлиентами;
		КонецЕсли;
		
	КонецЦикла;
	
	НайденныеДоговоры = РаспознаваниеДокументовСлужебный.НайтиДоговорыРаспознанныхДокументов(ДокументыБезДоговора);
	Для Каждого СтрокаТаблицы Из ТаблицаДопРеквизитов Цикл
		ДоговорДокумента = НайденныеДоговоры.Получить(СтрокаТаблицы.Ссылка);
		Если ДоговорДокумента <> Неопределено Тогда
			СтрокаТаблицы.Договор = ДоговорДокумента;
			СтрокаИзменений = Новый Структура("Ссылка, ИмяРеквизита, Значение",
				СтрокаТаблицы.Ссылка,
				"Договор",
				ДоговорДокумента
			);
			ДанныеФоновыхОпераций.ИзмененияРеквизитовШапки.Добавить(СтрокаИзменений);
		КонецЕсли;
		
		СтрокаТаблицы.ДанныеЗаполнены = ПроверкаДанныеЗаполненыДопРеквизиты(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуютсяДоговорыКонтрагентов(ДанныеСоглашения, 
										ЭтоВходящееНаправление, 
										ИспользоватьСоглашенияСПоставщиками, 
										ИспользоватьСоглашенияСКлиентами)
										
	Если ТипЗнч(ДанныеСоглашения) = Тип("Структура") 
		И Не ДанныеСоглашения.Свойство("ВСоглашенииИспользуютсяДоговорыКонтрагентов") Тогда
		
		СоглашениеИспользуется = ?(ЗначениеЗаполнено(ДанныеСоглашения.Соглашение), 
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСоглашения.Соглашение, 
			"ИспользуютсяДоговорыКонтрагентов", Истина), Ложь);
		ДанныеСоглашения.Вставить("ВСоглашенииИспользуютсяДоговорыКонтрагентов", СоглашениеИспользуется);
	
	КонецЕсли;
	
	Если ЭтоВходящееНаправление Тогда
			
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками") Тогда
			ИспользуютсяДоговорыКонтрагентов = Истина;
			Если ЗначениеЗаполнено(ДанныеСоглашения.Соглашение) И ИспользоватьСоглашенияСПоставщиками Тогда
				ИспользуютсяДоговорыКонтрагентов = ИспользуютсяДоговорыКонтрагентов 
					И ДанныеСоглашения.ВСоглашенииИспользуютсяДоговорыКонтрагентов; 	
			КонецЕсли;
		Иначе	
			ИспользуютсяДоговорыКонтрагентов = Ложь;
		КонецЕсли;
		
	Иначе
		
		Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
			ИспользуютсяДоговорыКонтрагентов = Истина;
			Если ЗначениеЗаполнено(ДанныеСоглашения.Соглашение) И ИспользоватьСоглашенияСКлиентами Тогда
				ИспользуютсяДоговорыКонтрагентов = ИспользуютсяДоговорыКонтрагентов 
					И ДанныеСоглашения.ВСоглашенииИспользуютсяДоговорыКонтрагентов; 	
			КонецЕсли;
		Иначе	
			ИспользуютсяДоговорыКонтрагентов = Ложь;
		КонецЕсли;
		
	КонецЕсли;	 
	
	Возврат ИспользуютсяДоговорыКонтрагентов;

КонецФункции // ИспользуютсяДоговорыКонтрагентов()

&НаСервереБезКонтекста
Функция ВидДокументаПоУмолчанию(Направление)

	Если Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		ВидДокумента = "ПТИУ";	
	Иначе
		ВидДокумента = "РТИУ";
	КонецЕсли;
	
	Возврат ВидДокумента;

КонецФункции // ВидДокументаПоУмолчанию()

&НаКлиентеНаСервереБезКонтекста
Функция ПроверкаДанныеЗаполненыДопРеквизиты(СтрокаТаблицы)
	
	ИменаРеквизитов = ОбязательныеРеквизитыТаблицаДопРеквизитов(СтрокаТаблицы);
	Результат = Истина;
	Для Каждого ИмяРеквизита Из ИменаРеквизитов Цикл
		Результат = Результат И ЗначениеЗаполнено(СтрокаТаблицы[ИмяРеквизита]);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОбязательныеРеквизитыТаблицаДопРеквизитов(СтрокаТаблицы)
	
	ОбязательноеЗаполнениеСклада = (СтрокаТаблицы.ВидДокумента = "ПТИУ" Или СтрокаТаблицы.ВидДокумента = "РТИУ");
	ТипДокумента = СтрокаТаблицы.ТипДокумента;
	
	Результат = Новый Массив;
	Если ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.УПД")
		Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12")
		Или ТипДокумента = ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетФактура")
		Или ТипДокумента = ПредопределенноеЗначение(
		"Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг") Тогда
		
		Если СтрокаТаблицы.ИспользуютсяДоговорыКонтрагентов Тогда
			Результат.Добавить("Договор");
		КонецЕсли;
		
		Если ОбязательноеЗаполнениеСклада И СтрокаТаблицы.ЕстьТовары Тогда
			Результат.Добавить("Склад");
		КонецЕсли;
		
	ИначеЕсли ТипДокумента = ПредопределенноеЗначение(
			"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") Тогда
		
		Результат.Добавить("БанковскийСчет");
		
		Если СтрокаТаблицы.Направление = ПредопределенноеЗначение(
				"Перечисление.НаправленияРаспознанногоДокумента.Исходящий") Тогда
			Результат.Добавить("ПродавецОрганизация");
		Иначе
			Если СтрокаТаблицы.ИспользуютсяДоговорыКонтрагентов Тогда
				Результат.Добавить("Договор");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
		
	Если СтрокаТаблицы.ИспользоватьПартнеров Тогда
		Результат.Добавить("Партнер");
	КонецЕсли;
	
	Если СтрокаТаблицы.ИспользоватьСоглашение Тогда
		Результат.Добавить("Соглашение");
	КонецЕсли;
	
	Если СтрокаТаблицы.ИспользоватьПодразделения
		И СтрокаТаблицы.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий")
		И Не ЗначениеЗаполнено(СтрокаТаблицы.Подразделение) 
		И Не СтрокаТаблицы.ТипДокумента = ПредопределенноеЗначение(
		"Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") Тогда
		Результат.Добавить("Подразделение");
	КонецЕсли;
		
	Возврат Результат;
		
КонецФункции

&НаКлиенте
Процедура НастройкаСлужебныхПолейТаблицыДопРеквизитов(ТекущиеДанные, ИмяРеквизитаШапки)

	Если ИмяРеквизитаШапки = "Соглашение" Тогда
		
		ЗапросНаСервер = Новый Структура;
		
		ЭтоВходящееНаправление = ТекущиеДанные.Направление 
			= ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий");
			
		Если ЭтоВходящееНаправление Тогда
			Если ИспользоватьСоглашенияСПоставщиками И ЗначениеЗаполнено(ТекущиеДанные.Партнер) Тогда
				
				ПараметрыЗапроса = Новый Структура("Партнер", ТекущиеДанные.Партнер);
				ЗапросНаСервер.Вставить("ИспользоватьСоглашение", ПараметрыЗапроса);
				
			Иначе
				ТекущиеДанные.ИспользоватьСоглашение = ИспользоватьСоглашенияСПоставщиками;
			КонецЕсли;
		Иначе
			ТекущиеДанные.ИспользоватьСоглашение = ИспользоватьСоглашенияСКлиентами;	
		КонецЕсли;
			
		Если ЗначениеЗаполнено(ТекущиеДанные.Соглашение) Тогда
			
			ДанныеСоглашения = Новый Структура("Соглашение", ТекущиеДанные.Соглашение);
			
			ПараметрыЗапроса = ПараметрыЗапросаСлужебныеПоляДопРеквизитов();
			ПараметрыЗапроса.ДанныеСоглашения = ДанныеСоглашения;
			ПараметрыЗапроса.ЭтоВходящееНаправление = ЭтоВходящееНаправление;
			ПараметрыЗапроса.ИспользоватьСоглашенияСПоставщиками = ИспользоватьСоглашенияСПоставщиками;
			ПараметрыЗапроса.ИспользоватьСоглашенияСКлиентами = ИспользоватьСоглашенияСКлиентами;
			
			ЗапросНаСервер.Вставить("ИспользуютсяДоговорыКонтрагентов", ПараметрыЗапроса);
			
		Иначе
			ТекущиеДанные.ИспользуютсяДоговорыКонтрагентов = Ложь;	
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(ЗапросНаСервер) Тогда
			
			ОтветССервера = СоглашениеСвязанныеПоля(ЗапросНаСервер);
			
			Для Каждого ЭлементСтруктуры Из ОтветССервера Цикл
				ТекущиеДанные[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение;
			КонецЦикла;
			
		КонецЕсли;
			    	
	КонецЕсли;	

КонецПроцедуры // НастройкаСлужебныхПолейТаблицыДопРеквизитов()

&НаКлиенте
Функция ПараметрыЗапросаСлужебныеПоляДопРеквизитов()

	ПараметрыЗапроса = Новый Структура();
	ПараметрыЗапроса.Вставить("ДанныеСоглашения");
	ПараметрыЗапроса.Вставить("ЭтоВходящееНаправление");
	ПараметрыЗапроса.Вставить("ИспользоватьСоглашенияСПоставщиками");
	ПараметрыЗапроса.Вставить("ИспользоватьСоглашенияСКлиентами");
	
	Возврат ПараметрыЗапроса;

КонецФункции // ПараметрыЗапросаСлужебныеПоляДопРеквизитов()

&НаСервереБезКонтекста
Функция СоглашениеСвязанныеПоля(ДанныеЗапроса)

	ДанныеОтвета = Новый Структура;
	
	ПараметрыЗапроса = Неопределено;
	ДанныеЗапроса.Свойство("ИспользоватьСоглашение", ПараметрыЗапроса);
	Если ЗначениеЗаполнено(ПараметрыЗапроса) Тогда
		КоличествоСоглашений = ЗакупкиВызовСервера.ПолучитьКоличествоСоглашенийСПоставщиком(ПараметрыЗапроса.Партнер);
		ДанныеОтвета.Вставить("ИспользоватьСоглашение", КоличествоСоглашений > 0);
	КонецЕсли;
	
	ПараметрыЗапроса = Неопределено;
	ДанныеЗапроса.Свойство("ИспользуютсяДоговорыКонтрагентов", ПараметрыЗапроса);
	Если ЗначениеЗаполнено(ПараметрыЗапроса) Тогда
		ИспользуютсяДоговорыКонтрагентов = ИспользуютсяДоговорыКонтрагентов(ПараметрыЗапроса.ДанныеСоглашения, 
			ПараметрыЗапроса.ЭтоВходящееНаправление, ПараметрыЗапроса.ИспользоватьСоглашенияСПоставщиками, 
			ПараметрыЗапроса.ИспользоватьСоглашенияСКлиентами);
		ДанныеОтвета.Вставить("ИспользуютсяДоговорыКонтрагентов", ИспользуютсяДоговорыКонтрагентов);
	КонецЕсли;
	
	Возврат ДанныеОтвета

КонецФункции // СоглашениеСвязанныеПоля()

#КонецОбласти

#Область СлужебныеПроцедурыНоменклатуры

&НаКлиенте
Процедура ПослеЗаполненияСтрокНоменклатуры()
	
	ОбновитьВидимостьНадписиПроЗаполненность();
	ПроверитьИЗагрузитьПустыеКартинки();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверкаДанныеЗаполненыНоменклатура(СтрокаТаблицы)
	
	СейчасЕстьНоменклатура = ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура);
	Если СтрокаТаблицы.ЕстьНоменклатура <> СейчасЕстьНоменклатура Тогда
		СтрокаТаблицы.ЕстьНоменклатура = ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура);
		СтрокаСКонтрагентом = СтрокаТаблицы.ПолучитьРодителя();
		Если СтрокаТаблицы.ЕстьНоменклатура Тогда
			
			Если Не СтрокаСКонтрагентом.ЕстьНоменклатура Тогда
				ВоВсехЕстьНоменклатура = Истина;
				Для Каждого СтрокаПодчиненная Из СтрокаСКонтрагентом.ПолучитьЭлементы() Цикл
					Если Не СтрокаПодчиненная.ЕстьНоменклатура Тогда
						ВоВсехЕстьНоменклатура = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				СтрокаСКонтрагентом.ЕстьНоменклатура = ВоВсехЕстьНоменклатура;
			КонецЕсли;
			
		Иначе
			СтрокаСКонтрагентом.ЕстьНоменклатура = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуНоменклатура(ИмяШага)
	
	ПерейтиНаСтраницуНоменклатураНаСервере(ИмяШага);
	
	// Вместо вызова Подключаемый_ПриАктивизацииЯчейкиНоменклатуры и ВыделитьНезаполненнуюЯчейкуНаШаге
	Для Каждого СтрокаГруппировки Из ТаблицаНоменклатуры.ПолучитьЭлементы() Цикл
		Если СкрытьЗаполненнуюНоменклатуру И СтрокаГруппировки.ЕстьНоменклатура Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого СтрокаТаблицы Из СтрокаГруппировки.ПолучитьЭлементы() Цикл
			Если СкрытьЗаполненнуюНоменклатуру И СтрокаТаблицы.ЕстьНоменклатура Тогда
				Продолжить;
			КонецЕсли;
			ТекущийЭлемент = Элементы.ТаблицаНоменклатуры;
			ТекущийЭлемент.ТекущаяСтрока = СтрокаТаблицы.ПолучитьИдентификатор();
			Прервать;
		КонецЦикла;
		
		Прервать;
	КонецЦикла;
	Элементы.ТаблицаНоменклатуры.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуНоменклатураНаСервере(ИмяШага)
	
	ТекущийШаг = ИмяШага;
	ЗаполнитьТаблицуНоменклатурыНаСервере();
	ВыделитьШагТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуНоменклатурыНаСервере()
	
	РаспознанныеДокументы = ВыбранныеДокументы();
	ПропуститьЗаполнение = ДокументыПропуститьЗаполнение.ВыгрузитьЗначения();
	ПропуститьТаблицу = ДокументыПропуститьТаблицу.ВыгрузитьЗначения();
	Для Каждого ДокументСсылка Из ПропуститьТаблицу Цикл
		ПропуститьЗаполнение.Добавить(ДокументСсылка);
	КонецЦикла;
	
	ВидыДокументов = Новый СписокЗначений;
	ВидыДокументов.Добавить("");
	ВидыДокументов.Добавить("ПТИУ");
	ВидыДокументов.Добавить("РТИУ");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", РаспознанныеДокументы);
	Запрос.УстановитьПараметр("ПропуститьЗаполнение", ПропуститьЗаполнение); 
	Запрос.УстановитьПараметр("НеСопоставлена", НСтр("ru = 'Не сопоставлена: ';
													|en = 'Not mapped: '"));
	Запрос.УстановитьПараметр("ВидыДокументов", ВидыДокументов);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка КАК Ссылка,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Направление КАК Направление,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.ТипДокумента КАК ТипДокумента,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Контрагент КАК Контрагент,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.ДатаДокумента КАК ДатаДокумента,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Дата КАК Дата,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.РаспознанныйТекст КАК СТРОКА(1024)) КАК Наименование
	|ПОМЕСТИТЬ ОсновнаяТаблица
	|ИЗ
	|	Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК РаспознанныйДокументРеквизитыТабличныхЧастей
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК РаспознанныйДокументРеквизитыДокумента
	|		ПО РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка = РаспознанныйДокументРеквизитыДокумента.Ссылка
	|			И (РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита = ""ВидДокумента"")
	|			И (РаспознанныйДокументРеквизитыДокумента.Значение В (&ВидыДокументов))
	|ГДЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка В(&РаспознанныеДокументы)
	|	И НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка В (&ПропуститьЗаполнение)
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.ИмяРеквизита = ""Номенклатура""
	|	И НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.СтрокаУдалена
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И НЕ РаспознанныйДокументРеквизитыДокумента.Ссылка ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	ОсновнаяТаблица.Контрагент КАК Контрагент,
	|	ОсновнаяТаблица.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	ОсновнаяТаблица.Номенклатура.Родитель КАК Родитель,
	|	NULL КАК ИспользованиеХарактеристик,
	|	ИСТИНА КАК КартинкаОткрытия,
	|	ВЫБОР
	|		КОГДА ОсновнаяТаблица.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ЕстьНоменклатура,
	|	ОсновнаяТаблица.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	ОсновнаяТаблица.Номенклатура КАК Номенклатура,
	|	&НеСопоставлена + ОсновнаяТаблица.Наименование КАК НоменклатураРаспознано,
	|	ОсновнаяТаблица.Наименование КАК Наименование,
	|	СтрокаАртикул.РаспознанныйТекст КАК Артикул,
	|	СтрокаЕдиницаИзмерения.Значение КАК ЕдиницаИзмерения,
	|	СтрокаСтавкаНДС.Значение КАК СтавкаНДС,
	|	СтрокаТипНоменклатуры.Значение КАК ТипНоменклатуры,
	|	СтрокаВидДокумента.Значение КАК ВидДокумента
	|ИЗ
	|	ОсновнаяТаблица КАК ОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаАртикул
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаАртикул.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаАртикул.НомерСтрокиТЧ
	|			И (СтрокаАртикул.ИмяРеквизита = ""Артикул"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаЕдиницаИзмерения
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаЕдиницаИзмерения.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаЕдиницаИзмерения.НомерСтрокиТЧ
	|			И (СтрокаЕдиницаИзмерения.ИмяРеквизита = ""ЕдиницаИзмерения"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтавкаНДС
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтавкаНДС.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтавкаНДС.НомерСтрокиТЧ
	|			И (СтрокаСтавкаНДС.ИмяРеквизита = ""СтавкаНДС"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаТипНоменклатуры
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаТипНоменклатуры.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаТипНоменклатуры.НомерСтрокиТЧ
	|			И (СтрокаТипНоменклатуры.ИмяРеквизита = ""ТипНоменклатуры"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаВидДокумента
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВидДокумента.Ссылка
	|			И (СтрокаВидДокумента.ИмяРеквизита = ""ВидДокумента"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновнаяТаблица.Контрагент.Наименование,
	|	ОсновнаяТаблица.Номенклатура.Наименование,
	|	Наименование,
	|	ОсновнаяТаблица.Дата,
	|	НомерСтрокиТЧ
	|ИТОГИ
	|	МИНИМУМ(Контрагент),
	|	МИНИМУМ(ЕстьНоменклатура)
	|ПО
	|	Контрагент";
	
	ДеревоНоменклатуры = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	ЗначениеВРеквизитФормы(ДеревоНоменклатуры, "ТаблицаНоменклатуры");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНаСтраницуСумм()
	
	ПерейтиНаСтраницуСуммНаСервере();
	ВыделитьНезаполненнуюЯчейкуНаШаге();
	#Если ВебКлиент Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиСумм", 0.1, Истина);
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуСуммНаСервере()
	
	ТекущийШаг = "Суммы";
	Элементы.ОбработатьДокументы.Видимость = Ложь;
	Элементы.КнопкаДалее.Видимость = Истина;
	
	ЗаполнитьТаблицуСумм();
	
	ВыделитьШагТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСумм()
	
	РаспознанныеДокументы = ВыбранныеДокументы();
	ПропуститьЗаполнение = ДокументыПропуститьЗаполнение.ВыгрузитьЗначения();
	ПропуститьТаблицу = ДокументыПропуститьТаблицу.ВыгрузитьЗначения();
	Для Каждого ДокументСсылка Из ПропуститьТаблицу Цикл
		ПропуститьЗаполнение.Добавить(ДокументСсылка);
	КонецЦикла;
	
	УчитыватьДоходыРасходы = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	УчитыватьАктивыПассивы = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов");
	ИспользоватьСкладыВТЧ = ПолучитьФункциональнуюОпцию("ИспользоватьСкладыВТабличнойЧастиДокументовПродажи");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("РаспознанныеДокументы", РаспознанныеДокументы);
	Запрос.УстановитьПараметр("ПропуститьЗаполнение", ПропуститьЗаполнение);
	Запрос.УстановитьПараметр("НаОтдельныеПозиции", РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции);
	Запрос.УстановитьПараметр("ПустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	Запрос.УстановитьПараметр("НеСопоставлена", НСтр("ru = 'Не сопоставлена: ';
													|en = 'Not mapped: '"));
	Запрос.УстановитьПараметр("УчетПрочихДоходовРасходов", УчитыватьДоходыРасходы);
	Запрос.УстановитьПараметр("УчетПрочихАктивовПассивов", УчитыватьАктивыПассивы);
	Запрос.УстановитьПараметр("ИспользоватьСкладыВТЧ", ИспользоватьСкладыВТЧ);
	Запрос.УстановитьПараметр("ИспользоватьИмпортныеТовары", ИспользоватьИмпортныеТовары);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка КАК Ссылка,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Направление КАК Направление,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Дата КАК Дата,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.ТипДокумента КАК ТипДокумента,
	|	&НеСопоставлена + (ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.РаспознанныйТекст КАК СТРОКА(1024))) КАК НоменклатураРаспознано,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Номенклатура,
	|	ВЫРАЗИТЬ(РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Справочник.Номенклатура).ТипНоменклатуры КАК ТипНоменклатуры
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК РаспознанныйДокументРеквизитыТабличныхЧастей
	|ГДЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка В(&РаспознанныеДокументы)
	|	И НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка В (&ПропуститьЗаполнение)
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.ИмяРеквизита = ""Номенклатура""
	|	И НЕ РаспознанныйДокументРеквизитыТабличныхЧастей.СтрокаУдалена
	|	И РаспознанныйДокументРеквизитыТабличныхЧастей.Значение <> &ПустаяНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	ОсновнаяТаблица.Направление КАК Направление,
	|	ОсновнаяТаблица.Дата КАК Дата,
	|	ОсновнаяТаблица.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	ОсновнаяТаблица.ТипДокумента КАК ТипДокумента,
	|	ВЫРАЗИТЬ(ЕСТЬNULL(СтрокаСуммаВключаетНДС.Значение, ЛОЖЬ) КАК БУЛЕВО)
	|		И (ОсновнаяТаблица.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг)
	|			ИЛИ ОсновнаяТаблица.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату)) КАК СуммаВключаетНДС,
	|	ОсновнаяТаблица.НоменклатураРаспознано КАК НоменклатураРаспознано,
	|	ОсновнаяТаблица.Номенклатура КАК Номенклатура,
	|	ОсновнаяТаблица.Номенклатура.ВестиУчетПоГТД КАК ВедетсяУчетПоГТД,
	|	ОсновнаяТаблица.ТипНоменклатуры КАК ТипНоменклатуры
	|ПОМЕСТИТЬ ОсновнаяТаблица
	|ИЗ
	|	ТаблицаНоменклатуры КАК ОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаСуммаВключаетНДС
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСуммаВключаетНДС.Ссылка
	|			И (СтрокаСуммаВключаетНДС.ИмяРеквизита = ""СуммаВключаетНДС"")
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновнаяТаблица.Ссылка КАК Ссылка,
	|	ИСТИНА КАК КартинкаОткрытия,
	|	ОсновнаяТаблица.Дата КАК Дата,
	|	ОсновнаяТаблица.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	ОсновнаяТаблица.ТипДокумента КАК ТипДокумента,
	|	ВЫБОР
	|		КОГДА СтрокаВидДокумента.Значение = ""ПУИПА""
	|				ИЛИ СтрокаВидДокумента.Значение = ""РУИПА""
	|			ТОГДА СтрокаСодержание.Значение
	|		ИНАЧЕ ОсновнаяТаблица.Номенклатура
	|	КОНЕЦ КАК Номенклатура,
	|	&ИспользоватьИмпортныеТовары
	|		И ЕСТЬNULL(ОсновнаяТаблица.ВедетсяУчетПоГТД, ЛОЖЬ) КАК ВедетсяУчетПоГТД,
	|	ОсновнаяТаблица.НоменклатураРаспознано КАК НоменклатураРаспознано,
	|	СтрокаКоличество.Значение КАК Количество,
	|	СтрокаЦена.Значение КАК Цена,
	|	СтрокаСумма.Значение КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОсновнаяТаблица.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату)
	|				И ЕСТЬNULL(СтрокаВидСкидки.Значение, 0) = &НаОтдельныеПозиции
	|			ТОГДА ЕСТЬNULL(СтрокаСуммаСкидки.Значение, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаСкидки,
	|	ВЫБОР
	|		КОГДА ОсновнаяТаблица.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату)
	|			ТОГДА ЕСТЬNULL(СтрокаВидСкидки.Значение, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ВидСкидки,
	|	ОсновнаяТаблица.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	СтрокаСтавкаНДС.Значение КАК СтавкаНДС,
	|	СтрокаСуммаНДС.Значение КАК СуммаНДС,
	|	СтрокаВсего.Значение КАК Всего,
	|	СтрокаСтранаПроисхождения.Значение КАК СтранаПроисхождения,
	|	СтрокаНомерГТД.Значение КАК НомерГТД,
	|	СтрокаВидДокумента.Значение КАК ВидДокумента,
	|	СтрокаПодразделение.Значение КАК ТПодразделение,
	|	СтрокаСклад.Значение КАК ТСклад,
	|	ВЫБОР
	|		КОГДА &ИспользоватьСкладыВТЧ
	|				И СтрокаСклад.Значение ССЫЛКА Справочник.Склады
	|				И НЕ СтрокаСклад.Значение = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА ЕСТЬNULL(СтрокаСклад.Значение.ЭтоГруппа, ЛОЖЬ)
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СкладГруппа,
	|	ОсновнаяТаблица.Направление КАК Направление,
	|	СтрокаСписатьНаРасходы.Значение КАК СписатьНаРасходы,
	|	СтрокаСтатьяРасходов.Значение КАК СтатьяРасходов,
	|	СтрокаСтатьяДоходов.Значение КАК СтатьяДоходов,
	|	СтрокаСтатья.Значение КАК Статья,
	|	&УчетПрочихДоходовРасходов КАК УчетПрочихДоходовРасходов,
	|	&УчетПрочихАктивовПассивов КАК УчетПрочихАктивовПассивов,
	|	ОсновнаяТаблица.ТипНоменклатуры КАК ТипНоменклатуры
	|ИЗ
	|	ОсновнаяТаблица КАК ОсновнаяТаблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаКоличество
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаКоличество.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаКоличество.НомерСтрокиТЧ
	|			И (СтрокаКоличество.ИмяРеквизита = ""Количество"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаЦена
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаЦена.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаЦена.НомерСтрокиТЧ
	|			И (СтрокаЦена.ИмяРеквизита = ""Цена"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСумма
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСумма.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСумма.НомерСтрокиТЧ
	|			И (СтрокаСумма.ИмяРеквизита = ""Сумма"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтавкаНДС
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтавкаНДС.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтавкаНДС.НомерСтрокиТЧ
	|			И (СтрокаСтавкаНДС.ИмяРеквизита = ""СтавкаНДС"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСуммаНДС
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСуммаНДС.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСуммаНДС.НомерСтрокиТЧ
	|			И (СтрокаСуммаНДС.ИмяРеквизита = ""СуммаНДС"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаВсего
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВсего.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаВсего.НомерСтрокиТЧ
	|			И (СтрокаВсего.ИмяРеквизита = ""Всего"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтранаПроисхождения
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтранаПроисхождения.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтранаПроисхождения.НомерСтрокиТЧ
	|			И (СтрокаСтранаПроисхождения.ИмяРеквизита = ""СтранаПроисхождения"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаНомерГТД
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаНомерГТД.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаНомерГТД.НомерСтрокиТЧ
	|			И (СтрокаНомерГТД.ИмяРеквизита = ""НомерГТД"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСуммаСкидки
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСуммаСкидки.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСуммаСкидки.НомерСтрокиТЧ
	|			И (СтрокаСуммаСкидки.ИмяРеквизита = ""СуммаСкидки"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаВидСкидки
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВидСкидки.Ссылка
	|			И (СтрокаВидСкидки.ИмяРеквизита = ""ВидСкидки"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК СтрокаВидДокумента
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаВидДокумента.Ссылка
	|			И (СтрокаВидДокумента.ИмяРеквизита = ""ВидДокумента"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаПодразделение
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаПодразделение.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаПодразделение.НомерСтрокиТЧ
	|			И (СтрокаПодразделение.ИмяРеквизита = ""ТПодразделение"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСклад
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСклад.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСклад.НомерСтрокиТЧ
	|			И (СтрокаСклад.ИмяРеквизита = ""ТСклад"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСодержание
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСодержание.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСодержание.НомерСтрокиТЧ
	|			И (СтрокаСодержание.ИмяРеквизита = ""Содержание"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСписатьНаРасходы
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСписатьНаРасходы.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСписатьНаРасходы.НомерСтрокиТЧ
	|			И (СтрокаСписатьНаРасходы.ИмяРеквизита = ""СписатьНаРасходы"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтатьяРасходов
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтатьяРасходов.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтатьяРасходов.НомерСтрокиТЧ
	|			И (СтрокаСтатьяРасходов.ИмяРеквизита = ""СтатьяРасходов"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтатьяДоходов
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтатьяДоходов.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтатьяДоходов.НомерСтрокиТЧ
	|			И (СтрокаСтатьяДоходов.ИмяРеквизита = ""СтатьяДоходов"")
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК СтрокаСтатья
	|		ПО ОсновнаяТаблица.Ссылка = СтрокаСтатья.Ссылка
	|			И ОсновнаяТаблица.НомерСтрокиТЧ = СтрокаСтатья.НомерСтрокиТЧ
	|			И (СтрокаСтатья.ИмяРеквизита = ""Статья"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	ОсновнаяТаблица.Дата,
	|	ОсновнаяТаблица.НомерСтрокиТЧ";
	
	ТаблицаСумм.Загрузить(Запрос.Выполнить().Выгрузить());
	
	Для Каждого СтрокаТаблицы Из ТаблицаСумм Цикл
		ЗаполнитьОшибкиСуммВСтроке(СтрокаТаблицы);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииЯчейкиНоменклатуры() Экспорт
	
	ТаблицаЭлемент = Элементы.ТаблицаНоменклатуры;
	ДанныеЭтойСтроки = ТаблицаЭлемент.ТекущиеДанные;
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДляТаблиц
		Или Элементы.СтраницыТаблиц.ТекущаяСтраница <> Элементы.СтраницаНоменклатура
		Или ДанныеЭтойСтроки = Неопределено
		Или Не ЗначениеЗаполнено(ДанныеЭтойСтроки.Ссылка)
		Или ТаблицаЭлемент.ТекущийЭлемент = Неопределено
		Или (СкрытьЗаполненнуюНоменклатуру И ВсеСтрокиНаШагеЗаполнены) Тогда
		
		Возврат;
	КонецЕсли;
	
	Если ПолнаяКартинкаВидна И ТипЗнч(Элементы.ПолеПросмотра.Документ) <> Тип("ВнешнийОбъект") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиНоменклатуры", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	Если СтрНачинаетсяС(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаНоменклатурыНоменклатура")
		И ТаблицаЭлемент.ТекущийЭлемент.Имя <> "ТаблицаНоменклатурыНоменклатура" Тогда
		
		ИмяРеквизита = СтрЗаменить(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаНоменклатурыНоменклатура", "");
	ИначеЕсли СтрНачинаетсяС(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаНоменклатуры") Тогда
		ИмяРеквизита = СтрЗаменить(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаНоменклатуры", "");
	Иначе
		ИмяРеквизита = ТаблицаЭлемент.ТекущийЭлемент.Имя;
	КонецЕсли;
	ТекущееИмяРеквизита = ИмяРеквизита;
	
	Если ИмяРеквизита = "Контрагент" Тогда
		Возврат;
	ИначеЕсли ИмяРеквизита = "Наименование" Тогда
		ИмяРеквизита = "Номенклатура";
	КонецЕсли;
	
	ОбновитьПолнуюКартинку = (ПолнаяКартинкаВидна И ТекущаяСтрокаДляСпискаВыбора <> ТаблицаЭлемент.ТекущаяСтрока);
	ТекущаяСтрокаДляСпискаВыбора = ТаблицаЭлемент.ТекущаяСтрока;
	
	ПриАктивизацииЯчейкиТаблицы(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриАктивизацииЯчейкиТаблицы(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку)
	
	ИмяКолонки = ТаблицаЭлемент.ТекущийЭлемент.Имя;
	ТекущиеДанные = ТаблицаЭлемент.ТекущиеДанные;
	
	Если ИмяРеквизита = "КартинкаОткрытия"
		Или ИмяРеквизита = "НоменклатураРодитель"
		Или ИмяРеквизита = "НоменклатураВидНоменклатуры"
		Или ИмяРеквизита = "Родитель"
		Или ИмяРеквизита = "ВидНоменклатуры" Тогда
		
		Если ОбновитьПолнуюКартинку И HTMLДокументСформирован Тогда
			АдресПолнойКартинки = ПолучитьАдресПолнойКартинки(ТекущиеДанные.Ссылка, УникальныйИдентификатор);
			РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресПолнойКартинки);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("Ссылка", ТекущиеДанные.Ссылка);
	ПараметрыПолучения.Вставить("НомерСтрокиТЧ", ТекущиеДанные.НомерСтрокиТЧ);
	ПараметрыПолучения.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыПолучения.Вставить("ОбновитьПолнуюКартинку", ОбновитьПолнуюКартинку);
	ПараметрыПолучения.Вставить("ИмяРеквизита", ИмяРеквизита);
	ДанныеЯчейки = РаспознаваниеДокументовКомплектыВызовСервера.ДанныеЯчейкиТаблицыНаСервере(ПараметрыПолучения);
	
	Если ОбновитьПолнуюКартинку И ДанныеЯчейки.Свойство("АдресПолнойКартинки") Тогда
		АдресПолнойКартинки = ДанныеЯчейки.АдресПолнойКартинки;
		Если HTMLДокументСформирован Тогда
			РаспознаваниеДокументовСлужебныйКлиент.ЗагрузитьКартинкуПоАдресу(Элементы.ПолеПросмотра, АдресПолнойКартинки);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеЯчейки.Свойство("КартинкаВнутриHTML") Тогда
		КартинкаРеквизита = ДанныеЯчейки.КартинкаВнутриHTML;
	КонецЕсли;
	
	// список заполняется через "Быстрый выбор"
	Если Не ИмяРеквизита = "ЕдиницаИзмерения" И Не ИмяРеквизита = "СписатьНаРасходы" Тогда 
		ЭлементКолонка = Элементы[ИмяКолонки];
		ЭлементКолонка.СписокВыбора.Очистить();
		ТипЗначения = ТипЗнч(ТекущиеДанные[ТекущееИмяРеквизита]);
		
		ЕстьПравоСоздания = СправочникиБезПравНаСоздание.НайтиПоЗначению(ИмяРеквизита) = Неопределено;
		
		Если ЗначениеЗаполнено(ДанныеЯчейки.РаспознанныйТекст) Тогда
			Если ДанныеЯчейки.ЗначенияВыбора.Количество() И ТекущееИмяРеквизита <> "Наименование" Тогда
				СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ПолучитьЗначенияСпискаВыбора(
					ДанныеЯчейки.РаспознанныйТекст, ТипЗначения, ДанныеЯчейки.ЗначенияВыбора, ЕстьПравоСоздания);
			Иначе
				СписокДляВыбора = РаспознаваниеДокументовСлужебныйКлиент.ПолучитьЗначенияСпискаВыбора(
					ДанныеЯчейки.РаспознанныйТекст, ТипЗначения, Неопределено, ЕстьПравоСоздания);
			КонецЕсли;
		Иначе
			СписокДляВыбора = Новый СписокЗначений;
		КонецЕсли;
		
		Для Каждого ДанныеВыбора Из СписокДляВыбора Цикл
			ЭлементКолонка.СписокВыбора.Добавить(ДанныеВыбора.Значение, ДанныеВыбора.Представление, , ДанныеВыбора.Картинка);
		КонецЦикла;
	КонецЕсли;
	
	Если ПолнаяКартинкаВидна Тогда
		КоординатыВыделения = ДанныеЯчейки.КоординатыКартинки;
		СтрокВИзображенииВыделения = ДанныеЯчейки.СтрокВИзображении;
		ПодключитьОбработчикОжидания("Подключаемый_ПриблизитьПоКоординатам", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураНоменклатураПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтметкуДляЗаполнения(Элемент)
	
	ИмяРеквизитаФормы = "Отметка" + Элемент.Имя;
	ЭтотОбъект[ИмяРеквизитаФормы] = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТаблицуСозданнойНоменклатурой(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьТаблицуСозданнойНоменклатуройНаСервере(Результат, ДополнительныеПараметры);
	ПослеЗаполненияСтрокНоменклатуры();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуСозданнойНоменклатуройНаСервере(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИнформацияОНоменклатуре = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	ПолучитьСообщенияПользователю(Истина);
	Для Каждого Сообщение Из Результат.Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
	Для Каждого Данные Из ИнформацияОНоменклатуре Цикл
		СтрокаЗаполнения = ТаблицаНоменклатуры.НайтиПоИдентификатору(Данные.ИдентификаторИсходнойСтроки);
		СтрокаЗаполнения.Номенклатура = Данные.Номенклатура;
		ПроверкаДанныеЗаполненыНоменклатура(СтрокаЗаполнения);
		
		Если Данные.ЭтоДубль Тогда
			РеквизитыИзБазы = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Данные.Номенклатура, "Наименование, Артикул");
			
			НоменклатураКСозданию = СтрокаЗаполнения.Наименование;
			Если ЗначениеЗаполнено(СтрокаЗаполнения.Артикул) Тогда
				НоменклатураКСозданию = НоменклатураКСозданию + ", " + СтрокаЗаполнения.Артикул;
			КонецЕсли;
			
			НоменклатураВБазе = РеквизитыИзБазы.Наименование;
			Если ЗначениеЗаполнено(РеквизитыИзБазы.Артикул) Тогда
				НоменклатураВБазе = НоменклатураВБазе + ", " + РеквизитыИзБазы.Артикул;
			КонецЕсли;
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон(НСтр("ru = 'Новая номенклатура ""%1"" не создана. Реквизит сопоставлен с имеющейся в базе номенклатурой ""%2""';
											|en = 'A new ""%1"" item is not created. The attribute is mapped to the ""%2"" item existing in the infobase'"),
				НоменклатураКСозданию, НоменклатураВБазе);
			Сообщение.Сообщить();
		КонецЕсли;
		
		СтрокаИзменений = Новый Структура("Ссылка, НомерСтрокиТЧ, ИмяРеквизита, Значение",
			СтрокаЗаполнения.Ссылка,
			СтрокаЗаполнения.НомерСтрокиТЧ,
			"Номенклатура",
			Данные.Номенклатура
		);
		ДанныеФоновыхОпераций.ИзмененияНоменклатуры.Добавить(СтрокаИзменений);
	КонецЦикла;
	
	Документы.РаспознанныйДокумент.ЗаписатьИзмененияРеквизитовТаблицы(ДанныеФоновыхОпераций.ИзмененияНоменклатуры);
	ДанныеФоновыхОпераций.ИзмененияНоменклатуры.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтерактивноеГрупповоеСозданиеНоменклатуры(ВсегоЭлементов = Неопределено)
	
	ВыделенныеСтроки = Элементы.ТаблицаНоменклатуры.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Перед созданием номенклатуры необходимо выделить нужные строки в таблице.';
									|en = 'Before creating items, select the required rows in the table.'");
		ПоказатьПредупреждение( , ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	ВыделеноСтрок = ВыделенныеСтроки.Количество();
	
	ПоследнийИндекс = ВыделеноСтрок - 1;
	Для Сч = 0 По ПоследнийИндекс Цикл
		ТекущийИндекс = ПоследнийИндекс - Сч;
		ИдентификаторСтроки = ВыделенныеСтроки[ТекущийИндекс];
		СтрокаТаблицы = ТаблицаНоменклатуры.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура)
			Или Не ЗначениеЗаполнено(СтрокаТаблицы.Ссылка) Тогда
			ВыделенныеСтроки.Удалить(ТекущийИндекс);
		КонецЕсли;
	КонецЦикла;
	
	ВыделеноСтрок = ВыделенныеСтроки.Количество();
	Если ВыделеноСтрок = 0 Тогда
		Возврат;		
	КонецЕсли;
	
	Если ВсегоЭлементов = Неопределено Тогда
    	ВсегоЭлементов = ВыделеноСтрок;
	КонецЕсли;
			
	ТекущийЭлементНоменклатуры = ВсегоЭлементов - ВыделеноСтрок + 1;
	
	ИдентификаторСтроки = ВыделенныеСтроки[0];
	СтрокаТаблицы = ТаблицаНоменклатуры.НайтиПоИдентификатору(ИдентификаторСтроки);
		
	ОповещениеОЗакрытииФормы = Новый ОписаниеОповещения("ПослеЗакрытияФормыСозданияЭлемента",
		ЭтотОбъект, Новый Структура("ИдентификаторСтроки, ВсегоЭлементов", ИдентификаторСтроки, ВсегоЭлементов));
	ТекстЗаголовка = НСтр("ru = 'Групповое создание нераспознанной номенклатуры (%1 из %2)';
							|en = 'Bulk creation of unrecognized items (%1 out of %2)'");
	ЗаголовокФормыЭлементаБРД = СтрШаблон(ТекстЗаголовка, ТекущийЭлементНоменклатуры, ВсегоЭлементов);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Артикул", СтрокаТаблицы.Артикул);
	ДополнительныеПараметры.Вставить("ВидНоменклатуры", СтрокаТаблицы.ВидНоменклатуры);
	ДополнительныеПараметры.Вставить("Родитель", СтрокаТаблицы.Родитель);
	ДополнительныеПараметры.Вставить("ЕдиницаИзмерения", СтрокаТаблицы.ЕдиницаИзмерения);
	ДополнительныеПараметры.Вставить("СтавкаНДС", СтрокаТаблицы.СтавкаНДС);
	ДополнительныеПараметры.Вставить("ЗаголовокФормыЭлементаБРД", ЗаголовокФормыЭлементаБРД);
	
	ДополнительныеДанные = Новый Структура;
	ДополнительныеДанные.Вставить("ИмяТаблицы", "РеквизитыТабличныхЧастей");
	ДополнительныеДанные.Вставить("РаспознанныйДокумент", СтрокаТаблицы.Ссылка);
	
	ДанныеРаспознанногоДокумента = ДанныеРаспознанногоДокумента(СтрокаТаблицы.Ссылка);
	НаборДанных = РаспознаваниеДокументовСлужебныйКлиентСервер.НаборДанныхСтрокиТаблицы(
		ДанныеРаспознанногоДокумента.СвойстваЯчеекТаблицы, СтрокаТаблицы.НомерСтрокиТЧ);
	НаборДанных = РаспознаваниеДокументовСлужебныйКлиент.НаборДанныхСозданияЭлемента(НаборДанных);

	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДополнительныеПараметры", ДополнительныеПараметры);
	ПараметрыОткрытия.Вставить("ИмяЭлемента", "Номенклатура");
	ПараметрыОткрытия.Вставить("ТекстЗаполнения", СтрокаТаблицы.Наименование);
	ПараметрыОткрытия.Вставить("ДополнительныеДанные", ДополнительныеДанные);
	ПараметрыОткрытия.Вставить("НаборДанных", НаборДанных);
	ПараметрыОткрытия.Вставить("ТипДокумента", ДанныеРаспознанногоДокумента.ТипДокумента);
	ПараметрыОткрытия.Вставить("Направление", ДанныеРаспознанногоДокумента.Направление);
		
	ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", ПараметрыОткрытия, ЭтотОбъект, , , ,
		ОповещениеОЗакрытииФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыСозданияЭлемента(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		НоменклатураСсылка = РезультатЗакрытия;
	Иначе
		НоменклатураСсылка = Неопределено;
		ДополнительныеПараметры.Свойство("НоменклатураСсылка", НоменклатураСсылка);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоменклатураСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ПослеЗакрытияФормыСозданияЭлементаНаСервере(НоменклатураСсылка, ДополнительныеПараметры.ИдентификаторСтроки);
	ПроверитьИЗагрузитьПустыеКартинки();
	
	ИнтерактивноеГрупповоеСозданиеНоменклатуры(ДополнительныеПараметры.ВсегоЭлементов);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗакрытияФормыСозданияЭлементаНаСервере(Номенклатура, ИдентификаторСтроки)
	
	СтрокаТаблицы = ТаблицаНоменклатуры.НайтиПоИдентификатору(ИдентификаторСтроки);
	СтрокаТаблицы.Номенклатура = Номенклатура;
	ПроверкаДанныеЗаполненыНоменклатура(СтрокаТаблицы);
	
	СтрокаИзменений = Новый Структура("Ссылка, НомерСтрокиТЧ, ИмяРеквизита, Значение",
		СтрокаТаблицы.Ссылка,
		СтрокаТаблицы.НомерСтрокиТЧ,
		"Номенклатура",
		Номенклатура
	);
	
	ДанныеФоновыхОпераций.ИзмененияНоменклатуры.Добавить(СтрокаИзменений);
	Документы.РаспознанныйДокумент.ЗаписатьИзмененияРеквизитовТаблицы(ДанныеФоновыхОпераций.ИзмененияНоменклатуры);
	ДанныеФоновыхОпераций.ИзмененияНоменклатуры.Очистить();
	
	ОбновитьВидимостьНадписиПроЗаполненность();

КонецПроцедуры // ПослеЗакрытияФормыСозданияЭлементаНаСервере()
	
&НаСервереБезКонтекста
Функция ДанныеРаспознанногоДокумента(РаспознанныйДокумент)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.ИмяРеквизита КАК ИмяРеквизита,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Значение КАК Значение,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.УверенностьНайденногоЗначения КАК УверенностьНайденногоЗначения,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.РаспознанныйТекст КАК РаспознанныйТекст,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.ЗаполненоВручную КАК ЗаполненоВручную,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.СтрокВИзображении КАК СтрокВИзображении,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.ЭлементСоздан КАК ЭлементСоздан,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.НомерСтроки КАК НомерСтроки,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.НомерСтрокиТЧ КАК НомерСтрокиТЧ,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.НайденноеЗначение КАК НайденноеЗначение,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.КоординатаX0 КАК КоординатаX0,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.КоординатаY0 КАК КоординатаY0,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.КоординатаX1 КАК КоординатаX1,
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.КоординатаY1 КАК КоординатаY1
	|ИЗ
	|	Документ.РаспознанныйДокумент.РеквизитыТабличныхЧастей КАК РаспознанныйДокументРеквизитыТабличныхЧастей
	|ГДЕ
	|	РаспознанныйДокументРеквизитыТабличныхЧастей.Ссылка = &РаспознанныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
	|	РаспознанныйДокумент.Направление КАК Направление
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка = &РаспознанныйДокумент";
	
	Запрос.УстановитьПараметр("РаспознанныйДокумент", РаспознанныйДокумент);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаДокумента = РезультатЗапроса[0].Выгрузить();
	РеквизитыТабличныхЧастей = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаДокумента);
	
	Объект = Новый Структура("Ссылка, РеквизитыТабличныхЧастей", РаспознанныйДокумент, РеквизитыТабличныхЧастей);
	
	СвойстваЯчеекТаблицы = РаспознаваниеДокументовСлужебный.СвойстваЯчеекТаблицы(Объект);
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Выборка.Следующий();
	
	ДанныеДокумента = Новый Структура("СвойстваЯчеекТаблицы, ТипДокумента, Направление",
		СвойстваЯчеекТаблицы, Выборка.ТипДокумента, Выборка.Направление);
		
	Возврат ДанныеДокумента;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыСумм

&НаКлиенте
Процедура ПослеЗаполненияСтрокСумм()
	
	СкрытьЗаполненныеСтрокиШапки();
	ОбновитьВидимостьНадписиПроЗаполненность();
	ПроверитьИЗагрузитьПустыеКартинки();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьЗаполненныеСтрокиСумм()
	
	Если СкрытьЗаполненныеСтрокиСумм Тогда
		Элементы.ТаблицаСумм.ОтборСтрок = Новый ФиксированнаяСтруктура("ЕстьОшибка", Истина);
	Иначе
		Элементы.ТаблицаСумм.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриАктивизацииЯчейкиСумм() Экспорт
	
	ТаблицаЭлемент = Элементы.ТаблицаСумм;
	ДанныеЭтойСтроки = ТаблицаЭлемент.ТекущиеДанные;
	Если Элементы.СтраницыШаги.ТекущаяСтраница <> Элементы.СтраницаДляТаблиц
		Или Элементы.СтраницыТаблиц.ТекущаяСтраница <> Элементы.СтраницаСуммы
		Или ДанныеЭтойСтроки = Неопределено Тогда
		
		Возврат;
	КонецЕсли;
	
	Если ПолнаяКартинкаВидна И ТипЗнч(Элементы.ПолеПросмотра.Документ) <> Тип("ВнешнийОбъект") Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПриАктивизацииЯчейкиСумм", 0.3, Истина);
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(ТаблицаЭлемент.ТекущийЭлемент.Имя, "ТаблицаСумм", "");
	ТекущееИмяРеквизита = ИмяРеквизита;
	
	ОбновитьВидимостьИСуммыДляОшибкиТЧ(ДанныеЭтойСтроки, ИмяРеквизита);
	
	ОбновитьПолнуюКартинку = (ПолнаяКартинкаВидна И ТекущаяСтрокаДляСпискаВыбора <> ТаблицаЭлемент.ТекущаяСтрока);
	ТекущаяСтрокаДляСпискаВыбора = ТаблицаЭлемент.ТекущаяСтрока;
	
	ПриАктивизацииЯчейкиТаблицы(ТаблицаЭлемент, ИмяРеквизита, ОбновитьПолнуюКартинку);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОшибкиСуммВСтроке(СтрокаТаблицы)
	
	// Объединить проверки с РаспознаваниеДокументовСлужебный.ДобавитьОшибкиКоличествоЦенаСумма
	// и заменить в РаспознаваниеДокументовСлужебныйКлиентСервер.ДобавитьОшибкиКоличествоЦенаСумма
	Если СтрокаТаблицы.ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
		Если Окр(СтрокаТаблицы.Количество * СтрокаТаблицы.Цена, 2) <> СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаСкидки
			И (СтрокаТаблицы.Количество = 0
			Или Окр((СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаСкидки) / СтрокаТаблицы.Количество, 2) <> СтрокаТаблицы.Цена) Тогда
			
			СтрокаТаблицы.ОшибкаЦены = Истина;
		Иначе
			СтрокаТаблицы.ОшибкаЦены = Ложь;
		КонецЕсли;
	Иначе
		Если Окр(СтрокаТаблицы.Количество * СтрокаТаблицы.Цена, 2) <> СтрокаТаблицы.Сумма
			И (СтрокаТаблицы.Количество = 0 Или Окр(СтрокаТаблицы.Сумма / СтрокаТаблицы.Количество, 2) <> СтрокаТаблицы.Цена) Тогда
			
			СтрокаТаблицы.ОшибкаЦены = Истина;
		Иначе
			СтрокаТаблицы.ОшибкаЦены = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	СтрокаТаблицы.ОшибкаНДС = Не РассчитанныйНДССовпадаетСЗаполненным(СтрокаТаблицы, СтрокаТаблицы.СуммаВключаетНДС);
	СтрокаТаблицы.ОшибкаВсего = Не ЗначенияСуммаСуммаНДСВсегоСовпадают(СтрокаТаблицы, СтрокаТаблицы.СуммаВключаетНДС);
	
	СтрокаТаблицы.ЕстьОшибка =
		Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура)
		Или Не ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС)
		Или СтрокаТаблицы.Количество = 0
		Или СтрокаТаблицы.Цена = 0
		Или СтрокаТаблицы.Сумма = 0
		Или СтрокаТаблицы.ОшибкаЦены Или СтрокаТаблицы.ОшибкаНДС Или СтрокаТаблицы.ОшибкаВсего
		Или ТаблицаСуммОшибкаЗаполненияНомераГТД(СтрокаТаблицы)
		Или ТаблицаСуммОшибкаЗаполненияСтатьяДоходов(СтрокаТаблицы)
		Или ТаблицаСуммОшибкаЗаполненияСтатьяРасходов(СтрокаТаблицы)
		Или ТаблицаСуммОшибкаЗаполненияТПодразделение(СтрокаТаблицы); 
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаСуммОшибкаЗаполненияНомераГТД(СтрокаТаблицы)
	
	Если СтрокаТаблицы.ТипДокумента 
		= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату")
		Или СтрокаТаблицы.ТипДокумента 
		= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.ТОРГ12") Тогда
	
		Ошибка = Ложь;
	
	Иначе
	
		Ошибка = СтрокаТаблицы.ВедетсяУчетПоГТД И (Не ЗначениеЗаполнено(СтрокаТаблицы.НомерГТД) 
			Или Не ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения));	
	
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции // ТаблицаСуммОшибкаЗаполненияГТД()

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаСуммОшибкаЗаполненияСтатьяДоходов(СтрокаТаблицы)
	
	Ошибка = Ложь;
	
	Если СтрокаТаблицы.ВидДокумента = "РУИПА"
		И Не СтрокаТаблицы.ТипДокумента 
		= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату")
		И Не ЗначениеЗаполнено(СтрокаТаблицы.СтатьяДоходов) Тогда	
		
		Ошибка = Истина;	
	
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции // ТаблицаСуммОшибкаЗаполненияСтатьяДоходов()

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаСуммОшибкаЗаполненияСтатьяРасходов(СтрокаТаблицы)
	
	СтатьяРасходовНеЗаполнена = СтрокаТаблицы.СписатьНаРасходы И Не ЗначениеЗаполнено(СтрокаТаблицы.СтатьяРасходов);
	
	Если СтрокаТаблицы.ТипДокумента 
		= ПредопределенноеЗначение("Перечисление.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату") Тогда
	
	    Ошибка = СтатьяРасходовНеЗаполнена И СтрокаТаблицы.Направление 
			= ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий");
		
	Иначе
		
		Ошибка = СтатьяРасходовНеЗаполнена И СтрокаТаблицы.ВидДокумента = "ПТИУ";
	
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции // ТаблицаСуммОшибкаЗаполненияСтатьяРасходов()

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаСуммОшибкаЗаполненияТПодразделение(СтрокаТаблицы)
	
	ВидимостьПодразделения = Истина;
	
	ТипыНоменклатуры = Новый Массив;
	ТипыНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	ТипыНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));  
	ТипыНоменклатуры.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Услуга"));
	
	ТипыНоменклатурыТоварТара = Новый Массив;
	ТипыНоменклатурыТоварТара.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар"));
	ТипыНоменклатурыТоварТара.Добавить(ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара"));
	
	Если (Не ТипыНоменклатуры.Найти(СтрокаТаблицы.ТипНоменклатуры) = Неопределено
		И СтрокаТаблицы.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Исходящий")
		И СтрокаТаблицы.СкладГруппа) 
		Или (Не ТипыНоменклатурыТоварТара.Найти(СтрокаТаблицы.ТипНоменклатуры) = Неопределено
		И СтрокаТаблицы.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий") 
		И СтрокаТаблицы.СкладГруппа) Тогда
	
		ВидимостьПодразделения = Ложь;
	
	КонецЕсли;
	
	Если ВидимостьПодразделения Тогда
	    Ошибка = СтрокаТаблицы.СписатьНаРасходы И Не ЗначениеЗаполнено(СтрокаТаблицы.ТПодразделение)
			И СтрокаТаблицы.Направление = ПредопределенноеЗначение("Перечисление.НаправленияРаспознанногоДокумента.Входящий")
			И СтрокаТаблицы.ВидДокумента = "ПТИУ";
	Иначе
		Ошибка = Ложь;
	КонецЕсли;
	
	Возврат Ошибка;
	
КонецФункции // ТаблицаСуммОшибкаЗаполненияСтатьяРасходов()

&НаКлиентеНаСервереБезКонтекста
Функция ЗначенияСуммаСуммаНДСВсегоСовпадают(ПроверяемаяЗапись, СуммаВключаетНДС)
	
	// Объединить или заменить проверки с РаспознаваниеДокументовСлужебный.ЗначенияСуммаСуммаНДСВсегоСовпадают
	Если СуммаВключаетНДС Тогда
		Возврат ПроверяемаяЗапись.Сумма = ПроверяемаяЗапись.Всего;
	Иначе
		Возврат ПроверяемаяЗапись.Сумма + ПроверяемаяЗапись.СуммаНДС = ПроверяемаяЗапись.Всего;
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РассчитанныйНДССовпадаетСЗаполненным(ПроверяемаяЗапись, СуммаВключаетНДС)
	
	СуммаНДС = РассчитатьОкругленнуюСуммуНДС(ПроверяемаяЗапись, СуммаВключаетНДС);
	РазницаНДС = СуммаНДС - ПроверяемаяЗапись.СуммаНДС;
	
	Возврат (-0.01 <= РазницаНДС И РазницаНДС <= 0.01);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РассчитатьОкругленнуюСуммуНДС(ПроверяемаяЗапись, СуммаВключаетНДС)
	
	СтавкаНДС = УчетНДСУПКлиентСервер.ЗначениеСтавкиНДС(ПроверяемаяЗапись.СтавкаНДС);
	СуммаНДС = РаспознаваниеДокументовСлужебныйКлиентСервер.РассчитатьСуммуНДС(ПроверяемаяЗапись.Сумма, СуммаВключаетНДС, СтавкаНДС);
	
	Возврат Окр(СуммаНДС, 2);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьВидимостьИСуммыДляОшибкиТЧ(ДанныеЭтойСтроки, ИмяРеквизита);
	
	Если ДанныеЭтойСтроки.ОшибкаЦены И (ИмяРеквизита = "Количество"
		Или ИмяРеквизита = "Цена"
		Или ИмяРеквизита = "СуммаСкидки"
		Или ИмяРеквизита = "Сумма") Тогда
		
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		Если ДанныеЭтойСтроки.ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
			НадписьОшибкаТЧ = СтрШаблон(НСтр("ru = 'Количество * Цена - Скидка (= %1) не совпадает с Сумма (= %2)';
											|en = 'Quantity * Price - Discount (= %1) does not match Amount (= %2)'"),
				ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Количество * ДанныеЭтойСтроки.Цена - ДанныеЭтойСтроки.СуммаСкидки),
				ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Сумма));
		Иначе
			Если ИмяРеквизита = "СуммаСкидки" Тогда
				Элементы.НадписьОшибкаТЧ.Видимость = Ложь;
				Возврат;
			КонецЕсли;
			
			НадписьОшибкаТЧ = СтрШаблон(НСтр("ru = 'Количество * Цена (= %1) не совпадает с Сумма (= %2)';
											|en = 'Quantity * Price (= %1) does not match Amount (= %2)'"),
				ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Количество * ДанныеЭтойСтроки.Цена),
				ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Сумма));
		КонецЕсли;
		
	ИначеЕсли ДанныеЭтойСтроки.Количество = 0 И ИмяРеквизита = "Количество" Тогда
		
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		НадписьОшибкаТЧ = НСтр("ru = 'Количество не может быть равно 0';
								|en = 'Quantity cannot be equal to 0'");
		
	ИначеЕсли ДанныеЭтойСтроки.Цена = 0 И ИмяРеквизита = "Цена" Тогда
		
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		НадписьОшибкаТЧ = НСтр("ru = 'Цена не может быть равна 0,00';
								|en = 'Price cannot be 0.00'");
		
	ИначеЕсли ДанныеЭтойСтроки.Сумма = 0 И ИмяРеквизита = "Сумма" Тогда
		
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		НадписьОшибкаТЧ = НСтр("ru = 'Сумма не может быть равна 0,00';
								|en = 'Amount cannot be 0.00'");
		
	ИначеЕсли ДанныеЭтойСтроки.ОшибкаНДС И (ИмяРеквизита = "СтавкаНДС" Или ИмяРеквизита = "СуммаНДС") Тогда
		
		ОкругленнаяСуммаНДС = РассчитатьОкругленнуюСуммуНДС(ДанныеЭтойСтроки, ДанныеЭтойСтроки.СуммаВключаетНДС);
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		НадписьОшибкаТЧ = СтрШаблон(НСтр("ru = 'Рассчитанная сумма НДС (= %1) не совпадает с заполненной суммой НДС (= %2)';
										|en = 'The calculated VAT amount (= %1) does not match the filled VAT amount (= %2)'"),
			ПредставлениеДенежнойСуммы(ОкругленнаяСуммаНДС),
			ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.СуммаНДС));
		
	ИначеЕсли ДанныеЭтойСтроки.ОшибкаВсего И (ИмяРеквизита = "Сумма"
		Или ИмяРеквизита = "СуммаНДС"
		Или ИмяРеквизита = "Всего")
		Тогда
		
		Элементы.НадписьОшибкаТЧ.Видимость = Истина;
		НадписьОшибкаТЧ = СтрШаблон(НСтр("ru = 'Сумма + Сумма НДС (= %1) не совпадает с Всего (= %2)';
										|en = 'Amount + VAT amount (= %1) does not match Total (= %2)'"),
			ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Сумма + ДанныеЭтойСтроки.СуммаНДС),
			ПредставлениеДенежнойСуммы(ДанныеЭтойСтроки.Всего));
	Иначе
		Элементы.НадписьОшибкаТЧ.Видимость = Ложь;
		НадписьОшибкаТЧ = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНовоеЗначениеРеквизитаТаблицы(Параметры)
	
	Для Каждого СтрокаДляЗаполнения Из Параметры.СтрокиТаблицы Цикл
		ДанныеЭтойСтроки = Параметры.ТаблицаФормы.ДанныеСтроки(СтрокаДляЗаполнения);
		
		СтрокаИзменений = Новый Структура("Ссылка, НомерСтрокиТЧ, ИмяРеквизита, Значение",
			ДанныеЭтойСтроки.Ссылка,
			ДанныеЭтойСтроки.НомерСтрокиТЧ,
			Параметры.ИмяРеквизита,
			Параметры.ЗначениеЗаполнения
		);
		Параметры.ДанныеИзменений.Добавить(СтрокаИзменений);
	КонецЦикла;
	
	ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицы()
	
	Если Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаНоменклатура Тогда
		ПараметрыОперации = ДанныеФоновыхОпераций.ИзмененияНоменклатуры;
	ИначеЕсли Элементы.СтраницыТаблиц.ТекущаяСтраница = Элементы.СтраницаСуммы Тогда
		ПараметрыОперации = ДанныеФоновыхОпераций.ИзмененияТаблицыСумм;
	Иначе
		Возврат;
	КонецЕсли;
	
	ЕстьНезаписанныеРеквизитыТаблицы = Истина;
	Результат = ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицыНаСервере(ПараметрыОперации, УникальныйИдентификатор);
	Если Результат.ОперацияЗапущена Тогда
		
		ПараметрыОперации.Очистить();
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицыЗавершение", ЭтотОбъект, ПараметрыОперации);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат.ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		ПовторноЗапуститьЗаполнениеРеквизитовТаблицы = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицыНаСервере(ПараметрыОперации, ИдентификаторФормы)
	
	Результат = Новый Структура("ОперацияЗапущена, ДлительнаяОперация", Ложь);
	
	КлючЗадания = "ГрупповаяОбработкаРаспознанныхДокументовРеквизитовТаблицы";
	
	ОтборЗаданий = Новый Структура;
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	ОтборЗаданий.Вставить("Ключ", КлючЗадания);
	
	УстановитьПривилегированныйРежим(Истина);
	Отказ = (ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий).Количество() > 0);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не Отказ Тогда
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(ИдентификаторФормы);
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Групповой ввод реквизитов таблицы';
																|en = 'Bulk input of table attributes'");
		
		ИмяМетода = "Документы.РаспознанныйДокумент.ЗаписатьИзмененияРеквизитовТаблицы";
		
		Результат.ОперацияЗапущена = Истина;
		Результат.ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыОперации, ПараметрыВыполнения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // отменено пользователем
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = Результат.ПодробноеПредставлениеОшибки;
		Сообщение.Сообщить();
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		Если ПовторноЗапуститьЗаполнениеРеквизитовТаблицы Тогда
			ПовторноЗапуститьЗаполнениеРеквизитовТаблицы = Ложь;
			ЗапуститьФоновоеЗаданиеЗаписиРеквизитовТаблицы();
		Иначе
			ЕстьНезаписанныеРеквизитыТаблицы = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыСкладПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыСуммаВключаетНДСПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыСрокОплатыПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыПродавецОрганизацияПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДопРеквизитыВидСкидкиПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВШапкеДатаПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВШапкеКонтрагентПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВШапкеОрганизацияПриИзменении(Элемент)
	УстановитьОтметкуДляЗаполнения(Элемент);
КонецПроцедуры

&НаКлиенте
Функция НовыеПараметрыЗаписиСтрокиТаблицыСумм(СтрокиТаблицы, ИмяРеквизита, ЗначениеЗаполнения)

	ПараметрыЗаписиРеквизитов = Новый Структура;
	ПараметрыЗаписиРеквизитов.Вставить("ТаблицаФормы", Элементы.ТаблицаСумм);
	ПараметрыЗаписиРеквизитов.Вставить("ДанныеИзменений", ДанныеФоновыхОпераций.ИзмененияТаблицыСумм);
	ПараметрыЗаписиРеквизитов.Вставить("СтрокиТаблицы", СтрокиТаблицы);
	ПараметрыЗаписиРеквизитов.Вставить("ИмяРеквизита", ИмяРеквизита);
	ПараметрыЗаписиРеквизитов.Вставить("ЗначениеЗаполнения", ЗначениеЗаполнения);
	ПараметрыЗаписиРеквизитов.Вставить("ОбновитьЗначениеВЯчейке", Ложь);
	
	Возврат ПараметрыЗаписиРеквизитов;

КонецФункции // НовыеПараметрыЗаписиСтрокиТаблицыСумм()


#КонецОбласти

#Область СлужебныеПроцедурыИтого

&НаКлиенте
Процедура ПерейтиНаСтраницуИтого()
	
	Если ЕстьНезаписанныеРеквизитыШапки Или ЕстьНезаписанныеРеквизитыТаблицы Тогда
		ПодключитьОбработчикОжидания("ПерейтиНаСтраницуИтого", 0.5, Истина);
		Возврат;
	КонецЕсли;
	
	ПерейтиНаСтраницуИтогоНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиНаСтраницуИтогоНаСервере()
	
	ТекущийШаг = "Итого";
	
	Элементы.ОбработатьДокументы.Видимость = Истина;
	Элементы.КнопкаДалее.Видимость = Ложь;
	ЗаполнитьТаблицыИтого();
	
	ТребуетсяОбработатьДокументы = ТаблицаИтогоОдиночныеДокументы.Количество() > 0
		Или ТаблицаИтогоПрикрепитьСканы.Количество() > 0 Или ТаблицаИтогоКомплекты.ПолучитьЭлементы().Количество() > 0;
	Элементы.ОбработатьДокументы.Доступность = ТребуетсяОбработатьДокументы;
	
	ВыделитьШагТекущейСтраницы();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицыИтого()
	
	// Проверка на валидность комплектов. Если ее нет, то расформировываем комплект.
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
		Если СтрокиКомплекта.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Расформировать = Ложь;
		Если Не РаспознаваниеДокументовКомплектыКлиентСервер.ЭтоТипКомплектаМожноОбработать(СтрокиКомплекта[0].ТипКомплекта) Тогда
			Расформировать = Истина;
		Иначе
			Для Каждого СтрокаДанных Из СтрокиКомплекта Цикл
				Если СтрокаДанных.НаправлениеДокумента <> СтрокиКомплекта[0].НаправлениеДокумента Тогда
					Расформировать = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если Расформировать Тогда
			УдалитьКомплектНаСервере(НомерКомплекта, Ложь, Ложь);
		КонецЕсли;
	КонецЦикла;
	
	ДокументыВсе = ВыбранныеДокументы();
	ПропуститьЗаполнение = ДокументыПропуститьЗаполнение.ВыгрузитьЗначения();
	ДокументыОдиночные = ДанныеДокументов.Выгрузить(Новый Структура("НомерКомплекта", 0), "Ссылка").ВыгрузитьКолонку("Ссылка");
	
	ТаблицаОшибок = РаспознаваниеДокументовКомплекты.ОшибкиПоДокументам(ДокументыВсе, "ТолькоМешающиеПроведению");
	
	// Обновление ошибок в комплектах
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
		ИмяГруппыШапкиКомплекта = "ГруппаКнопкиТаблицы" + ИмяТаблицы;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяГруппыШапкиКомплекта) Тогда
			СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
			Если СтрокиКомплекта.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ПараметрыПоиска = Новый Структура("ДокументыПоТипам, ТипКомплекта, НаправлениеДокумента", Новый Соответствие);
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				ПараметрыПоиска.ДокументыПоТипам.Вставить(СтрокаТаблицы.ТипДокумента, СтрокаТаблицы.Ссылка);
			КонецЦикла;
			ПараметрыПоиска.ТипКомплекта = СтрокаТаблицы.ТипКомплекта;
			ПараметрыПоиска.НаправлениеДокумента = СтрокаТаблицы.Направление;
			ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(СтрокиКомплекта[0].Ссылка, "ВидДокумента", "");
			Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
				ВидДокумента = ?(СтрокаТаблицы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий,
					"ПТИУ", "РТИУ");	
			КонецЕсли;
			ПараметрыПоиска.Вставить("ВидДокумента", ВидДокумента);
			
			ОсновнойДокумент = РаспознаваниеДокументовКомплектыКлиентСервер.НайтиОсновнойДокументКомплекта(ПараметрыПоиска);
			
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				Если СтрокаТаблицы.Ссылка <> ОсновнойДокумент Тогда
					СтрокаСОшибкой = ТаблицаОшибок.НайтиСтроки(Новый Структура("Ссылка", СтрокаТаблицы.Ссылка));
					Если СтрокаСОшибкой.Количество() <> 1 Тогда
						Продолжить;
					Иначе
						СтрокаСОшибкой = СтрокаСОшибкой[0];
					КонецЕсли;
					
					СтрокаСОшибкой.КоличествоОшибок = 0;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Комплектные = ДокументыКомплектные.ВыгрузитьЗначения();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыВсе", ДокументыВсе);
	Запрос.УстановитьПараметр("ДокументыОдиночные", ДокументыОдиночные);
	Запрос.УстановитьПараметр("ПропуститьЗаполнение", ПропуститьЗаполнение);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ДокументРаспознанныйДокумент.Ссылка КАК Ссылка,
	|	СвязанныеОбъектыРаспознаниеДокументов.ПотенциальныйКандидат КАК ПотенциальныйКандидат
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК ДокументРаспознанныйДокумент
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СвязанныеОбъектыРаспознаниеДокументов КАК СвязанныеОбъектыРаспознаниеДокументов
	|		ПО (ДокументРаспознанныйДокумент.Ссылка В (&ДокументыОдиночные))
	|			И (ДокументРаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый))
	|			И ДокументРаспознанныйДокумент.Ссылка = СвязанныеОбъектыРаспознаниеДокументов.РаспознанныйДокумент
	|			И (СвязанныеОбъектыРаспознаниеДокументов.ПотенциальныйКандидат = ИСТИНА)
	|			И (НЕ СвязанныеОбъектыРаспознаниеДокументов.СсылкаНаОбъект.ПометкаУдаления)";
	
	ОдиночныеБезСканов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	Запрос.УстановитьПараметр("ОдиночныеБезСканов", ОдиночныеБезСканов);
	
	// Сначала разделим комплектные документы на те, которые нужно создать, из которых нужно прикрепить сканы
	// и оставшиеся, по которым делать ничего не нужно
	ДокументыКомплектовСоздать = Новый Массив;
	ДокументыКомплектовПрикрепитьСкан = Новый Массив;
	
	Для НомерКомплекта = 1 По КоличествоКомплектов Цикл
		ИмяТаблицы = "Комплект" + Формат(НомерКомплекта, "ЧГ=");
		ИмяГруппыШапкиКомплекта = "ГруппаКнопкиТаблицы" + ИмяТаблицы;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элементы, ИмяГруппыШапкиКомплекта) Тогда
			СтрокиКомплекта = ДанныеДокументов.НайтиСтроки(Новый Структура("НомерКомплекта", НомерКомплекта));
			Если СтрокиКомплекта.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ДокументыПоТипам = Новый Соответствие;
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				ДокументыПоТипам.Вставить(СтрокаТаблицы.ТипДокумента, СтрокаТаблицы.Ссылка);
			КонецЦикла;
			
			ПараметрыПоиска = Новый Структура;
			ПараметрыПоиска.Вставить("НаправлениеДокумента", СтрокаТаблицы.Направление);
			ПараметрыПоиска.Вставить("ТипКомплекта", СтрокаТаблицы.ТипКомплекта);
			ПараметрыПоиска.Вставить("РаспознанныеДокументыПоТипам", ДокументыПоТипам);
			
			ВидДокумента = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(СтрокиКомплекта[0].Ссылка,
				"ВидДокумента", "");
			Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
				ВидДокумента = ?(СтрокаТаблицы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий,
					"ПТИУ", "РТИУ");	
			КонецЕсли;
			ПараметрыПоиска.Вставить("ВидДокумента", ВидДокумента);
			
			РаспознаваниеДокументовКомплекты.ОбновитьИЗаполнитьСвязанныеИСозданныеДокументы(ПараметрыПоиска);
			
			Для Каждого СтрокаТаблицы Из СтрокиКомплекта Цикл
				Если ПропуститьЗаполнение.Найти(СтрокаТаблицы.Ссылка) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				Для Каждого СозданныйДокумент Из ПараметрыПоиска.СозданныеДокументы Цикл
					ТипыРаспознанныхДокументов = РаспознаваниеДокументовКомплектыКлиентСервер.ПодходящиеТипыРаспознанногоДокумента(СозданныйДокумент.Ключ);
					Если ТипыРаспознанныхДокументов.Найти(СтрокаТаблицы.ТипДокумента) = Неопределено Тогда
						// Точно пропускаем случаи, когда СозданныйДокумент.Ключ = "Счет от поставщика", СтрокаТаблицы.ТипДокумента = Акт и т.п.
						Продолжить;
					КонецЕсли;
					
					Если Не СозданныйДокумент.Значение.ДокументНайден Тогда
						ДокументыКомплектовСоздать.Добавить(СтрокаТаблицы.Ссылка);
					ИначеЕсли Не СозданныйДокумент.Значение.СканУжеПрикреплен Тогда
						ДокументыКомплектовПрикрепитьСкан.Добавить(СтрокаТаблицы.Ссылка);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	Запрос.УстановитьПараметр("ДокументыКомплектовСоздать", ДокументыКомплектовСоздать);
	Запрос.УстановитьПараметр("ДокументыКомплектовПрикрепитьСкан", ДокументыКомплектовПрикрепитьСкан);
	Запрос.УстановитьПараметр("ТаблицаОшибок", ТаблицаОшибок);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОшибок.Ссылка КАК Ссылка,
	|	ТаблицаОшибок.КоличествоОшибок КАК КоличествоОшибок
	|ПОМЕСТИТЬ ВТ_ТаблицаОшибок
	|ИЗ
	|	&ТаблицаОшибок КАК ТаблицаОшибок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	РаспознанныйДокумент.Ссылка КАК Ссылка,
	|	РаспознанныйДокумент.Направление КАК Направление,
	|	ВЫБОР
	|		КОГДА РаспознанныйДокумент.Направление = ЗНАЧЕНИЕ(Перечисление.НаправленияРаспознанногоДокумента.Входящий)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК НаправлениеДокумента,
	|	РаспознанныйДокумент.Дата КАК Дата,
	|	РаспознанныйДокумент.ТипДокумента КАК ТипДокумента,
	|	РаспознанныйДокумент.Наименование КАК Наименование,
	|	РаспознанныйДокумент.Контрагент КАК Контрагент,
	|	РаспознанныйДокумент.Организация КАК Организация
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТаблицаОшибок КАК ВТ_ТаблицаОшибок
	|		ПО РаспознанныйДокумент.Ссылка = ВТ_ТаблицаОшибок.Ссылка
	|ГДЕ
	|	&УсловиеТаблицы
	|	И РаспознанныйДокумент.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	// Не будут обработаны, если (КоличествоОшибок <> 0) И Не ПропуститьЗаполнение
	// Будут созданы, если (КоличествоОшибок = 0) И ((ДокументыОдиночные И Не ОдиночныеБезСканов) Или ДокументыКомплектовСоздать)
	// Будут прикреплены сканы, если (КоличествоОшибок = 0) И (ОдиночныеБезСканов или ДокументыКомплектовПрикрепитьСкан)
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&УсловиеТаблицы",
		"РаспознанныйДокумент.Ссылка В (&ДокументыВсе)
		|	И НЕ РаспознанныйДокумент.Ссылка В(&ПропуститьЗаполнение)
		|	И ВТ_ТаблицаОшибок.КоличествоОшибок <> 0");
	КоличествоНеОбработано = Запрос.Выполнить().Выгрузить().Количество();
	Если КоличествоНеОбработано = 0 Тогда
		Элементы.ДекорацияИтогоНеОбработано.Видимость = Ложь;
	Иначе
		Элементы.ДекорацияИтогоНеОбработано.Видимость = Истина;
		Элементы.ДекорацияИтогоНеОбработано.Заголовок =
			СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
				НСтр("ru = ';Не будет обработан %1 документ, поскольку в нем;;
						  |Не будут обработаны %1 документа, поскольку в них;
						  |Не будут обработаны %1 документов, поскольку в них;
						  |Не будут обработаны %1 документа, поскольку в них';
						  |en = ';%1 document will not be processed as it contains;;;;
						  |%1 documents will not be processed as they contain'"), КоличествоНеОбработано)
			+ НСтр("ru = ' остались нераспознанные реквизиты. Для корректировки вернитесь на предыдущие шаги.';
					|en = ' there are unrecognized attributes. To correct them, return to the previous steps.'");
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&УсловиеТаблицы",
		"(РаспознанныйДокумент.Ссылка В (&ДокументыОдиночные)
		|				И НЕ РаспознанныйДокумент.Ссылка В (&ОдиночныеБезСканов)
		|			ИЛИ РаспознанныйДокумент.Ссылка В (&ДокументыКомплектовСоздать))
		|	И ВТ_ТаблицаОшибок.КоличествоОшибок = 0");
	
	ВыборкаОдиночныеДокументы = Запрос.Выполнить().Выбрать();
	ТаблицаИтогоОдиночныеДокументы.Очистить();
	
	Пока ВыборкаОдиночныеДокументы.Следующий() Цикл
	
		Если ВыборкаОдиночныеДокументы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
			
			Если ВыборкаОдиночныеДокументы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
				ИмяТипа = "ЗаказПоставщикуБРД";
			Иначе
				ИмяТипа = "ЗаказКлиентаБРД";
			КонецЕсли;
			
			ТипСоздаваемого = Метаданные.ОпределяемыеТипы[ИмяТипа].Тип.Типы()[0];
			ИмяДокумента = СтрРазделить(XMLТип(ТипСоздаваемого).ИмяТипа, ".")[1];
			Если Не ПравоДоступа("Добавление", Метаданные.Документы[ИмяДокумента]) Тогда
				Продолжить;
			КонецЕсли;
			
		КонецЕсли;
		
		НоваяСтрокаТаблицыДокументов = ТаблицаИтогоОдиночныеДокументы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицыДокументов, ВыборкаОдиночныеДокументы);
	
	КонецЦикла;
	
	Элементы.ГруппаТаблицаИтогоОдиночныеДокументы.Заголовок = Элементы.ГруппаТаблицаИтогоОдиночныеДокументы.Подсказка
	+ СтрШаблон(" (%1)", ТаблицаИтогоОдиночныеДокументы.Количество());
	
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&УсловиеТаблицы",
		"(РаспознанныйДокумент.Ссылка В (&ОдиночныеБезСканов)
		|			ИЛИ РаспознанныйДокумент.Ссылка В (&ДокументыКомплектовПрикрепитьСкан))
		|	И ВТ_ТаблицаОшибок.КоличествоОшибок = 0");
	ТаблицаИтогоПрикрепитьСканы.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Проверить фактическое наличие скана и убрать лишние
	ИдСтроки = ТаблицаИтогоПрикрепитьСканы.Количество();
	Пока ИдСтроки > 0 Цикл
		ИдСтроки = ИдСтроки - 1;
		СтрокаТаблицы = ТаблицаИтогоПрикрепитьСканы[ИдСтроки];
		
		ДанныеДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтрокаТаблицы.Ссылка, "Статус, ИсходноеИзображение");
		Если ДанныеДокумента.Статус <> Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый Тогда
			// Дополнительная проверка
			Продолжить;
		КонецЕсли;
		
		УбратьИзСписка = Истина;
		СвязанныеДокументы = РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ВсеСвязанныеДокументы(СтрокаТаблицы.Ссылка);
		Для Каждого Документ Из СвязанныеДокументы Цикл
			АдресКартинки = ПоместитьВоВременноеХранилище(ДанныеДокумента.ИсходноеИзображение.Получить());
			Если Документ.ПотенциальныйКандидат Тогда
				Если Не РаспознаваниеДокументовСлужебный.СканУжеЕстьУВладельца(СтрокаТаблицы.Ссылка, Документ.Ссылка, АдресКартинки) Тогда
					СтрокиВКомплектах = ДанныеДокументов.НайтиСтроки(Новый Структура("Ссылка", СтрокаТаблицы.Ссылка));
					Если СтрокиВКомплектах.Количество() <> 0 И СтрокиВКомплектах[0].НомерКомплекта <> 0 Тогда
						НужноПрикрепитьДополнительныйСкан = 
							РаспознаваниеДокументовКомплекты.НужноПрикрепитьДополнительныйСкан(
								СтрокиВКомплектах[0].ТипКомплекта,
								СтрокаТаблицы.ТипДокумента,
								ТипЗнч(Документ.Ссылка));
						Если НужноПрикрепитьДополнительныйСкан Тогда
							
							УбратьИзСписка = Ложь;
							Прервать;
						КонецЕсли;
					Иначе
						УбратьИзСписка = Ложь;
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если УбратьИзСписка Тогда
			// Документ есть и скан прикреплен
			ТаблицаИтогоПрикрепитьСканы.Удалить(ИдСтроки);
			
			РаспознанныйОбъект = СтрокаТаблицы.Ссылка.ПолучитьОбъект();
			РаспознанныйОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
			РаспознанныйОбъект.Записать();
			
			ОбратнаяСвязь = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("Проведен");
			Пакет = Новый Структура("created", ОбратнаяСвязь);
			РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(РаспознанныйОбъект.ИдентификаторРезультата, Пакет);
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ГруппаТаблицаИтогоПрикрепитьСканы.Заголовок = Элементы.ГруппаТаблицаИтогоПрикрепитьСканы.Подсказка
	+ СтрШаблон(" (%1)", ТаблицаИтогоПрикрепитьСканы.Количество());
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументИзСтроки(ДанныеСтроки)
	ПоказатьЗначение(, ДанныеСтроки.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВопросаСозданияДокументов(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ДанныеОбработки = РаспознаваниеДокументовКомплектыКлиентСервер.НовыеДанныеОбработкиКомплектов();
		ДанныеОбработки.Вставить("ДокументовВсего", 0);
		ДанныеОбработки.Вставить("ДокументовНеУдалосьОбработать", 0);
		ДанныеОбработки.Вставить("СозданоДокументов", 0);
		ДанныеОбработки.Вставить("ПрикрепленоСканов", 0);
		
		ДанныеОбработки.ГрупповаяОбработка = Истина;
		ДанныеОбработки.РезультатОбратнойСвязи = РезультатОбратнойСвязи;
		
		СоздатьДокументыИКомплектыНаСервере(ДанныеОбработки);
		
		Для Каждого СтрокаДерева Из ТаблицаИтогоКомплекты.ПолучитьЭлементы() Цикл
			Элементы.ТаблицаИтогоКомплекты.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
		КонецЦикла;
		
		Если ДанныеОбработки.КомплектовНеУдалосьОбработать + ДанныеОбработки.ДокументовНеУдалосьОбработать <> 0 Тогда
			ЗаполнитьТаблицыИтого();
			
			ТекстПредупреждения = СтрШаблон(НСтр("ru = 'Некоторые документы содержат ошибки и не могут быть корректно обработаны.
			|Не удалось обработать: %1
			|
			|Повторите операцию после исправления ошибок:
			| - документы комплекта должны быть заполнены корректно,
			| - документы должны принадлежать одному комплекту.';
			|en = 'Some documents contain errors and cannot be processed correctly.
			|Cannot process: %1
			|
			|Correct the following errors and try again:
			| - The kit documents must be filled correctly.
			| - The documents must belong to the same kit.'"),
			ДанныеОбработки.КомплектовНеУдалосьОбработать + ДанныеОбработки.ДокументовНеУдалосьОбработать);
			
			ПараметрыФормы = Новый Структура("ОшибкиПроведения, ТекстПредупреждения", ДанныеОбработки.ОшибкиПроведения, ТекстПредупреждения);
			ОткрытьФорму("Обработка.РаспознаваниеДокументов.Форма.ОтчетПоОшибкам",
				ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			
			Если ДанныеОбработки.СозданоДокументов <> 0 И ДанныеОбработки.ПрикрепленоСканов <> 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';В базу данных был добавлен %1 документ;;
							  |В базу данных было добавлено %1 документа;
							  |В базу данных было добавлено %1 документов;
							  |В базу данных было добавлено %1 документа';
							  |en = ';%1 document was added to the database;;;;
							  |%1 documents were added to the database'"), ДанныеОбработки.СозданоДокументов)
				+ " " + СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';и %1 новый скан.;;
							  |и %1 новых скана.;
							  |и %1 новых сканов.;
							  |и %1 новых скана.';
							  |en = ';and %1 new scanned copy.;;;;
							  |and %1 new scanned copies.'"), ДанныеОбработки.ПрикрепленоСканов);
			ИначеЕсли ДанныеОбработки.СозданоДокументов <> 0 И ДанныеОбработки.ПрикрепленоСканов = 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';В базу данных был добавлен %1 документ.;;
							  |В базу данных было добавлено %1 документа.;
							  |В базу данных было добавлено %1 документов.;
							  |В базу данных было добавлено %1 документа.';
							  |en = ';%1 document was added to the database.;;;;
							  |%1 documents were added to the database.'"), ДанныеОбработки.СозданоДокументов);
			ИначеЕсли ДанныеОбработки.СозданоДокументов = 0 И ДанныеОбработки.ПрикрепленоСканов <> 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
					НСтр("ru = ';В базу данных был добавлен %1 новый скан.;;
							  |В базу данных было добавлено %1 новых скана.;
							  |В базу данных было добавлено %1 новых сканов.;
							  |В базу данных было добавлено %1 новых скана.';
							  |en = ';%1 new scanned copy is added to the database.;;;;
							  |%1 new scanned copies are added to the database.'"), ДанныеОбработки.ПрикрепленоСканов);
			Иначе
				ТекстСообщения = "";
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ТекстСообщения) Тогда
				ПоказатьПредупреждение(, ТекстСообщения);
			КонецЕсли;
			
			ЗакрытьФормуБезПодтверждения = Истина;
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыИКомплектыНаСервере(ДанныеОбработки)
	
	СоздатьКомплектыНаСервере(ДанныеОбработки);
	СоздатьОдиночныеДокументыНаСервере(ДанныеОбработки);
	ПрикрепитьСканыНаСервере(ДанныеОбработки);
	
КонецПроцедуры

&НаСервере
Процедура СоздатьОдиночныеДокументыНаСервере(ДанныеОбработки)
	
	Комплектные = ДокументыКомплектные.ВыгрузитьЗначения();
	
	СоответствиеТиповДокументов = СоответствиеТиповДокументов(ТаблицаИтогоОдиночныеДокументы.Выгрузить( , "Ссылка"));
	
	Для Каждого СтрокаТаблицы Из ТаблицаИтогоОдиночныеДокументы Цикл
		Если Комплектные.Найти(СтрокаТаблицы.Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РаспознанныйОбъект = СтрокаТаблицы.Ссылка.ПолучитьОбъект();
		Если РаспознанныйОбъект.Статус <> Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеОбработки.ДокументовВсего = ДанныеОбработки.ДокументовВсего + 1;
		ПараметрыОперации = РаспознаваниеДокументовКомплектыКлиентСервер.ТипДокументаИВидОперацииДляДокументаПоУмолчанию(
			СтрокаТаблицы.Направление, СтрокаТаблицы.ТипДокумента);
			
		Если СтрокаТаблицы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
			Или СтрокаТаблицы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг
			Или СтрокаТаблицы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			Или СтрокаТаблицы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			
			Если СтрокаТаблицы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
				ИмяТипа = "ПоступлениеТоваровУслугБРД";
			Иначе
				ИмяТипа = "РеализацияТоваровУслугБРД";
			КонецЕсли;
			
			ТипыСоздаваемого = Метаданные.ОпределяемыеТипы[ИмяТипа].Тип.Типы();
			Для Каждого Тип Из ТипыСоздаваемого Цикл
				Если Тип = СоответствиеТиповДокументов.Получить(СтрокаТаблицы.Ссылка) Тогда
					ТипСоздаваемого = Тип;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли СтрокаТаблицы.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
			Если СтрокаТаблицы.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
				ИмяТипа = "ЗаказПоставщикуБРД";
			Иначе
				ИмяТипа = "ЗаказКлиентаБРД";
			КонецЕсли;
			
			ТипСоздаваемого = Метаданные.ОпределяемыеТипы[ИмяТипа].Тип.Типы()[0];
			
		КонецЕсли;
		
		ПараметрыЗаполнения = РаспознаваниеДокументовКомплектыВызовСервера.ПараметрыЗаполненияДляСозданияДокумента(
			СтрокаТаблицы.Ссылка, СтрокаТаблицы.ТипДокумента, ТипСоздаваемого, ПараметрыОперации);
		
		Если ПараметрыЗаполнения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
	
		СоздаваемыйДокумент = НовыйТиповойДокументНаОснованииРаспознанного(РаспознанныйОбъект, СтрокаТаблицы,
			ПараметрыЗаполнения, ДанныеОбработки, РежимЗаписиДокумента.Проведение);
			
		Если Не ЗначениеЗаполнено(СоздаваемыйДокумент) Тогда
			СоздаваемыйДокумент = НовыйТиповойДокументНаОснованииРаспознанного(РаспознанныйОбъект, СтрокаТаблицы,
				ПараметрыЗаполнения, ДанныеОбработки, Неопределено);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СоздаваемыйДокумент) Тогда
			Продолжить;
		КонецЕсли;

		ДанныеОбработки.СозданоДокументов = ДанныеОбработки.СозданоДокументов + 1;
		ДанныеОбработки.ПрикрепленоСканов = ДанныеОбработки.ПрикрепленоСканов + 1;
		
		ДанныеСозданногоДокумента = ПолучитьОбратнуюСвязьДляСозданногоДокумента(СоздаваемыйДокумент, РаспознанныйОбъект);
		
		Пакет = Новый Структура;
		Пакет.Вставить("created", ДанныеСозданногоДокумента);
		
		РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь(РаспознанныйОбъект.ИдентификаторРезультата, Пакет);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция НовыйТиповойДокументНаОснованииРаспознанного(РаспознанныйОбъект,
														СтрокаТаблицы,
														ПараметрыЗаполнения,
														ДанныеОбработки,
														РежимЗаписи)
	
	СоздаваемыйДокумент = Неопределено;

	НачатьТранзакцию();
	Попытка
		
		// Изменения в документе "Распознанный документ"
		РаспознанныйОбъект.Статус = Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан;
		РаспознанныйОбъект.Записать();
		
		СоздаваемыйДокумент = РаспознаваниеДокументовСлужебный.СоздатьДокументНаОснованииРаспознанного(
			СтрокаТаблицы.Ссылка, ПараметрыЗаполнения.ТипДокументаСтрокой, ПараметрыЗаполнения, РежимЗаписи);
			
		АдресКартинки = ПоместитьВоВременноеХранилище(РаспознанныйОбъект.ИсходноеИзображение.Получить());
		РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(РаспознанныйОбъект, СоздаваемыйДокумент, АдресКартинки);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
			СообщенияОбОшибках = Новый Массив(ПолучитьСообщенияПользователю(Истина));
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			СообщенияОбОшибках.Вставить(0, Сообщение);
			ДанныеОбработки.ОшибкиПроведения.Вставить(РаспознанныйОбъект.Ссылка, СообщенияОбОшибках);
		Иначе
			ДанныеОбработки.ДокументовНеУдалосьОбработать = ДанныеОбработки.ДокументовНеУдалосьОбработать + 1;
		КонецЕсли;
		
	КонецПопытки;
	
	Возврат СоздаваемыйДокумент;

КонецФункции


&НаСервереБезКонтекста
Функция СоответствиеТиповДокументов(ТаблицаДокументов)
	
	СоответствиеТипов = Новый Соответствие;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДокументов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ втДокументы
		|ИЗ
		|	&ТаблицаДокументов КАК ТаблицаДокументов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДокументы.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(РаспознанныйДокументРеквизитыДокумента.Значение, """") КАК ВидДокумента,
		|	РаспознанныйДокументРеквизитыДокумента.Ссылка.Направление КАК Направление
		|ИЗ
		|	втДокументы КАК втДокументы
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК РаспознанныйДокументРеквизитыДокумента
		|		ПО втДокументы.Ссылка = РаспознанныйДокументРеквизитыДокумента.Ссылка
		|			И (РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита = ""ВидДокумента"")";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ВидДокумента = Выборка.ВидДокумента;
		
		Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
			ВидДокумента = ?(Выборка.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий, "ПТИУ", "РТИУ");
		КонецЕсли;
		
		Если ВидДокумента = "ПТИУ" Тогда
			СоответствиеТипов.Вставить(Выборка.Ссылка, Тип("ДокументСсылка.ПриобретениеТоваровУслуг"));
		ИначеЕсли ВидДокумента = "ПУИПА" Тогда
			СоответствиеТипов.Вставить(Выборка.Ссылка, Тип("ДокументСсылка.ПриобретениеУслугПрочихАктивов"));
		ИначеЕсли ВидДокумента = "РТИУ" Тогда
			СоответствиеТипов.Вставить(Выборка.Ссылка, Тип("ДокументСсылка.РеализацияТоваровУслуг"));
		ИначеЕсли ВидДокумента = "РУИПА" Тогда
			СоответствиеТипов.Вставить(Выборка.Ссылка, Тип("ДокументСсылка.РеализацияУслугПрочихАктивов"));
		КонецЕсли;
	КонецЦикла;
	
	Возврат СоответствиеТипов;

КонецФункции // СоответствиеТиповДокументов()


&НаСервере
Процедура ПрикрепитьСканыНаСервере(ДанныеОбработки)
	
	Комплектные = ДокументыКомплектные.ВыгрузитьЗначения();
	
	Для Каждого СтрокаТаблицы Из ТаблицаИтогоПрикрепитьСканы Цикл
		Если Комплектные.Найти(СтрокаТаблицы.Ссылка) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		РаспознанныйОбъект = СтрокаТаблицы.Ссылка.ПолучитьОбъект();
		Если РаспознанныйОбъект.Статус <> Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Новый Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеОбработки.ДокументовВсего = ДанныеОбработки.ДокументовВсего + 1;
		
		СвязанныеДокументы = РегистрыСведений.СвязанныеОбъектыРаспознаниеДокументов.ВсеСвязанныеДокументы(СтрокаТаблицы.Ссылка);
		Для Каждого Документ Из СвязанныеДокументы Цикл
			АдресКартинки = ПоместитьВоВременноеХранилище(РаспознанныйОбъект.ИсходноеИзображение.Получить(), УникальныйИдентификатор);
			Если Документ.ПотенциальныйКандидат Тогда
				РаспознаваниеДокументовСлужебный.ДобавитьПрисоединенныйФайл(РаспознанныйОбъект, Документ.Ссылка, АдресКартинки);
				Если РаспознанныйОбъект.Статус <> Перечисления.СтатусыСозданныхДокументовРаспознаваниеДокументов.Обработан Тогда
					Документы.РаспознанныйДокумент.ОтметитьДокументОбработанным(РаспознанныйОбъект);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОбратнуюСвязьДляСозданногоДокумента(СоздаваемыйДокумент, РаспознанныйОбъект)
	
	Результат = РаспознаваниеДокументов.ОписаниеОбратнойСвязи("Проведен");
	Результат.IdСозданногоДокумента = Строка(СоздаваемыйДокумент.УникальныйИдентификатор());
	Результат.НомерРаспознанногоДокумента = РаспознанныйОбъект.Номер;
	Результат.ЭтоВходящийДокумент = (РаспознанныйОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	Результат.НомерДокумента = РаспознанныйОбъект.РеквизитыДокумента[0].Значение;
	Результат.ДатаДокумента = РаспознанныйОбъект.РеквизитыДокумента[1].Значение;
	Результат.СуммаДокумента = РаспознанныйОбъект.СуммаДокумента;
	Результат.Контрагент = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(РаспознанныйОбъект.Контрагент);
	Результат.Организация = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(РаспознанныйОбъект.Организация);
	Результат.ТипРаспознанногоДокумента = Строка(РаспознанныйОбъект.ТипДокумента);
	Результат.ВариантСоздания = "ГрупповаяОбработка";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ОтправитьОбратнуюСвязьПоГрупповойОбработкеКлиент(Результат, ДополнительныеПараметры) Экспорт
	
	ОтправитьОбратнуюСвязьПоГрупповойОбработке();
	
КонецПроцедуры

&НаСервере
Процедура ОтправитьОбратнуюСвязьПоГрупповойОбработке()
	
	ТаблицыКЗаполнению = ТаблицыКЗаполнению();
	
	Для Каждого ТаблицаКЗаполнению Из ТаблицыКЗаполнению Цикл
		
		Если ТаблицаКЗаполнению.Ключ = "ВыбранныеДокументы" Тогда
			ТекущаяКоллекция = ВыбранныеДокументы();
		Иначе
			ТекущаяКоллекция = ЭтотОбъект[ТаблицаКЗаполнению.Ключ];
		КонецЕсли;
		
		ДокументыТаблицы = Новый Массив;
		
		Если ТипЗнч(ТекущаяКоллекция) = Тип("ДанныеФормыДерево") Тогда
			Для Каждого Строка Из ТекущаяКоллекция.ПолучитьЭлементы() Цикл
				Для Каждого СтрокаДетальныеЗаписи Из Строка.ПолучитьЭлементы() Цикл
					Если ДокументыТаблицы.Найти(СтрокаДетальныеЗаписи.Ссылка) = Неопределено Тогда
						ДокументыТаблицы.Добавить(СтрокаДетальныеЗаписи.Ссылка);
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
		Иначе
			Для Каждого Строка Из ТекущаяКоллекция Цикл
				Если ДокументыТаблицы.Найти(Строка.Ссылка) = Неопределено Тогда
					ДокументыТаблицы.Добавить(Строка.Ссылка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	
		ИдентификаторыРезультата = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ДокументыТаблицы, "ИдентификаторРезультата");
		
		Для Каждого КлючИЗначение Из ИдентификаторыРезультата Цикл
			ТаблицыКЗаполнению[ТаблицаКЗаполнению.Ключ].Добавить(КлючИЗначение.Значение);
		КонецЦикла;
		
	КонецЦикла;
	
	ДокументыИтого = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДокументыИтого, ТаблицыКЗаполнению.ТаблицаИтогоОдиночныеДокументы, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДокументыИтого, ТаблицыКЗаполнению.ТаблицаИтогоКомплекты, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДокументыИтого, ТаблицыКЗаполнению.ТаблицаИтогоПрикрепитьСканы, Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДокументыИтого, ТаблицыКЗаполнению.ТаблицаИтогоБезОбработки, Истина);
	
	ПакетОбратнойСвязи = НовыйПакетОбратнойСвязи();
	ПакетОбратнойСвязи.ОсновныеРеквизиты = ТаблицыКЗаполнению.РеквизитыШапки;
	ПакетОбратнойСвязи.Дубли = ТаблицыКЗаполнению.ВыбранныеДокументы;
	ПакетОбратнойСвязи.Комплекты = ТаблицыКЗаполнению.ДанныеДокументов;
	ПакетОбратнойСвязи.ДопРеквизиты = ТаблицыКЗаполнению.ТаблицаДопРеквизитов;
	ПакетОбратнойСвязи.Номенклатура = ТаблицыКЗаполнению.ТаблицаНоменклатуры;
	ПакетОбратнойСвязи.ДанныеТаблиц = ТаблицыКЗаполнению.ТаблицаСумм;
	ПакетОбратнойСвязи.Готово = ДокументыИтого;
	
	Пакет = Новый Структура;
	Пакет.Вставить("group_creation", ПакетОбратнойСвязи);
	РаспознаваниеДокументовКоннекторСлужебный.ПередатьОбратнуюСвязь("group_" + Новый УникальныйИдентификатор, Пакет);
	
КонецПроцедуры

&НаСервере
Функция ТаблицыКЗаполнению()
	
	Результат = Новый Структура;
	Результат.Вставить("РеквизитыШапки", Новый Массив);
	Результат.Вставить("ВыбранныеДокументы", Новый Массив);
	Результат.Вставить("ДанныеДокументов", Новый Массив);
	Результат.Вставить("ТаблицаДопРеквизитов", Новый Массив);
	Результат.Вставить("ТаблицаНоменклатуры", Новый Массив);
	Результат.Вставить("ТаблицаСумм", Новый Массив);
	Результат.Вставить("ТаблицаИтогоОдиночныеДокументы", Новый Массив);
	Результат.Вставить("ТаблицаИтогоКомплекты", Новый Массив);
	Результат.Вставить("ТаблицаИтогоПрикрепитьСканы", Новый Массив);
	Результат.Вставить("ТаблицаИтогоБезОбработки", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция НовыйПакетОбратнойСвязи()
	
	Результат = Новый Структура;
	Результат.Вставить("ТекущийШаг", ТекущийШаг);
	Результат.Вставить("ОсновныеРеквизиты", Новый Массив);
	Результат.Вставить("Дубли", Новый Массив);
	Результат.Вставить("Комплекты", Новый Массив);
	Результат.Вставить("ДопРеквизиты", Новый Массив);
	Результат.Вставить("Номенклатура", Новый Массив);
	Результат.Вставить("ДанныеТаблиц", Новый Массив);
	Результат.Вставить("Готово", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ТаблицаСуммСтавкаНДСНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТаблицаСуммСтавкаНДСНачалоВыбораНаСервере(ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТаблицаСуммСтавкаНДСНачалоВыбораНаСервере(ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	РаспознаваниеДокументовПереопределяемый.ПриПолученииДанныхВыбораСтавкиНДС(ДанныеВыбора,	СтандартнаяОбработка);	
	
КонецПроцедуры

&НаКлиенте
Функция ПредставлениеДенежнойСуммы(ЗначениеСуммы)

	Возврат Формат(ЗначениеСуммы, "ЧДЦ=2; ЧН=0,00");

КонецФункции

&НаКлиенте
Процедура ТаблицаСуммСтатьиДоходовРасходовНачалоВыбора(Статья, Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Статья", Статья);
	ПараметрыФормы.Вставить("ПараметрыВыбора", Элемент.ПараметрыВыбора);
	ПараметрыФормы.Вставить("ОграничениеТипа", Элемент.ОграничениеТипа);
	
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораСтатьи", ПараметрыФормы, Элемент);
	
КонецПроцедуры


#КонецОбласти

