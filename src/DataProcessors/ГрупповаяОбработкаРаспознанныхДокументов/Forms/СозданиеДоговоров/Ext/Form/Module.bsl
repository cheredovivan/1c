
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьПартнеровИКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	
	Для Каждого ДанныеСтроки Из Параметры.ДанныеСоздания Цикл
		
		СтрокаТаблицы = ТаблицаДоговоров.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеСтроки, , "ИдСтрокДокументов, РаспознанныеДокументы");
		СтрокаТаблицы.ТипДоговора = ДанныеСтроки.ВидДоговора;
		Если ПустаяСтрока(СтрокаТаблицы.Наименование) Тогда
			СтрокаТаблицы.Наименование = СтрокаТаблицы.ТипДоговора;
		КонецЕсли;
		
		Если СтрокаТаблицы.ТипДоговора = Перечисления.ТипыДоговоров.СПокупателем Тогда
			ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту;
		ИначеЕсли СтрокаТаблицы.ТипДоговора = Перечисления.ТипыДоговоров.СПоставщиком Тогда
			ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика;
		Иначе
			ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПустаяСсылка();
		КонецЕсли;
		
		СтрокаТаблицы.ХозяйственнаяОперация = ХозяйственнаяОперация;
		
		СтрокаТаблицы.ИдСтрокДокументов.ЗагрузитьЗначения(ДанныеСтроки.ИдСтрокДокументов);
		СтрокаТаблицы.РаспознанныеДокументы.ЗагрузитьЗначения(ДанныеСтроки.РаспознанныеДокументы);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКонтрагентов

&НаКлиенте
Процедура ТаблицаКонтрагентовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДоговоровВидДоговораПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТаблицаКонтрагентов.ТекущиеДанные;
	
	Если ТекущиеДанные.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПокупателем") Тогда
		ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.РеализацияКлиенту");
	ИначеЕсли ТекущиеДанные.ТипДоговора = ПредопределенноеЗначение("Перечисление.ТипыДоговоров.СПоставщиком") Тогда
		ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщика");
	Иначе
		ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПустаяСсылка");
	КонецЕсли;
	
	ТекущиеДанные.ХозяйственнаяОперация = ХозяйственнаяОперация;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьВсе(Команда)
	
	Договоры = ВсеДоговоры();
	
	Если ЗначениеЗаполнено(Договоры) Тогда
		Результат = Новый Структура;
		Результат.Вставить("ДанныеСоздания", Договоры);
		ОповеститьОВыборе(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВсеДоговоры()
	
	Результат = Неопределено;
	
	Если ПроверитьФлагиПартнераКлиентПоставщик() Тогда
		
		Таблица = ТаблицаДоговоров.Выгрузить();
		
		РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(Таблица.Колонки.Номер.Имя, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
		РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(Таблица.Колонки.Дата.Имя, Тип("СправочникСсылка.ДоговорыКонтрагентов"));
		
		Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(Таблица);
		Для Каждого СтрокаРезультата Из Результат Цикл
			СтрокаРезультата.ИдСтрокДокументов = СтрокаРезультата.ИдСтрокДокументов.ВыгрузитьЗначения();
			СтрокаРезультата.РаспознанныеДокументы = СтрокаРезультата.РаспознанныеДокументы.ВыгрузитьЗначения();
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьФлагиПартнераКлиентПоставщик()
	
	РезультатПроверки = Истина;
	
	Контрагенты = ТаблицаДоговоров.Выгрузить( , "Контрагент").ВыгрузитьКолонку("Контрагент");
		
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Контрагенты.Ссылка КАК Контрагент,
	|	Контрагенты.Партнер КАК Партнер,
	|	Контрагенты.Партнер.Клиент КАК Клиент,
	|	Контрагенты.Партнер.Поставщик КАК Поставщик
	|ИЗ
	|	Справочник.Контрагенты КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Ссылка В(&Контрагенты)";
	
	Запрос.УстановитьПараметр("Контрагенты", Контрагенты);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Отбор = Новый Структура("Контрагент", Выборка.Контрагент);
		НайденныеСтроки = ТаблицаДоговоров.НайтиСтроки(Отбор);
		Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
			
			ИндексСтроки = ТаблицаДоговоров.Индекс(НайденнаяСтрока);
			ПолеКонтрагент = "ТаблицаДоговоров[" + ИндексСтроки + "].Контрагент";
			
			Если ЗначениеЗаполнено(Выборка.Партнер) Тогда
				ТекстСообщения = "";
				ФлагиПартнера = Новый Структура("Клиент, Поставщик", Выборка.Клиент, Выборка.Поставщик);
				Если Не Справочники.ДоговорыКонтрагентов.ПроверитьФлагиПартнераКлиентПоставщик(Выборка.Партнер,
					НайденнаяСтрока.ХозяйственнаяОперация, Истина, Ложь, ФлагиПартнера, ТекстСообщения) Тогда
					РезультатПроверки = Ложь;
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПолеКонтрагент);
				КонецЕсли;
			Иначе
				РезультатПроверки = Ложь;
				ТекстСообщения = НСтр("ru = 'Для контрагента не указан партнер.';
										|en = 'No partner is specified for the counterparty.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , ПолеКонтрагент);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти


