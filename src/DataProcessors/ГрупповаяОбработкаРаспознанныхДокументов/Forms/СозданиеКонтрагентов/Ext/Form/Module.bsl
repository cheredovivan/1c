
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для Каждого ДанныеСтроки Из Параметры.ДанныеСоздания Цикл
		СтрокаТаблицы = ТаблицаКонтрагентов.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеСтроки, , "ИдСтрокДокументов, РаспознанныеДокументы");
		СтрокаТаблицы.ИдСтрокДокументов.ЗагрузитьЗначения(ДанныеСтроки.ИдСтрокДокументов);
		СтрокаТаблицы.РаспознанныеДокументы.ЗагрузитьЗначения(ДанныеСтроки.РаспознанныеДокументы);
	КонецЦикла;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИмяПараметра = "РаспознаваниеДокументов.ИспользоватьСервис1СКонтрагент";
	Если ПараметрыПриложения[ИмяПараметра] <> Ложь Тогда
		// Неопределено или Истина
		Использовать1СКонтрагент = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКонтрагентов

&НаКлиенте
Процедура ТаблицаКонтрагентовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьВсе(Команда)
	
	Отказ = Ложь;
	Для Каждого СтрокаТаблицы Из ТаблицаКонтрагентов Цикл
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Наименование) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru = 'Поле ""Наименование"" должно быть заполнено во всех строках';
									|en = 'The ""Description"" field is required in all lines'");
			Сообщение.Сообщить();
			
			Отказ = Истина;
			Прервать;
		КонецЕсли;
		
		Если Не СтрокаТаблицы.ЮридическоеФизическоеЛицо
			= ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
			СтрокаТаблицы.КПП = "";
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПараметра = "РаспознаваниеДокументов.ИспользоватьСервис1СКонтрагент";
	ПараметрыПриложения.Вставить(ИмяПараметра, Использовать1СКонтрагент);
	
	Результат = Новый Структура;
	Результат.Вставить("Использовать1СКонтрагент", Использовать1СКонтрагент);
	Результат.Вставить("ДанныеСоздания", ВсеКонтрагенты());
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ВсеКонтрагенты()
	
	Таблица = ТаблицаКонтрагентов.Выгрузить();
	
	РаспознаваниеДокументовПереопределяемый.ИмяРеквизитаОбъекта(Таблица.Колонки.ЮридическоеФизическоеЛицо.Имя, Тип("СправочникСсылка.Контрагенты"));
	
	Результат = ОбщегоНазначения.ТаблицаЗначенийВМассив(Таблица);
	
	Для Каждого СтрокаРезультата Из Результат Цикл
		СтрокаРезультата.ИдСтрокДокументов = СтрокаРезультата.ИдСтрокДокументов.ВыгрузитьЗначения();
		СтрокаРезультата.РаспознанныеДокументы = СтрокаРезультата.РаспознанныеДокументы.ВыгрузитьЗначения();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"ТаблицаКонтрагентов.ЮридическоеФизическоеЛицо", ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаКонтрагентовКПП");

КонецПроцедуры // УстановитьУсловноеОформление()

#КонецОбласти


