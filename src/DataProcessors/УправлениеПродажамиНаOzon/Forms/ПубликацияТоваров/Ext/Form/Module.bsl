
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УчетнаяЗаписьМаркетплейса = Параметры.УчетнаяЗапись;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьМаркетплейса) Тогда
		УчетнаяЗаписьМаркетплейса = Справочники.УчетныеЗаписиМаркетплейсов.ОсновнаяУчетнаяЗапись(ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon"));
	КонецЕсли;

	ОбновлениеДанныхРазрешено = ИнтеграцияСМаркетплейсомOzonВызовСервера.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗаписьМаркетплейса);

	ИнициализироватьПараметрыОтображенияОстатковЦен();
	ОбновитьКнопкуОтбораПоСтатусу(Команды.ПоказатьВсе.Имя);

	РезультатПроверкиИмпорта = Неопределено;
	
	Если Параметры.ВРежимеИмпортаДанных Тогда
		Заголовок = НСтр("ru = 'Импорт и сопоставление товаров';
						|en = 'Item import and mapping'");
		
		Элементы.УчетнаяЗаписьМаркетплейса.Доступность = Ложь;
		
		Элементы.ПодсказкаИмпортТоваров.Видимость = Истина;
		Элементы.ПодсказкаПросмотрИРедактированиеТоваров.Видимость = Ложь;
		Элементы.ПодсказкаИсправлениеОшибок.Видимость = Ложь;
		
		Элементы.КнопкаИмпортТоваров.Видимость = Ложь;
		Элементы.КнопкаПросмотрИРедактированиеТоваров.Видимость = Ложь;
		Элементы.КнопкаИсправлениеОшибок.Видимость = Ложь;
		
		ПриАктивацииГиперСсылкиНаПанели(
			"СтраницаПросмотрИРедактированиеТоваров",
			"КнопкаИмпортТоваров");
	Иначе
		ПриАктивацииГиперСсылкиНаПанели(
			"СтраницаПросмотрИРедактированиеТоваров",
			"КнопкаПросмотрИРедактированиеТоваров",
			Истина); // Возможно изменение команды на "КнопкаИмпортТоваров".
	КонецЕсли;
	
	УстановитьОтборПоВходящимПараметрам();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	УстановитьДоступностьВидимостьЭлементов();
	ОбновитьСтатистикуТоваров();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаСерверногоУведомления", ЭтотОбъект);
	УведомленияКлиента.ПодключитьОбработчик(
		ИнтеграцияСМаркетплейсамиКлиентСервер.КлючСерверныхУведомлений(), ОписаниеОповещения);
	
	Если Параметры.ВРежимеИмпортаДанных Тогда
		ВыполнитьПолучениеТоваровИзСервиса();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборТоваровВДокументПродажи.Форма.Форма" Тогда	// Подобрать
		ОбработкаВыбораПодборДлительнаяОперация(ВыбранноеЗначение);

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.УправлениеПродажамиНаOzon.Форма.НастройкаОтображенияОстатков" Тогда
		Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") Тогда
			ПараметрыОтображенияОстатковЦен = Новый Структура;
			ПараметрыОтображенияОстатковЦенПеременная = ВыбранноеЗначение.ПараметрыОтображенияОстатковЦен;
			Для Каждого КлючЗначение Из ПараметрыОтображенияОстатковЦенПеременная Цикл
				Если ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
					ПараметрыОтображенияОстатковЦен.Вставить(КлючЗначение.Ключ, ОбщегоНазначенияКлиент.СкопироватьРекурсивно(КлючЗначение.Значение));
				ИначеЕсли ПараметрыОтображенияОстатковЦен.Свойство(КлючЗначение.Ключ) Тогда
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтображенияОстатковЦен[КлючЗначение.Ключ], КлючЗначение.Значение, Истина);
				Иначе
					ПараметрыОтображенияОстатковЦен.Вставить(КлючЗначение.Ключ, ОбщегоНазначенияКлиент.СкопироватьРекурсивно(КлючЗначение.Значение));
				КонецЕсли;
			КонецЦикла;

			УстановитьПараметрыСпискаТоваров();
			УстановитьВидимостьКолонокЦеныОстатки();
		КонецЕсли;

	ИначеЕсли ИсточникВыбора.ИмяФормы = "Обработка.ПодборТоваровПоОтбору.Форма.Форма" Тогда	// Добавить по отбору
		ОбработкаВыбораПодборДлительнаяОперация(ВыбранноеЗначение);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ОбновитьСтраницуИмпорта = Ложь;
	
	Если ИмяСобытия = "УчетнаяЗаписьДеактивирована" Тогда
		Если Параметр = УчетнаяЗаписьМаркетплейса Тогда
			УчетнаяЗаписьМаркетплейса = ПредопределенноеЗначение("Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка");
			УчетнаяЗаписьПриИзменении(Неопределено);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_УчетныеЗаписиМаркетплейсов" Тогда
		Если Параметр = УчетнаяЗаписьМаркетплейса Тогда
			ОбновлениеДанныхРазрешено = ИнтеграцияСМаркетплейсомOzonВызовСервера.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗаписьМаркетплейса);
			УстановитьДоступностьВидимостьЭлементов();
			
			Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
					И Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
				ОбновитьСтраницуИмпорта = Истина;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзмененаВалютаУчетнойЗаписиМаркетплейса" И Параметр.УчетнаяЗаписьМаркетплейса = УчетнаяЗаписьМаркетплейса Тогда
		ВалютаУчетнойЗаписи = Строка(Параметр.ВалютаУчета);
		ОбновитьИнформационноеПолеОстаткиЦены();
		Товары.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.НастройкиУчетнойЗаписи.Вставить("ВалютаУчета", Параметр.ВалютаУчета);
		ОбновитьДанныеСпискаТоваров();
		
	ИначеЕсли ИмяСобытия = "Ozon_ЗавершеноСопоставлениеНоменклатуры"
				И Источник = УчетнаяЗаписьМаркетплейса
				И Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров Тогда
		ОбновитьДанныеСпискаТоваров();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант"
				И Источник = "ИспользоватьНоменклатуруПартнеров"
				И Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
				И Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		ОбновитьСтраницуИмпорта = Истина;
		
	КонецЕсли;
	
	Если ОбновитьСтраницуИмпорта Тогда
		ОбновитьСтраницуИмпортаИОчиститьРезультатПроверки();
	КонецЕсли;
	
	ОбработатьОповещенияИмпортаТоварногоКаталога(ИмяСобытия, Параметр);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.ВРежимеИмпортаДанных Тогда
		ЗакрытьФорму = Истина;
	КонецЕсли;
	
	Если Не ЗакрытьФорму Тогда
		ЗакрытьФорму = Истина;
		
		ПараметрЗакрытия = ИнтеграцияСМаркетплейсамиВызовСервера.ДанныеИдентификаторовТоваров(
			УчетнаяЗаписьМаркетплейса,
			Параметры.ОтборПоИдентификаторуПубликации.ВыгрузитьЗначения(),
			Параметры.ОтборПоИдентификаторуМаркетплейса.ВыгрузитьЗначения(),
			Параметры.ОтборПоSKU.ВыгрузитьЗначения());
		
		Закрыть(ПараметрЗакрытия);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	УведомленияКлиента.ОтключитьОбработчик(ИнтеграцияСМаркетплейсамиКлиентСервер.КлючСерверныхУведомлений());
	
	Если Параметры.ВРежимеИмпортаДанных Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗавершениеРаботы Тогда
		СохранитьДанныеФормы();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)

	УчетнаяЗаписьПриИзмененииНаСервере();
	УстановитьДоступностьВидимостьЭлементов();
	ОбновитьСтатистикуТоваров();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриАктивизацииСтроки(Элемент)

	ОбновитьИнформационноеПолеОстаткиЦены();

	УстановитьДоступностьЭлементовДляСтатуса();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ТоварыПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Если Не Настройки.ДополнительныеСвойства.ОбновлятьЦеныИОстатки Тогда
		Возврат;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Обработка.УправлениеПродажамиНаOzon.Форма.ПубликацияТоваров.ДополнениеДанныхДинСпискаПоТоварам");

	ТекущаяДата = ТекущаяДатаСеанса();
	ТаблицаТоваров = ИнтеграцияСМаркетплейсомOzonСервер.СведенияОВыгруженныхДанных();

	Для Каждого КлючЗаписи Из Строки.ПолучитьКлючи() Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КлючЗаписи);
	КонецЦикла;

	ПараметрыОтображенияОстатковЦен = Настройки.ДополнительныеСвойства.ПараметрыОтображенияОстатковЦен;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = "";

	Если ПараметрыОтображенияОстатковЦен.ОстатокПоУчету.ОтображатьВТаблице
			Или ПараметрыОтображенияОстатковЦен.ОстатокПоУчету.ОтображатьВИнформационномПоле Тогда
		СопоставленныеСклады = ИнтеграцияСМаркетплейсамиСервер.ПолучитьСопоставленныеСклады(Настройки.ДополнительныеСвойства.УчетнаяЗаписьМаркетплейса, Ложь, Истина);
		Запрос.УстановитьПараметр("ТаблицаСкладов", СопоставленныеСклады);

		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ТаблицаСкладов.Склад КАК Справочник.Склады) КАК Склад,
			|	ВЫРАЗИТЬ(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыFBO КАК БУЛЕВО) КАК ИспользуетсяДляСхемыРаботыFBO,
			|	ВЫРАЗИТЬ(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыFBS КАК БУЛЕВО) КАК ИспользуетсяДляСхемыРаботыFBS,
			|	ВЫРАЗИТЬ(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыDBS КАК БУЛЕВО) КАК ИспользуетсяДляСхемыРаботыDBS
			|ПОМЕСТИТЬ СопоставленныеСклады
			|ИЗ
			|	&ТаблицаСкладов КАК ТаблицаСкладов
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Склад
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	СопоставленныеСклады.Склад КАК Склад,
			|	МАКСИМУМ(СопоставленныеСклады.ИспользуетсяДляСхемыРаботыFBO) КАК ИспользуетсяДляСхемыРаботыFBO,
			|	МАКСИМУМ(СопоставленныеСклады.ИспользуетсяДляСхемыРаботыFBS) КАК ИспользуетсяДляСхемыРаботыFBS,
			|	МАКСИМУМ(СопоставленныеСклады.ИспользуетсяДляСхемыРаботыDBS) КАК ИспользуетсяДляСхемыРаботыDBS
			|ПОМЕСТИТЬ ТаблицаСкладов
			|ИЗ
			|	СопоставленныеСклады КАК СопоставленныеСклады
			|СГРУППИРОВАТЬ ПО
			|	СопоставленныеСклады.Склад
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Склад
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ СопоставленныеСклады" 
			
			+ ОбщегоНазначения.РазделительПакетаЗапросов();
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ТаблицаТоваров.УчетнаяЗаписьМаркетплейса КАК Справочник.УчетныеЗаписиМаркетплейсов) КАК УчетнаяЗаписьМаркетплейса,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.ВидОбъектаМаркетплейса КАК Перечисление.ВидыОбъектовМаркетплейсов) КАК ВидОбъектаМаркетплейса,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.Упаковка КАК Справочник.УпаковкиЕдиницыИзмерения) КАК Упаковка,
		|	ВЫРАЗИТЬ(ТаблицаТоваров.НоменклатураПартнера КАК Справочник.НоменклатураКонтрагентов) КАК НоменклатураПартнера
		|ПОМЕСТИТЬ ТоварыБезЦен
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ВидыЦен.Ссылка КАК ВидЦены,
		|	&РеквизитОстатка КАК ОстатокПоУчету,
		|	&ПоляДляЦенообразованияВыборка
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	ТоварыБезЦен КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО ВидыЦен.Ссылка В (&ВидыЦен)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
		|		ПО (ВидыНоменклатуры.Ссылка = ТаблицаТоваров.Номенклатура.ВидНоменклатуры)
		|		&ТаблицаОстатка
		|	ИНДЕКСИРОВАТЬ ПО
		|		&ПоляДляИндексирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЦеныНоменклатуры.*
		|ПОМЕСТИТЬ ЦеныТоваров
		|	ИЗ
		|		&ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|	ИНДЕКСИРОВАТЬ ПО
		|		&ПоляДляИндексирования
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ТаблицаТоваров.ОстатокПоУчету КАК ОстатокПоУчету,
		|	&РеквизитыЗапроса
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныТоваров КАК ТоварыСЦенами
		|		ПО &УсловиеСоединенияЦеныНоменклатуры
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаТоваров.Номенклатура,
		|	ТаблицаТоваров.Характеристика,
		|	ТаблицаТоваров.Упаковка,
		|	ТаблицаТоваров.НоменклатураПартнера,
		|	ТаблицаТоваров.ОстатокПоУчету";

	НастройкаЦенообразования = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
	НастройкаЦенообразования.ИсточникТоваров = "ТаблицаТоваров";
	НастройкаЦенообразования.ПриемникТоваров = "";
	НастройкаЦенообразования.ПолеСерия = "";

	ИспользуетсяЦенообразование25 = Настройки.ДополнительныеСвойства.ИспользуетсяЦенообразование25;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ПоляДляИндексирования",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыИндексирование(ИспользуетсяЦенообразование25));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ЦеныНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры("ТаблицаТоваров",
			"&ДатаЦен",
			Новый Структура("ВТаблице", "ВидЦены"),
			ИспользуетсяЦенообразование25));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&УсловиеСоединенияЦеныНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
		"ТаблицаТоваров",
		"ТоварыСЦенами",
		"ТаблицаТоваров.ВидЦены",
		ИспользуетсяЦенообразование25));

	ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования,,
			ИспользуетсяЦенообразование25);
	Если ЗначениеЗаполнено(ТекстЗамены) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", ТекстЗамены);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", "ИСТИНА");
	КонецЕсли;

	РеквизитыЗапроса = Новый Массив;
	НастройкиУчетнойЗаписи = Настройки.ДополнительныеСвойства.НастройкиУчетнойЗаписи;
	ВидыЦен = НастройкиУчетнойЗаписи.ВидыЦен;

	СтруктураПараметров = Новый Структура();
	СтруктураПараметров.Вставить("Запрос", Запрос);
	СтруктураПараметров.Вставить("РеквизитыЗапроса", РеквизитыЗапроса);
	СтруктураПараметров.Вставить("ПараметрыОтображенияОстатковЦен", ПараметрыОтображенияОстатковЦен);
	СтруктураПараметров.Вставить("НастройкиУчетнойЗаписи", НастройкиУчетнойЗаписи);

	МассивИменПолейЦен = Новый Массив;

	// Выгружаемые виды цен
	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныСУчетомСкидок);
	СтруктураПараметров.Вставить("ИмяРеквизита", "ЦенаСоСкидкой");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныДоСкидок);
	СтруктураПараметров.Вставить("ИмяРеквизита", "ЦенаДоСкидки");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныМинимальныхЦен);
	СтруктураПараметров.Вставить("ИмяРеквизита", "МинимальнаяЦена");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	// Загружаемые виды цен
	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныСАкциямиПродавца);
	СтруктураПараметров.Вставить("ИмяРеквизита", "ЦенаСУчетомАкцийПродавца");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныСоВсемиАкциями);
	СтруктураПараметров.Вставить("ИмяРеквизита", "ЦенаСУчетомВсехАкций");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	СтруктураПараметров.Вставить("ВидЦен", ВидыЦен.ВидЦеныПоставщика);
	СтруктураПараметров.Вставить("ИмяРеквизита", "ЦенаПоставщика");
	ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров);
	МассивИменПолейЦен.Добавить(СтруктураПараметров.ИмяРеквизита);

	// Остаток
	Если ПараметрыОтображенияОстатковЦен.ОстатокПоУчету.ОтображатьВТаблице
			Или ПараметрыОтображенияОстатковЦен.ОстатокПоУчету.ОтображатьВИнформационномПоле Тогда
		ТекстРеквизитаОстатка = 
			"СУММА(ВЫБОР
			|			КОГДА СтатусыПубликации.ПродаетсяПоСхемеРаботыFBO И ЕСТЬNULL(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыFBO, ЛОЖЬ)
			|					ИЛИ СтатусыПубликации.ПродаетсяПоСхемеРаботыFBS И ЕСТЬNULL(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыFBS, ЛОЖЬ)
			|					ИЛИ СтатусыПубликации.ПродаетсяПоСхемеРаботыDBS И ЕСТЬNULL(ТаблицаСкладов.ИспользуетсяДляСхемыРаботыDBS, ЛОЖЬ)
			|				ТОГДА ЕСТЬNULL(ИсточникОстатков.ВНаличииОстаток - ИсточникОстатков.РезервироватьНаСкладеОстаток - ИсточникОстатков.РезервироватьПоМереПоступленияОстаток, 0) 
			|						/ ВЫБОР
			|							КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1) = 0
			|								ТОГДА 1
			|							ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковки, 1)
			|						КОНЕЦ
			|			ИНАЧЕ 0
			|		КОНЕЦ)";
		ТекстРеквизитаОстатка = СтрЗаменить(ТекстРеквизитаОстатка,
			"&ТекстЗапросаКоэффициентУпаковки",
			Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
				"(ВЫБОР
				|	КОГДА ТаблицаТоваров.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
				|		ТОГДА ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения
				|	ИНАЧЕ ТаблицаТоваров.Упаковка
				|КОНЕЦ)",
				"ТаблицаТоваров.Номенклатура"));
			
		ТекстТаблицыОстатка = "
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыИПотребности.Остатки(,
			|		(Номенклатура, Характеристика, Склад, Назначение) В(
			|			ВЫБРАТЬ РАЗЛИЧНЫЕ
			|				ТаблицаТоваров.Номенклатура КАК Номенклатура,
			|				ТаблицаТоваров.Характеристика КАК Характеристика,
			|				ТаблицаСкладов.Склад КАК Склад,
			|				ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
			|			ИЗ
			|				ТоварыБезЦен КАК ТаблицаТоваров
			|					ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладов КАК ТаблицаСкладов
			|					ПО (ИСТИНА))) КАК ИсточникОстатков
			|		ПО ИсточникОстатков.Номенклатура = ТаблицаТоваров.Номенклатура
			|			И ИсточникОстатков.Характеристика = ТаблицаТоваров.Характеристика
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
			|		ПО ТаблицаТоваров.УчетнаяЗаписьМаркетплейса = СтатусыПубликации.УчетнаяЗаписьМаркетплейса
			|			И ТаблицаТоваров.ВидОбъектаМаркетплейса = СтатусыПубликации.ВидОбъектаМаркетплейса
			|			И ТаблицаТоваров.Номенклатура = СтатусыПубликации.Номенклатура
			|			И ТаблицаТоваров.Характеристика = СтатусыПубликации.Характеристика
			|			И ТаблицаТоваров.Упаковка = СтатусыПубликации.Упаковка
			|			И ТаблицаТоваров.НоменклатураПартнера = СтатусыПубликации.НоменклатураПартнера
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСкладов КАК ТаблицаСкладов
			|		ПО ТаблицаСкладов.Склад = ИсточникОстатков.Склад
			|
			|СГРУППИРОВАТЬ ПО
			|	ТаблицаТоваров.Номенклатура,
			|	ТаблицаТоваров.Характеристика,
			|	ТаблицаТоваров.Упаковка,
			|	ТаблицаТоваров.НоменклатураПартнера,
			|	&ГруппировкиДляЦенообразованияВыборка";

		ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования, Истина,
			ИспользуетсяЦенообразование25);
		Если ЗначениеЗаполнено(ТекстЗамены) Тогда
			ТекстЗамены = ТекстЗамены + ", ВидыЦен.Ссылка";
		Иначе
			ТекстЗамены = "ВидыЦен.Ссылка";
		КонецЕсли;
		
		ТекстТаблицыОстатка = СтрЗаменить(ТекстТаблицыОстатка, "&ГруппировкиДляЦенообразованияВыборка", ТекстЗамены);
	Иначе
		ТекстРеквизитаОстатка = "0";
		ТекстТаблицыОстатка = "";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&РеквизитОстатка", ТекстРеквизитаОстатка);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаОстатка", ТекстТаблицыОстатка);

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&РеквизитыЗапроса",
		СтрСоединить(РеквизитыЗапроса, "," + Символы.ПС));

	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковкиЦены",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТоварыСЦенами.Упаковка",
			"ТоварыСЦенами.Номенклатура"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковкиТовара",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));

	БазоваяВалюта = ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию();

	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаЦен", ТекущаяДата);
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	Запрос.УстановитьПараметр("БазоваяВалюта", БазоваяВалюта);
	Запрос.УстановитьПараметр("ВидыЦен", ИнтеграцияСМаркетплейсомOzonСервер.ИспользуемыеВидыЦен(Настройки.ДополнительныеСвойства.УчетнаяЗаписьМаркетплейса));
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);

	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;

	СтруктураОтбора = Новый Структура();
	СтруктураОтбора.Вставить("УчетнаяЗаписьМаркетплейса", Настройки.ДополнительныеСвойства.УчетнаяЗаписьМаркетплейса);
	СтруктураОтбора.Вставить("ВидОбъектаМаркетплейса", ПредопределенноеЗначение("Перечисление.ВидыОбъектовМаркетплейсов.Товар"));
	СтруктураОтбора.Вставить("Номенклатура");
	СтруктураОтбора.Вставить("Характеристика");
	СтруктураОтбора.Вставить("Упаковка");
	СтруктураОтбора.Вставить("ОбъектПубликации", Неопределено);
	СтруктураОтбора.Вставить("НоменклатураПартнера");

	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		СтруктураОтбора.Номенклатура = Выборка.Номенклатура;
		СтруктураОтбора.Характеристика = Выборка.Характеристика;
		СтруктураОтбора.Упаковка = Выборка.Упаковка;
		СтруктураОтбора.НоменклатураПартнера = Выборка.НоменклатураПартнера;
		КлючЗаписи = РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.СоздатьКлючЗаписи(СтруктураОтбора);
		ДанныеСтрокиСписка = Строки[КлючЗаписи].Данные;

		Для Каждого ИмяПоляЦены Из МассивИменПолейЦен Цикл
			ДанныеСтрокиСписка[ИмяПоляЦены] = РаботаСКурсамиВалютУТ.ПересчитатьВВалюту(Выборка[ИмяПоляЦены], БазоваяВалюта,
					Выборка[ИмяПоляЦены + "Валюта"], НастройкиУчетнойЗаписи.ВалютаУчета, ТекущаяДата);
		КонецЦикла;

		ДанныеСтрокиСписка.ОстатокПоУчету = Выборка.ОстатокПоУчету;
	КонецЦикла;

	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ТаблицаТоваров.Количество());

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)

	Отказ = Истина;

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);

	ОткрытьФорму("РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.Форма.ФормаЗаписи", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)

	Отказ = Истина;
	
	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Товары.ВыделенныеСтроки.Количество() = 1 Тогда
		ТекстВопроса = НСтр("ru = 'Удалить выбранный товар?';
							|en = 'Remove the selected item?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Удалить выбранные товары?';
							|en = 'Remove the selected items?'");
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("УдалитьТоварЗавершение", ЭтотОбъект,);
	ПоказатьВопрос(ОповещениеОЗавершении, 
		ТекстВопроса, 
		РежимДиалогаВопрос.ДаНет, 
		, 
		КодВозвратаДиалога.Нет, 
		НСтр("ru = 'Публикация товаров';
			|en = 'Goods publication'"), 
		КодВозвратаДиалога.Нет);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыСОшибками

&НаКлиенте
Процедура ТоварыСОшибкамиПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элементы.ТоварыСОшибками.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УчетнаяЗаписьВыбрана = Не УчетнаяЗаписьМаркетплейса.Пустая();
	
	Элементы.ТоварыСОшибкамиИзменитьТовар.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ТоварыСОшибкамиУдалитьТовар.Доступность = УчетнаяЗаписьВыбрана И ТекущиеДанные.ДоступноУдаление;
	Элементы.ТоварыСОшибкамиКонтекстноеМенюУдалить.Доступность = УчетнаяЗаписьВыбрана И ТекущиеДанные.ДоступноУдаление;
	Элементы.ТоварыСОшибкамиОтправитьНаМодерацию.Доступность = УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСОшибкамиПередУдалением(Элемент, Отказ)

	Отказ = Истина;
	
	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Товары.ВыделенныеСтроки.Количество() = 1 Тогда
		ТекстВопроса = НСтр("ru = 'Удалить выбранный товар?';
							|en = 'Remove the selected item?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Удалить выбранные товары?';
							|en = 'Remove the selected items?'");
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("УдалитьТоварЗавершение", ЭтотОбъект,);
	ПоказатьВопрос(ОповещениеОЗавершении, 
		ТекстВопроса, 
		РежимДиалогаВопрос.ДаНет, 
		, 
		КодВозвратаДиалога.Нет, 
		НСтр("ru = 'Публикация товаров';
			|en = 'Goods publication'"), 
		КодВозвратаДиалога.Нет);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьТовар(Команда)

	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.Форма.ФормаЗаписи.ОткрытиеФормы");

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);

	ОткрытьФорму("РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.Форма.ФормаЗаписи", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКСсылкеНаТовар(Команда)

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ПустаяСтрока(ТекущиеДанные.SKU) Тогда
		Возврат;
	КонецЕсли;

	НавигационнаяСсылка = ИнтеграцияСМаркетплейсомOzonКлиентСервер.НавигационнаяСсылкаНаТовар(ТекущиеДанные.SKU);
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылка);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаМодерацию(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ОтправитьНаМодерациюВФоне(Элементы.Товары.ВыделенныеСтроки);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаМодерациюТоварыСОшибками(Команда)
	
	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	ОтправитьНаМодерациюВФоне(Элементы.ТоварыСОшибками.ВыделенныеСтроки);

КонецПроцедуры

&НаКлиенте
Процедура Подобрать(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	ПараметрыУказанияСерий = НоменклатураКлиентСервер.ПараметрыУказанияСерий();
	ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры = Ложь;
	ПараметрыУказанияСерий.УчитыватьСебестоимостьПоСериям = Ложь;

	НастройкиУчетнойЗаписи = ИнтеграцияСМаркетплейсомOzonВызовСервера.НастройкиУчетнойЗаписи(УчетнаяЗаписьМаркетплейса);
	ВалютаРУ = ЗначениеНастроекКлиентСерверПовтИсп.ВалютаРегламентированногоУчетаОрганизации(НастройкиУчетнойЗаписи.Организация);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",                         НастройкиУчетнойЗаписи.Организация);
	ПараметрыФормы.Вставить("ЦенаВключаетНДС",                     Истина);
	ПараметрыФормы.Вставить("ВозвращатьМногооборотнуюТару",        Ложь);
	ПараметрыФормы.Вставить("ОтборПоТипуНоменклатуры",             Новый ФиксированныйМассив(ИнтеграцияСМаркетплейсомOzonКлиентСервер.ОтборПоНоменклатуре()));
	ПараметрыФормы.Вставить("СкрыватьРучныеСкидки",                Истина);
	ПараметрыФормы.Вставить("СкрыватьКнопкуЗапрашиватьКоличество", Истина);
	ПараметрыФормы.Вставить("ОстаткиПоВсемСкладам",                Истина);
	ПараметрыФормы.Вставить("ИспользоватьДатыОтгрузки",            Ложь);
	ПараметрыФормы.Вставить("Валюта",                              ВалютаРУ);
	ПараметрыФормы.Вставить("Заголовок",                           НСтр("ru = 'Публикация товаров на Ozon';
																		|en = 'Publish goods on Ozon'"));
	ПараметрыФормы.Вставить("ЗаголовокКнопкиПеренести",            НСтр("ru = 'Перенести для публикации';
																		|en = 'Move for publication'"));
	ПараметрыФормы.Вставить("Дата",                                ОбщегоНазначенияКлиент.ДатаСеанса());
	ПараметрыФормы.Вставить("Документ",                            ПредопределенноеЗначение("Документ.УстановкаЦенНоменклатуры.ПустаяСсылка"));
	ПараметрыФормы.Вставить("ПараметрыУказанияСерий",              ПараметрыУказанияСерий);
	ПараметрыФормы.Вставить("ПодборВариантовОбеспечения",          Ложь);
	ПараметрыФормы.Вставить("ВидЦены",                             НастройкиУчетнойЗаписи.ВидыЦен.ВидЦеныСУчетомСкидок);
	ПараметрыФормы.Вставить("РежимПодбораБезСоглашенийСКлиентами", Истина);
	ПараметрыФормы.Вставить("РежимПодбораБезКоличественныхПараметров", Истина);
	ПараметрыФормы.Вставить("РежимПодбораБезСуммовыхПараметров",   Истина);
	ПараметрыФормы.Вставить("НеРазбиватьНаборыПоКомплектующим",    Истина);
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса",           УчетнаяЗаписьМаркетплейса);

	ОткрытьФорму("Обработка.ПодборТоваровВДокументПродажи.Форма", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура СопоставлениеКатегорий(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.СопоставлениеКатегорий", ПараметрыФормы);
	Оповестить("ОткрытиеФормыСопоставленияКатегорий", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьОстатки(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ПоказатьОповещениеПользователя(НСтр("ru = 'Выгрузка остатков';
										|en = 'Export stock balance'"),, НСтр("ru = 'Запущена фоновая выгрузка остатков товаров на маркетплейс.';
																			|en = 'Background stock balance export to the marketplace is started.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Выгрузка остатков';
													|en = 'Export stock balance'");
	ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Завершена выгрузка остатков товаров на маркетплейс';
													|en = 'The stock balance is exported to the trading platform'");
	ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При выгрузке остатков товаров на маркетплейс возникли ошибки';
														|en = 'Errors occurred while exporting the stock balance to the trading platform'");

	ДлительнаяОперация = ВыгрузитьОстаткиНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьЦены(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ПоказатьОповещениеПользователя(НСтр("ru = 'Выгрузка цен';
										|en = 'Export prices'"),, НСтр("ru = 'Запущена фоновая выгрузка цен товаров на маркетплейс.';
																		|en = 'Background goods price export to the marketplace is started.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Выгрузка цен';
													|en = 'Export prices'");
	ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Завершена выгрузка цен товаров на маркетплейс';
													|en = 'The item prices are exported to the trading platform'");
	ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При выгрузке цен товаров на маркетплейс возникли ошибки';
														|en = 'Errors occurred while exporting the item prices to the trading platform'");

	ДлительнаяОперация = ВыгрузитьЦеныНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьОстатки(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка остатков';
										|en = 'Import balance'"),, НСтр("ru = 'Запущена фоновая загрузка остатков товаров с маркетплейса.';
																			|en = 'Background stock balance import from the marketplace is started.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Загрузка остатков';
													|en = 'Import balance'");
	ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Завершена загрузка остатков товаров с маркетплейса';
													|en = 'The stock balance is imported from the trading platform'");
	ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При загрузке остатков товаров с маркетплейса возникли ошибки';
														|en = 'Errors occurred while importing the stock balance from the trading platform'");

	ДлительнаяОперация = ЗагрузитьОстаткиНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьЦены(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	ПоказатьОповещениеПользователя(НСтр("ru = 'Загрузка цен';
										|en = 'Import prices'"),, НСтр("ru = 'Запущена фоновая загрузка цен товаров с маркетплейса.';
																		|en = 'Background import of goods prices from the marketplace is started.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Загрузка цен';
															|en = 'Import prices'");
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершена фоновая загрузка цен товаров с маркетплейса.';
																|en = 'Background goods price import from the marketplace is completed.'");

	ДлительнаяОперация = ЗагрузитьЦеныНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Неопределено, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбновлениеТоварногоКаталога(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();

	Если Элементы.Товары.ВыделенныеСтроки.Количество() > 0 Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыполнитьОбновлениеТоварногоКаталогаПродолжить", ЭтотОбъект);
		ПоказатьВопрос(
			Оповещение,
			НСтр("ru = 'Выполнить замену описания товаров данными из Ozon для выделенных позиций?';
				|en = 'Replace the item details with data from Ozon for the selected items?'"),
			РежимДиалогаВопрос.ДаНетОтмена,
			60,
			КодВозвратаДиалога.Нет,
			НСтр("ru = 'Обновление товарного каталога';
				|en = 'Update the goods directory'"),
			КодВозвратаДиалога.Отмена);
			
	Иначе
		ВыполнитьОбновлениеТоварногоКаталогаПродолжить(КодВозвратаДиалога.Нет, Неопределено);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НастроитьИнформационнуюПанель(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	ПараметрыФормы = Новый Структура("УчетнаяЗаписьМаркетплейса, ПараметрыОтображенияОстатковЦен", УчетнаяЗаписьМаркетплейса, ПараметрыОтображенияОстатковЦен);
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.НастройкаОтображенияОстатков", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ИмпортТоваров(Команда)

	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
			И Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОбновитьСтраницуИмпортаИОчиститьРезультатПроверки();
	
	Элементы.ПодсказкаИмпортТоваров.Видимость = Истина;
	Элементы.ПодсказкаПросмотрИРедактированиеТоваров.Видимость = Ложь;
	Элементы.ПодсказкаИсправлениеОшибок.Видимость = Ложь;
	
	ОбновитьСтатистикуТоваров();
	
	УстановитьОтборТоваровПоМаркеру("", "", "ПоказатьВсе");

КонецПроцедуры

&НаКлиенте
Процедура ПросмотрИРедактированиеТоваров(Команда)

	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
			И Не Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	РезультатПроверкиИмпорта = Неопределено;
	
	ПриАктивацииГиперСсылкиНаПанели(
		"СтраницаПросмотрИРедактированиеТоваров",
		"КнопкаПросмотрИРедактированиеТоваров");
	
	Элементы.ПодсказкаИмпортТоваров.Видимость = Ложь;
	Элементы.ПодсказкаПросмотрИРедактированиеТоваров.Видимость = Истина;
	Элементы.ПодсказкаИсправлениеОшибок.Видимость = Ложь;
	
	ОбновитьСтатистикуТоваров();
	
	УстановитьОтборТоваровПоМаркеру("", "", "ПоказатьВсе");

КонецПроцедуры

&НаКлиенте
Процедура ИсправлениеОшибок(Команда)

	ОчиститьСообщения();
	
	ПриАктивацииГиперСсылкиНаПанели(
		"СтраницаИсправлениеОшибок",
		"КнопкаИсправлениеОшибок");
	
	Элементы.ПодсказкаИмпортТоваров.Видимость = Ложь;
	Элементы.ПодсказкаПросмотрИРедактированиеТоваров.Видимость = Ложь;
	Элементы.ПодсказкаИсправлениеОшибок.Видимость = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПоОтбору(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("УникальныйИдентификатор",		 УникальныйИдентификатор);
	ПараметрыФормы.Вставить("ОтборПоТипуНоменклатуры",		 Новый ФиксированныйМассив(ИнтеграцияСМаркетплейсомOzonКлиентСервер.ОтборПоНоменклатуре()));
	ПараметрыФормы.Вставить("ПодбиратьНоменклатуруПартнера", Ложь);
	ПараметрыФормы.Вставить("ИспользуетсяЦенообразование25", ЦенообразованиеКлиент.ИспользуетсяЦенообразование25(ОбщегоНазначенияКлиент.ДатаСеанса()));
	ПараметрыФормы.Вставить("Заголовок",					 НСтр("ru = 'Публикация товаров на Ozon';
																	|en = 'Publish goods on Ozon'"));
	ПараметрыФормы.Вставить("ЗаголовокКнопкиПеренести",		 НСтр("ru = 'Перенести для публикации';
																	|en = 'Move for publication'"));
	ПараметрыФормы.Вставить("РежимВыбора",					 Истина);
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса",	 УчетнаяЗаписьМаркетплейса);

	ОткрытьФорму("Обработка.ПодборТоваровПоОтбору.Форма.Форма", ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруГотовы(Команда)

	УстановитьОтборТоваровПоМаркеру("МаркерСтатуса", 3, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруОшибка(Команда)

	УстановитьОтборТоваровПоМаркеру("МаркерСтатуса", 0, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруВПроцессе(Команда)

	УстановитьОтборТоваровПоМаркеру("МаркерСтатуса", 1, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруВАрхиве(Команда)

	УстановитьОтборТоваровПоМаркеру("МаркерСтатуса", 2, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруИзмененыРеквизиты(Команда)

	УстановитьОтборТоваровПоМаркеру("ИзмененыРеквизиты", Истина, Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсе(Команда)

	УстановитьОтборТоваровПоМаркеру("", "", Команда.Имя);

КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВАрхив(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПеренестиВАрхивЗавершение", ЭтотОбъект,);
	ПоказатьВопрос(ОповещениеОЗавершении, 
		НСтр("ru = 'После отправки товаров в архив их возврат возможен только из личного кабинета. Продолжить?';
			|en = 'Once the goods are archived, you can return them from the archive only in the personal account. Continue?'"), 
		РежимДиалогаВопрос.ДаНет, 
		, 
		КодВозвратаДиалога.Нет, 
		НСтр("ru = 'Публикация товаров';
			|en = 'Goods publication'"), 
		КодВозвратаДиалога.Нет);

КонецПроцедуры

&НаКлиенте
Процедура ВернутьИзАрхива(Команда)

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Возврат из архива';
													|en = 'Return from the archive'");
	ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Товар возвращен из архива';
													|en = 'The item is returned from the archive'");
	ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При возврате товаров из архива возникли ошибки';
														|en = 'Errors occurred when returning goods from the archive'");

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	ДлительнаяОперация = ВернутьИзАрхиваНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКУстановкеЦен(Команда)

	ПараметрыОткрытияФормы = ПараметрыОткрытияПрайсЛиста(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПрайсЛист.Форма", ПараметрыОткрытияФормы, ЭтотОбъект,,,, ОповещениеОЗавершении);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеСписка(Команда)

	ОбновитьДанныеСпискаТоваров();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатистику(Команда)

	ОчиститьСообщения();
	
	ОбновитьСтатистикуТоваров();

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТоварыИзСервиса(Команда)

	ОчиститьСообщения();
	
	ВыполнитьПолучениеТоваровИзСервиса();

КонецПроцедуры

&НаКлиенте
Процедура СопоставитьДанные(Команда)

	ОчиститьСообщения();
	
	ИнтеграцияСМаркетплейсамиКлиент.ОткрытьСопоставлениеНоменклатуры(
		ЭтотОбъект,
		"Товары");

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обработчик серверных уведомлений подсистемы.
//
// Параметры:
//   Данные - см. ИнтеграцияСМаркетплейсамиКлиентСервер.НовыеДанныеСерверногоУведомления
//   ДополнительныеПараметры - Структура - Не используется
//
&НаКлиенте
Процедура ОбработкаСерверногоУведомления(Данные, ДополнительныеПараметры) Экспорт
	
	ОбработатьОповещенияИмпортаТоварногоКаталога(Данные.ИмяСобытияОповещения, Данные.УчетнаяЗапись);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОповещенияИмпортаТоварногоКаталога(ИмяСобытия, Параметр)
	
	ОбновитьСтраницуИмпорта = Ложь;
	ОбновитьСтраницуИмпортаСЗадержкой = Ложь;
	
	Если (ИмяСобытия = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияНачалаИмпортаТоварногоКаталога()
				Или ИмяСобытия = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияЗавершенияИмпортаТоварногоКаталога())
			И Параметр = УчетнаяЗаписьМаркетплейса
			И Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
			И Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		
		ОбновитьСтраницуИмпорта = Истина;
		ОбновитьСтраницуИмпортаСЗадержкой = 
			(ИмяСобытия = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяСобытияЗавершенияИмпортаТоварногоКаталога());
		
	ИначеЕсли ИмяСобытия = "ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьДанные"
				И Параметр = УчетнаяЗаписьМаркетплейса Тогда
		
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров Тогда
			
			ОбновитьДанныеСпискаТоваров();
			
			Если Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
				ОбновитьСтраницуИмпорта = Истина;
			КонецЕсли;
			
		Иначе
			Элементы.ТоварыСОшибками.Обновить();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ОбновитьСтраницуИмпорта Тогда
		Возврат;
		
	ИначеЕсли ОбновитьСтраницуИмпортаСЗадержкой Тогда
		ПодключитьОбработчикОжидания("ОбновитьСтраницуИмпортаИОчиститьРезультатПроверки", 1, Истина);
		
	Иначе
		ОбновитьСтраницуИмпортаИОчиститьРезультатПроверки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтраницуИмпортаИОчиститьРезультатПроверки()
	
	РезультатПроверкиИмпорта = Неопределено;
	
	ПриАктивацииГиперСсылкиНаПанели(
		"СтраницаПросмотрИРедактированиеТоваров",
		"КнопкаИмпортТоваров");
	
КонецПроцедуры

&НаСервере
Процедура ПриАктивацииГиперСсылкиНаПанели(ИмяСтраницы, ИмяАктивнойКоманды, ЭтоОткрытиеФормы = Ложь)

	ЗаполнитьРезультатПроверкиИмпорта(
		РезультатПроверкиИмпорта,
		УчетнаяЗаписьМаркетплейса,
		УникальныйИдентификатор,
		Параметры.ВРежимеИмпортаДанных);
	
	Если ЭтоОткрытиеФормы И Не РезультатПроверкиИмпорта.ДанныеСуществуют Тогда
		ИмяАктивнойКоманды = "КнопкаИмпортТоваров";
	КонецЕсли;
	
	ЭтоИмпорт = (ИмяАктивнойКоманды = "КнопкаИмпортТоваров");
	
	Элементы.КнопкаИмпортТоваров.Заголовок = НСтр("ru = 'Импорт товаров';
													|en = 'Import items'")
		+ ?(Не ЭтоИмпорт И РезультатПроверкиИмпорта.ТребуютСопоставления, "*", "");
	
	Если ИмяСтраницы = "СтраницаПросмотрИРедактированиеТоваров" Тогда
		Элементы.ГруппаДлительноеОжиданиеИмпорт.Видимость = ЭтоИмпорт;
		Элементы.КомандыРаботыСИмпортомТоваров.Видимость = ЭтоИмпорт;
		Элементы.ГруппаДобавитьУдалитьТовар.Видимость = Не ЭтоИмпорт;
		Элементы.ГруппаПодборОтбор.Видимость = Не ЭтоИмпорт;
		Элементы.ГруппаРаботыСТоварнымКаталогом.Видимость = Не ЭтоИмпорт;
		Элементы.ГруппаИнформация.Видимость = Не ЭтоИмпорт;
		Элементы.ТоварыГруппаОстатки.Видимость = Не ЭтоИмпорт;
		Элементы.ТоварыГруппаЦены.Видимость = Не ЭтоИмпорт;
		Элементы.ТоварыЗаданиеПубликации.Видимость = Не ЭтоИмпорт;
		Элементы.ТоварыДатаВыгрузкиЦенПродажи.Видимость = Не ЭтоИмпорт;
		Элементы.ИнформацияПоСчетчикуТоваров.Видимость = Не ЭтоИмпорт;
		Элементы.ТоварыТребуетсяСопоставление.Видимость = ЭтоИмпорт;
		
		Если ЭтоИмпорт Тогда
			АдресРезультатаИмпорта = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
			
			ОбновитьПодсказкиИДоступностьИмпорта(
				Элементы.КомандыРаботыСИмпортомТоваров,
				Элементы.ГруппаДлительноеОжиданиеИмпорт,
				РезультатПроверкиИмпорта);
			
			Если Элементы.КнопкаПросмотрИРедактированиеТоваров.Видимость Тогда // Доступно переключение
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
					Товары,
					"ТребуетсяСопоставление",
					Истина,
					ВидСравненияКомпоновкиДанных.Равно,
					"ОтборПоСопоставлению",
					Истина,
					РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			Иначе
				ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
					Товары,
					"ТребуетсяСопоставление",
					"ОтборПоСопоставлению");
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				Товары,
				"ТребуетсяСопоставление",
				Ложь,
				ВидСравненияКомпоновкиДанных.Равно,
				"ОтборПоСопоставлению",
				Истина,
				РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		КонецЕсли;
	КонецЕсли;
	
	МассивИменКомандПанели = Новый Массив;
	МассивИменКомандПанели.Добавить("КнопкаИмпортТоваров");
	МассивИменКомандПанели.Добавить("КнопкаПросмотрИРедактированиеТоваров");
	МассивИменКомандПанели.Добавить("КнопкаИсправлениеОшибок");
	
	Для Каждого ЭлементМассива Из МассивИменКомандПанели Цикл
		Если ЭлементМассива = ИмяАктивнойКоманды Тогда
			Элементы[ЭлементМассива].ЦветТекста = ЦветаСтиля.ЦветРамки;
		Иначе
			Элементы[ЭлементМассива].ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.Страницы.ТекущаяСтраница = Элементы[ИмяСтраницы];
	
	УстановитьПараметрыСпискаТоваров();

КонецПроцедуры

&НаСервере
Процедура ИнициализироватьПараметрыОтображенияОстатковЦен()

	ПараметрыОтображенияОстатковЦен = ИнтеграцияСМаркетплейсомOzonСервер.НовыеПараметрыОтображенияОстатковЦен(УчетнаяЗаписьМаркетплейса);
	ПараметрыОтображенияОстатковЦенПеременная = ОбщегоНазначения.ХранилищеНастроекДанныхФормЗагрузить(ИмяФормы,
		"ПараметрыОтображенияОстатковЦен", ПараметрыОтображенияОстатковЦен);
	Для Каждого КлючЗначение Из ПараметрыОтображенияОстатковЦенПеременная Цикл
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
			ПараметрыОтображенияОстатковЦен.Вставить(КлючЗначение.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(КлючЗначение.Значение));
		ИначеЕсли ПараметрыОтображенияОстатковЦен.Свойство(КлючЗначение.Ключ) Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтображенияОстатковЦен[КлючЗначение.Ключ], КлючЗначение.Значение, Истина);
		Иначе		
			ПараметрыОтображенияОстатковЦен.Вставить(КлючЗначение.Ключ, ОбщегоНазначения.СкопироватьРекурсивно(КлючЗначение.Значение));
		КонецЕсли;
	КонецЦикла;

	СтрокаПолей = "ОстатокПоУчету,ОстатокНаOzon,ОстатокFBO,ОстатокFBS,ЦенаСоСкидкой,ЦенаДоСкидки,МинимальнаяЦена,ЦенаСУчетомАкцийПродавца,ЦенаСУчетомВсехАкций,ЦенаПоставщика";
	СписокПолей = СтрРазделить(СтрокаПолей, ",");
	Для Каждого ЭлементКоллекции Из СписокПолей Цикл
		ПолеНабораДанных = Товары.Поля.Найти(ЭлементКоллекции);
		Если ПолеНабораДанных <> Неопределено Тогда
			ПолеНабораДанных.ОграничениеИспользования.Группировка = Истина;
			ПолеНабораДанных.ОграничениеИспользования.Порядок = Истина;
			ПолеНабораДанных.ОграничениеИспользования.Условие = Истина;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьКолонокЦеныОстатки()

	Если Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		Возврат;
	КонецЕсли;

	// Управление содержанием информационных надписей

	ОбновитьИнформационноеПолеОстаткиЦены();

	// Управление списком колонок таблицы товаров

	ВыводитьОстатокПоУчету = ПараметрыОтображенияОстатковЦен.ОстатокПоУчету.ОтображатьВТаблице;
	ВыводитьОстатокНаOzon = ПараметрыОтображенияОстатковЦен.ОстатокНаOzon.ОтображатьВТаблице;
	ВыводитьОстатокFBO = ПараметрыОтображенияОстатковЦен.ОстатокFBO.ОтображатьВТаблице;
	ВыводитьОстатокFBS = ПараметрыОтображенияОстатковЦен.ОстатокFBS.ОтображатьВТаблице;

	Элементы.ТоварыОстатокПоУчету.Видимость = ВыводитьОстатокПоУчету;
	Элементы.ТоварыОстатокНаOzon.Видимость = ВыводитьОстатокНаOzon;
	Элементы.ТоварыРезервОбщийOzon.Видимость = ВыводитьОстатокНаOzon;
	Элементы.ТоварыОстатокFBO.Видимость = ВыводитьОстатокFBO;
	Элементы.ТоварыРезервFBO.Видимость = ВыводитьОстатокFBO;
	Элементы.ТоварыОстатокFBS.Видимость = ВыводитьОстатокFBS;
	Элементы.ТоварыРезервFBS.Видимость = ВыводитьОстатокFBS;

	Элементы.ТоварыЦенаСоСкидкой.Видимость = ПараметрыОтображенияОстатковЦен.ЦенаСоСкидкой.ОтображатьВТаблице;
	Элементы.ТоварыЦенаДоСкидки.Видимость = ПараметрыОтображенияОстатковЦен.ЦенаДоСкидки.ОтображатьВТаблице;
	Элементы.ТоварыМинимальнаяЦена.Видимость = ПараметрыОтображенияОстатковЦен.МинимальнаяЦена.ОтображатьВТаблице;
	Элементы.ТоварыЦенаСУчетомАкцийПродавца.Видимость = ПараметрыОтображенияОстатковЦен.ЦенаСУчетомАкцийПродавца.ОтображатьВТаблице;
	Элементы.ТоварыЦенаСУчетомВсехАкций.Видимость = ПараметрыОтображенияОстатковЦен.ЦенаСУчетомВсехАкций.ОтображатьВТаблице;
	Элементы.ТоварыРекомендованнаяЦена.Видимость = Ложь;
	Элементы.ТоварыЦенаПоставщика.Видимость = ПараметрыОтображенияОстатковЦен.ЦенаПоставщика.ОтображатьВТаблице;

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСпискаТоваров()

	Товары.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	ТоварыСОшибками.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	
	ПостфиксНеиспользуемойКатегории = ИнтеграцияСМаркетплейсомOzonСервер.ПостфиксНеиспользуемойКатегории();

	ПостфиксУстаревшейКатегории = ИнтеграцияСМаркетплейсомOzonСервер.ПостфиксУстаревшейКатегории();
	Товары.Параметры.УстановитьЗначениеПараметра("ПризнакУстаревшейКатегории", ПостфиксУстаревшейКатегории);
	ТоварыСОшибками.Параметры.УстановитьЗначениеПараметра("ПризнакУстаревшейКатегории", ПостфиксУстаревшейКатегории);

	СвойстваСпискаТоваров = Товары.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	СвойстваСпискаТоваров.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	СвойстваСпискаТоваров.Вставить("ПараметрыОтображенияОстатковЦен", ПараметрыОтображенияОстатковЦен);

	НастройкиУчетнойЗаписи = ИнтеграцияСМаркетплейсомOzonСервер.НастройкиУчетнойЗаписиСЗагружаемымиВидамиЦен(УчетнаяЗаписьМаркетплейса);
	СвойстваСпискаТоваров.Вставить("НастройкиУчетнойЗаписи", НастройкиУчетнойЗаписи);

	ВалютаУчетнойЗаписи = Строка(НастройкиУчетнойЗаписи.ВалютаУчета); // Строка

	СвойстваСпискаТоваров.Вставить("ИспользуетсяЦенообразование25", ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ТекущаяДатаСеанса()));

	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров Тогда
		СвойстваСпискаТоваров.Вставить("ОбновлятьЦеныИОстатки", Не Элементы.КомандыРаботыСИмпортомТоваров.Видимость);
	Иначе
		СвойстваСпискаТоваров.Вставить("ОбновлятьЦеныИОстатки", Ложь);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоВходящимПараметрам()

	Если Параметры.ОтборПоSKU.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Товары,
			"SKU",
			Параметры.ОтборПоSKU,
			ВидСравненияКомпоновкиДанных.ВСписке,
			"ОтборПоSKU",,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуМаркетплейса.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Товары,
			"ИдентификаторТовара",
			Параметры.ОтборПоИдентификаторуМаркетплейса,
			ВидСравненияКомпоновкиДанных.ВСписке,
			"ОтборПоИдентификаторуМаркетплейса",,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуПубликации.Количество() > 0 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Товары,
			"ИдентификаторПубликации",
			Параметры.ОтборПоИдентификаторуПубликации,
			ВидСравненияКомпоновкиДанных.ВСписке,
			"ОтборПоИдентификаторуПубликации",,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформационноеПолеОстаткиЦены()

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ИнформацияПоЦенам = "";
		ИнформацияПоОстаткам = "";

		ИнформацияПоОстаткамДоп = "";
		Элементы.ГруппаИнформацияПоОстаткам.Доступность = Ложь;

		ИнформацияПоЦенамДоп = "";
		Элементы.ГруппаИнформацияПоЦенам.Доступность = Ложь;

		Возврат;
	КонецЕсли;

	// Остатки
	ИмяПоля = "Наименование";
	
	ВыводимыеПоказатели = Новый Массив;
	Для Каждого Показатель Из ПараметрыОтображенияОстатковЦен.ОстаткиИнфоПоля Цикл
		ПредставлениеОстатка = ПараметрыОтображенияОстатковЦен[Показатель][ИмяПоля] + ": "
			+ Формат(ТекущиеДанные[Показатель], "ЧН=; ЧГ=") + " " + Строка(ТекущиеДанные.Упаковка);
		ВыводимыеПоказатели.Добавить(ПредставлениеОстатка);
	КонецЦикла;
	Если ВыводимыеПоказатели.Количество() = 0 Тогда
		ИнформацияПоОстаткам = "";
	Иначе
		ИнформацияПоОстаткам = СтрСоединить(ВыводимыеПоказатели, ", ");
	КонецЕсли;

	ВыводимыеПоказатели = Новый Массив;
	Для Каждого Показатель Из ПараметрыОтображенияОстатковЦен.ОстаткиПодсказки Цикл
		ПредставлениеОстатка = ПараметрыОтображенияОстатковЦен[Показатель][ИмяПоля] + ": "
			+ Формат(ТекущиеДанные[Показатель], "ЧН=; ЧГ=") + " " + Строка(ТекущиеДанные.Упаковка);
		ВыводимыеПоказатели.Добавить(ПредставлениеОстатка);
	КонецЦикла;
	Если ВыводимыеПоказатели.Количество() = 0 Тогда
		ИнформацияПоОстаткамДоп = "";
		Элементы.ГруппаИнформацияПоОстаткам.Доступность = Ложь;
	Иначе
		ИнформацияПоОстаткамДоп = СтрСоединить(ВыводимыеПоказатели, Символы.ПС);
		Элементы.ГруппаИнформацияПоОстаткам.Доступность = Истина;
	КонецЕсли;

	// Цены
	ВыводимыеПоказатели = Новый Массив;
	Для Каждого Показатель Из ПараметрыОтображенияОстатковЦен.ЦеныИнфоПоля Цикл
		ПредставлениеОстатка = ПараметрыОтображенияОстатковЦен[Показатель][ИмяПоля] + ": "
			+ Формат(ТекущиеДанные[Показатель], "ЧДЦ=2; ЧН=; ЧГ=") + " " + ВалютаУчетнойЗаписи;
		ВыводимыеПоказатели.Добавить(ПредставлениеОстатка);
	КонецЦикла;
	Если ВыводимыеПоказатели.Количество() = 0 Тогда
		ИнформацияПоЦенам = "";
	Иначе
		ИнформацияПоЦенам = СтрСоединить(ВыводимыеПоказатели, ", ");
	КонецЕсли;

	ВыводимыеПоказатели = Новый Массив;
	Для Каждого Показатель Из ПараметрыОтображенияОстатковЦен.ЦеныПодсказки Цикл
		ПредставлениеОстатка = ПараметрыОтображенияОстатковЦен[Показатель][ИмяПоля] + ": "
			+ Формат(ТекущиеДанные[Показатель], "ЧДЦ=2; ЧН=; ЧГ=") + " " + ВалютаУчетнойЗаписи;
		ВыводимыеПоказатели.Добавить(ПредставлениеОстатка);
	КонецЦикла;
	Если ВыводимыеПоказатели.Количество() = 0 Тогда
		ИнформацияПоЦенамДоп = "";
		Элементы.ГруппаИнформацияПоЦенам.Доступность = Ложь;
	Иначе
		ИнформацияПоЦенамДоп = СтрСоединить(ВыводимыеПоказатели, Символы.ПС);
		Элементы.ГруппаИнформацияПоЦенам.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьЭлементовДляСтатуса()

	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	УчетнаяЗаписьВыбрана = Не УчетнаяЗаписьМаркетплейса.Пустая();
	СтатусВАрхиве        = (ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыОбъектовМаркетплейсаOzon.ВАрхиве"));

	Элементы.ТоварыИзменить.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ТоварыУдалить.Доступность = УчетнаяЗаписьВыбрана И ТекущиеДанные.ДоступноУдаление;
	Элементы.УдалитьТовар.Доступность = УчетнаяЗаписьВыбрана И ТекущиеДанные.ДоступноУдаление;
	Элементы.ТоварыКонтекстноеМенюУдалить.Доступность = УчетнаяЗаписьВыбрана И ТекущиеДанные.ДоступноУдаление;
	Элементы.ОтправитьНаМодерацию.Доступность = УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	Элементы.ПеренестиВАрхив.Доступность = УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено 
		И (Не СтатусВАрхиве И Не ПустаяСтрока(ТекущиеДанные.ИдентификаторТовара)
			Или Элементы.Товары.ВыделенныеСтроки.Количество() > 1);
	
	Элементы.ПерейтиКСсылкеНаТовар.Доступность = Не ПустаяСтрока(ТекущиеДанные.SKU);
	Элементы.ТоварыКонтекстноеМенюПерейтиКСсылкеНаТовар.Доступность = Не ПустаяСтрока(ТекущиеДанные.SKU);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеСпискаТоваров()

	ОбновитьСтатистикуТоваров();
	Элементы.Товары.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатистикуТоваров()

	Если УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Элементы.ГруппаСтатистикаПоВыгрузке.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	ОбновитьСтатистикуТоваровПродолжениеНаСервере();

	Если Параметры.ВРежимеИмпортаДанных Тогда
		Возврат;
	КонецЕсли;

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьСтатистикуТоваровПродолжение", ЭтотОбъект);
	ДлительнаяОперация = ПолучитьСчетчикВыгрузки(УчетнаяЗаписьМаркетплейса, УникальныйИдентификатор);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатистикуТоваровПродолжение(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Структура") 
			И Результат.Статус = "Выполнено" 
			И Результат.Свойство("АдресРезультата") Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОперации.ЗначениеУстановлено Тогда
			Если РезультатОперации.ОбщийЛимитСоздания <= 0 Тогда
				ШаблонСообщения =
					НСтр("ru = 'Возможны публикация %1 новых позиций и обновление %3 позиций. Сброс счетчиков будет выполнен %4';
						|en = 'You can publish %1 new items and update %3 items. The counter will be reset at %4'");
			Иначе
				ШаблонСообщения =
					НСтр("ru = 'Возможны публикация %1 новых позиций из доступных %2 позиций и обновление %3 позиций. Сброс счетчиков будет выполнен %4';
						|en = 'You can publish %1 new items out of %2 available items and update %3 items. The counter will be reset at %4'");
			КонецЕсли;

			ИнформацияПоСчетчикуТоваров = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				РезультатОперации.СуточныйОстатокСоздания,
				РезультатОперации.ОбщийОстатокСоздания,
				РезультатОперации.СуточныйЛимитОбновления,
				РезультатОперации.ВремяСбросаСчетчиков);
		Иначе
			ИнформацияПоСчетчикуТоваров = "";
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьСтатистикуТоваровПродолжениеНаСервере()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СУММА(ВложенныйЗапрос.ЗагруженСОшибками) КАК ЗагруженСОшибками,
	|	СУММА(ВложенныйЗапрос.КПубликации) КАК КПубликации,
	|	СУММА(ВложенныйЗапрос.ИзмененыРеквизиты) КАК ИзмененыРеквизиты
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 101
	|		КОЛИЧЕСТВО(ИСТИНА) КАК ЗагруженСОшибками,
	|		0 КАК КПубликации,
	|		0 КАК ИзмененыРеквизиты
	|	ИЗ
	|		РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
	|	ГДЕ
	|		СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|		И СтатусыПубликации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.ЗагруженСОшибками)
	|		И СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И &Условие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 101
	|		КОЛИЧЕСТВО(ИСТИНА),
	|		0,
	|		0
	|	ИЗ
	|		РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
	|	ГДЕ
	|		СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|		И СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И НЕ СтатусыПубликации.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.ЗагруженСОшибками), ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.ВАрхиве))
	|		И (ВЫРАЗИТЬ(СтатусыПубликации.КодОшибки КАК СТРОКА(1024))) <> """"
	|		И &Условие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 101
	|		0,
	|		КОЛИЧЕСТВО(ИСТИНА),
	|		0
	|	ИЗ
	|		РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
	|	ГДЕ
	|		СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|		И СтатусыПубликации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.КПубликации)
	|		И СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И (ВЫРАЗИТЬ(СтатусыПубликации.КодОшибки КАК СТРОКА(1024))) = """"
	|		И &Условие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 101
	|		0,
	|		КОЛИЧЕСТВО(ИСТИНА),
	|		0
	|	ИЗ
	|		РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
	|	ГДЕ
	|		СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|		И СтатусыПубликации.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОбъектовМаркетплейсаOzon.НаМодерации)
	|		И СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И &Условие
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 101
	|		0,
	|		0,
	|		КОЛИЧЕСТВО(ИСТИНА)
	|	ИЗ
	|		РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации
	|	ГДЕ
	|		СтатусыПубликации.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|		И СтатусыПубликации.ИзмененыРеквизиты = ИСТИНА
	|		И СтатусыПубликации.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И &Условие) КАК ВложенныйЗапрос";
	
	ТекстыУсловия = Новый Массив;
	
	Если Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
		Если Не Параметры.ВРежимеИмпортаДанных Тогда
			ТекстыУсловия.Добавить("СтатусыПубликации.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)");
		КонецЕсли;
	Иначе
		ТекстыУсловия.Добавить("СтатусыПубликации.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)");
	КонецЕсли;
	
	Если Параметры.ОтборПоSKU.Количество() > 0 Тогда
		ТекстыУсловия.Добавить("(СтатусыПубликации.ИдентификаторFBOSKU В (&ИдентификаторыSKU)
								|		ИЛИ СтатусыПубликации.ИдентификаторFBSSKU В (&ИдентификаторыSKU))");
		Запрос.УстановитьПараметр("ИдентификаторыSKU", Параметры.ОтборПоSKU.ВыгрузитьЗначения());
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуМаркетплейса.Количество() > 0 Тогда
		ТекстыУсловия.Добавить("СтатусыПубликации.ИдентификаторПубликации В (&ИдентификаторыМаркетплейса)");
		Запрос.УстановитьПараметр("ИдентификаторыМаркетплейса", Параметры.ОтборПоИдентификаторуМаркетплейса.ВыгрузитьЗначения());
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуПубликации.Количество() > 0 Тогда
		ТекстыУсловия.Добавить("СтатусыПубликации.ИдентификаторПубликации В (&ИдентификаторыПубликации)");
		Запрос.УстановитьПараметр("ИдентификаторыПубликации", Параметры.ОтборПоИдентификаторуПубликации.ВыгрузитьЗначения());
	КонецЕсли;
	
	Если ТекстыУсловия.Количество() > 0 Тогда
		ТекстУсловия = "И" + " " + СтрСоединить(ТекстыУсловия, " И ");
	Иначе
		ТекстУсловия = "";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &Условие", ТекстУсловия);
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗаписьМаркетплейса);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Элементы.ГруппаСтатистикаПоВыгрузке.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	Элементы.ГруппаСтатистикаПоВыгрузке.Видимость = Истина;
	Выборка = Результат.Выбрать();
	Выборка.Следующий();

	ПредставлениеПревышенияСчетчика = НСтр("ru = 'более 100 позиций';
											|en = 'over 100 positions'");

	ОжидаютИсправленияОшибок = НСтр("ru = 'Ожидают исправления ошибок:';
									|en = 'Pending error correction:'") + " ";
	Если Выборка.ЗагруженСОшибками <= 100 Тогда
		МассивСклонений = ПолучитьСклоненияСтрокиПоЧислу("позиция", Выборка.ЗагруженСОшибками, "",
			"ЧС=Количественное", "ПД=Именительный; ПЧ=Число", "L=ru_RU; ЧН=; ЧГ=");
		ОжидаютИсправленияОшибок = ОжидаютИсправленияОшибок + МассивСклонений[0];
	Иначе
		ОжидаютИсправленияОшибок = ОжидаютИсправленияОшибок + ПредставлениеПревышенияСчетчика;
	КонецЕсли;

	ПодготовленыКПубликации = НСтр("ru = 'В процессе обработки:';
									|en = 'Processing:'") + " ";
	Если Выборка.КПубликации <= 100 Тогда
		МассивСклонений = ПолучитьСклоненияСтрокиПоЧислу("позиция", Выборка.КПубликации, "",
			"ЧС=Количественное", "ПД=Именительный; ПЧ=Число", "L=ru_RU; ЧН=; ЧГ=");
		ПодготовленыКПубликации = ПодготовленыКПубликации + МассивСклонений[0];
	Иначе
		ПодготовленыКПубликации = ПодготовленыКПубликации + ПредставлениеПревышенияСчетчика;
	КонецЕсли;

	ОбнаруженыИзмененияРеквизитов = НСтр("ru = 'Изменены реквизиты:';
										|en = 'Attributes modified:'") + " ";
	Если Выборка.ИзмененыРеквизиты <= 100 Тогда
		МассивСклонений = ПолучитьСклоненияСтрокиПоЧислу("позиция", Выборка.ИзмененыРеквизиты, "",
			"ЧС=Количественное", "ПД=Именительный; ПЧ=Число", "L=ru_RU; ЧН=; ЧГ=");
		ОбнаруженыИзмененияРеквизитов = ОбнаруженыИзмененияРеквизитов + МассивСклонений[0];
	Иначе
		ОбнаруженыИзмененияРеквизитов = ОбнаруженыИзмененияРеквизитов + ПредставлениеПревышенияСчетчика;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Структура") 
			И Результат.Статус = "Выполнено"
			И Результат.Свойство("АдресРезультата") Тогда
		Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров
				И Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
			ОбновитьПодсказкиИДоступностьСопоставления();
		КонецЕсли;
		
		РезультатДействия = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ИнтеграцияСМаркетплейсомOzonКлиент.ВывестиСостояние(РезультатДействия, ДополнительныеПараметры, Истина);
	КонецЕсли;

	Элементы.Товары.Обновить();
	Элементы.ТоварыСОшибками.Обновить();
	ТоварыПриАктивизацииСтроки(Элементы.Товары);
	ТоварыСОшибкамиПриАктивизацииСтроки(Элементы.ТоварыСОшибками);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборТоваровПоМаркеру(Поле, Значение, ИмяКоманды)

	ИдентификаторОтбора = "ОтборПоМаркеру";
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Товары,, ИдентификаторОтбора);
	
	Если Не ПустаяСтрока(Поле) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Товары,
			Поле,
			Значение,
			ВидСравненияКомпоновкиДанных.Равно,
			ИдентификаторОтбора,,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	КонецЕсли;

	ОбновитьКнопкуОтбораПоСтатусу(ИмяКоманды);

КонецПроцедуры

&НаСервере
Процедура ОбновитьКнопкуОтбораПоСтатусу(ИмяКоманды)

	Команда = Команды.Найти(ИмяКоманды);
	Если Команда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Элементы.ОтборПоМаркерам.Заголовок = Команда.Заголовок;
	Элементы.ОтборПоМаркерам.Картинка = Команда.Картинка;

КонецПроцедуры

// Функция - Таблица товаров из выделенных строк
//  Возвращает заполненную таблицу товаров по переданному массиву ключей строк (выделенных строк дин.списка).
// Параметры:
//  ВыделенныеСтроки			 - Массив из РегистрСведенийКлючЗаписи - ключи строк, обязательно содержащие реквизиты Номенклатура, Характеристика и Упаковка.
//  УчетнаяЗаписьМаркетплейса - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись для дозаполнения колонки таблицы товаров.
//  ВидОбъектаМаркетплейса - ПеречислениеСсылка.ВидыОбъектовМаркетплейсов - Вид объекта для дозаполнения колонки таблицы товаров.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - См.описание в ИнтеграцияСМаркетплейсомOzonСервер.СведенияОВыгруженныхДанных().
//
&НаСервереБезКонтекста
Функция ТаблицаТоваровИзВыделенныхСтрок(ЗНАЧ ВыделенныеСтроки, УчетнаяЗаписьМаркетплейса = Неопределено, ВидОбъектаМаркетплейса = Неопределено)

	ТаблицаТоваров = ИнтеграцияСМаркетплейсомOzonСервер.СведенияОВыгруженныхДанных();

	ПоляЗаполнения = "Номенклатура, Характеристика, Упаковка, НоменклатураПартнера";
	Если Не ЗначениеЗаполнено(УчетнаяЗаписьМаркетплейса) Тогда
		ПоляЗаполнения = "УчетнаяЗаписьМаркетплейса, " + ПоляЗаполнения;
	КонецЕсли;

	Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
		НоваяСтрока = ТаблицаТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, КлючЗаписи, ПоляЗаполнения);
	КонецЦикла;
	Если ЗначениеЗаполнено(УчетнаяЗаписьМаркетплейса) Тогда
		ТаблицаТоваров.ЗаполнитьЗначения(УчетнаяЗаписьМаркетплейса, "УчетнаяЗаписьМаркетплейса");
	КонецЕсли;
	Если ЗначениеЗаполнено(ВидОбъектаМаркетплейса) Тогда
		ТаблицаТоваров.ЗаполнитьЗначения(ВидОбъектаМаркетплейса, "ВидОбъектаМаркетплейса");
	Иначе
		ТаблицаТоваров.ЗаполнитьЗначения(ПредопределенноеЗначение("Перечисление.ВидыОбъектовМаркетплейсов.Товар"), "ВидОбъектаМаркетплейса");
	КонецЕсли;

	Возврат ТаблицаТоваров;

КонецФункции

&НаСервереБезКонтекста
Функция ИдентификаторыПубликацииИзВыделенныхСтрок(ЗНАЧ ВыделенныеСтроки, ИмяИдентификатора = "ИдентификаторПубликации")

	ИдентификаторыПубликации = Новый Массив;
	
	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаТоваров.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
		|	ТаблицаТоваров.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Характеристика КАК Характеристика,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ТаблицаТоваров.НоменклатураПартнера КАК НоменклатураПартнера
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	&ТаблицаТоваров КАК ТаблицаТоваров
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СтатусыПубликацииОбъектовМаркетплейсаOzon.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	СтатусыПубликацииОбъектовМаркетплейсаOzon.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
		|	ВЫБОР
		|		КОГДА СтатусыПубликацииОбъектовМаркетплейсаOzon.ИдентификаторFBOSKU = """"
		|			ТОГДА СтатусыПубликацииОбъектовМаркетплейсаOzon.ИдентификаторFBSSKU
		|		ИНАЧЕ СтатусыПубликацииОбъектовМаркетплейсаOzon.ИдентификаторFBOSKU
		|	КОНЕЦ КАК ИдентификаторSKU
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектовМаркетплейсаOzon
		|		ПО ТаблицаТоваров.УчетнаяЗаписьМаркетплейса = СтатусыПубликацииОбъектовМаркетплейсаOzon.УчетнаяЗаписьМаркетплейса
		|			И ТаблицаТоваров.ВидОбъектаМаркетплейса = СтатусыПубликацииОбъектовМаркетплейсаOzon.ВидОбъектаМаркетплейса
		|			И ТаблицаТоваров.Номенклатура = СтатусыПубликацииОбъектовМаркетплейсаOzon.Номенклатура
		|			И ТаблицаТоваров.Характеристика = СтатусыПубликацииОбъектовМаркетплейсаOzon.Характеристика
		|			И ТаблицаТоваров.Упаковка = СтатусыПубликацииОбъектовМаркетплейсаOzon.Упаковка
		|			И ТаблицаТоваров.НоменклатураПартнера = СтатусыПубликацииОбъектовМаркетплейсаOzon.НоменклатураПартнера
		|			И (СтатусыПубликацииОбъектовМаркетплейсаOzon.ОбъектПубликации = НЕОПРЕДЕЛЕНО)
		|ГДЕ
		|	СтатусыПубликацииОбъектовМаркетплейсаOzon.Статус В(&ОбновляемыеСтатусы)";
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", ТаблицаТоваров);
	
	ОбновляемыеСтатусы = ИнтеграцияСМаркетплейсомOzonСервер.ОбновляемыеСтатусыПубликации();
	Запрос.УстановитьПараметр("ОбновляемыеСтатусы", ОбновляемыеСтатусы);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаИдентификаторов = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	Пока ВыборкаИдентификаторов.Следующий() Цикл
		Идентификатор = ВыборкаИдентификаторов[ИмяИдентификатора];
		Если Не ПустаяСтрока(Идентификатор) Тогда
			ИдентификаторыПубликации.Добавить(Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ИдентификаторыПубликации;

КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораПодборДлительнаяОперация(ВыбранноеЗначение)

	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Обработка.УправлениеПродажамиНаOzon.Форма.ПубликацияТоваров.ПодборТоваров");

	ДлительнаяОперация = ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение);

	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработкаВыбораПодборДлительнаяОперацияЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)

	ТаблицаТоваров = ИнтеграцияСМаркетплейсомOzonСервер.НоваяТаблицаПубликацииТоваров();

	ПодобранныеТовары = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресТоваровВХранилище);

	Если ТипЗнч(ПодобранныеТовары) <> Тип("ТаблицаЗначений")
			Или ПодобранныеТовары.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25()
			И ПодобранныеТовары.Колонки.Найти("ХарактеристикаЦО") <> Неопределено
			И ПодобранныеТовары.Колонки.Найти("УпаковкаЦО") <> Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ПодобранныеТовары.Номенклатура КАК Номенклатура,
			|	ПодобранныеТовары.Характеристика КАК Характеристика,
			|	ПодобранныеТовары.ХарактеристикаЦО КАК ХарактеристикаЦО,
			|	ПодобранныеТовары.УпаковкаЦО КАК УпаковкаЦО
			|ПОМЕСТИТЬ ПодобранныеТовары
			|ИЗ
			|	&ПодобранныеТовары КАК ПодобранныеТовары
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПодобранныеТовары.Номенклатура КАК Номенклатура,
			|	ПодобранныеТовары.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ПодобранныеТовары.УпаковкаЦО = СправНоменклатура.ЕдиницаИзмерения
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ИНАЧЕ ПодобранныеТовары.УпаковкаЦО
			|	КОНЕЦ КАК Упаковка
			|ИЗ
			|	ПодобранныеТовары КАК ПодобранныеТовары
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправНоменклатура
			|		ПО ПодобранныеТовары.Номенклатура = СправНоменклатура.Ссылка";
		
		Запрос.УстановитьПараметр("ПодобранныеТовары", ПодобранныеТовары);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
	Иначе
		Для Каждого СтрокаТовара Из ПодобранныеТовары Цикл
			НоваяСтрока = ТаблицаТоваров.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТовара);
		КонецЦикла;
	КонецЕсли;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ОбработкаВыбораПодбор";
	НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Обработка подбора товаров';
										|en = 'Ozon. Process goods selection'");

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеФоновогоЗадания;

	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
			ТаблицаТоваров, УчетнаяЗаписьМаркетплейса);

КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораПодборДлительнаяОперацияЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Элементы.Товары.Обновить();

	ОписаниеДействия = НСтр("ru = 'Добавление товаров';
							|en = 'Add goods'");
	ДобавлениеЗаписейВыполнено = Ложь;

	Если ТипЗнч(Результат) = Тип("Структура") 
			И Результат.Статус = "Выполнено"
			И Результат.Свойство("АдресРезультата") Тогда
		Результат = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ДобавлениеЗаписейВыполнено = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ДобавлениеЗаписейВыполнено", ДобавлениеЗаписейВыполнено);

		СообщениеПользователю = "";
		Если Результат.Свойство("СообщениеПользователю", СообщениеПользователю) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеПользователю);
		КонецЕсли;
	КонецЕсли;

	Если ДобавлениеЗаписейВыполнено Тогда
		Состояние(ОписаниеДействия,, НСтр("ru = 'Добавлены подобранные товары';
											|en = 'Selected goods are added'"));
	Иначе
		Состояние(ОписаниеДействия,, НСтр("ru = 'Не найдены товары для добавления';
											|en = 'No goods to add are found'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбновлениеТоварногоКаталогаПродолжить(Ответ, ДопоплнительныеПараметры) Экспорт
	
	ДопустимыеВариантыОтвета = Новый Массив; // Массив из КодВозвратаДиалога
	
	ДопустимыеВариантыОтвета.Добавить(КодВозвратаДиалога.Да);
	ДопустимыеВариантыОтвета.Добавить(КодВозвратаДиалога.Нет);
	
	Если ДопустимыеВариантыОтвета.Найти(Ответ) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Обновление товарного каталога';
										|en = 'Update the goods directory'"),, НСтр("ru = 'Запущено фоновое обновление товарного каталога.';
																						|en = 'Background goods directory update is started.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Обновление товарного каталога';
															|en = 'Update the goods directory'");
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершено фоновое обновление товарного каталога.';
																|en = 'Background goods directory update is completed.'");

	ДлительнаяОперация = ВыполнитьОбновлениеТоварногоКаталогаНаСервере(
		УчетнаяЗаписьМаркетплейса,
		?(Ответ = КодВозвратаДиалога.Да, Элементы.Товары.ВыделенныеСтроки, Новый Массив));

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьНаМодерациюВФоне(ВыделенныеСтроки)

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Отправка товаров на модерацию';
													|en = 'Send the goods to moderation'");
	ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Товары отправлены на модерацию';
													|en = 'The goods are sent to moderation'");
	ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При отправке товаров на модерацию возникли ошибки';
														|en = 'Errors occurred when sending the goods to moderation'");

	ДлительнаяОперация = ОтправитьНаМодерациюНаСервере(УчетнаяЗаписьМаркетплейса, ВыделенныеСтроки);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтправитьНаМодерациюНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне();
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Публикация товаров.';
															|en = 'Ozon. Publish goods.'");

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ВыполнитьПубликациюТоваровСУстановкойСтатусаКПубликации";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаСервереБезКонтекста
Функция УдалитьТоварыНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Удалить товары.';
															|en = 'Ozon. Delete goods.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.УдалитьСписокТоваровБезSKUИзАрхива";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаСервереБезКонтекста
Функция ВыгрузитьОстаткиНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Выгрузить остатки товаров.';
															|en = 'Ozon. Export stock balance.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ВыгрузитьОстаткиТоваров";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров, Ложь, Ложь);

КонецФункции

&НаСервереБезКонтекста
Функция ВыгрузитьЦеныНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Выгрузить цены товаров.';
															|en = 'Ozon. Export goods prices.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ВыгрузитьЦеныТоваров";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров, Ложь);

КонецФункции

&НаСервереБезКонтекста
Функция ЗагрузитьОстаткиНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Загрузить остатки товаров.';
															|en = 'Ozon. Import stock balance.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ЗагрузитьОстаткиТоваров";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаСервереБезКонтекста
Функция ЗагрузитьЦеныНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Загрузить цены товаров.';
															|en = 'Ozon. Import goods prices.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ЗагрузитьЦеныТоваров";
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаСервереБезКонтекста
Функция ВыполнитьОбновлениеТоварногоКаталогаНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Выполнить обновление товарного каталога.';
															|en = 'Ozon. Update goods directory.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ПараметрыМетода =
		ИнтеграцияСМаркетплейсомOzonКлиентСервер.ДополнительныеПараметрыРегламентногоЗадания(
			"ОбновлениеТоварногоКаталога");

	ПараметрыМетода.Идентификаторы.ИдентификаторыМаркетплейса =
		ИдентификаторыПубликацииИзВыделенныхСтрок(ВыделенныеСтроки, "ИдентификаторОбъектаМаркетплейса");

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьТоварныйКаталогРегламентнымЗаданием";
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(
		ПараметрыВыполнения,
		ИмяМетода,
		УчетнаяЗаписьМаркетплейса,
		ПараметрыМетода);

КонецФункции

&НаКлиенте
Процедура ПеренестиВАрхивЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

		ДлительнаяОперация = ВыгрузитьНулевыеОстатки(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Неопределено, ПараметрыОжидания);

		ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
		ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Перенос в архив';
														|en = 'Move to the archive'");
		ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Товар перенесен в архив';
														|en = 'The item is moved to the archive'");
		ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'При переносе товаров в архив возникли ошибки';
															|en = 'Errors occurred when moving goods to the archive'");

		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
		ДлительнаяОперация = ПеренестиВАрхивНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыгрузитьНулевыеОстатки(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Выгрузить нулевые остатки товаров.';
															|en = 'Ozon. Export zero stock balance.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ВыгрузитьОстаткиТоваров";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров, Истина);

КонецФункции

&НаСервереБезКонтекста
Функция ПеренестиВАрхивНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Перенести товары в архив.';
															|en = 'Ozon. Move goods to archive.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ПеренестиСписокТоваровВАрхив";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаСервереБезКонтекста
Функция ВернутьИзАрхиваНаСервере(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки, УникальныйИдентификатор)

	ТаблицаТоваров = ТаблицаТоваровИзВыделенныхСтрок(ВыделенныеСтроки);

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Вернуть товары из архива.';
															|en = 'Ozon. Return goods from archive.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ВернутьСписокТоваровИзАрхива";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, ТаблицаТоваров);

КонецФункции

&НаКлиенте
Процедура УдалитьТоварЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;

		ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
		ДополнительныеПараметры.ОписаниеДействия = НСтр("ru = 'Удаление товара';
														|en = 'Delete the item'");
		ДополнительныеПараметры.УспешныйРезультат = НСтр("ru = 'Товар удален';
														|en = 'The item is deleted'");
		ДополнительныеПараметры.РезультатСОшибками = НСтр("ru = 'Товар не удален';
															|en = 'The item is not deleted'");

		ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
		ДлительнаяОперация = УдалитьТоварыНаСервере(УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСчетчикВыгрузки(УчетнаяЗаписьМаркетплейса, УникальныйИдентификатор)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Получить счетчик выгрузки товаров.';
															|en = 'Ozon. Get goods export counter.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьСчетчикВыгрузкиИзСервиса";
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса);

КонецФункции

&НаСервереБезКонтекста
Процедура ДобавитьПолучениеЦеныВТекстЗапроса(СтруктураПараметров)

	Если (СтруктураПараметров.ПараметрыОтображенияОстатковЦен[СтруктураПараметров.ИмяРеквизита].ОтображатьВТаблице = Ложь
		И СтруктураПараметров.ПараметрыОтображенияОстатковЦен[СтруктураПараметров.ИмяРеквизита].ОтображатьВТаблице = Ложь)
		Или СтруктураПараметров.ВидЦен.Пустая() Тогда
		СтруктураПараметров.РеквизитыЗапроса.Добавить("0 КАК " + СтруктураПараметров.ИмяРеквизита);
		СтруктураПараметров.РеквизитыЗапроса.Добавить("&БазоваяВалюта КАК " + СтруктураПараметров.ИмяРеквизита + "Валюта");
		Возврат;
	КонецЕсли;

	ТекстРеквизита = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"	МАКСИМУМ(ВЫБОР
		|				КОГДА ТоварыСЦенами.ВидЦены = &%1
		|					ТОГДА ЕСТЬNULL(ТоварыСЦенами.Цена, 0)
		|							* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиТовара, 1)
		|							/ ВЫБОР
		|								КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиЦены, 1) = 0
		|									ТОГДА 1
		|								ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиЦены, 1)
		|							КОНЕЦ
		|				ИНАЧЕ 0
		|			КОНЕЦ)", 
		СтруктураПараметров.ИмяРеквизита);

	СтруктураПараметров.Запрос.УстановитьПараметр(СтруктураПараметров.ИмяРеквизита, СтруктураПараметров.ВидЦен);
	СтруктураПараметров.РеквизитыЗапроса.Добавить(ТекстРеквизита + " КАК " + СтруктураПараметров.ИмяРеквизита);
	СтруктураПараметров.РеквизитыЗапроса.Добавить("МАКСИМУМ(ЕСТЬNULL(ТоварыСЦенами.Валюта, &БазоваяВалюта))" + " КАК " + СтруктураПараметров.ИмяРеквизита + "Валюта");

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыОткрытияПрайсЛиста(УчетнаяЗаписьМаркетплейса, ЗНАЧ ВыделенныеСтроки);

	Товары = Новый Массив;
	Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
		Товары.Добавить(КлючЗаписи.Номенклатура);
	КонецЦикла;
	ВидыЦен = ИнтеграцияСМаркетплейсомOzonСервер.ИспользуемыеВидыЦен(УчетнаяЗаписьМаркетплейса);
	Возврат Новый Структура("Номенклатура, ВидыЦен, УчетнаяЗаписьМаркетплейса", Товары, ВидыЦен, УчетнаяЗаписьМаркетплейса);

КонецФункции

&НаСервере
Процедура СохранитьДанныеФормы()

	ОбщегоНазначения.ХранилищеНастроекДанныхФормСохранить(ИмяФормы, "ПараметрыОтображенияОстатковЦен",
		ПараметрыОтображенияОстатковЦен);

КонецПроцедуры

&НаСервере
Процедура УчетнаяЗаписьПриИзмененииНаСервере()
	
	ОбновлениеДанныхРазрешено = ИнтеграцияСМаркетплейсомOzonВызовСервера.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗаписьМаркетплейса);
	
	ИнициализироватьПараметрыОтображенияОстатковЦен();
	УстановитьПараметрыСпискаТоваров();
	ОбновитьКнопкуОтбораПоСтатусу(Команды.ПоказатьВсе.Имя);
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПросмотрИРедактированиеТоваров Тогда
		РезультатПроверкиИмпорта = Неопределено;
		
		Если Элементы.КомандыРаботыСИмпортомТоваров.Видимость Тогда
			ИмяАктивнойКоманды = "КнопкаИмпортТоваров";
		Иначе
			ИмяАктивнойКоманды = "КнопкаПросмотрИРедактированиеТоваров";
		КонецЕсли;
		
		ПриАктивацииГиперСсылкиНаПанели(
			"СтраницаПросмотрИРедактированиеТоваров",
			ИмяАктивнойКоманды);
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаИсправлениеОшибок Тогда
		ПриАктивацииГиперСсылкиНаПанели(
			"СтраницаИсправлениеОшибок",
			"КнопкаИсправлениеОшибок");
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Публикация товаров на Ozon';
					|en = 'Publish goods on Ozon'");
	Если Не УчетнаяЗаписьМаркетплейса.Пустая() Тогда
		Заголовок = Заголовок + " - " + УчетнаяЗаписьМаркетплейса;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВидимостьЭлементов()

	УстановитьВидимостьКолонокЦеныОстатки();
	
	УчетнаяЗаписьВыбрана = Не УчетнаяЗаписьМаркетплейса.Пустая();
	СтрокаТаблицыВыбрана = Элементы.Товары.ТекущаяСтрока <> Неопределено;
	
	Элементы.ТоварыСоздать.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ТоварыИзменить.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана;
	Элементы.ТоварыУдалить.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана;
	Элементы.ТоварыПодбор.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ТоварыДобавитьПоОтбору.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтобратьПоМаркеруИзмененыРеквизиты.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтобратьПоМаркеруГотовы.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтобратьПоМаркеруОшибка.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтобратьПоМаркеруВПроцессе.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтобратьПоМаркеруВАрхиве.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ПоказатьВсе.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ПерейтиКСсылкеНаТовар.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОтправитьНаМодерацию.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	Элементы.ВыгрузитьОстатки.Доступность = УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	Элементы.ВыгрузитьЦены.Доступность = УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	Элементы.ОбновитьОстаткиТоваровСМП.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ОбновитьЦеныТоваровСМП.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ВыполнитьОбновлениеТоварногоКаталога.Доступность = УчетнаяЗаписьВыбрана;
	Элементы.ПеренестиВАрхив.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;
	Элементы.НастроитьИнформационнуюПанель.Доступность = УчетнаяЗаписьВыбрана;

	СтрокаТаблицыВыбрана = Элементы.ТоварыСОшибками.ТекущаяСтрока <> Неопределено;
	Элементы.ТоварыСОшибкамиИзменитьТовар.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана;
	Элементы.ТоварыСОшибкамиУдалитьТовар.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана;
	Элементы.ТоварыСОшибкамиОтправитьНаМодерацию.Доступность = СтрокаТаблицыВыбрана И УчетнаяЗаписьВыбрана И ОбновлениеДанныхРазрешено;

КонецПроцедуры

#Область ИмпортТоваровСлужебный

&НаКлиенте
Процедура ВыполнитьПолучениеТоваровИзСервиса()

	ОчиститьСообщения();
	
	ПараметрыИмпорта = ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыПолученияТоваровИзСервиса(
		ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon"));
	ПараметрыИмпорта.УникальныйИдентификатор = УникальныйИдентификатор;
	ПараметрыИмпорта.ВыводитьОкноОжидания    = Параметры.ВРежимеИмпортаДанных;
	ПараметрыИмпорта.КлючЗадания             = КлючЗаданияИмпортаТоварногоКаталога(
		УчетнаяЗаписьМаркетплейса.УникальныйИдентификатор(),
		УникальныйИдентификатор,
		Параметры.ВРежимеИмпортаДанных);
	ПараметрыИмпорта.АдресРезультата         = АдресРезультатаИмпорта;
	
	// Настройки получения порции вызываемого метода - параметры выполнения вызываемого метода.
	ПараметрыМетода = ПараметрыИмпорта.ПараметрыМетода;
	
	ПараметрыМетода.УчетнаяЗапись = УчетнаяЗаписьМаркетплейса;
	ПараметрыМетода.Партнер       = РезультатПроверкиИмпорта.Партнер;
	
	ПараметрыМетода.ВыводитьСообщения       = Параметры.ВРежимеИмпортаДанных;
	ПараметрыМетода.ИдентификаторНазначения = УникальныйИдентификатор;
	
	Если Параметры.ОтборПоSKU.Количество() > 0 Тогда
		ПараметрыМетода.Идентификаторы.ИдентификаторыSKU =
			Параметры.ОтборПоSKU.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуМаркетплейса.Количество() > 0 Тогда
		ПараметрыМетода.Идентификаторы.ИдентификаторыМаркетплейса =
			Параметры.ОтборПоИдентификаторуМаркетплейса.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если Параметры.ОтборПоИдентификаторуПубликации.Количество() > 0 Тогда
		ПараметрыМетода.Идентификаторы.ИдентификаторыПубликации =
			Параметры.ОтборПоИдентификаторуПубликации.ВыгрузитьЗначения();
	КонецЕсли;
	
	ПараметрыМетода.НастройкиИмпорта.ЗаписыватьНовые           = Истина;
	ПараметрыМетода.НастройкиИмпорта.ЗаписыватьНовыеАрхивные   = Параметры.ВРежимеИмпортаДанных;
	ПараметрыМетода.НастройкиИмпорта.ОбновлятьНесопоставленные = Истина;
	
	Если Не Параметры.ВРежимеИмпортаДанных И РезультатПроверкиИмпорта.РучнойИмпортРегламентнымЗаданием Тогда
		ИнтеграцияСМаркетплейсамиВызовСервера.ПроверитьИЗапуститьРучнойИмпортТоварногоКаталога(ПараметрыИмпорта);
	Иначе
		
		ИнтеграцияСМаркетплейсамиКлиент.ВыполнитьПолучениеТоваровИзСервиса(
			ЭтотОбъект, ПараметрыИмпорта, Не Параметры.ВРежимеИмпортаДанных);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючЗаданияИмпортаТоварногоКаталога(ИдентификаторУчетнойЗаписи, ИдентификаторФормы, ВРежимеИмпортаДанных = Ложь)

	Если ВРежимеИмпортаДанных Тогда
		ПостфиксКлючаЗадания = ИдентификаторФормы;
	Иначе
		ПостфиксКлючаЗадания = ИдентификаторУчетнойЗаписи;
	КонецЕсли;
	
	КлючЗадания = ИнтеграцияСМаркетплейсамиКлиентСервер.КлючЗаданияИмпортаТоварногоКаталога(ПостфиксКлючаЗадания);
	
	Возврат КлючЗадания;

КонецФункции

&НаСервереБезКонтекста
Процедура ОбновитьПодсказкиИДоступностьИмпорта(ГруппаКоманд, ГруппаПодсказок, РезультатПроверкиИмпорта,
	ЗаписьПослеСопоставления = Ложь)

	ГруппаКоманд.Доступность =
		РезультатПроверкиИмпорта.ИспользоватьНоменклатуруПартнеров
		И ЗначениеЗаполнено(РезультатПроверкиИмпорта.Партнер);
	
	ГруппаКоманд.ПодчиненныеЭлементы.ПолучитьТоварыИзСервиса.Доступность =
		ГруппаКоманд.Доступность
		И Не РезультатПроверкиИмпорта.ЗаданиеАктивно
		И Не ЗаписьПослеСопоставления;
	
	ГруппаКоманд.ПодчиненныеЭлементы.СопоставитьДанные.Доступность =
		ГруппаКоманд.Доступность
		И РезультатПроверкиИмпорта.ТребуютСопоставления
		И Не ЗаписьПослеСопоставления;
	
	ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаИмпортТоваровНеИспользуется.Видимость =
		Не РезультатПроверкиИмпорта.ИспользоватьНоменклатуруПартнеров;
	
	ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаНеЗаполненПартнер.Видимость =
		РезультатПроверкиИмпорта.ИспользоватьНоменклатуруПартнеров
		И Не ЗначениеЗаполнено(РезультатПроверкиИмпорта.Партнер)
		И Не ЗаписьПослеСопоставления;
	
	ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаИмпортТоваровДлительноеОжидание.Видимость =
		РезультатПроверкиИмпорта.ИспользоватьНоменклатуруПартнеров
		И РезультатПроверкиИмпорта.ЗаданиеАктивно
		И Не ЗаписьПослеСопоставления;
	
	ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаИмпортТоваровЗавершен.Видимость =
		РезультатПроверкиИмпорта.ИспользоватьНоменклатуруПартнеров
		И ЗначениеЗаполнено(РезультатПроверкиИмпорта.ЗаданиеЗавершено)
		И Не РезультатПроверкиИмпорта.ЗаданиеАктивно
		И Не ЗаписьПослеСопоставления;
	
	ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаЗаписьТоваровДлительноеОжидание.Видимость =
		ЗаписьПослеСопоставления;
	
	Если Не ЗаписьПослеСопоставления Тогда
		ГруппаПодсказок.ПодчиненныеЭлементы.ПодсказкаИмпортТоваровЗавершен.Заголовок =
			ИнтеграцияСМаркетплейсамиВызовСервера.ПредставлениеРезультатаВыполненияЗаданияИмпортаКаталога(
				РезультатПроверкиИмпорта.ЗаданиеЗавершено);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьРезультатПроверкиИмпорта(РезультатПроверкиИмпорта, УчетнаяЗапись, ИдентификаторФормы,
			ВРежимеИмпортаДанных = Ложь)

	Если РезультатПроверкиИмпорта <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗадания = КлючЗаданияИмпортаТоварногоКаталога(
		УчетнаяЗапись.УникальныйИдентификатор(),
		ИдентификаторФормы,
		ВРежимеИмпортаДанных);
	
	ПараметрыПроверки = ИнтеграцияСМаркетплейсамиСервер.НовыеПараметрыПроверкиНаличияДанныхИЗапускаИмпортаКаталога();
	ПараметрыПроверки.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыПроверки.КлючЗадания   = КлючЗадания;
	
	РезультатПроверкиИмпорта =
		ИнтеграцияСМаркетплейсамиСервер.ПроверитьНаличиеДанныхИЗапускИмпортаКаталога(ПараметрыПроверки);

КонецПроцедуры

#КонецОбласти

#Область СопоставлениНоменклатурыСлужебный

&НаКлиенте
Процедура ОбработатьСопоставлениеНоменклатуры(РезультатСопоставления, ДополнительныеПараметры) Экспорт
	
	ЗапуститьОбновлениеДанныхСтатусовПубликацииПоНоменклатуреПартнера();
	
	Если РезультатСопоставления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповестить("Ozon_ЗавершеноСопоставлениеНоменклатуры",, УчетнаяЗаписьМаркетплейса);

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОбновлениеДанныхСтатусовПубликацииПоНоменклатуреПартнера()

	ОбновитьПодсказкиИДоступностьСопоставления(Истина);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Параметры.ВРежимеИмпортаДанных;
	
	ДополнительныеПараметры = ИнтеграцияСМаркетплейсомOzonКлиент.НовоеОписаниеПараметровДействия();
	
	ДополнительныеПараметры.ОписаниеДействия =
		НСтр("ru = 'Обновление товарного каталога';
			|en = 'Update the goods directory'");
	
	ДополнительныеПараметры.УспешныйРезультат =
		НСтр("ru = 'Завершено обновление импортированных товаров после сопоставления данных';
			|en = 'The imported goods are updated after mapping data'");
	
	ДополнительныеПараметры.РезультатСОшибками =
		НСтр("ru = 'При обновлении импортированных товаров после сопоставления данных возникли ошибки';
			|en = 'Errors occurred when updating imported goods after mapping data'");
	
	ПоказатьОповещениеПользователя(ДополнительныеПараметры.ОписаниеДействия,,
		НСтр("ru = 'Запущено фоновое обновление импортированных товаров после сопоставления данных.';
			|en = 'Background imported goods update after mapping data is started.'"),
		БиблиотекаКартинок.ЛоготипOzon2);
	
	ДлительнаяОперация = ДлительнаяОперацияОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера(
		УчетнаяЗаписьМаркетплейса, Элементы.Товары.ВыделенныеСтроки, УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект, ДополнительныеПараметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Процедура ОбновитьПодсказкиИДоступностьСопоставления(ЗаписьПослеСопоставления = Ложь)

	ОбновитьПодсказкиИДоступностьИмпорта(
		Элементы.КомандыРаботыСИмпортомТоваров,
		Элементы.ГруппаДлительноеОжиданиеИмпорт,
		РезультатПроверкиИмпорта,
		ЗаписьПослеСопоставления);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДлительнаяОперацияОбновленияДанныхСтатусовПубликацииПоНоменклатуреПартнера(УчетнаяЗаписьМаркетплейса,
			Знач ВыделенныеСтроки, УникальныйИдентификатор)
	
	МассивНоменклатурыПартнера = Новый Массив;
	
	Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
		МассивНоменклатурыПартнера.Добавить(КлючЗаписи.НоменклатураПартнера);
	КонецЦикла;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Обновление данных импортированных товаров.';
															|en = 'Ozon. Update imported goods data.'");
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.ОбновитьДанныеСтатусовПубликацииПоНоменклатуреПартнера";
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, ИмяМетода, УчетнаяЗаписьМаркетплейса, МассивНоменклатурыПартнера);
	
КонецФункции

#КонецОбласти

#КонецОбласти
