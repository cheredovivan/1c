#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция возвращает информацию о количестве документов по учету НДС к обработке по организации за период.
//
// Параметры:
//	ПараметрыЗапроса - Структура вида "МассивОрганизаций, НачалоПериода, КонецПериода"
//	СтруктураВидимостиЭлементов - Структура, определяет - формировать ли запрос по документу. 
// 									См. ПолучитьСтруктуруВидимостиЭлементов
//	ТолькоОбязательныеОперации - Булево, Отбирать для анализа только обязательыне операции (требуется для 
//									индикации состояния в помощнике закрытия месяца)
// Возвращаемое значение:
//	ТаблицаЗначений - Таблица документов в обработке и уже сформированных (Документ, КоличествоКОформлению, КоличествоОформленных).
//
Функция СформироватьЗапросПоДокументамКОформлению(ПараметрыЗапроса, СтруктураВидимостиЭлементов = Неопределено, ТолькоОбязательныеОперации = Ложь) Экспорт
	
	Если СтруктураВидимостиЭлементов = Неопределено Тогда
		СтруктураВидимостиЭлементов = ВидимостьЭлементов(ПараметрыЗапроса.МассивОрганизаций, ПараметрыЗапроса.НачалоПериода);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ПараметрыЗапроса.КонецПериода = Дата('209901010000');
	КонецЕсли;
	
	МассивОрганизаций = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ПараметрыЗапроса.МассивОрганизаций);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
		МассивОрганизаций,
		Справочники.Организации.УправленческаяОрганизация);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОрганизаций",	МассивОрганизаций);
	Запрос.УстановитьПараметр("НачалоПериода",		ПараметрыЗапроса.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",		ПараметрыЗапроса.КонецПериода);
	
	ТекстыЗапросов = Новый Массив;
	
	ПараметрыЗапроса.Вставить("ТолькоПеревыставление", Истина);
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, 
		"СчетФактураВыданный", "ПеревыставлениеКомитенту", "И СчетФактура.Перевыставленный"));
	ПараметрыЗапроса.Вставить("ТолькоОформление", Истина);
	
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураВыданный"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураВыданныйАванс"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураПолученныйАванс"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураКомиссионеру"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "ЗаявлениеОВвозеТоваров"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураНалоговыйАгент"));
	ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураПолученныйНалоговыйАгент"));
	
	Если ТолькоОбязательныеОперации = Ложь Тогда
		
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураПолученный"));
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураКомитента"));
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "ТаможеннаяДекларацияИмпорт"));
		//++ НЕ УТ
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "ТаможеннаяДекларацияЭкспорт"));
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "ЗаявлениеОВвозеТоваровПолученное"));
		//-- НЕ УТ
		ТекстыЗапросов.Добавить(ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, ПараметрыЗапроса, "СчетФактураНаНеподтвержденнуюРеализацию0"));
		
	КонецЕсли;
	
	Запрос.Текст = СтрСоединить(ТекстыЗапросов, РазделительЗапросов());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция СформироватьЗапросПоПрочимОперациям(ПараметрыЗапроса) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыЗапроса.КонецПериода) Тогда
		ПараметрыЗапроса.КонецПериода = Дата('209901010000');
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.ЗаписьКнигиПродаж КАК ЗаписьКнигиПродаж
	|ГДЕ
	|	ЗаписьКнигиПродаж.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаписьКнигиПродаж.Организация В (&МассивОрганизаций)
	|	И ЗаписьКнигиПродаж.Проведен
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.ЗаписьКнигиПокупок КАК ЗаписьКнигиПокупок
	|ГДЕ
	|	ЗаписьКнигиПокупок.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И ЗаписьКнигиПокупок.Организация В (&МассивОрганизаций)
	|	И ЗаписьКнигиПокупок.Проведен
	|;
	|
	|ВЫБРАТЬ
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата) КАК ДатаПоследнегоДокумента
	|ПОМЕСТИТЬ ДатыПоследнихОперацийПоБлокировке
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
	|ГДЕ
	|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
	|	И БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата <= &КонецПериода
	|	И БлокировкаВычетаНДССчетаФактуры.Ссылка.Организация В (&МассивОрганизаций)
	|СГРУППИРОВАТЬ ПО
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	ДатаПоследнегоДокумента
	|;
	|
	|ВЫБРАТЬ
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура КАК СчетФактура,
	|	МАКСИМУМ(БлокировкаВычетаНДССчетаФактуры.Ссылка) КАК Ссылка
	|ПОМЕСТИТЬ ВТ_СрезПоследнихДокументовБлокировки
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК БлокировкаВычетаНДССчетаФактуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДатыПоследнихОперацийПоБлокировке
	|		ПО ДатыПоследнихОперацийПоБлокировке.СчетФактура = БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|			И ДатыПоследнихОперацийПоБлокировке.ДатаПоследнегоДокумента = БлокировкаВычетаНДССчетаФактуры.Ссылка.Дата
	|ГДЕ
	|	НЕ БлокировкаВычетаНДССчетаФактуры.Ссылка.ПометкаУдаления
	|СГРУППИРОВАТЬ ПО
	|	БлокировкаВычетаНДССчетаФактуры.СчетФактура
	|ИНДЕКСИРОВАТЬ ПО
	|	СчетФактура,
	|	Ссылка
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.БлокировкаВычетаНДС.СчетаФактуры КАК ЗаблокированныеСФ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СрезПоследнихДокументовБлокировки КАК ВТ_СрезПоследнихДокументовБлокировки
	|		ПО ЗаблокированныеСФ.СчетФактура = ВТ_СрезПоследнихДокументовБлокировки.СчетФактура
	|			И ЗаблокированныеСФ.Ссылка = ВТ_СрезПоследнихДокументовБлокировки.Ссылка
	|ГДЕ
	|	(ЗаблокированныеСФ.СрокБлокировки = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ИЛИ ЗаблокированныеСФ.СрокБлокировки > &КонецПериода)
	|	И ЗаблокированныеСФ.Ссылка.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияБлокировкиВычетаНДС.Установлена)
	|;
	|
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Документ.СписаниеНДСнаРасходы КАК СписаниеНДСнаРасходы
	|ГДЕ
	|	СписаниеНДСнаРасходы.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И СписаниеНДСнаРасходы.Организация В (&МассивОрганизаций)
	|	И СписаниеНДСнаРасходы.Проведен
	|";
	
	Запрос.УстановитьПараметр("НачалоПериода",		ПараметрыЗапроса.НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода",		ПараметрыЗапроса.КонецПериода);
	Запрос.УстановитьПараметр("МассивОрганизаций",  ПараметрыЗапроса.МассивОрганизаций);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	Результат.Вставить("ЗаписиКнигиПродаж",   0);
	Результат.Вставить("ЗаписиКнигиПокупок",  0);
	Результат.Вставить("БлокировкиВычетаНДС", 0);
	Результат.Вставить("СписанияНДСнаРасходы", 0);
	
	Выборка = РезультатЗапроса[0].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("ЗаписиКнигиПродаж", Выборка.Количество);
	КонецЕсли;
	
	Выборка = РезультатЗапроса[1].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("ЗаписиКнигиПокупок", Выборка.Количество);
	КонецЕсли;
	
	Выборка = РезультатЗапроса[4].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("БлокировкиВычетаНДС", Выборка.Количество);
	КонецЕсли;

	Выборка = РезультатЗапроса[5].Выбрать();
	Если Выборка.Следующий() Тогда
		Результат.Вставить("СписанияНДСнаРасходы", Выборка.Количество);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Функция возвращает информацию о наличии документов по учету НДС к обработке по организации за период.
//
// Параметры:
//	ПараметрыЗапроса - Структура :
//	* МассивОрганизаций - Массив из СправочникСсылка.Организации - массив организаций.
//	* НачалоПериода - Дата.
//	* КонецПериода - Дата.
//
// Возвращаемое значение:
//	Массив из Структура :
//	* Организация - СправочникСсылка.Организации.
//	* ЕстьОперацииКВыполнению - Булево.
//	* ЕстьОбязательныеОперацииКВыполнению - Булево.
//	* ЕстьСчетФактураПолученныйАвансОшибкиСумм - Булево.
//	* ЕстьСчетФактураВыданныйАвансОшибкиСумм - Булево.
//
Функция ОперацииПоНДСКВыполнению(ПараметрыЗапроса) Экспорт
	
	МассивОрганизаций = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыЗапроса.МассивОрганизаций);
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(
		МассивОрганизаций,
		Справочники.Организации.УправленческаяОрганизация);
	
	ПорядокДокументов = Новый Массив;
	
	ПорядокДокументов.Добавить("СчетФактураВыданный");
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах") Тогда
		ПорядокДокументов.Добавить("СчетФактураКомиссионеру");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки") Тогда
		ПорядокДокументов.Добавить("ЗаявлениеОВвозеТоваров");
	КонецЕсли;
	ПорядокДокументов.Добавить("СчетФактураНалоговыйАгент");
	Если ПолучитьФункциональнуюОпцию("ПокупкаТоваровОблагаемыхНДСУПокупателя") Тогда
		ПорядокДокументов.Добавить("СчетФактураПолученныйНалоговыйАгент");
	КонецЕсли;
	ОрганизацииИспользующиеСчетФактурыВыданныеАванс = Новый Массив;       
	ОрганизацииИспользующиеСчетФактурыПолученныеАванс = Новый Массив;     
	ПокупкаТоваровОблагаемыхНДСУПокупателя = ПолучитьФункциональнуюОпцию("ПокупкаТоваровОблагаемыхНДСУПокупателя");
	Для Каждого Организация Из МассивОрганизаций Цикл    
		ЭтоПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, ПараметрыЗапроса.НачалоПериода);
		Если ЭтоПлательщикНДС Тогда
			ОрганизацииИспользующиеСчетФактурыВыданныеАванс.Добавить(Организация);      
		КонецЕсли;   
		Если ПокупкаТоваровОблагаемыхНДСУПокупателя Тогда
			ОрганизацииИспользующиеСчетФактурыПолученныеАванс.Добавить(Организация);	
		ИначеЕсли ЭтоПлательщикНДС Тогда
			ПараметрыУчетаОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Организация, ПараметрыЗапроса.НачалоПериода);
			Если не ПараметрыУчетаОрганизации.ОсновноеНалогообложениеНДСПродажи = 
			Перечисления.ТипыНалогообложенияНДС.ЛьготноеНалогообложениеНДСНаУСН Тогда
				ОрганизацииИспользующиеСчетФактурыПолученныеАванс.Добавить(Организация);	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ОрганизацииИспользующиеСчетФактурыВыданныеАванс.Количество() > 0 Тогда
		ПорядокДокументов.Добавить("СчетФактураВыданныйАванс");
	КонецЕсли;
	Если ОрганизацииИспользующиеСчетФактурыПолученныеАванс.Количество() > 0 Тогда
		ПорядокДокументов.Добавить("СчетФактураПолученныйАванс");
	КонецЕсли;
	
	РезультатыПоОрганизациям = Новый Массив;
	
	Параметры = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыЗапроса);
	Параметры.Вставить("ТолькоОформление", Истина);
	Параметры.Вставить("ПоОрганизациям", Истина);
	Для Каждого ИмяДокумента Из ПорядокДокументов Цикл
		Если МассивОрганизаций.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если ИмяДокумента = "СчетФактураВыданныйАванс" Тогда
			Если ОрганизацииИспользующиеСчетФактурыВыданныеАванс.Количество() > 0 Тогда
				ОставшиесяОрганизации = Новый Массив;
				Для Каждого ОрганизацияИспользующаяСчетФактуры Из ОрганизацииИспользующиеСчетФактурыВыданныеАванс Цикл
					Если не МассивОрганизаций.Найти(ОрганизацияИспользующаяСчетФактуры) = Неопределено Тогда
						ОставшиесяОрганизации.Добавить(ОрганизацияИспользующаяСчетФактуры);
					КонецЕсли;
				КонецЦикла;
				ОрганизацииИспользующиеСчетФактурыВыданныеАванс.Очистить(); 
				Если ОставшиесяОрганизации.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				МассивОрганизаций = ОставшиесяОрганизации;
			КонецЕсли;	
			Параметры.Вставить("ВернутьПолнуюТаблицу");
			Параметры.Вставить("ИсключитьИУДА");
		КонецЕсли;
		Если ИмяДокумента = "СчетФактураПолученныйАванс" Тогда
			Если ОрганизацииИспользующиеСчетФактурыПолученныеАванс.Количество() > 0 Тогда
				ОставшиесяОрганизации = Новый Массив;
				Для Каждого ОрганизацияИспользующаяСчетФактуры Из ОрганизацииИспользующиеСчетФактурыПолученныеАванс Цикл
					Если не МассивОрганизаций.Найти(ОрганизацияИспользующаяСчетФактуры) = Неопределено Тогда
						ОставшиесяОрганизации.Добавить(ОрганизацияИспользующаяСчетФактуры);
					КонецЕсли;
				КонецЦикла;
				ОрганизацииИспользующиеСчетФактурыПолученныеАванс.Очистить(); 
				Если ОставшиесяОрганизации.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				МассивОрганизаций = ОставшиесяОрганизации;
			КонецЕсли;	
			Параметры.Вставить("ВернутьПолнуюТаблицу");
			Параметры.Вставить("ИсключитьИУДА");
		КонецЕсли;
		
		Параметры.МассивОрганизаций = МассивОрганизаций;
		
		ОрганизацииКОформлению = Документы[ИмяДокумента].ЕстьСчетаФактурыКОформлению(Параметры);

		Если (ИмяДокумента = "СчетФактураВыданныйАванс" ИЛИ ИмяДокумента = "СчетФактураПолученныйАванс")
			И ТипЗнч(ОрганизацииКОформлению) = Тип("ТаблицаЗначений") Тогда
			ОрганизацииКОформлению.Индексы.Добавить("Организация"); 
			Для Каждого ОрганизацияКОформлению Из МассивОрганизаций Цикл
				СтруктураОтбора = Новый Структура;
				СтруктураОтбора.Вставить("Организация", ОрганизацияКОформлению);
				СтруктураОтбора.Вставить("СФСформирован", Ложь);
				КОформлению = ОрганизацииКОформлению.НайтиСтроки(СтруктураОтбора);
				Если КОформлению.Количество() > 0 Тогда
					ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МассивОрганизаций,ОрганизацияКОформлению);
	       			Результат = Новый Структура;
					Результат.Вставить("Организация", ОрганизацияКОформлению);
					Результат.Вставить("ЕстьОперацииКВыполнению", Истина);
					Результат.Вставить("ЕстьОбязательныеОперацииКВыполнению", (ИмяДокумента = "СчетФактураВыданныйАванс"));
					Результат.Вставить("ЕстьСчетФактураПолученныйАвансОшибкиСумм", Ложь);
					Результат.Вставить("ЕстьСчетФактураВыданныйАвансОшибкиСумм", Ложь);   
					
					РезультатыПоОрганизациям.Добавить(Результат); 
				Иначе
					СтруктураОтбора = Новый Структура;
					СтруктураОтбора.Вставить("Организация", ОрганизацияКОформлению);
					КОформлению = ОрганизацииКОформлению.НайтиСтроки(СтруктураОтбора);
 					Если КОформлению.Количество() > 0 Тогда
						
						ПоляГруппировки = "ДокументОснование, СФСформирован, СуммаСчетаФактуры";
						Если ИмяДокумента = "СчетФактураВыданныйАванс" Тогда
							ПолеСуммирования = "Сумма";
						Иначе
							ПолеСуммирования = "СуммаАвансаРегл";
						КонецЕсли;
						
						ЕстьДокументыСОшибкойСумм = Ложь;
						ДокументыПоОрганизацииКОформлению = ОрганизацииКОформлению.Скопировать(КОформлению);
						ДокументыПоОрганизацииКОформлению.Свернуть(ПоляГруппировки, ПолеСуммирования);
						ДокументыПоОрганизацииКОформлению.Колонки.Добавить("ОшибкаСумм");
						Для Каждого Строка Из ДокументыПоОрганизацииКОформлению Цикл
							Если Строка.СФСформирован = Истина И Строка[ПолеСуммирования] <> Строка.СуммаСчетаФактуры Тогда
								ЕстьДокументыСОшибкойСумм = Истина;
							КонецЕсли;
						КонецЦикла;
						
						Если ЕстьДокументыСОшибкойСумм Тогда
							ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МассивОрганизаций,ОрганизацияКОформлению);
			       			Результат = Новый Структура;
							Результат.Вставить("Организация", ОрганизацияКОформлению);
							Результат.Вставить("ЕстьОперацииКВыполнению", Истина);
							Результат.Вставить("ЕстьОбязательныеОперацииКВыполнению", Ложь);
							Результат.Вставить("ЕстьСчетФактураПолученныйАвансОшибкиСумм", (ИмяДокумента = "СчетФактураПолученныйАванс"));
							Результат.Вставить("ЕстьСчетФактураВыданныйАвансОшибкиСумм", (ИмяДокумента = "СчетФактураВыданныйАванс"));   
							
							РезультатыПоОрганизациям.Добавить(Результат);   
						КонецЕсли;
					КонецЕсли;	
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЗнч(ОрганизацииКОформлению) = Тип("Массив") Тогда
			Для Каждого ОрганизацияКОформлению Из ОрганизацииКОформлению Цикл
				ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МассивОрганизаций,ОрганизацияКОформлению);
				Результат = Новый Структура;
				Результат.Вставить("Организация", ОрганизацияКОформлению);
				Результат.Вставить("ЕстьОперацииКВыполнению", Истина);
				Результат.Вставить("ЕстьОбязательныеОперацииКВыполнению", Истина);
				Результат.Вставить("ЕстьСчетФактураПолученныйАвансОшибкиСумм", Ложь);
				Результат.Вставить("ЕстьСчетФактураВыданныйАвансОшибкиСумм", Ложь);
				
				РезультатыПоОрганизациям.Добавить(Результат);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Организация Из МассивОрганизаций Цикл	
		Результат = Новый Структура;
		Результат.Вставить("Организация", Организация);
		Результат.Вставить("ЕстьОперацииКВыполнению", Ложь);
		Результат.Вставить("ЕстьОбязательныеОперацииКВыполнению", Ложь);
		Результат.Вставить("ЕстьСчетФактураПолученныйАвансОшибкиСумм", Ложь);
		Результат.Вставить("ЕстьСчетФактураВыданныйАвансОшибкиСумм", Ложь);

	КонецЦикла;

	Возврат РезультатыПоОрганизациям;
	
КонецФункции

Процедура ОпределитьСтатусРегламентныхЗаданийФоновоеЗадание(Параметры, АдресХранилища) Экспорт
	
	СостояниеЭтапов = ЗакрытиеМесяцаСервер.ОпределитьСостояниеЭтаповРасчета(
			Параметры.ЭтапыЗакрытияМесяца,
			Параметры.Период,
			Параметры.Организация);
	
	ПоместитьВоВременноеХранилище(СостояниеЭтапов, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#Область ТекстыЗапросов

Функция ТекстЗапросаСчетФактура(СтруктураВидимостиЭлементов, Параметры, ИмяИсточника, ВидИсточника = "", ОтборВидаИсточника = "")
	
	Если ВидИсточника = "" Тогда
		ВидИсточника = ИмяИсточника;
	КонецЕсли;
	
	Если ИмяИсточника = "СчетФактураВыданныйАванс" ИЛИ ИмяИсточника = "СчетФактураПолученныйАванс" Тогда
		Параметры.Вставить("ВернутьПолнуюТаблицу");
		Параметры.Вставить("ИсключитьИУДА");
	КонецЕсли;
	
	Текст = "";

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ВидИсточника КАК Документ,
	|	&КоличествоКОформлению КАК КоличествоКОформлению,
	|	КОЛИЧЕСТВО(СчетФактура.Ссылка) КАК КоличествоОформленных
	|ИЗ
	|	Документ.СчетФактураВыданный КАК СчетФактура
	|ГДЕ
	|	СчетФактура.Проведен
	|	И СчетФактура.Организация В (&МассивОрганизаций)
	|	И СчетФактура.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И &ОтборВидаИсточника
	|
	|";
	
	Если СтруктураВидимостиЭлементов[ИмяИсточника] Тогда
		
		Результат = Документы[ИмяИсточника].ЕстьСчетаФактурыКОформлению(Параметры);
		
		Если (ИмяИсточника = "СчетФактураВыданныйАванс" ИЛИ ИмяИсточника = "СчетФактураПолученныйАванс") 
				И ТипЗнч(Результат) = Тип("ТаблицаЗначений")Тогда 

			КоличествоДокументов = Результат.НайтиСтроки(Новый Структура("СФСформирован", Ложь)).Количество();
			
			ПоляГруппировки = "ДокументОснование, СФСформирован, СуммаСчетаФактуры";
			Если ИмяИсточника = "СчетФактураВыданныйАванс" Тогда
				ПолеСуммирования = "Сумма";
			Иначе
				ПолеСуммирования = "СуммаАвансаРегл";
			КонецЕсли;
			
			КоличествоДокументовСОшибкойСумм = 0;
			Результат.Свернуть(ПоляГруппировки, ПолеСуммирования);
			Результат.Колонки.Добавить("ОшибкаСумм");
			Для Каждого Строка Из Результат Цикл
				Если Строка.СФСформирован = Истина И Строка[ПолеСуммирования] <> Строка.СуммаСчетаФактуры Тогда
					СтрокиОшибкиСуммы = Результат.НайтиСтроки(Новый Структура("ДокументОснование", Строка.ДокументОснование));
					Для Каждого СтрокаОшибки Из СтрокиОшибкиСуммы Цикл
						СтрокаОшибки.ОшибкаСумм = Истина;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
			КоличествоДокументовСОшибкойСумм = Результат.НайтиСтроки(Новый Структура("ОшибкаСумм", Истина)).Количество();

			Текст =
			"
			|ВЫБРАТЬ
			|	&ВидИсточника КАК Документ,
			|	&КоличествоКОформлению КАК КоличествоКОформлению,
			|	КОЛИЧЕСТВО(СчетФактура.Ссылка) КАК КоличествоОформленных
			|ИЗ
			|	Документ.СчетФактураВыданный КАК СчетФактура
			|ГДЕ
			|	СчетФактура.Проведен
			|	И СчетФактура.Организация В (&МассивОрганизаций)
			|	И СчетФактура.Дата МЕЖДУ &НачалоПериода И &КонецПериода
			|	И &ОтборВидаИсточника
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
			
			Если ИмяИсточника = "СчетФактураВыданныйАванс" Тогда
				ВидИсточникаОшибокСумм = "СчетФактураВыданныйАвансОшибкиСумм";
			Иначе 
				ВидИсточникаОшибокСумм = "СчетФактураПолученныйАвансОшибкиСумм";
			КонецЕсли; 
			
			Текст = СтрЗаменить(Текст, "СчетФактураВыданный", ИмяИсточника);
			Текст = СтрЗаменить(Текст, "&КоличествоКОформлению", Формат(КоличествоДокументовСОшибкойСумм, "ЧН=; ЧГ=0"));
			Текст = СтрЗаменить(Текст, "&ВидИсточника", """" + ВидИсточникаОшибокСумм + """");
			Текст = СтрЗаменить(Текст, "И &ОтборВидаИсточника", ОтборВидаИсточника); 
			
		Иначе
			КоличествоДокументов = Результат;
		КонецЕсли;
	Иначе
		КоличествоДокументов = 0;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "СчетФактураВыданный", ИмяИсточника);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоКОформлению", Формат(КоличествоДокументов, "ЧН=; ЧГ=0"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВидИсточника", """" + ВидИсточника + """");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ОтборВидаИсточника", ОтборВидаИсточника);
	
	ТекстЗапроса = Текст + ТекстЗапроса;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция РазделительЗапросов()
	Возврат 
		"
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|";
КонецФункции

#КонецОбласти
	
#Область Прочее

Функция ВидимостьЭлементов(Знач Организация, Период) Экспорт
	
	ПлательщикНДС = УчетнаяПолитика.ПлательщикНДС(Организация, Период); 
	
	Если ТипЗнч(Организация) = Тип("СписокЗначений") Тогда
		Организация = Организация.ВыгрузитьЗначения();
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("Массив") И Организация.Количество() = 1 Тогда
		 Организация = Организация.Получить(0);
	КонецЕсли;
	
	Если ТипЗнч(Организация) = Тип("СправочникСсылка.Организации") Тогда 
		ПараметрыУчетаОрганизации = УчетНДСУП.ПараметрыУчетаПоОрганизации(Организация, Период);
		ПрименяетсяЛьготноеНалогообложениеНДС = ПараметрыУчетаОрганизации.ОсновноеНалогообложениеНДСПродажи = Перечисления.ТипыНалогообложенияНДС.ЛьготноеНалогообложениеНДСНаУСН;
	Иначе
		ПрименяетсяЛьготноеНалогообложениеНДС =  Ложь;
	КонецЕсли;
	
	СтруктураВидимостиЭлементов = Новый Структура;
	СтруктураВидимостиЭлементов.Вставить("СчетФактураВыданный",			Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураПолученный",		Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураВыданныйАванс",	ПлательщикНДС);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураПолученныйАванс",	ПлательщикНДС И НЕ ПрименяетсяЛьготноеНалогообложениеНДС
																			Или ПолучитьФункциональнуюОпцию("ПокупкаТоваровОблагаемыхНДСУПокупателя"));
	СтруктураВидимостиЭлементов.Вставить("СчетФактураНалоговыйАгент",	Истина);
	СтруктураВидимостиЭлементов.Вставить("СчетФактураКомитента",		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриЗакупках"));
	СтруктураВидимостиЭлементов.Вставить("СчетФактураКомиссионеру",		ПолучитьФункциональнуюОпцию("ИспользоватьКомиссиюПриПродажах"));
	СтруктураВидимостиЭлементов.Вставить("ЗаявлениеОВвозеТоваров",		ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки"));
	СтруктураВидимостиЭлементов.Вставить("ПодтвержденияОплатыВБюджет",	ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки"));
	СтруктураВидимостиЭлементов.Вставить("ТаможеннаяДекларацияИмпорт",	ПолучитьФункциональнуюОпцию("ИспользоватьИмпортныеЗакупки"));
	
	СтруктураВидимостиЭлементов.Вставить("СчетФактураПолученныйНалоговыйАгент", ПолучитьФункциональнуюОпцию("ПокупкаТоваровОблагаемыхНДСУПокупателя"));
	
	СтруктураВидимостиЭлементов.Вставить("ФормированиеЗаписейРаздела7",			ПолучитьФункциональнуюОпцию("ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС"));
	СтруктураВидимостиЭлементов.Вставить("РеестрДокументовПодтверждающихЛьготу",ПолучитьФункциональнуюОпцию("ИспользоватьЗаполнениеРаздела7ДекларацииПоНДС"));
	
	ИспользоватьПродажиНаЭкспорт = ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспортНесырьевыхТоваров")
						ИЛИ ПолучитьФункциональнуюОпцию("ИспользоватьПродажиНаЭкспортСырьевыхТоваровУслуг"); 
	СтруктураВидимостиЭлементов.Вставить("СчетФактураНаНеподтвержденнуюРеализацию0", ИспользоватьПродажиНаЭкспорт);
	
	Если ПолучитьФункциональнуюОпцию("УправлениеТорговлей") Тогда
		СтруктураВидимостиЭлементов.Вставить("ТаможеннаяДекларацияЭкспорт", Ложь);
	//++ НЕ УТ
	Иначе
		СтруктураВидимостиЭлементов.Вставить("ТаможеннаяДекларацияЭкспорт", ПолучитьФункциональнуюОпцию("ВестиУчетТаможенныхДекларацийНаЭкспорт"));
		СтруктураВидимостиЭлементов.Вставить("ЗаявлениеОВвозеТоваровПолученное", ИспользоватьПродажиНаЭкспорт);
	//-- НЕ УТ
	КонецЕсли;
	
	Возврат СтруктураВидимостиЭлементов;
	
КонецФункции

#КонецОбласти

#Область ФормированиеГиперссылкиВЖурналеДокументыПродажи

Функция СформироватьГиперссылкуСмТакжеВРаботе(Параметры) Экспорт
	
	Если НЕ ПравоДоступа("Просмотр", Метаданные.Обработки.ПомощникПоУчетуНДС)
		ИЛИ ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекстГиперссылки = НСтр("ru = 'Помощник по учету НДС';
							|en = 'VAT accounting wizard'");
	
	Возврат Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяФормыРабочееМесто());
	
КонецФункции

Функция ИмяФормыРабочееМесто() Экспорт
	
	Возврат "Обработка.ПомощникПоУчетуНДС.Форма.Форма";
	
КонецФункции

#КонецОбласти

#КонецЕсли