
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Возврат при получении формы для анализа.
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеРегистраРаспоряжениеНаОтгрузкуКВыполнениюЗавершено = Не Константы.УдалитьРаспоряженияНаОтгрузкуКВыполнениюНеЗаполнен.Получить(); 
	Если Не ОбновлениеРегистраРаспоряжениеНаОтгрузкуКВыполнениюЗавершено Тогда
		СписокРаспоряженияНаОформление.ТекстЗапроса = ТекстЗапросаОстаткиЗаказовКлиентов();
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокФормы) Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	// Получение ФО
	ИспользоватьПартнеровИКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов");
	ИспользоватьНесколькоСкладов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	ИспользоватьРасширенныеВозможностиЗаказаКлиента = ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента");
	ИспользоватьРеализациюПоНесколькимЗаказам = ПолучитьФункциональнуюОпцию("ИспользоватьРеализациюПоНесколькимЗаказам");
	ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам = ПолучитьФункциональнуюОпцию("ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам");
	ИспользоватьОрдернуюСхемуПриОтгрузке = ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриОтгрузке");
	ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5 =
		ПолучитьФункциональнуюОпцию("ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5");
	
	Элементы.СтраницаКПереходуПраваСобственности.Видимость = ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5;
	Если ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5 = Ложь Тогда
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	// Установка отборов.
	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("Организация",        Организация);
		СтруктураБыстрогоОтбора.Свойство("Склад",              Склад);
		СтруктураБыстрогоОтбора.Свойство("СостояниеНакладной", СостояниеНакладной);
		СтруктураБыстрогоОтбора.Свойство("СостояниеОрдера",    СостояниеОрдера);
	КонецЕсли;
	
	Если Не ИспользоватьНесколькоСкладов Тогда
		Если Не ЗначениеЗаполнено(Склад) Тогда
			Склад = Справочники.Склады.СкладПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = ТекстЗапросаСпискаРаспоряженийНаОформление();
	СвойстваСписка.ОсновнаяТаблица = "";
	СвойстваСписка.ДинамическоеСчитываниеДанных = Неопределено;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокРаспоряженияНаОформление, СвойстваСписка);
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(СписокРаспоряженияНаОформление,
		"СостояниеНакладной",
		?(ЗначениеЗаполнено(СостояниеНакладной), Число(СостояниеНакладной), СостояниеНакладной),
		СтруктураБыстрогоОтбора,,,
		Истина);
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(СписокРаспоряженияНаОформление,
		"СостояниеОрдера",
		?(ЗначениеЗаполнено(СостояниеОрдера), Число(СостояниеОрдера), СостояниеОрдера),
		СтруктураБыстрогоОтбора,,,
		Истина);
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриСозданииНаСервере(СписокРаспоряженияНаОформление,
		"Организация",
		Организация,
		СтруктураБыстрогоОтбора);

	УстановитьПараметрыОрганизацииИСкладаНаСервере();
		
	Элементы.Склад.Видимость = ИспользоватьНесколькоСкладов;
	
	Элементы.СписокРаспоряженияНаОформлениеСостояниеОрдера.Видимость = ИспользоватьОрдернуюСхемуПриОтгрузке;
	Элементы.СписокРаспоряженияНаОформлениеСостояниеНакладной.Видимость = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	Элементы.СостояниеОрдера.Видимость = ИспользоватьОрдернуюСхемуПриОтгрузке;
	Элементы.СостояниеНакладной.Видимость = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СписокРаспоряженияНаОформлениеОрганизация",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СписокРаспоряженияНаОформлениеСклад",
		"Видимость",
		ИспользоватьНесколькоСкладов);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СписокРаспоряженияНаОформлениеСделка",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьСделкиСКлиентами"));
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СписокРаспоряженияНаОформлениеВалюта",
		"Видимость",
		ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоВалют"));
	
	СписокРаспоряженияНаОформление.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	
	ЕстьПравоЧтенияОстатков = ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат)
								И ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыКОтгрузке);
	
	ЕстьПравоДобавленияПередачиТоваров = ПравоДоступа("Добавление", Метаданные.Документы.ПередачаТоваровХранителю);
	
	Элементы.СписокРаспоряженияНаОформление.Видимость = ЕстьПравоЧтенияОстатков;
	
	Элементы.СписокРаспоряженияНаОформлениеОформитьПоРаспоряжениям.Видимость = ЕстьПравоДобавленияПередачиТоваров
																			Или ПравоДоступа("Добавление", Метаданные.Документы.АктВыполненныхРабот)
																			Или ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг);
	Элементы.СписокРаспоряженияНаОформлениеОформитьПоОтгрузке.Видимость = ЕстьПравоДобавленияПередачиТоваров
																			Или ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг);
	Элементы.СписокРаспоряженияНаОформлениеСостояниеВыполнения.Видимость = ПравоДоступа("Просмотр", Метаданные.Отчеты.СостояниеВыполненияДокументов);
	
	Элементы.СписокРаспоряженияНаОформлениеОформитьОтгрузкуПоЗаказам.Видимость = ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5
																				И ПравоДоступа("Добавление", Метаданные.Документы.ОтгрузкаТоваровКлиенту);
																				
	Элементы.СписокРаспоряженияНаОформлениеОформитьОтгрузкуПоОрдеру.Видимость = ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5
																				И ИспользоватьОрдернуюСхемуПриОтгрузке
																				И ПравоДоступа("Добавление", Метаданные.Документы.ОтгрузкаТоваровКлиенту);
	ЗаполнитьСпискиВыбораПоСостояниямОтгрузки();
	
	Элементы.СписокРаспоряженияНаОформлениеКонтрагент.Видимость = ИспользоватьПартнеровИКонтрагентов;
	Элементы.СписокКПереходуПраваСобственностиКонтрагент.Видимость = ИспользоватьПартнеровИКонтрагентов;
	
	Если ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5 Тогда
		ЗаполнитьКомандыПоУсловиюВидимости();
	КонецЕсли;
	
	СформироватьГиперссылкуСмТакжеВРаботе();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_РеализацияТоваровУслуг"
		ИЛИ ИмяСобытия = "Запись_ЗаказКлиента"
		ИЛИ ИмяСобытия = "Запись_ПередачаТоваровХранителю"
		ИЛИ ИмяСобытия = "Запись_ЗаявкаНаВозвратТоваровОтКлиента"
		ИЛИ ИмяСобытия = "Запись_АктВыполненныхРабот"
		ИЛИ ИмяСобытия = "Запись_РасходныйОрдерНаТовары"
		ИЛИ ИмяСобытия = "Запись_ПриходныйКассовыйордер"
		ИЛИ ИмяСобытия = "Принять_ФормаСозданныхДокументов"
		ИЛИ ИмяСобытия = "ОформленДокументПродажи"
		ИЛИ ИмяСобытия = "Запись_ОтгрузкаТоваровКлиенту"
		ИЛИ ИмяСобытия = "ОтменаПроведенияДокументовПродажи" Тогда
		
		Элементы.СписокРаспоряженияНаОформление.Обновить();
		Если ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5 Тогда 
			Элементы.СписокКПереходуПраваСобственности.Обновить();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ОтменитьПакетноеФормирование" Тогда
		ОтменитьПакетноеФормирование = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ЖурналДокументовПродажи.Форма.ПараметрыОформленияДокументовПродажи" Тогда
		Если ВыбранноеЗначение <> Неопределено Тогда
			ПродажиВызовСервера.СохранитьНастройкуКомплектаДокументов(
				ВыбранноеЗначение,
				"Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если СтруктураБыстрогоОтбора <> Неопределено Тогда
		СтруктураБыстрогоОтбора.Свойство("Склад", Склад);
		Настройки.Удалить("Склад");
		СтруктураБыстрогоОтбора.Свойство("СостояниеНакладной", СостояниеНакладной);
		Настройки.Удалить("СостояниеНакладной");
		СтруктураБыстрогоОтбора.Свойство("СостояниеОрдера", СостояниеОрдера);
		Настройки.Удалить("СостояниеОрдера");
		СтруктураБыстрогоОтбора.Свойство("Организация", Организация);
		Настройки.Удалить("Организация");
	Иначе
		Склад              = Настройки.Получить("Склад");
		СостояниеНакладной = Настройки.Получить("СостояниеНакладной");
		СостояниеОрдера    = Настройки.Получить("СостояниеОрдера");
		Организация        = Настройки.Получить("Организация");
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек(СписокРаспоряженияНаОформление,
		"СостояниеНакладной",
		?(ЗначениеЗаполнено(СостояниеНакладной), Число(СостояниеНакладной), СостояниеНакладной),
		СтруктураБыстрогоОтбора, 
		Настройки,
		ЗначениеЗаполнено(СостояниеНакладной),
		,
		Истина);
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек(СписокРаспоряженияНаОформление,
		"СостояниеОрдера",
		?(ЗначениеЗаполнено(СостояниеОрдера), Число(СостояниеОрдера), СостояниеОрдера),
		СтруктураБыстрогоОтбора,
		Настройки,
		ЗначениеЗаполнено(СостояниеОрдера),
		,
		Истина);
	
	ОтборыСписковКлиентСервер.ОтборПоЗначениюСпискаПриЗагрузкеИзНастроек(СписокРаспоряженияНаОформление,
		"Организация",
		Организация,
		СтруктураБыстрогоОтбора,
		Настройки,
		ЗначениеЗаполнено(Организация));
	
	УстановитьПараметрыОрганизацииИСкладаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	СписокРаспоряженияНаОформление.Параметры.УстановитьЗначениеПараметра("Склад", Склад);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРаспоряженияНаОформлениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Распоряжение);
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеНакладнойПриИзменении(Элемент)
	
	Состояние = ?(ЗначениеЗаполнено(СостояниеНакладной), Число(СостояниеНакладной), 0);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРаспоряженияНаОформление,
		"СостояниеНакладной",
		Состояние,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(СостояниеНакладной));
		
КонецПроцедуры

&НаКлиенте
Процедура СостояниеОрдераПриИзменении(Элемент)
	
	Состояние = ?(ЗначениеЗаполнено(СостояниеОрдера), Число(СостояниеОрдера), 0);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(СписокРаспоряженияНаОформление,
		"СостояниеОрдера",
		Состояние,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(СостояниеОрдера));
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокРаспоряженияНаОформление,
		"Организация",
		Организация,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Организация));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРаспоряженияНаОформление, "Организация", Организация);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокКПереходуПраваСобственности, "Организация", Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура СмТакжеВРаботеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючНазначенияФормы", "");
	ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", Ложь);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Обработка.ЖурналДокументовПродажи.Форма.СписокДокументов" Тогда
		//++ Локализация
		ИнтеграцияСМаркетплейсамиКлиент.ДополнитьПараметрыФормы(ПараметрыФормы,
			СписокРаспоряженияНаОформление.КомпоновщикНастроек.Настройки.ДополнительныеСвойства, НавигационнаяСсылкаФорматированнойСтроки);
		//-- Локализация
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Документ.СчетФактураВыданный.Форма.ФормаСпискаДокументов"
		ИЛИ НавигационнаяСсылкаФорматированнойСтроки = "Документ.СчетФактураВыданный.Форма.ФормаРабочееМестоДляОформленияИсправленныхСчетовФактурПоОтгрузкам" Тогда
		СтруктураБыстрогоОтбора = Новый Структура;
		СтруктураБыстрогоОтбора.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	КонецЕсли;
	
	ОткрытьФорму(НавигационнаяСсылкаФорматированнойСтроки, ПараметрыФормы,, ПараметрыФормы.УникальныйИдентификаторФормы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокКПереходуПраваСобственности

&НаКлиенте
Процедура СписокКПереходуПраваСобственностиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные.Распоряжение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокКПереходуПраваСобственностиПриАктивизацииСтроки(Элемент)
		
	ОтключитьОбработчикОжидания("ОбновитьКомандыПоУсловиюВидимости");
	ПодключитьОбработчикОжидания("ОбновитьКомандыПоУсловиюВидимости", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОформитьПоРаспоряжениям(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Документ.РеализацияТоваровУслуг.КОформлениюНакладных.Команда.ОформитьПоРаспоряжениям");
	
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	Если Не ОбщегоНазначенияУТКЛиент.ВыбраныДокументыКОформлению(ВыделенныеСтроки, ПараметрыЖурнала("Накладные")) Тогда
		Возврат;
	КонецЕсли;
	
	ПоОрдерам = Ложь;
	ОформитьОтгрузку = Ложь;
	ОформитьПродажу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьПоОтгрузке(Команда)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина,
		"Документ.РеализацияТоваровУслуг.КОформлениюНакладных.Команда.ОформитьПоОтгрузке");
		
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	Если Не ОбщегоНазначенияУТКЛиент.ВыбраныДокументыКОформлению(ВыделенныеСтроки, ПараметрыЖурнала("Накладные")) Тогда
		Возврат;
	КонецЕсли;
	
	ПоОрдерам = Истина;
	ОформитьОтгрузку = Ложь;
	ОформитьПродажу();
	
КонецПроцедуры

&НаКлиенте
Процедура СостояниеВыполнения(Команда)
	
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписокДокументов = Новый СписокЗначений;
	МассивТиповДокументов = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		
		ВыделенныйДокумент = Элементы.СписокРаспоряженияНаОформление.ДанныеСтроки(ВыделеннаяСтрока).Ссылка;
		ТипВыделенногоДокумента = ТипЗнч(ВыделенныйДокумент);
		
		Если МассивТиповДокументов.Найти(ТипВыделенногоДокумента) = Неопределено Тогда
			МассивТиповДокументов.Добавить(ТипВыделенногоДокумента);
			Если МассивТиповДокументов.Количество() > 1 Тогда
				ПоказатьПредупреждение( , НСтр("ru = 'Команда не может быть выполнена для документов разного вида.';
												|en = 'Command cannot be executed for documents of different kinds.'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если СписокДокументов.НайтиПоЗначению(ВыделенныйДокумент) = Неопределено Тогда
			СписокДокументов.Добавить(ВыделенныйДокумент);
		КонецЕсли;
			
	КонецЦикла;
	
	Если СписокДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Отчет.СостояниеВыполненияДокументов.Форма.ФормаОтчета", 
		Новый Структура("ВходящиеДокументы", СписокДокументов), 
		ЭтаФорма,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура СвязанныеДокументы(Команда)
	
	ТекущиеДанные = Элементы.СписокРаспоряженияНаОформление.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыбранныйДокумент = ТекущиеДанные.Ссылка;
	Если НЕ ЗначениеЗаполнено(ВыбранныйДокумент) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("ОбщаяФорма.СвязанныеДокументы",
		Новый Структура("ОбъектОтбора", ВыбранныйДокумент),
			ЭтаФорма,
			Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьОтгрузкуТоваров(Команда)
	
	ИмяКомандыОтгрузки = Команда.Имя;
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ.ОтгрузкаТоваровКлиенту.КОформлениюНакладных.Команда." + ИмяКомандыОтгрузки);
	
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	Если Не ОбщегоНазначенияУТКЛиент.ВыбраныДокументыКОформлению(ВыделенныеСтроки, ПараметрыЖурнала("Накладные")) Тогда
		Возврат;
	КонецЕсли;
	
	ПоОрдерам = Ложь;
	ОформитьОтгрузку = Истина;
	Если ИмяКомандыОтгрузки = "ОформитьОтгрузкуПоОрдеру" Тогда
		ПоОрдерам = Истина;
	КонецЕсли;
	
	ОформитьПродажу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьДокументыПереходаПраваСобственности(Команда)
	
	ИмяКомандыОтгрузки = Команда.Имя;
	ИмяДокумента = "РеализацияТоваровУслуг";
	
	// Акт о расхождении после отгрузки
	Если ИмяКомандыОтгрузки = "ОформитьАктОРасхожденииПослеОтгрузки" Тогда
		ИмяДокумента = "АктОРасхожденияхПослеОтгрузки";
	
	// Исправление отгрузки	
	ИначеЕсли ИмяКомандыОтгрузки = "ОформитьИсправлениеОтгрузки" Тогда
		ИмяДокумента = "ОтгрузкаТоваровКлиенту";
		
	// Поступление товаров	
	ИначеЕсли ИмяКомандыОтгрузки = "ОформитьПоступлениеТоваров" Тогда
		ИмяДокумента = "ПоступлениеТоваровОтКлиента";
		
	// Оприходование излишков	
	ИначеЕсли ИмяКомандыОтгрузки = "ОформитьОприходованиеИзлишков" Тогда
		ИмяДокумента = "ОприходованиеИзлишковОтгруженныхТоваров";
			
	// Списание недостач	
	ИначеЕсли ИмяКомандыОтгрузки = "ОформитьСписаниеНедостач" Тогда
		ИмяДокумента = "СписаниеОтгруженныхТоваров";
	КонецЕсли;
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Документ." + ИмяДокумента + ".КОформлениюНакладных.Команда." + ИмяКомандыОтгрузки);
	
	ВыделенныеСтроки = Элементы.СписокКПереходуПраваСобственности.ВыделенныеСтроки;
	Если Не ОбщегоНазначенияУТКЛиент.ВыбраныДокументыКОформлению(ВыделенныеСтроки, ПараметрыЖурнала("Накладные")) Тогда
		Возврат;
	КонецЕсли;
	
	ОформитьДокументыНаОтгрузку();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Условное оформление динамического списка "СписокРаспоряженияНаОформление"
	СписокУсловноеОформление = СписокРаспоряженияНаОформление.КомпоновщикНастроек.Настройки.УсловноеОформление;
	СписокУсловноеОформление.Элементы.Очистить();
	
	// Документ имеет высокий приоритет
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Документ имеет высокий приоритет';
								|en = 'High priority document'");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приоритет");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.Приоритеты.ПолучитьВысшийПриоритет();
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ВысокийПриоритетДокумента);
	
	// Документ имеет низкий приоритет
	Элемент = СписокУсловноеОформление.Элементы.Добавить();
	Элемент.Представление = НСтр("ru = 'Документ имеет низкий приоритет';
								|en = 'Low priority document'");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Приоритет");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.Приоритеты.ПолучитьНизшийПриоритет();
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.НизкийПриоритетДокумента);
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "СписокРаспоряженияНаОформление.ДатаДокумента", Элементы.СписокРаспоряженияНаОформлениеДатаДокумента.Имя);
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "СписокКПереходуПраваСобственности.ДатаДокумента", Элементы.СписокКПереходуПраваСобственностиДатаДокумента.Имя);
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаСпискаРаспоряженийНаОформление()
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Распоряжение КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		ИЛИ Таблица.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		И Таблица.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке > &ТекущаяДата
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеОрдераВрем,
	|	ВЫБОР
	|		КОГДА Склады.ЭтоГруппа
	|		И Склады.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РазбиватьРеализациюПоСкладам,
	|	ВЫБОР
	|		КОГДА Склады.ЭтоГруппа
	|		И Склады.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОбъединятьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтОстаткиПоЗаказуКлиента
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО (Склады.Ссылка = ВЫБОР
	|			КОГДА Таблица.Распоряжение ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаказКлиента).Склад
	|			КОГДА Таблица.Распоряжение ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|				ТОГДА ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ОтгрузкаТоваровКлиенту).Склад
	|			КОГДА Таблица.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(Таблица.Распоряжение КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Склад
	|		КОНЕЦ)
	|ГДЕ
	|	ТИПЗНАЧЕНИЯ(Таблица.Распоряжение) В (ТИП(Документ.ЗаказКлиента), ТИП(Документ.ОтгрузкаТоваровКлиенту),
	|		ТИП(Документ.ЗаявкаНаВозвратТоваровОтКлиента))
	|	И Таблица.СостояниеКОтгрузке В (ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.НеОформлены),
	|		ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.ЧастичноОформлены))
	|	И &Склад В (Таблица.Склад, ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И &Организация В (Таблица.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
	|
	|;
	|//////////////////////////////////////////////////////////////////////////////// 1
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.Ссылка КАК ЗаказКлиента,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОформления
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|//////////////////////////////////////////////////////////////////////////////// 2
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ЗаказКлиента КАК ЗаказКлиента,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьНакладные
	|ИЗ
	|	ВтДокументыОформления КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Распоряжения
	|		ПО Таблица.ЗаказКлиента = Распоряжения.Распоряжение
	|		И (Таблица.Склад = Распоряжения.Склад
	|		ИЛИ Распоряжения.Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|ГДЕ
	|	Распоряжения.Активность
	|	И Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг
	|	И Распоряжения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Оформлено)
	|	И Распоряжения.Количество > 0
	|	И Распоряжения.НомерСтроки = 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 3
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ЗаказКлиента КАК ЗаказКлиента,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьОтгрузки
	|ИЗ
	|	ВтДокументыОформления КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РаспоряженияНаОтгрузкуИВозврат КАК Распоряжения
	|		ПО Таблица.ЗаказКлиента = Распоряжения.Распоряжение
	|		И Таблица.Склад = Распоряжения.Склад
	|ГДЕ
	|	Распоряжения.Активность
	|	И Регистратор ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|	И Распоряжения.Показатель = ЗНАЧЕНИЕ(Перечисление.ПоказателиРаспоряженийНаОтгрузкуИВозврат.Отгружено)
	|	И Распоряжения.Количество > 0
	|	И Распоряжения.НомерСтроки = 1
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 4
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.Ссылка КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаЕстьНакладные.ЗаказКлиента ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СостояниеНакладнойВрем,
	|	Таблица.СостояниеОрдераВрем КАК СостояниеОрдераВрем,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаЕстьОтгрузки.ЗаказКлиента ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СостояниеОтгрузкиВрем,
	|	Таблица.СостояниеОрдераВрем КАК СостояниеОрдераОтгрузкиВрем,
	|	Таблица.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам,
	|	Таблица.ОбъединятьРеализациюПоСкладам КАК ОбъединятьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтСостояниеНакладной
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьНакладные КАК ТаблицаЕстьНакладные
	|		ПО ТаблицаЕстьНакладные.ЗаказКлиента = Таблица.Ссылка
	|		И ТаблицаЕстьНакладные.Склад = Таблица.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьОтгрузки КАК ТаблицаЕстьОтгрузки
	|		ПО ТаблицаЕстьОтгрузки.ЗаказКлиента = Таблица.Ссылка
	|		И ТаблицаЕстьОтгрузки.Склад = Таблица.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 5
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад,
	|	Таблица.Номенклатура КАК Номенклатура,
	|	Таблица.Характеристика КАК Характеристика,
	|	Таблица.Серия КАК Серия,
	|	Таблица.Назначение КАК Назначение,
	|	(Таблица.КОтгрузкеОстаток - Таблица.СобраноОстаток) = Таблица.КОформлениюОстаток КАК ОстаткиРавны
	|ПОМЕСТИТЬ ВтОстаткиТоварыКОтгрузке
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(, (Склад = &Склад
	|	ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю)) КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 6
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтгрузки
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК Таблица
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 7
	|ВЫБРАТЬ
	|	Таблица.ДокументОтгрузки КАК ДокументОтгрузки,
	|	Таблица.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьОрдера
	|ИЗ
	|	ВтДокументыОтгрузки КАК Таблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузке
	|		ПО Таблица.ДокументОтгрузки = ТоварыКОтгрузке.ДокументОтгрузки
	|		И Таблица.Склад = ТоварыКОтгрузке.Склад
	|ГДЕ
	|	ТоварыКОтгрузке.Активность
	|	И (ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ТоварыКОтгрузке.КОтгрузке > 0
	|	ИЛИ ТоварыКОтгрузке.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И ТоварыКОтгрузке.Собрано > 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 8
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Таблица.ДокументОтгрузки КАК Ссылка,
	|	Таблица.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдера.ДокументОтгрузки ЕСТЬ NULL
	|		И НЕ СпрСклады.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА Таблица.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СостояниеОрдераВрем
	|ПОМЕСТИТЬ ВтСостояниеОрдера
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК Таблица
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьОрдера КАК ТаблицаЕстьОрдера
	|		ПО ТаблицаЕстьОрдера.ДокументОтгрузки = Таблица.ДокументОтгрузки
	|		И ТаблицаЕстьОрдера.Склад = Таблица.Склад
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСклады
	|		ПО СпрСклады.Ссылка = Таблица.Склад
	|		И СпрСклады.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		И СпрСклады.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдера.ДокументОтгрузки ЕСТЬ NULL
	|		И НЕ СпрСклады.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА Таблица.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 9
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладной.Ссылка ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдера.Ссылка
	|		ИНАЧЕ ВтСостояниеНакладной.Ссылка
	|	КОНЕЦ КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладной.Склад ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдера.Склад
	|		ИНАЧЕ ВтСостояниеНакладной.Склад
	|	КОНЕЦ КАК Склад,
	|	ЕСТЬNULL(ВтСостояниеНакладной.СостояниеНакладнойВрем, 0) КАК СостояниеНакладнойВрем,
	|	ЕСТЬNULL(ВтСостояниеОрдера.СостояниеОрдераВрем, ВтСостояниеНакладной.СостояниеОрдераВрем) КАК СостояниеОрдераВрем,
	|	ЕСТЬNULL(ВтСостояниеНакладной.СостояниеОтгрузкиВрем, 0) КАК СостояниеОтгрузкиВрем,
	|	ЕСТЬNULL(ВтСостояниеОрдера.СостояниеОрдераВрем, ВтСостояниеНакладной.СостояниеОрдераОтгрузкиВрем) КАК
	|		СостояниеОрдераОтгрузкиВрем,
	|	ЕСТЬNULL(ВтСостояниеНакладной.РазбиватьРеализациюПоСкладам, ЛОЖЬ) КАК РазбиватьРеализациюПоСкладам,
	|	ЕСТЬNULL(ВтСостояниеНакладной.ОбъединятьРеализациюПоСкладам, ЛОЖЬ) КАК ОбъединятьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтНовыеОрдера
	|ИЗ
	|	ВтСостояниеНакладной КАК ВтСостояниеНакладной
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВтСостояниеОрдера КАК ВтСостояниеОрдера
	|		ПО ВтСостояниеНакладной.Ссылка = ВтСостояниеОрдера.Ссылка
	|		И ВтСостояниеНакладной.Склад = ВтСостояниеОрдера.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|
	|//////////////////////////////////////////////////////////////////////////////// 10
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК Ссылка,
	|	ТоварыКОтгрузке.ДокументОтгрузки КАК ЗаказКлиента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Номер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Дата
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Дата
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТИПЗНАЧЕНИЯ(ТоварыКОтгрузке.ДокументОтгрузки) КАК ТипРаспоряжения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Проведен
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Проведен
	|	КОНЕЦ КАК Проведен,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Партнер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Партнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Контрагент
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Организация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Соглашение
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Соглашение
	|	КОНЕЦ КАК Соглашение,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Договор
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Договор
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Сделка
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Сделка
	|	КОНЕЦ КАК Сделка,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Валюта
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Валюта
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).СуммаДокумента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).СуммаДокумента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).СуммаЗамены
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СуммаДокумента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ГрафикОплаты
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ГрафикОплаты
	|	КОНЕЦ КАК ГрафикОплаты,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Склад
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Склад
	|	КОНЕЦ КАК СкладШапки,
	|	ТоварыКОтгрузке.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Статус
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Статус
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Менеджер
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ДокументОснование
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).ДокументОснование
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ЗаказКлиента
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|	КОНЕЦ КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Касса
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Кассы.ПустаяСсылка)
	|	КОНЕЦ КАК Касса,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СпособДоставки
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).СпособДоставки
	|	КОНЕЦ КАК СпособДоставки,
	|	ТоварыКОтгрузке.СостояниеНакладнойВрем КАК СостояниеНакладной,
	|	ТоварыКОтгрузке.СостояниеОрдераВрем КАК СостояниеОрдера,
	|	ТоварыКОтгрузке.СостояниеОтгрузкиВрем КАК СостояниеОтгрузки,
	|	ТоварыКОтгрузке.СостояниеОрдераОтгрузкиВрем КАК СостояниеОрдераОтгрузки,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета,
	|
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).Проведен
	|						ТОГДА 0
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаказКлиента).ПометкаУдаления
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ОтгрузкаТоваровКлиенту
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).Проведен
	|						ТОГДА 0
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ОтгрузкаТоваровКлиенту).ПометкаУдаления
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Проведен
	|						ТОГДА 0
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).ПометкаУдаления
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Проведен
	|						ТОГДА 0
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ПометкаУдаления
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|		КОГДА ТоварыКОтгрузке.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
	|			ТОГДА
	|				ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Проведен
	|						ТОГДА 0
	|					КОГДА ВЫРАЗИТЬ(ТоварыКОтгрузке.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).ПометкаУдаления
	|						ТОГДА 1
	|					ИНАЧЕ 2
	|				КОНЕЦ
	|	КОНЕЦ КАК НестандартнаяКартинка,
	|	ТоварыКОтгрузке.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам,
	|	ТоварыКОтгрузке.ОбъединятьРеализациюПоСкладам КАК ОбъединятьРеализациюПоСкладам
	|ИЗ
	|	ВтНовыеОрдера КАК ТоварыКОтгрузке
	|ГДЕ
	|	НЕ ТоварыКОтгрузке.ДокументОтгрузки.Ссылка ЕСТЬ NULL";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСпискиВыбораПоСостояниямОтгрузки()
	
	СписокВыбора = Элементы.СостояниеНакладной.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("0", НСтр("ru = 'Все оформлено';
									|en = 'Fully issued/received'"), Ложь, БиблиотекаКартинок.ОформленаНакладная);
	СписокВыбора.Добавить("1", НСтр("ru = 'Создать накладную';
									|en = 'Create invoice'"), Ложь, БиблиотекаКартинок.СоздатьНакладную);
	СписокВыбора.Добавить("2", НСтр("ru = 'Дооформить накладную';
									|en = 'Pending issue/receipt'"), Ложь, БиблиотекаКартинок.ДооформитьНакладную);
	
	СписокВыбора = Элементы.СостояниеОрдера.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить("0", НСтр("ru = 'Соответствуют накладным';
									|en = 'Notes correspond to the invoices'"), Ложь, БиблиотекаКартинок.ОформленаНакладная);
	СписокВыбора.Добавить("1", НСтр("ru = 'Не оформлены';
									|en = 'Pending creation'"), Ложь, БиблиотекаКартинок.СоздатьНакладную);
	СписокВыбора.Добавить("3", НСтр("ru = 'Не соответствуют накладным';
									|en = 'Received vs invoiced mismatch'"), Ложь, БиблиотекаКартинок.НесоответствиеОрдерНакладная);
	СписокВыбора.Добавить("4", НСтр("ru = 'Не используется ордерная схема';
									|en = 'Warehouse management is not used'"), Ложь, БиблиотекаКартинок.ПустаяКартинка);
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаОстаткиЗаказовКлиентов()
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.Распоряжение КАК Ссылка,
	|	ТаблицаПереопределяемый.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаПереопределяемый.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		ИЛИ ТаблицаПереопределяемый.Склад.ИспользоватьОрдернуюСхемуПриОтгрузке
	|		И ТаблицаПереопределяемый.Склад.ДатаНачалаОрдернойСхемыПриОтгрузке > &ТекущаяДата
	|			ТОГДА 4
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СостояниеОрдераВрем,
	|	ВЫБОР
	|		КОГДА СкладыПереопределяемый.ЭтоГруппа
	|		И СкладыПереопределяемый.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказах)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РазбиватьРеализациюПоСкладам,
	|	ВЫБОР
	|		КОГДА СкладыПереопределяемый.ЭтоГруппа
	|		И СкладыПереопределяемый.ВыборГруппы = ЗНАЧЕНИЕ(Перечисление.ВыборГруппыСкладов.РазрешитьВЗаказахИНакладных)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОбъединятьРеализациюПоСкладам
	|ИЗ
	|	РегистрСведений.РаспоряженияНаОтгрузкуИВозвратКВыполнению КАК ТаблицаПереопределяемый
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СкладыПереопределяемый
	|		ПО СкладыПереопределяемый.Ссылка = ТаблицаПереопределяемый.Склад
	|ГДЕ
	|	(ТаблицаПереопределяемый.Склад = &Склад
	|	ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаказКлиента
	|	ИЛИ ТаблицаПереопределяемый.Распоряжение ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента)
	|	И
	|		ТаблицаПереопределяемый.СостояниеКОформлению <> ЗНАЧЕНИЕ(Перечисление.СостояниеОформленияДокументовПоРаспоряжению.Оформлены)
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаПереопределяемый.Ссылка КАК ЗаказКлиента,
	|	ТаблицаПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОформления
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК ТаблицаПереопределяемый
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДокументыОформленияПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОформленияСклады
	|ИЗ
	|	ВтДокументыОформления КАК ВтДокументыОформленияПереопределяемый
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗаказыКлиентовПереопределяемый.ЗаказКлиента КАК ЗаказКлиента,
	|	ЗаказыКлиентовПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтобранные
	|ИЗ
	|	РегистрНакопления.УдалитьЗаказыКлиентов КАК ЗаказыКлиентовПереопределяемый
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОформленияСклады КАК ВтДокументыОформленияСкладыПереопределяемый
	|		ПО ВтДокументыОформленияСкладыПереопределяемый.Склад = ЗаказыКлиентовПереопределяемый.Склад
	|ГДЕ
	|	ЗаказыКлиентовПереопределяемый.Активность
	|	И ЗаказыКлиентовПереопределяемый.КОформлению > 0
	|	И ЗаказыКлиентовПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.ЗаказКлиента КАК ЗаказКлиента,
	|	ТаблицаПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьНакладные
	|ИЗ
	|	ВтДокументыОформления КАК ТаблицаПереопределяемый
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтобранные КАК ДокументыОтобранныеПереопределяемый
	|		ПО ТаблицаПереопределяемый.ЗаказКлиента = ДокументыОтобранныеПереопределяемый.ЗаказКлиента
	|		И ТаблицаПереопределяемый.Склад = ДокументыОтобранныеПереопределяемый.Склад
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЗаказКлиента,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.Ссылка КАК Ссылка,
	|	ТаблицаПереопределяемый.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА НЕ ТаблицаЕстьНакладныеПереопределяемый.ЗаказКлиента ЕСТЬ NULL
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СостояниеНакладнойВрем,
	|	ТаблицаПереопределяемый.СостояниеОрдераВрем КАК СостояниеОрдераВрем,
	|	ТаблицаПереопределяемый.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам,
	|	ТаблицаПереопределяемый.ОбъединятьРеализациюПоСкладам КАК ОбъединятьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтСостояниеНакладной
	|ИЗ
	|	ВтОстаткиПоЗаказуКлиента КАК ТаблицаПереопределяемый
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьНакладные КАК ТаблицаЕстьНакладныеПереопределяемый
	|		ПО (ТаблицаЕстьНакладныеПереопределяемый.ЗаказКлиента = ТаблицаПереопределяемый.Ссылка)
	|		И (ТаблицаЕстьНакладныеПереопределяемый.Склад = ТаблицаПереопределяемый.Склад)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ТаблицаПереопределяемый.Склад КАК Склад,
	|	ТаблицаПереопределяемый.Номенклатура КАК Номенклатура,
	|	ТаблицаПереопределяемый.Характеристика КАК Характеристика,
	|	ТаблицаПереопределяемый.Серия КАК Серия,
	|	ТаблицаПереопределяемый.Назначение КАК Назначение,
	|	ТаблицаПереопределяемый.КОтгрузкеОстаток -
	|		ТаблицаПереопределяемый.СобраноОстаток = ТаблицаПереопределяемый.КОформлениюОстаток КАК ОстаткиРавны
	|ПОМЕСТИТЬ ВтОстаткиТоварыКОтгрузке
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке.Остатки(, (Склад = &Склад
	|	ИЛИ &Склад = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка))
	|	И (ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|	ИЛИ ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг)) КАК ТаблицаПереопределяемый
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ТаблицаПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтгрузки
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК ТаблицаПереопределяемый
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтДокументыОтгрузкиПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтДокументыОтгрузкиСклады
	|ИЗ
	|	ВтДокументыОтгрузки КАК ВтДокументыОтгрузкиПереопределяемый
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ТоварыКОтгрузкеПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтТоварыКОтгрузкеПереопределяемый
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузкеПереопределяемый
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтгрузкиСклады КАК ВтДокументыОтгрузкиСкладыПереопределяемый
	|		ПО ВтДокументыОтгрузкиСкладыПереопределяемый.Склад = ТоварыКОтгрузкеПереопределяемый.Склад
	|ГДЕ
	|	ТоварыКОтгрузкеПереопределяемый.Активность
	|	И ТоварыКОтгрузкеПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки,
	|	ТоварыКОтгрузкеПереопределяемый.Склад
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОтгрузкеПереопределяемый.КОтгрузке) > 0
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ТоварыКОтгрузкеПереопределяемый.Склад КАК Склад
	|ИЗ
	|	РегистрНакопления.ТоварыКОтгрузке КАК ТоварыКОтгрузкеПереопределяемый
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыОтгрузкиСклады КАК ВтДокументыОтгрузкиСкладыПереопределяемый
	|		ПО ВтДокументыОтгрузкиСкладыПереопределяемый.Склад = ТоварыКОтгрузкеПереопределяемый.Склад
	|ГДЕ
	|	ТоварыКОтгрузкеПереопределяемый.Активность
	|	И ТоварыКОтгрузкеПереопределяемый.КОтгрузке > 0
	|	И ТоварыКОтгрузкеПереопределяемый.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки,
	|	ТоварыКОтгрузкеПереопределяемый.Склад
	|ИМЕЮЩИЕ
	|	СУММА(ТоварыКОтгрузкеПереопределяемый.Собрано) > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.ДокументОтгрузки КАК ДокументОтгрузки,
	|	ТаблицаПереопределяемый.Склад КАК Склад
	|ПОМЕСТИТЬ ВтЕстьОрдера
	|ИЗ
	|	ВтДокументыОтгрузки КАК ТаблицаПереопределяемый
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтТоварыКОтгрузкеПереопределяемый КАК ВтТоварыКОтгрузкеПереопределяемый
	|		ПО ТаблицаПереопределяемый.Склад = ВтТоварыКОтгрузкеПереопределяемый.Склад
	|		И ТаблицаПереопределяемый.ДокументОтгрузки = ВтТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки,
	|	Склад
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаПереопределяемый.ДокументОтгрузки КАК Ссылка,
	|	ТаблицаПереопределяемый.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдераПереопределяемый.ДокументОтгрузки ЕСТЬ NULL
	|		И
	|		НЕ СпрСкладыПереопределяемый.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА ТаблицаПереопределяемый.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ КАК СостояниеОрдераВрем
	|ПОМЕСТИТЬ ВтСостояниеОрдера
	|ИЗ
	|	ВтОстаткиТоварыКОтгрузке КАК ТаблицаПереопределяемый
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВтЕстьОрдера КАК ТаблицаЕстьОрдераПереопределяемый
	|		ПО (ТаблицаЕстьОрдераПереопределяемый.ДокументОтгрузки = ТаблицаПереопределяемый.ДокументОтгрузки)
	|		И (ТаблицаЕстьОрдераПереопределяемый.Склад = ТаблицаПереопределяемый.Склад)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК СпрСкладыПереопределяемый
	|		ПО (СпрСкладыПереопределяемый.Ссылка = ТаблицаПереопределяемый.Склад)
	|		И (СпрСкладыПереопределяемый.ИспользоватьОрдернуюСхемуПриОтгрузке)
	|		И (СпрСкладыПереопределяемый.ДатаНачалаОрдернойСхемыПриОтгрузке <= &ТекущаяДата)
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ТаблицаЕстьОрдераПереопределяемый.ДокументОтгрузки ЕСТЬ NULL
	|		И
	|		НЕ СпрСкладыПереопределяемый.Ссылка ЕСТЬ NULL
	|			ТОГДА 1
	|		КОГДА ТаблицаПереопределяемый.ОстаткиРавны
	|			ТОГДА 0
	|		ИНАЧЕ 3
	|	КОНЕЦ > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТаблицаПереопределяемый.ДокументОтгрузки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладнойПереопределяемый.Ссылка ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдераПереопределяемый.Ссылка
	|		ИНАЧЕ ВтСостояниеНакладнойПереопределяемый.Ссылка
	|	КОНЕЦ КАК ДокументОтгрузки,
	|	ВЫБОР
	|		КОГДА ВтСостояниеНакладнойПереопределяемый.Склад ЕСТЬ NULL
	|			ТОГДА ВтСостояниеОрдераПереопределяемый.Склад
	|		ИНАЧЕ ВтСостояниеНакладнойПереопределяемый.Склад
	|	КОНЕЦ КАК Склад,
	|	ЕСТЬNULL(ВтСостояниеНакладнойПереопределяемый.СостояниеНакладнойВрем, 0) КАК СостояниеНакладнойВрем,
	|	ЕСТЬNULL(ВтСостояниеОрдераПереопределяемый.СостояниеОрдераВрем,
	|		ВтСостояниеНакладнойПереопределяемый.СостояниеОрдераВрем) КАК СостояниеОрдераВрем,
	|	ЕСТЬNULL(ВтСостояниеНакладнойПереопределяемый.РазбиватьРеализациюПоСкладам, ЛОЖЬ) КАК РазбиватьРеализациюПоСкладам,
	|	ЕСТЬNULL(ВтСостояниеНакладнойПереопределяемый.ОбъединятьРеализациюПоСкладам, ЛОЖЬ) КАК ОбъединятьРеализациюПоСкладам
	|ПОМЕСТИТЬ ВтНовыеОрдера
	|ИЗ
	|	ВтСостояниеНакладной КАК ВтСостояниеНакладнойПереопределяемый
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВтСостояниеОрдера КАК ВтСостояниеОрдераПереопределяемый
	|		ПО ВтСостояниеНакладнойПереопределяемый.Ссылка = ВтСостояниеОрдераПереопределяемый.Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументОтгрузки
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Распоряжение,
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Ссылка,
	|	ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК ЗаказКлиента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).ПометкаУдаления
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.РеализацияТоваровУслуг).ПометкаУдаления
	|	КОНЕЦ КАК ПометкаУдаления,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Номер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Номер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Номер
	|	КОНЕЦ КАК Номер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Дата
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Дата
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Дата
	|	КОНЕЦ КАК ДатаДокумента,
	|	ТИПЗНАЧЕНИЯ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки) КАК ТипРаспоряжения,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Проведен
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Проведен
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Проведен
	|	КОНЕЦ КАК Проведен,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Партнер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Партнер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Партнер
	|	КОНЕЦ КАК Партнер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Контрагент
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Контрагент
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Организация
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Организация
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Организация
	|	КОНЕЦ КАК Организация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Соглашение
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Соглашение
	|	КОНЕЦ КАК Соглашение,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Договор
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Договор
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Договор
	|	КОНЕЦ КАК Договор,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Сделка
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Сделка
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Сделка
	|	КОНЕЦ КАК Сделка,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Валюта
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Валюта
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Валюта
	|	КОНЕЦ КАК Валюта,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).СуммаДокумента
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).СуммаЗамены
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).ГрафикОплаты
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).ГрафикОплаты
	|	КОНЕЦ КАК ГрафикОплаты,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Склад
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаявкаНаВозвратТоваровОтКлиента).Склад
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Склад
	|	КОНЕЦ КАК СкладШапки,
	|	ТоварыКОтгрузкеПереопределяемый.Склад КАК Склад,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Статус
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Статус
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Статус
	|	КОНЕЦ КАК Статус,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Менеджер
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Менеджер
	|	КОНЕЦ КАК Менеджер,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).ДокументОснование
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).ЗаказКлиента
	|	КОНЕЦ КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|	КОНЕЦ КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).ХозяйственнаяОперация
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.РеализацияТоваровУслуг).ХозяйственнаяОперация
	|	КОНЕЦ КАК ХозяйственнаяОперация,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Касса
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).Касса
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.РеализацияТоваровУслуг).Касса
	|	КОНЕЦ КАК Касса,
	|	ВЫБОР
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|				Документ.ЗаявкаНаВозвратТоваровОтКлиента).СпособДоставки
	|		КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
	|			ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).СпособДоставки
	|	КОНЕЦ КАК СпособДоставки,
	|	ТоварыКОтгрузкеПереопределяемый.СостояниеНакладнойВрем КАК СостояниеНакладной,
	|	ТоварыКОтгрузкеПереопределяемый.СостояниеОрдераВрем КАК СостояниеОрдера,
	|	ВЫБОР
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|					Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания)
	|			ТОГДА 0
	|		КОГДА ВЫБОР
	|			КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаказКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК Документ.ЗаказКлиента).Приоритет
	|			КОГДА ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки ССЫЛКА Документ.ЗаявкаНаВозвратТоваровОтКлиента
	|				ТОГДА ВЫРАЗИТЬ(ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки КАК
	|					Документ.ЗаявкаНаВозвратТоваровОтКлиента).Приоритет
	|		КОНЕЦ В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				Приоритеты.Ссылка КАК Приоритет
	|			ИЗ
	|				Справочник.Приоритеты КАК Приоритеты
	|			УПОРЯДОЧИТЬ ПО
	|				Приоритеты.РеквизитДопУпорядочивания УБЫВ)
	|			ТОГДА 2
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК КартинкаПриоритета,
	|	ТоварыКОтгрузкеПереопределяемый.РазбиватьРеализациюПоСкладам КАК РазбиватьРеализациюПоСкладам,
	|	ТоварыКОтгрузкеПереопределяемый.ОбъединятьРеализациюПоСкладам КАК ОбъединятьРеализациюПоСкладам
	|ИЗ
	|	ВтНовыеОрдера КАК ТоварыКОтгрузкеПереопределяемый
	|ГДЕ
	|	НЕ ТоварыКОтгрузкеПереопределяемый.ДокументОтгрузки.Ссылка ЕСТЬ NULL";
	Возврат ТекстЗапроса;
Конецфункции

&НаСервере
Процедура ЗаполнитьКомандыПоУсловиюВидимости()

	МассивТиповЗаказаОтгрузки = Новый Массив;
	МассивТиповЗаказаОтгрузки.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	МассивТиповЗаказаОтгрузки.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту"));
	
	МассивТипаОтгрузка = Новый Массив;
	МассивТипаОтгрузка.Добавить(Тип("ДокументСсылка.ОтгрузкаТоваровКлиенту"));
	
	КомандыПоУсловиюВидимости = Новый Структура();
	Для Каждого КомандаЭлемент Из Элементы.КомандыОформитьСписокКПереходуПраваСобственности.ПодчиненныеЭлементы Цикл
		
		МассивТипов = МассивТиповЗаказаОтгрузки;
		ИмяКоманды = КомандаЭлемент.Имя;
		Если ИмяКоманды = "ОформитьИсправлениеОтгрузки" Тогда
			МассивТипов = МассивТипаОтгрузка;
		КонецЕсли;
		КомандыПоУсловиюВидимости.Вставить(ИмяКоманды, Новый ОписаниеТипов(МассивТипов));
	
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКомандыПоУсловиюВидимости()
	
	Перем ТипДокумента;
	
	ТекущиеДанные = Элементы.СписокКПереходуПраваСобственности.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипДокумента = ТипЗнч(ТекущиеДанные.Ссылка);
	Для Каждого ЭлементСтруктуры Из КомандыПоУсловиюВидимости Цикл
		
		КомандаЭлемент = Элементы[ЭлементСтруктуры.Ключ];
		Видимость = Истина;
		Если Не ЭлементСтруктуры.Значение.СодержитТип(ТипДокумента) Тогда
			Видимость = Ложь;
		КонецЕсли;
		КомандаЭлемент.Видимость = Видимость;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыОрганизацииИСкладаНаСервере()

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРаспоряженияНаОформление, "Организация", Организация);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокКПереходуПраваСобственности, "Организация", Организация);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокРаспоряженияНаОформление, "Склад", Склад);

КонецПроцедуры

#Область Создание_Документов

&НаКлиенте
Процедура ОформитьПродажу()
	
	ПроверитьСклады = ЗаполнитьСписокДокументовПоДаннымФормы();

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтаФорма);
	ДополнительныеПараметры.Вставить("МассивСсылок", СписокРаспоряжений);
	ДополнительныеПараметры.Вставить("ПоОрдерам", ПоОрдерам);
	ДополнительныеПараметры.Вставить("ОформитьОтгрузку", ОформитьОтгрузку);
	ДополнительныеПараметры.Вставить("ИмяКомандыОтгрузки", ИмяКомандыОтгрузки);
	ДополнительныеПараметры.Вставить("Склад", Склад);
	ДополнительныеПараметры.Вставить("ПроверитьСклады", ПроверитьСклады);
	ДополнительныеПараметры.Вставить("КлючОбъекта", "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные");
	
	ПродажиКлиент.СформироватьКомплектДокументовВызов(Неопределено, ДополнительныеПараметры);
	
	Элементы.СписокРаспоряженияНаОформление.Обновить();
		
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьСписокДокументовПоДаннымФормы()
	
	ПроверитьСклады = Новый Соответствие;	
	СписокРаспоряжений.Очистить();
	
	ВыделенныеСтроки = Элементы.СписокРаспоряженияНаОформление.ВыделенныеСтроки;
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		НайденнаяСтрока = Элементы.СписокРаспоряженияНаОформление.ДанныеСтроки(Строка);
		
		ДобавленнаяСтрока = СписокРаспоряжений.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, НайденнаяСтрока);
		
		Если НайденнаяСтрока.ОбъединятьРеализациюПоСкладам Тогда
			СкладыПоСсылке = ПроверитьСклады.Получить(НайденнаяСтрока.Ссылка);
			Если СкладыПоСсылке = Неопределено Тогда
				СкладыПоСсылке = Новый Массив;
				СкладыПоСсылке.Добавить(ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"));
			КонецЕсли;
			
			СкладыПоСсылке.Добавить(НайденнаяСтрока.Склад);
			ПроверитьСклады.Вставить(НайденнаяСтрока.Ссылка, СкладыПоСсылке);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПроверитьСклады;
	
КонецФункции

&НаКлиенте
Функция ПараметрыЖурнала(КлючНазначенияИспользования)
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Организация",Организация);
	СтруктураБыстрогоОтбора.Вставить("Склад",Склад);
	
	ПараметрыЖурнала = Новый Структура;
	ПараметрыЖурнала.Вставить("СтруктураБыстрогоОтбора",СтруктураБыстрогоОтбора);
	ПараметрыЖурнала.Вставить("ИмяРабочегоМеста","ЖурналДокументовПродажи");
	ПараметрыЖурнала.Вставить("КлючНазначенияФормы",КлючНазначенияИспользования);
	ПараметрыЖурнала.Вставить("СинонимЖурнала",НСтр("ru = 'Документы продажи';
													|en = 'Sales documents'"));
	
	Возврат ПараметрыЖурнала;
	
КонецФункции

&НаКлиенте
Процедура ОформитьДокументыНаОтгрузку()
	
	СписокРаспоряжений.Очистить();
	
	ВыделенныеСтроки = Элементы.СписокКПереходуПраваСобственности.ВыделенныеСтроки;
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		НайденнаяСтрока = Элементы.СписокКПереходуПраваСобственности.ДанныеСтроки(Строка);
		ДобавленнаяСтрока = СписокРаспоряжений.Добавить();
		ЗаполнитьЗначенияСвойств(ДобавленнаяСтрока, НайденнаяСтрока);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Форма", ЭтотОбъект);
	ДополнительныеПараметры.Вставить("МассивСсылок", СписокРаспоряжений);
	ДополнительныеПараметры.Вставить("ПоОрдерам", Ложь);
	ДополнительныеПараметры.Вставить("ОформитьОтгрузку", Истина);
	ДополнительныеПараметры.Вставить("ИмяКомандыОтгрузки", ИмяКомандыОтгрузки);
	ДополнительныеПараметры.Вставить("Склад", Склад);
	ДополнительныеПараметры.Вставить("КлючОбъекта", "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные");
	
	ПродажиКлиент.СформироватьКомплектДокументовВызов(Неопределено, ДополнительныеПараметры);
	
	Элементы.СписокКПереходуПраваСобственности.Обновить();
		
КонецПроцедуры

#Область НастройкаКомплектаДокументов

&НаКлиенте
Процедура ПолучитьНастройкуКомплектаДокументов(Команда)
	
	КлючОбъекта = "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные";
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьНастройкуКомплектаДокументовЗавершение", ЭтотОбъект, КлючОбъекта);
	
	ПараметрыФормы = ПродажиВызовСервера.ПолучитьНастройкуКомплектаДокументов(КлючОбъекта);
	ОткрытьФорму("Обработка.ЖурналДокументовПродажи.Форма.ПараметрыОформленияДокументовПродажи", ПараметрыФормы,
		ЭтаФорма, , , , Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьНастройкуКомплектаДокументовЗавершение(Результат, КлючОбъекта) Экспорт 
	
	Если Результат <> Неопределено Тогда
		ПродажиВызовСервера.СохранитьНастройкуКомплектаДокументов(Результат, КлючОбъекта);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ГиперссылкаСмТакжеВРаботе

&НаСервере
Процедура СформироватьГиперссылкуСмТакжеВРаботе()

	МассивСтрок = Новый Массив;
	ЗаглавнаяСтрока = НСтр("ru = 'См. также: ';
							|en = 'See also:'");
	МассивСтрок.Добавить(ЗаглавнаяСтрока);
	
	ТекстГиперссылки = НСтр("ru = 'Документы продажи (оформленные накладные)';
							|en = 'Sales documents (issued invoices)'");
	ИмяОткрываемойФормы = "Обработка.ЖурналДокументовПродажи.Форма.СписокДокументов";
	Гиперссылка = Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяОткрываемойФормы);
	МассивСтрок.Добавить(Гиперссылка);
	МассивСтрок.Добавить("; ");
	
	ТекстГиперссылки = НСтр("ru = 'Счета-фактуры (к оформлению)';
							|en = 'Tax invoices (for registration)'");
	ИмяОткрываемойФормы = "Документ.СчетФактураВыданный.Форма.ФормаСпискаДокументов";
	Гиперссылка = Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяОткрываемойФормы);
	МассивСтрок.Добавить(Гиперссылка);
	
	Если ИспользоватьОтгрузкуБезПереходаПраваСобственности2_5 Тогда
		ТекстГиперссылки = НСтр("ru = 'ИСФ/КСФ по отгрузкам (к оформлению)';
								|en = 'Replacement/corrective tax invoice for shipments (for registration)'");
		ИмяОткрываемойФормы = "Документ.СчетФактураВыданный.Форма.ФормаРабочееМестоДляОформленияИсправленныхСчетовФактурПоОтгрузкам";
		Гиперссылка = Новый ФорматированнаяСтрока(ТекстГиперссылки,,,, ИмяОткрываемойФормы);
		МассивСтрок.Добавить("; ");
		МассивСтрок.Добавить(Гиперссылка);
	КонецЕсли;
	
	СмТакжеВРаботе = Новый ФорматированнаяСтрока(МассивСтрок)

КонецПроцедуры

#КонецОбласти

#КонецОбласти
