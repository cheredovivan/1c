// @strict-types

#Область ОбработчикиСобытийФормы

// Код процедур и функций
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Элементы.Подписант.ТолькоПросмотр = Параметры.Свойство("Подписант", Объект.Подписант);

	СоглашениеНаЗагрузкуСертификата =
		Обработки.ПодписаниеДанныхВМобильномЭДО.ПолучитьМакет("СоглашениеНаЗагрузкуСертификата");
	
	СервисМояПодписьДоступен = ИнтеграцияБСПБЭДПовтИсп.ДоступноИспользованиеСервисаМояПодпись();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Код процедур и функций
&НаКлиенте
Асинх Процедура Подписать(Команда)
	
	Если Не СервисМояПодписьДоступен Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Сервис ""Моя Подпись"" не доступен';
														|en = 'The ""My signature"" service is not available'"));
		Возврат;
	КонецЕсли;
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура();
	СтруктураПредставления = Новый Структура();
	СтруктураПредставления.Вставить("Представление", НСтр("ru = 'Соглашение на загрузку сертификата';
															|en = 'Certificate import agreement'"));
	СтруктураПредставления.Вставить("ИмяФайла", НСтр("ru = 'Соглашение на загрузку сертификата.txt';
													|en = 'Certificate import agreement.txt'"));

	ОписаниеДанных.Вставить("Представление", СтруктураПредставления);
	ОписаниеДанных.Вставить("Данные", ПолучитьДвоичныеДанныеИзСтроки(СоглашениеНаЗагрузкуСертификата.ПолучитьТекст()));
	
	ОписаниеДанных.Вставить("ПодписаниеВЭтомПриложении", Ложь);
	
	ПараметрыПодписанта = ИнтеграцияБСПБЭДВызовСервера.ПараметрыПодписантаМобильнойПодписи(Объект.Подписант);
	
	ОписаниеДанных.Вставить("ВыбранныйПодписант", ПараметрыПодписанта);
	
	ПараметрыВыполнения = Новый Структура("ОписаниеДанных, ИдентификаторФормы, Форма",
		ОписаниеДанных,
		УникальныйИдентификатор,
		ЭтотОбъект);
		
	КонтекстПодписания = Новый Структура("ПараметрыВыполнения, Подписант", ПараметрыВыполнения, ПараметрыПодписанта);
	
	ПараметрыПодписания = Новый Структура("ПакетнаяОтправкаНаПодписание", Ложь);
	
	ПодписатьЗавершение = Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект, КонтекстПодписания);
	
	МодульСервисМобильнойПодписиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисМобильнойПодписиКлиент");
	
	МодульСервисМобильнойПодписиКлиент.ПодписатьДанныеВСервисе(ПодписатьЗавершение,
		КонтекстПодписания,
		ПараметрыПодписания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодписатьЗавершение(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;

	ОтменаПодписания = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "ОтменаПодписания");
	Если ОтменаПодписания <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ошибка = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Результат, "Ошибка");
	Если Ошибка <> Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибка);
		Возврат;
	КонецЕсли;

	ОписаниеДанных = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Контекст.ПараметрыВыполнения, "ОписаниеДанных");
	
	Если ОписаниеДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СертификатПодписанта = КриптографияБЭДСлужебныйКлиент.НайтиСоздатьСертификатПодписантаИзСвойствПодписи(
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ОписаниеДанных, "СвойстваПодписи"),
		Объект.Подписант);
	
	Закрыть(СертификатПодписанта);
	
КонецПроцедуры

#КонецОбласти
