///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОписаниеПеременных

&НаКлиенте
Перем ВнутренниеДанные,
	СвойстваПароля,
	ОписаниеДанных,
	ФормаОбъекта,
	ОбработкаПослеПредупреждения,
	ТекущийСписокПредставлений,
	ПараметрыКомпонентыРутокен;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипПодписанта = "ПодписатьСертификатом";
	
	ЭлектроннаяПодписьСлужебный.НастроитьПояснениеВводаПароля(ЭтотОбъект, ,
		Элементы.ПояснениеУсиленногоПароля.Имя);
	
	НовыйПараметрТипПодписи = Новый Структура;
	НовыйПараметрТипПодписи.Вставить("ТипыПодписи", Новый Массив);
	НовыйПараметрТипПодписи.Вставить("Видимость", Ложь);
	НовыйПараметрТипПодписи.Вставить("Доступность", Ложь);
	НовыйПараметрТипПодписи.Вставить("ВыборДоверенности", Ложь);
	НовыйПараметрТипПодписи.Вставить("РазрешитьОтправкуНаПодписание", Ложь);
	НовыйПараметрТипПодписи.Вставить("ПакетнаяОтправкаНаПодписание", Неопределено);
	НовыйПараметрТипПодписи.Вставить("ПроверятьСертификат", ЭлектроннаяПодписьСлужебныйКлиентСервер.ПроверятьКвалифицированные());
	
	Если Не ТипЗнч(Параметры.ТипПодписи) = Тип("Структура") Тогда
		Если ЗначениеЗаполнено(Параметры.ТипПодписи) Тогда
			НовыйПараметрТипПодписи.ТипыПодписи.Добавить(Параметры.ТипПодписи);
		КонецЕсли;
	Иначе
		ЗаполнитьЗначенияСвойств(НовыйПараметрТипПодписи, Параметры.ТипПодписи);
	КонецЕсли;
	
	УстановитьТипПодписи(НовыйПараметрТипПодписи);
	ЭлектроннаяПодписьСлужебный.НастроитьФормуПодписанияШифрованияРасшифровки(ЭтотОбъект);
	
	// СтандартныеПодсистемы.СервисМобильнойПодписи
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.СервисМобильнойПодписи") Тогда
		Если НовыйПараметрТипПодписи.РазрешитьОтправкуНаПодписание
			И ЭлектроннаяПодпись.ИспользоватьСервисМобильнойПодписи() Тогда
			ЭлектроннаяПодписьЛокализация.ПриПолученииТиповПодписания(Элементы.ТипПодписанта.СписокВыбора);
		КонецЕсли;
		Элементы.ТипПодписанта.Видимость = Элементы.ТипПодписанта.СписокВыбора.Количество() > 1;
		
		ЭлементСпискаОтправитьНаПодписание = Элементы.ТипПодписанта.СписокВыбора.НайтиПоЗначению("ОтправитьНаПодписание");
		
		Если ЭлементСпискаОтправитьНаПодписание <> Неопределено Тогда
			
			ЭлементСпискаОтправитьНаПодписание.Представление = НСтр("ru = 'В приложении ""Моя подпись""';
																	|en = 'In the ""My signature"" application'");
			
			Если ТипЗнч(Параметры.ОтборСертификатов) = Тип("Структура") И Параметры.ОтборСертификатов.Свойство("Организация") Тогда
				ПараметрыВыбора = Новый Массив;
				ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Организация", Параметры.ОтборСертификатов.Организация));
				Элементы.Подписант.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Параметры.Подписант) Или (ТипЗнч(Параметры.ОтпечаткиСертификатовНаКлиенте) = Тип(
				"Массив") И Параметры.ОтпечаткиСертификатовНаКлиенте.Количество() = 0 Или ТипЗнч(
				Параметры.ОтпечаткиСертификатовНаКлиенте) = Тип("Строка"))
				И Не ЭлектроннаяПодпись.СоздаватьЭлектронныеПодписиНаСервере()
				И Не ЭлектроннаяПодписьСлужебный.ИспользоватьЭлектроннуюПодписьВМоделиСервиса() Тогда

				Если ЗначениеЗаполнено(Подписант) И Параметры.БезПодтверждения Тогда
					БезПодтверждения = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Конец СтандартныеПодсистемы.СервисМобильнойПодписи
	
	// Токен Рутокен
	МобильныйЭДО.НастроитьФормуПодписанияИШифрования(ЭтотОбъект);
	// Конец Токен Рутокен

	ВидимостьСтраницТипаПодписания(ЭтотОбъект);

	ОбновитьВидимостьПредупреждения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВнутренниеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяПоляАктивизироватьПоУмолчанию) Тогда
		ТекущийЭлемент = Элементы[ИмяПоляАктивизироватьПоУмолчанию];
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ОчиститьПеременныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ВРег(ИмяСобытия) = ВРег("Запись_СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		ПодключитьОбработчикОжидания("ПриИзмененииСпискаСертификатов", 0.1, Истина);
		
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("ПодтверждениеВыполнитьОсновнуюОперацию") И Источник = УникальныйИдентификатор Тогда
		Если Параметр.Выполнено Тогда
			ДанныеПакета = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "ДанныеПакета");
			ЭлектроннаяПодписьСлужебныйКлиент.УстановитьСвойстваОблачнойПодписи(ОписаниеДанных, Новый Структура("ДанныеПакета", ДанныеПакета));
			СвойстваОблачнойПодписи = ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьСвойстваОблачнойПодписи(ОписаниеДанных);
			ОповещениеПриПодтверждении = СвойстваОблачнойПодписи.ОповещениеПриПодтверждении;
			
			Если ОповещениеПриПодтверждении = Неопределено Тогда
				ПодписатьДанные(Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект));
			Иначе
				ВыполнитьОбработкуОповещения(ОповещениеПриПодтверждении, ЭтотОбъект);
			КонецЕсли;
		Иначе
			Элементы.Подписать.Видимость = Истина;
			Элементы.Подписать.Доступность = Истина;
			Элементы.Подписать.КнопкаПоУмолчанию = Истина;
			ОбработатьОшибку(Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект),
				Новый Структура("ОписаниеОшибки", Параметр.Ошибка), Новый Структура);
		КонецЕсли;
		
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("ПодтверждениеАвторизация") И Источник = УникальныйИдентификатор Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьОтпечаткиСертификатовНаКлиенте(
			Новый ОписаниеОповещения("СертификатОбработкаВыбораЗавершение", ЭтотОбъект));
	
	ИначеЕсли ВРег(ИмяСобытия) = ВРег("ПодтверждениеПодготовитьДанные") И Источник = УникальныйИдентификатор Тогда
		ВыбранныйСертификат = Новый Структура;
		ВыбранныйСертификат.Вставить("Ссылка",    Сертификат);
		ВыбранныйСертификат.Вставить("Отпечаток", СертификатОтпечаток);
		ВыбранныйСертификат.Вставить("Данные",    СертификатАдрес);
		Параметр.ОписаниеДанных.Вставить("ВыбранныйПодписант",    Неопределено);
		Параметр.ОписаниеДанных.Вставить("ВыбранныйСертификат",   ВыбранныйСертификат);
		Параметр.ОписаниеДанных.Вставить("ВыбраннаяДоверенность", МашиночитаемаяДоверенность);
		Параметр.ОписаниеДанных.Вставить("РезультатПроверкиМЧДДляПодписания", РезультатПроверкиМЧДДляПодписания);

		ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьДанныеДляОблачнойПодписи(
			Параметр.ОбработчикСледующий, Параметр.КонтекстФормы, 
			Параметр.ОписаниеДанных, Параметр.Данные, Истина);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	Если ТипПодписанта = "ОтправитьНаПодписание" Тогда
		Индекс = ПроверяемыеРеквизиты.Найти("Сертификат");
		Если ПроверяемыеРеквизиты.Найти("Сертификат") <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс);
		КонецЕсли;
		Если ПроверяемыеРеквизиты.Найти("Подписант") = Неопределено
			И (Не ЗначениеЗаполнено(ПараметрыПодписанта)
			Или ЗначениеЗаполнено(ПараметрыПодписанта) И Не ЗначениеЗаполнено(ПараметрыПодписанта.ИННФЛ)) Тогда
			ПроверяемыеРеквизиты.Добавить(Подписант);
		КонецЕсли;
		Индекс = ПроверяемыеРеквизиты.Найти("Пароль");
		Если Индекс <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс);
		КонецЕсли;
	Иначе
		Индекс = ПроверяемыеРеквизиты.Найти("Подписант");
		Если ПроверяемыеРеквизиты.Найти("Подписант") <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(Индекс);
		КонецЕсли;
		Если ПроверяемыеРеквизиты.Найти("Сертификат") = Неопределено Тогда
			ПроверяемыеРеквизиты.Добавить("Сертификат");
		КонецЕсли;
		
		Если ПроверяемыеРеквизиты.Найти("Пароль") = Неопределено Тогда
			ПроверяемыеРеквизиты.Добавить("Пароль");
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	СчитыватьСертификатыПриОткрытии = Истина;
	СчитыватьСертификатыПриОткрытииНастройка = Настройки.Получить("СчитыватьСертификатыПриОткрытии");
	
	Если СчитыватьСертификатыПриОткрытииНастройка <> Неопределено Тогда
		СчитыватьСертификатыПриОткрытии = СчитыватьСертификатыПриОткрытииНастройка;
	КонецЕсли;
	
	ПодключитьБиометрию = Ложь;
	ПодключитьБиометриюНастройка = Настройки.Получить("ПодключитьБиометрию");

	Если ПодключитьБиометриюНастройка <> Неопределено Тогда
		ПодключитьБиометрию = ПодключитьБиометриюНастройка;
	КонецЕсли;

	ТипПодписантаНастройка = Настройки.Получить("ТипПодписанта");
	Если ТипПодписантаНастройка <> Неопределено Тогда

		Если СчитыватьСертификатыПриОткрытии = Истина Тогда
			ТипПодписанта = "ПодписатьТокеномРутокен";
		Иначе
			ТипПодписанта = ТипПодписантаНастройка;
		КонецЕсли;

		ВидимостьСтраницТипаПодписания(ЭтотОбъект);
	КонецЕсли;

	ПодписантНастройка = Настройки.Получить("Подписант");
	Если ПодписантНастройка <> Неопределено Тогда
		Подписант = ПодписантНастройка;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)

	Если Источник = "com.e1c.edms" И Событие = "EDMS_RUTOKEN_EVENT" Тогда
		
		ПараметрыОперации = МобильныйЭДОСлужебныйКлиент.НовыйПараметрыОперацииРутокен();
		ПараметрыОперации.Операция = ПараметрыКомпонентыРутокен.ТекущаяОперация;
		ПараметрыОперации.Форма = ЭтотОбъект;
		ПараметрыОперации.ОписаниеДанных = ОписаниеДанных;
		ПараметрыОперации.ПараметрыКомпоненты = ПараметрыКомпонентыРутокен;

		ЗавершитьОбработкуОперации = Новый ОписаниеОповещения("ЗавершитьОбработкуОперацииРутокен",
			МобильныйЭДОКлиент,
			ПараметрыОперации);

		ВыполнитьОбработкуОповещения(ЗавершитьОбработкуОперации, Данные);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	Если ТипПодписанта = "ПодписатьСертификатом" Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ПолеПароляНачалоВыбора(ЭтотОбъект,
			ВнутренниеДанные, СвойстваПароля, СтандартнаяОбработка);
	КонецЕсли;
	
	Если ТипПодписанта = "ПодписатьТокеномРутокен" Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПарольРутокен = Не ПоказатьПарольРутокен ;
		ВведенныйПароль = Элемент.ТекстРедактирования;
		Элемент.РежимПароля = Не ПоказатьПарольРутокен;
		Элемент.КартинкаКнопкиВыбора = ?(ПоказатьПарольРутокен , БиблиотекаКартинок.ВводимыеСимволыСкрыты,
			БиблиотекаКартинок.ВводимыеСимволыВидны);
		Пароль = ВведенныйПароль;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеДанныхНажатие(Элемент, СтандартнаяОбработка)
	
	МобильныйЭДОСлужебныйКлиент.ПредставлениеДанныхНажатие(ЭтотОбъект,
		Элемент, СтандартнаяОбработка, ТекущийСписокПредставлений);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатПриИзменении(Элемент)
	
	Если ТипПодписанта = "ПодписатьСертификатом" Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьОтпечаткиСертификатовНаКлиенте(
			Новый ОписаниеОповещения("СертификатПриИзмененииЗавершение", ЭтотОбъект));
	КонецЕсли;

	Если ТипПодписанта = "ПодписатьТокеномРутокен" Тогда
		МобильныйЭДОСлужебныйКлиент.ЗаполнитьСвойстваПоСертификату(ЭтотОбъект, ПараметрыКомпонентыРутокен);
		МобильныйЭДОСлужебныйКлиент.ЗаполнитьПоДаннымБиометрии(ЭтотОбъект);
	КонецЕсли

КонецПроцедуры

// Продолжение процедуры СертификатПриИзменении.
&НаКлиенте
Процедура СертификатПриИзмененииЗавершение(ОтпечаткиСертификатовНаКлиенте, Контекст) Экспорт
	
	СертификатПриИзмененииНаСервере(ОтпечаткиСертификатовНаКлиенте);
	
	ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект, ВнутренниеДанные, СвойстваПароля, Новый Структура("ПриОткрытии", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипПодписанта = "ПодписатьСертификатом" Тогда
		Если ТипЗнч(ОтборСертификатов) = Тип("СписокЗначений") И ОтборСертификатов.Количество() > 0 Тогда
			ЭлектроннаяПодписьСлужебныйКлиент.НачалоВыбораСертификатаПриУстановленномОтборе(ЭтотОбъект);
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ВыбранныйСертификат", Сертификат);
		ПараметрыФормы.Вставить("ДляШифрованияИРасшифровки", Ложь);
		ПараметрыФормы.Вставить("ВернутьПароль", Истина);
		Если ТипЗнч(ОтборСертификатов) = Тип("Структура") И ОтборСертификатов.Свойство("Организация") Тогда
			ПараметрыФормы.Вставить("ОтборПоОрганизации", ОтборСертификатов);
		КонецЕсли;
		ПараметрыФормы.Вставить("ВыполнятьНаСервере", ВыполнятьНаСервере);
		ПараметрыФормы.Вставить("ОтображатьКомандыДобавления", Истина);
		
		ИнтеграцияБСПБЭДСлужебныйКлиент.ВыборСертификатаДляПодписанияИлиРасшифровки(ПараметрыФормы);
		
	КонецЕсли;
	
	Если ТипПодписанта = "ПодписатьТокеномРутокен" И ПараметрыКомпонентыРутокен.СодержимоеКонтейнераПрочитано <> Истина Тогда
		НачатьОперациюСКонтейнеромРутокен();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СертификатОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ТипПодписанта = "ПодписатьСертификатом" Тогда
		Если ВыбранноеЗначение = Истина Тогда
			Сертификат = ВнутренниеДанные["ВыбранныйСертификат"];
			ВнутренниеДанные.Удалить("ВыбранныйСертификат");
		Иначе
			Сертификат = ВыбранноеЗначение;
		КонецЕсли;
		
		ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьОтпечаткиСертификатовНаКлиенте(
			Новый ОписаниеОповещения("СертификатОбработкаВыбораЗавершение", ЭтотОбъект, ВыбранноеЗначение));
	КонецЕсли;
	
	Если ТипПодписанта = "ПодписатьТокеномРутокен" Тогда
		Если ЗначениеЗаполнено(Сертификат) Тогда
			ПерейтиПоНавигационнойСсылке(ПолучитьНавигационнуюСсылку(Сертификат));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры СертификатОбработкаВыбора.
&НаКлиенте
Процедура СертификатОбработкаВыбораЗавершение(ОтпечаткиСертификатовНаКлиенте, ВыбранноеЗначение) Экспорт
	
	СертификатПриИзмененииНаСервере(ОтпечаткиСертификатовНаКлиенте);
	
	Если ВыбранноеЗначение = Истина
	   И ВнутренниеДанные["ВыбранныйСертификатПароль"] <> Неопределено Тогда
		
		ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект,
			ВнутренниеДанные, СвойстваПароля,, ВнутренниеДанные["ВыбранныйСертификатПароль"]);
		ВнутренниеДанные.Удалить("ВыбранныйСертификатПароль");
	Иначе
		ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект, ВнутренниеДанные, СвойстваПароля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ЭлектроннаяПодписьСлужебныйКлиент.СертификатПодборИзСпискаВыбора(ЭтотОбъект, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	ЭлектроннаяПодписьСлужебныйКлиент.СертификатПодборИзСпискаВыбора(ЭтотОбъект, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	Если ТипПодписанта = "ПодписатьСертификатом" Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект,
			ВнутренниеДанные, СвойстваПароля, Новый Структура("ПриИзмененииРеквизитаПароль", Истина));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьПарольПриИзменении(Элемент)
	
	ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект,
		ВнутренниеДанные, СвойстваПароля, Новый Структура("ПриИзмененииРеквизитаЗапомнитьПароль", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеУстановленногоПароляНажатие(Элемент)
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПояснениеУстановленногоПароляНажатие(ЭтотОбъект, Элемент, СвойстваПароля);
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеУстановленногоПароляРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, СтандартнаяОбработка)
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПояснениеУстановленногоПароляОбработкаНавигационнойСсылки(
		ЭтотОбъект, Элемент, НавигационнаяСсылка, СтандартнаяОбработка, СвойстваПароля);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПодписиОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипПодписантаПриИзменении(Элемент)
	
	ВидимостьСтраницТипаПодписания(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписантОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	// СтандартныеПодсистемы.СервисМобильнойПодписи
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.СервисМобильнойПодписи") Тогда
		Возврат;
	КонецЕсли;
	МодульСервисМобильнойПодписиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СервисМобильнойПодписиКлиент");
	МодульСервисМобильнойПодписиКлиент.ПодписантПриОткрытии(ЭтотОбъект, Элемент, СтандартнаяОбработка);
	// Конец СтандартныеПодсистемы.СервисМобильнойПодписи
	
КонецПроцедуры


&НаКлиенте
Процедура ПодключитьБиометриюПриИзменении(Элемент)
	МобильныйЭДОСлужебныйКлиент.ПодключитьОтключитьБиометрию(ЭтотОбъект, "ПарольРутокен");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подписать(Команда)
	
	ОписаниеДанных.Вставить("ПользовательНажалКнопкуПодписать", Истина);
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипПодписанта = "ПодписатьСертификатом" Или ТипПодписанта = "ПодписатьТокеномРутокен" Тогда
		
		Если Не Элементы.Подписать.Доступность Тогда
			Возврат;
		КонецЕсли;
			
		Элементы.Подписать.Доступность = Ложь;
		ОписаниеДанных.Вставить("ПодписаниеВЭтомПриложении", Истина);
		ПодписатьДанные(Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект));

	// СтандартныеПодсистемы.СервисМобильнойПодписи
	Иначе
		
		Если Не Элементы.Подписать.Доступность Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.Подписать.Доступность = Ложь;
		ОписаниеДанных.Вставить("ПодписаниеВЭтомПриложении", Ложь);
		ПодписатьДанныеВСервисеМобильнойПодписи(Новый ОписаниеОповещения("ПодписатьЗавершение", ЭтотОбъект));
		
	// Конец СтандартныеПодсистемы.СервисМобильнойПодписи
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры Подписать.
&НаКлиенте
Процедура ПодписатьЗавершение(Результат, Контекст) Экспорт
	
	Элементы.Подписать.Доступность = Истина;
	
	Если Результат = Истина Тогда
		Закрыть(Истина);
	ИначеЕсли ЭлектроннаяПодписьСлужебныйКлиент.ЭтоОперацияОблачнойПодписи(ЭтотОбъект) Тогда
		Элементы.Подписать.Видимость = Истина;
		Элементы.Подписать.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСодержимоеКонтейнераРутокен(Команда)
	
	ПараметрыКомпонентыРутокен.СодержимоеКонтейнераПрочитано = Ложь;
	
	НачатьОперациюСКонтейнеромРутокен();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВидимостьСтраницТипаПодписания(Форма)
	
	ВидимостьКомандаОблачногоСервиса = Ложь;

	Если Форма.ТипПодписанта = "ПодписатьСертификатом" Тогда

		Форма.Элементы.СтраницыПодписание.ТекущаяСтраница = Форма.Элементы.СтраницаПодписание;
		ВидимостьКомандаОблачногоСервиса = Истина;
		Форма.ИмяПоляАктивизироватьПоУмолчанию = "Сертификат";
	
	ИначеЕсли Форма.ТипПодписанта = "ПодписатьТокеномРутокен" Тогда

		Форма.Элементы.СтраницыПодписание.ТекущаяСтраница = Форма.Элементы.СтраницаПодписаниеРутокен;
		Форма.ИмяПоляАктивизироватьПоУмолчанию = "СертификатРутокен";
	Иначе

		Форма.Элементы.СтраницыПодписание.ТекущаяСтраница = Форма.Элементы.СтраницаОтправитьНаПодписание;
		Форма.ИмяПоляАктивизироватьПоУмолчанию = "Подписант";
	КонецЕсли;
	
	Форма.Элементы.ГруппаКонтейнер.Видимость = ВидимостьКомандаОблачногоСервиса;
	Форма.Элементы.ГруппаКонтейнер1.Видимость = ВидимостьКомандаОблачногоСервиса;
	Форма.Элементы.ГруппаКомандыПодтверждения.Видимость = ВидимостьКомандаОблачногоСервиса;

КонецПроцедуры

&НаСервере
Процедура УстановитьТипПодписи(Знач ПараметрТипПодписи)
	
	ПроверятьСертификат = ПараметрТипПодписи.ПроверятьСертификат;
	
	Элементы.ТипПодписи.СписокВыбора.Очистить();
	Элементы.ТипПодписи.СписокВыбора.ЗагрузитьЗначения(ПараметрТипПодписи.ТипыПодписи);
	
	ТипПодписи = ПараметрТипПодписи.ТипыПодписи[0];
	
	Элементы.ТипПодписи.Видимость = ПараметрТипПодписи.Видимость;
	Элементы.ТипПодписи.Доступность = ПараметрТипПодписи.Доступность;
	
	Элементы.ГруппаДоверенность.Видимость = Ложь;
	
	Элементы.ПакетнаяОтправка.Видимость = ПараметрТипПодписи.ПакетнаяОтправкаНаПодписание = Неопределено;
	ПакетнаяОтправка = ПараметрТипПодписи.ПакетнаяОтправкаНаПодписание;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОшибкаСертификатПомеченКакОтозванный()
	
	Возврат ЭлектроннаяПодписьСлужебный.ОшибкаСертификатПомеченКакОтозванный()
	
КонецФункции

&НаКлиенте
Процедура ПродолжитьОткрытие(Оповещение, ОбщиеВнутренниеДанные, КлиентскиеПараметры) Экспорт
	
	Если КлиентскиеПараметры = ВнутренниеДанные Тогда
		КлиентскиеПараметры = Новый Структура("Сертификат, СвойстваПароля", Сертификат, СвойстваПароля);
		Возврат;
	КонецЕсли;
	
	Если КлиентскиеПараметры.Свойство("УказанКонтекстДругойОперации") Тогда
		СвойстваСертификата = ОбщиеВнутренниеДанные;
		КлиентскиеПараметры.ОписаниеДанных.КонтекстОперации.ПродолжитьОткрытие(Неопределено, Неопределено, СвойстваСертификата);
		Если СвойстваСертификата.Сертификат = Сертификат Тогда
			СвойстваПароля = СвойстваСертификата.СвойстваПароля;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеДанных             = КлиентскиеПараметры.ОписаниеДанных;
	Элементы.ПакетнаяОтправка.Видимость = Элементы.ПакетнаяОтправка.Видимость И ОписаниеДанных.Свойство("НаборДанных")
		И ОписаниеДанных.НаборДанных.Количество() > 1;
	ФормаОбъекта               = КлиентскиеПараметры.Форма;
	ТекущийСписокПредставлений = КлиентскиеПараметры.ТекущийСписокПредставлений;
	
	ВнутренниеДанные = ОбщиеВнутренниеДанные;
	Контекст = Новый Структура("Оповещение", Оповещение);
	Оповещение = Новый ОписаниеОповещения("ПродолжитьОткрытие", ЭтотОбъект);
	
	ПродолжитьОткрытиеПослеНачала(Истина, Контекст);
	
	ПараметрыКомпонентыРутокен = МобильныйЭДОСлужебныйКлиент.НовыйПараметрыКомпонентыРутокен();

	СистемнаяИнформация = Новый СистемнаяИнформация;
	ВерсияОС = СистемнаяИнформация.ВерсияОС;
	ПараметрыКомпонентыРутокен.ПодписаниеЧерезФайловоеХранилище = (СтрНайти(НРег(ВерсияОС), "android") > 0);

	Если СчитыватьСертификатыПриОткрытии Тогда
		НачатьОперациюСКонтейнеромРутокен();
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПродолжитьОткрытие.
&НаКлиенте
Процедура ПродолжитьОткрытиеПослеНачала(Результат, Контекст) Экспорт
	
	Если Результат <> Истина Тогда
		ПродолжитьОткрытиеЗавершение(Контекст);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПриОткрытии", Истина);
	Если СвойстваПароля <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("ПриУстановкеПароляИзДругойОперации", Истина);
	КонецЕсли;
	ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект,
		ВнутренниеДанные, СвойстваПароля, ДополнительныеПараметры);
	
	ОткрытьДляВыбораДоверенности = Ложь;
	Если ПоДоверенности И Не ЗначениеЗаполнено(МашиночитаемаяДоверенность) Тогда
		ОткрытьДляВыбораДоверенности = Истина;
	КонецЕсли;
	
	Если БезПодтверждения И Не ОткрытьДляВыбораДоверенности Тогда
		
		Если ТипПодписанта = "ПодписатьСертификатом" И (ДополнительныеПараметры.ПарольУказан
			Или ДополнительныеПараметры.ВводитьПарольВПрограммеЭлектроннойПодписи
			Или ОблачныйПарольПодтвержден) Тогда
				
			ОписаниеДанных.Вставить("ПодписаниеВЭтомПриложении", Истина);

			ОбработкаПослеПредупреждения = Неопределено;
			ПодписатьДанные(Новый ОписаниеОповещения("ПродолжитьОткрытиеПослеПодписанияДанных", ЭтотОбъект, Контекст));
			Возврат;
		КонецЕсли;
			
		// СтандартныеПодсистемы.СервисМобильнойПодписи
		Если ТипПодписанта = "ОтправитьНаПодписание" И ЗначениеЗаполнено(Подписант) Тогда
			
			ОписаниеДанных.Вставить("ПодписаниеВЭтомПриложении", Ложь);
			ОбработкаПослеПредупреждения = Неопределено;
			ПодписатьДанныеВСервисеМобильнойПодписи(Новый ОписаниеОповещения("ПродолжитьОткрытиеПослеПодписанияДанных", ЭтотОбъект, Контекст));
			Возврат;
		// Конец СтандартныеПодсистемы.СервисМобильнойПодписи
		КонецЕсли;
		
	КонецЕсли;
		
	Открыть();
	
	ПродолжитьОткрытиеЗавершение(Контекст);
	
КонецПроцедуры

// Продолжение процедуры ПродолжитьОткрытие.
&НаКлиенте
Процедура ПродолжитьОткрытиеПослеПодписанияДанных(Результат, Контекст) Экспорт
	
	ПродолжитьОткрытиеЗавершение(Контекст, Результат = Истина);
	
КонецПроцедуры

// Продолжение процедуры ПродолжитьОткрытие.
&НаКлиенте
Процедура ПродолжитьОткрытиеЗавершение(Контекст, Результат = Неопределено)
	
	Если Не Открыта() Тогда
		ОчиститьПеременныеФормы();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьПредупреждения()
	
	Элементы.ГруппаОтозван.Видимость = СертификатОтозван;
	
	Если ЗначениеЗаполнено(РезультатПроверкиУдостоверяющегоЦентра) Тогда
		Элементы.ГруппаДополнительныеСведения.Видимость = Не СертификатОтозван И ЗначениеЗаполнено(
			РезультатПроверкиУдостоверяющегоЦентра.Предупреждение.ДополнительныеСведения);
		Элементы.ДекорацияДополнительныеСведения.Заголовок =
			РезультатПроверкиУдостоверяющегоЦентра.Предупреждение.ДополнительныеСведения;
	Иначе
		Элементы.ГруппаДополнительныеСведения.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПеременныеФормы()
	
	ОписаниеДанных             = Неопределено;
	ФормаОбъекта               = Неопределено;
	ТекущийСписокПредставлений = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Функция ПеременныеОчищены()
	
	Возврат ОписаниеДанных = Неопределено
		И ФормаОбъекта = Неопределено
		И ТекущийСписокПредставлений = Неопределено;
	
КонецФункции

// АПК:78-выкл: для безопасной передачи данных на клиенте между формами, не отправляя их на сервер.
&НаКлиенте
Процедура ВыполнитьПодписание(КлиентскиеПараметры, ОбработкаЗавершения) Экспорт
// АПК:78-вкл: для безопасной передачи данных на клиенте между формами, не отправляя их на сервер.
	
	ЭлектроннаяПодписьСлужебныйКлиент.ОбновитьФормуПередПовторнымИспользованием(ЭтотОбъект, КлиентскиеПараметры);
	
	ОписаниеДанных             = КлиентскиеПараметры.ОписаниеДанных;
	ФормаОбъекта               = КлиентскиеПараметры.Форма;
	ТекущийСписокПредставлений = КлиентскиеПараметры.ТекущийСписокПредставлений;
	
	ОбработкаПослеПредупреждения = ОбработкаЗавершения;
	
	Контекст = Новый Структура("ОбработкаЗавершения", ОбработкаЗавершения);
	ПодписатьДанные(Новый ОписаниеОповещения("ВыполнитьПодписаниеЗавершение", ЭтотОбъект, Контекст));
	
КонецПроцедуры

// Продолжение процедуры ВыполнитьПодписание.
&НаКлиенте
Процедура ВыполнитьПодписаниеЗавершение(Результат, Контекст) Экспорт
	
	ВыполнитьОбработкуОповещения(Контекст.ОбработкаЗавершения, Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСпискаСертификатов()
	
	ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьОтпечаткиСертификатовНаКлиенте(
		Новый ОписаниеОповещения("ПриИзмененииСпискаСертификатовЗавершение", ЭтотОбъект));
	
КонецПроцедуры

// Продолжение процедуры ПриИзмененииСпискаСертификатов.
&НаКлиенте
Процедура ПриИзмененииСпискаСертификатовЗавершение(ОтпечаткиСертификатовНаКлиенте, Контекст) Экспорт
	
	СертификатПриИзмененииНаСервере(ОтпечаткиСертификатовНаКлиенте, Истина);
	
	ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект,
		ВнутренниеДанные, СвойстваПароля, Новый Структура("ПриИзмененииСвойствСертификата", Истина));
	
КонецПроцедуры

&НаСервере
Процедура СертификатПриИзмененииНаСервере(ОтпечаткиСертификатовНаКлиенте, ПроверитьСсылку = Ложь)
	
	Если ПроверитьСсылку
		И ЗначениеЗаполнено(Сертификат)
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Сертификат, "Ссылка") <> Сертификат Тогда
		
		Сертификат = Неопределено;
	КонецЕсли;
	
	ЭлектроннаяПодписьСлужебный.СертификатПриИзмененииНаСервере(ЭтотОбъект, ОтпечаткиСертификатовНаКлиенте);
	
	ОбновитьВидимостьПредупреждения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДанные(Оповещение)
	
	ОписаниеДанных.Вставить("ТипПодписи", ТипПодписи);
	
	Если ЭлектроннаяПодписьСлужебныйКлиент.ЭтоОперацияОблачнойПодписи(ЭтотОбъект) Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.УстановитьСвойстваОблачнойПодписи(ОписаниеДанных,
			Новый Структура("УчетнаяЗапись, ДанныеПодтверждения", 
					ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьДанныеОблачнойПодписи(ЭтотОбъект, "НастройкиПользователя"),
					ЭлектроннаяПодписьСлужебныйКлиент.ПолучитьДанныеОблачнойПодписи(ЭтотОбъект, "ДанныеПодтверждения")));
	Иначе	
		ЭлектроннаяПодписьСлужебныйКлиент.УстановитьСвойстваОблачнойПодписи(ОписаниеДанных,
			Новый Структура("УчетнаяЗапись, ДанныеПодтверждения"));
	КонецЕсли;	
	
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Контекст.Вставить("ОшибкаНаКлиенте", Новый Структура);
	Контекст.Вставить("ОшибкаНаСервере", Новый Структура);
	
	Если СертификатДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
		
		ДатаОкончанияЗакрытогоКлюча = ДатаОкончанияЗакрытогоКлюча(СертификатАдрес);
		Если ЗначениеЗаполнено(ДатаОкончанияЗакрытогоКлюча) И ДатаОкончанияЗакрытогоКлюча
			< ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", НСтр("ru = 'У выбранного сертификата истек срок действия закрытого ключа.
																	 |Выберите другой сертификат.';
																	 |en = 'The selected certificate''s private key has expired.
																	 |Select another certificate.'"));
		Иначе
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", НСтр("ru = 'У выбранного сертификата истек срок действия.
																	 |Выберите другой сертификат.';
																	 |en = 'The selected certificate has expired.
																	 |Select another certificate.'"));
		КонецЕсли;
		
		ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере);
		Возврат;
	КонецЕсли;

	Если СертификатОтозван Тогда
		Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки",
			НСтр("ru = 'Сертификат помечен в приложении как отозванный.
			|Выберите другой сертификат.';
			|en = 'The certificate is marked as revoked.
			|Select another certificate.'"));
		ДополнительныеДанныеОшибки = Новый Структура("ДополнительныеДанныеПроверокНаКлиенте", ОшибкаСертификатПомеченКакОтозванный());
		ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере,,ДополнительныеДанныеОшибки);
		Возврат;
	КонецЕсли;
	
	// СтандартныеПодсистемы.МашиночитаемыеДоверенности
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.МашиночитаемыеДоверенности")
		И ПоДоверенности
		И ЗначениеЗаполнено(МашиночитаемаяДоверенность) Тогда
		
		МодульМашиночитаемыеДоверенностиФНССлужебныйКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("МашиночитаемыеДоверенностиФНССлужебныйКлиент");
		РезультатПроверкиМЧДДляПодписания = МодульМашиночитаемыеДоверенностиФНССлужебныйКлиент.РезультатПроверкиМЧДДляПодписания(
			МашиночитаемаяДоверенность, СертификатАдрес);
		Если РезультатПроверкиМЧДДляПодписания.ПроверкаПодписанта.Верна = Ложь Тогда
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", РезультатПроверкиМЧДДляПодписания.ПроверкаПодписанта.ТекстПроверки);
			ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Конец СтандартныеПодсистемы.МашиночитаемыеДоверенности
	
	Если ЗначениеЗаполнено(РезультатПроверкиУдостоверяющегоЦентра)
		И Не РезультатПроверкиУдостоверяющегоЦентра.Действует Тогда
		
		Если РезультатПроверкиУдостоверяющегоЦентра.Предупреждение.РазрешитьПодписание Тогда
			ПодписаниеРазрешено = ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекЗагрузить(
				Сертификат, "РазрешитьПодписание", Неопределено);
			
			Если ПодписаниеРазрешено = Неопределено Тогда
				Оповещение = Новый ОписаниеОповещения("ПодписатьДанныеПослеОтветаНаВопросОбУЦ", ЭтотОбъект, Контекст);
				
				ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
				ПараметрыВопроса.Картинка = БиблиотекаКартинок.ДиалогВосклицание;
				ПараметрыВопроса.Заголовок = НСтр("ru = 'Запрос разрешения на подпись';
													|en = 'Request for permission to sign'");
				ПараметрыВопроса.ТекстФлажка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Запомнить выбор для сертификата ""%1""';
						|en = 'Remember my choice for certificate ""%1""'"), Сертификат);
				
				Кнопки = Новый СписокЗначений;
				Кнопки.Добавить(Истина, НСтр("ru = 'Разрешить подписание';
											|en = 'Allow signing'"));
				Кнопки.Добавить(Ложь,   НСтр("ru = 'Отменить подписание';
											|en = 'Cancel signing'"));
				
				ПредупреждениеОбОшибке = РезультатПроверкиУдостоверяющегоЦентра.Предупреждение; // см. ЭлектроннаяПодписьСлужебныйКлиентСервер.ПредупреждениеПриПроверкеУдостоверяющегоЦентраСертификата
				СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ПредупреждениеОбОшибке.ТекстОшибки, Кнопки, ПараметрыВопроса);
				Возврат;
			КонецЕсли;
		Иначе
			ПодписаниеРазрешено = Ложь;
		КонецЕсли;
		
		ПодписатьДанныеПослеОтветаНаВопросОбУЦ(ПодписаниеРазрешено, Контекст);
		Возврат;
	КонецЕсли;
	
	ПодписатьДанныеПослеОтветаНаВопросОбУЦ(Истина, Контекст);
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеОтветаНаВопросОбУЦ(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		БольшеНеЗадаватьЭтотВопрос = Результат.БольшеНеЗадаватьЭтотВопрос;
		Результат = Результат.Значение;
		
		Если БольшеНеЗадаватьЭтотВопрос Тогда
			ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(
				Сертификат, "РазрешитьПодписание", Результат);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПроверятьСертификат = ЭлектроннаяПодписьСлужебныйКлиентСервер.НеПроверятьСертификат() Тогда
		ПодписатьДанныеПослеПроверкиВыбранногоСертификата(Истина, Контекст);
	Иначе
		Если Результат = Неопределено Или Не Результат Тогда
			ПредупреждениеОбОшибке = РезультатПроверкиУдостоверяющегоЦентра.Предупреждение; // см. ЭлектроннаяПодписьСлужебныйКлиентСервер.ПредупреждениеПриПроверкеУдостоверяющегоЦентраСертификата
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", ПредупреждениеОбОшибке.ТекстОшибки);
			ДополнительныеДанныеОшибки = Новый Структура("ДополнительныеДанныеПроверокНаКлиенте", ПредупреждениеОбОшибке);
			ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере, ,
				ДополнительныеДанныеОшибки);
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметрыПроверок = ЭлектроннаяПодписьСлужебныйКлиент.ДополнительныеПараметрыПроверкиСертификата();
		ДополнительныеПараметрыПроверок.ПоказатьОшибку = Ложь;
		ДополнительныеПараметрыПроверок.ДляПроверкиПодписи = Истина;
		ДополнительныеПараметрыПроверок.ОбъединитьОшибкиДанныхСертификата = Ложь;
		ДополнительныеПараметрыПроверок.ВыполнятьПроверкуУдостоверяющегоЦентра = ЭлектроннаяПодписьСлужебныйКлиентСервер.НеПроверятьСертификат();
	
		ЭлектроннаяПодписьСлужебныйКлиент.ПроверитьСертификат(Новый ОписаниеОповещения(
				"ПодписатьДанныеПослеПроверкиВыбранногоСертификата", ЭтотОбъект, Контекст),
			СертификатАдрес,,, ДополнительныеПараметрыПроверок);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеПроверкиВыбранногоСертификата(Результат, Контекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда // Ошибка при проверке сертификата.
		
		Если Результат.СертификатОтозван Тогда
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", НСтр("ru = 'Сертификат отозван.
																	 |Выберите другой сертификат.';
																	 |en = 'The certificate is revoked.
																	 |Select another certificate.'"));
			ДополнительныеДанныеОшибки = Новый Структура("ДополнительныеДанныеПроверокНаКлиенте",
				ОшибкаСертификатПомеченКакОтозванный());
			ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере, ,
				ДополнительныеДанныеОшибки);
			Возврат;
		КонецЕсли;
		
		Контекст.Вставить("СертификатВерен", Ложь);
		Контекст.Вставить("ТребуетсяПроверка", Результат.ТребуетсяПроверка);
		
		Оповещение = Новый ОписаниеОповещения("ПодписатьДанныеПослеПредупрежденияПодписьНеверна", ЭтотОбъект,
			Новый Структура("Контекст, РезультатПроверкиСертификата", Контекст, Результат));
		
		ПараметрыВопроса = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
		ПараметрыВопроса.Картинка = БиблиотекаКартинок.ДиалогВосклицание;
		ПараметрыВопроса.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить("ОтменитьПодписание",  НСтр("ru = 'Отменить подписание';
													|en = 'Cancel signing'"));
		Кнопки.Добавить("РазрешитьПодписание", НСтр("ru = 'Разрешить подписание';
													|en = 'Allow signing'"));
		Кнопки.Добавить("ПоказатьОшибку",      НСтр("ru = 'Подробности...';
													|en = 'Details...'"));
		
		Если Результат.ТребуетсяПроверка = Истина Тогда
			ПараметрыВопроса.Заголовок = НСтр("ru = 'Подпись будет не проверена';
												|en = 'Signature will not be verified'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Сертификат не прошел проверку. Подпись, установленная таким сертификатом, будет требовать проверки.
					|
					|%1';
					|en = 'The certificate failed validation. Any signature created with this certificate will require verification.
					|
					|%1'"), ЭлектроннаяПодписьСлужебныйКлиентСервер.ОбщееОписаниеОшибки(
				Результат.ОписаниеОшибкиНаКлиенте, Результат.ОписаниеОшибкиНаСервере, Неопределено));
		Иначе
			ПараметрыВопроса.Заголовок = НСтр("ru = 'Подпись будет неверна';
												|en = 'The signature will be invalid'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Сертификат не прошел проверку. Подпись, установленная таким сертификатом, будет неверна.
						|
						|%1';
						|en = 'The certificate has failed verification. A signature created by this certificate will be invalid.
						|
						|%1'"), ЭлектроннаяПодписьСлужебныйКлиентСервер.ОбщееОписаниеОшибки(
					Результат.ОписаниеОшибкиНаКлиенте, Результат.ОписаниеОшибкиНаСервере, Неопределено));
		КонецЕсли;
		
		СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(Оповещение, ТекстОшибки, Кнопки, ПараметрыВопроса);
		Возврат;
		
	КонецЕсли;
	
	Контекст.Вставить("СертификатВерен",   Истина);
	Контекст.Вставить("ТребуетсяПроверка", Ложь);
	ПодписатьДанныеПослеПредупрежденияПодписьНеверна(Новый Структура("Значение", "РазрешитьПодписание"), 
		Новый Структура("Контекст", Контекст));
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеПредупрежденияПодписьНеверна(Результат, ДополнительныеПараметры) Экспорт
	
	Контекст = ДополнительныеПараметры.Контекст;

	Если Результат = Неопределено Или Результат.Значение = "ОтменитьПодписание" Или Результат.Значение = "ПоказатьОшибку" Тогда
		
		Если ЗначениеЗаполнено(ДополнительныеПараметры.РезультатПроверкиСертификата.ОписаниеОшибкиНаКлиенте) Тогда
			Контекст.ОшибкаНаКлиенте.Вставить("ОписаниеОшибки", ДополнительныеПараметры.РезультатПроверкиСертификата.ОписаниеОшибкиНаКлиенте);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДополнительныеПараметры.РезультатПроверкиСертификата.ОписаниеОшибкиНаСервере) Тогда
			Контекст.ОшибкаНаСервере.Вставить("ОписаниеОшибки", ДополнительныеПараметры.РезультатПроверкиСертификата.ОписаниеОшибкиНаСервере);
		КонецЕсли;
		
		ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере,,, Результат <> Неопределено И Результат.Значение = "ПоказатьОшибку");
		Возврат;
	
	КонецЕсли;
		
	ВыбранныйСертификат = Новый Структура;
	ВыбранныйСертификат.Вставить("Ссылка",    Сертификат);
	ВыбранныйСертификат.Вставить("Отпечаток", СертификатОтпечаток);
	ВыбранныйСертификат.Вставить("Данные",    СертификатАдрес);
	ОписаниеДанных.Вставить("ВыбранныйПодписант",    Неопределено);
	ОписаниеДанных.Вставить("ВыбранныйСертификат",   ВыбранныйСертификат);
	ОписаниеДанных.Вставить("ВыбраннаяДоверенность", МашиночитаемаяДоверенность);
	ОписаниеДанных.Вставить("РезультатПроверкиМЧДДляПодписания", РезультатПроверкиМЧДДляПодписания);
	
	Если ОписаниеДанных.Свойство("ПередВыполнением")
	   И ТипЗнч(ОписаниеДанных.ПередВыполнением) = Тип("ОписаниеОповещения") Тогда
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("ОписаниеДанных", ОписаниеДанных);
		ПараметрыВыполнения.Вставить("Оповещение", Новый ОписаниеОповещения(
			"ПодписатьДанныеПослеОбработкиПередВыполнением", ЭтотОбъект, Контекст));
		
		ВыполнитьОбработкуОповещения(ОписаниеДанных.ПередВыполнением, ПараметрыВыполнения);
	Иначе
		ПодписатьДанныеПослеОбработкиПередВыполнением(Новый Структура, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеОбработкиПередВыполнением(Результат, Контекст) Экспорт
	
	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("ОписаниеОшибки") Тогда
		ОбработатьОшибку(Контекст.Оповещение, Новый Структура("ОписаниеОшибки", Результат.ОписаниеОшибки), Новый Структура);
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	Если ТипЗнч(ФормаОбъекта) = Тип("ФормаКлиентскогоПриложения") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта.УникальныйИдентификатор;
	ИначеЕсли ТипЗнч(ФормаОбъекта) = Тип("УникальныйИдентификатор") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта;
	КонецЕсли;
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ОписаниеДанных",     ОписаниеДанных);
	ПараметрыВыполнения.Вставить("Форма",              ЭтотОбъект);
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", Контекст.ИдентификаторФормы);
	ПараметрыВыполнения.Вставить("ЗначениеПароля",     СвойстваПароля.Значение);
	ПараметрыВыполнения.Вставить("СертификатАдрес",    СертификатАдрес);
	ПараметрыВыполнения.Вставить("СертификатВерен",    Контекст.СертификатВерен);
	ПараметрыВыполнения.Вставить("ТребуетсяПроверка",  Контекст.ТребуетсяПроверка);
	
	ПараметрыВыполнения.Вставить("ПолноеПредставлениеДанных",
		МобильныйЭДОСлужебныйКлиент.ПолноеПредставлениеДанных(ЭтотОбъект));
	ПараметрыВыполнения.Вставить("ТекущийСписокПредставлений", ТекущийСписокПредставлений);
	
	Контекст.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	
	Если ЭлектроннаяПодписьКлиент.СоздаватьЭлектронныеПодписиНаСервере()
	   И ВыполнятьНаСервере <> Ложь Тогда
		
		Если ЗначениеЗаполнено(СертификатНаСервереОписаниеОшибки) Тогда
			Результат = Новый Структура("Ошибка", СертификатНаСервереОписаниеОшибки);
			СертификатНаСервереОписаниеОшибки = Новый Структура;
			ПодписатьДанныеПослеВыполненияНаСторонеСервера(Результат, Контекст);
		Иначе
			// Попытка подписания на сервере.
			ЭлектроннаяПодписьСлужебныйКлиент.ВыполнитьНаСтороне(Новый ОписаниеОповещения(
					"ПодписатьДанныеПослеВыполненияНаСторонеСервера", ЭтотОбъект, Контекст),
				"Подписание", "НаСторонеСервера", Контекст.ПараметрыВыполнения);
		КонецЕсли;
	Иначе
		ПодписатьДанныеПослеВыполненияНаСторонеСервера(Неопределено, Контекст);
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Асинх Процедура ПодписатьДанныеПослеВыполненияНаСторонеСервера(Результат, Контекст) Экспорт
	
	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		ПодписатьДанныеПослеВыполнения(Результат);
	КонецЕсли;
	
	Если Результат <> Неопределено И Не Результат.Свойство("Ошибка") Тогда
		ПодписатьДанныеПослеВыполненияНаСторонеКлиента(Новый Структура, Контекст);
	Иначе
		Если Результат <> Неопределено Тогда
			Контекст.ОшибкаНаСервере = Результат.Ошибка;
			Если ВыполнятьНаСервере = Истина Тогда
				ПодписатьДанныеПослеВыполненияНаСторонеКлиента(Новый Структура, Контекст);
				Возврат;
			КонецЕсли;
		КонецЕсли;

		// Попытка подписания на клиенте.
		Если ТипПодписанта = "ПодписатьТокеномРутокен" Тогда
			
			КриптографияБЭДСлужебныйКлиент.ПреобразоватьДанныеНабораДанныхИзОписанияОповещения(ОписаниеДанных);
			
			ПараметрыКомпонентыРутокен.ОписаниеОповещенияПослеОперации = Новый ОписаниеОповещения(
				"ПодписатьДанныеПослеВыполненияНаСторонеКлиента",
				ЭтотОбъект,
				Контекст);
	
			НачатьОперациюСКонтейнеромРутокен(ПредопределенноеЗначение("Перечисление.ОперацииРутокенМобильногоЭДО.Подписание"),
				ОписаниеДанных);
		Иначе
			
			ЭлектроннаяПодписьСлужебныйКлиент.ВыполнитьНаСтороне(Новый ОписаниеОповещения(
					"ПодписатьДанныеПослеВыполненияНаСторонеКлиента", ЭтотОбъект, Контекст),
				"Подписание", "НаСторонеКлиента", Контекст.ПараметрыВыполнения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеВыполненияНаСторонеКлиента(Результат, Контекст) Экспорт
	
	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;
	
	ПодписатьДанныеПослеВыполнения(Результат);
	
	Если Результат.Свойство("Ошибка") Тогда
		
		Если ОписаниеДанных.Свойство("КонтекстОперации") Тогда
			ОписаниеДанных.КонтекстОперации = ЭтотОбъект;
		КонецЕсли;
		
		Контекст.ОшибкаНаКлиенте = Результат.Ошибка;
		НеподписанныеДанные = ЭлектроннаяПодписьСлужебныйКлиент.СвойстваТекущегоЭлементаДанных(
			Контекст.ПараметрыВыполнения);
			
		ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере, НеподписанныеДанные);
		Возврат;
		
	КонецЕсли;
	
	Если ОписаниеДанных.Свойство("КонтекстОперации") Тогда
		ОписаниеДанных.КонтекстОперации = ЭтотОбъект;
	КонецЕсли;
	
	Если ОповеститьОбОкончанииСрокаДействия Тогда
		
		ПараметрыОткрытияФормы = Новый Структура("Сертификат", Сертификат);
		Если РезультатПроверкиУдостоверяющегоЦентра <> Неопределено 
			И РезультатПроверкиУдостоверяющегоЦентра.Действует
			И (ЗначениеЗаполнено(РезультатПроверкиУдостоверяющегоЦентра.Предупреждение.ТекстОшибки) 
			Или ЗначениеЗаполнено(РезультатПроверкиУдостоверяющегоЦентра.Предупреждение.ДополнительныеСведения)) Тогда
			ПараметрыОткрытияФормы.Вставить("ДополнительныеДанныеПроверки", РезультатПроверкиУдостоверяющегоЦентра.Предупреждение);
		КонецЕсли;
		
		ДействиеПриНажатии = Новый ОписаниеОповещения("ОткрытьФормуОповещенияОНеобходимостиЗаменыСертификата",
			ЭлектроннаяПодписьСлужебныйКлиент, ПараметрыОткрытияФормы);
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Необходима замена сертификата';
				|en = 'You need to reissue the certificate'"), ДействиеПриНажатии, Сертификат,
			БиблиотекаКартинок.ДиалогВосклицание, СтатусОповещенияПользователя.Важное,
			Сертификат);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Истина);
	
КонецПроцедуры

// Продолжение процедуры ПодписатьДанные.
&НаКлиенте
Процедура ПодписатьДанныеПослеВыполнения(Результат)
	
	Если Результат.Свойство("ОперацияНачалась") Тогда
		ЭлектроннаяПодписьСлужебныйКлиент.ОбработатьПарольВФорме(ЭтотОбъект, ВнутренниеДанные,
			СвойстваПароля, Новый Структура("ПриУспешномВыполненииОперации", Истина));
	КонецЕсли;
	
	Если Результат.Свойство("ЕстьОбработанныеЭлементыДанных") Тогда
		// После начала подписания изменять сертификат более недопустимо,
		// иначе набор данных будет обработан по-разному.
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
				"Подписант",
				"ТолькоПросмотр",
				Истина);

		Элементы.Сертификат.ТолькоПросмотр = Истина;
		Элементы.Комментарий.ТолькоПросмотр = Истина;
		Элементы.ТипПодписанта.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Элементы.ГруппаДоверенность.Видимость Тогда
		Если ТипПодписанта = "ПодписатьСертификатом" Тогда
			ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(
				Сертификат, "ПоДоверенности", ПоДоверенности);
		ИначеЕсли ЗначениеЗаполнено(Подписант) Тогда
			ОбщегоНазначенияКлиент.ХранилищеОбщихНастроекСохранить(
				Подписант, "ПоДоверенности", ПоДоверенности);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДатаОкончанияЗакрытогоКлюча(АдресСертификата)
	СвойстваСертификата = ЭлектроннаяПодпись.СвойстваСертификата(ПолучитьИзВременногоХранилища(АдресСертификата));
	Возврат СвойстваСертификата.ДатаОкончанияЗакрытогоКлюча;
КонецФункции

&НаКлиенте
Процедура ОбработатьОшибку(Оповещение, ОшибкаНаКлиенте, ОшибкаНаСервере, НеподписанныеДанные = Неопределено, ДополнительныеДанные = Неопределено, ПоказыватьОшибку = Истина)
	
	Если ОписаниеДанных.Свойство("ПрекратитьВыполнение") Тогда
		
		Если ПоказыватьОшибку Тогда
			
			Если Не ОписаниеДанных.Свойство("ОписаниеОшибки") Тогда
				ОписаниеДанных.Вставить("ОписаниеОшибки");
			КонецЕсли;
		
		ОписаниеДанных.ОписаниеОшибки = ЭлектроннаяПодписьСлужебныйКлиентСервер.ОбщееОписаниеОшибки(
			ОшибкаНаКлиенте, ОшибкаНаСервере, НСтр("ru = 'Не удалось подписать документы по причине:';
													|en = 'Cannot sign documents due to:'"));

		КонецЕсли;
		
		Если Открыта() Тогда
			Закрыть(Ложь);
		Иначе
			ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		КонецЕсли;
		
	Иначе
		
		Если Не Открыта() И ОбработкаПослеПредупреждения = Неопределено Тогда
			Открыть();
		КонецЕсли;
		
		Если ПоказыватьОшибку Тогда
			ДополнительныеПараметры = Новый Структура;
			Если ЗначениеЗаполнено(ДополнительныеДанные) Тогда
				Для Каждого КлючИЗначение Из ДополнительныеДанные Цикл
					ДополнительныеПараметры.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
				КонецЦикла;
			КонецЕсли;
			Если НеподписанныеДанные <> Неопределено Тогда
				ДополнительныеПараметры.Вставить("НеподписанныеДанные", НеподписанныеДанные);
			КонецЕсли;
			ДополнительныеПараметры.Вставить("Сертификат", Сертификат);
			
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не удалось подписать документы';
					|en = 'Cannot sign documents'"));
			
		КонецЕсли;
			
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.СервисМобильнойПодписи
&НаКлиенте
Процедура ПодписатьДанныеВСервисеМобильнойПодписи(Оповещение)
	
	ОписаниеДанных.Вставить("ТипПодписи", ТипПодписи);
	Контекст = Новый Структура;
	Контекст.Вставить("Оповещение", Оповещение);
	Контекст.Вставить("ОшибкаНаКлиенте", Новый Структура);
	Контекст.Вставить("ОшибкаНаСервере", Новый Структура);
	
	ОписаниеДанных.Вставить("ВыбранныйСертификат", Неопределено);
	ОписаниеДанных.Вставить("ВыбраннаяДоверенность", МашиночитаемаяДоверенность);
	
	Если ЗначениеЗаполнено(Подписант) Тогда
		МодульСервисМобильнойПодписиСлужебныйВызовСервера = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"СервисМобильнойПодписиСлужебныйВызовСервера");
		ОписаниеДанных.Вставить("ВыбранныйПодписант", 
			МодульСервисМобильнойПодписиСлужебныйВызовСервера.ПараметрыПодписанта(Подписант));
	Иначе
		ОписаниеДанных.Вставить("ВыбранныйПодписант", ПараметрыПодписанта);
	КонецЕсли;
	
	Если ОписаниеДанных.Свойство("ПередВыполнением")
	   И ТипЗнч(ОписаниеДанных.ПередВыполнением) = Тип("ОписаниеОповещения") Тогда
		
		ПараметрыВыполнения = Новый Структура;
		ПараметрыВыполнения.Вставить("ОписаниеДанных", ОписаниеДанных);
		ПараметрыВыполнения.Вставить("Оповещение", Новый ОписаниеОповещения(
			"ПодписатьДанныеВСервисеПослеОбработкиПередВыполнением", ЭтотОбъект, Контекст));
		
		ВыполнитьОбработкуОповещения(ОписаниеДанных.ПередВыполнением, ПараметрыВыполнения);
	Иначе
		ПодписатьДанныеВСервисеПослеОбработкиПередВыполнением(Новый Структура, Контекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьДанныеВСервисеПослеОбработкиПередВыполнением(Результат, Контекст) Экспорт
	
	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("ОписаниеОшибки") Тогда
		ОбработатьОшибку(Контекст.Оповещение, Новый Структура("ОписаниеОшибки", Результат.ОписаниеОшибки), Новый Структура);
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ИдентификаторФормы", УникальныйИдентификатор);
	Если ТипЗнч(ФормаОбъекта) = Тип("ФормаКлиентскогоПриложения") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта.УникальныйИдентификатор;
	ИначеЕсли ТипЗнч(ФормаОбъекта) = Тип("УникальныйИдентификатор") Тогда
		Контекст.ИдентификаторФормы = ФормаОбъекта;
	КонецЕсли;
	
	Контекст.Вставить("Подписант", Подписант);
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("ОписаниеДанных",     ОписаниеДанных);
	ПараметрыВыполнения.Вставить("Форма",              ЭтотОбъект);
	ПараметрыВыполнения.Вставить("ИдентификаторФормы", Контекст.ИдентификаторФормы);
	ПараметрыВыполнения.Вставить("ТребуетсяПроверка",  Истина);
	
	ПараметрыВыполнения.Вставить("ПолноеПредставлениеДанных",
		МобильныйЭДОСлужебныйКлиент.ПолноеПредставлениеДанных(ЭтотОбъект));
	ПараметрыВыполнения.Вставить("ТекущийСписокПредставлений", ТекущийСписокПредставлений);
	
	Контекст.Вставить("ПараметрыВыполнения", ПараметрыВыполнения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияДанныхВСервисе", ЭтотОбъект, Контекст);
	
	ПараметрыПодписания = Новый Структура;
	ПараметрыПодписания.Вставить("ТипПодписи", ТипПодписи);
	Если ПоДоверенности И ЗначениеЗаполнено(МашиночитаемаяДоверенность) Тогда
		ПараметрыПодписания.Вставить("Доверенность", МашиночитаемаяДоверенность);
	КонецЕсли;
	ПараметрыПодписания.Вставить("ПакетнаяОтправкаНаПодписание", ПакетнаяОтправка);
	
	ИнтеграцияБСПБЭДСлужебныйКлиент.ПодписатьДанныеВСервисеМобильнойПодписи(ОписаниеОповещения,
		Контекст,
		ПараметрыПодписания);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПодписанияДанныхВСервисе(Результат, Контекст) Экспорт
	
	Если ПеременныеОчищены() Тогда
		Возврат;
	КонецЕсли;

	ПодписатьДанныеПослеВыполнения(Результат);
	
	Если Результат.Свойство("ОтменаПодписания") Тогда
		ВыполнитьОбработкуОповещения(Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;

	Если Результат.Свойство("Ошибка") Тогда
		
		Если ОписаниеДанных.Свойство("КонтекстОперации") Тогда
			ОписаниеДанных.КонтекстОперации = ЭтотОбъект;
		КонецЕсли;
		
		Контекст.ОшибкаНаКлиенте = Новый Структура("ОписаниеОшибки", Результат.Ошибка);
		ОбработатьОшибку(Контекст.Оповещение, Контекст.ОшибкаНаКлиенте, Контекст.ОшибкаНаСервере);
		Возврат;
		
	КонецЕсли;

	ВыполнитьОбработкуОповещения(Контекст.Оповещение, Истина);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.СервисМобильнойПодписи

#Область ПодписаниеРутокен

&НаКлиенте
Процедура НачатьОперациюСКонтейнеромРутокен(Операция = Неопределено, ОписаниеДанных = Неопределено)
	
	ПараметрыОперации = МобильныйЭДОСлужебныйКлиент.НовыйПараметрыОперацииРутокен();
	
	ПараметрыОперации.Форма = ЭтотОбъект;
	ПараметрыОперации.ПараметрыКомпоненты = ПараметрыКомпонентыРутокен;
	
	Если Операция <> Неопределено Тогда
		ПараметрыОперации.Операция = Операция;
	КонецЕсли;
	
	Если ОписаниеДанных <> Неопределено Тогда
		ПараметрыОперации.ОписаниеДанных = ОписаниеДанных;
	КонецЕсли;
	
	МобильныйЭДОКлиент.НачатьОбработкуОперацииРутокен(ПараметрыОперации);

КонецПроцедуры

#КонецОбласти

#КонецОбласти