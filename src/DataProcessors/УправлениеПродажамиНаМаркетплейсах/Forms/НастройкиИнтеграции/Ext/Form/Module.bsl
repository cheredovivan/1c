#Область ОписаниеПеременных

&НаКлиенте
Перем ТекущийСклад;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Обработки.УправлениеПродажамиНаМаркетплейсах) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр(
			"ru = 'Недостаточно прав для изменение настроек интеграции. Обратитесь к администратору.';
			|en = 'Insufficient rights to change the integration settings. Contact the administrator.'"));
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	
	ИнтеграцияСМаркетплейсамиСИ.ДобавитьЭлементыСостояния(ЭтотОбъект, Элементы.ГруппаУчетнаяЗапись);
	УстановитьКэшированныеЗначения();
	УстановитьУсловноеОформление();
	ИнициализироватьЭлементыФормы();
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.ПараметрыМенеджераДлительныхОпераций = Неопределено;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Маркетплейсы_УчетнаяЗаписьУдаление" Тогда
		
		УчетнаяЗаписьПоиск = Элементы.УчетнаяЗапись.СписокВыбора.НайтиПоЗначению(Параметр);
		Если УчетнаяЗаписьПоиск <> Неопределено Тогда
			Элементы.УчетнаяЗапись.СписокВыбора.Удалить(УчетнаяЗаписьПоиск);
		КонецЕсли;
		
		Кэш.УчетныеЗаписи.Удалить(Параметр);
		
		Если УчетнаяЗапись = Параметр Тогда
			УчетнаяЗапись = Неопределено;
			УчетнаяЗаписьПриИзмененииПродолжение();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность и Не ЗавершениеРаботы Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data has changed. Do you want to save the changes?'");
		Оповещение = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Или Не ЗначениеЗаполнено(Партнер) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьИнтеграциюПоОстаткам Тогда
		Если СкладыМаркетплейса.Количество() = 0 Тогда
			ПроверяемыеРеквизиты.Добавить("СкладыМаркетплейса");
			Возврат;
		КонецЕсли;
		УказанныеСклады = СкладыМаркетплейса.Выгрузить( , "Склад");
		УказанныеСклады.Свернуть("Склад");
		Если УказанныеСклады.Количество() = 1 И УказанныеСклады[0].Склад = Справочники.Склады.ПустаяСсылка() Тогда
			Если Кэш.ЭтоWildberries Тогда
				ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Должно быть настроено хотя бы одно соответствие.';
															|en = 'Set up at least one mapping.'"), ,
					"СкладыМаркетплейса", , Отказ);
			КонецЕсли;
			Если Кэш.ЭтоLamoda Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Необходимо указать склад, соответствующий торговой площадке.';
						|en = 'Specify the warehouse corresponding to the trading platform.'"), , , , Отказ);
			КонецЕсли;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ИспользоватьИнтеграциюПоЦенам Тогда
		ПроверяемыеРеквизиты.Добавить("ВалютаУчета");
		ПроверяемыеРеквизиты.Добавить("ВидЦеныДоСкидок");
		ПроверяемыеРеквизиты.Добавить("ВидЦеныСоСкидкойПродавца");
		Если Кэш.ЭтоWildberries Тогда
			ПроверяемыеРеквизиты.Добавить("ВидЦеныКлубная");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)
	
	ОчиститьСообщения();
	УчетнаяЗаписьПриИзмененииПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеВыгрузкиОстатковТоваровНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	
	Если ОписаниеУчетнойЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	ПараметрыФормы.Вставить("Наименование", ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеЗаданияРасчетОстатковТоваров(ВидМаркетплейса));
	ПараметрыФормы.Вставить("ИмяМетаданных", "ВыгрузкаОстатковНаМаркетплейсы");
	ПараметрыФормы.Вставить("Префикс", ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияРасчетОстатковТоваров(ВидМаркетплейса));
	ПараметрыФормы.Вставить("ОткрыватьПодОграниченнымиПравами", Истина);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.РегламентноеЗадание", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеВыгрузкиЦенТоваровНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	
	Если ОписаниеУчетнойЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	ПараметрыФормы.Вставить("Наименование", ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеЗаданияВыгрузкаЦенНаМаркетплейсы(ВидМаркетплейса));
	ПараметрыФормы.Вставить("ИмяМетаданных", "ВыгрузкаЦенНаМаркетплейсы");
	ПараметрыФормы.Вставить("Префикс", ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияВыгрузкаЦенНаМаркетплейсы(ВидМаркетплейса));
	ПараметрыФормы.Вставить("ОткрыватьПодОграниченнымиПравами", Истина);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.РегламентноеЗадание", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПартнерПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	Если ЗначениеЗаполнено(ПредыдущийПартнер) И Не ЗначениеЗаполнено(Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указан партнер, представляющий торговую площадку.';
				|en = 'The partner that represents the marketplace is not specified.'"), ,
			"Партнер");
		Партнер = ПредыдущийПартнер;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоКоррекцияОстатковПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		АвтоКоррекцияОстатков = Ложь;
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенаВыгрузкаОстатковНаМаркетплейсПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ИспользоватьИнтеграциюПоОстаткам = Ложь;
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьПоУчетнойЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоКоррекцияЦенПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		АвтоКоррекцияЦен = Ложь;
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешенаВыгрузкаЦенНаМаркетплейсПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		РазрешенаВыгрузкаЦенНаМаркетплейс = Ложь;
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтеграциюПоЦенамПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ИспользоватьИнтеграциюПоЦенам = Ложь;
	Иначе
		Модифицированность = Истина;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьПоУчетнойЗаписи();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СостояниеВСервисеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Показать" Тогда
		ПоказатьПредупреждение( , Элементы.СостояниеВСервисе.Подсказка);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура СкладМаркетплейсаПриИзменении(Элемент)
	
	Если СкладыМаркетплейса.Количество() = 0 Тогда
		 Возврат;
	КонецЕсли;
	
	СтрокаСклада = СкладыМаркетплейса[0];
		
	Если СтрокаСклада.Склад <> ТекущийСклад Тогда
		
		Модифицированность = Истина;
		СтрокаСклада.Изменение = Истина;
		
		Если ЗначениеЗаполнено(ТекущийСклад) Тогда
			ИсключаемыеСклады = Кэш.ИсключаемыеСклады; // Массив
				
			ОписаниеСклада = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса();
			ОписаниеСклада.ИдентификаторСкладаСервиса = СтрокаСклада.ИдентификаторСкладаСервиса;
			ОписаниеСклада.Склад = ТекущийСклад;
			ИсключаемыеСклады.Добавить(ОписаниеСклада);
			
		КонецЕсли;
	
		ТекущийСклад = СтрокаСклада.Склад;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСкладыМаркетплейса

&НаКлиенте
Процедура СкладыМаркетплейсаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаПередНачаломИзменения(Элемент, Отказ)

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		ТекущийСклад = ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка");
	Иначе
		ТекущийСклад = ТекущиеДанные.Склад;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если ОтменаРедактирования Тогда
		ТекущиеДанные.Склад = ТекущийСклад;
		Возврат;
	КонецЕсли;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ТекущиеДанные.Склад <> ТекущийСклад Тогда
		ТекущиеДанные.Изменение = Истина;
		ТекущийСклад = ТекущиеДанные.Склад;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаСкладПриИзменении(Элемент)

	ТекущиеДанные = Элементы.СкладыМаркетплейса.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПроверкаПройдена = ПроверитьПересечениеСкладовВНастройке(СкладыМаркетплейса,
		ТекущиеДанные.ИдентификаторСкладаСервиса, ТекущиеДанные.Склад);

	Если ПроверкаПройдена Тогда
		Если ЗначениеЗаполнено(ТекущийСклад) Тогда
			ИсключаемыеСклады = Кэш.ИсключаемыеСклады; // Массив
			
			ОписаниеСклада = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса();
			ОписаниеСклада.ИдентификаторСкладаСервиса = ТекущиеДанные.ИдентификаторСкладаСервиса;
			ОписаниеСклада.Склад = ТекущийСклад;
			ИсключаемыеСклады.Добавить(ОписаниеСклада);
			
		КонецЕсли;
	Иначе
		ТекущиеДанные.Склад = ТекущийСклад;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Элемент.ТекущийЭлемент = Элементы.СкладыМаркетплейсаСклад Тогда
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элементы.СкладыМаркетплейса.ТекущиеДанные;

	ПараметрыФормы = Новый Структура;
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ПараметрыФормы.Вставить("УчетнаяЗапись", Неопределено);
	Иначе
		ПараметрыФормы.Вставить("ОписаниеУчетнойЗаписи", Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись));
	КонецЕсли;
	ПараметрыФормы.Вставить("ИдентификаторСкладаСервиса", ТекущиеДанные.ИдентификаторСкладаСервиса);
	ПараметрыФормы.Вставить("НаименованиеСкладаМП", ТекущиеДанные.НаименованиеСкладаМП);
	ПараметрыФормы.Вставить("Склад", ТекущиеДанные.Склад);

	ПараметрыОписания = Новый Структура("ТекущаяСтрока", Элементы.СкладыМаркетплейса.ТекущаяСтрока);
	ОписаниеПриИзмененииСкладаОтдельнаяФорма = Новый ОписаниеОповещения("ЗавершениеРедактированияСоответствияСкладов",
		ЭтотОбъект, ПараметрыОписания);

	ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.СкладМаркетплейса", ПараметрыФормы, ЭтотОбъект, , ,
		, ОписаниеПриИзмененииСкладаОтдельнаяФорма, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СкладыМаркетплейсаПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкаОграниченийВыгрузкиОстатковТоваров

&НаКлиенте
Процедура НастройкаОграниченийВыгрузкиОстатковТоваровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель,
	ЭтоГруппа, Параметр)

	Отказ = Истина;

	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;

	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();
	ПараметрыФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыФормы.Вставить("ОписаниеУчетнойЗаписи", Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись));

	Если Копирование Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Элемент.ТекущиеДанные,,"Код, Наименование");
	КонецЕсли;

	ДопПараметры = Новый Структура("Добавление, Копирование, ИдентификаторСтроки", Истина, Копирование, Неопределено);

	ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.ОграниченияВыгрузкиОстатков", ПараметрыФормы,
		ЭтотОбъект, , , , Новый ОписаниеОповещения("ЗавершениеРедактированияОграниченияВыгрузкиОстатков", ЭтотОбъект,
		ДопПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаОграниченийВыгрузкиОстатковТоваровПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;

	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
	ПараметрыФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыФормы.Вставить("ОписаниеУчетнойЗаписи", Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись));

	ДопПараметры = Новый Структура("Добавление, Копирование, ИдентификаторСтроки", Ложь, Ложь, Элемент.ТекущаяСтрока);

	ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.ОграниченияВыгрузкиОстатков", ПараметрыФормы,
		ЭтотОбъект, , , , Новый ОписаниеОповещения("ЗавершениеРедактированияОграниченияВыгрузкиОстатков", ЭтотОбъект,
		ДопПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаОграниченийВыгрузкиОстатковТоваровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;

	ТекущиеДанные = Элемент.ТекущиеДанные;

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ТекущиеДанные);
	ПараметрыФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыФормы.Вставить("ОписаниеУчетнойЗаписи", Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись));

	ДопПараметры = Новый Структура("Добавление, Копирование, ИдентификаторСтроки", Ложь, Ложь, Элемент.ТекущаяСтрока);

	ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.ОграниченияВыгрузкиОстатков", ПараметрыФормы,
		ЭтотОбъект, , , , Новый ОписаниеОповещения("ЗавершениеРедактированияОграниченияВыгрузкиОстатков", ЭтотОбъект,
		ДопПараметры), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура НастройкаОграниченийВыгрузкиОстатковТоваровПередУдалением(Элемент, Отказ)

	Отказ = Истина;

	Для Каждого ВыделеннаяСтрока Из Элементы.НастройкаОграниченийВыгрузкиОстатковТоваров.ВыделенныеСтроки Цикл

		ТекущиеДанные = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиПоИдентификатору(ВыделеннаяСтрока);

		ТекущиеДанные.Удаление = Не ТекущиеДанные.Удаление;
		ТекущиеДанные.Изменение = Истина;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура НастройкаОграниченийВыгрузкиОстатковТоваровПриИзменении(Элемент)
	Модифицированность = Истина;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаписатьНастройкиНаКлиенте", ЭтотОбъект);
	
	Если ЗначениеЗаполнено(ПредыдущийПартнер) И ПредыдущийПартнер <> Партнер Тогда
		ТекстУточнение = 
		НСтр("ru = 'Для корректной работы функции импорта необходимо обновить данные о партнере в информации о загружаемых товарах.';
			|en = 'For the import to function correctly, update the partner information in the information about the goods to import.'");
		Если ПартнерУказанДляРазныхУчетныхЗаписей(ПредыдущийПартнер) Тогда
			ТекстУточнение = НСтр("ru = 'Прежний партнер связан с разными учетными записями и будет изменен во всех соответствующих товарных каталогах.';
									|en = 'The previous partner is linked to different accounts and will be changed in all relevant item catalogs.'");
		КонецЕсли;
		
		ПоказатьВопрос(ОповещениеОЗавершении, 
			СтрШаблон(НСтр("ru = 'Данные о партнере используются в процессе импорта товарного каталога. %1
						|Процесс обновления может занять продолжительное время. 
						|Продолжить?';
						|en = 'Partner data is used during the item catalog import. %1
						|The update process may take a long time. 
						|Continue?'"), ТекстУточнение), 
			РежимДиалогаВопрос.ДаНет, 
			, 
			КодВозвратаДиалога.Нет, 
			НСтр("ru = 'Запись данных';
				|en = 'Saving data'"),
			КодВозвратаДиалога.Нет);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСклады(Команда)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ТекущийСклад = Неопределено;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗапроситьДанныеСМаркетплейса", Истина);
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса());
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСклады(Команда)
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите учетную запись.';
														|en = 'Specify the account.'"), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗапроситьДанныеСМаркетплейса", Истина);
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады());
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОпераций(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	
	Если ИнтернетПоддержкаПодключена Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, ПараметрыОперации);
	Иначе
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДлительныхОперацийПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОперацийПродолжение(Результат, ПараметрыОперации) Экспорт
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
		ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации)
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.Очередь.Добавить(?(ЗначениеЗаполнено(УчетнаяЗапись), УчетнаяЗапись, Неопределено),
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	ПараметрыОперации.ОтменитьМодифицированность = Истина;
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаСервере
Процедура ПередВыполнениемДлительныхОпераций(ПараметрыОперации, ПараметрыЗаполнены, Отказ)

	КоличествоОпераций = ПараметрыОперации.Очередь.Количество() - 1;

	ПорядокОперации = 0;

	Пока ПорядокОперации <= КоличествоОпераций Цикл

		Операция = ПараметрыОперации.Очередь[ПорядокОперации];

		Метод = Операция.Представление;

		Если ПараметрыЗаполнены
			И Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьСкладыМаркетплейса() Тогда

			Если Операция.Значение.СкладыМаркетплейса.Количество() > 0 Тогда
				Склады = Новый Массив;
				Для Каждого Карточка Из Операция.Значение.СкладыМаркетплейса Цикл
					Если ЗначениеЗаполнено(Карточка.Склад) Тогда
						Склады.Добавить(Карточка.Склад);
					КонецЕсли;
				КонецЦикла;
				Если Склады.Количество() > 0 Тогда
					Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
						ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
						УзелОбмена = НайтиСоздатьУзелОбменаСИ(УчетнаяЗапись,
							ОписаниеУчетнойЗаписи.Представление, Истина);
					КонецЕсли;
					Если ЗначениеЗаполнено(УзелОбмена) Тогда
						ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, Склады);
					Иначе
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;

		ИначеЕсли Не ПараметрыЗаполнены
			И Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОграниченияОстатковТоваров() Тогда

			МаксимальныйКод = 0;

			КодыОграничений = НастройкаОграниченийВыгрузкиОстатковТоваров.Выгрузить( , "Код");
			КодыОграничений.Сортировать("Код");

			МаксимальныйКод = Макс(МаксимальныйКод, КодыОграничений[КодыОграничений.Количество() - 1].Код);

			ОтборСтрок = Новый Структура("Изменение, Код", Истина, 0);
			ОтмеченныеСтрокиБезНомера = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(ОтборСтрок);

			Для Каждого СтрокаБезНомера Из ОтмеченныеСтрокиБезНомера Цикл
				МаксимальныйКод = МаксимальныйКод + 1;
				СтрокаБезНомера.Код = МаксимальныйКод;
			КонецЦикла;

		ИначеЕсли Не ПараметрыЗаполнены И Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады() Тогда
			
			Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
				ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
				УзелОбмена = НайтиСоздатьУзелОбменаСИ(УчетнаяЗапись,
					ОписаниеУчетнойЗаписи.Представление, Истина);
				Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

		ПорядокОперации = ПорядокОперации + 1;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)

	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации,
		ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;

	Отказ = Ложь;

	ПередВыполнениемДлительныхОпераций(ПараметрыОперации, Ложь, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ПередВыполнениемДлительныхОпераций(ПараметрыОперации, Истина, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);

КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ПараметрыЗапросаПолучитьУчетныеЗаписи(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи() Тогда
			ПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьСкладыМаркетплейса(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьСкладыМаркетплейса() Тогда
			ПараметрыЗапросаОбновитьСкладыМаркетплейса(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады() Тогда
			//@skip-check query-in-loop - в массиве только одна такая операция
			ПараметрыЗапросаВыгрузитьСклады(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров() Тогда
			ПараметрыЗапросаПолучитьОграниченияОстатковТоваров(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОграниченияОстатковТоваров() Тогда
			ПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьОграниченияОстатковТоваров() Тогда
			ПараметрыЗапросаУдалитьОграниченияОстатковТоваров(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПеренестиНоменклатуруПартнера() Тогда
			ПараметрыЗапросаПеренестиНоменклатуруПартнера(Операция.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьУчетныеЗаписи(Значение)
	
	ЗначениеДо = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьУчетныеЗаписи();
	ЗаполнитьЗначенияСвойств(Значение, ЭтотОбъект, , "УчетнаяЗапись");
	Если ТипЗнч(ЗначениеДо) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Значение, ЗначениеДо);
	Иначе
		Значение.УчетнаяЗапись = ЗначениеДо;
	КонецЕсли;
	Значение.ВсеУчетныеЗаписи = Не ЗначениеЗаполнено(Значение.УчетнаяЗапись);
	Значение.ВидМаркетплейса = ВидМаркетплейса;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыЗапроса.Партнер = Партнер;
	ПараметрыЗапроса.АвтоКоррекцияОстатков = АвтоКоррекцияОстатков;
	ПараметрыЗапроса.ИспользоватьИнтеграциюПоОстаткам = ИспользоватьИнтеграциюПоОстаткам;
	ПараметрыЗапроса.АвтоКоррекцияЦен = АвтоКоррекцияЦен;
	ПараметрыЗапроса.ИспользоватьИнтеграциюПоЦенам = ИспользоватьИнтеграциюПоЦенам;
	ПараметрыЗапроса.РазрешенаВыгрузкаЦенНаМаркетплейс = РазрешенаВыгрузкаЦенНаМаркетплейс;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьСкладыМаркетплейса(ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаОбновитьСкладыМаркетплейса(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаОбновитьСкладыМаркетплейса();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

	Отбор = Новый Структура("Изменение", Истина);
	ОтмеченныеСтроки = СкладыМаркетплейса.НайтиСтроки(Отбор);

	Для Каждого Данные Из ОтмеченныеСтроки Цикл

		ОписаниеСклада = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса();

		ЗаполнитьЗначенияСвойств(ОписаниеСклада, Данные);

		ПараметрыЗапроса.СкладыМаркетплейса.Добавить(ОписаниеСклада);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаВыгрузитьСклады(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаВыгрузитьСклады();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

	Склады = ПараметрыЗапроса.Склады;
	Для Каждого СкладМаркетплейса Из СкладыМаркетплейса Цикл
		Если Не ЗначениеЗаполнено(СкладМаркетплейса.Склад) Тогда
			Продолжить;
		КонецЕсли;
		Если Склады.Найти(СкладМаркетплейса.Склад) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;

		Склады.Добавить(СкладМаркетплейса.Склад);
	КонецЦикла;

	ПолучитьИерархиюСкладов(Склады);

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьОграниченияОстатковТоваров(ПараметрыЗапроса)
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьОграниченияОстатковТоваров();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

	Отбор = Новый Структура("Изменение, Удаление", Истина, Ложь);

	ОтмеченныеСтроки = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(Отбор);

	Для Каждого Данные Из ОтмеченныеСтроки Цикл

		Ограничение = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();

		ЗаполнитьЗначенияСвойств(Ограничение, Данные);

		ПараметрыЗапроса.ОграниченияОстатковТоваров.Добавить(Ограничение);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаУдалитьОграниченияОстатковТоваров(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаУдалитьОграниченияОстатковТоваров();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

	Отбор = Новый Структура("Удаление", Истина);

	ОтмеченныеСтроки = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(Отбор);

	Для Каждого Данные Из ОтмеченныеСтроки Цикл
		
		Если Данные.Код = 0 Тогда
			Продолжить;
		КонецЕсли;

		Если Данные.Код = 0 Тогда
			Продолжить;
		КонецЕсли;

		Ограничение = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();

		ЗаполнитьЗначенияСвойств(Ограничение, Данные);

		ПараметрыЗапроса.ОграниченияОстатковТоваров.Добавить(Ограничение);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПеренестиНоменклатуруПартнера(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПеренестиНоменклатуруПартнера();
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыЗапроса.ТекущийВладелец = ПредыдущийПартнер;
	ПараметрыЗапроса.НовыйВладелец = Партнер;

КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Истина);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата, Кэш);
	Иначе
		РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Если ДополнительныеПараметры.ОтменитьМодифицированность Тогда
		КодСостояния = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "КодСостояния", 0);
		Если КодСостояния <> 200 Тогда
			ДополнительныеПараметры.ОтменитьМодифицированность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи() Тогда
			Если РезультатОперации Тогда
				Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) Тогда
					Оповестить("Wildberries_ОбновлениеНастроек");
				ИначеЕсли ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса) Тогда
					Оповестить("Lamoda_ОбновлениеНастроек");
				КонецЕсли;
			КонецЕсли;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса() Тогда
			Если РезультатОперации.КодСостояния = 200 И Кэш.ЭтоLamoda Тогда
				Если СкладыМаркетплейса.Количество() > 0 Тогда
					 ТекущийСклад = СкладыМаркетплейса[0].Склад;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

// Обрабатывает результат менеджера длительных операций на сервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
//  Кэш - Структура - Кэш
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки
&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(Знач АдресРезультата, Кэш)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	РезультатМенеджера.Вставить("КодСостояния", 200);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			Кэш = ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки);
			ИнтеграцияСМаркетплейсамиСИ.ЗаполнитьЭлементыСостояния(ЭтотОбъект);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи() Тогда
			Кэш = ОбработатьРезультатОбновитьНастройкиУчетнойЗаписи(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса() Тогда
			ОбработатьРезультатПолучитьСкладыМаркетплейса(РезультатОперации, РезультатОбработки);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров() Тогда
			ОбработатьРезультатПолучитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОграниченияОстатковТоваров() Тогда
			ОбработатьРезультатВыгрузитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьОграниченияОстатковТоваров() Тогда
			ОбработатьРезультатУдалитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьСкладыМаркетплейса() Тогда
			//@skip-check query-in-loop
			ОбработатьРезультатОбновитьСкладыМаркетплейса(РезультатОперации, РезультатОбработки);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПеренестиНоменклатуруПартнера() Тогда
			ОбработатьРезультатПеренестиНоменклатуруПартнера(РезультатОперации, РезультатОбработки);
		КонецЕсли;
		
		Если РезультатОперации.КодСостояния <> 200 Тогда
			РезультатМенеджера.КодСостояния = РезультатОперации.КодСостояния;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ОписаниеУчетнойЗаписи = Неопределено;
	
	Если ДополнительныеПараметры.ОтменитьМодифицированность Тогда
		Модифицированность = Ложь;
		Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
			ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
			Если ОписаниеУчетнойЗаписи <> Неопределено Тогда
				ЗаполнитьФормуПоУчетнойЗаписи(ОписаниеУчетнойЗаписи);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьПоУчетнойЗаписи(ОписаниеУчетнойЗаписи);
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработать результат получить учетные записи.
// 
// Параметры:
//  РезультатОперации - Структура - Результат операции
//  РезультатОбработки - Булево - Результат обработки
// 
// Возвращаемое значение:
//  Соответствие из Структура
&НаСервере
Функция ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации.Ответ.ОбновлениеСостояния Тогда
		Если РезультатОперации.КодСостояния = 200 И РезультатОперации.Ответ.УчетныеЗаписи.Количество() = 1 Тогда
			ОписаниеУчетнойЗаписи = РезультатОперации.Ответ.УчетныеЗаписи[0];
			Кэш.УчетныеЗаписи.Вставить(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи);
		Иначе
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		Возврат Кэш;
	КонецЕсли;
	
	Элементы.УчетнаяЗапись.СписокВыбора.Очистить();
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат Кэш;
	КонецЕсли;
	
	Кэш.УчетныеЗаписи.Очистить();
	
	Для Каждого ОписаниеУчетнойЗаписи Из РезультатОперации.Ответ.УчетныеЗаписи Цикл
		Элементы.УчетнаяЗапись.СписокВыбора.Добавить(ОписаниеУчетнойЗаписи.УчетнаяЗапись,
			ОписаниеУчетнойЗаписи.Представление);
		Кэш.УчетныеЗаписи.Вставить(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи);
	КонецЦикла;
	
	Если Кэш.УчетныеЗаписи.Количество() = 1 Тогда
		УчетнаяЗапись = ОписаниеУчетнойЗаписи.УчетнаяЗапись;
	КонецЕсли;
	
	Возврат Кэш;
	
КонецФункции

// Обработать результат обновить настройки учетной записи.
// 
// Параметры:
//  РезультатОперации - Структура - Результат операции
//  РезультатОбработки - Булево - Результат обработки
// 
// Возвращаемое значение:
//  Структура - Кэш
&НаСервере
Функция ОбработатьРезультатОбновитьНастройкиУчетнойЗаписи(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат Кэш;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		РезультатОбработки = Ложь;
	КонецЕсли;
	
	ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	ЗаполнитьЗначенияСвойств(ОписаниеУчетнойЗаписи, ЭтотОбъект);
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Возврат Кэш;
	КонецЕсли;
	
	// При необходимости создадим узлы плана обмена и зарегистрируем склады к выгрузке
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ДанныеФормы.Вставить("Партнер", Партнер);
	ДанныеФормы.Вставить("ВалютаУчета", ВалютаУчета);
	ДанныеФормы.Вставить("ВидЦеныДоСкидок", ВидЦеныДоСкидок);
	ДанныеФормы.Вставить("ВидЦеныСоСкидкойПродавца", ВидЦеныСоСкидкойПродавца);
	ДанныеФормы.Вставить("ВидЦеныКлубная", ВидЦеныКлубная);
	ДанныеФормы.Вставить("ЭтоWildberries", Кэш.ЭтоWildberries);
	ДанныеФормы.Вставить("ЭтоLamoda", Кэш.ЭтоLamoda);
	
	ПриЗаписиНаСервере(ОписаниеУчетнойЗаписи, ДанныеФормы, УзелОбмена);
	
	ИнтеграцияСМаркетплейсамиСИ.ИзменитьИспользованиеРегламентногоЗадания(ОписаниеУчетнойЗаписи,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияСинхронизацияССервисомИнтеграции(ВидМаркетплейса), Истина);
	
	ИнтеграцияСМаркетплейсамиСИ.ИзменитьИспользованиеРегламентногоЗадания(ОписаниеУчетнойЗаписи,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияРасчетОстатковТоваров(ВидМаркетплейса),
		ИспользоватьИнтеграциюПоОстаткам, Истина);
	
	ИнтеграцияСМаркетплейсамиСИ.ИзменитьИспользованиеРегламентногоЗадания(ОписаниеУчетнойЗаписи,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияВыгрузкаЦенНаМаркетплейсы(ВидМаркетплейса),
		ИспользоватьИнтеграциюПоЦенам, Истина);
	
	Возврат Кэш;
	
КонецФункции

&НаСервере
Процедура ОбработатьРезультатПолучитьСкладыМаркетплейса(РезультатОперации, РезультатОбработки)
	
	СкладыМаркетплейса.Очистить();
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	Для Каждого ОписаниеСкладаМаркетплейса Из РезультатОперации.СкладыМаркетплейса Цикл
		НоваяСтрока = СкладыМаркетплейса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеСкладаМаркетплейса);
		НоваяСтрока.НаименованиеСкладаМП = ОписаниеСкладаМаркетплейса.НаименованиеСкладаМаркетплейса;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатОбновитьСкладыМаркетплейса(Знач РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатОперации.УчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;
	
	// Зарегистрируем склады в регистре соответствий
	ИспользуемыеСклады = Новый ТаблицаЗначений;
	ИспользуемыеСклады.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ИспользуемыеСклады.Колонки.Добавить("ИдентификаторСкладаСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(50)));
	ИспользуемыеСклады.Колонки.Добавить("НаименованиеСкладаСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150)));
	
	Для Каждого ОписаниеСклада Из РезультатОперации.Склады Цикл
		Если ЗначениеЗаполнено(ОписаниеСклада.Склад) Тогда
			НоваяСтрока = ИспользуемыеСклады.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеСклада);
		КонецЕсли;
	КонецЦикла;
	
	ИсключаемыеСклады = ИспользуемыеСклады.СкопироватьКолонки();
	
	Для Каждого ОписаниеСклада Из Кэш.ИсключаемыеСклады Цикл
		Если ЗначениеЗаполнено(ОписаниеСклада.Склад) Тогда
			НоваяСтрока = ИсключаемыеСклады.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеСклада);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	//@skip-check bsl-ql-hub
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИспользуемыеСклады.Склад КАК Склад,
	|	ИспользуемыеСклады.ИдентификаторСкладаСервиса КАК ИдентификаторСкладаСервиса,
	|	ИспользуемыеСклады.НаименованиеСкладаСервиса КАК НаименованиеСкладаСервиса
	|ПОМЕСТИТЬ врИспользуемыеСклады
	|ИЗ
	|	&ИспользуемыеСклады КАК ИспользуемыеСклады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсключаемыеСклады.Склад КАК Склад,
	|	ИсключаемыеСклады.ИдентификаторСкладаСервиса КАК ИдентификаторСкладаСервиса,
	|	ИсключаемыеСклады.НаименованиеСкладаСервиса КАК НаименованиеСкладаСервиса
	|ПОМЕСТИТЬ врИсключаемыеСклады
	|ИЗ
	|	&ИсключаемыеСклады КАК ИсключаемыеСклады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад,
	|	врИспользуемыеСклады.ИдентификаторСкладаСервиса КАК ИдентификаторСклада,
	|	врИспользуемыеСклады.НаименованиеСкладаСервиса КАК НаименованиеСклада,
	|	&КодВключить КАК Действие,
	|	Склады.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ врИспользуемыеСклады КАК врИспользуемыеСклады
	|		ПО (врИспользуемыеСклады.Склад = Склады.Ссылка)
	|ГДЕ
	|	Склады.Ссылка В ИЕРАРХИИ(&ИспользуемыеСклады)
	|	И НЕ Склады.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Склады.Ссылка,
	|	врИсключаемыеСклады.ИдентификаторСкладаСервиса,
	|	врИсключаемыеСклады.НаименованиеСкладаСервиса,
	|	&КодИсключить,
	|	Склады.ЭтоГруппа
	|ИЗ
	|	Справочник.Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ врИсключаемыеСклады КАК врИсключаемыеСклады
	|		ПО (врИсключаемыеСклады.Склад = Склады.Ссылка)
	|ГДЕ
	|	Склады.Ссылка В ИЕРАРХИИ(&ИсключаемыеСклады)
	|	И НЕ Склады.ПометкаУдаления
	|ИТОГИ
	|	МАКСИМУМ(ИдентификаторСклада),
	|	МАКСИМУМ(НаименованиеСклада)
	|ПО
	|	Действие,
	|	Склад ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("ИспользуемыеСклады", ИспользуемыеСклады);
	Запрос.УстановитьПараметр("ИсключаемыеСклады", ИсключаемыеСклады);
	Запрос.УстановитьПараметр("КодВключить", 1);
	Запрос.УстановитьПараметр("КодИсключить", 0);
	
	ЕстьОшибки = Ложь;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		НаборВключить = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		НаборИсключить = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоответствияОбъектовМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗаписьМаркетплейса", РезультатОперации.УчетнаяЗапись);
		ЭлементБлокировки.УстановитьЗначение("ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.Склад);
		ЭлементБлокировки.УстановитьЗначение("Объект1С", "Склад");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ИдентификаторСклада = "";
		НаименованиеСклада = "";
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			ВыборкаДействие = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Действие");
			Пока ВыборкаДействие.Следующий() Цикл
			
				Выборка = ВыборкаДействие.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					Если Выборка.ИдентификаторСклада <> NULL Тогда
						ИдентификаторСклада = Выборка.ИдентификаторСклада;
						НаименованиеСклада = Выборка.НаименованиеСклада;
					КонецЕсли;
					
					Если Выборка.ТипЗаписи() <> ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
						Продолжить;
					КонецЕсли;
					
					Если Выборка.ЭтоГруппа Тогда
						Продолжить;
					КонецЕсли;
					
					Если ВыборкаДействие.Действие = Запрос.Параметры.КодВключить Тогда
						Запись = НаборВключить.Добавить();
						Запись.НаименованиеОбъектаМаркетплейса = НаименованиеСклада;
					ИначеЕсли ВыборкаДействие.Действие = Запрос.Параметры.КодИсключить Тогда
						Запись = НаборИсключить.Добавить();
					Иначе
						Продолжить;
					КонецЕсли;
					
					Запись.УчетнаяЗаписьМаркетплейса = РезультатОперации.УчетнаяЗапись;
					Запись.ВидОбъектаМаркетплейса = Перечисления.ВидыОбъектовМаркетплейсов.Склад;
					Запись.ИдентификаторОбъектаМаркетплейса = ИдентификаторСклада;
					Запись.Объект1С = Выборка.Склад;
					
				КонецЦикла;
			КонецЦикла;
			
			// Сначала очистим без отбора по идентификаторам объектов маркетплейса
			Если НаборИсключить.Количество() > 0 Тогда
				НаборИсключить.ОбменДанными.Получатели.Добавить(УзелОбмена);
				НаборИсключить.Записать(РежимЗамещения.Удаление);
			КонецЕсли;
			
			// Добавим записи по определенным идентификаторам объектов маркетплейса
			Если НаборВключить.Количество() > 0 Тогда
				НаборВключить.ОбменДанными.Получатели.Добавить(УзелОбмена);
				НаборВключить.Записать(РежимЗамещения.Слияние);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(ИнтеграцияСМаркетплейсамиСИ.СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		КонецПопытки;
		
	КонецЕсли;

	Если Не ЕстьОшибки Тогда
		Кэш.ИсключаемыеСклады.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатПолучитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки)

	НастройкаОграниченийВыгрузкиОстатковТоваров.Очистить();

	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат;
	КонецЕсли;

	Для Каждого ОписаниеОграничения Из РезультатОперации.ОграниченияОстатковТоваров Цикл
		НоваяСтрока = НастройкаОграниченийВыгрузкиОстатковТоваров.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОграничения);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатВыгрузитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатПеренестиНоменклатуруПартнера(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатУдалитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("УчетныеЗаписи", Новый Соответствие);
	Кэш.Вставить("ИсключаемыеСклады", Новый Массив); // Массив из СправочникСсылка.Склады
	Кэш.Вставить("ПравоНаОстатки", Пользователи.РолиДоступны("РедактированиеОстатковТоваровМаркетплейсов"));
	Кэш.Вставить("ПравоНаЦены", ПравоДоступа("Изменение", Метаданные.Документы.УстановкаЦенНоменклатуры));
	Кэш.Вставить("ЭтоWildberries", ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса));
	Кэш.Вставить("ЭтоLamoda", ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// Выделение цветом измененных складов
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.НаклонныйШрифтБЭД);

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СкладыМаркетплейсаНаименованиеСкладаСервиса.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СкладыМаркетплейсаСклад.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СкладыМаркетплейсаСхемаРаботыСклада.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СкладыМаркетплейсаНаименованиеСкладаМП.Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СкладыМаркетплейса.Изменение");
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	// Выделение цветом измененных ограничений
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.НаклонныйШрифтБЭД);

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровИспользуется.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровОбластьДействия.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровПроцентОстатка.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровСтраховойЗапас.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровМинимальныйОстаток.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровМаксимальныйОстаток.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровНаименование.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровКод.Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкаОграниченийВыгрузкиОстатковТоваров.Изменение");
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	// Выделение цветом отмеченных к удалению ограничений на остатки
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветНедоступногоТекста);

	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровИспользуется.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровОбластьДействия.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровПроцентОстатка.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровСтраховойЗапас.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровМинимальныйОстаток.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровМаксимальныйОстаток.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровНаименование.Имя);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.НастройкаОграниченийВыгрузкиОстатковТоваровКод.Имя);

	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("НастройкаОграниченийВыгрузкиОстатковТоваров.Удаление");
	ОтборЭлемента.ПравоеЗначение = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьЭлементыФормы()
	Заголовок = СтрШаблон(НСтр("ru = 'Настройки интеграции с %1';
								|en = '%1 integration settings'"), 
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса));

	Если Кэш.ЭтоLamoda Тогда
		Элементы.ДекорацияПодсказкаФункциональностьОстатки.РасширеннаяПодсказка.Заголовок = 
			НСтр("ru = 'Работа с остатками недоступна по данным авторизации';
				|en = 'Operations with balances are not available with the current authorization data'") ;
		Элементы.ДекорацияПодсказкаФункциональностьЦены.РасширеннаяПодсказка.Заголовок = 
			НСтр("ru = 'Работа с ценами недоступна по данным авторизации';
				|en = 'Operations with prices are not available with the current authorization data'") ;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьПоУчетнойЗаписи(ОписаниеУчетнойЗаписи = Неопределено)
	
	ДоступностьПоУчетнойЗаписи = ЗначениеЗаполнено(УчетнаяЗапись);
	ФункциональностьОстатки = Ложь;
	ДоступностьОстатки = Ложь;
	ФункциональностьЦены = Ложь;
	ДоступностьЦены = Ложь;
	
	Элементы.Партнер.Доступность = ДоступностьПоУчетнойЗаписи;
	Элементы.Записать.Доступность = ДоступностьПоУчетнойЗаписи;
	
	Если ОписаниеУчетнойЗаписи = Неопределено И ДоступностьПоУчетнойЗаписи Тогда
		ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	КонецЕсли;
	
	Если ОписаниеУчетнойЗаписи <> Неопределено Тогда
		ФункциональностьОстатки = ОписаниеУчетнойЗаписи.Функциональность.Найти(1) <> Неопределено;
		ДоступностьОстатки = Кэш.ПравоНаОстатки И ФункциональностьОстатки;
		ФункциональностьЦены = ОписаниеУчетнойЗаписи.Функциональность.Найти(2) <> Неопределено;
		ДоступностьЦены = Кэш.ПравоНаЦены И ФункциональностьЦены;
	КонецЕсли;
	
	Элементы.ГруппаРазрешенаВыгрузкаОстатков.Доступность = ДоступностьОстатки;
	Элементы.ГруппаНастройкиОстатки.Доступность = ИспользоватьИнтеграциюПоОстаткам И ДоступностьОстатки;
	Элементы.ГруппаРазрешенаВыгрузкаЦен.Доступность = ДоступностьЦены;
	Элементы.ГруппаНастройкиЦены.Доступность = ИспользоватьИнтеграциюПоЦенам И ДоступностьЦены;
	
	Элементы.ДекорацияПодсказкаФункциональностьОстатки.Видимость = ДоступностьПоУчетнойЗаписи И Не ФункциональностьОстатки;
	Элементы.ДекорацияПодсказкаФункциональностьЦены.Видимость = ДоступностьПоУчетнойЗаписи И Не ФункциональностьЦены;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ДекорацияЛоготип.Картинка = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ВидМаркетплейса);
	Элементы.ГруппаСкладыWildberries.Видимость = Кэш.ЭтоWildberries;
	Элементы.ГруппаСкладыLamoda.Видимость = Кэш.ЭтоLamoda;
	Элементы.ВидЦеныКлубная.Видимость = Кэш.ЭтоWildberries;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФормуПоУчетнойЗаписи(ОписаниеУчетнойЗаписи)
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, "ВалютаУчета, ВидыЦен");
	
	Партнер = ОписаниеУчетнойЗаписи.Партнер;
	ПредыдущийПартнер = ОписаниеУчетнойЗаписи.Партнер;
	АвтоКоррекцияОстатков = ОписаниеУчетнойЗаписи.АвтоКоррекцияОстатков;
	ИспользоватьИнтеграциюПоОстаткам = ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоОстаткам;
	АвтоКоррекцияЦен = ОписаниеУчетнойЗаписи.АвтоКоррекцияЦен;
	ИспользоватьИнтеграциюПоЦенам = ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоЦенам;
	РазрешенаВыгрузкаЦенНаМаркетплейс = ОписаниеУчетнойЗаписи.РазрешенаВыгрузкаЦенНаМаркетплейс;
	УзелОбмена = НайтиСоздатьУзелОбменаСИ(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи.Представление, Ложь);
	
	ВалютаУчета = РеквизитыУчетнойЗаписи.ВалютаУчета;
	
	ВидЦеныДоСкидок = Неопределено;
	ВидЦеныСоСкидкойПродавца = Неопределено;
	ВидЦеныКлубная = Неопределено;
	
	//@skip-check pruned-dynamic-feature-access-translation-ambiguity
	ВыборкаВидовЦен = РеквизитыУчетнойЗаписи.ВидыЦен.Выбрать();
	Пока ВыборкаВидовЦен.Следующий() Цикл
		Если ВыборкаВидовЦен.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныДоСкидок() Тогда
			ВидЦеныДоСкидок = ВыборкаВидовЦен.ВидЦены;
		ИначеЕсли ВыборкаВидовЦен.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныСоСкидкойПродавца() Тогда
			ВидЦеныСоСкидкойПродавца = ВыборкаВидовЦен.ВидЦены;
		ИначеЕсли ВыборкаВидовЦен.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныКлубная() Тогда
			ВидЦеныКлубная = ВыборкаВидовЦен.ВидЦены;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактированияСоответствияСкладов(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = СкладыМаркетплейса.НайтиПоИдентификатору(ДополнительныеПараметры["ТекущаяСтрока"]);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаПройдена = ПроверитьПересечениеСкладовВНастройке(СкладыМаркетплейса,
		ТекущиеДанные.ИдентификаторСкладаСервиса, Результат.Склад);
	
	Если ПроверкаПройдена Тогда
		Если ТекущиеДанные.Склад <> Результат.Склад Тогда
			Если ЗначениеЗаполнено(ТекущийСклад) Тогда
				ИсключаемыеСклады = Кэш.ИсключаемыеСклады; // Массив

				ОписаниеСклада = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса();
				ОписаниеСклада.ИдентификаторСкладаСервиса = ТекущиеДанные.ИдентификаторСкладаСервиса;
				ОписаниеСклада.Склад = ТекущийСклад;
				ИсключаемыеСклады.Добавить(ОписаниеСклада);
				
			КонецЕсли;
			ТекущиеДанные.Склад = Результат.Склад;
			ТекущиеДанные.Изменение = Истина;
		КонецЕсли;
		Модифицированность = Истина;
	Иначе
		ТекущиеДанные.Склад = ТекущийСклад;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеРедактированияОграниченияВыгрузкиОстатков(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Добавление Тогда
		ИдентификаторСтроки = 0;
	Иначе
		ИдентификаторСтроки = ДополнительныеПараметры.ИдентификаторСтроки;
	КонецЕсли;
	
	Если ЕстьДублиОграничения(Результат, ИдентификаторСтроки) > 0 Тогда
		ТекстСообщение = СтрШаблон(НСтр("ru = 'Запись невозможна. Существует ограничение для области действия %1.';
										|en = 'Saving is impossible. There is a limit for the ""%1"" scope.'"), Результат.ОбластьДействия);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщение);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.Добавление Тогда
		РедактируемаяСтрока = НастройкаОграниченийВыгрузкиОстатковТоваров.Добавить();
	Иначе
		РедактируемаяСтрока = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиПоИдентификатору(
			ДополнительныеПараметры.ИдентификаторСтроки);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(РедактируемаяСтрока, Результат);
	
	РедактируемаяСтрока.Изменение = Истина;
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Функция ЕстьДублиОграничения(Результат, ИдентификаторСтроки)
	
	ПараметрыОтбора = Новый Структура("ОбластьДействия", Результат.ОбластьДействия);
	НайденныеОграничения = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(ПараметрыОтбора);
	
	Если НайденныеОграничения.Количество() = 0 Тогда
		ЕстьДубли = Ложь;
	ИначеЕсли НайденныеОграничения.Количество() > 1 Тогда
		ЕстьДубли = Истина;
	Иначе
		ЕстьДубли = НайденныеОграничения[0].ПолучитьИдентификатор() <> ИдентификаторСтроки;
	КонецЕсли;
	
	Возврат ЕстьДубли;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиСоздатьУзелОбменаСИ(УчетнаяЗапись, ИмяУзла, СоздатьПриОтсутствии)
	Возврат ПланыОбмена.МаркетплейсыСИ.НайтиСоздатьУзел(УчетнаяЗапись, СоздатьПриОтсутствии, ИмяУзла);
КонецФункции

&НаСервереБезКонтекста
Процедура ПриЗаписиНаСервере(Знач ОписаниеУчетнойЗаписи, Знач ДанныеФормы, УзелОбмена)
	
	Если Не ЗначениеЗаполнено(ОписаниеУчетнойЗаписи) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(УзелОбмена) Тогда
		
		// Создадим узел для сервиса интеграции по учетной записи
		УзелОбмена = НайтиСоздатьУзелОбменаСИ(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи.Представление,
			Истина);
		
		// Всю иерархию групп складов сразу зафиксируем на плане обмена.
		// Элементы будем фиксировать по мере их сопоставления со складами маркетплейсов
		Склады = Новый Массив;
		ПолучитьИерархиюСкладов(Склады);
		Если Склады.Количество() > 0 Тогда
			ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, Склады);
		КонецЕсли;

	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
		ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ДанныеФормы.УчетнаяЗапись);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
	
		УчетнаяЗаписьОбъект = ДанныеФормы.УчетнаяЗапись.ПолучитьОбъект();
		УчетнаяЗаписьОбъект.ВалютаУчета = ДанныеФормы.ВалютаУчета;
		УчетнаяЗаписьОбъект.Партнер = ДанныеФормы.Партнер;
		УчетнаяЗаписьОбъект.ВидыЦен.Очистить();
		
		Если ЗначениеЗаполнено(ДанныеФормы.ВидЦеныДоСкидок) Тогда
			НоваяСтрока = УчетнаяЗаписьОбъект.ВидыЦен.Добавить();
			НоваяСтрока.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныДоСкидок();
			НоваяСтрока.ВидЦены = ДанныеФормы.ВидЦеныДоСкидок;
		КонецЕсли;
		Если ЗначениеЗаполнено(ДанныеФормы.ВидЦеныСоСкидкойПродавца) Тогда
			НоваяСтрока = УчетнаяЗаписьОбъект.ВидыЦен.Добавить();
			НоваяСтрока.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныСоСкидкойПродавца();
			НоваяСтрока.ВидЦены = ДанныеФормы.ВидЦеныСоСкидкойПродавца;
		КонецЕсли;
		Если ДанныеФормы.ЭтоWildberries И ЗначениеЗаполнено(ДанныеФормы.ВидЦеныКлубная) Тогда
			НоваяСтрока = УчетнаяЗаписьОбъект.ВидыЦен.Добавить();
			НоваяСтрока.ИмяНастройки = ИнтеграцияСМаркетплейсамиКлиентСервер.ИмяВидЦеныКлубная();
			НоваяСтрока.ВидЦены = ДанныеФормы.ВидЦеныКлубная;
		КонецЕсли;
		УчетнаяЗаписьОбъект.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ДанныеФормы.УчетнаяЗапись),
			УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Ошибка при восстановлении ""%1"". Возможно, объект уже редактируется.';
				|en = 'An error occurred when restoring ""%1"". The object might be locked for editing.'"),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеФормы.УчетнаяЗапись, "Наименование")));
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьИерархиюСкладов(Склады)

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Склады.Ссылка КАК Склад
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Ссылка В ИЕРАРХИИ(&Склады)
	|	И НЕ Склады.ЭтоГруппа
	|ИТОГИ ПО
	|	Склад ИЕРАРХИЯ";

	Запрос.УстановитьПараметр("Склады", Склады);

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Склады.Найти(Выборка.Склад) = Неопределено Тогда
			Склады.Добавить(Выборка.Склад);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьПересечениеСкладовВНастройке(Знач СкладыМаркетплейса, Знач ИдентификаторСкладаСервиса, Знач Склад)

	ПроверкаПройдена = Истина;

	Если Не ЗначениеЗаполнено(Склад) Тогда
		Возврат ПроверкаПройдена;
	КонецЕсли;

	СообщенияПроверки = Новый Массив;

	СчетчикСкладов = СкладыМаркетплейса.Количество() - 1;
	Пока СчетчикСкладов >= 0 Цикл
		Если Не ЗначениеЗаполнено(СкладыМаркетплейса[СчетчикСкладов].Склад) Тогда
			СкладыМаркетплейса.Удалить(СчетчикСкладов);
		ИначеЕсли СкладыМаркетплейса[СчетчикСкладов].ИдентификаторСкладаСервиса = ИдентификаторСкладаСервиса Тогда
			СкладыМаркетплейса.Удалить(СчетчикСкладов);
		КонецЕсли;

		СчетчикСкладов = СчетчикСкладов - 1;
	КонецЦикла;

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СкладыМаркетплейса", СкладыМаркетплейса.Выгрузить( , "НаименованиеСкладаСервиса, Склад"));
	Запрос.УстановитьПараметр("УстанавливаемыйСклад", Склад);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СкладыМаркетплейса.НаименованиеСкладаСервиса КАК СкладМаркетплейса,
	|	СкладыМаркетплейса.Склад КАК Склад
	|ПОМЕСТИТЬ врНастроенныеСоответствия
	|ИЗ
	|	&СкладыМаркетплейса КАК СкладыМаркетплейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(Склады.Ссылка) КАК ПредставлениеСклада,
	|	Склады.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Ссылка = &УстанавливаемыйСклад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врНастроенныеСоответствия.СкладМаркетплейса КАК СкладМаркетплейса,
	|	ПРЕДСТАВЛЕНИЕ(Склады.Наименование) КАК ПредставлениеСклада1С,
	|	Склады.ЭтоГруппа КАК ЭтоГруппа,
	|	врНастроенныеСоответствия.Склад = &УстанавливаемыйСклад КАК ЭтоВыбранныйСклад
	|ИЗ
	|	врНастроенныеСоответствия КАК врНастроенныеСоответствия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО врНастроенныеСоответствия.Склад = Склады.Ссылка
	|ГДЕ
	|	врНастроенныеСоответствия.Склад В ИЕРАРХИИ (&УстанавливаемыйСклад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врНастроенныеСоответствия.СкладМаркетплейса КАК СкладМаркетплейса,
	|	Склады.Наименование КАК НаименованиеСклада1С,
	|	ПРЕДСТАВЛЕНИЕ(Склады.Наименование) КАК ПредставлениеСклада1С,
	|	Склады.ЭтоГруппа КАК ЭтоГруппа,
	|	врНастроенныеСоответствия.Склад = &УстанавливаемыйСклад КАК ЭтоВыбранныйСклад
	|ИЗ
	|	врНастроенныеСоответствия КАК врНастроенныеСоответствия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО врНастроенныеСоответствия.Склад = Склады.Ссылка
	|		И Склады.ЭтоГруппа";

	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);

	// Проверка на вхождение записанных складов в иерархию выбранного склада.

	МассивПересечений = Новый Массив;

	ПолноеНаименованиеУстанавливаемогоСклада = "";
	ПредставлениеСклада = "";
	ЭтоГруппаСкладов = Ложь;

	ВыборкаСкладов = ПакетЗапроса[ПакетЗапроса.ВГраница() - 2].Выбрать();
	Если ВыборкаСкладов.Следующий() Тогда
		ПолноеНаименованиеУстанавливаемогоСклада = "/" + Склад.ПолноеНаименование() + "/";
		ПредставлениеСклада = ВыборкаСкладов.ПредставлениеСклада;
		ЭтоГруппаСкладов = ВыборкаСкладов.ЭтоГруппа;
	КонецЕсли;

	Если ЭтоГруппаСкладов Тогда
		ШаблонСовпаденияСклада = НСтр(
			"ru = 'Устанавливаемая группа складов <%1> совпадает со значением для склада маркетплейса <%2>.';
			|en = 'The <%1> warehouse group to set matches the value for the <%2> marketplace warehouse.'");
	Иначе
		ШаблонСовпаденияСклада = НСтр(
			"ru = 'Устанавливаемый склад <%1> совпадает со значением для склада маркетплейса <%2>.';
			|en = 'The <%1> warehouse to set matches the value for the <%2> marketplace warehouse.'");
	КонецЕсли;

	ШаблонПересеченияСклада = " - " + НСтр("ru = 'склад 1С <%1>, установленный для склада маркетплейса <%2>';
											|en = 'the <%1> 1C warehouse set for the <%2> marketplace warehouse'");
	ШаблонПересеченияГруппыСклада = " - " + НСтр(
		"ru = 'группа складов 1С <%1>, установленная для склада маркетплейса <%2>';
		|en = 'the <%1> 1C warehouse group set for the <%2> marketplace warehouse'");

	ВыборкаСкладов = ПакетЗапроса[ПакетЗапроса.ВГраница() - 1].Выбрать();
	Пока ВыборкаСкладов.Следующий() Цикл
		Если ВыборкаСкладов.ЭтоВыбранныйСклад Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСовпаденияСклада,
				ПредставлениеСклада, ВыборкаСкладов.СкладМаркетплейса);

			СообщенияПроверки.Добавить(ТекстСообщения);

			ПроверкаПройдена = Ложь;
			Продолжить;
		КонецЕсли;

		Шаблон = ?(ВыборкаСкладов.ЭтоГруппа, ШаблонПересеченияГруппыСклада, ШаблонПересеченияСклада);

		ОписаниеПересечения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Шаблон,
			ВыборкаСкладов.ПредставлениеСклада1С, ВыборкаСкладов.СкладМаркетплейса);

		МассивПересечений.Добавить(ОписаниеПересечения);
	КонецЦикла;

	Если МассивПересечений.Количество() > 0 Тогда
		ШаблонСообщенияПроверки = НСтр("ru = 'Обнаружены склады, входящие в состав выбранной группы складов <%1>: %2.';
										|en = 'Warehouses included in the selected <%1> warehouse group are found: %2.'");

		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщенияПроверки,
			ПредставлениеСклада, Символы.ПС + СтрСоединить(МассивПересечений, ";" + Символы.ПС));

		СообщенияПроверки.Добавить(ТекстСообщения);

		ПроверкаПройдена = Ложь;
	КонецЕсли;

	// Проверка на вхождение выбранного склада в иерархию записанных складов.

	Если ЗначениеЗаполнено(ПолноеНаименованиеУстанавливаемогоСклада) Тогда
		МассивПересечений = Новый Массив;

		ВыборкаСкладов = ПакетЗапроса[ПакетЗапроса.ВГраница()].Выбрать();
		Пока ВыборкаСкладов.Следующий() Цикл
			Если ВыборкаСкладов.ЭтоВыбранныйСклад Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСовпаденияСклада,
					ПредставлениеСклада, ВыборкаСкладов.СкладМаркетплейса);

				СообщенияПроверки.Добавить(ТекстСообщения);

				ПроверкаПройдена = Ложь;
				Продолжить;
			КонецЕсли;

			НаименованиеСкладаВыборки = "/" + ВыборкаСкладов.НаименованиеСклада1С + "/";
			Если СтрНайти(ПолноеНаименованиеУстанавливаемогоСклада, НаименованиеСкладаВыборки) Тогда
				ОписаниеПересечения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонПересеченияГруппыСклада, ВыборкаСкладов.ПредставлениеСклада1С,
					ВыборкаСкладов.СкладМаркетплейса);

				МассивПересечений.Добавить(ОписаниеПересечения);

				ПроверкаПройдена = Ложь;
			КонецЕсли;
		КонецЦикла;

		Если МассивПересечений.Количество() > 0 Тогда
			Если ЭтоГруппаСкладов Тогда
				ШаблонСообщенияПроверки = НСтр(
					"ru = 'Обнаружены группы складов, в состав которых входит выбранная группа складов <%1>: %2.';
					|en = 'Warehouse groups containing the selected <%1> warehouse group are found: %2.'");
			Иначе
				ШаблонСообщенияПроверки = НСтр(
					"ru = 'Обнаружены группы складов, в состав которых входит выбранный склад <%1>: %2.';
					|en = 'Warehouse groups containing the selected <%1> warehouse are found: %2.'");
			КонецЕсли;

			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку( ШаблонСообщенияПроверки,
				ПредставлениеСклада, Символы.ПС + СтрСоединить(МассивПересечений, ";" + Символы.ПС));

			СообщенияПроверки.Добавить(ТекстСообщения);

			ПроверкаПройдена = Ложь;
		КонецЕсли;
	КонецЕсли;

	Если СообщенияПроверки.Количество() > 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрСоединить(СообщенияПроверки, Символы.ПС));
	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		ПроверкаПройдена = Истина;
	КонецЕсли;

	Возврат ПроверкаПройдена;

КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаписатьНастройкиИнтеграции();
		Закрыть();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиНаКлиенте(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНастройкиИнтеграции();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНастройкиИнтеграции()
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОтменитьМодифицированность = Истина;
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи());
	
	Отбор = Новый Структура("Изменение", Истина);
	НайденныеСтроки = СкладыМаркетплейса.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады());
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьСкладыМаркетплейса());
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса());
	КонецЕсли;
		
	ПолучитьОграниченияОстатковТоваров = Ложь;
	
	Отбор = Новый Структура("Изменение, Удаление", Истина, Ложь);
	НайденныеСтроки = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ПолучитьОграниченияОстатковТоваров = Истина;
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОграниченияОстатковТоваров());
	КонецЕсли;
	
	Отбор = Новый Структура("Удаление", Истина);
	НайденныеСтроки = НастройкаОграниченийВыгрузкиОстатковТоваров.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() > 0 Тогда
		ПолучитьОграниченияОстатковТоваров = Истина;
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьОграниченияОстатковТоваров());
	КонецЕсли;
	
	Если ПолучитьОграниченияОстатковТоваров Тогда
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПредыдущийПартнер) И ПредыдущийПартнер <> Партнер Тогда
		ПараметрыМетода = Новый Структура("ЗависитОт, ЗависимыеПоля", ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи(), "");
		ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПеренестиНоменклатуруПартнера());
	КонецЕсли;
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьПриИзмененииПродолжение()
	
	ОписаниеУчетнойЗаписи = Неопределено;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		
		Партнер = Неопределено;
		ПредыдущийПартнер = Неопределено;
		УзелОбмена = Неопределено;
		ВалютаУчета = Неопределено;
		ВидЦеныДоСкидок = Неопределено;
		ВидЦеныСоСкидкойПродавца = Неопределено;
		ВидЦеныКлубная = Неопределено;
		СкладыМаркетплейса.Очистить();
		НастройкаОграниченийВыгрузкиОстатковТоваров.Очистить();
		АвтоКоррекцияОстатков = Ложь;
		ИспользоватьИнтеграциюПоОстаткам = Ложь;
		АвтоКоррекцияЦен = Ложь;
		ИспользоватьИнтеграциюПоЦенам = Ложь;
		РазрешенаВыгрузкаЦенНаМаркетплейс = Ложь;
		Элементы.ГруппаСостояниеВСервисе.Видимость = Ложь;
		
	Иначе
		
		ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
		ЗаполнитьФормуПоУчетнойЗаписи(ОписаниеУчетнойЗаписи);
		Кэш.ИсключаемыеСклады.Очистить();
		
		// См. ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительнуюОперацию
		ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
		ПараметрыМетода = Новый Структура("УчетнаяЗапись, ОбновлениеСостояния", УчетнаяЗапись, Истина);
		ПараметрыОперации.Очередь.Добавить(ПараметрыМетода, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
		НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
		
	КонецЕсли;
	
	УстановитьВидимостьДоступностьПоУчетнойЗаписи(ОписаниеУчетнойЗаписи);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПартнерУказанДляРазныхУчетныхЗаписей(Партнер)
	УчетныеЗаписиПартнера = Справочники.УчетныеЗаписиМаркетплейсов.УчетныеЗаписиПоПартнеру(Партнер);
	Возврат УчетныеЗаписиПартнера.Количество() > 1;
КонецФункции

#КонецОбласти