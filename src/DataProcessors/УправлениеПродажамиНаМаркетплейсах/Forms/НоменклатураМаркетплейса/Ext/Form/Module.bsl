#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьКэшированныеЗначения();
	
	Если ЗначениеЗаполнено(Параметры.ОписаниеУчетнойЗаписи) Тогда
		УчетнаяЗапись = Параметры.ОписаниеУчетнойЗаписи.УчетнаяЗапись;
		Кэш.УчетныеЗаписи.Вставить(УчетнаяЗапись, Параметры.ОписаниеУчетнойЗаписи);
	КонецЕсли;
	
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	НоменклатураПартнераОбъект = Параметры.НоменклатураПартнера.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(НоменклатураПартнераОбъект, "Объект");
	
	Заголовок = СтрШаблон("%1 : %2", Заголовок, Объект.Наименование); 
	
	Если Объект.КоличествоБазовойЕдиницыИзмерения <> 1 Тогда
		ВидУпаковки = "Упаковка";
	ИначеЕсли Объект.КоличествоУпаковок <> 1 Тогда
		ВидУпаковки = "Разупаковка";
	Иначе
		ВидУпаковки = "Упаковка";
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВидУпаковки"                        , ВидУпаковки);
	ДополнительныеПараметры.Вставить("НаименованиеУпаковки"               , Объект.НаименованиеУпаковки);
	ДополнительныеПараметры.Вставить("НаименованиеБазовойЕдиницыИзмерения", Объект.НаименованиеБазовойЕдиницыИзмерения);
	ДополнительныеПараметры.Вставить("КодОКЕИБазовойЕдиницыИзмерения"     , Объект.КодОКЕИБазовойЕдиницыИзмерения);
	ДополнительныеПараметры.Вставить("КоличествоБазовойЕдиницыИзмерения"  , Объект.КоличествоБазовойЕдиницыИзмерения);
	ДополнительныеПараметры.Вставить("КоличествоУпаковок"                 , Объект.КоличествоУпаковок);
	
	ЗаголовокНачало    = "";
	ЗаголовокОкончание = "";
	
	ИзменитьОтображениеВидаУпаковки(Элементы, ДополнительныеПараметры, ЗаголовокНачало, ЗаголовокОкончание);
	
	ЗаголовокНачало    = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокНачало);
	ЗаголовокОкончание = СтроковыеФункции.ФорматированнаяСтрока(ЗаголовокОкончание);
	
	УстановитьЗаголовокПредставленияЕдиницыИзмерения(Элементы, ЗаголовокНачало, ЗаголовокОкончание);
	
	УстановитьСвойстваПереопределяемыхЭлементовФормы();
	
	ПриЧтенииСозданииНаСервере();
	ТолькоПросмотр = Не ПравоДоступа("Изменение", Метаданные.Справочники.НоменклатураКонтрагентов);
	
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзмененииСервер();
	
	Данные = Новый Структура;
	Данные.Вставить("НаименованиеБазовойЕдиницыИзмерения", Объект.НаименованиеБазовойЕдиницыИзмерения);
	Данные.Вставить("КоличествоБазовойЕдиницыИзмерения", Объект.КоличествоБазовойЕдиницыИзмерения);
	Данные.Вставить("ВидУпаковки", ВидУпаковки);
	Данные.Вставить("НаименованиеУпаковки", Объект.НаименованиеУпаковки);
	Данные.Вставить("КоличествоУпаковок", Объект.КоличествоУпаковок);
	
	ИзменитьЕдиницуИзмеренияЗавершение(Данные, Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЕдиницуИзмеренияНажатие(Элемент)
	ИзменитьЕдиницуИзмеренияПродолжить();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИзменитьЕдиницуИзмеренияНажатие(Элемент)
	ИзменитьЕдиницуИзмеренияПродолжить();
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИзменитьЕдиницуИзмерения1Нажатие(Элемент)
	ИзменитьЕдиницуИзмеренияПродолжить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИзменения(Команда)
	
	Отказ = Ложь;
	ЗаписатьОбъект(Отказ);
	
	Если Не Отказ Тогда
		ОповеститьОбИзменении(Параметры.НоменклатураПартнера);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("УчетныеЗаписи", Новый Соответствие);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		НаборНоменклатуры = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Номенклатура);
		СвойстваНоменклатурИБ = Новый Соответствие;
		СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриОпределенииСвойствНоменклатурыИнформационнойБазы(НаборНоменклатуры, СвойстваНоменклатурИБ);
		
		СвойстваНоменклатуры  = СвойстваНоменклатурИБ[Объект.Номенклатура];
		Объект.ИспользоватьХарактеристики = СвойстваНоменклатуры.ИспользоватьХарактеристики;
		ЕдиницаИзмеренияПоУмолчанию       = СвойстваНоменклатуры.ЕдиницаИзмеренияПоУмолчанию;
		Элементы.Упаковка.ПодсказкаВвода  = ЕдиницаИзмеренияПоУмолчанию;
	КонецЕсли;
	
	Элементы.Характеристика.Доступность = Объект.ИспользоватьХарактеристики;
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииСервер()
	
	ДанныеСопоставления = Новый Структура;
	ДанныеСопоставления.Вставить("Номенклатура"               , Объект.Номенклатура);
	ДанныеСопоставления.Вставить("Характеристика"             , Объект.Характеристика);
	ДанныеСопоставления.Вставить("ИспользоватьХарактеристики" , Объект.ИспользоватьХарактеристики);
	ДанныеСопоставления.Вставить("Упаковка"                   , Объект.Упаковка);
	ДанныеСопоставления.Вставить("ЕдиницаИзмеренияПоУмолчанию", ЕдиницаИзмеренияПоУмолчанию);
	
	СопоставлениеНоменклатурыКонтрагентовПереопределяемый.ПриИзмененииНоменклатурыСопоставления_НоменклатураКонтрагентов(ЭтотОбъект, ДанныеСопоставления);
	
	ЗаполнитьЗначенияСвойств(Объект, ДанныеСопоставления);
	
	Если ЗначениеЗаполнено(Объект.Номенклатура) Тогда
		Если Не ЗначениеЗаполнено(Объект.Упаковка) Тогда
			Объект.Упаковка = ЕдиницаИзмеренияПоУмолчанию;
		КонецЕсли;
		
		Элементы.Характеристика.Доступность = ДанныеСопоставления.ИспользоватьХарактеристики;
		ЕдиницаИзмеренияПоУмолчанию         = ДанныеСопоставления.ЕдиницаИзмеренияПоУмолчанию;
		Элементы.Упаковка.ПодсказкаВвода    = ЕдиницаИзмеренияПоУмолчанию;
		
		Объект.НаименованиеБазовойЕдиницыИзмерения = ЕдиницаИзмеренияПоУмолчанию;
		Если Объект.КоличествоБазовойЕдиницыИзмерения = 0 Тогда
			Объект.КоличествоБазовойЕдиницыИзмерения = 1;
		КонецЕсли;
		
		Объект.НаименованиеУпаковки = ЕдиницаИзмеренияПоУмолчанию;
		Если Объект.КоличествоУпаковок = 0 Тогда
			Объект.КоличествоУпаковок = 1;
		КонецЕсли;
	Иначе
		Объект.Упаковка = Неопределено;
		Объект.КоличествоБазовойЕдиницыИзмерения = 0;
		Объект.КоличествоУпаковок = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЕдиницуИзмеренияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Объект, Результат);
	ВидУпаковки = Результат.ВидУпаковки;
	
	ЗаголовокНачало    = "";
	ЗаголовокОкончание = "";
	
	ИзменитьОтображениеВидаУпаковки(Элементы, Результат, ЗаголовокНачало, ЗаголовокОкончание);
	
	ЗаголовокНачало    = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ЗаголовокНачало);
	ЗаголовокОкончание = СтроковыеФункцииКлиент.ФорматированнаяСтрока(ЗаголовокОкончание);
	
	УстановитьЗаголовокПредставленияЕдиницыИзмерения(Элементы, ЗаголовокНачало, ЗаголовокОкончание);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьОтображениеВидаУпаковки(Элементы, Параметры, ЗаголовокНачало, ЗаголовокОкончание)
	
	Если НЕ ЗначениеЗаполнено(Параметры.НаименованиеБазовойЕдиницыИзмерения) Тогда
		Элементы.СтраницыПредставлениеУпаковки.ТекущаяСтраница = Элементы.СтраницаУказатьУпаковку;
		Возврат;
	ИначеЕсли Не УпаковкаИБазоваяЕдиницаИзмеренияРазличны(Параметры) Тогда
		Элементы.СтраницыПредставлениеУпаковки.ТекущаяСтраница = Элементы.СтраницаБазовойЕдиницыИзмерения;
	Иначе
		Элементы.СтраницыПредставлениеУпаковки.ТекущаяСтраница = Элементы.СтраницыЕдиницыИзмерения;
	КонецЕсли;
	
	Если Параметры.ВидУпаковки = "Упаковка" Тогда
		
		Элементы.СтраницыВидаУпаковок.ТекущаяСтраница = Элементы.СтраницаУпаковка;
		
		Элементы.КоличествоУпаковок.Видимость                = Ложь;
		Элементы.КоличествоБазовойЕдиницыИзмерения.Видимость = Истина;
		
		ЗаголовокНачало    = СтрШаблон(НСтр("ru = '<b>1 %1</b> состоит из';
											|en = '<b>1 %1</b> consists of'"), Параметры.НаименованиеУпаковки);
		ЗаголовокОкончание = СтрШаблон("<b>%1</b>", Параметры.НаименованиеБазовойЕдиницыИзмерения);
		
	ИначеЕсли Параметры.ВидУпаковки = "Разупаковка" Тогда
		
		Элементы.СтраницыВидаУпаковок.ТекущаяСтраница = Элементы.СтраницаРазупаковка;
		
		Элементы.КоличествоУпаковок.Видимость                = Истина;
		Элементы.КоличествоБазовойЕдиницыИзмерения.Видимость = Ложь;
		
		ЗаголовокНачало    = СтрШаблон("<b>%1</b>", Параметры.НаименованиеУпаковки);
		ЗаголовокОкончание = СтрШаблон(НСтр("ru = 'содержится в <b>1 %1</b>';
											|en = 'contained in <b>1 %1</b>'"), Параметры.НаименованиеБазовойЕдиницыИзмерения);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗаголовокПредставленияЕдиницыИзмерения(Элементы, ЗаголовокНачало, ЗаголовокОкончание)
	
	Элементы.ПредставлениеЧислителяЗнаменателяНачало.Заголовок     = ЗаголовокНачало;
	Элементы.ПредставлениеЧислителяЗнаменателяОкончание.Заголовок  = ЗаголовокОкончание;
	Элементы.ПредставлениеЧислителяЗнаменателяОкончание1.Заголовок = ЗаголовокОкончание;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваПереопределяемыхЭлементовФормы()

	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ПредставлениеЗаголовка = НСтр("ru = '(Создание)';
										|en = '(Create)'");
	Иначе
		ПредставлениеЗаголовка = Объект.Наименование;
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru = '%1: %2';
								|en = '%1: %2'"), НСтр("ru = 'Номенклатура партнера';
														|en = 'Vendor part number'"), ПредставлениеЗаголовка);
	
	НоваяСвязь = Новый СвязьПараметраВыбора("Номенклатура", "Объект.Номенклатура");
	ВсеСвязи = Новый Массив();
	ВсеСвязи.Добавить(НоваяСвязь);
	
	Элементы.Характеристика.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
	Элементы.Упаковка.СвязиПараметровВыбора = Новый ФиксированныйМассив(ВсеСвязи);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОбъект(Отказ)
	
	ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
	Если Не ДанныеОбъекта.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта.ДополнительныеСвойства.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	Попытка
		ДанныеОбъекта.Заблокировать();
		ДанныеОбъекта.Записать();
	Исключение
		Отказ = Истина;
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаписьВДанныеТорговойПлощадки(Источник, Приемник, Отступ = 0)
	
	ШаблонЗаголовка = Прав("                     %1 :", (Отступ + 4));
	ШаблонСтроки1 = Прав("                     %1", (Отступ + 2));
	ШаблонСтроки2 = Прав("                     %1 : %2", (Отступ + 7));
	
	Для Каждого Запись Из Источник Цикл
		
		Если ТипЗнч(Запись) = Тип("КлючИЗначение") Тогда
			
			Если ТипЗнч(Запись.Значение) = Тип("Строка") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Запись.Значение));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Число") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Формат(Запись.Значение, "ЧН=0; ЧГ=0")));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Дата") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Запись.Значение));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Булево") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Формат(Запись.Значение, "БЛ=Нет; БИ=Да")));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Соответствие") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонЗаголовка, Запись.Ключ));
				
				ДобавитьЗаписьВДанныеТорговойПлощадки(Запись.Значение, Приемник, (Отступ + 3));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Массив") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонЗаголовка, Запись.Ключ));
				
				ДобавитьЗаписьВДанныеТорговойПлощадки(Запись.Значение, Приемник, (Отступ + 3));
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Запись) = Тип("Строка") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Запись));
		ИначеЕсли ТипЗнч(Запись) = Тип("Число") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Формат(Запись, "ЧН=0; ЧГ=0")));
		ИначеЕсли ТипЗнч(Запись) = Тип("Дата") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Запись));
		ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Булево") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Формат(Запись, "БЛ=Нет; БИ=Да")));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЕдиницуИзмеренияПродолжить()
	
	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидУпаковки"                        , ВидУпаковки);
	ПараметрыФормы.Вставить("НаименованиеУпаковки"               , Объект.НаименованиеУпаковки);
	ПараметрыФормы.Вставить("НаименованиеБазовойЕдиницыИзмерения", Объект.НаименованиеБазовойЕдиницыИзмерения);
	ПараметрыФормы.Вставить("КодОКЕИБазовойЕдиницыИзмерения"     , Объект.КодОКЕИБазовойЕдиницыИзмерения);
	ПараметрыФормы.Вставить("КоличествоБазовойЕдиницыИзмерения"  , Объект.КоличествоБазовойЕдиницыИзмерения);
	ПараметрыФормы.Вставить("КоличествоУпаковок"                 , Объект.КоличествоУпаковок);
	
	ОповещениеОЗакрытие = Новый ОписаниеОповещения("ИзменитьЕдиницуИзмеренияЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.НоменклатураКонтрагентов.Форма.ДобавлениеУпаковки", ПараметрыФормы, ЭтотОбъект, , , ,
		ОповещениеОЗакрытие, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуНоменклатурыМаркетплейса());
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуНоменклатурыМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьКарточкуНоменклатурыМаркетплейса(Операция.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьКарточкуНоменклатурыМаркетплейса(ПараметрыЗапроса)
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьКарточкуНоменклатурыМаркетплейса();
	
	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыЗапроса.ИдентификаторНоменклатурыСервиса = Объект.Идентификатор;
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
		Возврат;
	КонецЕсли;
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		Метод = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуНоменклатурыМаркетплейса() Тогда
			ОбработатьРезультатПолучитьКарточкуНоменклатурыМаркетплейса(РезультатОперации, РезультатОбработки);
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолучитьКарточкуНоменклатурыМаркетплейса(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Карточка = РезультатОперации.НоменклатураМаркетплейса;	// см. ИнтеграцияСМаркетплейсамиСИ.НовыйКарточкаНоменклатурыМаркетплейса
	
	ЧастиДанныхТорговойПлощадки = Новый Массив;
	
	Если ЗначениеЗаполнено(Карточка.ДополнительныеСведения) Тогда
		ДобавитьЗаписьВДанныеТорговойПлощадки(Карточка.ДополнительныеСведения, ЧастиДанныхТорговойПлощадки);
	КонецЕсли;
	
	ДанныеТорговойПлощадки = СтрСоединить(ЧастиДанныхТорговойПлощадки, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

&НаСервереБезКонтекста
Функция УпаковкаИБазоваяЕдиницаИзмеренияРазличны(СвойстваУпаковки)
	
	Возврат Не ((СвойстваУпаковки.КоличествоУпаковок = 1 И СвойстваУпаковки.КоличествоБазовойЕдиницыИзмерения = 1
		Или СвойстваУпаковки.КоличествоУпаковок = 0 И СвойстваУпаковки.КоличествоБазовойЕдиницыИзмерения = 0)
		И СвойстваУпаковки.НаименованиеУпаковки = СвойстваУпаковки.НаименованиеБазовойЕдиницыИзмерения);
		
	КонецФункции

#КонецОбласти