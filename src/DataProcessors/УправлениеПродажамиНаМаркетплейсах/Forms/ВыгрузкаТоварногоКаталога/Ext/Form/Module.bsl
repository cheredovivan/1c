#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ПравоДоступа("Просмотр", Метаданные.Обработки.УправлениеПродажамиНаМаркетплейсах) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Недостаточно прав для работы с данной формой.';
													|en = 'Insufficient rights to access the form.'"));
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ДобавитьЭлементыСостояния(ЭтотОбъект, Элементы.ГруппаУчетнаяЗапись);
	
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	
	УстановитьКэшированныеЗначения();
	ИнициализацияЦеныОстаткиТоваровМП();
	ИнициализироватьЭлементыФормы();
	
	ОстаткиТоваровМПРЗВключено = Ложь;
	ЦеныТоваровМПРЗВключено = Ложь;
	
	Элементы["ПодсказкаНоменклатураПартнеров"].Видимость = Ложь;
	Элементы["СкрытьПодсказкуНоменклатураПартнеров"].Видимость = Ложь;
	Элементы["ПодсказкаОстаткиТоваровМП"].Видимость = Ложь;
	Элементы["СкрытьПодсказкуОстаткиТоваровМП"].Видимость = Ложь;
	Элементы["ПодсказкаЦеныОстаткиТоваровМП"].Видимость = Ложь;
	Элементы["СкрытьПодсказкуЦеныОстаткиТоваровМП"].Видимость = Ложь;
	
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьКнопкуОтбораПоСтатусу(Команды.ОтобратьПоказатьВсе.Имя);
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		ПараметрыОбъекта = Новый Структура;
		ПараметрыОбъекта.Вставить("ИмяФормы", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ИмяФормы);
		ПараметрыОбъекта.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
		
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций, ПараметрыОбъекта);
		Кэш.ПараметрыМенеджераДлительныхОпераций = Неопределено;
	КонецЕсли;
	
	РаскраситьКнопкиПанелиРазделов();
	
	ПодключитьОбработчикОжидания("Подключаемый_СинхронизироватьОстатки", 180, Истина);
	ПодключитьОбработчикОжидания("Подключаемый_СинхронизироватьЦены", 180, Истина);
	
	Обработчик = Новый ОписаниеОповещения("Подключаемый_ЦеныОжиданиеПолученияСтраницыДанных", ЭтотОбъект);
	УведомленияКлиента.ПодключитьОбработчик("Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций", Обработчик);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ИмяСобытияОбновленияНастроект = СтрШаблон("%1_ОбновлениеНастроек",
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркетплейсПоВиду(ВидМаркетплейса));
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИмяСобытияОбновленияНастроект Тогда
		
		ПолучитьУчетныеЗаписи("ОбновленыНастройки");
		
	ИначеЕсли ИмяСобытия = "Маркетплейсы_УчетнаяЗаписьУдаление" Тогда
		
		УчетнаяЗаписьПоиск = Элементы.УчетнаяЗапись.СписокВыбора.НайтиПоЗначению(Параметр);
		Если УчетнаяЗаписьПоиск <> Неопределено Тогда
			Элементы.УчетнаяЗапись.СписокВыбора.Удалить(УчетнаяЗаписьПоиск);
		КонецЕсли;
		
		Кэш.УчетныеЗаписи.Удалить(Параметр);
		
		Если УчетнаяЗапись = Параметр Тогда
			УчетнаяЗапись = Неопределено;
			УчетнаяЗаписьПриИзмененииПродолжение();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	ОтключитьОбработчикОжидания("Подключаемый_СинхронизироватьОстатки");
	ОтключитьОбработчикОжидания("Подключаемый_СинхронизироватьЦены");
	УведомленияКлиента.ОтключитьОбработчик("Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)
	
	ОчиститьСообщения();
	УчетнаяЗаписьПриИзмененииПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиТоваровМПРЗВключеноПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОстаткиТоваровМПРЗВключено = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	ОстаткиТоваровМПРЗВключено = ОстаткиТоваровМПИзменитьСостояниеРегламентногоЗадание(ВидМаркетплейса,
		ОписаниеУчетнойЗаписи,
		ОстаткиТоваровМПРЗВключено);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеныТоваровМПРЗВключеноПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЦеныТоваровМПРЗВключено = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОтменитьМодифицированность = Истина;
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	
	ПараметрыМетода = Новый Структура("РазрешенаВыгрузкаЦенНаМаркетплейс", ЦеныТоваровМПРЗВключено);
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СостояниеВСервисеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Показать" Тогда
		ПоказатьПредупреждение( , Элементы.СостояниеВСервисе.Подсказка);
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНоменклатураПартнеров

&НаКлиенте
Процедура НоменклатураПартнеровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.НоменклатураПартнеров.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаИзмененияНоменклатурыМаркетплейса", ЭтотОбъект);
	
	Если Поле.Имя = "НоменклатураПартнеровНоменклатура" Тогда
		
		ПараметрыФормы.Вставить("ВидМаркетплейса", ВидМаркетплейса);
		ПараметрыФормы.Вставить("НоменклатураПартнера", ТекущиеДанные.НоменклатураПартнера);
		ПараметрыФормы.Вставить("ОписаниеУчетнойЗаписи", Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись));
		
		ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НоменклатураМаркетплейса", ПараметрыФормы,
			ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	Иначе
		
		ПараметрыФормы.Вставить("Ключ", ТекущиеДанные.НоменклатураПартнера);
		ОткрытьФорму("Справочник.НоменклатураКонтрагентов.ФормаОбъекта", ПараметрыФормы,
			ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЦеныОстаткиТоваровМП

&НаКлиенте
Процедура ЦеныОстаткиТоваровМПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаСервере
Процедура ЦеныОстаткиТоваровМППриЗагрузкеПользовательскихНастроекНаСервере(Элемент, Настройки, ИспользуютсяСтандартныеНастройки)
	
	ДополнительныеСвойства = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	
	ПолеПорядок = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	Для Каждого ЭлементНастройки Из Настройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ПорядокКомпоновкиДанных") Тогда
			Для Каждого ЭлементПорядка Из ЭлементНастройки.Элементы Цикл
				Если ЭлементПорядка.Поле = ПолеПорядок И ЭлементПорядка.Использование Тогда
					Если ЭлементПорядка.ТипУпорядочивания <> ДополнительныеСвойства.НаправлениеСортировкиКД Тогда
						ДополнительныеСвойства.НаправлениеСортировкиКД = ЭлементПорядка.ТипУпорядочивания;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЦеныОстаткиТоваровМППриСохраненииПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	ДополнительныеСвойства = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	
	ТекущаяСтрока = Элементы.ЦеныОстаткиТоваровМП.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		
		Запись = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, ТекущаяСтрока);
		
		Запись.Прочитать();
		
		ЧастиКлюча = Новый Массив;
		ЧастиКлюча.Добавить(Запись.НаименованиеОбъектаМаркетплейса);
		ЧастиКлюча.Добавить(Запись.ИдентификаторОбъектаМаркетплейса);
		
		ТекущийКлюч = СтрСоединить(ЧастиКлюча, "_");
	КонецЕсли;
	
	ПолеПорядок = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	Для Каждого ЭлементНастройки Из Настройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ПорядокКомпоновкиДанных") Тогда
			Для Каждого ЭлементПорядка Из ЭлементНастройки.Элементы Цикл
				Если ЭлементПорядка.Поле = ПолеПорядок И ЭлементПорядка.Использование Тогда
					Если ЭлементПорядка.ТипУпорядочивания <> ДополнительныеСвойства.НаправлениеСортировкиКД Тогда
						ДополнительныеСвойства.НаправлениеСортировкиКД = ЭлементПорядка.ТипУпорядочивания;
						ДополнительныеСвойства.АдресаСтраницКэша.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
						
						// Сбросим кэш
						СтраницыКУдалению = Новый Массив;
						Для Каждого КэшСтраницы Из ДополнительныеСвойства.АдресаСтраницКэша Цикл
							Если КэшСтраницы.Значение.Первая Или КэшСтраницы.Значение.Последняя Тогда
								Продолжить;
							ИначеЕсли ТекущийКлюч >= КэшСтраницы.Значение.ПервыйКлюч И ТекущийКлюч <= КэшСтраницы.Значение.ПоследнийКлюч Тогда
								Продолжить;
							КонецЕсли;
							
							СтраницыКУдалению.Добавить(КэшСтраницы);
						КонецЦикла;
						
						Для Каждого УдаляемаяСтраница Из СтраницыКУдалению Цикл
							ДополнительныеСвойства.АдресаСтраницКэша.Удалить(УдаляемаяСтраница);
						КонецЦикла;
						
						ДополнительныеСвойства.ВерсияКэша = Новый УникальныйИдентификатор;
						ПоместитьВоВременноеХранилище(Новый Соответствие, ДополнительныеСвойства.АдресДействующихЗапросов);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЦеныОстаткиТоваровМППриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	Если Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.ВыгрузкаТоварногоКаталога.ПриПолученииДанныхНаСервере");
	
	ИнтеграцияСМаркетплейсамиСИ.ЦеныОстаткиТоваровМПЗаполнитьПоДаннымСИ(Настройки, Строки);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Строки.Количество() / 100);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбщиеНастройки(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НастройкиИнтеграции";
	ПараметрыФормы.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходПереходНастройкаНоменклатурыПартнеров(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаНоменклатурыПартнеров;
	РаскраситьКнопкиПанелиРазделов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходВыгрузкаОстатков(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыгрузкаОстатков;
	РаскраситьКнопкиПанелиРазделов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходВыгрузкаЦен(Команда)
	
	ОчиститьСообщения();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыгрузкаЦен;
	РаскраситьКнопкиПанелиРазделов();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПартнеровПолучитьИзСервиса(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	Партнер = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись).Партнер;
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Укажите партнера в настройках учетной записи.';
				|en = 'Specify the partner in the account settings.'"),,, "ОбщиеНастройки");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗапроситьДанныеСМаркетплейса", Истина);
	ПараметрыМетода.Вставить("ТолькоНесопоставленные", Истина);
	
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода ,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПартнеровПолучитьИзСервисаВсе(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	Партнер = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись).Партнер;
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Укажите партнера в настройках учетной записи.';
				|en = 'Specify the partner in the account settings.'"),,, "ОбщиеНастройки");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗапроситьДанныеСМаркетплейса", Истина);
	ПараметрыМетода.Вставить("ТолькоНесопоставленные", Ложь);
	
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода ,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПартнеровСопоставитьДанные(Команда)
	
	ОчиститьСообщения();
	
	Ссылки = Новый Массив;
	Для Каждого СтрокаСписка Из Элементы.НоменклатураПартнеров.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.НоменклатураПартнеров.ДанныеСтроки(СтрокаСписка);
		Ссылки.Добавить(ДанныеСтроки.НоменклатураПартнера);
	КонецЦикла;
	
	НоменклатураДляСопоставления = НоменклатураКонтрагентовПоСсылкам(Ссылки);
	
	Если Не ЗначениеЗаполнено(НоменклатураДляСопоставления) Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Сопоставление номенклатуры не требуется.';
										|en = 'Products mapping is not required.'"));
		Возврат;
	КонецЕсли;
	
	// Недопущение исключения чисел длиной менее 3-х символов из алгоритма сопоставления
	Для Каждого ОписаниеНК Из НоменклатураДляСопоставления Цикл
		Если СтрДлина(ОписаниеНК.Характеристика) <= 3 И ЭтоЧисло(ОписаниеНК.Характеристика) Тогда
			ОписаниеНК.Характеристика = "р. " + ОписаниеНК.Характеристика;
		КонецЕсли;
	КонецЦикла;
	
	Настройки = Новый Структура;
	Настройки.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Настройки.Вставить("ВладелецФормы", ЭтотОбъект);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьСопоставлениеНоменклатуры", ЭтотОбъект, Ссылки);
	
	СопоставлениеНоменклатурыКонтрагентовКлиент.ОткрытьСопоставлениеНоменклатуры(НоменклатураДляСопоставления, Настройки,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПартнеровВыгрузитьВСервис(Команда)
	
	ВыгрузитьСопоставлениеНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПартнеровВыгрузитьВСервисВыделенные(Команда)
	
	ВыгрузитьСопоставлениеНоменклатуры(Элементы.НоменклатураПартнеров.ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиТоваровМПРассчитать(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПодготовитьОстаткиТоваров());
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьПодготовленныеОстаткиТоваров());
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеныТоваровМПРассчитать(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиТоваровМППолныйПересчет(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПодготовитьОстаткиТоваров());
	ПараметрыОперации.Очередь.Добавить(Новый Структура("ВсеТовары", Истина),
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров());
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПодсказку(Команда)
	
	ОчиститьСообщения();
	ИзменитьВидимостьПодсказки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПодсказку(Команда)
	
	ОчиститьСообщения();
	ИзменитьВидимостьПодсказки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоказатьВсе(Команда)
	
	ОчиститьСообщения();
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	УстановитьОтборЦенПоДаннымСервисаИнтеграции(МаркерыСтатусов.Все, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруГотовы(Команда)
	
	ОчиститьСообщения();
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	УстановитьОтборЦенПоДаннымСервисаИнтеграции(МаркерыСтатусов.БезОшибок, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПоМаркеруОшибка(Команда)
	
	ОчиститьСообщения();
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	УстановитьОтборЦенПоДаннымСервисаИнтеграции(МаркерыСтатусов.Ошибка, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьНаКарантине(Команда)
	
	ОчиститьСообщения();
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	УстановитьОтборЦенПоДаннымСервисаИнтеграции(МаркерыСтатусов.НаКарантине, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобратьПроизвольно(Команда)
	
	Если УчетнаяЗапись.Пустая() Тогда
		Возврат;
	КонецЕсли;

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура("АдресПараметров", ПолучитьНастройкиДляОтбораДС("ЦеныОстаткиТоваровМП"));
	
	ДополнительныеПараметры = Новый Структура("ИмяДинамическогоСписка", "ЦеныОстаткиТоваровМП");
	ПриЗавершенииОтбора = Новый ОписаниеОповещения("ПриЗавершенииНастройкиОтбораДС", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НастройкаПроизвольныхОтборов", ПараметрыФормы, ЭтотОбъект,,,,ПриЗавершенииОтбора, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЦеныТоваровМППолныйПересчет(Команда)

	ОчиститьСообщения();
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Всех позиций';
												|en = 'Total items'"));
	Кнопки.Добавить("Выделенные", НСтр("ru = 'Выделенных позиций';
										|en = 'Selected items'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Перейти к полному пересчету цен...';
									|en = 'Go to the full price recalculation...'"), Кнопки);
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураМП = Новый Массив;
	
	Если Ответ = "Выделенные" Тогда
		ЗаполнитьНоменклатураМП(НоменклатураМП, Элементы.ЦеныОстаткиТоваровМП.ВыделенныеСтроки);
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("ВсяНоменклатураМП", Истина);
	ПараметрыЗапроса.Вставить("НоменклатураМП", НоменклатураМП);
	
	ПараметрыОперации.Очередь.Добавить(ПараметрыЗапроса, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОтредактироватьЦены(Команда)
	
	ОчиститьСообщения();
	
	Кнопки = Новый СписокЗначений;
	Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Всех позиций';
												|en = 'Total items'"));
	Кнопки.Добавить("Выделенные", НСтр("ru = 'Выделенных позиций';
										|en = 'Selected items'"));
	Кнопки.Добавить(КодВозвратаДиалога.Отмена);
	
	Ответ = Ждать ВопросАсинх(НСтр("ru = 'Перейти к редактированию цен...';
									|en = 'Go to price editing...'"), Кнопки);
	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если Ответ = "Выделенные" Тогда
		ПараметрыПрайсЛиста = СформироватьПараметрыРедактированияПрайсЛиста(УчетнаяЗапись, Элементы.ЦеныОстаткиТоваровМП.ВыделенныеСтроки);
	Иначе
		ПараметрыПрайсЛиста = СформироватьПараметрыРедактированияПрайсЛиста(УчетнаяЗапись, Неопределено);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДанныеРедактированияПрайсЛиста", Новый Структура);
	ПараметрыФормы.ДанныеРедактированияПрайсЛиста.Вставить("СписокВидовЦен", ПараметрыПрайсЛиста.ВидыЦен);
	ПараметрыФормы.ДанныеРедактированияПрайсЛиста.Вставить("СписокНоменклатуры", ПараметрыПрайсЛиста.Номенклатура);
	ПараметрыФормы.ДанныеРедактированияПрайсЛиста.Вставить("ИмяМодуля", "ИнтеграцияСМаркетплейсамиСервер");
	
	ОткрытьФорму("Обработка.ПрайсЛист.Форма", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОпераций(ПараметрыОперации)
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("ИмяФормы", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ИмяФормы);
	ПараметрыОбъекта.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыОбъекта.Вставить("Кэш", Новый Структура("УчетныеЗаписи", Кэш.УчетныеЗаписи));
	ПараметрыОбъекта.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыОбъекта.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	ДополнитьНаСервере = Ложь;
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл	
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ДополнитьНаСервере = Истина;
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
			ДополнитьНаСервере = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ИнтернетПоддержкаПодключена = Ложь;
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;
	
	Если ДополнитьНаСервере Тогда
		ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации, ПараметрыОбъекта);
	Иначе
		ВыполнитьДлительныеОперацииБезКонтекста(ИнтернетПоддержкаПодключена, ПараметрыОперации, ПараметрыОбъекта);
	КонецЕсли;
	
	Если ИнтернетПоддержкаПодключена Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, ПараметрыОперации, ПараметрыОбъекта);
	Иначе
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДлительныхОперацийПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОперацийПродолжение(Результат, ПараметрыОперации, ПараметрыОбъекта) Экспорт
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации, ПараметрыОбъекта);
		ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации)
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	ИмяМетодаПолучитьУчетныеЗаписи = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи();
	ВерсияКэша = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОжидатьЗавершение = 0;
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыОперации.Очередь.Добавить( , ИмяМетодаПолучитьУчетныеЗаписи);
	
	// Методы, зависимые от МетодПолучитьУчетныеЗаписи
	ПараметрыМетода = Новый Структура("ЗависитОт, ЗависимыеПоля", ИмяМетодаПолучитьУчетныеЗаписи, "УчетнаяЗапись");
	ПараметрыМетода.Вставить("ВерсияКэша", ВерсияКэша);
	ПараметрыМетода.Вставить("НаправлениеСортировки", "asc");
	
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса());
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды());
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории());
	
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	
	ПараметрыМетода = Новый Структура("ЗависитОт, ЗависимыеПоля", ИмяМетодаПолучитьУчетныеЗаписи, "УчетнаяЗапись");
	ПараметрыМетода.Вставить("ВерсияКэша", ВерсияКэша);
	ПараметрыМетода.Вставить("НаправлениеСортировки", "desc");
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("ИмяФормы", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ИмяФормы);
	ПараметрыОбъекта.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыОбъекта.Вставить("Элементы", Элементы);
	ПараметрыОбъекта.Вставить("Кэш", Кэш);
	ПараметрыОбъекта.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	ПараметрыОбъекта.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ПараметрыОбъекта.Вставить("ЦеныОстаткиТоваровМП_КомпоновщикНастроек", ЦеныОстаткиТоваровМП.КомпоновщикНастроек);
	ПараметрыОбъекта.Вставить("ЦеныОстаткиТоваровМП_Параметры", ЦеныОстаткиТоваровМП.Параметры);
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации, ПараметрыОбъекта);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьУчетныеЗаписи(Действие)
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	Если Действие = "Выбор" Тогда
		
		ПараметрыМетода = Новый Структура("УчетнаяЗапись, ОбновлениеСостояния", УчетнаяЗапись, Истина);
		ПараметрыОперации.Очередь.Добавить(ПараметрыМетода, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
		
		ПараметрыОперации.Очередь.Добавить(, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса());
		ПараметрыОперации.Очередь.Добавить(, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды());
		ПараметрыОперации.Очередь.Добавить(, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории());
		
		ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша = Новый УникальныйИдентификатор;
		ПоместитьВоВременноеХранилище(Новый Соответствие, ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
		
		ПараметрыМетода = Новый Структура;
		ПараметрыМетода.Вставить("ВерсияКэша", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша);
		ПараметрыМетода.Вставить("НаправлениеСортировки", "asc");
		ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
		ПараметрыМетода = Новый Структура;
		ПараметрыМетода.Вставить("ВерсияКэша", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша);
		ПараметрыМетода.Вставить("НаправлениеСортировки", "desc");
		ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
		
	Иначе
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	КонецЕсли;
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации, ПараметрыОбъекта)
	
	ИзменитьВерсиюКэша = Истина;
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			
			ИнтеграцияСМаркетплейсамиСИ.ЗаполнитьЭлементыСостояния(ЭтотОбъект);
			
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
			
			Если ИзменитьВерсиюКэша Тогда
				ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша = Новый УникальныйИдентификатор;
				ИзменитьВерсиюКэша = Ложь;
			КонецЕсли;
			
			Операция.Значение.Вставить("ВерсияКэша", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша);
			ПоместитьВоВременноеХранилище(Новый Соответствие, ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
			
			// Сбросим кэшированные страницы
			ДополнительныеСвойстваСКД = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
			Для Каждого ОписаниеСтраницы Из ДополнительныеСвойстваСКД.АдресаСтраницКэша Цикл
				УдалитьИзВременногоХранилища(ОписаниеСтраницы.Значение.АдресСтраницы);
			КонецЦикла;
			ДополнительныеСвойстваСКД.АдресаСтраницКэша.Очистить();
			
			// Сбросим отбор по номенклатуре
			ПараметрНоменклатураВключение = ЦеныОстаткиТоваровМП.Параметры.Элементы.Найти("НоменклатураВключение");
			Если ПараметрНоменклатураВключение <> Неопределено И ПараметрНоменклатураВключение.Значение <> Неопределено Тогда
				ПараметрНоменклатураВключение.Значение.Очистить();
			КонецЕсли;
			
			ПараметрыОбъекта.Вставить("НастройкиСКД", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.ПолучитьНастройки());
			ПараметрыОбъекта.Вставить("ЦеныОстаткиТоваровМП_КомпоновщикНастроек", ЦеныОстаткиТоваровМП.КомпоновщикНастроек);
			ПараметрыОбъекта.Вставить("ЦеныОстаткиТоваровМП_Параметры", ЦеныОстаткиТоваровМП.Параметры);
			
		КонецЕсли;
	КонецЦикла;
	
	ВыполнитьДлительныеОперацииБезКонтекста(ИнтернетПоддержкаПодключена, ПараметрыОперации, ПараметрыОбъекта);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьДлительныеОперацииБезКонтекста(ИнтернетПоддержкаПодключена, ПараметрыОперации, ПараметрыОбъекта)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ПараметрыОбъекта, ПараметрыОперации,
		ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ, ПараметрыОбъекта);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ПараметрыОбъекта, ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ, ПараметрыОбъекта)

	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ПараметрыЗапросаПолучитьУчетныеЗаписи(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи() Тогда
			ПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьНоменклатуруМаркетплейса(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса() Тогда
			//@skip-check query-in-loop - в массиве только одна такая операция
			ПараметрыЗапросаОбновитьНоменклатуруМаркетплейса(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПодготовитьОстаткиТоваров() Тогда
			ПараметрыЗапросаПодготовитьОстаткиТоваров(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров() Тогда
			ПараметрыЗапросаВыгрузитьОстаткиТоваров(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьПодготовленныеОстаткиТоваров() Тогда
			ПараметрыЗапросаВыгрузитьПодготовленныеОстаткиТоваров(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены() Тогда
			ПараметрыЗапросаВыгрузитьЦены(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли  Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды() Тогда
			ПараметрыЗапросаПолучитьБренды(ПараметрыОбъекта, Операция.Значение);
		ИначеЕсли  Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории() Тогда
			ПараметрыЗапросаПолучитьКатегории(ПараметрыОбъекта, Операция.Значение);
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьУчетныеЗаписи(ПараметрыОбъекта, Значение)
	
	ЗначениеДо = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьУчетныеЗаписи();
	Если ТипЗнч(ЗначениеДо) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Значение, ЗначениеДо);
	Иначе
		Значение.УчетнаяЗапись = ЗначениеДо;
	КонецЕсли;
	Значение.ВсеУчетныеЗаписи = Не ЗначениеЗаполнено(Значение.УчетнаяЗапись);
	Значение.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи();
	
	ОписаниеУчетнойЗаписи = ПараметрыОбъекта.Кэш.УчетныеЗаписи.Получить(ПараметрыОбъекта.УчетнаяЗапись);
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ОписаниеУчетнойЗаписи);
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьНоменклатуруМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьНоменклатуруМаркетплейса();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	ПараметрыЗапроса.Партнер = ПараметрыОбъекта.Кэш.УчетныеЗаписи.Получить(ПараметрыОбъекта.УчетнаяЗапись).Партнер;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаОбновитьНоменклатуруМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаОбновитьНоменклатуруМаркетплейса();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		
		НоменклатураПартнера = Новый Массив;
		Для Каждого Запись Из ВходящиеПараметры.НоменклатураПартнера Цикл
			НоменклатураПартнера.Добавить(Запись.Объект1С);
		КонецЦикла;
		
		Если НоменклатураПартнера.Количество() > 0 Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ
			|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторНоменклатурыСервиса,
			|	СоответствияОбъектовМаркетплейсов.Объект1С КАК НоменклатураКонтрагента
			|ИЗ
			|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
			|		ПО НоменклатураКонтрагентов.Идентификатор = СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса
			|		И НоменклатураКонтрагентов.Ссылка = СоответствияОбъектовМаркетплейсов.Объект1С
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1С
			|		ПО НоменклатураКонтрагентов.Номенклатура = Номенклатура1С.Ссылка
			|ГДЕ
			|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
			|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
			|	И СоответствияОбъектовМаркетплейсов.Объект1С В (&НоменклатураПартнера)";
			
			Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", ПараметрыОбъекта.УчетнаяЗапись);
			Запрос.УстановитьПараметр("НоменклатураПартнера", НоменклатураПартнера);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса();
				ЗаполнитьЗначенияСвойств(Карточка, Выборка);
				ПараметрыЗапроса.НоменклатураМаркетплейса.Добавить(Карточка);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗапроса.НоменклатураМаркетплейса) Тогда
		Узел = ПланыОбмена.МаркетплейсыСИ.НайтиСоздатьУзел(ПараметрыОбъекта.УчетнаяЗапись);
	
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	НоменклатураКонтрагентов.Идентификатор КАК ИдентификаторНоменклатурыСервиса,
		|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураКонтрагента
		|ИЗ
		|	Справочник.НоменклатураКонтрагентов.Изменения КАК ДанныеУзла
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО НоменклатураКонтрагентов.Ссылка = ДанныеУзла.Ссылка
		|ГДЕ
		|	ДанныеУзла.Узел = &Узел
		|	И НоменклатураКонтрагентов.Ссылка ЕСТЬ НЕ NULL";
		
		Запрос.УстановитьПараметр("Узел", Узел);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса();
			ЗаполнитьЗначенияСвойств(Карточка, Выборка);
			ПараметрыЗапроса.НоменклатураМаркетплейса.Добавить(Карточка);
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса)

	ВходящиеПараметры = ПараметрыЗапроса;

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса();

	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;

	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса)
	ИнтеграцияСМаркетплейсамиСИ.ПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПодготовитьОстаткиТоваров(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса();
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаВыгрузитьОстаткиТоваров(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаВыгрузитьОстаткиТоваров();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаВыгрузитьПодготовленныеОстаткиТоваров(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаВыгрузитьПодготовленныеОстаткиТоваров();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаВыгрузитьЦены(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаВыгрузитьЦены();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьБренды(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьБренды();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПараметрыЗапросаПолучитьКатегории(ПараметрыОбъекта, ПараметрыЗапроса)
	
	ВходящиеПараметры = ПараметрыЗапроса;
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьКатегории();
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата, Кэш);
	ИначеЕсли ДополнительныеПараметры.ОбработкаРезультатаНаСервереБезКонтекста Тогда
		
		Источники = Новый Структура;
		Источники.Вставить("ПоказателиНоменклатурыМП", ИсточникПоказателиНоменклатурыМП(ЭтотОбъект));
		Источники.Вставить("Бренды", ИсточникБрендыМП(ЭтотОбъект));
		Источники.Вставить("Категории", ИсточникКатегорииМП(ЭтотОбъект));
		
		// Обработка результата на сервере
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОперацийБезКонтекста(Результат.АдресРезультата, Источники);
		
		// Постобработка на клиенте
		ЗаполнитьЗначенияСвойств(ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства, Источники.ПоказателиНоменклатурыМП.ДополнительныеСвойства, "АдресаСтраницКэша");
		
		Если Источники.ПоказателиНоменклатурыМП.ДополнительныеСвойства.ОтобратьПоНоменклатуреСИ Тогда
			
			Если Источники.ПоказателиНоменклатурыМП.НоменклатураВключение.Количество() > 0 Тогда
				ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("НоменклатураВключение", Источники.ПоказателиНоменклатурыМП.НоменклатураВключение);
			Иначе
				ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("НоменклатураВключение", Новый СписокЗначений);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса() Тогда
			Если РезультатОперации Тогда
				Элементы.НоменклатураПартнеров.Обновить();
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИсточникПоказателиНоменклатурыМП(ДанныеФормы)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ДополнительныеСвойства", ДанныеФормы.ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства);
	Результат.Вставить("НоменклатураВключение", ДанныеФормы.ЦеныОстаткиТоваровМП.Параметры.Элементы.Найти("НоменклатураВключение").Значение);
	
	Порядок = ?(ДанныеФормы.ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.НаправлениеСортировкиКД
				= НаправлениеСортировкиКомпоновкиДанных.Возр, НаправлениеСортировки.Возр, НаправлениеСортировки.Убыв);
	
	Результат.Вставить("НаправлениеСортировки", Порядок);
	
	// Типизируем параметр НоменклатураВключение
	Параметр = ДанныеФормы.ЦеныОстаткиТоваровМП.Параметры.Элементы.Найти("НоменклатураВключение");
	Если Параметр <> Неопределено Тогда
		Если Параметр.Значение = Неопределено Тогда
			Параметр.Значение = Новый СписокЗначений;
		ИначеЕсли ТипЗнч(Параметр.Значение) = Тип("Строка") Тогда
			ЗначениеПараметра = Параметр.Значение;
			Параметр.Значение = Новый СписокЗначений;	// СписокЗначений Из Строка
			Если Не ПустаяСтрока(ЗначениеПараметра) Тогда
				Параметр.Значение.Добавить(ЗначениеПараметра);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИсточникБрендыМП(ДанныеФормы)
	
	Результат = Новый Структура;
	Результат.Вставить("ДополнительныеСвойства", ДанныеФормы.ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства);

	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИсточникКатегорииМП(ДанныеФормы)
	
	Результат = Новый Структура;
	Результат.Вставить("ДополнительныеСвойства", ДанныеФормы.ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ИсточникОстаткиТоваровМП()
	
	Результат = Новый Структура;
	Результат.Вставить("ОстаткиТоваровМП", ОстаткиТоваровМП);
	
	Возврат Результат;
	
КонецФункции

// Обрабатывает результат менеджера длительных операций на сервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
//  Кэш - Структура - Кэш
// 
// Возвращаемое значение:
//  Структура - Результат обработки:
//   * Очередь - СписокЗначений Из Произвольный - Результаты обработки методов
&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(Знач АдресРезультата, Кэш)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);

	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			РезультатОбработки = Истина;
			Кэш = ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки);
			УстановитьЗаголовкиТаблицыЦенТоваров(УчетнаяЗапись, Элементы.ЦеныОстаткиТоваровМПЦена1С, Элементы.ЦеныОстаткиТоваровМПЦенаМП);
			ИнтеграцияСМаркетплейсамиСИ.ЗаполнитьЭлементыСостояния(ЭтотОбъект);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьНоменклатуруМаркетплейса(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьОстаткиТоваровМаркетплейса(РезультатОперации, ИсточникОстаткиТоваровМП(), РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатВыгрузитьОстаткиТоваров(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьПодготовленныеОстаткиТоваров() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатВыгрузитьПодготовленныеОстаткиТоваров(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса() Тогда
			РезультатОбработки = Истина;
			//@skip-check query-in-loop - в массиве только одна такая операция
			ОбработатьРезультатОбновитьНоменклатуруМаркетплейса(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатВыгрузитьЦены(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьПоказателиНоменклатурыМаркетплейса(РезультатОперации, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьБренды(РезультатОперации, ИсточникБрендыМП(ЭтотОбъект), РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьКатегории(РезультатОперации, ИсточникКатегорииМП(ЭтотОбъект), РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		Иначе
			Операция.Значение = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

// Обрабатывает результат менеджера длительных операций на сервере без контекста.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
//  Источники - Структура - данные для обработки:
//   * ПоказателиНоменклатурыМП - Структура - источник показателей номенклатуры маркетплейса
//   * БрендыМП - Структура - источник брендов маркетплейса
//   * КатегорииМП - Структура - источник категорий маркетплейса
// 
// Возвращаемое значение:
//  Структура - Результат обработки:
//   * Очередь - СписокЗначений Из Произвольный - Результаты обработки методов
&НаСервереБезКонтекста
Функция ОбработатьРезультатМенеджераДлительныхОперацийБезКонтекста(Знач АдресРезультата, Источники)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);

	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьПоказателиНоменклатурыМаркетплейсаБезКонтекста(РезультатОперации, Источники.ПоказателиНоменклатурыМП, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьБренды(РезультатОперации, Источники.Бренды, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории() Тогда
			РезультатОбработки = Истина;
			ОбработатьРезультатПолучитьКатегории(РезультатОперации, Источники.Категории, РезультатОбработки);
			Операция.Значение = РезультатОбработки;
		Иначе
			Операция.Значение = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	УстановитьВидимостьДоступность();
	
	Если ДополнительныеПараметры.Свойство("ИмяКомандыОтбораПоСтатусу") Тогда
		ОбновитьКнопкуОтбораПоСтатусу(ДополнительныеПараметры.ИмяКомандыОтбораПоСтатусу);
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработать результат получить учетные записи.
// 
// Параметры:
//  РезультатОперации - Структура - Результат операции
//  РезультатОбработки - Булево - Результат обработки
// 
// Возвращаемое значение:
//  Структура - Кэш
&НаСервере
Функция ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации.Ответ.ОбновлениеСостояния Тогда
		Если РезультатОперации.КодСостояния = 200 И РезультатОперации.Ответ.УчетныеЗаписи.Количество() = 1 Тогда
			ОписаниеУчетнойЗаписи = РезультатОперации.Ответ.УчетныеЗаписи[0];
			Кэш.УчетныеЗаписи.Вставить(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи);
		Иначе
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		Возврат Кэш;
	КонецЕсли;
	
	Элементы.УчетнаяЗапись.СписокВыбора.Очистить();
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Возврат Кэш;
	КонецЕсли;
	
	Кэш.УчетныеЗаписи.Очистить();
	
	Для Каждого ОписаниеУчетнойЗаписи Из РезультатОперации.Ответ.УчетныеЗаписи Цикл
		Элементы.УчетнаяЗапись.СписокВыбора.Добавить(ОписаниеУчетнойЗаписи.УчетнаяЗапись,
			ОписаниеУчетнойЗаписи.Представление);
		Кэш.УчетныеЗаписи.Вставить(ОписаниеУчетнойЗаписи.УчетнаяЗапись, ОписаниеУчетнойЗаписи);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
	ИначеЕсли Кэш.УчетныеЗаписи.Количество() <> 1 Тогда
		ОписаниеУчетнойЗаписи = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОписаниеУчетнойЗаписи) Тогда
		
		УчетнаяЗапись = ОписаниеУчетнойЗаписи.УчетнаяЗапись;
		НоменклатураПартнеров.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", УчетнаяЗапись);
		ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		
		ОпределитьСостоянияРегламентныхЗаданий(ВидМаркетплейса, ОписаниеУчетнойЗаписи, ОстаткиТоваровМПРЗВключено);
		
		ЦеныТоваровМПРЗВключено = ОписаниеУчетнойЗаписи.РазрешенаВыгрузкаЦенНаМаркетплейс;
		
	Иначе
		
		НоменклатураПартнеров.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", Неопределено);
		ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", Неопределено);
		
		ОстаткиТоваровМПРЗВключено = Ложь;
		ЦеныТоваровМПРЗВключено = Ложь;
		
	КонецЕсли;
	
	Возврат Кэш;
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьЗаголовкиТаблицыЦенТоваров(УчетнаяЗапись, ЦеныОстаткиТоваровМПЦена1С, ЦеныОстаткиТоваровМПЦенаМП)
	
	Валюта = "";
	
	Если УчетнаяЗапись <> Неопределено Тогда
		Валюта = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВалютаУчета.Наименование");
	КонецЕсли;
	
	Цена1С = Новый Массив; // Массив Из Строка
	ЦенаМП = Новый Массив; // Массив Из Строка
	
	ЗаголовокЦена1С = СтрРазделить(ЦеныОстаткиТоваровМПЦена1С.Заголовок, ",", Ложь);
	Если ЗаголовокЦена1С.Количество() > 0 Тогда
		Цена1С.Добавить(ЗаголовокЦена1С.Получить(0));
	КонецЕсли;
	ЗаголовокЦенаМП = СтрРазделить(ЦеныОстаткиТоваровМПЦенаМП.Заголовок, ",", Ложь);
	Если ЗаголовокЦенаМП.Количество() > 0 Тогда
		ЦенаМП.Добавить(ЗаголовокЦенаМП.Получить(0));
	КонецЕсли;
	
	Если ТипЗнч(Валюта) = Тип("Строка") И ЗначениеЗаполнено(Валюта) Тогда
		Цена1С.Добавить(Валюта);
		ЦенаМП.Добавить(Валюта);
	КонецЕсли;
	
	ЦеныОстаткиТоваровМПЦена1С.Заголовок = СтрСоединить(Цена1С, ", ");
	ЦеныОстаткиТоваровМПЦенаМП.Заголовок = СтрСоединить(ЦенаМП, ", ");
	
КонецПроцедуры

// Обработать результат получить номенклатуру маркетплейса.
// 
// Параметры:
//  РезультатОперации - Структура - Результат операции
//  РезультатОбработки - Булево - Результат обработки
&НаСервере
Процедура ОбработатьРезультатПолучитьНоменклатуруМаркетплейса(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Результат = РезультатОперации.Ответ;
	
	МассивСообщений = Новый Массив;
	МассивСообщений.Добавить(СтрШаблон(НСтр("ru = 'Импортировано: %1 из %2';
											|en = 'Imported: %1 out of %2'"), Результат.Добавлено, Результат.Получено));
	Если Результат.Получено <> 0 Тогда
		МассивСообщений.Добавить(СтрШаблон(НСтр("ru = 'Обновлено: %1 из %2';
												|en = 'Updated: %1 out of %2'"), Результат.Обновлено, Результат.Получено));
	КонецЕсли;
	ОбщегоНазначения.СообщитьПользователю(СтрСоединить(МассивСообщений, ", "));
	
	Возврат;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатПолучитьОстаткиТоваровМаркетплейса(РезультатОперации, Источники, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Если РезультатОперации.Ошибки.Количество() > 0 Тогда
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Источники.ОстаткиТоваровМП.Очистить();
	
	Для Каждого ОписаниеОстатка Из РезультатОперации.ОстаткиТоваров Цикл
		НоваяСтрока = Источники.ОстаткиТоваровМП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеОстатка);
		НоваяСтрока.СкладМП = ОписаниеОстатка.НаименованиеСклада;
		НоваяСтрока.КоличествоМП = ОписаниеОстатка.КоличествоМаркетплейса;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатВыгрузитьПодготовленныеОстаткиТоваров(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатВыгрузитьОстаткиТоваров(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатОбновитьНоменклатуруМаркетплейса(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	УзелОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиСоздатьУзел(РезультатОперации.УчетнаяЗапись);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеНК.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	СоответствиеНК.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
	|	СоответствиеНК.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
	|	СоответствиеНК.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
	|	СоответствиеНК.Объект1С КАК Объект1С
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствиеНК
	|ГДЕ
	|	СоответствиеНК.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СоответствиеНК.ВидОбъектаМаркетплейса = &ВидОбъектаМаркетплейса
	|	И СоответствиеНК.Объект1С В(&Объект1С)";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", РезультатОперации.УчетнаяЗапись);
	Запрос.УстановитьПараметр("ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.Товар);
	Запрос.УстановитьПараметр("Объект1С", РезультатОперации.НоменклатураКонтрагентов);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		Для Каждого Измерение Из Метаданные.РегистрыСведений.СоответствияОбъектовМаркетплейсов.Измерения Цикл
			НаборЗаписей.Отбор[Измерение.Имя].Установить(Выборка[Измерение.Имя]);
		КонецЦикла;

		ПланыОбмена.ЗарегистрироватьИзменения(УзелОбмена, НаборЗаписей);
	КонецЦикла;
	
	Если РезультатОперации.НоменклатураКонтрагентов.Количество() > 0 Тогда
		ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, РезультатОперации.НоменклатураКонтрагентов);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатВыгрузитьЦены(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		РезультатОбработки = Ложь;
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатПолучитьПоказателиНоменклатурыМаркетплейса(РезультатОперации, РезультатОбработки)
	
	Настройки = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки;
	
	Источники = ИсточникПоказателиНоменклатурыМП(ЭтотОбъект);
	
	ОбработатьРезультатПолучитьПоказателиНоменклатурыМаркетплейсаБезКонтекста(РезультатОперации, Источники, РезультатОбработки);
	
	Если Не РезультатОбработки Тогда
		Возврат;
	КонецЕсли;
	
	Если Настройки.ДополнительныеСвойства.ОтобратьПоНоменклатуреСИ Тогда
		
		Если Источники.НоменклатураВключение.Количество() > 0 Тогда
		
			ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("НоменклатураВключение", Источники.НоменклатураВключение);
			
		Иначе
			ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("НоменклатураВключение", Новый СписокЗначений);
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатПолучитьПоказателиНоменклатурыМаркетплейсаБезКонтекста(РезультатОперации, Источники, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства = Источники.ДополнительныеСвойства;

	Если РезультатОперации.Свойство("Курсор") Тогда
		Если РезультатОперации.Курсор <> "" Тогда
			
			ДействующиеЗапросы = ПолучитьИзВременногоХранилища(ДополнительныеСвойства.АдресДействующихЗапросов);
			
			НеобходимоЗаписать = Ложь;
			Пока Истина Цикл
				РезультатПоиска = ДействующиеЗапросы.Получить(РезультатОперации.Курсор);
				Если РезультатПоиска <> Неопределено Тогда
					ДействующиеЗапросы.Удалить(РезультатОперации.Курсор);
					НеобходимоЗаписать = Истина;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если НеобходимоЗаписать Тогда
				ПоместитьВоВременноеХранилище(ДействующиеЗапросы, ДополнительныеСвойства.АдресДействующихЗапросов);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Если РезультатОперации.Ошибки.Количество() > 0 Тогда
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.ВерсияКэша <> РезультатОперации.ВерсияКэша Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	ПерваяСтраница = Неопределено;
	ПоследняяСтраница = Неопределено;
	// Удаление существующей страницы при обновлении
	Для Каждого ЭлементАдресаСтраницКэша Из ДополнительныеСвойства.АдресаСтраницКэша Цикл
		Если ЭлементАдресаСтраницКэша.Представление = РезультатОперации.СтраницаКэша.ПервыйКлюч Тогда
			ДополнительныеСвойства.АдресаСтраницКэша.Удалить(ЭлементАдресаСтраницКэша);
		Иначе
			Если ЭлементАдресаСтраницКэша.Значение.Первая Тогда
				ПерваяСтраница = ЭлементАдресаСтраницКэша;
			КонецЕсли;
			Если ЭлементАдресаСтраницКэша.Значение.Последняя Тогда
				ПоследняяСтраница = ЭлементАдресаСтраницКэша;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Пришла пустая страница - можно не продолжать
	Если РезультатОперации.Показатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Первая страница должна быть одна. Объединим
	Если ПерваяСтраница <> Неопределено И РезультатОперации.СтраницаКэша.Первая Тогда
		
		ДанныеКэша = ПолучитьИзВременногоХранилища(ПерваяСтраница.Значение.АдресСтраницы);
		Для Каждого ПоказателиНоменклатуры Из РезультатОперации.Показатели Цикл
			ДанныеКэша.Вставить(ПоказателиНоменклатуры.Ключ, ПоказателиНоменклатуры.Значение);
		КонецЦикла;
		ПоместитьВоВременноеХранилище(ДанныеКэша, ПерваяСтраница.Значение.АдресСтраницы);
		
		Возврат;
	КонецЕсли;
	
	// Последняя страница должна быть одна. Объединим
	Если ПоследняяСтраница <> Неопределено И РезультатОперации.СтраницаКэша.Последняя Тогда
		
		ДанныеКэша = ПолучитьИзВременногоХранилища(ПоследняяСтраница.Значение.АдресСтраницы);
		Для Каждого ПоказателиНоменклатуры Из РезультатОперации.Показатели Цикл
			ДанныеКэша.Вставить(ПоказателиНоменклатуры.Ключ, ПоказателиНоменклатуры.Значение);
		КонецЦикла;
		ПоместитьВоВременноеХранилище(ДанныеКэша, ПоследняяСтраница.Значение.АдресСтраницы);
		
		Возврат;
	КонецЕсли;
	
	РезультатОперации.СтраницаКэша.АдресСтраницы = ПоместитьВоВременноеХранилище(РезультатОперации.Показатели, ДополнительныеСвойства.УникальныйИдентификатор);
	ДополнительныеСвойства.АдресаСтраницКэша.Добавить(РезультатОперации.СтраницаКэша, РезультатОперации.СтраницаКэша.ПервыйКлюч);
	ДополнительныеСвойства.АдресаСтраницКэша.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	
	Если ДополнительныеСвойства.ОтобратьПоНоменклатуреСИ Тогда
		Если РезультатОперации.Показатели.Количество() > 0 Тогда
			
			Если Источники.НоменклатураВключение = Неопределено Тогда
				Источники.НоменклатураВключение = Новый СписокЗначений;
				
			ИначеЕсли ТипЗнч(Источники.НоменклатураВключение) <> Тип("СписокЗначений") Тогда
				Источники.НоменклатураВключение = Новый СписокЗначений;
			КонецЕсли;
			ЗначенияПараметра = Источники.НоменклатураВключение;	// СписокЗначений Из Строка
			
			Для Каждого ОписаниеПоказателя Из РезультатОперации.Показатели Цикл
				ЗначенияПараметра.Добавить(ОписаниеПоказателя.Значение.идНоменклатурыСИ);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатПолучитьБренды(РезультатОперации, Источники, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Если РезультатОперации.Ошибки.Количество() > 0 Тогда
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	БрендыМП = ПолучитьИзВременногоХранилища(Источники.ДополнительныеСвойства.БрендыМП);
	БрендыМП.Очистить();
	
	Для Каждого ОписаниеБренда Из РезультатОперации.Бренды Цикл
		БрендыМП.Добавить(ОписаниеБренда.Идентификатор, ОписаниеБренда.Наименование);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(БрендыМП, Источники.ДополнительныеСвойства.БрендыМП);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатПолучитьКатегории(РезультатОперации, Источники, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		Если РезультатОперации.Ошибки.Количество() > 0 Тогда
			Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
				ОбщегоНазначения.СообщитьПользователю(Ошибка);
			КонецЦикла;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	КатегорииМП = ПолучитьИзВременногоХранилища(Источники.ДополнительныеСвойства.КатегорииМП);
	КатегорииМП.Очистить();
	
	Для Каждого ОписаниеКатегории Из РезультатОперации.Категории Цикл
		КатегорииМП.Добавить(ОписаниеКатегории.Идентификатор, ОписаниеКатегории.Наименование);
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(КатегорииМП, Источники.ДополнительныеСвойства.КатегорииМП);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеНоменклатураПартнеров

&НаКлиенте
Процедура ОбработкаИзмененияНоменклатурыМаркетплейса(Результат, ДополнительныеПараметры) Экспорт
	Элементы.НоменклатураПартнеров.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьСопоставлениеНоменклатуры(РезультатСопоставления, ДополнительныеПараметры) Экспорт
	
	Если РезультатСопоставления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СопоставленнаяНоменклатураКонтрагентов = Новый Массив;
	НесопоставленнаяНоменклатураКонтрагентов = ДополнительныеПараметры;
	Для Каждого ЭлементСопоставления Из РезультатСопоставления Цикл
		НоменклатураКонтрагентаИзСопоставления = ЭлементСопоставления.НоменклатураКонтрагента.НоменклатураКонтрагента;
		Если ЗначениеЗаполнено(НоменклатураКонтрагентаИзСопоставления) Тогда
			СопоставленнаяНоменклатураКонтрагентов.Добавить(НоменклатураКонтрагентаИзСопоставления);
			
			ИндексСопоставленногоЭлемента = 
				НесопоставленнаяНоменклатураКонтрагентов.Найти(НоменклатураКонтрагентаИзСопоставления);
			Если ИндексСопоставленногоЭлемента <> Неопределено Тогда
				НесопоставленнаяНоменклатураКонтрагентов.Удалить(ИндексСопоставленногоЭлемента);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(НесопоставленнаяНоменклатураКонтрагентов) Тогда
		ТекстОшибки = УдалитьСопоставлениеНоменклатурыКонтрагентов(НесопоставленнаяНоменклатураКонтрагентов, УчетнаяЗапись);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
	КонецЕсли;
		
	Если СопоставленнаяНоменклатураКонтрагентов.Количество() > 0 Тогда
		Ошибки = ЗаполнитьКоличествоУпаковок(ВидМаркетплейса, СопоставленнаяНоменклатураКонтрагентов);
		Если Ошибки.Количество() > 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(СтрСоединить(Ошибки, Символы.ПС));
		КонецЕсли;
	КонецЕсли;
	
	Элементы.НоменклатураПартнеров.Обновить();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УдалитьСопоставлениеНоменклатурыКонтрагентов(СписокНоменклатуры, УчетнаяЗапись)

	Возврат ИнтеграцияСМаркетплейсамиСИ.УдалитьСопоставлениеНоменклатурыКонтрагентов(СписокНоменклатуры, 
		УчетнаяЗапись);

КонецФункции

&НаКлиенте
Процедура ВыгрузитьСопоставлениеНоменклатуры(НоменклатураКонтрагентов = Неопределено)

	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	Если НоменклатураКонтрагентов <> Неопределено Тогда
		ПараметрыМетода = Новый Структура("НоменклатураПартнера", НоменклатураКонтрагентов);
	Иначе
		ПараметрыМетода = Неопределено;
	КонецЕсли;
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса());
	
	// Методы, зависимые от МетодОбновитьНоменклатуруМаркетплейса 
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("ЗависитОт", ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса());
	ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша = Новый УникальныйИдентификатор;
	ПоместитьВоВременноеХранилище(Новый Соответствие, ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
	ПараметрыМетода.Вставить("ВерсияКэша", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша);
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

// Заполнить количество упаковок.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  НоменклатураКонтрагентов - Массив из СправочникСсылка.НоменклатураКонтрагентов - Номенклатура контрагентов
// 
// Возвращаемое значение:
//  Массив из Строка - Ошибки записи
&НаСервереБезКонтекста
Функция ЗаполнитьКоличествоУпаковок(Знач ВидМаркетплейса, Знач НоменклатураКонтрагентов)
	
	МассивОшибок = Новый Массив; // Массив из Строка
	
	Для Каждого НоменклатураСсылка Из НоменклатураКонтрагентов Цикл
		
		Если НоменклатураСсылка.КоличествоБазовойЕдиницыИзмерения <> 0
			И НоменклатураСсылка.КоличествоУпаковок <> 0
			И НоменклатураСсылка.НаименованиеБазовойЕдиницыИзмерения = НоменклатураСсылка.Упаковка.ПолноеНаименование() Тогда
			Продолжить;
		КонецЕсли;
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.НоменклатураКонтрагентов");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", НоменклатураСсылка);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Номенклатура = НоменклатураСсылка.ПолучитьОбъект(); // СправочникОбъект
			
			Номенклатура.НаименованиеБазовойЕдиницыИзмерения = Номенклатура.Упаковка;
			Если Номенклатура.КоличествоБазовойЕдиницыИзмерения = 0 Тогда
				Номенклатура.КоличествоБазовойЕдиницыИзмерения = 1;
			КонецЕсли;
			
			Номенклатура.НаименованиеУпаковки = Номенклатура.Упаковка;
			Если Номенклатура.КоличествоУпаковок = 0 Тогда
				Номенклатура.КоличествоУпаковок = 1;
			КонецЕсли;
			
			Номенклатура.Записать();
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				ИнтеграцияСМаркетплейсамиСИ.СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			МассивОшибок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при записи ""%1"". Возможно, объект уже редактируется.';
					|en = 'An error occurred when saving ""%1"". The object might be locked for editing.'"), НоменклатураСсылка));
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат МассивОшибок;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьКнопкуОтбораПоСтатусу(ИмяКоманды, Информация = "")
	
	Если ПустаяСтрока(Информация) Тогда
		ОтборыЗаголовок = Элементы[ИмяКоманды].Заголовок;
	Иначе
		ОтборыЗаголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", ИмяКоманды, Информация);
	КонецЕсли;
	
	Элементы.ГруппаЦеныОстаткиТоваровПодменюОтборы.Заголовок = ОтборыЗаголовок;
	Элементы.ГруппаЦеныОстаткиТоваровПодменюОтборы.Картинка = Элементы[ИмяКоманды].Картинка;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеОстаткиТоваровМП

&НаКлиенте
Процедура Подключаемый_СинхронизироватьОстатки()
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		
		ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
		ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
		ПараметрыОперации.ВыводитьСообщения = Ложь;
		
		ПараметрыОперации.Очередь.Добавить(,
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса());
		
		НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_СинхронизироватьОстатки", 120, Истина);
	
КонецПроцедуры

// Изменение состояния регламентного задания выгрузки остатков
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  ОписаниеУчетнойЗаписи - Структура - Описание учетной записи. Свойства:
//  * УчетнаяЗаписьИдентификатор - Строка - Идентификатор учетной записи
//  Использование - Булево - Использование
// 
// Возвращаемое значение:
//  Булево - Истина - Задание включено
&НаСервереБезКонтекста
Функция ОстаткиТоваровМПИзменитьСостояниеРегламентногоЗадание(Знач ВидМаркетплейса, Знач ОписаниеУчетнойЗаписи, Использование)
	
	// Это регламентное задание создается в ручном режиме в форме "Настройки интеграции".
	// Включать и выключать его можно только, если оно уже существует

	КлючЗадания = ИнтеграцияСМаркетплейсамиСИКлиентСервер.КлючЗаданияРасчетОстатковТоваров(
		ВидМаркетплейса,
		ОписаниеУчетнойЗаписи.УчетнаяЗаписьИдентификатор);

	УстановитьПривилегированныйРежим(Истина);
	Отбор = Новый Структура("Ключ", КлючЗадания);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);

	Если Задания.Количество() > 0 Тогда
		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Ключ", Задания[0].Ключ);
		ПараметрыЗадания.Вставить("Использование", Использование);
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задания[0].УникальныйИдентификатор, ПараметрыЗадания);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);

	Возврат ИнтеграцияСМаркетплейсамиСИ.ОпределитьСостояниеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы, КлючЗадания);

КонецФункции

#КонецОбласти

#Область СлужебныеЦеныОстаткиТоваровМП

&НаСервере
Процедура ИнициализацияЦеныОстаткиТоваровМП()
	
	НоменклатураПартнеров.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", Неопределено);
	
	ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", Неопределено);
	
	ДопСвойстваСКД = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
	// Настройки для менеджера длительных операций
	ДопСвойстваСКД.Вставить("ИмяФормы", Метаданные.Имя);
	ДопСвойстваСКД.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ДопСвойстваСКД.Вставить("НомерСеансаИБ", НомерСеансаИнформационнойБазы());
	ДопСвойстваСКД.Вставить("ВерсияКэша", Новый УникальныйИдентификатор);
	ДопСвойстваСКД.Вставить("АдресДействующихЗапросов", ПоместитьВоВременноеХранилище(Новый Соответствие, УникальныйИдентификатор));
	
	ПользовательскиеНастройки = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.ПользовательскиеНастройки;
	
	ПолеПорядок = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	Порядок = НаправлениеСортировкиКомпоновкиДанных.Возр;
	Для Каждого ЭлементНастройки Из ПользовательскиеНастройки.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ПорядокКомпоновкиДанных") Тогда
			Для Каждого ЭлементПорядка Из ЭлементНастройки.Элементы Цикл
				Если ЭлементПорядка.Поле = ПолеПорядок И ЭлементПорядка.Использование Тогда
					Порядок = ЭлементПорядка.ТипУпорядочивания;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	ДопСвойстваСКД.Вставить("НаправлениеСортировкиКД", Порядок);
	
	// Отбор по данным сервиса интеграции
	ДопСвойстваСКД.Вставить("ОтобратьПоНоменклатуреСИ", Ложь);
	
	// Кэширование данных из сервиса интеграции. Данные хранятся страницами
	ДопСвойстваСКД.Вставить("АдресаСтраницКэша", Новый СписокЗначений);
	ДопСвойстваСКД.Вставить("РазмерСтраницы", 1100);	// Немного больше платформенной страницы
	
	ДопСвойстваСКД.Вставить("БрендыМП", ПоместитьВоВременноеХранилище(Новый СписокЗначений, УникальныйИдентификатор));
	ДопСвойстваСКД.Вставить("КатегорииМП", ПоместитьВоВременноеХранилище(Новый СписокЗначений, УникальныйИдентификатор));
	
	МаркерыСтатусов = Новый СписокЗначений;
	МаркерыСтатусов.Добавить(8, НСтр("ru = 'Все';
									|en = 'All'"));
	МаркерыСтатусов.Добавить(3, НСтр("ru = 'Ошибок нет';
									|en = 'No errors'"));
	МаркерыСтатусов.Добавить(0, НСтр("ru = 'Требуют исправления';
									|en = 'Require correction'"));
	Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) Тогда
		МаркерыСтатусов.Добавить(5, НСтр("ru = 'На карантине';
										|en = 'In quarantine'"));
	КонецЕсли;	
	ДопСвойстваСКД.Вставить("МаркерыСтатусов", ПоместитьВоВременноеХранилище(МаркерыСтатусов, УникальныйИдентификатор));
	
	// Запрет платформенного поиска в списке цен
	ПоляДС = "НаименованиеМП,БрендМП,КатегорияМП";
	ЦеныОстаткиТоваровМП.УстановитьОграниченияИспользованияВОтборе(СтрРазделить(ПоляДС, ",", Ложь));
	
	// Условное оформление
	// Дата выгрузки остатков
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "ОстаткиТоваровМП.ДатаВыгрузки", Элементы.ОстаткиТоваровМПДатаВыгрузки.Имя);
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ДП='<%1>'", НСтр("ru = 'не выгружалось';
																					|en = 'pending export'")));
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОстаткиТоваровМПДатаВыгрузки.Имя);
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОстаткиТоваровМП.ДатаВыгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	// Представление данных, которые не выгружались.
	ЭлементОформления = ЦеныОстаткиТоваровМП.УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("ДП='<%1>'", НСтр("ru = 'не выгружалось';
																					|en = 'pending export'")));
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ДатаВыгрузкиЦен");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДатаВыгрузкиЦен");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СинхронизироватьЦены(Знач КомпоновщикНастроек, Знач КлючНоменклатурыМП, ВидМаркетплейса)
	
	ИнтеграцияСМаркетплейсамиСИ.СинхронизироватьЦены(КомпоновщикНастроек, КлючНоменклатурыМП, ВидМаркетплейса);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборЦенПоДаннымСервисаИнтеграции(Значение, ИмяКоманды)
	
	ОчиститьСообщения();
	
	// Сохраним настроенный отбор динамического списка
	Если Значение <> Неопределено Тогда
		
		Для Каждого ЭлементОтбора Из ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы Цикл
			Если СтрНайти(ЭлементОтбора.Параметр, "Использовать") > 0 И ЭлементОтбора.Значение = Истина Тогда
				ЭлементОтбора.Значение = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ДопСвойстваСКД = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
		
		Если Значение = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен().Все Тогда

			НоменклатураВключение = ЦеныОстаткиТоваровМП.Параметры.Элементы.Найти("НоменклатураВключение");
			Если НоменклатураВключение <> Неопределено Тогда
				НоменклатураВключение.Использование = Ложь;
				НоменклатураВключение.Значение = Новый СписокЗначений;
			КонецЕсли;
			
			ДопСвойстваСКД.Вставить("ОтобратьПоНоменклатуреСИ", Ложь);
			
		Иначе
			
			ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьМаркерСтатуса", Истина);
			ПользовательскиеНастройкиДС = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.ПользовательскиеНастройки;
	
			Для Каждого ЭлементНастройки Из ПользовательскиеНастройкиДС.Элементы Цикл
				Если ТипЗнч(ЭлементНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
					ЭлементНастройки.Элементы.Очистить();
					
					ЭлементОтбора = ЭлементНастройки.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
					ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("МаркерСтатуса");
					ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
					ЭлементОтбора.ПравоеЗначение = Значение;
					ЭлементОтбора.Использование = Ложь;
					
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			ДопСвойстваСКД.Вставить("ОтобратьПоНоменклатуреСИ", Истина);
			
			ЦеныОстаткиТоваровМП.Параметры.Элементы.Найти("НоменклатураВключение").Использование = Истина;
			
		КонецЕсли;
	КонецЕсли;
	
	// Получим данные из сервиса интеграции
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибкиПустойУчетнойЗаписи(), , "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	Партнер = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись).Партнер;
	Если Не ЗначениеЗаполнено(Партнер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Укажите партнера в настройках учетной записи.';
				|en = 'Specify the partner in the account settings.'"),,, "ОбщиеНастройки");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.Вставить("ИмяКомандыОтбораПоСтатусу", ИмяКоманды);
	ПараметрыОперации.РежимОткрытияОкнаОжидания = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ПараметрыМетода = Новый Структура("ВерсияКэша", ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки.ДополнительныеСвойства.ВерсияКэша);
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройкиДляОтбораДС(ИмяДинамическогоСписка)
	
	НастройкиСКД = Новый Структура;
	НастройкиСКД.Вставить("СхемаКомпоновкиДанных", Элементы[ИмяДинамическогоСписка].ПолучитьИсполняемуюСхемуКомпоновкиДанных());
	
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(НастройкиСКД.СхемаКомпоновкиДанных));
	Компоновщик.ЗагрузитьНастройки(ЭтотОбъект[ИмяДинамическогоСписка].КомпоновщикНастроек.ПолучитьНастройки());
	
	НастройкиСКД.Вставить("Настройки", Компоновщик.ПолучитьНастройки());
	
	Возврат ИнтеграцияСМаркетплейсамиСИ.ПолучитьНастройкиДляОтбораСпискаЦеныОстаткиТоваровМП(НастройкиСКД);

КонецФункции

&НаКлиенте
Процедура ПриЗавершенииНастройкиОтбораДС(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьНастройкиОтбораДС(Результат, ДополнительныеПараметры.ИмяДинамическогоСписка);
	УстановитьОтборЦенПоДаннымСервисаИнтеграции(Неопределено, Команды.ОтобратьПроизвольно.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиОтбораДС(НастройкиСКД, ИмяДинамическогоСписка)
	
	ПользовательскиеНастройкиДС = ЭтотОбъект[ИмяДинамическогоСписка].КомпоновщикНастроек.ПользовательскиеНастройки;	// ПользовательскиеНастройкиКомпоновкиДанных
	НастройкиДС = ЭтотОбъект[ИмяДинамическогоСписка].КомпоновщикНастроек.Настройки;	// НастройкиКомпоновкиДанных
	НастройкиОтбораСКД = НастройкиСКД.Отбор;	// ОтборКомпоновкиДанных
	
	ПолеБрендМП = Новый ПолеКомпоновкиДанных("БрендМП");
	ПолеКатегорияМП = Новый ПолеКомпоновкиДанных("КатегорияМП");
	ПолеТолькоВНаличии = Новый ПолеКомпоновкиДанных("ТолькоВНаличии");
	ПолеНаименованиеМП = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	ПолеМаркерСтатуса = Новый ПолеКомпоновкиДанных("МаркерСтатуса");
	
	НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьБрендыМП", Ложь);
	НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьКатегорииМП", Ложь);
	НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьТолькоВНаличии", Ложь);
	НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьНаименованиеМП", Ложь);
	НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьМаркерСтатуса", Ложь);
	
	Для Каждого ЭлементНастройки Из ПользовательскиеНастройкиДС.Элементы Цикл
		Если ТипЗнч(ЭлементНастройки) = Тип("ОтборКомпоновкиДанных") Тогда
			ЭлементНастройки.Элементы.Очистить();
			
			Для Каждого ЭлементОтбораСКД Из НастройкиОтбораСКД.Элементы Цикл
				Если ТипЗнч(ЭлементОтбораСКД) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
					Если ЭлементОтбораСКД.ЛевоеЗначение = ПолеБрендМП Тогда
						НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьБрендыМП", ЭлементОтбораСКД.Использование);
						ЭлементОтбораСКД.Использование = Ложь;
					ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеКатегорияМП Тогда
						НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьКатегорииМП", ЭлементОтбораСКД.Использование);
						ЭлементОтбораСКД.Использование = Ложь;
					ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеТолькоВНаличии Тогда
						НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьТолькоВНаличии", ЭлементОтбораСКД.Использование);
						ЭлементОтбораСКД.Использование = Ложь;
					ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеНаименованиеМП Тогда
						НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьНаименованиеМП", ЭлементОтбораСКД.Использование);
						ЭлементОтбораСКД.Использование = Ложь;
					ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеМаркерСтатуса Тогда
						НастройкиДС.ПараметрыДанных.УстановитьЗначениеПараметра("ИспользоватьМаркерСтатуса", ЭлементОтбораСКД.Использование);
						ЭлементОтбораСКД.Использование = Ложь;
					КонецЕсли;
					
					ЭлементОтбора = ЭлементНастройки.Элементы.Добавить(Тип(ЭлементОтбораСКД));
					ЗаполнитьЗначенияСвойств(ЭлементОтбора, ЭлементОтбораСКД);
				КонецЕсли;
			КонецЦикла;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ЭтотОбъект[ИмяДинамическогоСписка].КомпоновщикНастроек.Настройки.ДополнительныеСвойства.Вставить("ОтобратьПоНоменклатуреСИ", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СинхронизироватьЦены()
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		
		Настройки = ЦеныОстаткиТоваровМП.КомпоновщикНастроек.Настройки;
		
		ДействующиеЗапросы = ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
		Если ДействующиеЗапросы.Количество() = 0 Тогда
			// Актуализируем данные только в случае, если на них есть фокус
			ТекущиеДанные = Элементы.ЦеныОстаткиТоваровМП.ТекущиеДанные;
			Если ТекущиеДанные <> Неопределено Тогда
				КлючНоменклатурыМП = СтрШаблон("%1_%2", ТекущиеДанные.НаименованиеМП, ТекущиеДанные.ИдентификаторМП);
				СинхронизироватьЦены(ЦеныОстаткиТоваровМП.КомпоновщикНастроек, КлючНоменклатурыМП, ВидМаркетплейса);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_СинхронизироватьЦены", 120, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЦеныОжиданиеПолученияСтраницыДанных(ДанныеСообщения, ДополнительныеПараметры = Неопределено) Экспорт
	
	ФоновоеЗадание = ДанныеСообщения.ФоновоеЗадание;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ДанныеСообщения);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ДанныеСообщения);
	КонецЕсли;
	
	Элементы.ЦеныОстаткиТоваровМП.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

// Определение состояния регламентных заданий, которыми можно управлять на форме
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  ОписаниеУчетнойЗаписи - Структура - Описание учетной записи. Свойства:
//  * УчетнаяЗаписьИдентификатор - Строка - Идентификатор учетной записи
//  ОстаткиТоваровМПРЗВключено - Булево - текущее состояние регламентного задания
&НаСервереБезКонтекста
Процедура ОпределитьСостоянияРегламентныхЗаданий(Знач ВидМаркетплейса, Знач ОписаниеУчетнойЗаписи, ОстаткиТоваровМПРЗВключено)
	
	Ключ = ИнтеграцияСМаркетплейсамиСИКлиентСервер.КлючЗаданияРасчетОстатковТоваров(
		ВидМаркетплейса,
		ОписаниеУчетнойЗаписи.УчетнаяЗаписьИдентификатор);
	ОстаткиТоваровМПРЗВключено = ИнтеграцияСМаркетплейсамиСИ.ОпределитьСостояниеРегламентногоЗадания(
		Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы, Ключ);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоЧисло(Знач СтрокаПроверки)
	
	Для НомерСимвола = 1 По СтрДлина(СтрокаПроверки) Цикл
		
		КодСимвола = КодСимвола(Сред(СтрокаПроверки, НомерСимвола, 1));
		
		Если НЕ ((КодСимвола >= 48 И КодСимвола <= 57)
			ИЛИ КодСимвола = 44 ИЛИ КодСимвола = 46) Тогда
			Возврат Ложь;
		КонецЕсли;

	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступность(ОписаниеУчетнойЗаписи = Неопределено)
	ИнтеграцияСМаркетплейсамиСИКлиент.УстановитьВидимостьДоступность(ЭтотОбъект, ОписаниеУчетнойЗаписи);
	#Если ВебКлиент Тогда
		Элементы.ГруппаСтраницы.Видимость = Истина;
	#КонецЕсли
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НоменклатураКонтрагентовПоСсылкам(Знач Ссылки)
	Возврат ИнтеграцияСМаркетплейсамиВызовСервера.НоменклатураКонтрагентовПоСсылкам(Ссылки);
КонецФункции

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("УчетныеЗаписи", Новый Соответствие);
	Кэш.Вставить("ШрифтВыделеннойСтраницы", ШрифтыСтиля.ВажнаяНадписьШрифт);
	Кэш.Вставить("ШрифтОбычнойСтраницы", ШрифтыСтиля.ОбычныйШрифтТекста);
	Кэш.Вставить("ЦветФонаКнопкиТекущегоЭтапа", ЦветаСтиля.ЦветФонаЖелтыйСостояниеДокументаБЭД);
	Кэш.Вставить("ЦветФонаКнопки", ЦветаСтиля.ЦветФонаКнопки);
	Кэш.Вставить("ПравоНаОстатки", Пользователи.РолиДоступны("РедактированиеОстатковТоваровМаркетплейсов"));
	Кэш.Вставить("ПравоНаЦены", ПравоДоступа("Изменение", Метаданные.Документы.УстановкаЦенНоменклатуры));
	Кэш.Вставить("ЗаголовокЦены1С", Элементы.ЦеныОстаткиТоваровМПЦена1С.Заголовок);
	Кэш.Вставить("ЗаголовокЦеныМП", Элементы.ЦеныОстаткиТоваровМПЦенаМП.Заголовок);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьЭлементыФормы()
	
	Префикс = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксМаркетплейса(ВидМаркетплейса);
	ЭтоWildberries = ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса);
	
	Заголовок = СтрШаблон(НСтр("ru = 'Обмен с %1';
								|en = 'Exchange with %1'"),
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса));
		
	ЗадатьЗаголовокЭлементу(Элементы.НоменклатураПартнеровНаименованиеМП, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ОстаткиТоваровМПНоменклатураПартнера, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ОстаткиТоваровМПКоличествоМаркетплейса, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ЦеныОстаткиТоваровМПНаименованиеМП, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ЦеныОстаткиТоваровМПЦенаМП, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.КоличествоМП, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ЦеныОстаткиТоваровМПКатегорияМП, Префикс);
	ЗадатьЗаголовокЭлементу(Элементы.ЦеныОстаткиТоваровМПБрендМП, Префикс);
	
	Элементы.ОстаткиТоваровМПСклад.Видимость = ЭтоWildberries;
	Элементы.ОтобратьНаКарантине.Видимость = ЭтоWildberries;
	Элементы.ЦеныОстаткиТоваровМПЦенаСоВсемиСкидками.Видимость = ЭтоWildberries;
	Элементы.ЦеныОстаткиТоваровМПСкидкаWB.Видимость = ЭтоWildberries;
	Элементы.ЦеныОстаткиТоваровМПЦенаСоВсемиСкидкамиМП.Видимость = ЭтоWildberries;
	Элементы.ЦеныОстаткиТоваровМПСкидкаWBМП.Видимость = ЭтоWildberries;
		
КонецПроцедуры

&НаСервере
Процедура ЗадатьЗаголовокЭлементу(Элемент, Префикс)
	Элемент.Заголовок = СтрШаблон(НСтр("ru = '%1 %2';
										|en = '%1 %2'"), Элемент.Заголовок, Префикс);
КонецПроцедуры

&НаКлиенте
Процедура РаскраситьКнопкиПанелиРазделов()

	ИмяТекущейСтраницы = Элементы.ГруппаСтраницы.ТекущаяСтраница.Имя;

	Элементы[СтрЗаменить(ИмяТекущейСтраницы, "Страница", "Переход")].Шрифт = Кэш.ШрифтВыделеннойСтраницы;

	Для Каждого ЭлементСтраница Из Элементы.ГруппаСтраницы.ПодчиненныеЭлементы Цикл
		Если ЭлементСтраница.Имя <> ИмяТекущейСтраницы Тогда
			Элементы[СтрЗаменить(ЭлементСтраница.Имя, "Страница", "Переход")].Шрифт = Кэш.ШрифтОбычнойСтраницы;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВидимостьПодсказки(ВидимостьПодсказки)

	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНастройкаНоменклатурыПартнеров Тогда
		ИмяЭлементаПодсказки = "ПодсказкаНоменклатураПартнеров";
		ИмяКнопкиСкрыть      = "СкрытьПодсказкуНоменклатураПартнеров";
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыгрузкаОстатков Тогда
		ИмяЭлементаПодсказки = "ПодсказкаОстаткиТоваровМП";
		ИмяКнопкиСкрыть      = "СкрытьПодсказкуОстаткиТоваровМП";
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаВыгрузкаЦен Тогда
		ИмяЭлементаПодсказки = "ПодсказкаЦеныОстаткиТоваровМП";
		ИмяКнопкиСкрыть      = "СкрытьПодсказкуЦеныОстаткиТоваровМП";
	КонецЕсли;

	Элементы[ИмяЭлементаПодсказки].Видимость = ВидимостьПодсказки;
	Элементы[ИмяКнопкиСкрыть].Видимость      = ВидимостьПодсказки;

КонецПроцедуры

// Текст ошибки пустой учетной записи.
// 
// Возвращаемое значение:
//  Строка - Текст ошибки пустой учетной записи
&НаКлиентеНаСервереБезКонтекста
Функция ТекстОшибкиПустойУчетнойЗаписи()
	Возврат НСтр("ru = 'Укажите учетную запись.';
				|en = 'Specify the account.'");
КонецФункции

// Сформировать параметры для открытия формы обработки ПрайсЛист.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись маркетплейса
//  ВыделенныеСтроки - Неопределено, Массив из Число - Выделенные строки
// 
// Возвращаемое значение:
//  Структура - Сформировать параметры формы прайс лист:
// * ВидыЦен - СписокЗначений из СправочникСсылка.ВидыЦен - список видов цен, используемых в маркетплейсе
// * Номенклатура - СписокЗначений из СправочникСсылка.Номенклатура - список номенклатуры, для которой необходимо рассчитать цены
&НаСервереБезКонтекста
Функция СформироватьПараметрыРедактированияПрайсЛиста(Знач УчетнаяЗапись, Знач ВыделенныеСтроки = Неопределено)
	
	Возврат ИнтеграцияСМаркетплейсамиСИ.СформироватьПараметрыРедактированияПрайсЛиста(УчетнаяЗапись, ВыделенныеСтроки);
	
КонецФункции

// Заполнить массив НоменклатураМП выделенными элементами номенклатура контрагента.
// 
// Параметры:
//  НоменклатураМП - массив из СправочникСсылка.НоменклатураКонтрагентов - Учетная запись маркетплейса
//  ВыделенныеСтроки - Неопределено, Массив из Число - Выделенные строки
// 
&НаСервереБезКонтекста
Процедура ЗаполнитьНоменклатураМП(НоменклатураМП, Знач ВыделенныеСтроки)
	Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
		НоменклатураМП.Добавить(КлючЗаписи.Объект1С);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьПриИзмененииПродолжение()
	
	ОписаниеУчетнойЗаписи = Неопределено;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		
		Элементы.ГруппаСостояниеВСервисе.Видимость = Ложь;
		Элементы.ЦеныОстаткиТоваровМПЦена1С.Заголовок = Кэш.ЗаголовокЦены1С;
		Элементы.ЦеныОстаткиТоваровМПЦенаМП.Заголовок = Кэш.ЗаголовокЦеныМП;
		
		ОстаткиТоваровМПРЗВключено = Ложь;
		ЦеныТоваровМПРЗВключено = Ложь;
		
		ОстаткиТоваровМП.Очистить();
		
		НоменклатураПартнеров.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", Неопределено);
		ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", Неопределено);
		
	Иначе
		
		ОписаниеУчетнойЗаписи = Кэш.УчетныеЗаписи.Получить(УчетнаяЗапись);
		
		НоменклатураПартнеров.Параметры.УстановитьЗначениеПараметра("УчетнаяЗапись", УчетнаяЗапись);
		ЦеныОстаткиТоваровМП.Параметры.УстановитьЗначениеПараметра("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		
		ПолучитьУчетныеЗаписи("Выбор");
		
		ОпределитьСостоянияРегламентныхЗаданий(ВидМаркетплейса, ОписаниеУчетнойЗаписи, ОстаткиТоваровМПРЗВключено);
		
		ЦеныТоваровМПРЗВключено = ОписаниеУчетнойЗаписи.РазрешенаВыгрузкаЦенНаМаркетплейс;
		
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ОписаниеУчетнойЗаписи);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти