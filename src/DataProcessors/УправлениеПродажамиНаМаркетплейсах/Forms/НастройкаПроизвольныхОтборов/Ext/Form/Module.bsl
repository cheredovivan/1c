
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИнициализироватьФорму();
	ИнициализироватьКомпоновщикНастроек();
	УстановитьОтборыПоУмолчанию();
	
	Элементы.НастройкиОтборВидСравнения.УстановитьДействие("НачалоВыбора", "Подключаемый_НастройкиОтборВидСравненияНачалоВыбора");
	Элементы.НастройкиОтборЛевоеЗначение.УстановитьДействие("ПриИзменении", "Подключаемый_НастройкиОтборЛевоеЗначениеПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкиОтборВидСравненияНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.НастройкиОтбор.ТекущиеДанные;
	
	ДанныеОтбора = Новый Структура("Поле", ТекущиеДанные.ЛевоеЗначение);
	
	НайденныеСтроки = ДоступныеВидыСравнения.НайтиСтроки(ДанныеОтбора);
	Если НайденныеСтроки.Количество() > 0 Тогда 
		Элемент.СписокВыбора.ЗагрузитьЗначения(НайденныеСтроки[0].ВидыСравнения.ВыгрузитьЗначения());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НастройкиОтборЛевоеЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.НастройкиОтбор.ТекущиеДанные;
	
	ДанныеОтбора = Новый Структура("Поле", ТекущиеДанные.ЛевоеЗначение);
	
	НайденныеСтроки = ДоступныеВидыСравнения.НайтиСтроки(ДанныеОтбора);
	Если НайденныеСтроки.Количество() > 0 Тогда 
		Элемент.СписокВыбора.ЗагрузитьЗначения(НайденныеСтроки[0].ВидыСравнения.ВыгрузитьЗначения());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыНастройкиОтбор

&НаКлиенте
Процедура НастройкиОтборПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиПоУмолчанию(Команда)
	НастройкиПоУмолчаниюНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование(Команда)
	Закрыть(ПолучитьНастройки());
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьФорму()
	
	АдресПараметров = Параметры.АдресПараметров;

	Если Не ПустаяСтрока(Параметры.ЗаголовокФормы) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
	Если Не ПустаяСтрока(Параметры.ЗаголовокЗавершитьРедактирование) Тогда
		Элементы.ФормаЗавершитьРедактирование.Заголовок = Параметры.ЗаголовокЗавершитьРедактирование;
	КонецЕсли;
	
	Если Параметры.Отбор <> Неопределено Тогда
		Для Каждого КлючИЗначение Из Параметры.Отбор Цикл
			ДанныеСтроки = ОтборПоУмолчанию.Добавить();
			ДанныеСтроки.ИмяПоля = КлючИЗначение.Ключ;
			Если ТипЗнч(КлючИЗначение.Значение) = Тип("Структура") Тогда
				ДанныеСтроки.Значение = КлючИЗначение.Значение.Значение;
				ДанныеСтроки.ВидСравнения = КлючИЗначение.Значение.ВидСравнения;
			Иначе
				ДанныеСтроки.Значение = КлючИЗначение.Значение;
				ДанныеСтроки.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	ПараметрыНастройки = ПолучитьИзВременногоХранилища(АдресПараметров);
	
	СхемаКомпоновкиДанных = ПараметрыНастройки.СхемаКомпоновкиДанных;
	
	АдресСКД = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСКД));
	
	Если ПараметрыНастройки.Настройки <> Неопределено
		И ЗначениеЗаполнено(ПараметрыНастройки.Настройки) Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыНастройки.Настройки);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;
	
	КомпоновщикНастроек.Восстановить();

	Для Каждого ДоступноеПолеОтбора Из ПараметрыНастройки.Настройки.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		НоваяСтрока = ДоступныеВидыСравнения.Добавить();
		НоваяСтрока.Поле = ДоступноеПолеОтбора.Поле;
		НоваяСтрока.ВидыСравнения = ДоступноеПолеОтбора.ДоступныеВидыСравнения;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастройкиПоУмолчаниюНаСервере()
	
	ПараметрыНастройки = ПолучитьИзВременногоХранилища(АдресПараметров);
	СхемаКомпоновкиДанных = ПараметрыНастройки.СхемаКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	УстановитьОтборыПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборыПоУмолчанию()
	
	Для Каждого ДанныеСтроки Из ОтборПоУмолчанию Цикл
		
		ЭлементОтбора = КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(
			КомпоновщикНастроек, 
			ДанныеСтроки.ИмяПоля, 
			ДанныеСтроки.Значение,
			ДанныеСтроки.ВидСравнения,,);
		
		ДанныеСтроки.Идентификатор = ЭлементОтбора.ИдентификаторПользовательскойНастройки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНастройки() 
	
	Возврат КомпоновщикНастроек.ПолучитьНастройки();
	
КонецФункции

#КонецОбласти
