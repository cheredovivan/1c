
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Параметры.ОписаниеУчетнойЗаписи) Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствует обязательный параметр открытия формы ""Ограничения выгрузки остатков"".';
								|en = 'The required parameter for opening the ""Balance export restrictions"" form is missing.'");
	КонецЕсли;
	
	УстановитьКэшированныеЗначения();
	
	УчетнаяЗапись = Параметры.ОписаниеУчетнойЗаписи.УчетнаяЗапись;
	Кэш.УчетныеЗаписи.Вставить(УчетнаяЗапись, Параметры.ОписаниеУчетнойЗаписи);
	Элементы.УчетнаяЗапись.СписокВыбора.Добавить(УчетнаяЗапись, Параметры.ОписаниеУчетнойЗаписи.Представление);
	
	ВидМаркетплейса = Параметры.ВидМаркетплейса;
	Используется = Параметры.Используется;
	Код = Параметры.Код;
	Наименование = Параметры.Наименование;
	ОбластьДействия = Параметры.ОбластьДействия;
	ПроцентОстатка = Параметры.ПроцентОстатка;
	СтраховойЗапас = Параметры.СтраховойЗапас;
	МинимальныйОстаток = Параметры.МинимальныйОстаток;
	МаксимальныйОстаток = Параметры.МаксимальныйОстаток;
	
	ЗаполнитьТипОбластиДействия();
	
	Если МинимальныйОстаток <> 0 Или МаксимальныйОстаток <> 0 Тогда
		АвтоКорректировкаПоДиапазону = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбластьДействияПриИзменении(Элемент)
	ЗаполнитьТипОбластиДействия();
КонецПроцедуры

&НаКлиенте
Процедура ИспользуетсяПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПроцентОстаткаПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СтраховойЗапасПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура МинимальныйОстатокПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура МаксимальныйОстатокПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура АвтоКорректировкаПоДиапазонуПриИзменении(Элемент)
	УстановитьВидимостьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура АвтоматическоеНаименование(Команда)
	
	ОчиститьСообщения();
	СформироватьАвтоматическоеНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВНастройки(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверкаПройдена() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		СформироватьАвтоматическоеНаименование();
	КонецЕсли;
	
	ОчередьДлительныхОпераций = Новый Массив;
	
	ОписаниеМетода = Новый Структура("Параметры, Имя", Неопределено,
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров());
	ОчередьДлительныхОпераций.Добавить(ОписаниеМетода);
	
	ПараметрыМенеджераДО = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыМенеджераДО.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыМенеджераДО.ЗакрытьПослеЗавершенияОперации = Истина;
	ПараметрыМенеджераДО.ВыводитьОкноОжидания = Истина;
	ПараметрыМенеджераДО.Вставить("РежимОткрытияОкнаОжидания", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

	ПараметрыДлительнойОперации = ИнициироватьДлительнуюОперацию(ОчередьДлительныхОпераций, ПараметрыМенеджераДО, Ложь);
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыДлительнойОперации);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("УчетныеЗаписи", Новый Соответствие);
	Кэш.Вставить("ОбластиДействия", Новый Соответствие);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступность()
	
	Элементы.ГруппаОграничениеОстатковДетали.Доступность = Используется;
	Элементы.ГруппаОграничениеОстатковАвтокорректировка.Доступность = Используется;
	Элементы.ГруппаОграничениеОстатковДиапазон.Доступность = Используется И АвтоКорректировкаПоДиапазону;
	
	Если Используется И АвтоКорректировкаПоДиапазону Тогда
		Если МаксимальныйОстаток = 0 Тогда
			МаксимальныйОстаток = 99999;
		КонецЕсли;
	КонецЕсли;
	
	ОграничениеОтключено = НСтр("ru = 'Настройка ограничения выгрузки не используется';
								|en = 'The export restriction setting is not used'");
	ТекстОграничения1 = НСтр("ru = 'Остаток недоступен к выгрузке';
							|en = 'The balance is unavailable for export'");
	ТекстОграничения2 = НСтр("ru = 'К выгрузке доступно %1 % от учетного количества остатка номенклатуры';
							|en = 'You can export %1% of the accounted stock balance quantity'");
	ТекстОграничения3 = НСтр("ru = 'Из выгружаемого остатка номенклатуры исключается страховой запас в количестве %1 ед.';
							|en = 'Safety stock in the amount of %1 items is excluded from the stock balance to export'");
	ТекстОграничения4 = НСтр("ru = 'К выгрузке доступно %1 % от учетного количества остатка номенклатуры, но с обеспечением страхового запаса в количестве %2 ед. остатка номенклатуры';
							|en = 'You can export %1% of the accounted stock balance if you provide safety stock in the amount of %2 items of the stock balance'");
	
	Если Не Используется Тогда
		ПредставлениеОграничения = ОграничениеОтключено;
	ИначеЕсли ПроцентОстатка = 100 И ЗначениеЗаполнено(СтраховойЗапас) Тогда
		ПредставлениеОграничения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОграничения3, СтраховойЗапас);
	ИначеЕсли ЗначениеЗаполнено(ПроцентОстатка) И ЗначениеЗаполнено(СтраховойЗапас) Тогда
		ПредставлениеОграничения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОграничения4, ПроцентОстатка, СтраховойЗапас);
	ИначеЕсли ЗначениеЗаполнено(ПроцентОстатка) Тогда
		ПредставлениеОграничения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОграничения2, ПроцентОстатка);
	Иначе
		ПредставлениеОграничения = ТекстОграничения1;
	КонецЕсли;
	
	Если Используется И АвтоКорректировкаПоДиапазону Тогда
		
		ЧастиПредставления = Новый Массив;
		ЧастиПредставления.Добавить(ПредставлениеОграничения);
		ЧастиПредставления.Добавить(СтрШаблон(НСтр("ru = 'При остатке меньше %1, отобразится %1';
													|en = 'If the balance is less than %1, %1 will be displayed'"), МинимальныйОстаток));
		ЧастиПредставления.Добавить(СтрШаблон(НСтр("ru = 'При остатке больше %1, отобразится %1';
													|en = 'If the balance is grater than %1, %1 will be displayed'"), МаксимальныйОстаток));
		
		ПредставлениеОграничения = СтрСоединить(ЧастиПредставления, Символы.ПС);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипОбластиДействия()
	
	Если Не ЗначениеЗаполнено(ОбластьДействия) Тогда
		ТипОбластиДействия = "";
	ИначеЕсли ТипЗнч(ОбластьДействия) = Тип("СправочникСсылка.Номенклатура")
				И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОбластьДействия, "ЭтоГруппа") Тогда
		ТипОбластиДействия = "(" + НСтр("ru = 'Номенклатура';
										|en = 'Items'") + ")";
	ИначеЕсли ТипЗнч(ОбластьДействия) = Тип("СправочникСсылка.Номенклатура") Тогда
		ТипОбластиДействия = "(" + НСтр("ru = 'Группа номенклатуры';
										|en = 'Product group'") + ")";
	ИначеЕсли ТипЗнч(ОбластьДействия) = Тип("СправочникСсылка.ТоварныеКатегории") Тогда
		ТипОбластиДействия = "(" + НСтр("ru = 'Товарная категория';
										|en = 'Product category'") + ")";
	ИначеЕсли ТипЗнч(ОбластьДействия) = Тип("СправочникСсылка.ВидыНоменклатуры") Тогда
		ТипОбластиДействия = "(" + НСтр("ru = 'Вид номенклатуры';
										|en = 'Item kind'") + ")";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьАвтоматическоеНаименование()
	
	ЧастиНаименования = Новый массив;
	ЧастиНаименования.Добавить(СтрШаблон("%1%2", ПроцентОстатка, "%"));
	
	Если ЗначениеЗаполнено(СтраховойЗапас) Тогда
		ЧастиНаименования.Добавить(СтрШаблон("-%1 ед.", СтраховойЗапас));
	КонецЕсли;
	
	Если АвтоКорректировкаПоДиапазону Тогда
		ЧастиНаименования.Добавить(СтрШаблон(" (%1", МинимальныйОстаток));
		Если МаксимальныйОстаток = 99999 Тогда
			ЧастиНаименования.Добавить(" - ∞)");
		Иначе
			ЧастиНаименования.Добавить(СтрШаблон(" - %1)", МаксимальныйОстаток));
		КонецЕсли;
	КонецЕсли;
	
	ЧастиНаименования.Добавить(СтрШаблон(" (%1)", ОбластьДействия));
	
	Наименование = СтрСоединить(ЧастиНаименования);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверкаПройдена()
	
	Результат = Истина;
	
	Если МаксимальныйОстаток < МинимальныйОстаток Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Максимальный остаток должен быть больше минимального.';
														|en = 'The maximum balance must be greater than the minimum one.'")
			,,"МаксимальныйОстаток");
		Результат = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

// Инициировать длительные операции.
// 
// Параметры:
//  ОписанияМетодов - Массив из структура - описание методов, которые необходимо выполнить.Свойства:
//   * Параметры - Неопределено, Структура - параметры метода
//   * Имя - Строка - имя метода
//  ПараметрыОперации - Структура - Параметры операции
//  ВыводитьОкноОжидания - Булево - Вывести окно ожидания на время выполнения длительной операции.
//  ПередатьПараметрыЧерезКэш - Булево - указывает, что параметры для выполнения длительной операции передавались через кэш.
&НаСервере
Функция ИнициироватьДлительнуюОперацию(ОписанияМетодов, ПараметрыОперации, ПередатьПараметрыЧерезКэш = Ложь)

	Для Каждого ОписаниеМетода Из ОписанияМетодов Цикл
		ПараметрыОперации.Очередь.Добавить(ОписаниеМетода.Параметры, ОписаниеМетода.Имя);
	КонецЦикла;

	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);

	Если ПередатьПараметрыЧерезКэш Тогда
		Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;

		Возврат Неопределено;
	КонецЕсли;

	Возврат ПараметрыОперации;

КонецФункции

#КонецОбласти

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)

	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации,
		ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;

	Отказ = Ложь;

	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);

КонецПроцедуры

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)

	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл

		Если Операция.Представление 
			= ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров() Тогда
			ПараметрыЗапросаПолучитьОграниченияОстатковТоваров(Операция.Значение);
		КонецЕсли;

	КонецЦикла;
	
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;

КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьОграниченияОстатковТоваров(ПараметрыЗапроса)

	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьОграниченияОстатковТоваров();

	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;

КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)

	Отказ = ТипЗнч(Результат) <> Тип("Структура");

	// Операции чтения, запускаемые после успешного выполнения операций корректировки
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
		Возврат;
	КонецЕсли;
	
	Если Не Отказ Тогда
		РеквизитыФормы = Новый Структура;
		РеквизитыФормы.Вставить("ОбластьДействия", ОбластьДействия);
		РеквизитыФормы.Вставить("Код", Код);
	
		ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата, РеквизитыФормы, Отказ);
	КонецЕсли;

	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);

КонецПроцедуры

// Обрабатывает результат менеджера длительных операций на сервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
//  РеквизитыФормы - Структура - Параметры реквизита
//  Отказ - Булево - Признак отказа
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки
&НаСервереБезКонтекста
Функция ОбработатьРезультатМенеджераДлительныхОпераций(Знач АдресРезультата, РеквизитыФормы, Отказ)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);

	Для Каждого Операция Из Очередь Цикл

		Метод = Операция.Представление;
		РезультатОперации = Операция.Значение;
        РезультатОбработки = Истина;
		
		Если Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров() Тогда
			//@skip-check query-in-loop - в массиве только одна такая операция
			ОбработатьРезультатПолучитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки, РеквизитыФормы);
			Отказ = Отказ Или Не РезультатОбработки;	
		КонецЕсли;

	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
	Если Не Отказ И ДополнительныеПараметры.ЗакрытьПослеЗавершенияОперации Тогда
		ПараметрыЗакрытия = Новый Структура;
		ПараметрыЗакрытия.Вставить("Наименование", Наименование);
		ПараметрыЗакрытия.Вставить("Используется", Используется);
		ПараметрыЗакрытия.Вставить("ОбластьДействия", ОбластьДействия);
		ПараметрыЗакрытия.Вставить("ПроцентОстатка", ПроцентОстатка);
		ПараметрыЗакрытия.Вставить("СтраховойЗапас", СтраховойЗапас);
		
		Если АвтоКорректировкаПоДиапазону Или Не Используется Тогда
			ПараметрыЗакрытия.Вставить("МинимальныйОстаток", МинимальныйОстаток);
			ПараметрыЗакрытия.Вставить("МаксимальныйОстаток", МаксимальныйОстаток);
		Иначе
			ПараметрыЗакрытия.Вставить("МинимальныйОстаток", 0);
			ПараметрыЗакрытия.Вставить("МаксимальныйОстаток", 0);
		КонецЕсли;
		
		Закрыть(ПараметрыЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбработатьРезультатПолучитьОграниченияОстатковТоваров(РезультатОперации, РезультатОбработки, РеквизитыФормы)

	Если РезультатОперации = Неопределено Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;

	Если РезультатОперации.КодСостояния <> 200 И РезультатОперации.Ошибки.Количество() > 0 Тогда
		Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
			ОбщегоНазначения.СообщитьПользователю(Ошибка);
		КонецЦикла;
		РезультатОбработки = Ложь;
	КонецЕсли;
	
	Для Каждого ОписаниеОграничения Из РезультатОперации.ОграниченияОстатковТоваров Цикл
		Если ЗначениеЗаполнено(ОписаниеОграничения.ОбластьДействия) Тогда
			Если ОписаниеОграничения.ОбластьДействия = РеквизитыФормы.ОбластьДействия
				И ОписаниеОграничения.Код <> РеквизитыФормы.Код Тогда
				
				ТекстСообщение = СтрШаблон(НСтр("ru = 'Запись невозможна. Существует ограничение для области действия %1';
												|en = 'Saving is impossible. There is a limit for the ""%1"" scope'"), ОписаниеОграничения.ОбластьДействия);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщение);
				
				РезультатОбработки = Ложь;
				
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

 #КонецОбласти