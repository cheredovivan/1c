
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Не ЗначениеЗаполнено(Параметры.ОписаниеУчетнойЗаписи) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьКэшированныеЗначения();
	
	УчетнаяЗапись = Параметры.ОписаниеУчетнойЗаписи.УчетнаяЗапись;
	НаименованиеСкладаМП = Параметры.НаименованиеСкладаМП;
	Склад = Параметры.Склад;
	
	Кэш.УчетныеЗаписи.Вставить(УчетнаяЗапись, Параметры.ОписаниеУчетнойЗаписи);
	Элементы.УчетнаяЗапись.СписокВыбора.Добавить(УчетнаяЗапись, Параметры.ОписаниеУчетнойЗаписи.Представление);
	
	// Инициализация длительной операции
	ОчередьДлительныхОпераций = Новый Массив;
	ОписаниеМетода = Новый Структура("Параметры, Имя", Неопределено, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуСкладаМаркетплейса());
	ОчередьДлительныхОпераций.Добавить(ОписаниеМетода);
	
	ИнициироватьДлительнуюОперацию(ОчередьДлительныхОпераций, Ложь, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВНастройки(Команда)
	Результат = Новый Структура("Склад", Склад);
	Закрыть(Результат);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("УчетныеЗаписи", Новый Соответствие);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЗаписьВДанныеТорговойПлощадки(Источник, Приемник, Отступ = 0)
	
	ШаблонЗаголовка = Прав("                     %1 :", (Отступ + 4));
	ШаблонСтроки1 = Прав("                     %1", (Отступ + 2));
	ШаблонСтроки2 = Прав("                     %1 : %2", (Отступ + 7));
	
	Для Каждого Запись Из Источник Цикл
		
		Если ТипЗнч(Запись) = Тип("КлючИЗначение") Тогда
			
			Если ТипЗнч(Запись.Значение) = Тип("Строка") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Запись.Значение));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Число") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Формат(Запись.Значение, "ЧН=0; ЧГ=0")));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Дата") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Запись.Значение));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Булево") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонСтроки2, Запись.Ключ, Формат(Запись.Значение, "БЛ=Нет; БИ=Да")));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Соответствие") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонЗаголовка, Запись.Ключ));
				
				ДобавитьЗаписьВДанныеТорговойПлощадки(Запись.Значение, Приемник, (Отступ + 3));
				
			ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Массив") Тогда
				Приемник.Добавить(СтрШаблон(ШаблонЗаголовка, Запись.Ключ));
				
				ДобавитьЗаписьВДанныеТорговойПлощадки(Запись.Значение, Приемник, (Отступ + 3));
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(Запись) = Тип("Строка") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Запись));
		ИначеЕсли ТипЗнч(Запись) = Тип("Число") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Формат(Запись, "ЧН=0; ЧГ=0")));
		ИначеЕсли ТипЗнч(Запись) = Тип("Дата") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Запись));
		ИначеЕсли ТипЗнч(Запись.Значение) = Тип("Булево") Тогда
			Приемник.Добавить(СтрШаблон(ШаблонСтроки1, Формат(Запись, "БЛ=Нет; БИ=Да")));
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

// Инициировать длительные операции.
// 
// Параметры:
//  ОписанияМетодов - Массив из структура - описание методов, которые необходимо выполнить.Свойства:
//   * Параметры - Неопределено, Структура - параметры метода
//   * Имя - Строка - имя метода
//  ВыводитьОкноОжидания - Булево - 
//  ПередатьПараметрыЧерезКэш - Булево - 
&НаСервере
Функция ИнициироватьДлительнуюОперацию(ОписанияМетодов, ВыводитьОкноОжидания, ПередатьПараметрыЧерезКэш = Ложь)
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	
	Для Каждого ОписаниеМетода Из ОписанияМетодов Цикл
		ПараметрыОперации.Очередь.Добавить(ОписаниеМетода.Параметры, ОписаниеМетода.Имя);
	КонецЦикла;
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	
	Если ПередатьПараметрыЧерезКэш Тогда
		Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
		
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПараметрыОперации;
	
КонецФункции

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуСкладаМаркетплейса() Тогда
			ПараметрыЗапросаПолучитьКарточкуСкладаМаркетплейса(Операция.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьКарточкуСкладаМаркетплейса(ПараметрыЗапроса)
	
	ПараметрыЗапроса = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьКарточкуСкладаМаркетплейса();
	
	ПараметрыЗапроса.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыЗапроса.ИдентификаторСкладаСервиса = Параметры.ИдентификаторСкладаСервиса;
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
		Возврат;
	КонецЕсли;
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		Метод = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуСкладаМаркетплейса() Тогда
			ОбработатьРезультатПолучитьКарточкуСкладаМаркетплейса(РезультатОперации, РезультатОбработки);
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолучитьКарточкуСкладаМаркетплейса(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Карточка = РезультатОперации.СкладМаркетплейса; // См. ИнтеграцияСМаркетплейсомWildberries.НовыйКарточкаСкладаМаркетплейса
	
	НаименованиеСкладаСервиса = Карточка.НаименованиеСкладаСервиса;
	НаименованиеСкладаМП = Карточка.НаименованиеСкладаМаркетплейса;
	Если Не ЗначениеЗаполнено(Склад) Тогда
		Склад = Карточка.Склад;
	КонецЕсли;
	СхемаРаботыСклада = Карточка.СхемаРаботыСклада;
	
	ЧастиДанныхТорговойПлощадки = Новый Массив;
	
	Если ЗначениеЗаполнено(Карточка.ДополнительныеСведения) Тогда
		ДобавитьЗаписьВДанныеТорговойПлощадки(Карточка.ДополнительныеСведения, ЧастиДанныхТорговойПлощадки);
	КонецЕсли;
	
	ДанныеТорговойПлощадки = СтрСоединить(ЧастиДанныхТорговойПлощадки, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти