// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ФайлОригиналДоступен = Параметры.Свойство("ТекущийФайл", ТекущийФайл);
	Параметры.Свойство("ИдентификаторРегламента", ИдентификаторРегламента);
	Параметры.Свойство("ДляРедактирования", ДляРедактирования);
	Элементы.ФормаОткрытьБезШтампов.Видимость = ФайлОригиналДоступен;
	Элементы.ФормаРазблокироватьРаботуСВнешнимиРесурсами.КнопкаПоУмолчанию = Не ФайлОригиналДоступен;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьБезШтампов(Команда)

	ДанныеФайла = РаботаСФайламиКлиент.ДанныеФайла(ТекущийФайл, ИдентификаторРегламента,, ДляРедактирования);
	РаботаСФайламиКлиент.ОткрытьФайл(ДанныеФайла, ДляРедактирования);
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура РазблокироватьРаботуСВнешнимиРесурсами(Команда)
	РазблокироватьРаботуСВнешнимиРесурсамиВызовСервера();
	Если Не ЗначениеЗаполнено(ТекущийФайл) Тогда
		Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО());
		Закрыть();
		Возврат;
	КонецЕсли;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("УникальныйИдентификатор", ИдентификаторРегламента);
	ДополнительныеПараметры.Вставить("ДляРедактирования", ДляРедактирования);
	
	ПараметрыДанныхФайла = РаботаСФайламиКлиентСервер.ПараметрыДанныхФайла();
	ПараметрыДанныхФайла.ИдентификаторФормы = ИдентификаторРегламента;
	ПараметрыДанныхФайла.ВызыватьИсключение = Ложь;
	ПараметрыДанныхФайла.ДляРедактирования = ДляРедактирования;
	
	ДлительнаяОперация = ИнтерфейсДокументовЭДОВызовСервера.ДанныеФайлаДлительнаяОперация(ТекущийФайл,
		ПараметрыДанныхФайла);
	
	ОповещениеОЗавершенииПолученияФайлаPDF = Новый ОписаниеОповещения("ОткрытьФайлЗавершение",
		ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьСообщения = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация,
		ОповещениеОЗавершенииПолученияФайлаPDF, ПараметрыОжидания);
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура РазблокироватьРаботуСВнешнимиРесурсамиВызовСервера()
	РегламентныеЗаданияСервер.РазблокироватьРаботуСВнешнимиРесурсами();
КонецПроцедуры

#КонецОбласти
