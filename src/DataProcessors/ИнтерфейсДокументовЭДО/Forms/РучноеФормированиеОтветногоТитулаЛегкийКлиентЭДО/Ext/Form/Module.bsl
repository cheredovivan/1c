#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДанныеФормы();
	
	НастроитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Элементы.ГруппаЗакупкаДляГосударственныхНужд.Видимость = Ложь;
	
	Элементы.ГруппаДокументОРасхождениях.Видимость = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		И Параметр <> Неопределено Тогда
		
		ОбработатьОповещение = Истина;
		
		ДанныеПроверки = ИнтерфейсДокументовЭДОКлиент.НовыеДанныеПроверкиОповещения();
		ДанныеПроверки.ЭлектронныйДокумент = ЭлектронныйДокумент;
		
		ИнтерфейсДокументовЭДОКлиент.ПриОбработкеОповещенияФормыПросмотраЭД(ДанныеПроверки, Параметр, 
			ОбработатьОповещение);
		
		Если ОбработатьОповещение Тогда
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СоставительДокументаПриИзменении(Элемент)
	
	СоставительДокументаПриИзмененииНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ЕстьДокументОРасхожденияхПриИзменении(Элемент)
	
	ЕстьДокументОРасхожденияхПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КодИтогаПриИзменении(Элемент)
	
	Если КодИтога = "2" Тогда
		ЕстьДокументОРасхождениях = Истина;
		Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях;
	Иначе
		ЕстьДокументОРасхождениях = Ложь;
		Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеСогласиеСДаннымиОтправителяПриИзменении(Элемент)
	
	Если ПолноеСогласиеСДаннымиОтправителя Тогда
		ЕстьРасхождения = Ложь;
		Элементы.ПодписатьИОтправить.Доступность = Истина;
	Иначе
		ЕстьРасхождения = Истина;
		Элементы.ПодписатьИОтправить.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	Если Не ПроверкаЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураОтвета = ИнтерфейсДокументовЭДОКлиентСервер.ДанныеРучногоФормированияТитула(ЭтотОбъект);
	
	ИнтерфейсДокументовЭДОКлиент.ПодписатьИОтправитьДокументИзФормыРучногоФормированияОтветногоТитула(
		ЭтотОбъект, СтруктураОтвета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьПодписание(Команда)
	
	ЛегкийИнтерфейсДокументовЭДОКлиент.ВыполнитьДействиеОтклонитьЭлектронныйДокумент(ЭлектронныйДокумент);

КонецПроцедуры

#КонецОбласти // ОбработчикиКомандФормы

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДанныхФормы

&НаСервере
Процедура ЗаполнитьДанныеФормы()
	
	ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(Параметры.АдресСведенийОбОшибках);
	
	ЭлектронныйДокумент = ОшибкиПриФормированииДокументов[0].ОшибкаФормированияВПрикладнойЧасти.ЭлектронныйДокумент;
	
	СвойстваДокумента = ЭлектронныеДокументыЭДО.СвойстваДокумента(ЭлектронныйДокумент, "Организация");
	Покупатель = СвойстваДокумента.Организация;
	СоставительДокумента = СвойстваДокумента.Организация;
	СоставительДокументаНаименование =
		ИнтерфейсДокументовЭДО.НаименованиеСоставителяДокументаДляРучногоФормированияОтветногоТитула(
			СоставительДокумента);
	
	ИспользуетсяНесколькоОрганизаций = ИнтеграцияЭДО.ИспользуетсяНесколькоОрганизаций();
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	Данные = ОбменСКонтрагентами.ДанныеЭлектронногоДокумента(ЭлектронныйДокумент);
	
	ФорматОтвета = ФорматыЭДО.ФорматОтветногоТитула(Данные.ДанныеОтправителя.Формат);
	Форматы = ЭлектронныеДокументыЭДО.ПоддерживаемыеФорматы();
	
	ЭтоУПД = ФорматОтвета = Форматы.ФНС.УПД2019.ИнформацияПокупателя;
	ЭтоУПД_5_02 = (ФорматОтвета = Форматы.ФНС.УПД_5_02.ИнформацияПокупателя
		Или ФорматОтвета = Форматы.ФНС.УПД_5_03.ИнформацияПокупателя);
	ЭтоДополнительныеСведения = ФорматОтвета = Форматы.ФНС.АктОРасхождениях.ДополнительныеСведения;
	ЭтоАктСверкиВзаиморасчетов = ФорматОтвета = Форматы.ФНС.АктСверкиВзаиморасчетов.ИнформацияПолучателя;
	ЭтоУКД = ФорматОтвета = Форматы.ФНС.УКД2020.ИнформацияПокупателя;
	
	ДеревоДанных = Данные.ДанныеОтправителя.Содержание;
	
	Если ЭтоУПД Или ЭтоУПД_5_02 Тогда
		
		ЗаполнитьДанныеФормыДляУПД(ДеревоДанных);
	
	ИначеЕсли ЭтоУКД Тогда
		
		ЗаполнитьДанныеФормыДляУКД(ДеревоДанных);
		
	ИначеЕсли ЭтоДополнительныеСведения Тогда
		
		ЗаполнитьДанныеФормыДляАктаОРасхождениях(ДеревоДанных);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыДляУПД(ДеревоДанных)

	ПутьТовары = "СведенияОТоварах";
	ПутьГосКонтракт = "ДополнительныеСведенияОбУчастниках.ЗакупкаДляГосударственныхНужд.НомерГосКонтракта";
	ПутьИдентификаторГосКонтракт = "ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта";
	
	Если ЭтоУПД_5_02 Тогда
		ПутьТовары = "ТаблицаСчетаФактуры.СведенияОбОтгруженныхПозициях";
		ПутьГосКонтракт =
			"СведенияОСчетеФактуре.ДополнительныеСведенияОбУчастниках.СведенияОГосКонтракте.НомерКонтракта";
		ПутьИдентификаторГосКонтракт =
			"СведенияОСчетеФактуре.ДополнительныеСведенияОбУчастниках.ИдентификаторГосКонтракта";
	КонецЕсли;
	
	ТаблицаТоваровЭД = ЭлектронноеВзаимодействие.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, ПутьТовары);
	
	Для Каждого Строка Из ТаблицаТоваровЭД Цикл
		СтрокаДенежныеОбязательства = ДенежныеОбязательства.Добавить();
		СтрокаДенежныеОбязательства.НомерСтрокиИнформацииПродавца = ТаблицаТоваровЭД.Индекс(Строка) + 1;
	КонецЦикла;
	
	НомерГосКонтракта = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
		ДеревоДанных, ПутьГосКонтракт);
	ИдентификаторГосКонтракта = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(
		ДеревоДанных, ПутьИдентификаторГосКонтракт);
	Если ЗначениеЗаполнено(НомерГосКонтракта) И ЗначениеЗаполнено(ИдентификаторГосКонтракта) Тогда
		ЗакупкаДляГосударственныхНужд = Истина;
	КонецЕсли;
	
	ПутьКПолюДатаДокумента = "ДатаДокумента";
	Если ЭтоУПД_5_02 Тогда
		ПутьКПолюДатаДокумента = "СведенияОСчетеФактуре.ДатаДокумента";
	КонецЕсли;
	ДатаПолученияТоваров = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных, ПутьКПолюДатаДокумента);
	
	КодИтога = "1";
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыДляУКД(ДеревоДанных)
	
	ДатаСогласования = ЭлектронноеВзаимодействие.ЗначениеРеквизитаВДереве(ДеревоДанных,
		"ДатаНаправленияНаСогласование");
	Если Не ЗначениеЗаполнено(ДатаСогласования) Тогда
		ДатаСогласования = ТекущаяДатаСеанса();
	КонецЕсли;
	
	СодержаниеОперации_УКД = НСтр("ru = 'С изменением стоимости согласен';
									|en = 'Price change is accepted'");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормыДляАктаОРасхождениях(ДеревоДанных)
	
	ФормированиеДополнительныхСведений = ЭлектронноеВзаимодействие.ДанныеЭлементаДереваЭлектронногоДокумента(
		ДеревоДанных, "ФормированиеДополнительныхСведений");
	ПредставленияВариантов =
		ИнтерфейсДокументовЭДОКлиентСервер.ПредставленияВариантовФормированияДополнительныхСведенийАктаОРасхождениях();
	ВариантФормированияДополнительныхСведений = ПредставленияВариантов[ФормированиеДополнительныхСведений];
	
КонецПроцедуры

#КонецОбласти // ЗаполнениеДанныхФормы

&НаКлиенте
Функция ПроверкаЗаполнения()
	
	СписокОшибок = Новый СписокЗначений();
	
	Если ЭтоУПД Тогда
		
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУПД2019ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
		
	ИначеЕсли ЭтоУПД_5_02 Тогда
		
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУПД_5_02ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
		
	ИначеЕсли ЭтоДополнительныеСведения Тогда
			
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеДополнительныхСведенийВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
		
	ИначеЕсли ЭтоАктСверкиВзаиморасчетов Тогда
		
		Возврат Истина;
		
	Иначе
		
		ИнтерфейсДокументовЭДОКлиент.ПроверитьЗаполнениеУКД2020ВФормеРучногоФормированияОтветногоТитула(
			ЭтотОбъект, СписокОшибок);
		
	КонецЕсли;
	
	Если СписокОшибок.Количество() > 0 Тогда
		Для Каждого Ошибка Из СписокОшибок Цикл
			ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибка.Значение,, Ошибка.Представление);
		КонецЦикла;
		
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура НастроитьФорму()
	
	Элементы.ГруппаСоставительДокумента.Видимость = ИспользуетсяНесколькоОрганизаций;
	Элементы.СоставительДокументаДоверенность.Видимость = Ложь;
	Элементы.ГруппаОснованиеСоставителя.Видимость = Ложь;
	Элементы.ГруппаДокументОРасхождениях.Видимость = Ложь;
	
	Если ЭтоМобильныйКлиент Тогда
		Элементы.ПодписатьИОтправить.Заголовок = НСтр("ru = 'Подписать';
														|en = 'Sign'");
	КонецЕсли;
	
	Если ЭтоУПД Или ЭтоУПД_5_02 Тогда
		
		Элементы.ГруппаСтраницыФорматов.ТекущаяСтраница = Элементы.ГруппаСтраницаУПД;
		Элементы.ГруппаСтраницаУПД.Видимость = Истина;
		Элементы.ГруппаСтраницаАктОРасхождениях.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктСверкиВзаиморасчетов.Видимость = Ложь;
		Элементы.ГруппаСтраницаУКД.Видимость = Ложь;
		Элементы.ОтклонитьПодписание.Видимость = Ложь;
		
		Элементы.ГруппаЗакупкаДляГосударственныхНужд.Видимость = ЗакупкаДляГосударственныхНужд;
		Если ЭтоУПД_5_02 Тогда
			Элементы.ДокументОРасхожденияхВид.Видимость = Ложь;
		КонецЕсли;
		
	ИначеЕсли ЭтоДополнительныеСведения Тогда
		
		Элементы.ГруппаСтраницыФорматов.ТекущаяСтраница = Элементы.ГруппаСтраницаАктОРасхождениях;
		Элементы.ГруппаСтраницаУПД.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктОРасхождениях.Видимость = Истина;
		Элементы.ГруппаСтраницаАктСверкиВзаиморасчетов.Видимость = Ложь;
		Элементы.ГруппаСтраницаУКД.Видимость = Ложь;
		Элементы.ОтклонитьПодписание.Видимость = Ложь;
		
		Элементы.ДекорацияНадписьАктОРасхожденияхПояснение.Заголовок = СтрШаблон("%1 '%2'",
			Элементы.ДекорацияНадписьАктОРасхожденияхПояснение.Заголовок,
			ВариантФормированияДополнительныхСведений);
	
	ИначеЕсли ЭтоАктСверкиВзаиморасчетов Тогда
		
		Элементы.ГруппаСтраницыФорматов.ТекущаяСтраница = Элементы.ГруппаСтраницаАктСверкиВзаиморасчетов;
		Элементы.ГруппаСтраницаУПД.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктОРасхождениях.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктСверкиВзаиморасчетов.Видимость = Истина;
		Элементы.ГруппаСтраницаУКД.Видимость = Ложь;
		Элементы.ОтклонитьПодписание.Видимость = Истина;
		
		Элементы.ПодписатьИОтправить.Доступность = Ложь;
	
	ИначеЕсли ЭтоУКД Тогда
		
		Элементы.ГруппаСтраницыФорматов.ТекущаяСтраница = Элементы.ГруппаСтраницаУКД;
		Элементы.ГруппаСтраницаУПД.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктОРасхождениях.Видимость = Ложь;
		Элементы.ГруппаСтраницаАктСверкиВзаиморасчетов.Видимость = Ложь;
		Элементы.ГруппаСтраницаУКД.Видимость = Истина;
		Элементы.ОтклонитьПодписание.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЕстьДокументОРасхожденияхПриИзмененииНаСервере()
	
	Элементы.ГруппаДокументОРасхождениях.Видимость = ЕстьДокументОРасхождениях; 
	
	Если ЕстьДокументОРасхождениях Тогда
		КодИтога = "2";
		Элементы.ЕстьДокументОРасхождениях.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветАкцента;
	Иначе
		КодИтога = "1";
		Элементы.ЕстьДокументОРасхождениях.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветТекстаФормы;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоставительДокументаПриИзмененииНаСервере()
	
	ЭлементОснования = Элементы.СоставительДокументаДоверенность;
	Если ЭтоУПД_5_02 Тогда
		ЭлементОснования = Элементы.ГруппаОснованиеСоставителя;
	КонецЕсли;
	
	Если СоставительДокумента <> Покупатель Тогда
		ЭлементОснования.Видимость = Истина;
	Иначе
		ЭлементОснования.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СоставительДокумента) Тогда
		СоставительДокументаНаименование = 
			ИнтерфейсДокументовЭДО.НаименованиеСоставителяДокументаДляРучногоФормированияОтветногоТитула(
				СоставительДокумента);
	Иначе
		СоставительДокументаНаименование = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныеПроцедурыИФункции
