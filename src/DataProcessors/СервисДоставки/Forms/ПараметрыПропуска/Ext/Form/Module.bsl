
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ДанныеПропуска);
	СервисДоставки.ЗаполнитьСписокВыбораЗаказатьЗа(Элементы.ВремяЗаказа.СписокВыбора);
	СервисДоставки.ЗаполнитьСписокВыбораВозмещениеОплаты(Элементы.ПропускВозмещениеОплаты.СписокВыбора);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗаказыватьПропускПриИзменении(Элемент)
	УстановитьДоступность();
	Если ПропускЗаказывать И ПропускВремяЗаказа = 0 Тогда
		ПропускВремяЗаказа = 1;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СтоимостьПриИзменении(Элемент)
	Если ПропускСтоимость > 0 И ПропускВозмещениеОплаты = 0 Тогда
		ПропускВозмещениеОплаты = 1;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	СообщениеОбОшибке = "";
	Если Не ПараметрыПропускаЗаполненыВерно(СообщениеОбОшибке) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке);
		Возврат;
	КонецЕсли;
	ДанныеПропуска = Новый Структура;
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(СервисДоставкиКлиентСервер.НовыйПараметрыПропуска(), ДанныеПропуска, "Пропуск");
	ЗаполнитьЗначенияСвойств(ДанныеПропуска, ЭтотОбъект);
	Закрыть(ДанныеПропуска);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.ВремяЗаказа.Доступность = ПропускЗаказывать;
	Элементы.Стоимость.Доступность = ПропускЗаказывать;
	Элементы.ПропускВозмещениеОплаты.Доступность = ПропускЗаказывать;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыПропускаЗаполненыВерно(СообщениеОбОшибке)
	Результат = Истина;
	Если Не ПропускЗаказывать Тогда
		Возврат Результат;
	КонецЕсли;
	ТекстыОшибки = Новый Массив;
	ТекстыОшибки.Добавить(НСтр("ru = 'При заказе пропуска необходимо заполнить';
								|en = 'When ordering a pass certificate, fill in the'"));
	Если ПропускВремяЗаказа < 1 Тогда
		Результат = Ложь;
		ТекстыОшибки.Добавить(НСтр("ru = 'время заказа пропуска';
									|en = 'pass certificate order time'"));
	КонецЕсли;
	Если ПропускСтоимость > 0 И ПропускВозмещениеОплаты < 1 Тогда
		Результат = Ложь;
		Если ТекстыОшибки.Количество() > 1 Тогда
			ТекстыОшибки.Добавить(НСтр("ru = 'и';
										|en = 'and'"));
		КонецЕсли;
		ТекстыОшибки.Добавить(НСтр("ru = 'вариант возмещения оплаты';
									|en = 'refund option'"));
	КонецЕсли;
	
	Если Не Результат Тогда
		СообщениеОбОшибке = СтрСоединить(ТекстыОшибки, " ");
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецОбласти