#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СервисДоставки.УстановитьЗначенияРеквизитовПоПараметрам(ЭтотОбъект, Параметры);
	ОрганизацияБизнесСетиСсылка = Параметры.ОрганизацияБизнесСети;
	
	Если Не ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран тип грузоперевозки';
													|en = 'Cargo transportation type is not selected'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоПараметрам();
	ВыполнитьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(Кэш) = Тип("Структура") 
		И ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций.ФоновоеЗадание) Тогда
		ОжидатьЗавершениеВыполненияЗапроса(Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УказыватьАртикулыПриИзменении(Элемент)
	Элементы.АртикулыГрузомест.Видимость = УказыватьАртикулы;
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыАртикулыГрузомест

&НаКлиенте
Процедура АртикулыГрузоместПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ЗаполненоМест = АртикулыГрузомест.Итог("Количество");
КонецПроцедуры

&НаКлиенте
Процедура АртикулыГрузоместПослеУдаления(Элемент)
	ЗаполненоМест = АртикулыГрузомест.Итог("Количество");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ПараметрыГенерацииЗаполненыВерно() Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон(НСтр("ru = 'Общее количество мест в таблице %1 не совпадает с количеством мест в заказе %2';
							|en = 'The total number of cargo units in the ""%1"" table does not match the number of cargo units in the ""%2"" order'"), 
			ЗаполненоМест, ПараметрыЗаказа.КоличествоГрузовыхМест));
		Возврат;
	КонецЕсли;
	
	ОписаниеПараметров = ЗаполнитьПараметрыРезультата();
	
	Закрыть(ОписаниеПараметров);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьФормуПоПараметрам()
	Заголовок = СтрШаблон(НСтр("ru = '1С: Доставка. Параметры печатной формы %1';
								|en = '1C:Delivery. The ""%1"" print form parameters'"), ИмяПечатнойФормы);
	
	Элементы.ПараметрЗначение.Заголовок = НаименованиеПараметра;
	ЗаполнитьСписокВыбораЗначенийПараметра();
	
	ЭтоЗаказДеловыхЛиний = СервисДоставкиКлиентСервер.ЭтоДеловыеЛинии(ТипГрузоперевозки) 
		И ЗначениеЗаполнено(ПараметрыЗаказа);
	ПроверятьНеобходимостьГенерации = ЭтоЗаказДеловыхЛиний И Не ЗначениеЗаполнено(ПараметрыГенерации);
	ПараметрыГенерацииДоступны = ЭтоЗаказДеловыхЛиний И ЗначениеЗаполнено(ПараметрыГенерации);
	
	Элементы.ГруппаОжидания.Видимость = ПроверятьНеобходимостьГенерации;
	Элементы.ГруппаПараметрыГенерации.Видимость = ПараметрыГенерацииДоступны;
	
	УказыватьАртикулы = ПараметрыГенерацииДоступны;
	Элементы.АртикулыГрузомест.Видимость = УказыватьАртикулы;
	
	Если УказыватьАртикулы Тогда
		Для Каждого ОписаниеПараметра Из ПараметрыГенерации Цикл
			НоваяСтрока = АртикулыГрузомест.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеПараметра.Значение);
		КонецЦикла;
		ЗаполненоМест = АртикулыГрузомест.Итог("Количество");
	КонецЕсли;
	Если ЭтоЗаказДеловыхЛиний Тогда
		Элементы.АртикулыГрузомест.Подсказка = 
			СтрШаблон(Элементы.АртикулыГрузомест.Подсказка, ПараметрыЗаказа.КоличествоГрузовыхМест);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораЗначенийПараметра()
	СписокВыбора = Элементы.ПараметрЗначение.СписокВыбора;
	СписокВыбора.Очистить();
	
	Для Каждого Элемент Из ДоступныеЗначения Цикл
		СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыГенерацииЗаполненыВерно()
	Если Не Элементы.ГруппаПараметрыГенерации.Видимость 
		Или Не УказыватьАртикулы Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат ЗаполненоМест = ПараметрыЗаказа.КоличествоГрузовыхМест;  
КонецФункции

&НаКлиенте
Функция ЗаполнитьПараметрыРезультата()

	Результат = Новый Структура("ПараметрПредставление, ПараметрЗначение, ПараметрыГенерации");
	
	Результат.ПараметрЗначение = ПараметрЗначение;
	
	ЭлементСписка = ДоступныеЗначения.НайтиПоЗначению(ПараметрЗначение);
	Если ЭлементСписка <> Неопределено И Не ПустаяСтрока(ЭлементСписка.Представление) Тогда
		ПараметрПредставление = ЭлементСписка.Представление;
	Иначе
		ПараметрПредставление = Строка(ПараметрЗначение);
	КонецЕсли;
	ПредставлениеРезультата = СтрШаблон("%1: %2", НаименованиеПараметра, ПараметрПредставление);
	
	Если Элементы.ГруппаПараметрыГенерации.Видимость Тогда
		Результат.ПараметрыГенерации = ВыгрузитьАртикулы();
		
		Если УказыватьАртикулы Тогда
			ПредставлениеРезультата = СтрШаблон("%1, %2", ПредставлениеРезультата, НСтр("ru = 'Указаны артикулы';
																						|en = 'Item IDs are specified'"));
		КонецЕсли;
	КонецЕсли;
	
	Результат.ПараметрПредставление = ПредставлениеРезультата;
	
	Возврат Результат
КонецФункции

&НаКлиенте
Функция ВыгрузитьАртикулы() 
	Результат = Новый СписокЗначений();
	Если Не УказыватьАртикулы Тогда
		Возврат Результат;
	КонецЕсли;

	Для Каждого Параметр Из АртикулыГрузомест Цикл
		Результат.Добавить(Новый Структура("Наименование, Количество", Параметр.Наименование, Параметр.Количество));
	КонецЦикла;
	Возврат Результат;
		
КонецФункции

#Область ДлительныеОперации

&НаСервере
Процедура ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не СервисДоставки.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ПараметрыЗапроса = ПараметрыЗапроса(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ЗапуститьФоновоеЗадание(ЭтотОбъект, ПараметрыОперации, ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации)
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		Элементы.ДекорацияОжидание.Видимость = Истина;
		Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Идет проверка создания формы...';
													|en = 'Checking whether the form is created...'");
		
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(ПараметрыОперации.ИмяПроцедурыЗавершения, ЭтотОбъект, ПараметрыОперации);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыОжидания, ПараметрыОперации);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры)
	
	Очередь = ДополнительныеПараметры.Очередь;
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьФайлыПечатныхФорм() Тогда
			
			Элементы.ДекорацияОжидание.Видимость = Ложь;
			Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Обработка результата...';
														|en = 'Result processing...'");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры);
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	СервисДоставкиКлиент.ОбработатьРезультатФоновогоЗадания(Результат, ДополнительныеПараметры, Отказ);
	
	Если Отказ Или Результат.Статус <> "Выполнено" Или Не ЗначениеЗаполнено(Результат.АдресРезультата)
		Или Не ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата);
	Иначе
		РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		ОперацияВыполнена = Истина;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьФайлыПечатныхФорм() Тогда
			
			ЗагрузитьРезультатЗапросаПолучитьФайлыПечатныхФорм(РезультатОперации, ОперацияВыполнена);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры)
	
	Очередь = ДополнительныеПараметры.Очередь;
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьФайлыПечатныхФорм() Тогда
			
			Элементы.ДекорацияСостояние.Заголовок = "";
			
		КонецЕсли;
		
	КонецЦикла;
	
	СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

#Область ИнициализацияДлительныхОпераций

&НаСервере
Процедура ВыполнитьДлительныеОперации()
	
	Если Не ПроверятьНеобходимостьГенерации Тогда
		Элементы.ГруппаОжидания.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Проверка необходимости генерации.';
											|en = 'Check whether generation is required.'");
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьФайлыПечатныхФорм());
	
	ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	Кэш = Новый Структура("ПараметрыМенеджераДлительныхОпераций", ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Функция ПараметрыЗапроса(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = Новый Структура;
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		ИмяМетода = Операция.Представление;
	
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьФайлыПечатныхФорм() Тогда
			Операция.Значение = ПараметрыЗапросаПолучитьФайлыПечатныхФорм(ПараметрыОперации, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыЗапроса.Вставить("Очередь", ПараметрыОперации.Очередь);
	ПараметрыЗапроса.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьФайлыПечатныхФорм(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаЗагрузитьФайлыПечатныхФорм();
	
	ЭлементПараметраЗапроса = СервисДоставки.НовыйЭлементПараметровЗапросаЗагрузитьФайлыПечатныхФорм();
	ЗаполнитьЗначенияСвойств(ЭлементПараметраЗапроса, ЭтотОбъект);
	ЭлементПараметраЗапроса.ИдентификаторЗаказа = ПараметрыЗаказа.ИдентификаторЗаказа;
	
	ПараметрыФормы = СервисДоставки.НовыйПечатнаяФормаДляЗапроса();
	ПараметрыФормы.ИдентификаторДокумента = ПараметрыЗаказа.ИдентификаторЗаказа;
	ПараметрыФормы.ИдентификаторПечатнойФормы = ИдентификаторПечатнойФормы;
	ДанныеДоступногоПараметра = Новый Структура;
	ДанныеДоступногоПараметра.Вставить("Наименование", НаименованиеПараметра);
	ДанныеДоступногоПараметра.Вставить("Идентификатор", ПараметрЗначение);
	ПараметрыФормы.ДоступныеПараметры.Добавить(ДанныеДоступногоПараметра);
	ПараметрыФормы.ПроверитьГенерацию = Истина;
		
	ЭлементПараметраЗапроса.Список.Добавить(ПараметрыФормы);
	ПараметрыЗапроса.Параметры.Добавить(ЭлементПараметраЗапроса);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

#КонецОбласти

#Область РезультатыДлительныхОпераций

// Обрабатывает результат менеджера длительных операций на сервере.
// Для этого в параметрах выполняемой операции д.б. установлен флаг ОбработкаРезультатаНаСервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки
&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(АдресРезультата)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	// Метод добавлен в рамках единого подхода к обработке результатов 
	Возврат РезультатМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьРезультатЗапросаПолучитьФайлыПечатныхФорм(Результат, ОперацияВыполнена)
	
	Если ЗначениеЗаполнено(Результат) И ТипЗнч(Результат) = Тип("Структура") Тогда
		ПараметрыГенерацииДоступны = ТипЗнч(Результат.Список) = Тип("Массив") И Результат.Список.Количество() = 0;
	Иначе
		ПараметрыГенерацииДоступны = Ложь;
	КонецЕсли;
	Элементы.ГруппаПараметрыГенерации.Видимость = ПараметрыГенерацииДоступны;
	Элементы.ГруппаОжидания.Видимость = ПараметрыГенерацииДоступны;
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти