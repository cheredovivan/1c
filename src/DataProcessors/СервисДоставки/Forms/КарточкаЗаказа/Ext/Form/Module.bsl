#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СервисДоставки.УстановитьЗначенияРеквизитовПоПараметрам(ЭтотОбъект, Параметры);
	ОрганизацияБизнесСетиСсылка = Параметры.ОрганизацияБизнесСети;
	ТипГрузоперевозки = СервисДоставкиКлиентСервер.ЧисловойИдентификаторГрузоперевозчика(Параметры.ТипГрузоперевозки);
	
	Если СервисДоставки.НедоступнаРаботаСФормой(ЭтотОбъект, Отказ, ДоступнаОтправкаЗаказовНаДоставку) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РежимМастера) И Не ЗначениеЗаполнено(ИдентификаторЗаказа) Тогда
		ТекстСообщения = НСтр("ru = 'Идентификатор заказа не указан. Работа с заказом на доставку невозможна.';
								|en = 'Order identifier is not specified. Work with delivery order is not possible.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ОрганизацияБизнесСетиСсылка);
		Возврат;
	КонецЕсли;
	
	УстановитьКэшированныеЗначения();
	ЗаполнитьСпискиВыбора();
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
		
		КлючИдемпотентности = Новый УникальныйИдентификатор;
		
		Если Параметры.ПараметрыЗаказа <> Неопределено Тогда
			ПараметрыЗаказа = Параметры.ПараметрыЗаказа;
			ПараметрыЗаказа.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
		ИначеЕсли ДокументыОснования.Количество() Тогда
			ПараметрыЗаказа = ПараметрыЗаказаНаДоставкуПоДокументамОснованиям(ТипГрузоперевозки, ДокументыОснования);
		ИначеЕсли Кэш.ЭтоЯндексДоставка Или ЭтоЗаполнениеКопированием Тогда
			ПараметрыЗаказа = ПараметрыЗаказаНаДоставкуПоУмолчанию(ТипГрузоперевозки);
		Иначе
			ПараметрыШаблона = Параметры.ПараметрыШаблона;
			ПараметрыЗаказа = СервисДоставки.ПолучитьПараметрыЗаказаИзШаблона(ПараметрыШаблона);
			Если ПараметрыЗаказа = Неопределено Тогда
				ПараметрыЗаказа = СервисДоставки.ПараметрыШаблонаПоУмолчанию(ТипГрузоперевозки);
			КонецЕсли;
			Если ПараметрыЗаказа = Неопределено Тогда
				ПараметрыЗаказа = ПараметрыЗаказаНаДоставкуПоУмолчанию(ТипГрузоперевозки);
			Иначе
				ЭтоЗаполнениеШаблоном = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОрганизацияБизнесСетиСсылка) Тогда
			ПараметрыЗаказа.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
		КонецЕсли;
		
		ОбработатьПараметры(ПараметрыЗаказа);
		ОбработатьПредставлениеАдресаПриНеобходимости();
		ПропускПредставление = СервисДоставки.ПредставлениеПропуска(Новый Структура);
		ПлательщикиУслугИзменены = Истина;
		
		Если ЗначениеЗаполнено(ДатаОтгрузки)
			И ДатаОтгрузки < НачалоДня(ТекущаяДатаСеанса()) Тогда
			ДатаОтгрузки = НачалоДня(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаДоставки)
			И ДатаДоставки < НачалоДня(ТекущаяДатаСеанса()) Тогда
			ДатаДоставки = НачалоДня(ТекущаяДатаСеанса());
		КонецЕсли;
		
		Если ПараметрыЗаказа.Свойство("ТоварныйСостав") Тогда
			Для Каждого ТекСтрока Из ПараметрыЗаказа.ТоварныйСостав Цикл
				НоваяСтрока = ТоварныйСостав.Добавить();
				Если НоваяСтрока.СтавкаНДСПредставление = "" Тогда
					НоваяСтрока.СтавкаНДСПредставление = НоваяСтрока.СтавкаНДС;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ПервоначальнаяНастройкаФормы();
	СформироватьПредставлениеДокументаОснования();
	УстановитьВидимостьДоступность();
	СформироватьЗаголовокФормы();
	СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформации();
	УстановитьУсловноеОформление();
	ВыполнитьДлительныеОперации();
	
	Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторЗаказа) И Не ЭтоЗаполнениеКопированием Тогда
		
		ЗаписьЖурнала = РегистрыСведений.ДанныеЗаказовСервисДоставки.СоздатьНаборЗаписей();
		ЗаписьЖурнала.Отбор.Организация.Установить(ОрганизацияБизнесСетиСсылка);
		ЗаписьЖурнала.Отбор.УдалитьИдентификаторДокумента.Установить(ИдентификаторЗаказа);
		ЗаписьЖурнала.Прочитать();
		
		ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЗаписьЖурнала, ЭтотОбъект);
		
		Если ТолькоПросмотр Тогда
			ДоступныеДляИзмененияРеквизитыТолькоПросмотр(Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		ОжидатьЗавершениеВыполненияЗапроса(Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.ПараметрыМенеджераДлительныхОпераций = Неопределено;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьНаСтраницеОсновная();
	УстановитьВидимостьДоступностьНаСтраницеПараметровГруза();
	УстановитьВидимостьДоступностьНаСтраницеПараметрыТарифа();
	УстановитьВидимостьДоступностьНаСтраницеКарточки();
	УстановитьВидимостьДоступностьНаСтраницеТарифы();
	УстановитьКомандыПересчетаПараметровГруза();
	ОбновитьФормуДляДокументаОснования();
	СформироватьПредставлениеТарифаИГрузоперевозчика();
	
	ЭтоЗаполнениеШаблоном = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект),
			НСтр("ru = 'Данные были изменены. Сохранить изменения?';
				|en = 'Data has changed. Save the changes?'"),
			РежимДиалогаВопрос.ДаНетОтмена);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область СтраницаОсновное

&НаКлиенте
Процедура МультизаказПредставлениеНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ОткрытьФормуМультизаказа();
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументыОснованияПредставлениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылка, 
	СтандартнаяОбработка)

	РежимИзмененияСоставаОснований = 0;
	Если НавигационнаяСсылка = "ДобавитьДокументыОснования" Тогда
		СтандартнаяОбработка = Ложь;
		РежимИзмененияСоставаОснований = 1;
		ОткрытьФормуВыбораДокументаОснования();
	ИначеЕсли НавигационнаяСсылка = "ИзменитьДокументыОснования" Тогда
		РежимИзмененияСоставаОснований = 2;
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуВыбораДокументаОснования();
	ИначеЕсли НавигационнаяСсылка = "ОчиститьДокументыОснования" Тогда
		СтандартнаяОбработка = Ложь;
		ДокументыОснования.Очистить();
		НаложенныйПлатежВидОплаты = 0;
		СформироватьПредставлениеДокументаОснования();
		УстановитьВидимостьДоступностьНаложенныйПлатеж();
		Модифицированность = Истина;
	ИначеЕсли НавигационнаяСсылка = "ОткрытьФормуОснования" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОснования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ОтправительКонтрагентЭтоОрганизация Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	Иначе
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	Если ПлательщикКонтрагентЭтоОрганизация Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	Иначе
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ПолучательКонтрагентЭтоОрганизация Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	Иначе
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = 1 Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	ИначеЕсли ВыбранноеЗначение = 2 Тогда
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = 1 Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	ИначеЕсли ВыбранноеЗначение = 2 Тогда
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ВыбранноеЗначение = 1 Тогда
		ОткрытьФормуВыбора("ОрганизацияСервисДоставки", Элемент.Имя + "Ссылка");
	ИначеЕсли ВыбранноеЗначение = 2 Тогда
		ОткрытьФормуВыбора("КонтрагентСервисДоставки", Элемент.Имя + "Ссылка");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КонтрагентОткрытие(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтрагентОткрытие(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	
	КонтрагентОткрытие(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	КонтрагентОткрытие(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтрагентОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОтбора = Новый Структура(); 
	
	Если ОтправительКонтрагентЭтоОрганизация Тогда
		ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора, ОтправительКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоОрганизацииСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	Иначе
		ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора, ОтправительКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоКонтрагентаСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	ПараметрыОтбора = Новый Структура(); 
	
	Если ПлательщикКонтрагентЭтоОрганизация Тогда
		ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора, ПлательщикКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоОрганизацииСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	Иначе
		ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора, ПлательщикКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоКонтрагентаСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОтбора = Новый Структура(); 
	
	Если ПолучательКонтрагентЭтоОрганизация Тогда
		ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора, ПолучательКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоОрганизацииСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	Иначе
		ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора, ПолучательКонтрагентСсылка);
		ОткрытьФормуВыбора("КонтактноеЛицоКонтрагентаСервисДоставки", Элемент.Имя + "Ссылка", ПараметрыОтбора);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоОчистка(Элемент, СтандартнаяОбработка)
	ОчиститьРеквизит(Элемент, СтандартнаяОбработка);
КонецПроцедуры

 &НаКлиенте
 Процедура НаложенныйПлатежВидОплатыПриИзменении(Элемент)
 	
 	УстановитьВидимостьДоступностьНаложенныйПлатеж()
 	
 КонецПроцедуры

&НаКлиенте
Процедура ВремяОтгрузкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораВремениИДаты(1, Элемент.ТолькоПросмотр);
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяДоставкиОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуВыбораВремениИДаты(2, Элемент.ТолькоПросмотр);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонПриИзменении(Элемент)
	
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ОтправительКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ОтправительКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ОтправительКонтактноеЛицоТелефон.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ОтправительКонтактноеЛицоТелефонЗначение = ПоискЗначения.Значение;
			ОтправительКонтактноеЛицоТелефонПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонДополнительныйПриИзменении(Элемент)
	
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонДополнительныйОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ОтправительКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительКонтактноеЛицоТелефонДополнительныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ОтправительКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ОтправительКонтактноеЛицоТелефонДополнительный.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ОтправительКонтактноеЛицоТелефонДополнительныйЗначение = ПоискЗначения.Значение;
			ОтправительКонтактноеЛицоТелефонДополнительныйПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонПриИзменении(Элемент)
	
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ПолучательКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ПолучательКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ПолучательКонтактноеЛицоТелефон.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ПолучательКонтактноеЛицоТелефонЗначение = ПоискЗначения.Значение;
			ПолучательКонтактноеЛицоТелефонПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонДополнительныйПриИзменении(Элемент)
	
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонДополнительныйОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ПолучательКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоТелефонДополнительныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ПолучательКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ПолучательКонтактноеЛицоТелефонДополнительный.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ПолучательКонтактноеЛицоТелефонДополнительныйЗначение = ПоискЗначения.Значение;
			ПолучательКонтактноеЛицоТелефонДополнительныйПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательКонтактноеЛицоEmailПриИзменении(Элемент)
	
	Модифицированность = Истина;
	Отказ = Ложь;
	ПроверитьПредставлениеКонтактнойИнформации(Элемент.Имя, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонПриИзменении(Элемент)
		
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");

КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонОткрытие(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ПлательщикКонтрагентЭтоОрганизация);

КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ПлательщикКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ПлательщикКонтактноеЛицоТелефон.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ПлательщикКонтактноеЛицоТелефонЗначение = ПоискЗначения.Значение;
			ПлательщикКонтактноеЛицоТелефонПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонДополнительныйПриИзменении(Элемент)
	
	ОбработатьИзменениеКонтактнойИнформации(Элемент, "Телефон");
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонДополнительныйОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТелефонНачалоВыбора(Элемент, ПлательщикКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикКонтактноеЛицоТелефонДополнительныйОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение = "ПроизвольныйТелефон" Тогда
		
		СтандартнаяОбработка = Ложь;
		ТелефонНачалоВыбора(Элемент, ПлательщикКонтрагентЭтоОрганизация);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;
		
		ПоискЗначения = Элементы.ПлательщикКонтактноеЛицоТелефонДополнительный.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
		Если ПоискЗначения <> Неопределено Тогда
			
			ПлательщикКонтактноеЛицоТелефонДополнительныйЗначение = ПоискЗначения.Значение;
			ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление = ПредставлениеКонтактнойИнформации(ПоискЗначения.Значение);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПолучательКонтрагентНажатие(Элемент)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ОбработатьИзменениеРеквизитаФормыНаСервере("ПолучательКонтрагентСсылка", Отказ);
	ЗаполнитьПолучателяПоОтправителю();
	УстановитьВидимостьОнЖе();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительАдресОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресНачалоВыбора(Элемент, ОтправительКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикАдресОткрытие(Элемент, СтандартнаяОбработка)
		
	СтандартнаяОбработка = Ложь;
	АдресНачалоВыбора(Элемент, ПлательщикКонтрагентЭтоОрганизация);

КонецПроцедуры

&НаКлиенте
Процедура ПолучательАдресОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	АдресНачалоВыбора(Элемент, ПолучательКонтрагентЭтоОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительДатаПлановойОтгрузкиПриИзменении(Элемент)
	ОчиститьСообщения();
	
	УстановитьДатуОтгрузки(ДатаОтгрузки);
	
	Если Кэш.ЭтоДеловыеЛинии Тогда
		ЕстьОшибки = Ложь;
		ПроверитьВремяОтгрузки(ЕстьОшибки, Истина);
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов();
КонецПроцедуры

&НаКлиенте
Процедура ВремяОтгрузкиОчистка(Элемент, СтандартнаяОбработка)
	
	ВремяОтгрузкиС = Неопределено;
	ВремяОтгрузкиПо =  Неопределено;
	ВремяОтгрузкиОбедС = Неопределено;
	ВремяОтгрузкиОбедПо = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяДоставкиОчистка(Элемент, СтандартнаяОбработка)
	
	ВремяДоставкиС = Неопределено;
	ВремяДоставкиПо =  Неопределено;
	ВремяДоставкиОбедС = Неопределено;
	ВремяДоставкиОбедПо = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикРольОчистка(Элемент, СтандартнаяОбработка)
	
	ПлательщикРоль = 0;
	ПлательщикРольПредставление = "";
	ПлательщикКонтрагентСсылка = Неопределено;
	ОчиститьРеквизит(Элементы.ПлательщикКонтрагент, Ложь);
	УстановитьВидимостьДоступностьНаСтраницеОсновная();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикРольОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ЗаказчикРольПриИзменении(Элемент)
	ОбработатьПрименениеОтборов(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ВариантВремениОтгрузкиПриИзменении(Элемент)
	
	ОбновитьИнформациюПоВремениОтгрузки(ОбщегоНазначенияКлиент.ДатаСеанса());
	ВариантВремениОтгрузкиПриИзмененииНаСервере();
	ЗаполнитьСписокВыбораПериодовОтгрузки(Истина);
	ОбработатьПрименениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОтгрузкиПриИзменении(Элемент)
	
	УстановитьДатуОтгрузки(ДатаОтгрузки);
	ЗаполнитьСписокВыбораПериодовОтгрузки(Истина);
	ОбработатьПрименениеОтборов(Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодОтгрузкиПриИзменении(Элемент)
	
	ИзменитьПериодОтгрузки();
	ОбработатьПрименениеОтборов(Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВремяОтгрузкиКПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ЕстьОшибки = Ложь;
	ПроверитьВремяОтгрузки(ЕстьОшибки,Истина);
	ОбновитьИнформациюПоВремениОтгрузки();
	ОбработатьПрименениеОтборов(Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикРольОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПлательщикРольОбработкаВыбораНаКлиенте(ВыбранноеЗначение);

КонецПроцедуры

&НаКлиенте
Процедура ОтправительДобавитьДополнительныйТелефоныНажатие(Элемент)
	
	ОтправительКонтактноеЛицоТелефонДополнительныйДоступен = Истина;
	УстановитьВидимостьДополнительныхТелефонов("Отправитель");
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательДобавитьДополнительныйТелефоныНажатие(Элемент)
	
	ПолучательКонтактноеЛицоТелефонДополнительныйДоступен = Истина;
	УстановитьВидимостьДополнительныхТелефонов("Получатель");
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикДобавитьДополнительныйТелефоныНажатие(Элемент)
	
	ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен = Истина;
	УстановитьВидимостьДополнительныхТелефонов("Плательщик");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительУдалитьДополнительныйТелефонНажатие(Элемент)
	
	ОтправительКонтактноеЛицоТелефонДополнительныйЗначение = "";
	ОтправительКонтактноеЛицоТелефонДополнительныйПредставление = "";

	ОтправительКонтактноеЛицоТелефонДополнительныйДоступен = Ложь;
	УстановитьВидимостьДополнительныхТелефонов("Отправитель");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцедураПолучательУдалитьДополнительныйТелефонНажатие(Элемент)
	
	ПолучательКонтактноеЛицоТелефонДополнительныйЗначение = "";
	ПолучательКонтактноеЛицоТелефонДополнительныйПредставление = "";
	
	ПолучательКонтактноеЛицоТелефонДополнительныйДоступен = Ложь;
	УстановитьВидимостьДополнительныхТелефонов("Получатель");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроцедураПлательщикУдалитьДополнительныйТелефонНажатие(Элемент)
	
	ПлательщикКонтактноеЛицоТелефонДополнительныйЗначение = "";
	ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление = "";
	
	ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен = Ложь;
	УстановитьВидимостьДополнительныхТелефонов("Плательщик");
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительАдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикАдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательАдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательАдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительАдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикАдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПропускПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ПропускНажатиеЗавершение", ЭтотОбъект);
	ДанныеПропуска = Новый Структура;
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(СервисДоставкиКлиентСервер.НовыйПараметрыПропуска(), ДанныеПропуска, "Пропуск");
	ЗаполнитьЗначенияСвойств(ДанныеПропуска, ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ДанныеПропуска", ДанныеПропуска);
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ПараметрыПропуска", ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПромокодПриИзменении(Элемент)
	ЗарегистрироватьИзменениеОтборов();
КонецПроцедуры

#КонецОбласти

#Область СтраницаПараметрыГруза

&НаКлиенте
Процедура Подключаемый_ГрузСодержимоеНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытия.Вставить("ОрганизацияБизнесСети", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытия.Вставить("ХарактерГрузаИдентификатор", ГрузСодержимоеИдентификатор);
	ПараметрыОткрытия.Вставить("ХарактерГрузаСтрокой", ГрузСодержимое);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.СписокХарактеровГруза", ПараметрыОткрытия, Элемент, , , ,
		 , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ГрузСодержимоеОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ГрузСодержимое = ВыбранноеЗначение.Наименование;
	ГрузСодержимоеИдентификатор = ВыбранноеЗначение.Идентификатор;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ГрузСодержимоеАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрДлина(Текст) < 3 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	Результат = ПолучитьХарактерыГрузаАвтоподбор(Текст); // СписокЗначений
	Если Результат <> Неопределено Тогда
		ОбработатьХарактерыГрузаАвтоподбор(Элемент, Результат);
		ДанныеВыбора = Результат;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ГрузСодержимоеПриИзменении(Элемент)

	Если ЗначениеЗаполнено(ГрузСодержимое) Тогда

		Если Не ЗначениеЗаполнено(Кэш.ОписаниеХарактеровГруза) Тогда
			ПараметрыКэш = Новый Структура("ОрганизацияБизнесСетиСсылка, ТипГрузоперевозки", 
				ОрганизацияБизнесСетиСсылка, ТипГрузоперевозки);
			ПолучитьИСохранитьВКэшСписокХарактеровГрузаНаСервере(Кэш, ПараметрыКэш);
		КонецЕсли;

		Если ЗначениеЗаполнено(ГрузСодержимоеИдентификатор) Тогда
			// вернем описание, соответствующее идентификатору, если ввели некорректные данные
			СодержимоеГрузаПоКлассификатору = Кэш.ОписаниеХарактеровГруза.Получить(ГрузСодержимоеИдентификатор);
			Если ГрузСодержимое <> СодержимоеГрузаПоКлассификатору Тогда
				ГрузСодержимое = СодержимоеГрузаПоКлассификатору;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ГрузСодержимоеИдентификатор = "";
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПереключательРасшифровкиПриИзменении(Элемент)
	
	УстановитьВидимостьДоступностьГруппыВГХ();
	
	ГрузКоличествоГрузовыхМестДо = ГрузКоличествоГрузовыхМест;
	ГрузКоличествоГрузовыхМест = ?(ПереключательРасшифровки = 0, 1, 0);
	
	ГрузОбъемДо = ГрузОбъем;
	РассчитатьОбъем();
	РассчитатьВес();
	
	Если ГрузКоличествоГрузовыхМестДо <> ГрузКоличествоГрузовыхМест 
		ИЛИ ГрузОбъемДо <> ГрузОбъем 
		ИЛИ ГрузВес <> ГрузМаксимальныйВес Тогда
		ЗарегистрироватьИзменениеОтборов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОдноМестоВесПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ГрузМаксимальныйВес = ГрузВес;
	ПроверитьВГХ("ГрузВес");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаОбъявленнойЦенностиПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ПроверитьВГХ("ГрузСтоимость");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОдноМестоКоличествоГрузовыхМестПриИзменении(Элемент)
	
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузКоличествоГрузовыхМестПриИзменении(Элемент)
	
	РассчитатьОбъем();
	РассчитатьВес();
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОбщийВесПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ПроверитьВГХ("ГрузВес");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОбщийОбъемПриИзменении(Элемент)
	
	ОчиститьСообщения();
	ПроверитьВГХ("ГрузОбъем");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОдноМестоВысотаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	ПроверитьВГХ("ГрузМаксимальнаяВысота");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОдноМестоШиринаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	ПроверитьВГХ("ГрузМаксимальнаяШирина");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузОдноМестоДлинаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	ПроверитьВГХ("ГрузМаксимальнаяДлина");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузМаксимальнаяВысотаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	РассчитатьВес();
	ПроверитьВГХ("ГрузМаксимальнаяВысота");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузМаксимальнаяШиринаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	РассчитатьВес();
	ПроверитьВГХ("ГрузМаксимальнаяШирина");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузМаксимальнаяДлинаПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	РассчитатьВес();
	ПроверитьВГХ("ГрузМаксимальнаяДлина");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГрузМаксимальныйВесПриИзменении(Элемент)
	
	ОчиститьСообщения();
	РассчитатьОбъем();
	РассчитатьВес();
	ПроверитьВГХ("ГрузМаксимальныйВес");
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

#КонецОбласти

#Область СтраницаТарифы

&НаКлиенте
Процедура ТарифыТекущийГрузоперевозчикНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуГрузоперевозчика();
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифыТекущийТарифПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуТарифа();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Нажатие(Элемент)
	
	СвойствоИмя = "Имя";
	
	Если СтрНайти(Элемент[СвойствоИмя], "ОчиститьОтбор_") Тогда
		
		// Очистка отбора.
		ИмяРеквизита = Сред(Элемент[СвойствоИмя], СтрНайти(Элемент.Имя, "_") + 1);
		
		Если СтрНайти(ИмяРеквизита, "_") Тогда
			
			Идентификатор = Сред(ИмяРеквизита, СтрНайти(ИмяРеквизита, "_") + 1);
			ИмяРеквизита = Лев(ИмяРеквизита, СтрНайти(ИмяРеквизита, "_") - 1);
			
			СтрокаТаблицы = ЭтотОбъект[ИмяРеквизита].НайтиПоИдентификатору(Идентификатор);
			
			Если СтрокаТаблицы <> Неопределено Тогда 
				ЗначениеРеквизита = СтрокаТаблицы.Значение;
				СтрокаТаблицы.Использовать = Ложь;
				
				Если ТипЗнч(ЗначениеРеквизита) = Тип("Булево") Тогда
					СтрокаТаблицы.Значение = Ложь;
				ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
					СтрокаТаблицы.Значение = 0;
				ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Строка") Тогда
					СтрокаТаблицы.Значение = "";
				ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Дата") Тогда
					СтрокаТаблицы.Значение = Неопределено;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
			
			Если ТипЗнч(ЗначениеРеквизита) = Тип("Булево") Тогда
				ЭтотОбъект[ИмяРеквизита] = Ложь;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Число") Тогда
				ЭтотОбъект[ИмяРеквизита] = 0;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("Строка") Тогда
				ЭтотОбъект[ИмяРеквизита] = "";
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СписокЗначений") Тогда 
				Для Каждого ЭлементСписка Из ЭтотОбъект[ИмяРеквизита] Цикл
					ЭлементСписка.Пометка = Ложь;
				КонецЦикла;
			ИначеЕсли ТипЗнч(ЗначениеРеквизита) = Тип("СтандартныйПериод") Тогда
				ЭтотОбъект[ИмяРеквизита] = Неопределено;
			Иначе
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		ОбработатьПрименениеОтборов(Ложь);
		
	ИначеЕсли СтрНайти(Элемент.Имя, "ЗаголовокОтбора_") Тогда
		
		ИмяРеквизита = Сред(Элемент.Имя, СтрНайти(Элемент.Имя, "_") + 1);
		
		Если СтрНайти(ИмяРеквизита, "_") Тогда
			
			Идентификатор = Сред(ИмяРеквизита, СтрНайти(ИмяРеквизита, "_") + 1);
			ИмяРеквизита = Лев(ИмяРеквизита, СтрНайти(ИмяРеквизита, "_") - 1);
			
			Элементы[ИмяРеквизита].ТекущаяСтрока = Идентификатор;
			
		КонецЕсли;
		
		ТекущийЭлемент = Элементы[ИмяРеквизита];
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтгрузкиПриИзменении(Элемент)
	СпособОтгрузкиДоставкиПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура СпособДоставкиПриИзменении(Элемент)
	СпособОтгрузкиДоставкиПриИзмененииНаКлиенте();
КонецПроцедуры

#КонецОбласти

#Область ПараметрыТарифа

&НаКлиенте
Процедура ОтправительТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТерминалНачалоВыбора(1, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	 ТерминалНачалоВыбора(2, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительТерминалОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуТерминала(ПунктПриемаГрузаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучательТерминалОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуТерминала(ПунктВыдачиГрузаИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаОплатыОчистка(Элемент, СтандартнаяОбработка)
	
	ФормаОплаты = 0;
	ФормаОплатыПредставление = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ФормаОплатыОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ЗначениеСписка = Элементы.ФормаОплаты.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	Если ЗначениеСписка = Неопределено Тогда
		ФормаОплаты = 0;
		ФормаОплатыПредставление = "";
	Иначе
		ФормаОплаты = ВыбранноеЗначение;
		ФормаОплатыПредставление = ЗначениеСписка.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправительТерминалОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СброситьТерминал("ПунктПриема");
КонецПроцедуры

&НаКлиенте
Процедура ПолучательТерминалОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	СброситьТерминал("ПунктВыдачи");
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	
	Адресат = ОпределитьАдресата(Элемент.Имя);
	
	ЭтотОбъект[Адресат + "АдресЗначение"] = "";
	ЭтотОбъект[Адресат + "АдресВладелец"] = "";
	ЭтотОбъект[Адресат + "АдресВладелецНаименование"] = "";
	ЭтотОбъект[Адресат + "АдресПредставление"] = СокрЛП(ЭтотОбъект[Адресат + "АдресПредставление"]);
	
	ЗарегистрироватьИзменениеОтборов(Истина);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ИтоговаяИнформацияОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	Если Расшифровка = "Грузоперевозчик" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуГрузоперевозчика();
		
	ИначеЕсли Расшифровка = "ОтправительТерминалОтправительАдрес" Тогда
		
		СтандартнаяОбработка = Ложь;
		Если СпособОтгрузки = 1 Тогда
			ОткрытьФормуТерминала(ПунктПриемаГрузаИдентификатор);
		Иначе
			АдресНачалоВыбора(Элементы.ОтправительАдрес, ОтправительКонтрагентЭтоОрганизация);
		КонецЕсли;
	ИначеЕсли Расшифровка = "ПолучательТерминалПолучательАдрес" Тогда
		
		СтандартнаяОбработка = Ложь;
		Если СпособДоставки = 1 Тогда
			ОткрытьФормуТерминала(ПунктВыдачиГрузаИдентификатор);
		Иначе
			АдресНачалоВыбора(Элементы.ПолучательАдрес, ПолучательКонтрагентЭтоОрганизация);
		КонецЕсли;
		
	ИначеЕсли Расшифровка = "ОтправительКонтрагент" Тогда
		
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ОтправительКонтрагентСсылка) Тогда
			КонтрагентОткрытие(Элементы.ОтправительКонтрагент);
		КонецЕсли;
		
	ИначеЕсли Расшифровка = "ПолучательКонтрагент" Тогда
		
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ПолучательКонтрагентСсылка) Тогда
			КонтрагентОткрытие(Элементы.ПолучательКонтрагент);
		КонецЕсли;
		
	ИначеЕсли Расшифровка = "ПлательщикКонтрагент" Тогда
		
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(ПлательщикКонтрагентСсылка) Тогда
			КонтрагентОткрытие(Элементы.ПлательщикКонтрагент);
		КонецЕсли;
		
	ИначеЕсли Расшифровка = "Тариф" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуТарифа();
		
	ИначеЕсли Расшифровка = "ОткрытьФормуОснования" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуОснования();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИтоговаяИнформацияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ТрекНомер" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФормуГрафикаДвиженияЗаказа();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеПредставлениеНажатие(Элемент)
	
	Если Кэш.ЭтоЯндексДоставка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ТрекНомер) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Трек номер неопределен, невозможно перейти по ссылке.';
														|en = 'The track number is undefined, cannot follow the link.'"));
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СервисДоставкиКлиентСервер.АдресСтраницыЗаказаНаДоставку1СДоставка(ТрекНомер));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТарифы

&НаКлиенте
Процедура ТарифыПриАктивизацииСтроки(Элемент)
	
	Если Элементы.ГруппаТарифы <> Элементы.ГруппаСтраницы.ТекущаяСтраница Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьИнформациюПоТарифу();
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ВыбратьТарифПродолжить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборыТарифов

&НаКлиенте
Процедура ОтборыТарифовИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтборыТарифов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Использовать Тогда
		СброситьВзаимоисключающие(ТекущиеДанные, "ОтборыТарифов");
		Элементы.ОтборыТарифов.Развернуть(ТекущиеДанные.ПолучитьИдентификатор());
	Иначе
		Элементы.ОтборыТарифов.Свернуть(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыТарифовПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ОтборыТарифов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
	
		Поле = Элемент.ТекущийЭлемент;
		Поле.ТолькоПросмотр = Не ТекущиеДанные.ТребуетсяЗначение;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыТарифовЗначениеПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Элементы.ОтборыТарифов.ТекущиеДанные.Значение) Тогда
		
		СтрокаОтбора = ОтборыТарифов.НайтиПоИдентификатору(Элементы.ОтборыТарифов.ТекущаяСтрока);
		Если СтрокаОтбора <> Неопределено Тогда
			СтрокаУслуги = СтрокаОтбора.ПолучитьРодителя();
			СтрокаУслуги.Использовать = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗарегистрироватьИзменениеОтборов(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыТарифовПередРазворачиванием(Элемент, Строка, Отказ)
	
	ТекущиеДанные = ОтборыТарифов.НайтиПоИдентификатору(Строка);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (Не ТекущиеДанные.Использовать) И ТекущиеДанные.ТипСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыТарифовПередСворачиванием(Элемент, Строка, Отказ)
	
	ТекущиеДанные = ОтборыТарифов.НайтиПоИдентификатору(Строка);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Использовать И ТекущиеДанные.ТипСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтборыТарифовПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ОтборыТарифов.ТекущиеДанные;
	
	Если Элементы.ОтборыТарифов.ТекущийЭлемент = Элементы.ОтборыТарифовПоказыватьИнформацию
		И ТекущиеДанные.ПоказыватьИнформацию Тогда
			ОткрытьФормуУслуги(ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТарифыДетализацияСтоимостиПоУслугам

&НаКлиенте
Процедура ТарифыДетализацияСтоимостиПоУслугамПриИзменении(Элемент)
	ПлательщикиУслугИзменены = Истина;
	Модифицированность = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставлениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТарифыДетализацияСтоимостиПоУслугам.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоМежтермитальнаяПеревозка(ТекущиеДанные) Тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ЭтоМежтермитальнаяПеревозка", Истина);
		ПоказатьВопрос(Новый ОписаниеОповещения("ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставлениеПриИзмененииЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			НСтр("ru = 'Межтерминальная перевозка оплачивается основным плательщиком документа.
				|Заменить основного плательщика заказа на доставку?';
				|en = 'Interterminal transportation is paid by the main document payer.
				|Change the main payer of the delivery order?'"),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставлениеПриИзмененииЗавершение(КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьТариф(Команда)
	ВыбратьТарифПродолжить();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьТарифПоУмолчанию(Команда)
	
	ТекущиеДанные = Элементы.Тарифы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыУстановитьТарифПоУмолчанию());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Установка тарифа по умолчанию.';
											|en = 'Set default fare.'");
	ПараметрыОперации.Вставить("ГрузоперевозчикИдентификатор", ТекущиеДанные.ГрузоперевозчикИдентификатор);
	ПараметрыОперации.Вставить("ТарифИдентификатор", ТекущиеДанные.ТарифИдентификатор);
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьТарифПоУмолчанию(Команда)
	
	ТекущиеДанные = Элементы.Тарифы.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ТекущиеДанные.ПоУмолчанию Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыСброситьТарифПоУмолчанию());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Сброс тарифа по умолчанию.';
											|en = 'Reset default fare.'");
	ПараметрыОперации.Вставить("ГрузоперевозчикИдентификатор", ТекущиеДанные.ГрузоперевозчикИдентификатор);
	ПараметрыОперации.Вставить("ТарифИдентификатор", "");
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиОсновное(Команда)
	
	ПерейтиКШагуОсновное();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиГруз(Команда)
	
	ПерейтиКШагуГруз();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиТарифы(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеОбязательныхРеквизитов(2, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиКШагуТарифы();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиПараметрыТарифа(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеОбязательныхРеквизитов(3, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиКШагуПараметрыТарифа();

КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиПроверка(Команда)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеОбязательныхРеквизитов(4, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиКШагуПроверка();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыГруза(Команда)
	
	ИмяКоманды = Команда.Имя;
	
	Если ИмяКоманды = "ОбновитьПараметрыГруза" Тогда
		МассивРеквизитовДляПересчета = РеквизитыДляПересчета();
		
		Для Каждого ТекРеквизитДляПересчета Из МассивРеквизитовДляПересчета Цикл
			ИмяРеквизита = "Груз" + ТекРеквизитДляПересчета;
			ОбновитьПараметрГруза(ИмяРеквизита);
		КонецЦикла;
		
	Иначе
		
		ИмяРеквизита = СтрЗаменить(ИмяКоманды, "Обновить", "Груз");
		ОбновитьПараметрГруза(ИмяРеквизита);
		
	КонецЕсли;
	
	УстановитьКомандыПересчетаПараметровГруза();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПерейтиКарточка(Команда)
	
	ПерейтиКШагуКарточка();
	
КонецПроцедуры
&НаКлиенте
Процедура ОформитьСохранитьЗаказ(Команда)
	
	ЗакрытьПослеЗавершенияОперации = Ложь;
	
	Если ДоступнаОтправкаЗаказовНаДоставку Тогда
		ОформитьЗаказ(ЗакрытьПослеЗавершенияОперации);
	Иначе
		СоздатьИзменитьЗаказНаДоставку(,ЗакрытьПослеЗавершенияОперации);
	КонецЕсли;
	
	ЭтоЗаполнениеКопированием = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СортироватьТарифыПоЦене(Команда)
	ТарифыУстановитьРежимСортировки(Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура СортироватьТарифыПоСроку(Команда)
	ТарифыУстановитьРежимСортировки(Команда.Имя);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТарифы(Команда)
	
	ОчиститьСообщения();
	
	Если ОтборыИзменение Тогда
		ОбработатьПрименениеОтборов(Ложь);
	Иначе
		ПолучитьТарифы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьКакЧерновик(Команда)
	
	ОчиститьСообщения();
	
	СоздатьИзменитьЗаказНаДоставку();
	
	ЭтоЗаполнениеКопированием = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьИзменения(Команда)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	КонтрольЗаполненияРеквизитовОформленногоЗаказа(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СоздатьИзменитьЗаказНаДоставку(Ложь, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	ОчиститьСообщения();
	
	ОписаниеОЗавершении = Новый ОписаниеОповещения("ЗавершениеОтменитьЗаказ", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОЗавершении, НСтр("ru = 'Отменить заказ?';
											|en = 'Cancel order?'"), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьОтборы(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеЗначенийОтборовТарифов(Отказ);
	ПроверитьЗаполнениеАдресов(Отказ);
	
	Если Не Отказ Тогда
		
		Модифицированность = Истина;
		Элементы.ГруппаОтборы.Скрыть();
		ОбработатьИзменениеВариантовДоставки();
		ОбработатьПрименениеОтборов(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ИдентификаторЗаказа);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	Если Кэш.ЭтоДеловыеЛинии Тогда
		ПараметрыОткрытияФормы.Вставить("ПараметрыЗаказа", 
			Новый Структура("ИдентификаторЗаказа, КоличествоГрузовыхМест", 
			ИдентификаторЗаказа, ГрузКоличествоГрузовыхМест));
	КонецЕсли;

	ОткрытьФорму(
		"Обработка.СервисДоставки.Форма.ПечатныеФормы",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеПоЗаказуУГрузоперевозчика(Команда)
	
	ОчиститьСообщения();
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		ОбновитьЗаказНаДоставку();
	КонецЕсли;
	
КонецПроцедуры

#Область КомандыРедактированияРеквизитовЗаказа

&НаКлиенте
Процедура ИзменитьПараметрыЗаказа(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьДоступныеДляИзмененияРеквизиты());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Получение реквизитов для изменения.';
											|en = 'Receive attributes to edit.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьИзменения(Команда)
	
	РежимИзмененияРеквизитов(Ложь);
	ДоступныеДляИзмененияРеквизитыТолькоПросмотр(Истина);
	
	Если Модифицированность Тогда
		ПолучитьЗаказНаДоставку();
		Модифицированность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ОбновитьТарифКоманда(Команда)
	
	ОчиститьСообщения();
	ОбновитьТариф();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура ВыполнитьЗапрос(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	
	Если ИнтернетПоддержкаПодключена = Ложь Тогда
		Оповещение = Новый ОписаниеОповещения("ВыполнитьЗапросПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	Иначе
		ВыполнитьЗапросПродолжение(Истина, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	ФоновоеЗадание = ДополнительныеПараметры.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ИнтернетПоддержкаПодключена = Ложь;
		ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ДополнительныеПараметры);
		ФоновоеЗадание = ДополнительныеПараметры.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеВыполненияЗапроса(ДополнительныеПараметры);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ВыполнитьЗапросЗавершение(ФоновоеЗадание, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не СервисДоставки.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ЗапуститьФоновоеЗадание(ЭтотОбъект, ПараметрыОперации, ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации)
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		Для Каждого Операция Из ФоновоеЗадание.Очередь Цикл
			
			ИмяМетода = Операция.Представление;
			
			Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов() Тогда
				
				Элементы.ОтправительСпособОтгрузки.Доступность = Ложь;
				Элементы.ПолучательСпособДоставки.Доступность = Ложь;
				Элементы.ГруппаОжиданиеЗагрузкиОтборов.Видимость = Истина;
				Элементы.ОбновитьТарифы.Доступность = Ложь;
				
			ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы() Тогда
				
				Элементы.ОбновитьТарифы.Картинка = БиблиотекаКартинок.СинхронизацияДанныхДлительнаяОперация;
				Элементы.ДекорацияСостояниеВыполненияЗапросаТарифы.Заголовок = НСтр("ru = 'Поиск подходящих тарифов...';
																					|en = 'Searching for service plans...'");
				
			ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф() Тогда
				
				Элементы.ГруппаОжиданияОбновленияТарифа.Видимость = Истина;
				Элементы.ГруппаПараметрыТарифа.ТолькоПросмотр = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(ПараметрыОперации.ИмяПроцедурыЗавершения, ЭтотОбъект, ПараметрыОперации);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыОжидания, ПараметрыОперации);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации()
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов());
	Если РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераНовый() Или ЭтоЗаполнениеКопированием Тогда
		ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку());
	КонецЕсли;
	
	ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОжиданиеЗагрузкиЗаказа;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТарифы(ПолучитьТарифыАвтоматически = Ложь)
	
	Тарифы.Очистить();
	РежимСозданияЗаказаПоУмолчанию = ПолучитьТарифыАвтоматически;
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Поиск подходящих тарифов.';
											|en = 'Search for service plans.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.ВыводитьСообщения = Ложь;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьТарифыВФоне(ПолучитьТарифыАвтоматически = Ложь)
	
	Тарифы.Очистить();
	РежимСозданияЗаказаПоУмолчанию = ПолучитьТарифыАвтоматически;
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Поиск подходящих тарифов.';
											|en = 'Search for service plans.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.ВыводитьСообщения = Ложь;
	
	ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТариф()
	
	ВыделенныйТариф = "";
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Обновление параметров тарифа.';
											|en = 'Update fare settings.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьУслугиТарифов()
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Получение услуг тарифов.';
											|en = 'Receive fare services.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьИзменитьЗаказНаДоставку(ОформитьЗаказ = Ложь, ЗакрытьПослеЗавершенияОперации = Ложь)
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыСоздатьИзменитьЗаказНаДоставку());
	ПараметрыОперации.ЗакрытьПослеЗавершенияОперации = ЗакрытьПослеЗавершенияОперации;
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Истина;
	Если ЗначениеЗаполнено(ИдентификаторЗаказа) Тогда
		ТекстСообщения = НСтр("ru = 'Сохранение заказа на доставку.';
								|en = 'Save the delivery order.'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Создание заказа на доставку.';
								|en = 'Create a delivery order.'");
	КонецЕсли;
	ПараметрыОперации.ТекстСообщения = ТекстСообщения;
	ПараметрыОперации.Вставить("ОформитьЗаказ", ОформитьЗаказ);
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьЗаказНаДоставку(ТекстСообщения = "", ЗакрытьПослеЗавершенияОперации = Ложь)
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыОформитьЗаказНаДоставку());
	Если ТекстСообщения = "" Тогда
		ТекстСообщения = НСтр("ru = 'Отправка заказа на доставку грузоперевозчику.';
								|en = 'Send order for delivery to carrier.'");
	КонецЕсли;
	ПараметрыОперации.ТекстСообщения = ТекстСообщения;
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ЗакрытьПослеЗавершенияОперации = ЗакрытьПослеЗавершенияОперации;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаказНаДоставку(ПлатнаяОтмена = Ложь)
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Отмена заказа на доставку.';
											|en = 'Cancel the order for delivery'");
	ПараметрыОперации.Вставить("ПлатнаяОтмена", ПлатнаяОтмена);
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаказНаДоставку()
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыОбновитьЗаказНаДоставку());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Обновление данных по заказу на доставку.';
											|en = 'Update delivery order data.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьЗаказНаДоставку()
	ВыполнитьЗапрос(ПараметрыОперацииПолучитьЗаказНаДоставку());
КонецПроцедуры

&НаСервере
Процедура ПолучитьЗаказНаДоставкуВФоне()
	ВыполнитьЗапросВФоне(Ложь, ПараметрыОперацииПолучитьЗаказНаДоставку());
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов() Тогда
			 ПараметрыЗапросаПолучитьУслугиТарифов(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы() Тогда
			ПараметрыЗапросаПолучитьТарифы(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф() Тогда
			ПараметрыЗапросаПолучитьТарифы(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыСоздатьИзменитьЗаказНаДоставку() Тогда
			ПараметрыЗапросаСоздатьИзменитьЗаказНаДоставку(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыОформитьЗаказНаДоставку() Тогда
			ПараметрыЗапросаОформитьЗаказНаДоставку(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку() Тогда
			ПараметрыЗапросаПолучитьЗаказНаДоставку(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыОбновитьЗаказНаДоставку() Тогда
			ПараметрыЗапросаОбновитьЗаказНаДоставку(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку() Тогда
			ПараметрыЗапросаОтменитьЗаказНаДоставку(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТипыГрузоперевозки() Тогда
			ПараметрыЗапросаПолучитьТипыГрузоперевозки(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыУстановитьТарифПоУмолчанию() Тогда
			ПараметрыЗапросаУстановитьТарифПоУмолчанию(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыСброситьТарифПоУмолчанию() Тогда
			ПараметрыЗапросаУстановитьТарифПоУмолчанию(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьДоступныеДляИзмененияРеквизиты() Тогда
			ПараметрыЗапросаПолучитьДоступныеДляИзмененияРеквизиты(Операция.Значение, ПараметрыОперации, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОперации.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьУслугиТарифов(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьУслугиТарифов();
	Значение.ТипГрузоперевозки = ТипГрузоперевозки;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьЗаказНаДоставку(Значение, ПараметрыОперации, Отказ)
	
	ПараметрыИнициации = Значение;
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьЗаказНаДоставку();
	
	ЗаполнитьЗначенияСвойств(Значение, ЭтотОбъект);
	Значение.Идентификатор = ИдентификаторЗаказа;
	Значение.ЭтоСозданиеШаблона = ?(ПараметрыИнициации = Неопределено, Ложь, ПараметрыИнициации.ЭтоСозданиеШаблона);
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаОбновитьЗаказНаДоставку(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаОбновитьЗаказНаДоставку();
	Значение.Идентификатор = ИдентификаторЗаказа;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаОтменитьЗаказНаДоставку(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаОтменитьЗаказНаДоставку();
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗаказа) Или Не ДоступнаОтмена Тогда
		ТекстСообщения = НСтр("ru = 'Отмена данного заказа недоступна. Невозможно отменить заказ.';
								|en = 'Cancellation of this order is not available. Cannot cancel the order.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Значение.Вставить("Идентификатор", ИдентификаторЗаказа);
	Значение.Вставить("ОтменитьЗаказПлатно", ?(ПараметрыОперации.ПлатнаяОтмена, "1", "0"));
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьТарифы(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьТарифы();
	
	ЗаполнитьЗначенияСвойств(Значение, ЭтотОбъект);
	
	Значение.ЗаказчикРоль = ?(ЗначениеЗаполнено(ЗаказчикРоль), ЗаказчикРоль, 3);
	Если ЗаказчикРоль = 1 Тогда
		ЗаказчикИНН = ОтправительКонтрагентИНН;
	ИначеЕсли ЗаказчикРоль = 2 Тогда
		ЗаказчикИНН = ПолучательКонтрагентИНН;
	ИначеЕсли ЗаказчикРоль = 3 Тогда
		Если ПлательщикРоль = 1 Тогда
			ЗаказчикИНН = ОтправительКонтрагентИНН;
		ИначеЕсли ПлательщикРоль = 2 Тогда
			ЗаказчикИНН = ПолучательКонтрагентИНН;
		Иначе
			ЗаказчикИНН = ПлательщикКонтрагентИНН;
		КонецЕсли;
	Иначе
		ЗаказчикИНН = "";
	КонецЕсли;
	Значение.Вставить("ЗаказчикИНН", ЗаказчикИНН);
	
	Значение.ОтправительАдрес = ОтправительАдресПредставление; // Полный адрес
	Значение.ОтправительАдресЗначение = Новый ХранилищеЗначения(ОтправительАдресЗначение,
		Новый СжатиеДанных(9)); // Формат JSON 1С:БСП
	
	Значение.ПолучательАдрес = ПолучательАдресПредставление; // Полный адрес
	Значение.ПолучательАдресЗначение = Новый ХранилищеЗначения(ПолучательАдресЗначение,
		Новый СжатиеДанных(9)); // Формат JSON 1С:БСП
		
	Если ПустаяСтрока(ПлательщикАдресПредставление) Тогда
		Если ПлательщикРоль = 1 Тогда
			Если ПустаяСтрока(ОтправительАдресПредставление) Тогда
				УстановленныйПлательщикАдрес = ОтправительКонтрагентЮридическийАдресПредставление;
				УстановленныйПлательщикАдресЗначение = ОтправительКонтрагентЮридическийАдресЗначение;
			Иначе
				УстановленныйПлательщикАдрес = ОтправительАдресПредставление;
				УстановленныйПлательщикАдресЗначение = ОтправительАдресЗначение;
			КонецЕсли;
		ИначеЕсли ПлательщикРоль = 2 Тогда
			Если ПустаяСтрока(ПолучательАдресПредставление) Тогда
				УстановленныйПлательщикАдрес = ПолучательКонтрагентЮридическийАдресПредставление;
				УстановленныйПлательщикАдресЗначение = ПолучательКонтрагентЮридическийАдресЗначение;
			Иначе
				УстановленныйПлательщикАдрес = ПолучательАдресПредставление;
				УстановленныйПлательщикАдресЗначение = ПолучательАдресЗначение;
			КонецЕсли;
		Иначе
			УстановленныйПлательщикАдрес = ПлательщикКонтрагентЮридическийАдресПредставление;
			УстановленныйПлательщикАдресЗначение = ПлательщикКонтрагентЮридическийАдресЗначение;	
		КонецЕсли;
	Иначе
		УстановленныйПлательщикАдрес = ПлательщикАдресПредставление;
		УстановленныйПлательщикАдресЗначение = ПлательщикАдресЗначение;	
	КонецЕсли;
	Значение.ПлательщикАдрес = УстановленныйПлательщикАдрес; // Полный адрес
	Значение.ПлательщикАдресЗначение = Новый ХранилищеЗначения(УстановленныйПлательщикАдресЗначение,
			Новый СжатиеДанных(9)); // Формат JSON 1С:БСП	
	
	Значение.ГрузОбщийВес = ГрузВес;
	Значение.ГрузОбщийОбъем = ГрузОбъем;
	
	Для Каждого ТекущаяУслуга Из УслугиТарифов Цикл
		Если ТекущаяУслуга.Использовать Тогда
			НоваяУслуга = Значение.Услуги.Добавить();
			НоваяУслуга.Идентификатор = ТекущаяУслуга.Идентификатор;
			НоваяУслуга.Свойства = Новый Массив();
			Для Каждого ТекущееСвойство Из ТекущаяУслуга.Свойства Цикл
				ПараметрыСвойстваУслуги = СервисДоставки.НовыйПараметрыСвойстваУслуги();
				ПараметрыСвойстваУслуги.Идентификатор = ТекущееСвойство.Идентификатор;
				ПараметрыСвойстваУслуги.Значение = ТекущееСвойство.Значение;
				НоваяУслуга.Свойства.Добавить(ПараметрыСвойстваУслуги);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Если Кэш.ЭтоДеловыеЛинии Тогда
		Для Каждого Элемент Из ТарифДополнительныеУслуги.ПолучитьЭлементы() Цикл
			Для Каждого ТекущаяУслуга Из Элемент.ПолучитьЭлементы() Цикл
				НоваяУслуга = Значение.Услуги.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
				НоваяУслуга.Свойства = Новый Массив;
				ПараметрыСвойстваУслуги = СервисДоставки.НовыйПараметрыСвойстваУслуги();
				ПараметрыСвойстваУслуги.Значение = ТекущаяУслуга.Использовать;
				НоваяУслуга.Свойства.Добавить(ПараметрыСвойстваУслуги);
				Для Каждого ТекущееСвойство Из ТекущаяУслуга.ПолучитьЭлементы() Цикл
					ПараметрыСвойстваУслуги = СервисДоставки.НовыйПараметрыСвойстваУслуги();
					ЗаполнитьЗначенияСвойств(ПараметрыСвойстваУслуги, ТекущееСвойство);
					НоваяУслуга.Свойства.Добавить(ПараметрыСвойстваУслуги);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаТовара Из ТоварныйСостав Цикл
		НовыйТовар = Значение.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйТовар, СтрокаТовара);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаСоздатьИзменитьЗаказНаДоставку(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаСоздатьИзменитьЗаказНаДоставку();
	Значение.КлючИдемпотентности = КлючИдемпотентности;
	ЗаполнитьИнформациюПоДокументамОснования(Значение);
	
	Если ПлательщикРоль = 0 Тогда
		ПлательщикРоль = ?(ОтправительКонтрагентЭтоОрганизация, 1, 2); // 1 - отправитель, 2 - получатель, 3 - третье лицо
	КонецЕсли;
	
	РеквизитыФормы = ПолучитьРеквизиты();
	ДанныеФормы = Новый Структура();
	Для Каждого ТекущийРеквизит Из РеквизитыФормы Цикл
		ДанныеФормы.Вставить(ТекущийРеквизит.Имя, ЭтотОбъект[ТекущийРеквизит.Имя]);
	КонецЦикла;
	ДанныеФормы.Удалить("ГрузовыеМеста");
	СервисДоставкиКлиентСервер.ЗаполнитьСтруктуруПоЛинейнымДанным(Значение, ДанныеФормы);
	
	Для Каждого ТекущаяУслуга Из УслугиТарифа Цикл
		Если ТекущаяУслуга.Использовать Тогда
			НоваяУслуга = Значение.Услуги.Добавить();
			НоваяУслуга.Идентификатор = ТекущаяУслуга.Идентификатор;
			НоваяУслуга.Свойства = Новый Массив();
			Для Каждого ТекущееСвойство Из ТекущаяУслуга.Свойства Цикл
				ПараметрыСвойстваУслуги = СервисДоставки.НовыйПараметрыСвойстваУслуги();
				ПараметрыСвойстваУслуги.Идентификатор = ТекущееСвойство.Идентификатор;
				ПараметрыСвойстваУслуги.Значение = ТекущееСвойство.Значение;
				НоваяУслуга.Свойства.Добавить(ПараметрыСвойстваУслуги);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	ДобавитьДанныеТаблицы(Значение, "ТоварныйСостав", "Товары");
	СохранитьПлательщиковУслуг();
	ДобавитьДанныеТаблицы(Значение, "ОплатаСтоимостьУслуг", "ПлательщикиУслуг");
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаОформитьЗаказНаДоставку(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаОформитьЗаказНаДоставку();
	Значение.Идентификатор = ИдентификаторЗаказа;
	Значение.ТипГрузоперевозки = ТипГрузоперевозки;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьТипыГрузоперевозки(Значение, ПараметрыОперации, Отказ)
	Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьТипыГрузоперевозки();
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаУстановитьТарифПоУмолчанию(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаУстановитьТарифПоУмолчанию();
	
	Значение.Вставить("ГрузоперевозчикИдентификатор", ПараметрыОперации.ГрузоперевозчикИдентификатор);
	Значение.Вставить("ТарифИдентификатор", ПараметрыОперации.ТарифИдентификатор);
	Значение.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыЗапросаПолучитьДоступныеДляИзмененияРеквизиты(Значение, ПараметрыОперации, Отказ)
	
	Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьДоступныеДляИзмененияРеквизиты();
	Значение.Вставить("Идентификатор", ИдентификаторЗаказа);
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры, Отказ)
	
	Очередь = ДополнительныеПараметры.Очередь;
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы() Тогда
			
			Элементы.ВыбратьТариф.Доступность = Истина;
			Элементы.ОбновитьТарифы.Картинка = БиблиотекаКартинок.Обновить;
			Элементы.ДекорацияСостояниеВыполненияЗапросаТарифы.Заголовок = "";
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф() Тогда
			
			Элементы.ГруппаОжиданияОбновленияТарифа.Видимость = Ложь;
			Элементы.ГруппаПараметрыТарифа.ТолькоПросмотр = Ложь;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов() Тогда
			
			Элементы.ОтправительСпособОтгрузки.Доступность = Истина;
			Элементы.ПолучательСпособДоставки.Доступность = Истина;
			Элементы.ГруппаОжиданиеЗагрузкиОтборов.Видимость = Ложь;
			Элементы.ОбновитьТарифы.Доступность = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры, Отказ);
	
	СервисДоставкиКлиент.ОбработатьРезультатФоновогоЗадания(Результат, ДополнительныеПараметры, Отказ);
	
	Если Отказ Или Результат.Статус <> "Выполнено" Или Не ЗначениеЗаполнено(Результат.АдресРезультата)
		Или Не ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата);
	Иначе
		РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		ОперацияВыполнена = Истина;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыСоздатьИзменитьЗаказНаДоставку() Тогда
			
			ЭтоСоздание = (ИдентификаторЗаказа = "");
			ЗагрузитьРезультатСозданияЗаказа(РезультатОперации, ОперацияВыполнена);
			
			Если ОперацияВыполнена Тогда
				
				Если ЭтоСоздание Тогда
					ТекстПояснения = НСтр("ru = 'Заказ на доставку создан.';
											|en = 'Delivery order is created'");
					ТипОперации = НСтр("ru = 'Создание:';
										|en = 'Creation:'");
					ОбновитьКлючУникальности();
				Иначе
					ТекстПояснения = НСтр("ru = 'Заказ на доставку изменен.';
											|en = 'Delivery order is changed.'");
					ТипОперации = НСтр("ru = 'Изменение:';
										|en = 'Change:'");
				КонецЕсли;
				
				ПоказатьОповещениеПользователя(ТипОперации,, ТекстПояснения, БиблиотекаКартинок.Информация32);
				УстановитьВидимостьДоступностьОрганизации();
				
			КонецЕсли;
			
			Если ОперацияВыполнена Тогда
				
				Модифицированность = Ложь;
				
				Если ЭтоСоздание И Не ДополнительныеПараметры.ОформитьЗаказ Тогда
					Оповестить("ОбновитьСписокЗаказовНаДоставку", ТипГрузоперевозки);
				КонецЕсли;
				
				// Завершено изменение оформленного заказа
				Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
					ПолучитьЗаказНаДоставку();
					КлючиРеквизитовДляИзменения.Очистить();
				КонецЕсли;
				
				Если ДополнительныеПараметры.ОформитьЗаказ Тогда
					ОформитьЗаказНаДоставку( , ДополнительныеПараметры.ЗакрытьПослеЗавершенияОперации);
				ИначеЕсли ДополнительныеПараметры.ЗакрытьПослеЗавершенияОперации Тогда
					Закрыть();
				КонецЕсли;
				
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыОформитьЗаказНаДоставку() Тогда
			
			Если РезультатОперации = 1 Тогда
				ТекстПояснения = НСтр("ru = 'Заказ на доставку передан к оформлению грузоперевозчику';
										|en = 'Delivery order is passed to registration to carrier'");
				ТипОперации = НСтр("ru = 'Оформление:';
									|en = 'Appearance:'");
				ПоказатьОповещениеПользователя(ТипОперации, , ТекстПояснения, БиблиотекаКартинок.Информация32);
				Оповестить("ОбновитьСписокЗаказовНаДоставку", ТипГрузоперевозки);
				Если ДополнительныеПараметры.ЗакрытьПослеЗавершенияОперации Тогда
					Закрыть();
				КонецЕсли;
			ИначеЕсли РезультатОперации = 2 Тогда
				ОформитьЗаказНаДоставку(НСтр("ru = 'Расчет стоимости заказа на доставку';
											|en = 'Calculating delivery order cost'"),
					ДополнительныеПараметры.ЗакрытьПослеЗавершенияОперации);
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы() Тогда
			
			СформироватьПредставлениеТарифаИГрузоперевозчика();
			ТарифыУстановитьРежимСортировки(ТарифыРежимСортировки);
			ОтборыИзменение = Ложь;
			ПлательщикиУслугИзменены = Истина;
			СформироватьНадписьОтбора();
			
			ОбновитьЭлементыФормыГруппаПредупрежденияТарифа();
			
			// Если получение тарифов запущено автоматически и заполнен тариф по умолчанию, то необходимо перейти
			// на сразу вкладку "Проверка" или "Параметры тарифа".
			Если РежимСозданияЗаказаПоУмолчанию И ТарифИдентификатор <> "" Тогда
				ПерейтиКТекущемуШагуМастера(Ложь);
				РежимСозданияЗаказаПоУмолчанию = Ложь;
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф() Тогда
			
			ЗагрузитьРезультатПолученияТарифа(РезультатОперации, ОперацияВыполнена);
			СформироватьЗаголовокГруппыДополнительныхУслуг();
			СформироватьПредставлениеТарифаИГрузоперевозчика();
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов() Тогда
			
			РазвернутьРодителейСпискаСВыбраннымиСтроками();
			Если РезультатОперации Тогда
				ПроверитьЗаполнениеЗначенийОтборовТарифов(Отказ);
				Если Не Отказ Тогда
					Элементы.ГруппаОтборы.Скрыть();
					ОбработатьИзменениеВариантовДоставки();
					Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый()
						И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТарифы Тогда
						ОбработатьПрименениеОтборов(, Ложь);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если Не ЭтоЗаполнениеКопированием Тогда
				ПолучитьТарифыАвтоматически();
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку() Тогда
			
			Если ТипЗнч(РезультатОперации) = Тип("Структура") И РезультатОперации.ЭтоСозданиеШаблона Тогда
				СервисДоставкиКлиент.СоздатьШаблонПоЗаказуНаДоставку(ЭтотОбъект, РезультатОперации.Значение);
				Прервать;
			КонецЕсли;
			
			Если РезультатОперации Тогда
				УстановитьДоступностьДополнительныхТелефонов();
				УстановитьВидимостьДоступностьНаСтраницеОсновная();
				ОбновитьФормуДляДокументаОснования();
				СформироватьПредставлениеТарифаИГрузоперевозчика();
				Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЧерновик() Тогда
					ПерейтиКТекущемуШагуМастера(Ложь);
				ИначеЕсли ЭтоЗаполнениеКопированием Тогда
					Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОсновное;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ДокументОснованиеДляДобавления) Тогда
					РежимИзмененияСоставаОснований = 0;
					Если ДокументыОснования.Количество() > 0 Тогда
						НайденныйЭлемент = ДокументыОснования.НайтиПоЗначению(ДокументОснованиеДляДобавления);
						Если НайденныйЭлемент = Неопределено Тогда
							РежимИзмененияСоставаОснований = 1;
							ДокументыОснования.Добавить(ДокументОснованиеДляДобавления);
						КонецЕсли;
					Иначе
						ДокументыОснования.Добавить(ДокументОснованиеДляДобавления);
						РежимИзмененияСоставаОснований = 2;
					КонецЕсли;
					
					Если РежимИзмененияСоставаОснований <> 0 Тогда
						ОбработатьИзменениеРеквизитаФормы("ДокументыОснования");
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ЭтоЗаполнениеКопированием = Ложь;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыОбновитьЗаказНаДоставку() Тогда
			
			Если РезультатОперации Тогда
				ПолучитьУслугиТарифов();
				ТарифыУстановитьРежимСортировки(ТарифыРежимСортировки);
				УстановитьВидимостьДоступностьНаСтраницеОсновная();
				ОбновитьФормуДляДокументаОснования();
				СформироватьПредставлениеТарифаИГрузоперевозчика();
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыОтменитьЗаказНаДоставку() Тогда
			
			РезультатОтмены = 0;
			СуммаОтмены = 0;
			ЗагрузитьРезультатОтменыЗаказа(РезультатОперации, РезультатОтмены, СуммаОтмены);
			Если РезультатОтмены = 1 Тогда //Отменен
				
				ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Заказ на доставку %1 отменен.';
						|en = 'The %1 delivery order is canceled.'"), НомерЗаказа);
				ПоказатьОповещениеПользователя(НСтр("ru = 'Отмена:';
													|en = 'Cancel:'"),, ТекстПояснения, БиблиотекаКартинок.Информация32);
				Оповестить("ОбновитьСписокЗаказовНаДоставку", ТипГрузоперевозки);
				Модифицированность = Ложь;
				Закрыть();
				
			ИначеЕсли РезультатОтмены = 2 Тогда //Возможна платная отмена
				
				ОписаниеОЗавершении = Новый ОписаниеОповещения("ЗавершениеОтменитьЗаказ", ЭтотОбъект);
				
				СписокКнопок = Новый СписокЗначений();
				СписокКнопок.Добавить(КодВозвратаДиалога.Повторить, НСтр("ru = 'Да';
																		|en = 'Yes'"));
				СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Доступна только платная отмена заказа %1.';
						|en = 'Only paid cancellation of the %1 order is available.'"), НомерЗаказа);
				Если СуммаОтмены > 0 Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Сумма отмены заказа %1 - %2.';
							|en = 'The %1 order cancellation amount is %2.'"), НомерЗаказа, СуммаОтмены);
				КонецЕсли;
				ТекстСообщения = ТекстСообщения + " " + НСтр("ru = 'Отменить платно?';
															|en = 'Cancel for a fee?'");
				ПоказатьВопрос(ОписаниеОЗавершении, ТекстСообщения, СписокКнопок);
				
			ИначеЕсли РезультатОтмены = 3 Тогда //Отмена заказа уже невозможна
				
				ТекстПояснения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Заказ на доставку %1 невозможно отменить.';
						|en = 'Cannot cancel the %1 delivery order.'"), НомерЗаказа);
				ПоказатьОповещениеПользователя(НСтр("ru = 'Отмена:';
													|en = 'Cancel:'"),, ТекстПояснения, БиблиотекаКартинок.Информация32);
				
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыУстановитьТарифПоУмолчанию() Тогда
			
			ЗагрузитьРезультатУстановкиТарифаПоУмолчанию(РезультатОперации, ОперацияВыполнена);
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыСброситьТарифПоУмолчанию() Тогда
			
			ЗагрузитьРезультатСбросаТарифаПоУмолчанию(РезультатОперации, ОперацияВыполнена);
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьДоступныеДляИзмененияРеквизиты() Тогда
			
			Если Не РезультатОперации Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось получить перечень изменяемых реквизитов.';
																|en = 'Cannot receive the list of editable attributes.'"));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыФормыГруппаПредупрежденияТарифа()

	Элементы.ГруппаПредупрежденияТарифа.Видимость = Истина;
	Если ПустаяСтрока(ИдентификаторТекущегоТарифа) Тогда
		Элементы.ГруппаПредупрежденияТарифа.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	ТекущийТариф = ОпределитьЭлементКоллекции(Тарифы, ИдентификаторТекущегоТарифа, "ТарифИдентификатор");
	Если ТекущийТариф = Неопределено Тогда
		Элементы.ГруппаПредупрежденияТарифа.Видимость = Ложь;
		Возврат;
	КонецЕсли;

	Элементы.ДекорацияПредупреждениеСтоимостьУслуги.Видимость = 
		ТекущийТариф.КартинкаТариф = 1 Или ТекущийТариф.КартинкаТариф = 3;
	Элементы.ДекорацияПредупреждениеУслугаНедоступна.Видимость = 
		ТекущийТариф.КартинкаТариф = 2 Или ТекущийТариф.КартинкаТариф = 3;
	Элементы.ДекорацияПредупреждениеНегабарит.Видимость = ТекущийТариф.Негабарит;
	Элементы.ДекорацияПредупреждениеНегабарит.Заголовок = ТекущийТариф.ОписаниеНеГабарит;
	Элементы.ДекорацияОписаниеТарифа.Видимость = ЗначениеЗаполнено(ТекущийТариф.Описание);
	Элементы.ДекорацияОписаниеТарифа.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		НСтр("ru = '<b>Информация по тарифу:</b>
			 |%1';
			 |en = '<b>Information on the service plan:</b>
			 |%1'"), ТекущийТариф.Описание);
	
КонецПроцедуры

// Обрабатывает результат менеджера длительных операций на сервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки
&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(Знач АдресРезультата)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		ОперацияВыполнена = Истина;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьУслугиТарифов() Тогда
			
			ЗагрузитьРезультатПолученияУслугТарифов(РезультатОперации, ОперацияВыполнена);
			Операция.Значение = ОперацияВыполнена;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку() Тогда
			
			ЗагрузитьРезультатПолученияЗаказаНаДоставку(РезультатОперации, ОперацияВыполнена);
			Операция.Значение = ОперацияВыполнена;
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыОбновитьЗаказНаДоставку() Тогда
			
			ЗагрузитьРезультатОбновленияЗаказа(РезультатОперации, ОперацияВыполнена);
			Если ОперацияВыполнена Тогда
				ЗагрузитьРезультатПолученияЗаказаНаДоставку(РезультатОперации, ОперацияВыполнена);
			КонецЕсли;
			Операция.Значение = ОперацияВыполнена;
		
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТарифы() Тогда
			
			ЗагрузитьРезультатПолученияТарифов(РезультатОперации, ОперацияВыполнена);
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьТариф() Тогда
			
			ЗагрузитьРезультатПолученияТарифа(РезультатОперации, ОперацияВыполнена);
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыОформитьЗаказНаДоставку() Тогда
			
			Операция.Значение = ЗагрузитьРезультатОформленияЗаказа(РезультатОперации, ОперацияВыполнена);
			
		ИначеЕсли ИмяМетода = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьДоступныеДляИзмененияРеквизиты() Тогда
			
			ОбработатьРезультатаПолученияДоступныхДляИзмененияРеквизитов(РезультатОперации, ОперацияВыполнена);
			Операция.Значение = ОперацияВыполнена;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьРезультатПолученияУслугТарифов(Результат, ОперацияВыполнена = Истина)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьУслугиТарифовДляТипаГрузоперевозки(Результат.Список);
	ЗагрузитьНесовместимыеУслуги(Результат.СписокНесовместимыеУслуги);
	СформироватьОтборыТарифов(Истина);
	СформироватьПараметрыОтбора();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияТарифов(Результат, ОперацияВыполнена)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ДоступныПериодыОтгрузки = Результат.ДоступныПериодыОтгрузки;
	МассивТарифовПоУмолчанию = Новый Массив();
		
	Для Каждого ТекущаяСтрока Из Результат.Список Цикл
		
		НоваяСтрока = Тарифы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока, , "ДополнительныеУслуги, ФормаОплаты");
		Если ТипЗнч(ТекущаяСтрока.ФормаОплаты) = Тип("Массив") Тогда
			НоваяСтрока.ФормаОплаты.ЗагрузитьЗначения(ТекущаяСтрока.ФормаОплаты);
		КонецЕсли;
		НоваяСтрока.Срок = СтрШаблон("%1 - %2", НоваяСтрока.МинимальныйСрок, НоваяСтрока.МаксимальныйСрок);
		
		Для Каждого ТекущаяУслуга Из ТекущаяСтрока.ДетализацияСтоимости Цикл
			НоваяУслуга = НоваяСтрока.ДетализацияСтоимостиПоУслугам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
			НоваяУслуга.ПлательщикРоль = ПлательщикУслуги(ТекущаяСтрока, ТекущаяУслуга);
			НоваяУслуга.ПлательщикРольПредставление = ПредставлениеПоРоли(НоваяУслуга.ПлательщикРоль);
		КонецЦикла;
		
		Для Каждого ТекущаяДопУслуга Из ТекущаяСтрока.ДополнительныеУслуги Цикл
			
			НоваяДопУслуга = НоваяСтрока.ДополнительныеУслуги.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяДопУслуга, ТекущаяДопУслуга, , "Свойства");
			
			Для Каждого ТекущееСвойство Из ТекущаяДопУслуга.Свойства Цикл
				НовоеСвойство = НоваяДопУслуга.Свойства.Добавить();
				ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
				Если ТекущееСвойство.Использовать = Истина Тогда
					НовоеСвойство.Значение = НовоеСвойство.ТипЗначения.ПривестиЗначение(НовоеСвойство.Значение);
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
		
		Для Каждого ТекущееСвойство Из ТекущаяСтрока.ПунктПриемаГруза Цикл
			ИмяСвойства = "ПунктПриемаГруза"+ТекущееСвойство.Ключ;
			Если НоваяСтрока.Свойство(ИмяСвойства) Тогда
				НоваяСтрока[ИмяСвойства] = ТекущееСвойство.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекущееСвойство Из ТекущаяСтрока.ПунктВыдачиГруза Цикл
			ИмяСвойства = "ПунктВыдачиГруза"+ТекущееСвойство.Ключ;
			Если НоваяСтрока.Свойство(ИмяСвойства) Тогда
				НоваяСтрока[ИмяСвойства] = ТекущееСвойство.Значение;
			КонецЕсли;
		КонецЦикла;
		
		НоваяСтрока.КартинкаТариф = 0;
		
		ЕстьУслугиНеВключенныеВТариф = (НоваяСтрока.ДетализацияСтоимостиПоУслугам.НайтиСтроки(
			Новый Структура("ВариантУчета", 3)).Количество());
		ЕстьУслугиНеВключенныеВРасчет = (НоваяСтрока.ДетализацияСтоимостиПоУслугам.НайтиСтроки(
			Новый Структура("ВариантУчета", 2)).Количество());
		
		Если ЕстьУслугиНеВключенныеВРасчет Тогда
			НоваяСтрока.КартинкаТариф = ?(ЕстьУслугиНеВключенныеВТариф, 3, 1);
		ИначеЕсли ЕстьУслугиНеВключенныеВТариф Тогда
			НоваяСтрока.КартинкаТариф = ?(ЕстьУслугиНеВключенныеВРасчет, 3, 2);
		КонецЕсли;

		Для Каждого ОписаниеСтрокиГрафика Из ТекущаяСтрока.ГрафикДоставки Цикл
			НоваяСтрокаГрафика = НоваяСтрока.ДанныеГрафикаДоставки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаГрафика, ОписаниеСтрокиГрафика);
		КонецЦикла;

		НоваяСтрока.ПоУмолчанию = ТекущаяСтрока.ПоУмолчанию;
		СформироватьПредставлениеТарифа(НоваяСтрока);
		
		Если НоваяСтрока.ГрузоперевозчикИдентификатор = ГрузоперевозчикИдентификатор
			И ТарифИдентификатор = ""
			И НоваяСтрока.ПоУмолчанию Тогда
			МассивТарифовПоУмолчанию.Добавить(НоваяСтрока.ПолучитьИдентификатор())
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивТарифовПоУмолчанию.Количество() = 1 Или Тарифы.Количество() = 1 Тогда
		
		Если МассивТарифовПоУмолчанию.Количество() = 1 Тогда
			ТарифПоУмолчанию = Тарифы.НайтиПоИдентификатору(МассивТарифовПоУмолчанию[0]);
		Иначе
			ТарифПоУмолчанию = Тарифы[0];
		КонецЕсли;
		
		ВыделенныйТариф = ТарифПоУмолчанию.ТарифИдентификатор;
		ОбновитьДанныеПоТарифу(ТарифПоУмолчанию);
		
	КонецЕсли;
	
	Если Кэш.ЭтоЯндексДоставка Тогда
		ДоступныеПериодыОтгрузки.Очистить();
		Если ДоступныПериодыОтгрузки Тогда
			Для Каждого СтрокаПериодаОтгрузки Из Результат.ДоступныеПериодыОтгрузки Цикл
				НоваяСтрока = ДоступныеПериодыОтгрузки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПериодаОтгрузки);
			КонецЦикла;
			ЗаполнитьСписокВыбораПериодовОтгрузки();
		КонецЕсли;
		ЗаполнитьСписокВыбораВремениОтгрузки();
	КонецЕсли;
	
	КоличествоСтрок = Тарифы.Количество();
	Если КоличествоСтрок = 0 Тогда
		СостояниеВыполненияЗапроса = НСтр("ru = 'Подходящие тарифы не найдены. Проверьте условия запроса.
			|Возможно для работы с перевозчиками требуется авторизация. Проверьте настройки перевозчиков.';
			|en = 'No matching service plans found. Check the request conditions.
			|The authorization may be required to work with carriers. Check your carrier settings.'");
	Иначе
		СостояниеВыполненияЗапроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Найдено подходящих тарифов: %1';
				|en = '%1 appropriate service plans were found'"),
			КоличествоСтрок);
	КонецЕсли;
	Элементы.ДекорацияСостояниеВыполненияЗапросаТарифы.Заголовок = СостояниеВыполненияЗапроса;
	
	Если ПустаяСтрока(ИдентификаторТекущегоТарифа) И Тарифы.Количество() > 0 Тогда
		ИдентификаторТекущегоТарифа = Тарифы[0].ТарифИдентификатор;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияТарифа(Результат, ОперацияВыполнена)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеУслугиИзменение = Ложь;
	
	Для Каждого ТекущаяСтрока Из Результат.Список Цикл
		
		Если Не (ТекущаяСтрока.ТарифИдентификатор = ТарифИдентификатор
			И ОтправительАдресШирота = ТекущаяСтрока.ТочкаОтправленияШирота
			И ОтправительАдресДолгота = ТекущаяСтрока.ТочкаОтправленияДолгота
			И ПолучательАдресШирота = ТекущаяСтрока.ТочкаДоставкиШирота
			И ПолучательАдресДолгота = ТекущаяСтрока.ТочкаДоставкиДолгота) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыТарифа = Тарифы.Выгрузить().Колонки;
		
		НоваяСтрока = Новый Структура();
		Для Каждого ТекущийПараметр Из ПараметрыТарифа Цикл
			НоваяСтрока.Вставить(ТекущийПараметр.Имя);
		КонецЦикла;
		
		НоваяСтрока.Вставить("ДетализацияСтоимости");
		НоваяСтрока.Вставить("ДополнительныеУслуги");
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		
		НоваяСтрока.Вставить("ФормаОплаты", Новый СписокЗначений);
		
		Если ТипЗнч(ТекущаяСтрока.ФормаОплаты) = Тип("Массив") Тогда
			НоваяСтрока.ФормаОплаты.ЗагрузитьЗначения(ТекущаяСтрока.ФормаОплаты);
		КонецЕсли;
		
		НоваяСтрока.Срок = "" + НоваяСтрока.МинимальныйСрок + " - " + НоваяСтрока.МаксимальныйСрок + "";
		НоваяСтрока.Вставить("ДетализацияСтоимостиПоУслугам", Новый Массив);
		
		Для Каждого ТекущаяУслуга Из ТекущаяСтрока.ДетализацияСтоимости Цикл
			НоваяУслуга = Новый Структура("Идентификатор, Наименование, Стоимость, ВариантУчета, Описание");
			ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
			НоваяСтрока.ДетализацияСтоимостиПоУслугам.Добавить(НоваяУслуга);
		КонецЦикла;
		
		НоваяСтрока.Вставить("ДополнительныеУслуги", Новый Массив());
		
		Для Каждого ТекущаяДопУслуга Из ТекущаяСтрока.ДополнительныеУслуги Цикл
	
			НоваяДопУслуга = Новый Структура("Идентификатор,Наименование,Обязательная,Категория,Использовать,ПоказыватьИнформацию");
			НоваяДопУслуга.Идентификатор = ТекущаяДопУслуга.Идентификатор;
			НоваяДопУслуга.Наименование = ТекущаяДопУслуга.Наименование;
			НоваяДопУслуга.Обязательная = ТекущаяДопУслуга.Обязательная;
			НоваяДопУслуга.Категория = ТекущаяДопУслуга.Категория;
			НоваяДопУслуга.Использовать = ТекущаяДопУслуга.Использовать;
			НоваяДопУслуга.ПоказыватьИнформацию = ТекущаяДопУслуга.ПоказыватьИнформацию;
			НоваяДопУслуга.Вставить("Свойства", Новый Массив);
			
			Для Каждого ТекущееСвойство Из ТекущаяДопУслуга.Свойства Цикл
				
				НовоеСвойство = Новый Структура("Идентификатор,Наименование,ЕдиницаИзмерения,ТипЗначения,Использовать,Значение");
				НовоеСвойство.Идентификатор = ТекущееСвойство.Идентификатор;
				НовоеСвойство.Наименование = ТекущееСвойство.Наименование;
				НовоеСвойство.ЕдиницаИзмерения = ТекущееСвойство.ЕдиницаИзмерения;
				НовоеСвойство.ТипЗначения = ТекущееСвойство.ТипЗначения;
				Если ТекущееСвойство.Свойство("Использовать")
					И ТекущееСвойство.Использовать Тогда
					НовоеСвойство.Использовать = ТекущееСвойство.Использовать;
					НовоеСвойство.Значение = НовоеСвойство.ТипЗначения.ПривестиЗначение(НовоеСвойство.Значение);
				КонецЕсли;
				
				НоваяДопУслуга.Свойства.Добавить(НовоеСвойство);
				
			КонецЦикла;
			
			НоваяСтрока.ДополнительныеУслуги.Добавить(НоваяДопУслуга);
			
		КонецЦикла;
		
		Для Каждого ТекущееСвойство Из ТекущаяСтрока.ПунктПриемаГруза Цикл
			ИмяСвойства = "ПунктПриемаГруза"+ТекущееСвойство.Ключ;
			Если НоваяСтрока.Свойство(ИмяСвойства) Тогда
				НоваяСтрока[ИмяСвойства] = ТекущееСвойство.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого ТекущееСвойство Из ТекущаяСтрока.ПунктВыдачиГруза Цикл
			ИмяСвойства = "ПунктВыдачиГруза"+ТекущееСвойство.Ключ;
			Если НоваяСтрока.Свойство(ИмяСвойства) Тогда
				НоваяСтрока[ИмяСвойства] = ТекущееСвойство.Значение;
			КонецЕсли;
		КонецЦикла;
		
		НеЗагружатьУслуги = Истина;
		ОбновитьДанныеПоТарифу(НоваяСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатСозданияЗаказа(Результат, ОперацияВыполнена)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ОперацияВыполнена = Ложь;
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗаказа = Результат["Идентификатор"];
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
	КонтрольныйЭлемент = Элементы.ГруппаСтраницы.ПодчиненныеЭлементы.ГруппаОсновное;
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован()
		И КонтрольныйЭлемент.Родитель.ТекущаяСтраница = КонтрольныйЭлемент Тогда
		ИзменениеЗарегистрированногоЗаказаЗавершение();
		Если Не ПустаяСтрока(Результат.ЧастичныйОтказИзменений) Тогда
			ОбщегоНазначения.СообщитьПользователю(Результат.ЧастичныйОтказИзменений);
			ПолучитьЗаказНаДоставкуВФоне();
		КонецЕсли;
	КонецЕсли;
	
	Если ОперацияВыполнена Тогда
		Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
			РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЧерновик();
		КонецЕсли;
		ДоступнаОтмена = Истина;
		СформироватьЗаголовокФормы();
		СформироватьИтоговуюИнформацию();
		ТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
		ГруппаТарифыГрузоперевозчикВидимость = Элементы.ГруппаТарифыГрузоперевозчик.Видимость;
		УстановитьВидимостьДоступность();
		СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
		Элементы.ГруппаТарифыГрузоперевозчик.Видимость = ГруппаТарифыГрузоперевозчикВидимость;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = ТекущаяСтраница;
	КонецЕсли;
	
	Если ОперацияВыполнена Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаСозданияИзмененияЗаказаНаДоставку(Результат, ДокументыОснования);
		СоздатьОбновитьДанныеЗаказаНаДоставку();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗагрузитьРезультатОформленияЗаказа(Результат, ОперацияВыполнена)
	
	РезультатОперации = 0;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат РезультатОперации;
	КонецЕсли;
	
	РезультатОперации = 1;
	
	Если ЗначениеЗаполнено(Результат.ДатаСозданияЗаказа) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован();
		СформироватьЗаголовокФормы();
		СформироватьИтоговуюИнформацию();
		УстановитьВидимостьДоступность();
		СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
		СформироватьПредставлениеДокументаОснования();
	Иначе
		РезультатОперации = 0;
	КонецЕсли;
	
	Если Результат.ОжидатьРасчета = Истина Тогда
		РезультатОперации = 2;
	КонецЕсли;
	
	Если РезультатОперации = 1 Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОформленияЗаказаНаДоставку(Результат, ДокументыОснования);
	КонецЕсли;
	
	Возврат РезультатОперации;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьРезультатОтменыЗаказа(Результат, РезультатВыполнения, СуммаОтмены)
	
	ОперацияВыполнена = Истина;
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ОперацияВыполнена = Ложь;
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ДокументОтменен = Истина Тогда
		РезультатВыполнения = 1;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		СформироватьИтоговуюИнформацию();
		ТекущаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
		УстановитьВидимостьДоступность();
		СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
		Элементы.ГруппаСтраницы.ТекущаяСтраница = ТекущаяСтраница;
	ИначеЕсли Результат.ДоступнаПлатнаяОтмена = Истина Тогда
		РезультатВыполнения = 2;
		СуммаОтмены = Результат.СуммаПлатнойОтмены;
	ИначеЕсли Результат.ДоступнаОтмена = Ложь Тогда
		РезультатВыполнения = 3;
	КонецЕсли;
			
	Если ОперацияВыполнена Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОтменыЗаказаНаДоставку(Результат, ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатОбновленияЗаказа(Результат, ОперацияВыполнена)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияВыполнена = ЗначениеЗаполнено(Результат.ДатаОбновления) И Результат.ЗаказОбновлен;
	
	Если Не ОперацияВыполнена Тогда
		ДанныеЗаказа = Результат.Данные;
		ВерсияПолученныхДанных = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ДанныеЗаказа);
		ОперацияВыполнена = ВерсияПолученныхДанных <> ВерсияДанных;
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Текущая версия заказа является актуальной.';
													|en = 'The current order version is relevant.'"));
	КонецЕсли;
	
	Если ОперацияВыполнена Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаОбновленияЗаказаНаДоставку(Результат, ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатПолученияЗаказаНаДоставку(Результат, ОперацияВыполнена = Истина)
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
	КонецЕсли;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЗаказа = Результат.Данные;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаказа) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЭтоСозданиеШаблона Тогда
		ОперацияВыполнена = Новый Структура;
		ОперацияВыполнена.Вставить("Значение", ЗначениеВСтрокуВнутр(ДанныеЗаказа));
		ОперацияВыполнена.Вставить("ЭтоСозданиеШаблона", Истина);
		Возврат;
	КонецЕсли;
	
	ВерсияДанных = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ДанныеЗаказа);
	
	Если ЭтоЗаполнениеКопированием Тогда
		РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый();
	Иначе
		РежимМастера = ?(ДанныеЗаказа.СостояниеИдентификатор = 0,
			СервисДоставкиКлиентСервер.РежимМастераЧерновик(),
			СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован());
	КонецЕсли;
	
	ОбработатьПараметры(ДанныеЗаказа);
	ОбработатьПредставлениеАдресаПриНеобходимости();
	УслугиТарифа.Очистить();
	Если ДанныеЗаказа.Услуги.Количество() > 0 Тогда
		
		Для Каждого ТекущаяУслуга Из ДанныеЗаказа.Услуги Цикл
			
			НоваяУслуга = УслугиТарифа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
			НоваяУслуга.Использовать = Истина;
			НайденныеСтроки = УслугиТарифов.НайтиСтроки(Новый Структура("Идентификатор", НоваяУслуга.Идентификатор));
			
			ТекущаяУслугаТарифов = Неопределено;
			Если НайденныеСтроки.Количество() Тогда
				ТекущаяУслугаТарифов = НайденныеСтроки[0];
				ЗаполнитьЗначенияСвойств(ТекущаяУслугаТарифов, НоваяУслуга,,"Свойства");
				ТекущаяУслугаТарифов.Свойства.Очистить();
			КонецЕсли;
			
			Для Каждого ТекущееСвойство Из ТекущаяУслуга.СвойстваУслуги Цикл
				НовоеСвойство = НоваяУслуга.Свойства.Добавить();
				ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
				Если ТекущаяУслугаТарифов <> Неопределено Тогда
					НовоеСвойствоУслугиТарифов = ТекущаяУслугаТарифов.Свойства.Добавить();
					ЗаполнитьЗначенияСвойств(НовоеСвойствоУслугиТарифов, ТекущееСвойство);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если РежимМастера < СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() 
		И ДанныеЗаказа.ДополнительныеУслуги.Количество() > 0 Тогда
		
		Для Каждого ТекущаяУслуга Из ДанныеЗаказа.ДополнительныеУслуги Цикл
			
			НоваяУслуга = ДополнительныеУслугиТарифа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
			НайденныеСтроки = УслугиТарифов.НайтиСтроки(Новый Структура("Идентификатор", НоваяУслуга.Идентификатор));
			
			ТекущаяУслугаТарифов = Неопределено;
			Если НайденныеСтроки.Количество() Тогда
				ТекущаяУслугаТарифов = НайденныеСтроки[0];
				ЗаполнитьЗначенияСвойств(ТекущаяУслугаТарифов, НоваяУслуга,,"Свойства");
				ТекущаяУслугаТарифов.Свойства.Очистить();
			КонецЕсли;
			
			Для Каждого ТекущееСвойство Из ТекущаяУслуга.СвойстваУслуги Цикл
				НовоеСвойство = НоваяУслуга.Свойства.Добавить();
				ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
				Если ТекущаяУслугаТарифов <> Неопределено Тогда
					НовоеСвойствоУслугиТарифов = ТекущаяУслугаТарифов.Свойства.Добавить();
					ЗаполнитьЗначенияСвойств(НовоеСвойствоУслугиТарифов, ТекущееСвойство);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		СформироватьДопУслугиПоТарифу(ДополнительныеУслугиТарифа);
		ОбновитьИнформациюПоВыбраннымДопУслугам();
		Элементы.ГруппаПредупрежденияПоДопУслугам.Видимость = ПроверитьЗаполнениеЗначенийДополнительныхУслуг(ЭтотОбъект);
		
	КонецЕсли;
	
	ТоварныйСостав.Загрузить(ДанныеЗаказа.Товары);
	
	ОплатаСтоимостьУслуг.Очистить();
	Для Каждого Строка Из ДанныеЗаказа.ПлательщикиУслуг Цикл
		ЗаполнитьЗначенияСвойств(ОплатаСтоимостьУслуг.Добавить(), Строка);
	КонецЦикла;
	
	СписокДокументов.Очистить();
	Если ДанныеЗаказа.Документы.Количество() > 0 Тогда
		Для Каждого ТекущийДокумент Из ДанныеЗаказа.Документы Цикл
			НовыйДокумент = СписокДокументов.Добавить();
			ЗаполнитьЗначенияСвойств(НовыйДокумент, ТекущийДокумент);
			НовыйДокумент.Дата = ПрочитатьДатуJSON(ТекущийДокумент.Дата, ФорматДатыJSON.ISO);
		КонецЦикла;
	КонецЕсли;
	
	ДополнительныеДанные.Очистить();
	Если ДанныеЗаказа.ДополнительныеДанныеЗаказа.Количество() > 0 Тогда
		
		Для Каждого ТекущиеДанные Из ДанныеЗаказа.ДополнительныеДанныеЗаказа Цикл
			НоваяСтрока = ДополнительныеДанные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные,,"Список");
			Для Каждого ТекущийЭлементСписка Из ТекущиеДанные.Список Цикл
				НовыйЭлемент = НоваяСтрока.Список.Добавить();
				ЗаполнитьЗначенияСвойств(НовыйЭлемент, ТекущийЭлементСписка);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
		
	ПереключательРасшифровки = ?(ГрузКоличествоГрузовыхМест = 1, 0, 1);
	СформироватьПредставлениеДокументаОснования();
	УстановитьВидимостьДоступность();
	СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
	СформироватьЗаголовокФормы();
	
	ДанныеПропуска = Новый Структура;
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(СервисДоставкиКлиентСервер.НовыйПараметрыПропуска(), ДанныеПропуска, "Пропуск");
	ЗаполнитьЗначенияСвойств(ДанныеПропуска, ЭтотОбъект);
	ЗаполнитьПредставлениеПропуска(ДанныеПропуска);
	
	ВремяОтгрузкиПредставление = ПредставлениеДатыВремени(ДатаОтгрузки,
									ВремяОтгрузкиС,
									ВремяОтгрузкиПо,
									ВремяОтгрузкиОбедС,
									ВремяОтгрузкиОбедПо,
									ВремяОтгрузкиФиксированное,
									1);
	
	ВремяДоставкиПредставление = ПредставлениеДатыВремени(ДатаДоставки,
									ВремяДоставкиС,
									ВремяДоставкиПо,
									ВремяДоставкиОбедС,
									ВремяДоставкиОбедПо,
									ВремяДоставкиФиксированное,
									2);
	
	Если ЗначениеЗаполнено(ТарифИдентификатор) Тогда
		СформироватьОтборыТарифов(Истина);
		ЗаполнитьИнформациюПоТекущемуТарифу();
	КонецЕсли;
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		СформироватьИтоговуюИнформацию();
	КонецЕсли;
	
	Если ОперацияВыполнена Тогда
		СервисДоставкиПереопределяемый.ОбработатьРезультатЗапросаПолученияЗаказаНаДоставку(Результат, ДокументыОснования);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатУстановкиТарифаПоУмолчанию(Результат, ОперацияВыполнена)
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
	КонецЕсли;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияВыполнена = Результат.ОперацияВыполнена;
	
	НайденныеСтроки = 
		Тарифы.НайтиСтроки(Новый Структура("ГрузоперевозчикИдентификатор", Результат.ГрузоперевозчикИдентификатор));
	Для Каждого ТекущийТариф Из НайденныеСтроки Цикл
		Если ТекущийТариф.ПоУмолчанию Тогда
			ТекущийТариф.ПоУмолчанию = Ложь;
			СформироватьПредставлениеТарифа(ТекущийТариф);
		КонецЕсли;
	КонецЦикла;
	
	НайденныеСтроки = Тарифы.НайтиСтроки(Новый Структура("ТарифИдентификатор", Результат.ТарифИдентификатор));
	Если НайденныеСтроки.Количество() Тогда
		НайденныеСтроки[0].ПоУмолчанию = Истина;
		СформироватьПредставлениеТарифа(НайденныеСтроки[0]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьРезультатСбросаТарифаПоУмолчанию(Результат, ОперацияВыполнена)
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
	КонецЕсли;
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияВыполнена = Результат.ОперацияВыполнена;
	
	Если Результат.Свойство("ГрузоперевозчикИдентификатор") Тогда
		НайденныеСтроки = Тарифы.НайтиСтроки(Новый Структура("ГрузоперевозчикИдентификатор", Результат.ГрузоперевозчикИдентификатор));
		Для Каждого ТекущийТариф Из НайденныеСтроки Цикл
			Если ТекущийТариф.ПоУмолчанию Тогда
				ТекущийТариф.ПоУмолчанию = Ложь;
				СформироватьПредставлениеТарифа(ТекущийТариф);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатаПолученияДоступныхДляИзмененияРеквизитов(Результат, ОперацияВыполнена)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ОперацияВыполнена = Ложь;
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	КлючиРеквизитовДляИзменения.Очистить();
	ДоступныеЭлементы = Новый Массив;
	
	СписокДоступных = ЭлементыФормыДляИзменения(Результат.ДоступностьРеквизитов);
	Для Каждого ТекущийЭлементФормы Из СписокДоступных Цикл
		ТекЭлемент = Элементы.Найти(ТекущийЭлементФормы.Значение);
		Если ТекЭлемент <> Неопределено Тогда
			ДоступныеЭлементы.Добавить(ТекЭлемент);
		КонецЕсли;
	КонецЦикла;
	ДоступныеЭлементы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДоступныеЭлементы);
	
	Если ДоступныеЭлементы.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Нет реквизитов, доступных для редактирования.';
													|en = 'No attributes available for editing.'"));
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущийЭлементФормы Из ДоступныеЭлементы Цикл
		ПодчиненныеПоляВводаТолькоПросмотр(ТекущийЭлементФормы, Ложь);
	КонецЦикла;
	
	Для Каждого ТекущийРеквизитФормы Из Результат.ДоступностьРеквизитов Цикл
		Если ТекущийРеквизитФормы.Значение Тогда
			КлючиРеквизитовДляИзменения.Добавить(ТекущийРеквизитФормы.Ключ);
		КонецЕсли;
	КонецЦикла;
	РежимИзмененияРеквизитов(КлючиРеквизитовДляИзменения.Количество() > 0);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область РедактированиеРеквизитовЗаказа

&НаСервере
Процедура РежимИзмененияРеквизитов(Знач Разрешено)
	
	Если Не Разрешено Тогда
		КлючиРеквизитовДляИзменения.Очистить();
	КонецЕсли;
	
	УстановитьДоступноеДействие(ИмяДействияПриИзменении(), Разрешено, Разрешено);
	
	Если Разрешено Тогда
		ЦелеваяСтраница = Элементы.ГруппаОсновное;
		Для каждого ЭлемСтраница Из ЦелеваяСтраница.Родитель.ПодчиненныеЭлементы Цикл
			Если ЭлемСтраница = ЦелеваяСтраница ИЛИ ЭлемСтраница = Элементы.ГруппаКарточка Тогда
				Продолжить;
			Иначе
				ЭлемСтраница.Доступность = Ложь;
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЦелеваяСтраница = Элементы.ГруппаКарточка;
	КонецЕсли;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница <> ЦелеваяСтраница Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = ЦелеваяСтраница;
	КонецЕсли;
	
	СформироватьСтрокуПредставлениеСостоянияЗаказаФормы();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
// Возвращаемое значение:
//	Структура:
//	*Ключ - Строка - ключ группы реквизитов для редактирования
//	*Значение - СписокЗначений:
//	**Значение - имя реквизита формы для разрешения
//	**Пометка - контроль заполнения связанного реквизита
Функция ГруппыРеквизитовПоКлючам()

	Результат = Новый Структура;
	
	// ПлательщикАдрес
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПлательщикАдрес");
	Результат.Вставить("ПлательщикАдрес", ТекЭлементыФормы);
	
	// ПлательщикКонтактнаяИнформация
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПлательщикКонтактноеЛицо");
	ТекЭлементыФормы.Добавить("ПлательщикКонтактноеЛицоТелефон");
	ТекЭлементыФормы.Добавить("ПлательщикКонтактноеЛицоТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ПлательщикДобавитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ПлательщикУдалитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ГруппаПлательщикТелефон");
	Результат.Вставить("ПлательщикКонтактнаяИнформация", ТекЭлементыФормы);
	
	// ПлательщикКонтрагент
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПлательщикРоль",, Истина);
	ТекЭлементыФормы.Добавить("ПлательщикКонтрагент");
	Результат.Вставить("ПлательщикКонтрагент", ТекЭлементыФормы);
	
	// ПолучательАдрес
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПолучательАдрес",, Истина);
	Результат.Вставить("ПолучательАдрес", ТекЭлементыФормы);
	
	// ПолучательКонтактнаяИнформация
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПолучательКонтактноеЛицо",, Истина);
	ТекЭлементыФормы.Добавить("ПолучательКонтактноеЛицоТелефон",, Истина);
	ТекЭлементыФормы.Добавить("ПолучательКонтактноеЛицоТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ПолучательДобавитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ПолучательУдалитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ГруппаПолучательТелефон");
	Результат.Вставить("ПолучательКонтактнаяИнформация", ТекЭлементыФормы);
	
	// ПолучательКонтрагент
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПолучательКонтрагент",, Истина);
	Результат.Вставить("ПолучательКонтрагент", ТекЭлементыФормы);
	
	// ПолучательДатаВремяДоставки
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ПолучательВремяПлановойДоставки");
	Результат.Вставить("ПолучательДатаВремяДоставки", ТекЭлементыФормы);
	
	// ОтправительАдрес
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ОтправительАдрес",, Истина);
	Результат.Вставить("ОтправительАдрес", ТекЭлементыФормы);
	
	// ОтправительКонтактнаяИнформация
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ОтправительКонтактноеЛицо",, Истина);
	ТекЭлементыФормы.Добавить("ОтправительКонтактноеЛицоТелефон",, Истина);
	ТекЭлементыФормы.Добавить("ОтправительКонтактноеЛицоТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ОтправительДобавитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ОтправительУдалитьТелефонДополнительный");
	ТекЭлементыФормы.Добавить("ГруппаОтправительТелефон");
	Результат.Вставить("ОтправительКонтактнаяИнформация", ТекЭлементыФормы);
	
	// ОтправительДатаВремяОтгрузки 
	ТекЭлементыФормы = Новый СписокЗначений;
	ТекЭлементыФормы.Добавить("ОтправительВремяПлановойОтгрузки");
	Результат.Вставить("ОтправительДатаВремяОтгрузки", ТекЭлементыФормы);
			
	Возврат Результат;
	
КонецФункции 

&НаСервере
Процедура ДоступныеДляИзмененияРеквизитыТолькоПросмотр(Знач ТолькоПросмотр)
	
	Элементы.ГруппаОснование.ТолькоПросмотр = ТолькоПросмотр;
	Элементы.ГруппаДополнительно.ТолькоПросмотр = ТолькоПросмотр;
	
	ПодчиненныеПоляВводаТолькоПросмотр(Элементы.ГруппаРеквизиты, ТолькоПросмотр);
	
КонецПроцедуры

&НаСервере
Процедура ПодчиненныеПоляВводаТолькоПросмотр(ЭлементФормы, Знач ТолькоПросмотр)
	
	ЦветРедактирования = РедактируемыйРеквизитЦветФона;
	ЦветНейтральный = Новый Цвет();
	
	КоллекцияПолей = Новый Массив;
	ПолучитьПодчиненныеЭлементыФормы(КоллекцияПолей, ЭлементФормы, Тип("ПолеФормы"));
	ПолучитьПодчиненныеЭлементыФормы(КоллекцияПолей, ЭлементФормы, Тип("ДекорацияФормы"));
	
	Для каждого ТекущийЭлементФормы Из КоллекцияПолей Цикл
		Если ТипЗнч(ТекущийЭлементФормы) = Тип("ПолеФормы") Тогда
			ТекущийЭлементФормы.ТолькоПросмотр = ТолькоПросмотр;
			Если НЕ ТолькоПросмотр Тогда
				ТекущийЭлементФормы.ЦветФона = ЦветРедактирования;
			ИначеЕсли ТолькоПросмотр И ТекущийЭлементФормы.ЦветФона = ЦветРедактирования Тогда
				ТекущийЭлементФормы.ЦветФона = ЦветНейтральный;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ТекущийЭлементФормы) = Тип("ДекорацияФормы") Тогда
			ТекущийЭлементФормы.Доступность = НЕ ТолькоПросмотр;
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
// Получает коллекцию подчиненных элементов определенного типа (кроме групп) 
// Параметры:
// 	КоллекцияЭлементов - Массив из ЭлементыФормы -
//	КорневойЭлемент - ЭлементФормы - исходный элемент формы, внутри которого нужно искать
//	ТипЭлемента - ТипЗначения - тип искомых элементов. Негрупповые элементы.
Процедура ПолучитьПодчиненныеЭлементыФормы(КоллекцияЭлементов, Знач КорневойЭлемент, Знач ТипЭлемента)
	
	Если ТипЗнч(КоллекцияЭлементов) <> Тип("Массив") Тогда
		КоллекцияЭлементов = Новый Массив;
	КонецЕсли;
	
	Если ТипЗнч(КорневойЭлемент) = ТипЭлемента Тогда
	
		КоллекцияЭлементов.Добавить(КорневойЭлемент);
		
	ИначеЕсли ТипЗнч(КорневойЭлемент) = Тип("ГруппаФормы") Тогда
	
		Для каждого ТекущийЭлементФормы Из КорневойЭлемент.ПодчиненныеЭлементы Цикл
			
			Если ТипЗнч(ТекущийЭлементФормы) = Тип("ГруппаФормы") Тогда
				ПолучитьПодчиненныеЭлементыФормы(КоллекцияЭлементов, ТекущийЭлементФормы, ТипЭлемента);
			ИначеЕсли ТипЗнч(ТекущийЭлементФормы) = ТипЭлемента Тогда
				КоллекцияЭлементов.Добавить(ТекущийЭлементФормы);
			КонецЕсли; 
			
		КонецЦикла; 
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьГруппуДоступныхДействий(Элементы)

	Возврат Элементы.ГруппаДоступныеДействия;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Возвращаемый результат:
// СписокЗначений:
//	*Значение - Строка - имя элемента формы, разрешенного к редактированию
//	*Пометка - Булево - флаг обязательности заполнения связанного реквизита формы
Функция ЭлементыФормыДляИзменения(ДоступныеГруппыРеквизитов)
	
	Результат = Новый СписокЗначений;
	
	СтруктураГрупп = ГруппыРеквизитовПоКлючам();
	
	Для Каждого ТекущийЭлементФормы Из ДоступныеГруппыРеквизитов Цикл
		
		Если Не ТекущийЭлементФормы.Значение Тогда
			Продолжить;
		КонецЕсли;
		
		ГруппыРеквизитов = Новый СписокЗначений;
		Если СтруктураГрупп.Свойство(ТекущийЭлементФормы.Ключ, ГруппыРеквизитов) Тогда
			Для каждого ЭлемСписка Из ГруппыРеквизитов Цикл
				Если Результат.НайтиПоЗначению(ЭлемСписка.Значение) = Неопределено Тогда
					Результат.Добавить(ЭлемСписка.Значение,, ЭлемСписка.Пометка);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
// Возвращаемый результат:
// СписокЗначений:
//	*Значение - Строка - имя реквизита формы
//	*Представление - Строка - синоним реквизита формы
Функция РеквизитыФормыДляИзменения(ДоступныеГруппыРеквизитов)
	
	Результат = Новый СписокЗначений;
	
	КонтрольныеРеквизитыФормы = ЭлементыФормыДляИзменения(ДоступныеГруппыРеквизитов);
	ВсеРеквизиты = ПолучитьРеквизиты();
	
	Для каждого ОписаниеЭлемента Из КонтрольныеРеквизитыФормы Цикл
		
		Если НЕ ОписаниеЭлемента.Пометка Тогда
			Продолжить;
		КонецЕсли; 
		
		ТекЭлементФормы = Элементы.Найти(ОписаниеЭлемента.Значение);
		Если ТекЭлементФормы = Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		
		ИмяРеквизита = ТекЭлементФормы.ПутьКДанным;
		
		Для каждого ЭлемРеквизит Из ВсеРеквизиты Цикл
			Если ЭлемРеквизит.Имя = ИмяРеквизита Тогда
				РеквизитПредставление = ?(ПустаяСтрока(ЭлемРеквизит.Заголовок),ИмяРеквизита,ЭлемРеквизит.Заголовок);
				Результат.Добавить(ИмяРеквизита, РеквизитПредставление);
				Прервать;
			КонецЕсли; 
		КонецЦикла; 
		
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьГруппуДействийПоИмениДействия(Знач ИмяДействия)
	
	ПараметрыДействия = Неопределено;
	Если НастройкиДоступныхДействий.Свойство(ИмяДействия, ПараметрыДействия) Тогда
		
		Возврат Элементы[ПараметрыДействия.ИмяЭлемента];
		
	КонецЕсли; 
	
	Возврат Неопределено;

КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоступныхДействий()
	
	ГруппаДоступныеДействия = ПолучитьГруппуДоступныхДействий(Элементы);
	ГруппаДействияПриИзменении = ПолучитьГруппуДействийПоИмениДействия(ИмяДействияПриИзменении());
	
	// Скорректируем настройки (устраним возможные противоречия)
	ЭлементОтменитьЗаказ = НастройкиДоступныхДействий[ИмяДействияОтменитьЗаказ()];
	ЭлементОтменитьЗаказ.Видимость = ДоступнаОтмена;
	ЭлементОтменитьЗаказ.Доступность = ДоступнаОтмена;
	
	Если ДоступноРедактированиеПослеОформления Тогда
		ВПроцессеИзменения = КлючиРеквизитовДляИзменения.Количество() > 0;
		НастройкиДоступныхДействий[ИмяДействияИзменить()].Видимость = НЕ ВПроцессеИзменения;
		НастройкиДоступныхДействий[ИмяДействияИзменить()].Доступность = НЕ ВПроцессеИзменения;
		НастройкиДоступныхДействий[ИмяДействияПриИзменении()].Видимость = ВПроцессеИзменения;
		НастройкиДоступныхДействий[ИмяДействияПриИзменении()].Доступность = ВПроцессеИзменения;
	Иначе
		НастройкиДоступныхДействий[ИмяДействияИзменить()].Видимость = Ложь;
		НастройкиДоступныхДействий[ИмяДействияПриИзменении()].Видимость = Ложь;
	КонецЕсли;
	
	Для каждого ТекущееДействие Из НастройкиДоступныхДействий Цикл
		ЗаполнитьЗначенияСвойств(Элементы[ТекущееДействие.Значение.ИмяЭлемента], ТекущееДействие.Значение, "Видимость, Доступность");
	КонецЦикла;
	
	ЕстьВидимыеПодчиненные = Ложь;
	Для каждого ТекущееДействие Из ГруппаДоступныеДействия.ПодчиненныеЭлементы Цикл
		Если ТекущееДействие.Видимость Тогда
			ЕстьВидимыеПодчиненные = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ГруппаДоступныеДействия.Видимость = ЕстьВидимыеПодчиненные И НЕ ГруппаДействияПриИзменении.Видимость;
	
	Элементы.Печать.Доступность = НЕ ГруппаДействияПриИзменении.Видимость;
	Элементы.Обновить.Доступность = НЕ ГруппаДействияПриИзменении.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступноеДействие(Знач ИмяДействия, Знач Видимость, Знач Доступность)
	
	ТекНастройка = Неопределено;
	Если НЕ НастройкиДоступныхДействий.Свойство(ИмяДействия, ТекНастройка) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Элементы[ТекНастройка.ИмяЭлемента], ТекНастройка);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
// Возвращаемое значение:
//	Структура:
//	*ИмяДействия - Структура - имя доступного действия:
//	**ИмяЭлемента - Строка - имя элемента формы, содержащий команды действия(-й)
//	**Видимость - Булево - признак видимости элементов
Функция ПолучитьВсеДоступныеДействия()
	
	Результат = Новый Структура;
	ПараметрыДействияШаблон = Новый Структура("ИмяЭлемента, Видимость, Доступность", "", Ложь, Ложь);
	
	КлючДействия = ИмяДействияИзменить();
	ПараметрыДействия = СкопироватьРекурсивно(ПараметрыДействияШаблон);
	ПараметрыДействия.Видимость = Истина;
	ПараметрыДействия.Доступность = Истина;
	Результат.Вставить(КлючДействия, ПараметрыДействия);
	
	КлючДействия = ИмяДействияОтменитьЗаказ();
	ПараметрыДействия = СкопироватьРекурсивно(ПараметрыДействияШаблон);
	Результат.Вставить(КлючДействия, ПараметрыДействия);
	
	КлючДействия = ИмяДействияОтменитьДоставкуЗаказа();
	ПараметрыДействия = СкопироватьРекурсивно(ПараметрыДействияШаблон);
	Результат.Вставить(КлючДействия, ПараметрыДействия);
	
	КлючДействия = ИмяДействияПриИзменении();
	ПараметрыДействия = СкопироватьРекурсивно(ПараметрыДействияШаблон);
	Результат.Вставить(КлючДействия, ПараметрыДействия);
	
	Для каждого ТекущийЭлемент Из Результат Цикл
		Если НЕ ЗначениеЗаполнено(ТекущийЭлемент.Значение.ИмяЭлемента) Тогда
			ТекущийЭлемент.Значение.Вставить("ИмяЭлемента", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"Группа%1",
				ТекущийЭлемент.Ключ));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДействияИзменить()
	Возврат "ДоступныеДействияИзменить";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДействияОтменитьЗаказ()
	Возврат "ДоступныеДействияОтменитьЗаказ";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДействияОтменитьДоставкуЗаказа()
	Возврат "ДоступныеДействияОтменитьДоставкуЗаказа";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДействияПриИзменении()
	Возврат "ДействияПриИзменении";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьВремяСОкруглением(Дата)
	
	ВремяЧасы   = Час(Дата);
	ВремяМинуты = Минута(Дата);
	
	Если ВремяМинуты = 0 Тогда
		ВремяМинуты = "30";
	ИначеЕсли ВремяМинуты <= 30 Тогда
		ВремяМинуты = "00";
		ВремяЧасы = ВремяЧасы + 1;
	Иначе
		ВремяМинуты = "30";
		ВремяЧасы = ВремяЧасы + 1;
	КонецЕсли;
	
	Если ВремяЧасы > 23 Тогда
		Возврат Дата("00010101235959");
	КонецЕсли;
	
	ВремяЧасы = Строка(ВремяЧасы);
	
	Возврат Дата("00010101" + ?(СтрДлина(ВремяЧасы) = 1, "0", "") + ВремяЧасы + ВремяМинуты + "00");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыОперацииПолучитьЗаказНаДоставку()
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку());
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Получение данных по заказу на доставку.';
											|en = 'Receive delivery order data.'");
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Истина;
	
	Возврат ПараметрыОперации;
	
КонецФункции

#КонецОбласти

#Область Шаблоны

&НаКлиенте
Процедура СозданиеШаблонаПродолжение(Результат, СоздатьИзменить) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("ЭтоСозданиеШаблона", Истина);
		
		ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
		Если СоздатьИзменить Тогда
			ПараметрыОперации.Очередь.Добавить(ПараметрыЗапроса, СервисДоставкиКлиентСервер.ИмяПроцедурыСоздатьИзменитьЗаказНаДоставку());
		КонецЕсли;
		ПараметрыОперации.Очередь.Добавить(ПараметрыЗапроса, СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьЗаказНаДоставку());
		
		ВыполнитьЗапрос(ПараметрыОперации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораШаблона(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ЗаполнитьПоШаблонуНаСервере(Результат);
		СброситьТариф();
		УстановитьВидимостьДоступностьНаСтраницеОсновная();
		ПроверитьЗаполнениеОбязательныхРеквизитов(4, Ложь, , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоШаблонуНаСервере(Знач ПараметрыШаблона)
	
	ПараметрыЗаказа = СервисДоставки.ПолучитьПараметрыЗаказаИзШаблона(ПараметрыШаблона);
	
	ОбработатьПараметры(ПараметрыЗаказа);
	СформироватьПредставлениеДокументаОснования();
	УстановитьДатуОтгрузки(ДатаОтгрузки);
	
	Если ОрганизацияБизнесСетиСсылка = ОтправительКонтрагентСсылка Тогда
		ЗаказчикРоль = 1;
	ИначеЕсли ОрганизацияБизнесСетиСсылка = ПолучательКонтрагентСсылка Тогда
		ЗаказчикРоль = 2;
	ИначеЕсли ОрганизацияБизнесСетиСсылка = ПлательщикКонтрагентСсылка Тогда
		ЗаказчикРоль = 3;
	Иначе
		ЗаказчикРоль = 4;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		СоздатьИзменитьЗаказНаДоставку(, Истина);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	РедактируемыйРеквизитЦветФона = ЦветаСтиля.ДобавленныйРеквизитФон;
	
	// Категории отборов тарифов.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтборыТарифовИспользовать.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// Категории отборов тарифов.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтборыТарифовНаименование.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	
	// Значение отборов тарифов.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтборыТарифовИспользовать.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 2;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	// Значение отборов тарифов.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтборыТарифов.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 2;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.НаклонныйТекстСервисДоставки);
	
	// Значение отборов тарифов.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтборыТарифовЗначение.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ТребуетсяЗначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.Значение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Пусто>';
																					|en = '<Blank>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Центр);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);

	// Время отгрузки.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОтправительВремяПлановойОтгрузки.Имя);
	ОтборГруппа = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ОтправительВремяОтгрузкиПо");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "";
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОтборыТарифов.ОтправительВремяОтгрузкиС");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "";
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	// Тарифы "Текущий тариф".
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Тарифы.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тарифы.ТарифИдентификатор");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ВыделенныйТариф");
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тарифы.ТочкаОтправленияШирота");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ОтправительАдресШирота");
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тарифы.ТочкаДоставкиШирота");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ПолучательАдресШирота");
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона",
		ЦветаСтиля.ЦветФонаКнопкаПоУмолчаниюСервисДоставки);
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("ПрименитьОтборы");

	/////////////////////////
	
	// Категории дополнительных услуг.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОборотыДопУслугИспользовать.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

	// Категории дополнительных услуг.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОборотыДопУслугНаименование.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	
	// Значение дополнительных услуг.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОборотыДопУслугИспользовать.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 2;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

	// Значение дополнительных услуг.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОборотыДопУслугЗначение.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.ТребуетсяЗначение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.Значение");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Пусто>';
																					|en = '<Blank>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Центр);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);

	// Значение дополнительных услуг.
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОборотыДопУслугИспользовать.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.Обязательная");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифДополнительныеУслуги.ТипСтроки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 1;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставление.Имя);
	ОтборГруппа = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ОтборГруппа.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тарифы.ДетализацияСтоимостиПоУслугам.Стоимость");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ОтборЭлемента = ОтборГруппа.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Тарифы.ДетализацияСтоимостиПоУслугам.Идентификатор");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "";
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);  

КонецПроцедуры

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ЭтоЯндексДоставка", СервисДоставкиКлиентСервер.ЭтоЯндексДоставка(ТипГрузоперевозки));
	Кэш.Вставить("ЭтоДеловыеЛинии", Не Кэш.ЭтоЯндексДоставка);
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("ВариантыВремениОтгрузки", СервисДоставкиПовтИсп.ВариантыВремениОтгрузки());
	Кэш.Вставить("СписокХарактеровГруза", Новый СписокЗначений);
	Кэш.Вставить("ОписаниеХарактеровГруза", Новый Соответствие);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьЗаказ(ЗакрытьПослеЗавершенияОперации = Ложь)
	
	ОчиститьСообщения();
	
	Отказ = Ложь;
	ПроверитьЗаполнениеОбязательныхРеквизитов(5, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Элементы.ПерейтиПроверка.Пометка Тогда
		ПерейтиКШагуПроверка();
	ИначеЕсли Модифицированность Или ИдентификаторЗаказа = "" Тогда
		СоздатьИзменитьЗаказНаДоставку(Истина, ЗакрытьПослеЗавершенияОперации);
	Иначе
		ОформитьЗаказНаДоставку(, ЗакрытьПослеЗавершенияОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметры(Параметры, Префикс = "")
	
	ПараметрыДляФормы = Новый Структура;
	
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(Параметры, ПараметрыДляФормы, Префикс);
	
	ПараметрыДляФормы.Удалить("ТоварныйСостав");
	ПараметрыДляФормы.Удалить("ГрузовыеМеста");
	Если ПараметрыДляФормы.Свойство("ГрузоперевозчикИдентификатор") И ПараметрыДляФормы.ГрузоперевозчикИдентификатор = "" Тогда
		ПараметрыДляФормы.Удалить("ГрузоперевозчикИдентификатор");
		ПараметрыДляФормы.Удалить("ГрузоперевозчикНаименование");
	КонецЕсли;
	Если Не ПустаяСтрока(ДополнительнаяИнформация) Тогда
		ПараметрыДляФормы.Удалить("ДополнительнаяИнформация");
	КонецЕсли;

	Если ПараметрыДляФормы.Свойство("ТипГрузоперевозки") Тогда
		Если ПараметрыДляФормы.ТипГрузоперевозки = 0 Тогда
			ПараметрыДляФормы.ТипГрузоперевозки = ?(ТипГрузоперевозки = 0,
				СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка(), ТипГрузоперевозки);
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыДляФормы);
	УстановитьПризнакОрганизаций();
	ЗаполнитьЗначенияСписковВыбора();
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформации();
	
	Если Префикс = "" И ГрузКоличествоГрузовыхМест = 1 Тогда
		ГрузМаксимальныйВес = ГрузВес;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьПредставлениеАдресаПриНеобходимости()

	Если Не Кэш.ЭтоДеловыеЛинии Или Не РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Адресаты = Новый Массив;
	Адресаты.Добавить("Отправитель");
	Адресаты.Добавить("Получатель");
	Адресаты.Добавить("ОтправительКонтрагентЮридический");
	Адресаты.Добавить("ПолучательКонтрагентЮридический");
	Адресаты.Добавить("ПлательщикКонтрагентЮридический");
	Для Каждого Адресат Из Адресаты Цикл
		ЗаменитьПредставлениеАдресаВАдресате(Адресат);	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольЗаполненияРеквизитовОформленногоЗаказа(Отказ, ВыводитьПредупреждения = Истина)

	Если РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован()
		ИЛИ КлючиРеквизитовДляИзменения.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ОчиститьСообщения();
	
	СтруктураКлючей = Новый Структура;
	Для каждого ТекущийКлючРеквизита Из КлючиРеквизитовДляИзменения Цикл
		СтруктураКлючей.Вставить(ТекущийКлючРеквизита.Значение, Истина);
	КонецЦикла;
	КонтролируемыеРеквизиты = РеквизитыФормыДляИзменения(СтруктураКлючей);
	
	ЕстьОшибки = Ложь;
	
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПолучательКонтрагент") <> Неопределено Тогда
		ПроверитьРеквизитыКонтрагента(ЭтотОбъект, "ПолучательКонтрагент", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПлательщикКонтрагент") <> Неопределено
		И ПлательщикРоль = 3 Тогда
		ПроверитьРеквизитыКонтрагента(ЭтотОбъект, "ПлательщикКонтрагент", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
	КонтрольЗаполненияРеквизитовОформленногоЗаказаНаСервере(КлючиРеквизитовДляИзменения, ЕстьОшибки, ВыводитьПредупреждения);
	
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ОтправительКонтактнаяИнформация") <> Неопределено Тогда
		ПроверитьНомерТелефона(ЭтотОбъект, "ОтправительКонтактноеЛицоТелефон", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПолучательКонтактнаяИнформация") <> Неопределено Тогда
		ПроверитьНомерТелефона(ЭтотОбъект, "ПолучательКонтактноеЛицоТелефон", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Поле ""%1"" не заполнено';
						|en = 'Field %1 is required'");
	
	// Выполняем проверки заполнения реквизитов формы.
	Для Каждого ТекущийРеквизит Из КонтролируемыеРеквизиты Цикл
		
		Попытка
			ЗначениеРеквизита = ЭтотОбъект[ТекущийРеквизит.Значение];
		Исключение
			Продолжить;
		КонецПопытки;
		
		Если Не ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				ТекущийРеквизит.Представление);
			
			Отказ = Истина;
			
			Если ВыводитьПредупреждения Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , ТекущийРеквизит.Значение);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура КонтрольЗаполненияРеквизитовОформленногоЗаказаНаСервере(КлючиРеквизитовДляИзменения, ЕстьОшибки, ВыводитьПредупреждения = Истина)

	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ОтправительАдрес") <> Неопределено Тогда
		ПроверитьРеквизитыАдреса("ОтправительАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПолучательАдрес") <> Неопределено Тогда
		ПроверитьРеквизитыАдреса("ПолучательАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	Если КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПлательщикАдрес") <> Неопределено 
		И ПлательщикРоль = 3 Тогда
		ПроверитьРеквизитыАдреса("ПлательщикАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменениеЗарегистрированногоЗаказаЗавершение()
	
	РежимИзмененияРеквизитов(Ложь);
	ДоступныеДляИзмененияРеквизитыТолькоПросмотр(Истина);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьПредставлениеАдресаВАдресате(Адресат)
	АдресЗначение = ЭтотОбъект[Адресат + "АдресЗначение"];
	Если ЗначениеЗаполнено(АдресЗначение) Тогда
		ПредставлениеАдресаДо = ЭтотОбъект[Адресат + "АдресПредставление"];
		Представление = ПредставлениеАдресаБезМуниципальнойЧасти(АдресЗначение);
		ЭтотОбъект[Адресат + "АдресПредставление"] = ?(ПустаяСтрока(Представление), ПредставлениеАдресаДо,
			Представление);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрыПослеДобавленияОснований(Параметры)
	
	ПараметрыДляФормы = Новый Структура;
	
	СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(Параметры, ПараметрыДляФормы);
	
	МассивРеквизитовДляПересчета = РеквизитыДляПересчета();
	
	Для Каждого ТекРеквизитДляПересчета Из МассивРеквизитовДляПересчета Цикл
		
		ИмяРеквизита = "Груз" + ТекРеквизитДляПересчета;
		ЗначениеРеквизитаДляПересчета = ПараметрыДляФормы[ИмяРеквизита];
		
		Если ТипЗнч(ЗначениеРеквизитаДляПересчета) = Тип("Число")
			И ЭтотОбъект[ИмяРеквизита] <> ЗначениеРеквизитаДляПересчета 
			И ЗначениеРеквизитаДляПересчета <> 0 Тогда
			ЭтотОбъект[ИмяРеквизита + "ДляПересчета"] = ЗначениеРеквизитаДляПересчета;
		ИначеЕсли ТипЗнч(ЗначениеРеквизитаДляПересчета) = Тип("Строка")
			И ЭтотОбъект[ИмяРеквизита] <> ЗначениеРеквизитаДляПересчета 
			И ЗначениеРеквизитаДляПересчета <> "" Тогда
			ЭтотОбъект[ИмяРеквизита + "ДляПересчета"] = ЗначениеРеквизитаДляПересчета;
		Иначе
			ЭтотОбъект[ИмяРеквизита + "ДляПересчета"] = Неопределено;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ГрузКоличествоГрузовыхМест = 1 Тогда
		ГрузМаксимальныйВесДляПересчета = ГрузВесДляПересчета;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПризнакОрганизаций()
	
	ТипКонтрагента = ТипЗнч(ОтправительКонтрагентСсылка);
	ОтправительКонтрагентЭтоОрганизация
		= Метаданные.ОпределяемыеТипы.ОрганизацияСервисДоставки.Тип.СодержитТип(ТипКонтрагента);
	
	ТипКонтрагента = ТипЗнч(ПолучательКонтрагентСсылка);
	ПолучательКонтрагентЭтоОрганизация
		= Метаданные.ОпределяемыеТипы.ОрганизацияСервисДоставки.Тип.СодержитТип(ТипКонтрагента);
	
	ТипКонтрагента = ТипЗнч(ПлательщикКонтрагентСсылка);
	ПлательщикКонтрагентЭтоОрганизация
		= Метаданные.ОпределяемыеТипы.ОрганизацияСервисДоставки.Тип.СодержитТип(ТипКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьТариф()
	
	ВыделенныйТариф = "";
	ТарифИдентификатор = "";
	ТарифНаименование = "";
	ТарифНеГабарит = Ложь;
	ГрузНегабаритныйВес = 0;
	ГрузНегабаритныйОбъем = 0;
	ГрузКоличествоНегабаритныхГрузовыхМест = 0;
	ОтправительАдресШирота = 0;
	ОтправительАдресДолгота = 0;
	ПолучательАдресШирота = 0;
	ПолучательАдресДолгота = 0;
	СуммаДокумента = 0;
	СуммаСкидки = 0;
	
	ИнформацияПоТекущемуТарифу = Новый ТабличныйДокумент;
	
	Если ТипГрузоперевозки <> СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка() Тогда
		СброситьТерминал("ПунктПриема");
		СброситьТерминал("ПунктВыдачи");
	КонецЕсли;
	
	ФормаОплаты = 0;
	ФормаОплатыПредставление = "";
	
	УслугиТарифа.Очистить();
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПараметрыТарифа Тогда
		ПерейтиКШагуТарифы();
	КонецЕсли;
	
	СформироватьПредставлениеТарифаИГрузоперевозчика();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеОбязательныхРеквизитов(НомерШага, Отказ, ВыводитьПредупреждения = Истина, ПолучитьТарифыАвтоматически = Ложь)
	
	ОчиститьСообщения();
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Или НомерШага <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	ШагОшибки = 10;
	ТекстОшибки = НСтр("ru = 'Поле ""%1"" на шаге ""%2"" не заполнено';
						|en = 'Field ""%1"" is not filled in on the step ""%2""'");
	ТекстОшибкиКлассификатораДЛ = НСтр("ru = 'Поле ""%1"" на шаге ""%2"" заполнено не по классификатору';
										|en = 'The ""%1"" field in step ""%2"" is not filled in according to the classifier'");
	
	ОбязательныеРеквизиты = ОбязательныеРеквизитыДляПереходаНаСтраницу(НомерШага);
	
	// Выполняем проверки заполнения реквизитов формы.
	Для Каждого ТекущийРеквизит Из ОбязательныеРеквизиты Цикл
		
		ПараметрыРеквизита = СтрРазделить(ТекущийРеквизит.Значение, ".");
		ШагТекущейОшибки = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ПараметрыРеквизита[0]);
		Значение = ПараметрыРеквизита[1];
		РеквизитНаФорме = Значение;
		ТекстТекущейОшибки = ТекстОшибки;
		Если Значение = "ГрузСодержимоеИдентификатор" Тогда
			РеквизитНаФорме = ПараметрыРеквизита[2];
			ТекстТекущейОшибки = ТекстОшибкиКлассификатораДЛ;
		КонецЕсли;
		
		Если ШагТекущейОшибки > ШагОшибки Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ЭтотОбъект[Значение]) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстТекущейОшибки,
				ТекущийРеквизит.Представление, Элементы.ГруппаСтраницы.ПодчиненныеЭлементы[ШагТекущейОшибки].Заголовок);
			Отказ = Истина;
			Если ВыводитьПредупреждения Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , РеквизитНаФорме);
			КонецЕсли;
			ШагОшибки = Мин(ШагОшибки, ШагТекущейОшибки);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Отказ Тогда
		ВыполнитьПереходКШагуОшибки(ШагОшибки, ПолучитьТарифыАвтоматически);
		Возврат;
	КонецЕсли;
	
	ПроверитьЗаполнениеРеквизитовНаКлиенте(ШагОшибки, Отказ, ВыводитьПредупреждения);
	
	Если Не Кэш.ЭтоДеловыеЛинии Тогда
		ПроверитьЗаполнениеРеквизитовНаСервере(ШагОшибки, Отказ, ВыводитьПредупреждения);
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	ПроверитьТоварныйСостав(ЕстьОшибки, Истина);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 1;
	КонецЕсли;
	
	Если НомерШага = 4 Тогда
		ЕстьОшибки = Ложь;
		ТекущийШагОшибки = ПроверитьВремяОтгрузки(ЕстьОшибки, ВыводитьПредупреждения);
		Если ЕстьОшибки Тогда
			ШагОшибки = Мин(ШагОшибки, ТекущийШагОшибки);
			Отказ = Истина;
		КонецЕсли;
		Если Не Отказ И ПроверитьЗаполнениеЗначенийДополнительныхУслуг(ЭтотОбъект) Тогда
			ЕстьОшибки = Истина;
			Если ВыводитьПредупреждения Тогда
				ТекстСообщения = НСтр("ru = 'Есть незаполненные свойства для выбранных дополнительных услуг.';
										|en = 'There are empty properties for selected additional services.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
			КонецЕсли;
			Отказ = Истина;
			ШагОшибки = 3;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	ПроверитьВГХ("", ЕстьОшибки, ВыводитьПредупреждения);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 1;
	КонецЕсли;
	
	Если НомерШага = 5 И Не Отказ Тогда
		ТекущийШагОшибки = ПроверитьВремяОтгрузки(ЕстьОшибки, Истина);
		Если ЕстьОшибки Тогда
			ШагОшибки = Мин(ШагОшибки, ТекущийШагОшибки);
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если Отказ Тогда
		ВыполнитьПереходКШагуОшибки(ШагОшибки, ПолучитьТарифыАвтоматически);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПереходКШагуОшибки(ШагОшибки, ПолучитьТарифыАвтоматически)
	
	Если ШагОшибки = 0 Тогда
		ПерейтиКШагуОсновное();
	ИначеЕсли ШагОшибки = 1 Тогда
		ПерейтиКШагуГруз();
	ИначеЕсли ШагОшибки = 2 Тогда
		ПерейтиКШагуТарифы(ПолучитьТарифыАвтоматически);
	ИначеЕсли ШагОшибки = 3 Тогда
		ПерейтиКШагуПараметрыТарифа();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДатуОтгрузки(ДатаОтгрузки)
	
	#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
		ТекущаяДата = ТекущаяДатаСеанса();
	#Иначе
		ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	#КонецЕсли
	
	ДатаОтгрузки = НачалоДня(ДатаОтгрузки);
	
	Если ДатаОтгрузки < НачалоДня(ТекущаяДата) Тогда
		ДатаОтгрузки = НачалоДня(ТекущаяДата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериодОтгрузкиПоУмолчанию()
	
	ВремяОтгрузкиС = '00010101090000';
	ВремяОтгрузкиПо = '00010101180000';
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПериодДоставкиПоУмолчанию()
	
	ВремяДоставкиС = '00010101090000';
	ВремяДоставкиПо = '00010101180000';
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиВыбора()
	
	СписокВыбораТиповДокументовОснований();
	
	СписокВыбора = Элементы.ОтправительКонтрагент.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Наша организация...';
									|en = 'Our company...'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Контрагент...';
									|en = 'Counterparty...'"));

	СписокВыбора = Элементы.ПолучательКонтрагент.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Наша организация...';
									|en = 'Our company...'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Контрагент...';
									|en = 'Counterparty...'"));
	
	СписокВыбора = Элементы.ПлательщикКонтрагент.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Наша организация...';
									|en = 'Our company...'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Контрагент...';
									|en = 'Counterparty...'"));

	СписокВыбора = Элементы.ФормаОплаты.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Безналичная';
									|en = 'Electronic'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Наличная';
									|en = 'Cash'"));
	
	СписокВыбора = Элементы.ПлательщикРоль.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Совпадает с отправителем';
									|en = 'Matches the sender'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Совпадает с получателем';
									|en = 'Matches the recipient'"));
	СписокВыбора.Добавить(3, НСтр("ru = 'Третье лицо';
									|en = 'Third party'"));
	
	СписокВыбора = Элементы.ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставление.СписокВыбора;
	СписокВыбора.Очистить();
	Для Каждого Строка Из ТаблицаРолейПлательщиков() Цикл
		СписокВыбора.Добавить(Строка.Представление, Строка.Представление);
	КонецЦикла;
	
	СписокВыбора = Элементы.ЗаказчикРоль.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(1, НСтр("ru = 'Отправителя';
									|en = 'Sender'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Получателя';
									|en = 'Recipient'"));
	СписокВыбора.Добавить(3, НСтр("ru = 'Плательщика';
									|en = 'Payer'"));
	СписокВыбора.Добавить(4, НСтр("ru = 'Третьего лица';
									|en = 'Third party'"));
	
	СписокВыбора = Элементы.НаложенныйПлатежВидОплаты.СписокВыбора;
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(0, НСтр("ru = 'Не требуется';
									|en = 'Not required'"));
	СписокВыбора.Добавить(1, НСтр("ru = 'Картой';
									|en = 'By card'"));
	СписокВыбора.Добавить(2, НСтр("ru = 'Наличными';
									|en = 'Cash'"));
	
	СписокВыбора = Элементы.ОрганизацияБизнесСетиСсылка.СписокВыбора;
	ОрганизацииБизнесСети = ОрганизацииБизнесСетиНаСервере();
	Для Каждого ТекущаяОрганизация Из ОрганизацииБизнесСети Цикл
		СписокВыбора.Добавить(ТекущаяОрганизация.Организация, ТекущаяОрганизация["Наименование"]);
	КонецЦикла;
	
КонецПроцедуры

// Определяет единственный элемент коллекции по значению и свойству.
// 
// Параметры:
//  Коллекция - ДанныеФормыКоллекция - Коллекция
//  Значение - Произвольный - Значение
//  Свойство - Строка - Свойство
// 
// Возвращаемое значение:
//  Неопределено, ДанныеФормыЭлементКоллекции - Результат поиска элемента в коллекции
&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьЭлементКоллекции(Знач Коллекция, Знач Значение, Знач Свойство = "Идентификатор")
	
	Результат = Неопределено;
	
	Если ТипЗнч(Значение) <> Тип("Строка") Или ПустаяСтрока(Значение) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Поиск = Коллекция.НайтиСтроки(Новый Структура(Свойство, Значение));
	
	Если Поиск.Количество() > 0 Тогда
		Результат = Поиск[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ДобавитьПредопределенныеУслуги(ИдентификаторУслуги)
	
	Если ИдентификаторУслуги = "000000002" И Элементы.СпособОтгрузки.СписокВыбора.НайтиПоЗначению(1) = Неопределено Тогда
		Элементы.СпособОтгрузки.СписокВыбора.Добавить(1, ПредставлениеСпособаОтгрузки(1));
		Элементы.ОтправительСпособОтгрузки.СписокВыбора.Добавить(1, ПредставлениеСпособаОтгрузки(1));
	КонецЕсли;
	
	Если ИдентификаторУслуги = "000000001" И Элементы.СпособОтгрузки.СписокВыбора.НайтиПоЗначению(2) = Неопределено Тогда
		Элементы.СпособОтгрузки.СписокВыбора.Добавить(2, ПредставлениеСпособаОтгрузки(2));
		Элементы.ОтправительСпособОтгрузки.СписокВыбора.Добавить(2, ПредставлениеСпособаОтгрузки(2));
	КонецЕсли;
	
	Если ИдентификаторУслуги = "000000004" И Элементы.СпособДоставки.СписокВыбора.НайтиПоЗначению(1) = Неопределено Тогда
		Элементы.СпособДоставки.СписокВыбора.Добавить(1, ПредставлениеСпособаДоставки(1));
		Элементы.ПолучательСпособДоставки.СписокВыбора.Добавить(1, ПредставлениеСпособаДоставки(1));
	КонецЕсли;
	
	Если ИдентификаторУслуги = "000000003" И Элементы.СпособДоставки.СписокВыбора.НайтиПоЗначению(2) = Неопределено Тогда
		Элементы.СпособДоставки.СписокВыбора.Добавить(2, ПредставлениеСпособаДоставки(2));
		Элементы.ПолучательСпособДоставки.СписокВыбора.Добавить(2, ПредставлениеСпособаДоставки(2));
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСписковВыбора()
	
	СписокВыбора = Элементы.ФормаОплаты.СписокВыбора;
	СписокВыбора.Очистить();
	
	Если ТарифФормаОплаты.Количество() = 0 Тогда
		СписокВыбора.Добавить(1, НСтр("ru = 'Безналичная';
										|en = 'Electronic'"));
		СписокВыбора.Добавить(2, НСтр("ru = 'Наличная';
										|en = 'Cash'"));
	Иначе
		Для каждого ЗначениеФормыОплаты Из ТарифФормаОплаты Цикл
			Если ЗначениеФормыОплаты.Значение = 1 Тогда
				СписокВыбора.Добавить(1, НСтр("ru = 'Безналичная';
												|en = 'Electronic'"));
			ИначеЕсли ЗначениеФормыОплаты.Значение = 2 Тогда
				СписокВыбора.Добавить(2, НСтр("ru = 'Наличная';
												|en = 'Cash'"));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ЗначениеСписка = Элементы.ФормаОплаты.СписокВыбора.НайтиПоЗначению(ФормаОплаты);
	Если ЗначениеСписка = Неопределено Тогда
		ФормаОплатыПредставление = "";
	Иначе
		ФормаОплатыПредставление = ЗначениеСписка.Представление;
	КонецЕсли;
	
	ЗначениеСписка = Элементы.ПлательщикРоль.СписокВыбора.НайтиПоЗначению(ПлательщикРоль);
	Если ЗначениеСписка = Неопределено Тогда
		ПлательщикРольПредставление = "";
	Иначе
		ПлательщикРольПредставление = ЗначениеСписка.Представление;
	КонецЕсли;
	
	Если Кэш.ЭтоЯндексДоставка Тогда
		ЗаполнитьСписокВыбораВремениОтгрузки();
		ЗаполнитьСписокВыбораПериодовОтгрузки();
	КонецЕсли;
	
	УстановитьВидимостьВидаВремениОтгрузки();

	Если ТарифТолькоВремяДоставки Тогда
		ВремяОтгрузкиК = Дата(1,1,1) + (ДатаОтгрузки - НачалоДня(ДатаОтгрузки));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеПоТарифу(ДанныеПоТарифу)
	
	Модифицированность = Истина;
	ОбработатьВыборТарифа(ДанныеПоТарифу);
	ЗаполнитьЗначенияСписковВыбора();
	
	Если Не НеЗагружатьУслуги Тогда
		СформироватьДопУслугиПоТарифу(ДанныеПоТарифу.ДополнительныеУслуги);
	КонецЕсли;
	
	НеЗагружатьУслуги = Ложь;
	
	ОбновитьИнформациюПоВыбраннымДопУслугам();
	Элементы.ГруппаПредупрежденияПоДопУслугам.Видимость = ПроверитьЗаполнениеЗначенийДополнительныхУслуг(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоДокументамОснования(Результат)
	
	ЭтоДеловыеЛинии = СервисДоставкиКлиентСервер.ЭтоДеловыеЛинии(ТипГрузоперевозки);
	ТаблицаДокументов = Новый ТаблицаЗначений;
	ТаблицаДокументов.Колонки.Добавить("ДокументОснование", Метаданные.ОпределяемыеТипы.ОснованиеЗаказаСервисДоставки.Тип);
	
	Для Каждого ДокументОснование Из ДокументыОснования Цикл
		ТаблицаДокументов.Добавить().ДокументОснование = ДокументОснование.Значение;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументыОснования.ДокументОснование КАК ДокументОснование
		|ПОМЕСТИТЬ ВТ_Документы
		|ИЗ
		|	&ДокументыОснования КАК ДокументыОснования
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
		|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ДокументыОснования.ДокументОснование) КАК id,
		|	ДокументыОснования.ДокументОснование.Номер КАК number
		|ИЗ
		|	ВТ_Документы КАК ДокументыОснования";
	
	Запрос.УстановитьПараметр("ДокументыОснования", ТаблицаДокументов);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Результат.ДокументыОснованияИдентификаторы.Добавить(Выборка.id);
			Если ЭтоДеловыеЛинии Тогда
				ДокументыОснованияДанные = СервисДоставки.НовыйПараметрыДокументыОснованияДанные();
				ЗаполнитьЗначенияСвойств(ДокументыОснованияДанные, Выборка);
				Результат.ДокументыОснованияДанные.Добавить(ДокументыОснованияДанные);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПервоначальнаяНастройкаФормы()
	
	КлючНастроекФормы = "Обработка.СервисДоставки.Форма.КарточкаЗаказа/ТекущиеДанные";
	Настройки = ХранилищеСистемныхНастроек.Загрузить(КлючНастроекФормы);
	
	Если Настройки <> Неопределено И ТипЗнч(Настройки) = Тип("Соответствие") Тогда
		ТарифыРежимСортировки = Настройки.Получить("ТарифыРежимСортировки");
	КонецЕсли;
	
	Если Не ДоступнаОтправкаЗаказовНаДоставку Тогда
		Элементы.ОформитьЗаказ.Заголовок = НСтр("ru = 'Сохранить заказ';
												|en = 'Save order'");
	КонецЕсли;
	
	ГрузоперевозчикИдентификатор = СервисДоставкиКлиентСервер.ИдентификаторГрузоперевозчика(ТипГрузоперевозки);
	ГрузоперевозчикНаименование = СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки);
	
	Если Не ЗначениеЗаполнено(ГрузКоличествоГрузовыхМест) Тогда
		ГрузКоличествоГрузовыхМест = 1;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДатаОтгрузки) Тогда
		УстановитьДатуОтгрузки(ДатаОтгрузки);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВремяОтгрузкиПо) Тогда
		УстановитьПериодОтгрузкиПоУмолчанию();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВремяДоставкиПо) Тогда
		УстановитьПериодДоставкиПоУмолчанию();
	КонецЕсли;
	
	ВыбиратьКонтактноеЛицоКонтрагента
		= (Метаданные.ОпределяемыеТипы.КонтактноеЛицоКонтрагентаСервисДоставки.Тип.Типы().Количество() > 1)
		ИЛИ (Не Метаданные.ОпределяемыеТипы.КонтактноеЛицоКонтрагентаСервисДоставки.Тип.СодержитТип(Тип("Строка")));
	
	ВыбиратьКонтактноеЛицоОрганизации
		= (Метаданные.ОпределяемыеТипы.КонтактноеЛицоОрганизацииСервисДоставки.Тип.Типы().Количество() > 1)
		ИЛИ (Не Метаданные.ОпределяемыеТипы.КонтактноеЛицоОрганизацииСервисДоставки.Тип.СодержитТип(Тип("Строка")));
	
	Если Не ЗначениеЗаполнено(СпособОтгрузки) Тогда
		СпособОтгрузки = 1;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СпособДоставки) Тогда
		СпособДоставки = 1;
	КонецЕсли;
	
	ПараметрыВалюты = СервисДоставки.НовыйПараметрыВалюты();
	ВалютаНаименование = ПараметрыВалюты.Наименование;
	ВалютаКод = ПараметрыВалюты.Код;
	
	Если Не ЗначениеЗаполнено(ВалютаСсылка) Тогда
		ВалютаСсылка = ПараметрыВалюты.Ссылка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВалютаСсылка) Тогда
		ПараметрыВалюты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВалютаСсылка, "Код, Наименование");
		ВалютаНаименование = ПараметрыВалюты.Наименование;
		ВалютаКод = ПараметрыВалюты.Код;
	КонецЕсли;
	
	НастройкиДоступныхДействий = ПолучитьВсеДоступныеДействия();
	
	ПереключательРасшифровки = ?(ГрузКоличествоГрузовыхМест = 1, 0, 1);
	
	ГрузОграничениеВес = 20000;
	ГрузОграничениеОбъем = 80;
	ГрузОграничениеВысота = 240;
	ГрузОграничениеДлина = 1340;
	ГрузОграничениеШирина = 240;
	ГрузОграничениеСтоимость = 300000000;
	
	НастроитьФормуПоТипуПеревозки();
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуОсновное()
	
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Ложь;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиОсновное.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОсновное;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуГруз()
	
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Ложь;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиГруз.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПараметрыГруза;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуТарифы(ПолучитьТарифыАвтоматически = Ложь)
	
	ПерейтиКШагуТарифыНаСервере(ПолучитьТарифыАвтоматически);
	
	Если ОтборыИзменение Тогда
		ОбработатьПрименениеОтборов(Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		ОжидатьЗавершениеВыполненияЗапроса(Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.ПараметрыМенеджераДлительныхОпераций = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиКШагуТарифыНаСервере(ПолучитьТарифыАвтоматически = Ложь)
	
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Истина;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиТарифы.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТарифы;
	ТарифыУстановитьРежимСортировки(ТарифыРежимСортировки);
	
	Если Кэш.ЭтоЯндексДоставка Тогда
		УстановитьВидимостьВидаВремениОтгрузки();
	КонецЕсли;
	
	Если Не ОтборыИзменение Тогда
		СформироватьПараметрыОтбора();
		Если ПолучитьТарифыАвтоматически Или Тарифы.Количество() = 0 Тогда
			ПолучитьТарифыВФоне(ПолучитьТарифыАвтоматически);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуПараметрыТарифа()
	
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Истина;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиПараметрыТарифа.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПараметрыТарифа;
	Элементы.ГруппаНегабаритныеМеста.Видимость = (ТарифНеГабарит И ГрузКоличествоГрузовыхМест <> 1);
	Элементы.ГрузКоличествоНегабаритныхГрузовыхМест.МаксимальноеЗначение = ГрузКоличествоГрузовыхМест;
	Элементы.ГрузНегабаритныйВес.МаксимальноеЗначение = ГрузВес;
	Элементы.ГрузНегабаритныйОбъем.МаксимальноеЗначение = ГрузОбъем;
	
	ОбработатьИзменениеВариантовДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуПроверка()
	
	// Если были изменены дополнительные услуги, то требуется пересчитать тариф.
	Если ДополнительныеУслугиИзменение Тогда
		ПерейтиКШагуПараметрыТарифа();
		ОбновитьТариф();
		Возврат;
	КонецЕсли;
	
	СформироватьИтоговуюИнформацию();
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Ложь;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиПроверка.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКШагуКарточка()
	
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Ложь;
	СброситьПометкиКомандШапки(Элементы);
	Элементы.ПерейтиКарточка.Пометка = Истина;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПараметрыОтбора()
	
	БыстрыеОтборы.Очистить();
	
	Если СпособОтгрузки = 1 Тогда
		БыстрыеОтборы.Добавить("СпособОтгрузки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Способ отгрузки: %1';
				|en = 'Shipment method:%1'"),
			НСтр("ru = 'Самопривоз';
				|en = 'Customer delivery'")));
	Иначе
		БыстрыеОтборы.Добавить("СпособОтгрузки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Способ отгрузки: %1';
				|en = 'Shipment method:%1'"),
			НСтр("ru = 'Забор от адреса';
				|en = 'Pick up from address'")));
	КонецЕсли;
	
	Если СпособДоставки = 1 Тогда
		БыстрыеОтборы.Добавить("СпособДоставки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Способ доставки: %1';
				|en = 'Delivery method: %1'"),
			НСтр("ru = 'Самовывоз';
				|en = 'Customer pickup'")));
	Иначе
		БыстрыеОтборы.Добавить("СпособДоставки", СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Способ доставки: %1';
				|en = 'Delivery method: %1'"),
			НСтр("ru = 'Доставка до адреса';
				|en = 'Delivery to address'")));
	КонецЕсли;
	
	СтрокиОтборовВерхнийУровень = ОтборыТарифов.ПолучитьЭлементы();
	
	Для Каждого СтрокаОтбораВерхнийУровень Из СтрокиОтборовВерхнийУровень Цикл
		
		СтрокиОтборы = СтрокаОтбораВерхнийУровень.ПолучитьЭлементы();
		
		Если СтрокиОтборы.Количество() Тогда
			Для Каждого СтрокаОтборы Из СтрокиОтборы Цикл
				Если СтрокаОтборы.Использовать Тогда
					БыстрыеОтборы.Добавить("ОтборыТарифов_" + СтрокаОтборы.ПолучитьИдентификатор(),
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Услуга: %1';
								|en = 'Service: %1'"),
							СтрокаОтборы.Наименование));
				КонецЕсли;
			КонецЦикла;
		Иначе
			Если СтрокаОтбораВерхнийУровень.Использовать Тогда
				БыстрыеОтборы.Добавить("ОтборыТарифов_" + СтрокаОтбораВерхнийУровень.ПолучитьИдентификатор(),
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Услуга: %1';
							|en = 'Service: %1'"),
						СтрокаОтбораВерхнийУровень.Наименование));
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	// Удаление старых элементов быстрых отборов.
	КоличествоЭлементов = Элементы.ГруппаБыстрыхОтборов.ПодчиненныеЭлементы.Количество();
	Для ОбратныйИндекс = 1 По КоличествоЭлементов Цикл
		ЭлементОтбора = Элементы.ГруппаБыстрыхОтборов.ПодчиненныеЭлементы[КоличествоЭлементов - ОбратныйИндекс];
		Если ЭлементОтбора.Видимость Тогда
			Элементы.Удалить(ЭлементОтбора);
		КонецЕсли;
	КонецЦикла;
	
	// Создание новых элементов быстрых отборов.
	Для Каждого ЭлементОтбора Из БыстрыеОтборы Цикл
		
		// Добавление пустой группы.
		НоваяГруппа = Элементы.Добавить("ГруппаБыстрогоОтбора_" + ЭлементОтбора.Значение, Тип("ГруппаФормы"),
			Элементы.ГруппаБыстрыхОтборов);
		НоваяГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		НоваяГруппа.Отображение = ОтображениеОбычнойГруппы.Нет;
		НоваяГруппа.ОтображатьЗаголовок = Ложь;
		НоваяГруппа.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
		НоваяГруппа.Видимость = Истина;
		НоваяГруппа.ОтображатьОтступСлева = Ложь;
		НоваяГруппа.ЦветФона = WebЦвета.СветлоЖелтый;
		НоваяГруппа.ГоризонтальныйИнтервал = ИнтервалМеждуЭлементамиФормы.Нет;
		
		НовыйЭлемент = Элементы.Добавить("ЗаголовокОтбора_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
		
		// Разбитие на форматированную строку.
		НачалоПредставления = Лев(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":"));
		КонецПредставления = Сред(ЭлементОтбора.Представление, СтрНайти(ЭлементОтбора.Представление, ":") + 1);
		
		НовыйЭлемент.Заголовок
			= СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = '%1 <span style=""color: ЦветВажнойНадписиБИП"">%2</span>';
															|en = '%1 <span style=""color: ЦветВажнойНадписиБИП"">%2</span>'"),
			НачалоПредставления,
			КонецПредставления);
		НовыйЭлемент.ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
		НовыйЭлемент.Гиперссылка = Истина;
		НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		
		Если Не (ЭлементОтбора.Значение = "СпособОтгрузки" Или ЭлементОтбора.Значение = "СпособДоставки") Тогда
			НовыйЭлемент = Элементы.Добавить("ОчиститьОтбор_" + ЭлементОтбора.Значение, Тип("ДекорацияФормы"), НоваяГруппа);
			НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
			НовыйЭлемент.Картинка = БиблиотекаКартинок.ПолеВводаОчистить;
			НовыйЭлемент.Гиперссылка = Истина;
			НовыйЭлемент.УстановитьДействие("Нажатие", "Подключаемый_Нажатие");
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеДокументаОснования()
	
	ЕстьДоступныеДокументы = СписокВыбораДокументовОснований.Количество() <> 0;
	
	Элементы.ДокументыОснованияПредставление.Доступность = ЕстьДоступныеДокументы;
	ДокументыОснованияПредставление = Новый ФорматированнаяСтрока("");
	
	Если ЕстьДоступныеДокументы Тогда
		
		КоличествоОснований = ДокументыОснования.Количество();
		
		Если КоличествоОснований = 0 Тогда
			
			Если РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
				ДокументыОснованияПредставление = Новый ФорматированнаяСтрока(
					НСтр("ru = 'Выбрать';
						|en = 'Select'"), ,
					ЦветаСтиля.ГиперссылкаЦвет, ,
					"ИзменитьДокументыОснования");
			КонецЕсли;
			
		ИначеЕсли КоличествоОснований = 1 Тогда
			
			ПервыйДокумент = ДокументыОснования[0].Значение;
			
			СтрокаДляДокументыОснованияПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				"<a style=""color: ЦветГиперссылкиБЭД"" href = ""%1"">%2</a>",
				ПолучитьНавигационнуюСсылку(ПервыйДокумент), Строка(ПервыйДокумент));
				
			Если РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
				СтрокаДляДокументыОснованияПредставление = СтрокаДляДокументыОснованияПредставление
					+ " " + НСтр("ru = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ДобавитьДокументыОснования"">(Добавить)</a>';
								|en = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ДобавитьДокументыОснования""> (Add)</a>'");
					
				СтрокаДляДокументыОснованияПредставление = СтрокаДляДокументыОснованияПредставление
					+ " " + НСтр("ru = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ОчиститьДокументыОснования"">(Очистить)</a>';
								|en = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ОчиститьДокументыОснования""> (Clear)</a>'");
			КонецЕсли;
				
			ДокументыОснованияПредставление = СтроковыеФункции.ФорматированнаяСтрока(СтрокаДляДокументыОснованияПредставление);
				
		Иначе
			
			СтрокаДляДокументыОснованияПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ОткрытьФормуОснования"">Всего документов: %1</a>';
					|en = '<a style=""color: ЦветГиперссылкиБЭД"" href = ""ОткрытьФормуОснования"">Total documents: %1</a>'"), КоличествоОснований);
			
			ДокументыОснованияПредставление = СтроковыеФункции.ФорматированнаяСтрока(СтрокаДляДокументыОснованияПредставление);
				
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеГрузоперевозчика()
	
	ГрузоперевозчикПредставление = ?(ЗначениеЗаполнено(ГрузоперевозчикИдентификатор), ГрузоперевозчикНаименование, НСтр("ru = '<Не выбран>';
																														|en = '<Not selected>'"));
	Элементы.ТарифыТекущийГрузоперевозчик.Гиперссылка = ЗначениеЗаполнено(ГрузоперевозчикИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФормуДляДокументаОснования()
	
	УстановитьВидимостьДоступностьНаСтраницеПараметровГруза();
	УстановитьВидимостьДоступностьНаСтраницеКарточки();
	СформироватьПредставлениеВремениОтгрузки();
	СформироватьПредставлениеВремениДоставки();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьОнЖе()
	
	Элементы.ДекорацияПолучательКонтрагент.Заголовок = ?(ПолучательКонтрагентЭтоОрганизация, НСтр("ru = 'Она же';
																									|en = 'Same'"),
		НСтр("ru = 'Он же';
			|en = 'same'"));
	Элементы.ДекорацияПолучательКонтрагент.Видимость = (РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован())
												И (ТипЗнч(ПолучательКонтрагентСсылка) = ТипЗнч(ОтправительКонтрагентСсылка))
												И (ПолучательКонтрагентСсылка <> ОтправительКонтрагентСсылка);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьОрганизации()
	
	ДоступноИзменениеОрганизации = (РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый());
	Элементы.ОрганизацияБизнесСетиСсылка.ТолькоПросмотр = Не ДоступноИзменениеОрганизации;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаложенныйПлатеж()
	
	Если (ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер()) 
		И (ДокументыОснования.Количество() = 1) 
		И (НаложенныйПлатежВидОплаты > 0) Тогда
		СуммаНаложенногоПлатежа = ПолнаяСтоимость;
	Иначе
		СуммаНаложенногоПлатежа = 0;
	КонецЕсли;
	
	ДекорацияСуммаНаложенногоПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Сумма к оплате - %1 %2';
			|en = 'Amount due - %1 %2'")
		, ?(НаложенныйПлатежВидОплаты > 0, СуммаНаложенногоПлатежа, 0)
		, ВалютаНаименование);
		
	Элементы.ГруппаНаложенныйПлатеж.Видимость = 
		(ТипГрузоперевозки = СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СКурьер()) 
		И (ДокументыОснования.Количество() = 1)
		И СервисДоставкиКлиент.ЭтотДокументДоступенДляНаложенногоПлатежа(ДокументыОснования[0].Значение);
	Элементы.ГруппаГруппаНаложенныйПлатежДопРеквизиты.Доступность = (НаложенныйПлатежВидОплаты > 0)
															И ЗначениеЗаполнено(ПолучательКонтрагентНаименование);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность();
	
	Если РежимМастера < СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		
		Элементы.ГруппаПараметрыГруза.ТолькоПросмотр = Ложь;
		Элементы.ГруппаПараметрыТарифа.ТолькоПросмотр = Ложь;
		Элементы.ПерейтиКарточка.Видимость = Ложь;
		Элементы.ПерейтиТарифы.Видимость = Истина;
		Элементы.ПерейтиПроверка.Видимость = Истина;
		Элементы.ТарифыКонтекстноеМенюУстановитьТарифПоУмолчанию.Видимость = ДоступнаОтправкаЗаказовНаДоставку;
		Элементы.ТарифыКонтекстноеМенюСброситьТарифПоУмолчанию.Видимость = ДоступнаОтправкаЗаказовНаДоставку;
		
	Иначе
		
		Элементы.ПерейтиКарточка.Видимость = Истина;
		Элементы.ПерейтиТарифы.Видимость = Ложь;
		Элементы.ПерейтиПроверка.Видимость = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка;
		Элементы.ПерейтиКарточка.Пометка = Истина;
		Элементы.ГруппаДанныхПоДопУслугам.Видимость = Ложь;
		ДоступныеДляИзмененияРеквизитыТолькоПросмотр(Истина);
		Элементы.ГруппаПараметрыГруза.ТолькоПросмотр = Истина;
		Элементы.ГруппаПараметрыТарифа.ТолькоПросмотр = Истина;
		Элементы.ГруппаОснование.Видимость = ДокументыОснования.Количество();
		Элементы.ГруппаКоманднаяПанельНавигация.Видимость = Ложь;
		Если Кэш.ЭтоДеловыеЛинии Тогда
			Элементы.ДекорацияСостояниеПредставление.Гиперссылка = ЗначениеЗаполнено(ТрекНомер);
		КонецЕсли;
		
	КонецЕсли;
	
	РежимМастераНовый = РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый();
	РежимМастераЗарегистрирован = РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован();
	
	Элементы.СохранитьКакЧерновик.Видимость = Не РежимМастераЗарегистрирован И Не ТолькоПросмотр;
	Элементы.ОформитьЗаказ.Видимость = Не РежимМастераЗарегистрирован И ДоступнаОтправкаЗаказовНаДоставку И Не ТолькоПросмотр;
	Элементы.Печать.Видимость = РежимМастераЗарегистрирован;
	Элементы.Обновить.Видимость = РежимМастераЗарегистрирован;
	Элементы.ОрганизацияБизнесСетиСсылка.Видимость = Не РежимМастераЗарегистрирован;
	Элементы.ОрганизацияБизнесСетиГиперссылка.Видимость = РежимМастераЗарегистрирован;
	Элементы.ГруппаМультизаказ.Видимость = (МультизаказИдентификатор <> "");
	Элементы.ГруппаТарифыГрузоперевозчик.Видимость = Ложь;
	Элементы.СуммаСкидки.Видимость = СуммаСкидки > 0;
	Элементы.ГруппаПлательщикВидимость.Видимость = ПлательщикРоль = 3;
	Элементы.ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставление.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ГруппаШаблоны.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ОбщаяКомандаЗаполнитьПоШаблонуСервисДоставки.Доступность = Кэш.ЭтоДеловыеЛинии И Не РежимМастераЗарегистрирован;
	Элементы.ОбщаяКомандаСохранитьКакШаблонСервисДоставки.Доступность = Кэш.ЭтоДеловыеЛинии И Не РежимМастераНовый;
	Элементы.ОбщаяКомандаСкопироватьСервисДоставки.Доступность = Не РежимМастераНовый;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоТипуПеревозки()
		
	Элементы.ГруппаОсновноеПериодыПраво.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ОтправительВремяПлановойОтгрузки.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ОтправительДатаПлановойОтгрузки.Видимость = Не Кэш.ЭтоДеловыеЛинии;
	Элементы.ОтправительСпособОтгрузки.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ПолучательСпособДоставки.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставление.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ТарифыДанныеГрафикаДоставки.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ГруппаШаблоны.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.Промокод.Видимость = Кэш.ЭтоДеловыеЛинии;
	Элементы.ПропускПредставление.Видимость = Кэш.ЭтоДеловыеЛинии;
	
	Элементы.ОтправительТерминал.ТолькоПросмотр = Кэш.ЭтоДеловыеЛинии;
	Элементы.ПолучательТерминал.ТолькоПросмотр = Кэш.ЭтоДеловыеЛинии;
	
	Если Кэш.ЭтоДеловыеЛинии Тогда
		Элементы.ОтправительАдрес.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		Элементы.ПолучательАдрес.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
		
		НастраиваемыйЭлемент = Элементы.ГрузСодержимое;
		НастраиваемыйЭлемент.Высота = 0;
		НастраиваемыйЭлемент.АвтоМаксимальнаяВысота = Истина;
		НастраиваемыйЭлемент.МногострочныйРежим = Ложь;
		НастраиваемыйЭлемент.КнопкаВыбора = Истина;
		НастраиваемыйЭлемент.ОбновлениеТекстаРедактирования = ОбновлениеТекстаРедактирования.ПриИзмененииЗначения;
		НастраиваемыйЭлемент.Подсказка = НСтр("ru = 'Описание содержимого выбирается из классификатора Характеры груза.
											  |Значение по умолчанию можно задать в общих настройках.';
											  |en = 'The content description is selected from the Cargo nature classifier.
											  |You can set the default value in the general settings.'");
		НастраиваемыйЭлемент.ОтображениеПодсказки = ОтображениеПодсказки.Авто;
		
		НастраиваемыйЭлемент.УстановитьДействие("ОбработкаВыбора", "Подключаемый_ГрузСодержимоеОбработкаВыбора");
		НастраиваемыйЭлемент.УстановитьДействие("НачалоВыбора", "Подключаемый_ГрузСодержимоеНачалоВыбора");
		НастраиваемыйЭлемент.УстановитьДействие("АвтоПодбор", "Подключаемый_ГрузСодержимоеАвтоПодбор");
		НастраиваемыйЭлемент.УстановитьДействие("ПриИзменении", "Подключаемый_ГрузСодержимоеПриИзменении");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьДополнительныхТелефонов()

	ОтправительКонтактноеЛицоТелефонДополнительныйДоступен = ЗначениеЗаполнено(ОтправительКонтактноеЛицоТелефонДополнительныйПредставление);
	ПолучательКонтактноеЛицоТелефонДополнительныйДоступен = ЗначениеЗаполнено(ПолучательКонтактноеЛицоТелефонДополнительныйПредставление);
	ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен = ЗначениеЗаполнено(ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокФормы()
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
		ЗаказНаДоставкуПредставление = НСтр("ru = 'Заказ на доставку (создание)';
											|en = 'Delivery order (creation)'");
	ИначеЕсли ЗначениеЗаполнено(НомерЗаказа) Тогда
		ЗаказНаДоставкуПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заказ на доставку %1 от %2';
				|en = 'Delivery order %1 from %2'"), НомерЗаказа, Формат(ДатаЗаказа, "ДЛФ=DT"));
	Иначе
		ЗаказНаДоставкуПредставление = НСтр("ru = 'Заказ на доставку (открытие)';
											|en = 'Delivery order (open)'");
	КонецЕсли;

	ТекстЗаголовка =  НСтр("ru = '%1: %2';
							|en = '%1: %2'");
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка,
		СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки),
		ЗаказНаДоставкуПредставление);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСтрокуПредставлениеСостоянияЗаказаФормы()
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
		СостояниеПредставление = НСтр("ru = 'Новый';
										|en = 'New'");
	ИначеЕсли ЗначениеЗаполнено(Состояние) Тогда
		СостояниеПредставление = Состояние;
	КонецЕсли;
	
	УстановитьВидимостьДоступныхДействий();
	
	Если НастройкиДоступныхДействий[ИмяДействияПриИзменении()].Видимость Тогда
		СостояниеПредставление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 (%2)", СостояниеПредставление,
			НСтр("ru = 'режим изменения оформленного заказа';
				|en = 'registered order change mode'"));
	КонецЕсли;
	
	Элементы.ДекорацияСостояниеПредставление.Заголовок = СостояниеПредставление;
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеСпособаОтгрузки(СпособОтгрузки)
	Возврат СпособыОтгрузки().Получить(СпособОтгрузки);
КонецФункции

&НаКлиенте
Процедура ВыбратьТарифПродолжить()
	
	ТекущаяСтрока = Элементы.Тарифы.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НеЗагружатьУслуги = Ложь;
	Модифицированность = Истина;
	СброситьТариф();
	
	ВыделенныйТариф = ТекущаяСтрока.ТарифИдентификатор;
	
	ДанныеПоТарифу = Новый Структура;
	ДанныеПоТарифу.Вставить("ГрузоперевозчикИдентификатор", ТекущаяСтрока.ГрузоперевозчикИдентификатор);
	ДанныеПоТарифу.Вставить("ГрузоперевозчикНаименование", ТекущаяСтрока.ГрузоперевозчикНаименование);
	ДанныеПоТарифу.Вставить("ТарифИдентификатор", ТекущаяСтрока.ТарифИдентификатор);
	ДанныеПоТарифу.Вставить("ТарифНаименование", ТекущаяСтрока.ТарифНаименование);
	ДанныеПоТарифу.Вставить("Негабарит", ТекущаяСтрока.Негабарит);
	ДанныеПоТарифу.Вставить("ФормаОплаты", ТекущаяСтрока.ФормаОплаты);
	ДанныеПоТарифу.Вставить("ФормаОплатыПоУмолчанию", ТекущаяСтрока.ФормаОплатыПоУмолчанию);
	ДанныеПоТарифу.Вставить("ТолькоВремяДоставки", ТекущаяСтрока.ТолькоВремяДоставки);
	ДанныеПоТарифу.Вставить("ТочкаОтправленияШирота", ТекущаяСтрока.ТочкаОтправленияШирота);
	ДанныеПоТарифу.Вставить("ТочкаОтправленияДолгота", ТекущаяСтрока.ТочкаОтправленияДолгота);
	ДанныеПоТарифу.Вставить("ТочкаДоставкиШирота", ТекущаяСтрока.ТочкаДоставкиШирота);
	ДанныеПоТарифу.Вставить("ТочкаДоставкиДолгота", ТекущаяСтрока.ТочкаДоставкиДолгота);
	ДанныеПоТарифу.Вставить("Стоимость", ТекущаяСтрока.Стоимость);
	ДанныеПоТарифу.Вставить("СуммаСкидки", ТекущаяСтрока.СуммаСкидки);
	ДанныеПоТарифу.Вставить("Описание", ТекущаяСтрока.Описание);
	ДанныеПоТарифу.Вставить("ДополнительныеУслуги", ТекущаяСтрока.ДополнительныеУслуги);
	ДанныеПоТарифу.Вставить("ДетализацияСтоимостиПоУслугам", ТекущаяСтрока.ДетализацияСтоимостиПоУслугам);
	
	Адресат = "ПунктПриемаГруза";
	ДанныеПоТарифу.Вставить(Адресат + "Идентификатор", ТекущаяСтрока[Адресат + "Идентификатор"]);
	ДанныеПоТарифу.Вставить(Адресат + "Наименование", ТекущаяСтрока[Адресат + "Наименование"]);
	ДанныеПоТарифу.Вставить(Адресат + "Адрес", ТекущаяСтрока[Адресат + "Адрес"]);
	ДанныеПоТарифу.Вставить(Адресат + "Телефон", ТекущаяСтрока[Адресат + "Телефон"]);
	ДанныеПоТарифу.Вставить(Адресат + "ТипНаименование", ТекущаяСтрока[Адресат + "ТипНаименование"]);
	ДанныеПоТарифу.Вставить(Адресат + "ТипИдентификатор", ТекущаяСтрока[Адресат + "ТипИдентификатор"]);
	
	Адресат = "ПунктВыдачиГруза";
	ДанныеПоТарифу.Вставить(Адресат + "Идентификатор", ТекущаяСтрока[Адресат + "Идентификатор"]);
	ДанныеПоТарифу.Вставить(Адресат + "Наименование", ТекущаяСтрока[Адресат + "Наименование"]);
	ДанныеПоТарифу.Вставить(Адресат + "Адрес", ТекущаяСтрока[Адресат + "Адрес"]);
	ДанныеПоТарифу.Вставить(Адресат + "Телефон", ТекущаяСтрока[Адресат + "Телефон"]);
	ДанныеПоТарифу.Вставить(Адресат + "ТипНаименование", ТекущаяСтрока[Адресат + "ТипНаименование"]);
	ДанныеПоТарифу.Вставить(Адресат + "ТипИдентификатор", ТекущаяСтрока[Адресат + "ТипИдентификатор"]);
	
	ВыбратьТарифНаСервере(ДанныеПоТарифу);
	СформироватьПредставлениеТарифаИГрузоперевозчика();
	Элементы.ГруппаПредупрежденияПоДопУслугам.Видимость = ДанныеПоТарифу.ВидимостьПредупрежденияПоДопУслугам;
	
	ПерейтиКШагуПараметрыТарифа();
	
КонецПроцедуры

&НаСервере
Функция ПредставлениеСпособаДоставки(СпособДоставки)
	Возврат СпособыДоставки().Получить(СпособДоставки);
КонецФункции

&НаСервере
Функция СпособыОтгрузки()
	
	СпособыОтгрузки = Новый Соответствие();
	
	СпособыОтгрузки.Вставить(1, НСтр("ru = 'Самопривоз';
									|en = 'Customer delivery'"));
	СпособыОтгрузки.Вставить(2, НСтр("ru = 'Забор от адреса';
									|en = 'Pick up from address'"));
	
	Возврат СпособыОтгрузки;

КонецФункции

&НаСервере
Функция СпособыДоставки()
	
	СпособыДоставки = Новый Соответствие();
	
	СпособыДоставки.Вставить(1, НСтр("ru = 'Самовывоз';
									|en = 'Self-pickup'"));
	СпособыДоставки.Вставить(2, НСтр("ru = 'Доставка до адреса';
									|en = 'Delivery to address'"));
	
	Возврат СпособыДоставки;

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуВыбораДокументаОснования()
	
	Если СписокВыбораДокументовОснований.Количество() > 1 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборТипаОснования", ЭтотОбъект);
		СписокВыбораДокументовОснований.ПоказатьВыборЭлемента(ОписаниеОповещения);
	ИначеЕсли СписокВыбораДокументовОснований.Количество() = 1 Тогда
		ОбработатьВыборТипаОснования(СписокВыбораДокументовОснований[0], Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОснования()
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ДокументыОснования", ДокументыОснования.ВыгрузитьЗначения());
	ПараметрыОткрытия.Вставить("Режим", РежимМастера);
	ПараметрыОткрытия.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытия.Вставить("Отправитель", ОтправительКонтрагентСсылка);
	ПараметрыОткрытия.Вставить("Получатель", ПолучательКонтрагентСсылка);
	ПараметрыОткрытия.Вставить("ОтправительАдрес", ОтправительАдресПредставление);
	ПараметрыОткрытия.Вставить("ПолучательАдрес", ПолучательАдресПредставление);
	ПараметрыОткрытия.Вставить("ПлательщикАдрес", ПлательщикАдресПредставление);
	ПараметрыОткрытия.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	ПараметрыОткрытия.Вставить("СпособОтгрузки", СпособОтгрузки);
	ПараметрыОткрытия.Вставить("СпособДоставки", СпособДоставки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборЗначения", ЭтотОбъект,
		Новый Структура("ИмяРеквизита", "ДокументыОснования"));
	
	РежимИзмененияСоставаОснований = 1;
	ОткрытьФорму("Обработка.СервисДоставки.Форма.СписокДокументовОснований", ПараметрыОткрытия, ЭтотОбъект, , , ,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбора(ИмяСправочника, ИмяРеквизита, ПараметрыОтбора = Неопределено,
			ИмяПроцедурыОбработки="ОбработатьВыборЗначения")
	
	Если ПараметрыОтбора = Неопределено Тогда
		ПараметрыОтбора = Новый Структура();
	КонецЕсли;
	
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "1", "");
	ИмяРеквизита = СтрЗаменить(ИмяРеквизита, "2", "");
	
	ТекущееЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
	
	ИмяФормыВыбора = СервисДоставкиВызовСервера.ИмяФормыВыбораПоОпределяемомуТипу(ИмяСправочника);
	
	Если ИмяФормыВыбора <> "" Тогда
		
		ПараметрыОткрытия = Новый Структура("ТекущаяСтрока", ТекущееЗначениеРеквизита);
		ПараметрыОткрытия.Вставить("Отбор", ПараметрыОтбора);
		ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
		
		СервисДоставкиКлиент.ПередОткрытиемФормыВыбора(ПараметрыОткрытия, ИмяСправочника);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(ИмяПроцедурыОбработки, ЭтотОбъект,
			Новый Структура("ИмяРеквизита", ИмяРеквизита));
		
		ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОбъекта(ИмяОбъекта, ИмяРеквизита, ИмяПроцедурыОбработки = "ОбработатьИзменениеРеквизитовОбъекта")
	
	ТекущееЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
	
	ИмяФормыВыбора = СервисДоставкиВызовСервера.ИмяФормыОбъектаПоОпределяемомуТипу(ИмяОбъекта, ТекущееЗначениеРеквизита);
	
	Если ИмяФормыВыбора <> "" Тогда
		
		ПараметрыОткрытия = Новый Структура();
		ПараметрыОткрытия.Вставить("Ключ", ТекущееЗначениеРеквизита);
		
		Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() 
			Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаКарточка Тогда
			ПараметрыОткрытия.Вставить("ТолькоПросмотр", Истина);
			ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия, ЭтотОбъект, , , , ,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Иначе
			ОписаниеОповещения = Новый ОписаниеОповещения(ИмяПроцедурыОбработки, ЭтотОбъект,
				Новый Структура("ИмяРеквизита", ИмяРеквизита));
			ОткрытьФорму(ИмяФормыВыбора, ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения,
				РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборТипаОснования(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПолноеИмяОбъекта = Результат.Значение;
	
	ИмяФормыВыбора = "";
	СервисДоставкиКлиентПереопределяемый.УстановитьИмяФормыВыбораОбъектаПоИмени(ПолноеИмяОбъекта, ИмяФормыВыбора);
	
	Если ИмяФормыВыбора = "" Тогда
		ИмяФормыВыбора = ПолноеИмяОбъекта + ".ФормаВыбора";
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура();
	Если ДокументыОснования.Количество() = 1 Тогда
		ПараметрыОткрытия.Вставить("ТекущаяСтрока", ДокументыОснования[0].Значение);
	КонецЕсли;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Истина);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборЗначения", ЭтотОбъект,
		Новый Структура("ИмяРеквизита", "ДокументыОснования"));
	ОткрытьФорму(
		ИмяФормыВыбора,
		ПараметрыОткрытия,
		ЭтотОбъект,,,,ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЗначения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		РежимИзмененияСоставаОснований = 0;
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("КодВозвратаДиалога")
		И Результат = КодВозвратаДиалога.Отмена Тогда
		РежимИзмененияСоставаОснований = 0;
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		
		Если ДополнительныеПараметры.ИмяРеквизита = "ДокументыОснования" Тогда
			Если РежимИзмененияСоставаОснований = 1 
				И ЭтотОбъект.ДокументыОснования.Количество() = 1 Тогда
				Основание = ЭтотОбъект.ДокументыОснования[0].Значение;
				Для Каждого ТекОснование Из Результат Цикл
					Если ТекОснование <> Основание Тогда
						ЭтотОбъект.ДокументыОснования.Добавить(ТекОснование);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ЭтотОбъект.ДокументыОснования.ЗагрузитьЗначения(Результат);
			КонецЕсли;
			
			// Очистим наложенный платеж при необходимости
			Если ЭтотОбъект.ДокументыОснования.Количество() = 1 Тогда
				Если НЕ СервисДоставкиКлиент.ЭтотДокументДоступенДляНаложенногоПлатежа(ЭтотОбъект.ДокументыОснования[0].Значение) Тогда
					НаложенныйПлатежВидОплаты = 0;
				КонецЕсли; 
			Иначе
				НаложенныйПлатежВидОплаты = 0;
			КонецЕсли; 
		Иначе
			ЭтотОбъект[ДополнительныеПараметры.ИмяРеквизита] = Результат;
		КонецЕсли;
		ОбработатьИзменениеРеквизитаФормы(ДополнительныеПараметры.ИмяРеквизита);
		
	КонецЕсли;
	
	РежимИзмененияСоставаОснований = 0;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеРеквизитовОбъекта(Результат, ДополнительныеПараметры) Экспорт
	
	ОчиститьСообщения();
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры) Тогда
		
		ИмяРеквизита = СтрЗаменить(ДополнительныеПараметры.ИмяРеквизита, "Ссылка", "");
		ИмяРеквизита = ИмяРеквизита + "Реквизиты";
		ОбработатьИзменениеРеквизитаФормы(ИмяРеквизита);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеРеквизитаФормы(ИмяРеквизита)
	
	ОчиститьСообщения();
	Отказ = Ложь;
	ОбработатьИзменениеРеквизитаФормыНаСервере(ИмяРеквизита, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяРеквизита = "ДокументыОснования" Тогда
		Если РежимИзмененияСоставаОснований = 2 Тогда
			СформироватьОтборыТарифов(Истина);
			РазвернутьРодителейСпискаСВыбраннымиСтроками();
		ИначеЕсли РежимИзмененияСоставаОснований = 1 Тогда
			УстановитьКомандыПересчетаПараметровГруза();
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступностьНаСтраницеОсновная();
	СформироватьНадписьОтбора();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИзменениеРеквизитаФормыНаСервере(ИмяРеквизита, Отказ = Ложь)
	
	Модифицированность = Истина;
	
	Если ИмяРеквизита = "ДокументыОснования" Тогда
		
		ПараметрыРеквизита = ПараметрыЗаказаНаДоставкуПоДокументамОснованиям(ТипГрузоперевозки, ЭтотОбъект[ИмяРеквизита]);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		Если РежимИзмененияСоставаОснований = 2 Тогда
			
			ОбработатьПараметры(ПараметрыРеквизита);
			
			УстановитьДатуОтгрузки(ДатаОтгрузки);
		
			Если Не ЗначениеЗаполнено(ВремяОтгрузкиПо) Тогда
				УстановитьПериодОтгрузкиПоУмолчанию();
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ВремяДоставкиПо) Тогда
				УстановитьПериодДоставкиПоУмолчанию();
			КонецЕсли;
			
			ОтборыИзменение = Истина;
			ПереключательРасшифровки = ?(ГрузКоличествоГрузовыхМест = 1, 0, 1);
			
			ТоварныйСостав.Очистить();
			
			Если ПараметрыРеквизита.Свойство("ТоварныйСостав") Тогда
				Для Каждого ТекСтрока Из ПараметрыРеквизита.ТоварныйСостав Цикл
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			
			ОбработатьПараметрыПослеДобавленияОснований(ПараметрыРеквизита);
			
			ТоварныйСостав.Очистить();
			Если ПараметрыРеквизита.Свойство("ТоварныйСостав") Тогда
				Для Каждого ТекСтрока Из ПараметрыРеквизита.ТоварныйСостав Цикл
					НоваяСтрока = ТоварныйСостав.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекСтрока);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
		СформироватьПредставлениеДокументаОснования();
		
	ИначеЕсли ИмяРеквизита = "ОтправительКонтрагентСсылка"
		ИЛИ ИмяРеквизита = "ПолучательКонтрагентСсылка"
		ИЛИ ИмяРеквизита = "ПлательщикКонтрагентСсылка" Тогда
		
		Если ЭтоЗаполнениеШаблоном Тогда
			ПараметрыУчастника = Новый Структура;
		Иначе
			ПараметрыУчастника = ПараметрыУчастникаГрузоперевозки(ЭтотОбъект[ИмяРеквизита]);
		КонецЕсли;
		
		// В режиме редактирования реквизитов учесть доступность изменения адреса получателя
		Если ИмяРеквизита = "ПолучательКонтрагентСсылка"
			И КлючиРеквизитовДляИзменения.Количество() > 0
			И КлючиРеквизитовДляИзменения.НайтиПоЗначению("ПолучательАдрес") = Неопределено Тогда
			ПараметрыУчастника.Удалить("Адрес");
		КонецЕсли;
		
		ОбработатьПараметры(ПараметрыУчастника, СтрЗаменить(ИмяРеквизита, "КонтрагентСсылка", ""));
		Если Кэш.ЭтоДеловыеЛинии Тогда
			ЗаменитьПредставлениеАдресаВАдресате(СтрЗаменить(ИмяРеквизита, "Ссылка", "Юридический"));
		КонецЕсли;
		ОтборыИзменение = Истина;
		
		ИмяЭлемента = СтрЗаменить(ИмяРеквизита, "Ссылка", "");
		ЕстьОшибки = Ложь;
		ПроверитьРеквизитыКонтрагента(ЭтотОбъект, ИмяЭлемента, ЕстьОшибки, Истина);
		
	ИначеЕсли ИмяРеквизита = "ОтправительКонтрагентРеквизиты"
		ИЛИ ИмяРеквизита = "ПолучательКонтрагентРеквизиты"
		ИЛИ ИмяРеквизита = "ПлательщикКонтрагентРеквизиты" Тогда
		
		ИмяЭлемента = СтрЗаменить(ИмяРеквизита, "Реквизиты", "");
		ПараметрыКонтрагента = ПараметрыКонтрагента(ЭтотОбъект[ИмяЭлемента + "Ссылка"]);
		
		ЕстьИзменения = Ложь;
		Для Каждого ТекущийПараметр Из ПараметрыКонтрагента Цикл
			
			Если ТипЗнч(ТекущийПараметр.Значение) = Тип("Структура") Тогда
				Продолжить;
			ИначеЕсли ТекущийПараметр.Значение <> ЭтотОбъект[ИмяЭлемента + ТекущийПараметр.Ключ] Тогда
				ЕстьИзменения = Истина;
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПараметрыКонтрагента.ЮридическийАдрес.Представление
			<> ЭтотОбъект[ИмяЭлемента + "ЮридическийАдресПредставление"] Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если ПараметрыКонтрагента.ЮридическийАдрес.Значение
			<> ЭтотОбъект[ИмяЭлемента + "ЮридическийАдресЗначение"] Тогда
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Если ЕстьИзменения Тогда
			ОбработатьПараметры(ПараметрыКонтрагента, ИмяЭлемента);
			ЕстьОшибки = Ложь;
			ПроверитьРеквизитыКонтрагента(ЭтотОбъект, ИмяЭлемента, ЕстьОшибки, Истина);
		КонецЕсли;
		
	ИначеЕсли ИмяРеквизита = "ОтправительКонтактноеЛицоСсылка" 
		ИЛИ ИмяРеквизита = "ПолучательКонтактноеЛицоСсылка" 
		ИЛИ ИмяРеквизита = "ПлательщикКонтактноеЛицоСсылка" Тогда
		
		Если ТипЗнч(ЭтотОбъект[ИмяРеквизита]) <> Тип("Строка") Тогда
			ПараметрыРеквизита = ПараметрыКонтактногоЛица(ЭтотОбъект[ИмяРеквизита]);
			ОбработатьПараметры(ПараметрыРеквизита, СтрЗаменить(ИмяРеквизита, "Ссылка", ""));
		КонецЕсли;

	ИначеЕсли ИмяРеквизита = "ОтправительАдресВладелец"
		ИЛИ ИмяРеквизита = "ПолучательАдресВладелец"
		ИЛИ ИмяРеквизита = "ПлательщикАдресВладелец" Тогда
		
		ПараметрыРеквизита = ПараметрыАдреса(ЭтотОбъект[ИмяРеквизита]);
		
		Если Кэш.ЭтоДеловыеЛинии Тогда
			Представление = ПредставлениеАдресаБезМуниципальнойЧасти(ПараметрыРеквизита.Значение);
			ПараметрыРеквизита.Представление = ?(ПустаяСтрока(Представление), ПараметрыРеквизита.Представление,
				Представление);
		КонецЕсли;
		
		ИмяЭлемента = СтрЗаменить(ИмяРеквизита, "Владелец", "");
		ОбработатьПараметры(ПараметрыРеквизита, ИмяЭлемента);
		
		ЕстьОшибки = Ложь;
		ПроверитьРеквизитыАдреса(ИмяЭлемента, ЕстьОшибки, Истина);
		ОтборыИзменение = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыЗаказаНаДоставкуПоДокументамОснованиям(ТипГрузоперевозки, Значение)
	Возврат СервисДоставки.ПараметрыЗаказаНаДоставку(ТипГрузоперевозки, Значение);
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыЗаказаНаДоставкуПоУмолчанию(ТипГрузоперевозки)
	Возврат СервисДоставки.ПараметрыЗаказаНаДоставкуПоДокументуОснованию(ТипГрузоперевозки);
КонецФункции

&НаСервере
Функция ПараметрыУчастникаГрузоперевозки(Значение)
	
	ПараметрыУчастника = СервисДоставки.НовыйПараметрыУчастникаГрузоперевозки();
	ПараметрыУчастника.Контрагент = ПараметрыКонтрагента(Значение);
	ПараметрыПоУмолчанию = СервисДоставки.ПараметрыПоУмолчанию(ТипГрузоперевозки);
	
	Если ПараметрыУчастника.Контрагент.ЭтоОрганизация Тогда
		Если ПараметрыПоУмолчанию.СпособОпределенияКонтактногоЛица = 2
			И ПараметрыУчастника.Контрагент.ЭтоОрганизация Тогда
			
			Если ТипЗнч(ПараметрыПоУмолчанию.КонтактноеЛицо) = Тип("Строка") Тогда
				ПараметрыУчастника.КонтактноеЛицо.Наименование = ПараметрыПоУмолчанию.КонтактноеЛицо;
			Иначе
				ПараметрыУчастника.КонтактноеЛицо.Ссылка = ПараметрыПоУмолчанию.КонтактноеЛицо;
				СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(ПараметрыУчастника.КонтактноеЛицо);
			КонецЕсли;
			
		Иначе
			ПараметрыУчастника.КонтактноеЛицо.СпособОпределенияКонтактногоЛица = ПараметрыПоУмолчанию.СпособОпределенияКонтактногоЛица;
			СервисДоставкиПереопределяемый.ЗаполнитьПараметрыКонтактногоЛицаПоУмолчанию(ЭтотОбъект, ПараметрыУчастника);
		КонецЕсли;
	ИначеЕсли ПараметрыУчастника.Контрагент.ЮрФизЛицо = 2 Тогда
		ПараметрыУчастника.КонтактноеЛицо.Ссылка = ПараметрыУчастника.Контрагент.Ссылка;
		СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(ПараметрыУчастника.КонтактноеЛицо);
	КонецЕсли;
	
	Возврат ПараметрыУчастника;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыКонтрагента(Значение)
	
	ПараметрыКонтрагента = СервисДоставки.НовыйПараметрыКонтрагента();
	ПараметрыКонтрагента.Ссылка = Значение;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СервисДоставкиПереопределяемый.ЗаполнитьПараметрыКонтрагента(ПараметрыКонтрагента);
	КонецЕсли;
	
	Возврат ПараметрыКонтрагента;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыКонтактногоЛица(Значение)
	
	ПараметрыКонтактногоЛица = СервисДоставки.НовыйПараметрыКонтактногоЛица();
	ПараметрыКонтактногоЛица.Ссылка = Значение;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СервисДоставки.ЗаполнитьПараметрыКонтактногоЛица(ПараметрыКонтактногоЛица);
	КонецЕсли;
	
	Возврат ПараметрыКонтактногоЛица;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыАдреса(Значение)
	
	ПараметрыАдреса = СервисДоставки.НовыйПараметрыАдреса("АдресДоставки");
	ПараметрыАдреса.Владелец = Значение;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СервисДоставкиСлужебный.ЗаполнитьАдресПоПараметрам(ПараметрыАдреса);
	КонецЕсли;
	
	Возврат ПараметрыАдреса;
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаСтраницеОсновная()
	
	Элементы.ОтправительКонтрагент.Заголовок = ?(ОтправительКонтрагентЭтоОрганизация, НСтр("ru = 'Организация';
																							|en = 'Company'"),
		НСтр("ru = 'Контрагент';
			|en = 'Counterparty'"));
	Элементы.ПолучательКонтрагент.Заголовок = ?(ПолучательКонтрагентЭтоОрганизация, НСтр("ru = 'Организация';
																						|en = 'Company'"),
		НСтр("ru = 'Контрагент';
			|en = 'Counterparty'"));
	Элементы.ПлательщикКонтрагент.Заголовок = ?(ПолучательКонтрагентЭтоОрганизация, НСтр("ru = 'Организация';
																						|en = 'Company'"),
		НСтр("ru = 'Контрагент';
			|en = 'Counterparty'"));
	
	ЕстьОтправитель = ЗначениеЗаполнено(ОтправительКонтрагентСсылка);
	ЕстьПолучатель = ЗначениеЗаполнено(ПолучательКонтрагентСсылка);
	ЕстьПлательщик = ЗначениеЗаполнено(ПлательщикКонтрагентСсылка);
	
	Элементы.ОтправительКонтактноеЛицо.КнопкаВыбора = ЕстьОтправитель
														И ((ОтправительКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоОрганизации)
														ИЛИ (Не ОтправительКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоКонтрагента));
	Элементы.ПолучательКонтактноеЛицо.КнопкаВыбора = ЕстьПолучатель
														И ((ПолучательКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоОрганизации)
														ИЛИ (Не ПолучательКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоКонтрагента));
	
	Элементы.ПлательщикКонтактноеЛицо.КнопкаВыбора = ЕстьПлательщик
														И ((ПлательщикКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоОрганизации)
														ИЛИ (Не ПлательщикКонтрагентЭтоОрганизация
														И ВыбиратьКонтактноеЛицоКонтрагента));
	
	Элементы.ОтправительАдрес.КнопкаВыпадающегоСписка = ЕстьОтправитель;
	Элементы.ОтправительАдрес1.КнопкаВыпадающегоСписка = ЕстьОтправитель;
	Элементы.ПолучательАдрес.КнопкаВыпадающегоСписка = ЕстьПолучатель;
	Элементы.ПолучательАдрес1.КнопкаВыпадающегоСписка = ЕстьПолучатель;
	Элементы.ПлательщикАдрес.КнопкаВыпадающегоСписка = ЕстьПлательщик;
	Элементы.ГруппаПлательщикВидимость.Видимость = ПлательщикРоль = 3;
	
	Элементы.ГруппаОтправительРеквизиты.Доступность = ЗначениеЗаполнено(ОтправительКонтрагентНаименование);
	Элементы.ГруппаПолучательРеквизиты.Доступность = ЗначениеЗаполнено(ПолучательКонтрагентНаименование);
	Элементы.ГруппаПлательщикРеквизиты.Доступность = ЗначениеЗаполнено(ПлательщикКонтрагентНаименование);
	Элементы.ГруппаНаложенныйПлатеж.Доступность = ЗначениеЗаполнено(ПолучательКонтрагентНаименование);
	
	УстановитьВидимостьОнЖе();
	УстановитьВидимостьДоступностьОрганизации();
	УстановитьВидимостьСпособовОтгрузкиДоставки();
	УстановитьВидимостьДоступностьНаложенныйПлатеж();
	УстановитьВидимостьДополнительныхТелефонов();
	ОбновитьФормуДляДокументаОснования();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаСтраницеПараметровГруза()
	
	УстановитьВидимостьДоступностьГруппыВГХ();

	ЕстьПозиционныйСостав = ЗначениеЗаполнено(ТоварныйСостав);
	Элементы.ТоварныйСоставГруза.Видимость = ?(Кэш.ЭтоДеловыеЛинии, Ложь, ЕстьПозиционныйСостав И Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьГруппыВГХ()
	
	ОдноМесто = (ПереключательРасшифровки = 0);
	
	Элементы.ГруппаОдноМестоВГХ.Видимость = ОдноМесто;
	Элементы.ГруппаНесколькоМест.Видимость = Не ОдноМесто;
	Элементы.ДекорацияНесколькоМест.Видимость = Не ОдноМесто;
	
	ОдинаковыеМеста = (ПереключательРасшифровки = 1);
	Элементы.ГрузОбщийВес.Доступность = Не ОдинаковыеМеста;
	Элементы.ГрузОбщийОбъем.Доступность = Не ОдинаковыеМеста;
	
	Если ОдинаковыеМеста Тогда
		ДекорацияЗаголовок = "Весогабаритные характеристики";
	Иначе
		ДекорацияЗаголовок = "Максимальные весогабаритные характеристики";
	КонецЕсли;
	
	Элементы.ДекорацияНесколькоМест.Заголовок = ДекорацияЗаголовок;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаСтраницеТарифы()
	Элементы.ТарифыГруппаАдреса.Видимость = ?(Кэш.ЭтоДеловыеЛинии, Ложь, Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаСтраницеПараметрыТарифа()
	
	Элементы.ОтправительТерминал.КнопкаВыбора = (РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован());
	Элементы.ПолучательТерминал.КнопкаВыбора = (РежимМастера <> СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован());
	Элементы.ОтправительТерминал.КнопкаОткрытия = (РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован());
	Элементы.ПолучательТерминал.КнопкаОткрытия = (РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДоступностьНаСтраницеКарточки()
	ЕстьПозиционныйСостав = ЗначениеЗаполнено(ТоварныйСостав);
	Элементы.ТоварныйСостав.Видимость = ?(Кэш.ЭтоДеловыеЛинии, Ложь, ЕстьПозиционныйСостав И Истина);
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьУслугиТарифовДляТипаГрузоперевозки(Услуги)
	
	УслугиТарифов.Очистить();
	
	ИдентификаторУслугиОтгрузки = ?(СпособОтгрузки = 1, "000000002", "000000001");
	ИдентификаторУслугиДоставки = ?(СпособДоставки = 1, "000000004", "000000003");
	
	Для Каждого ТекущаяУслуга Из Услуги Цикл
		
		ДобавитьПредопределенныеУслуги(ТекущаяУслуга.Идентификатор);
		
		НоваяУслуга = УслугиТарифов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга, , "Свойства");
		
		НоваяУслуга.Использовать = (НоваяУслуга.Идентификатор = ИдентификаторУслугиОтгрузки
			Или НоваяУслуга.Идентификатор = ИдентификаторУслугиДоставки);
		
		НайденныеСтроки = УслугиТарифа.НайтиСтроки(Новый Структура("Идентификатор", НоваяУслуга.Идентификатор));
		
		БратьЗначенияИзТарифа = НайденныеСтроки.Количество();
		Если БратьЗначенияИзТарифа Тогда
			ДанныеИзТарифа = НайденныеСтроки[0];
			НоваяУслуга.Использовать = ДанныеИзТарифа.Использовать;
		КонецЕсли;
			
		Для Каждого ТекущееСвойство Из ТекущаяУслуга.Свойства Цикл
			
			НовоеСвойство = НоваяУслуга.Свойства.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
			НовоеСвойство.Значение = НовоеСвойство.ТипЗначения.ПривестиЗначение(НовоеСвойство.Значение);
			
			Если БратьЗначенияИзТарифа Тогда
				НайденныеСвойства = ДанныеИзТарифа.Свойства.НайтиСтроки(Новый Структура("Идентификатор",
					НовоеСвойство.Идентификатор));
				Если НайденныеСвойства.Количество() Тогда
					НовоеСвойство.Значение = НайденныеСвойства[0].Значение;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНесовместимыеУслуги(Услуги)
	
	НесовместимыеУслуги.Очистить();
	
	Для Каждого ТекущаяУслуга Из Услуги Цикл
		НоваяУслуга = НесовместимыеУслуги.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяУслуга);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеТарифаИГрузоперевозчика()
	
	ТарифПредставление = ?(ЗначениеЗаполнено(ТарифНаименование), ТарифНаименование, НСтр("ru = '<Не выбран>';
																						|en = '<Not selected>'"));
	Элементы.ТарифыТекущийТарифПредставление.Гиперссылка = ЗначениеЗаполнено(ТарифНаименование);
	СформироватьПредставлениеГрузоперевозчика();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьОбъем()
	
	Если ПереключательРасшифровки <> 2 Тогда
		ГрузОбъемРасчетный = ГрузКоличествоГрузовыхМест * ГрузМаксимальнаяВысота * ГрузМаксимальнаяДлина * ГрузМаксимальнаяШирина * 0.000001;
		Если ГрузОбъемРасчетный > 0 Тогда
			ГрузОбъем = Макс(ГрузОбъемРасчетный, 0.001);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьВес()
	
	Если ПереключательРасшифровки = 1 Тогда
		ГрузВес = ГрузКоличествоГрузовыхМест * ГрузМаксимальныйВес;
	ИначеЕсли ГрузКоличествоГрузовыхМест = 1 И ГрузМаксимальныйВес > 0 Тогда
		ГрузВес = Мин(ГрузВес, ГрузМаксимальныйВес);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВГХ(ИмяРеквизита = "", ЕстьОшибки = Ложь, ВыводитьПредупреждения = Истина)
	
	ПроверитьВсе = (ИмяРеквизита = "");
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузСтоимость")
		И ЗначениеЗаполнено(ГрузСтоимость) 
		И ЗначениеЗаполнено(ГрузОграничениеСтоимость)
		И ГрузСтоимость > ГрузОграничениеСтоимость Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Стоимость превышает максимальное значение, допустимо не более %1 рублей.';
								|en = 'Profit is more than maximum value, only %1 rubles are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(ГрузОграничениеСтоимость,"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузСтоимость");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузВес")
		И ЗначениеЗаполнено(ГрузВес) 
		И ЗначениеЗаполнено(ГрузОграничениеВес)
		И ГрузВес > ГрузОграничениеВес Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Вес превышает максимальное значение, допустимо не более %1 т.';
								|en = 'The weight exceeds the maximum value, no more than %1 t are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(Цел(ГрузОграничениеВес/1000),"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузВес");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузВес")
		И ЗначениеЗаполнено(ГрузВес) 
		И ЗначениеЗаполнено(ГрузМаксимальныйВес)
		И ГрузМаксимальныйВес > ГрузВес Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Общий вес меньше значения самого тяжелого места';
								|en = 'General weight is less than the value of the most heavy place'");
			ТекстСообщения = ТекстОшибки;
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузВес");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузОбъем")
		И ЗначениеЗаполнено(ГрузОбъем) 
		И ЗначениеЗаполнено(ГрузОграничениеОбъем)
		И ГрузОбъем > ГрузОграничениеОбъем Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Объем превышает максимальное значение, допустимо не более %1 м³.';
								|en = 'The volume exceeds the maximum value, only %1 m³ are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(ГрузОграничениеОбъем,"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузОбъем");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузМаксимальнаяДлина")
		И ЗначениеЗаполнено(ГрузМаксимальнаяДлина) 
		И ЗначениеЗаполнено(ГрузОграничениеДлина)
		И ГрузМаксимальнаяДлина > ГрузОграничениеДлина Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Длина превышает максимальное значение, допустимо не более %1 м.';
								|en = 'The length exceeds the maximum value, only %1 m are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(ГрузОграничениеДлина/100,"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузМаксимальнаяДлина");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузМаксимальнаяШирина")
		И ЗначениеЗаполнено(ГрузМаксимальнаяШирина) 
		И ЗначениеЗаполнено(ГрузОграничениеШирина)
		И ГрузМаксимальнаяШирина > ГрузОграничениеШирина Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Ширина превышает максимальное значение, допустимо не более %1 м.';
								|en = 'The width exceeds the maximum value, no more than %1 m are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(ГрузОграничениеШирина/100,"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузМаксимальнаяШирина");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузМаксимальнаяВысота")
		И ЗначениеЗаполнено(ГрузМаксимальнаяВысота) 
		И ЗначениеЗаполнено(ГрузОграничениеВысота)
		И ГрузМаксимальнаяВысота > ГрузОграничениеВысота Тогда
		
		ЕстьОшибки = Истина;
		Если ВыводитьПредупреждения Тогда
			ТекстОшибки = НСтр("ru = 'Высота превышает максимальное значение, допустимо не более %1 м.';
								|en = 'The height exceeds the maximum value, no more than %1 m are available.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				Формат(ГрузОграничениеВысота/100,"ЧГ=0"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузМаксимальнаяВысота");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПереключательРасшифровки = 1 Тогда
	
		Если (ИмяРеквизита = "ГрузМаксимальныйВес")
			И ЗначениеЗаполнено(ГрузВес) 
			И ЗначениеЗаполнено(ГрузМаксимальныйВес)
			И ГрузМаксимальныйВес > ГрузВес Тогда
			
			ЕстьОшибки = Истина;
			Если ВыводитьПредупреждения Тогда
				ТекстОшибки = НСтр("ru = 'Общий вес меньше значения самого тяжелого места.';
									|en = 'General weight is less than the value of the most heaviest place.'");
				ТекстСообщения = ТекстОшибки;
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузМаксимальныйВес");
			КонецЕсли;
			
		КонецЕсли;
		
		Если (ПроверитьВсе ИЛИ ИмяРеквизита = "ГрузМаксимальныйВес")
			И ЗначениеЗаполнено(ГрузМаксимальныйВес) 
			И ЗначениеЗаполнено(ГрузОграничениеВес)
			И ГрузМаксимальныйВес > ГрузОграничениеВес Тогда
			
			ЕстьОшибки = Истина;
			Если ВыводитьПредупреждения Тогда
				ТекстОшибки = НСтр("ru = 'Вес превышает максимальное значение, допустимо не более %1 т.';
									|en = 'The weight exceeds the maximum value, no more than %1 t are available.'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ТекстОшибки,
					Формат(Цел(ГрузОграничениеВес/1000),"ЧГ=0"));
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ГрузМаксимальныйВес");
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СписокВыбораТиповДокументовОснований()
	
	ТипыДокументов = Метаданные.ОпределяемыеТипы.ОснованиеЗаказаСервисДоставки.Тип.Типы();
	
	Для Каждого ТекущийТип Из ТипыДокументов Цикл
		
		ОбъектМетаданных = Метаданные.НайтиПоТипу(ТекущийТип);
		
		Если ОбъектМетаданных <> Неопределено
			И ПравоДоступа("Чтение", ОбъектМетаданных) Тогда
			
			ПолноеИмя = ОбъектМетаданных.ПолноеИмя();
			Если ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
				СписокВыбораДокументовОснований.Добавить(ПолноеИмя, ОбъектМетаданных.Синоним);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРеквизит(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ИмяРеквизита = Элемент.Имя + "Ссылка";
	ЭтотОбъект[ИмяРеквизита] = Неопределено;
	ОбработатьИзменениеРеквизитаФормы(ИмяРеквизита);
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефонНачалоВыбора(Элемент, ЭтоНашаОрганизация)
	
	КонтактнаяИнформацияНачалоВыбора(Элемент, ?(ЭтоНашаОрганизация, 
												"ТелефонКонтактногоЛицаОрганизации",
												"ТелефонКонтактногоЛицаКонтрагента"));
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ЭтоНашаОрганизация)

	КонтактнаяИнформацияНачалоВыбора(Элемент, ?(ЭтоНашаОрганизация, 
												"АдресСкладаОрганизации",
												"АдресСкладаКонтрагента"));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОпределитьАдресата(ИмяЭлемента)
	
	Если СтрНачинаетсяС(ИмяЭлемента, "Получатель") Тогда
		Адресат = "Получатель";
	ИначеЕсли СтрНачинаетсяС(ИмяЭлемента, "Отправитель") Тогда
		Адресат = "Отправитель";
	Иначе
		Адресат = "Плательщик";
	КонецЕсли;
	
	Возврат Адресат;
	
КонецФункции

&НаКлиенте
Процедура КонтактнаяИнформацияНачалоВыбора(Элемент, ВидКонтактнойИнформацииСтрока)
	
	ПараметрыВидаКонтактнойИнформации = ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформацииСтрока);
	
	Если Не ЗначениеЗаполнено(ПараметрыВидаКонтактнойИнформации) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = Элемент.Имя;
	ПоследнийСимвол = Прав(ИмяРеквизита, 1);
	ИмяРеквизита = ?(ПоследнийСимвол = "1" Или ПоследнийСимвол = "2", Лев(ИмяРеквизита, СтрДлина(ИмяРеквизита) - 1),
		ИмяРеквизита);
	
	ИмяРеквизитаПредставление = ИмяРеквизита + "Представление";
	ИмяРеквизитаЗначенияПолей = ИмяРеквизита + "Значение";
	
	ЕстьИзменение = Ложь;
	Если Элемент.ТекстРедактирования <> ЭтотОбъект[ИмяРеквизитаПредставление] Тогда 
		ЭтотОбъект[ИмяРеквизитаПредставление] = Элемент.ТекстРедактирования;
		ЕстьИзменение = Истина;
	КонецЕсли;
	
	Если (ЭтотОбъект[ИмяРеквизитаЗначенияПолей] = "" И ЭтотОбъект[ИмяРеквизитаПредставление] <> "")
		ИЛИ ЕстьИзменение Тогда
		ЭтотОбъект[ИмяРеквизитаЗначенияПолей]
			= СервисДоставкиВызовСервера.КонтактнаяИнформацияПоПредставлению(ЭтотОбъект[ИмяРеквизитаПредставление],
			ПараметрыВидаКонтактнойИнформации.ТипНаименование);
		
	КонецЕсли;
		
	ЭтотОбъект[ИмяРеквизитаЗначенияПолей] = СервисДоставкиВызовСервера.АдресСДополнительнымиПолями(ПараметрыВидаКонтактнойИнформации.Тип, ЭтотОбъект[ИмяРеквизитаЗначенияПолей]);
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ПараметрыВидаКонтактнойИнформации.Вид, ЭтотОбъект[ИмяРеквизитаЗначенияПолей],
		ЭтотОбъект[ИмяРеквизитаПредставление],, ПараметрыВидаКонтактнойИнформации.Тип);
		
	ПараметрыОткрытия.Вставить("Заголовок", Элемент.Заголовок);
	ПараметрыОткрытия.Вставить("ТолькоПросмотр", Элемент.ТолькоПросмотр Или Элементы.ГруппаСтраницы.ТекущаяСтраница
		= Элементы.ГруппаКарточка);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяРеквизита", ИмяРеквизита);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект,
		ДополнительныеПараметры);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, ЭтотОбъект, Оповещение);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформацииСтрока)
	
	Возврат СервисДоставкиСлужебный.ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформацииСтрока);
	
КонецФункции

&НаКлиенте
Процедура ОбновитьКлючУникальности()
	
	Если СтрНайти(КлючУникальности, "ЭтоНовый") > 0 Тогда
		КлючУникальности = ИдентификаторЗаказа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеКонтактнойИнформации(Элемент, ТипКонтактнойИнформацииСтрока)
	
	ИмяРеквизита = Элемент.Имя;
	ИмяРеквизитаПредставление = ИмяРеквизита + "Представление";
	ИмяРеквизитаЗначенияПолей = ИмяРеквизита + "Значение";
	
	Если ТипКонтактнойИнформацииСтрока = "Телефон" Тогда
		Значение = СервисДоставкиВызовСервера.КонтактнаяИнформацияПоПредставлению(ЭтотОбъект[ИмяРеквизитаПредставление], ТипКонтактнойИнформацииСтрока);
		ЭтотОбъект[ИмяРеквизитаЗначенияПолей] = Значение;
	КонецЕсли;
	
	Модифицированность = Истина;
	ЕстьИзменения = Ложь;
	ПроверитьПредставлениеКонтактнойИнформации(ИмяРеквизита, Ложь, ЕстьИзменения);
	Если ЕстьИзменения Тогда
		ЗарегистрироватьИзменениеОтборов(Ложь);
	КонецЕсли;
	
	Если ЭтотОбъект[ИмяРеквизитаПредставление] <> Элемент.ТекстРедактирования Тогда
		ЭтотОбъект[ИмяРеквизитаПредставление] = Элемент.ТекстРедактирования;
	КонецЕсли;
	
	ЭтотОбъект[ИмяРеквизитаЗначенияПолей] = СервисДоставкиВызовСервера.КонтактнаяИнформацияПоПредставлению(ЭтотОбъект[ИмяРеквизитаПредставление],
		ТипКонтактнойИнформацииСтрока);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьПредставлениеКонтактнойИнформации(Знач Префикс, ЕстьОшибки, ЕстьИзменения = Ложь)
	
	Если СтрНайти(Префикс, "Телефон") Тогда
		
		ПроверитьНомерТелефона(ЭтотОбъект, Префикс, ЕстьОшибки);
		
	ИначеЕсли СтрНайти(Префикс, "Адрес") Тогда
		
		ПроверитьДозаполнитьАдрес(Префикс, ЕстьОшибки, ЕстьИзменения);
		
	ИначеЕсли СтрНайти(Префикс, "Email") Тогда
		
		ПроверитьEmail(ЭтотОбъект, Префикс, ЕстьОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если (Не ЗначениеЗаполнено(Результат)) Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
		
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ЕстьИзменения = Ложь;
		Реквизит = ЭтотОбъект[ИмяРеквизита + "Представление"];
		Если Результат.Свойство("Представление") И (Реквизит <> Результат.Представление) Тогда
			Результат.Свойство("Представление", ЭтотОбъект[ИмяРеквизита + "Представление"]);
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		Реквизит = ЭтотОбъект[ИмяРеквизита + "Значение"];
		Если Результат.Свойство("Значение") И (Реквизит <> Результат.Значение) Тогда
			Результат.Свойство("Значение", ЭтотОбъект[ИмяРеквизита + "Значение"]);
			ЕстьИзменения = Истина;
		КонецЕсли;
		
		ОтборыИзменение = ОтборыИзменение ИЛИ ЕстьИзменения;
		
		Отказ = Ложь;
		ПроверитьПредставлениеКонтактнойИнформации(ИмяРеквизита, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюПоТарифу()
	
	ТекущиеДанные = Элементы.Тарифы.ТекущиеДанные;
	СтоимостьУслугТекущегоТарифа = 0;
	
	Если ТекущиеДанные = Неопределено Тогда
		ИдентификаторТекущегоТарифа = "";
	Иначе
		ИдентификаторТекущегоТарифа = ТекущиеДанные.ТарифИдентификатор;
		СтоимостьУслугТекущегоТарифа = ТекущиеДанные.Стоимость;
	КонецЕсли;
	
	ОбновитьЭлементыФормыГруппаПредупрежденияТарифа();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СброситьПометкиКомандШапки(Элементы)
	
	Элементы.ПерейтиОсновное.Пометка = Ложь;
	Элементы.ПерейтиГруз.Пометка = Ложь;
	Элементы.ПерейтиТарифы.Пометка = Ложь;
	Элементы.ПерейтиПараметрыТарифа.Пометка = Ложь;
	Элементы.ПерейтиПроверка.Пометка = Ложь;
	Элементы.ПерейтиКарточка.Пометка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеЗначенийОтборовТарифов(Отказ)
	
	ОчиститьСообщения();
	
	ЭлементыДереваСписка = ОтборыТарифов.ПолучитьЭлементы();
	ТекстОшибки = НСтр("ru = 'Значение поля ""%1"" услуги ""%2"" не заполнено';
						|en = 'Field value ""%1"" of the ""%2"" service is not filled in'");
	
	Для Каждого ЭлементДереваСписка Из ЭлементыДереваСписка Цикл
		ЭлементыУслуги = ЭлементДереваСписка.ПолучитьЭлементы();
		Для Каждого ЭлементДереваСпискаУслуги Из ЭлементыУслуги Цикл
			Если ЭлементДереваСпискаУслуги.Использовать Тогда
				ЭлементыДереваСпискаУслугиСвойства = ЭлементДереваСпискаУслуги.ПолучитьЭлементы();
				Для Каждого ЭлементДереваСпискаУслугиСвойства Из ЭлементыДереваСпискаУслугиСвойства Цикл
					
					Если Не ЗначениеЗаполнено(ЭлементДереваСпискаУслугиСвойства.Значение) Тогда
						
						ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							ТекстОшибки,
							ЭлементДереваСпискаУслугиСвойства.Наименование,
							ЭлементДереваСпискаУслуги.Наименование);
						ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,,,Отказ);
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеАдресов(Отказ)

	Если ТипГрузоперевозки <> СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка() Тогда
		Возврат;
	КонецЕсли;
	
	ОтправкаОтАдреса = СпособОтгрузки = 2;
	ДоставкаДоАдреса = СпособДоставки = 2;
	
	ПолеОшибки = "";
	Если ОтправкаОтАдреса Тогда
		Если ПустаяСтрока(ОтправительАдресПредставление) Тогда
			ПолеОшибки = Элементы.ОтправительАдрес.Заголовок;
		КонецЕсли;
	Иначе
		Если ПустаяСтрока(ПунктПриемаГрузаНаименование) Тогда
			ПолеОшибки = Элементы.ОтправительТерминал.Заголовок;
		КонецЕсли;
	КонецЕсли;
	Если Не ПустаяСтрока(ПолеОшибки) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Поле ""%1"" не заполнено';
								|en = 'Field %1 is required'"), ПолеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;

	ПолеОшибки = "";
	Если ДоставкаДоАдреса Тогда
		Если ПустаяСтрока(ПолучательАдресПредставление) Тогда
			ПолеОшибки = Элементы.ПолучательАдрес.Заголовок;
		КонецЕсли;
	Иначе
		Если ПустаяСтрока(ПунктВыдачиГрузаНаименование) Тогда
			ПолеОшибки = Элементы.ПолучательТерминал.Заголовок;
		КонецЕсли;
	КонецЕсли;
	Если Не ПустаяСтрока(ПолеОшибки) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Поле ""%1"" не заполнено';
								|en = 'Field %1 is required'"), ПолеОшибки);
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОткрытие(Элемент)
	
	ИмяРеквизита = Элемент.Имя + "Ссылка";
	ИмяРеквизитаЭтоОрганизация = Элемент.Имя + "ЭтоОрганизация";
	
	ЗначениеРеквизита = ЭтотОбъект[ИмяРеквизита];
	ЭтоОрганизация = ЭтотОбъект[ИмяРеквизитаЭтоОрганизация];
	
	Если ЗначениеЗаполнено(ЗначениеРеквизита) Тогда
		
		Если ЭтоОрганизация Тогда
			ОткрытьФормуОбъекта("ОрганизацияСервисДоставки", ИмяРеквизита);
		Иначе
			ОткрытьФормуОбъекта("КонтрагентСервисДоставки", ИмяРеквизита);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТерминалНачалоВыбора(Направление, ИмяЭлемента)
	
	ВсеПунктыПВЗ = Не (ИмяЭлемента = "ОтправительТерминал" Или ИмяЭлемента = "ПолучательТерминал");
	
	Если Не ЗначениеЗаполнено(ТарифИдентификатор) И Не ВсеПунктыПВЗ Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыОповещения = Новый Структура;
	ДополнительныеПараметрыОповещения.Вставить("Направление", Направление);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьПодборТерминала", ЭтотОбъект, ДополнительныеПараметрыОповещения);
	
	Если Направление = 1 Тогда
		Терминал = "ПунктПриемаГруза";
	Иначе
		Терминал = "ПунктВыдачиГруза";
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("Направление", Направление);
	ПараметрыОткрытияФормы.Вставить("ГрузоперевозчикИдентификатор", ГрузоперевозчикИдентификатор);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСети", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыОткрытияФормы.Вставить("ВсеПунктыПВЗ", ВсеПунктыПВЗ);
	ПараметрыОткрытияФормы.Вставить("НаселенныйПунктИдентификатор", ЭтотОбъект[Терминал + "НаселенныйПунктИдентификатор"]);
	ПараметрыОткрытияФормы.Вставить("Идентификатор", ЭтотОбъект[Терминал + "Идентификатор"]);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.СписокТерминалов", ПараметрыОткрытияФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьВидаВремениОтгрузки()
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ГруппаВремяОтгрузки.Имя,
		"Видимость", Не Кэш.ЭтоДеловыеЛинии);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ПараметрыТарифаВремяДоставки.Имя,
		"Видимость", Не ТарифТолькоВремяДоставки);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ПараметрыТарифаВремяОтгрузки.Имя,
		"Видимость", Не ТарифТолькоВремяДоставки);
	
	Если Не Кэш.ЭтоДеловыеЛинии Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ДатаОтгрузки.Имя,
			"Видимость", ВариантВремениОтгрузки <> Кэш.ВариантыВремениОтгрузки.КакМожноСкорее);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ВремяОтгрузкиК.Имя,
			"Видимость", ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВОпределенноеВремя);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ПериодОтгрузки.Имя,
			"Видимость", ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВТечениеДня);
		
		СпособОтображенияПодсказки = ?(ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.КакМожноСкорее,
			ОтображениеПодсказки.Кнопка, ОтображениеПодсказки.Нет);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, Элементы.ВариантВремениОтгрузки.Имя,
			"ОтображениеПодсказки", СпособОтображенияПодсказки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПоВремениОтгрузки(ТекущаяДата = Неопределено)
	
	Если ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВОпределенноеВремя Тогда
		Если Не ТекущаяДата = Неопределено Тогда
			ВремяОтгрузкиК = ТекущаяДата+3600;
		КонецЕсли;
		ДатаОтгрузки = НачалоДня(ДатаОтгрузки) + (ВремяОтгрузкиК-НачалоДня(ВремяОтгрузкиК));
	Иначе
		ВремяОтгрузкиК = '00010101000000';
		ДатаОтгрузки = НачалоДня(ДатаОтгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВариантВремениОтгрузкиПриИзмененииНаСервере()
	УстановитьВидимостьВидаВремениОтгрузки();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораВремениОтгрузки()
	
	// Список выбора варианта времени отгрузки
	СписокВыбора = Элементы.ВариантВремениОтгрузки.СписокВыбора;
	СписокВыбора.Очистить();
	
	ВариантыВремениОтгрузкиСписокВыбора = СервисДоставкиПовтИсп.СписокВариантовВремениОтгрузки();
	
	Для Каждого Элемент Из ВариантыВремениОтгрузкиСписокВыбора Цикл
		
		Если Не ДоступныПериодыОтгрузки
			И ВариантВремениОтгрузки <> Кэш.ВариантыВремениОтгрузки.ВТечениеДня
			И Элемент.Значение = Кэш.ВариантыВремениОтгрузки.ВТечениеДня Тогда
			Продолжить;
		КонецЕсли;
		
		СписокВыбора.Добавить(Элемент.Значение, Элемент.Представление);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПериодовОтгрузки(ИзменятьПериодОтгрузки = Ложь)

	Если Не ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВТечениеДня Тогда
		Возврат;
	КонецЕсли;
	
	// Список выбора периодов времени отгрузки
	СписокВыбора = Элементы.ПериодОтгрузки.СписокВыбора;
	СписокВыбора.Очистить();
	
	НайденныеПериоды = ДоступныеПериодыОтгрузки.НайтиСтроки(Новый Структура("ДатаОтгрузки", ДатаОтгрузки));
	Если НайденныеПериоды.Количество() Тогда
		
		НайденныйИдентификаторПериода = Неопределено;
		
		Для Каждого НайденныйПериод Из НайденныеПериоды Цикл
			СписокВыбора.Добавить(НайденныйПериод.ПолучитьИдентификатор(), НайденныйПериод.ПериодОтгрузкиПредставление);
			Если НайденныйИдентификаторПериода = Неопределено
				И НайденныйПериод.ВремяОтгрузкиС = ВремяОтгрузкиС
				И НайденныйПериод.ВремяОтгрузкиПо = ВремяОтгрузкиПо Тогда
				НайденныйИдентификаторПериода = НайденныйПериод.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЦикла;
		
		Если НайденныйИдентификаторПериода = Неопределено Тогда
			ИзменятьПериодОтгрузки = Истина;
		Иначе
			ПериодОтгрузки = НайденныйИдентификаторПериода;
		КонецЕсли;
		
	ИначеЕсли Не ИзменятьПериодОтгрузки Тогда
		ВосстановитьТекущееЗначениеСпискаВыбораПериодовОтгрузки();
	Иначе
		СписокВыбора.Добавить(0, НСтр("ru = '<...>';
										|en = '<...>'"));
	КонецЕсли;
	
	Если ИзменятьПериодОтгрузки Тогда
		ПериодОтгрузки = СписокВыбора[0].Значение;
		ИзменитьПериодОтгрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПериодОтгрузки()
	
	Если ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВТечениеДня Тогда
		НайденныйПериод = ДоступныеПериодыОтгрузки.НайтиПоИдентификатору(ПериодОтгрузки);
		
		Если НайденныйПериод = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ВремяОтгрузкиС = НайденныйПериод.ВремяОтгрузкиС;
		ВремяОтгрузкиПО = НайденныйПериод.ВремяОтгрузкиПо;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьТекущееЗначениеСпискаВыбораПериодовОтгрузки()

	НовыйПериод = ДоступныеПериодыОтгрузки.Добавить();
	НовыйПериод.ДатаОтгрузки = ДатаОтгрузки;
	НовыйПериод.ВремяОтгрузкиС = ВремяОтгрузкиС;
	НовыйПериод.ВремяОтгрузкиПо = ВремяОтгрузкиПо;
	НовыйПериод.ПериодОтгрузкиПредставление = Формат(ВремяОтгрузкиС, "ДФ=ЧЧ:мм; ДП=00:00") + " - " + Формат(ВремяОтгрузкиПо, "ДФ=ЧЧ:мм; ДП=00:00");

	Элементы.ПериодОтгрузки.СписокВыбора.Добавить(НовыйПериод.ПолучитьИдентификатор(), НовыйПериод.ПериодОтгрузкиПредставление);
	
	ПериодОтгрузки = НовыйПериод.ПолучитьИдентификатор();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьОтборыТарифов(ЗаполнитьИспользоватьПоТарифам = Ложь)
	
	ОтборыТарифовКатегории = ОтборыТарифов.ПолучитьЭлементы();
	
	ТекущиеДанные = Новый ТаблицаЗначений();
	ТекущиеДанные.Колонки.Добавить("Идентификатор");
	ТекущиеДанные.Колонки.Добавить("Свойства");
	
	Если Не ЗаполнитьИспользоватьПоТарифам Тогда
		Для Каждого ТекущаяКатегория Из ОтборыТарифовКатегории Цикл
			
			ОтборыТарифовЭлементы = ТекущаяКатегория.ПолучитьЭлементы();
			Для Каждого ТекущаяУслуга Из ОтборыТарифовЭлементы Цикл
				
				Если ТекущаяУслуга.Использовать Тогда
					
					НоваяСтрока = ТекущиеДанные.Добавить();
					НоваяСтрока.Идентификатор = ТекущаяУслуга.Идентификатор;
					НоваяСтрока.Свойства = Новый Массив();
					
					ОтборыТарифовСвойства = ТекущаяУслуга.ПолучитьЭлементы();
					
					Для Каждого ТекущееСвойство Из ОтборыТарифовСвойства Цикл
						
						СтрокаСвойство = Новый Структура();
						СтрокаСвойство.Вставить("Наименование", ТекущееСвойство.Наименование);
						СтрокаСвойство.Вставить("Использовать", Ложь);
						СтрокаСвойство.Вставить("Идентификатор", ТекущееСвойство.Идентификатор);
						СтрокаСвойство.Вставить("Значение", ТекущееСвойство.Значение);
						СтрокаСвойство.Вставить("ТребуетсяЗначение", Истина);
						СтрокаСвойство.Вставить("ЕдиницаИзмерения", ТекущееСвойство.ЕдиницаИзмерения);
						СтрокаСвойство.Вставить("ИдентификаторУслугТарифов", ТекущееСвойство.ИдентификаторУслугТарифов);
						
						НоваяСтрока.Свойства.Добавить(СтрокаСвойство);
						
					КонецЦикла;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	Если Элементы.СпособДоставки.СписокВыбора.НайтиПоЗначению(СпособДоставки) = Неопределено 
		И Элементы.СпособДоставки.СписокВыбора.Количество() > 0 Тогда
		СпособДоставки = Элементы.СпособДоставки.СписокВыбора[0].Значение;
	КонецЕсли;
	
	Если Элементы.СпособОтгрузки.СписокВыбора.НайтиПоЗначению(СпособОтгрузки) = Неопределено 
		И Элементы.СпособОтгрузки.СписокВыбора.Количество() > 0 Тогда
		СпособОтгрузки = Элементы.СпособОтгрузки.СписокВыбора[0].Значение;
	КонецЕсли;
	
	ОтборыТарифовКатегории.Очистить();
	
	ТекущаяКатегория = "";
	
	Для Каждого ТекущаяУслуга Из УслугиТарифов Цикл
		
		Если СпособОтгрузки = 1 
			И НесовместимыеУслуги.НайтиСтроки(Новый Структура("Идентификатор,ИдентификаторНесовместимойУслуги", "000000002",
				ТекущаяУслуга.Идентификатор)).Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Если СпособДоставки = 1 
			И НесовместимыеУслуги.НайтиСтроки(Новый Структура("Идентификатор,ИдентификаторНесовместимойУслуги", "000000004",
				ТекущаяУслуга.Идентификатор)).Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяУслуга.Обязательная Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяКатегория <> ТекущаяУслуга.Категория Тогда
			ТекущаяКатегория = ТекущаяУслуга.Категория;
			
			СтрокаДерева = ОтборыТарифовКатегории.Добавить();
			СтрокаДерева.Наименование = ТекущаяУслуга.Категория;
			СтрокаДерева.Использовать = Истина;
			СтрокаДерева.Идентификатор = "";
			СтрокаДерева.Значение = Неопределено;
			СтрокаДерева.ТребуетсяЗначение = Ложь;
			СтрокаДерева.Обязательный = Ложь;
			СтрокаДерева.ТипСтроки = 0;
			
		КонецЕсли;
		
		СтрокаДереваВетка = СтрокаДерева.ПолучитьЭлементы().Добавить();
		СтрокаДереваВетка.Наименование = ТекущаяУслуга.Наименование;
		СтрокаДереваВетка.Использовать = ?(ЗаполнитьИспользоватьПоТарифам, ТекущаяУслуга.Использовать, Ложь);
		СтрокаДереваВетка.Идентификатор = ТекущаяУслуга.Идентификатор;
		СтрокаДереваВетка.ТипСтроки = 1;
		СтрокаДереваВетка.ПоказыватьИнформацию = ТекущаяУслуга.ПоказыватьИнформацию;
		СтрокаДереваВетка.ИдентификаторУслугТарифов = ТекущаяУслуга.ПолучитьИдентификатор();
		
		НайденныеСтроки = ТекущиеДанные.НайтиСтроки(Новый Структура("Идентификатор", ТекущаяУслуга.Идентификатор));
		
		Свойства = Неопределено;
		Если НайденныеСтроки.Количество() Тогда
			СтрокаДереваВетка.Использовать = Истина;
			Свойства = НайденныеСтроки[0].Свойства;
		КонецЕсли;
		
		СвойстваУслуги = ТекущаяУслуга.Свойства;
		
		Если СвойстваУслуги.Количество() Тогда
			
			СтрокиСвойств = СтрокаДереваВетка.ПолучитьЭлементы();
			СтрокиСвойств.Очистить();
			
			ЗаполнитьИдентификаторУслугТарифов = (Свойства = Неопределено);
			Свойства = ?(ЗаполнитьИдентификаторУслугТарифов, СвойстваУслуги, Свойства);
			
			Для Каждого ТекущееСвойство Из Свойства Цикл
				
				СтрокаДереваСвойство = СтрокиСвойств.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДереваСвойство, ТекущееСвойство);
				СтрокаДереваСвойство.ТребуетсяЗначение = Истина;
				СтрокаДереваСвойство.ТипСтроки = 2;
				
				Если ЗаполнитьИдентификаторУслугТарифов Тогда
					СтрокаДереваСвойство.ИдентификаторУслугТарифов = ТекущееСвойство.ПолучитьИдентификатор();
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ГруппаОтборыОсновная.Видимость = ОтборыТарифов.ПолучитьЭлементы().Количество();
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьРодителейСпискаСВыбраннымиСтроками()
	
	ЭлементыДереваСписка = ОтборыТарифов.ПолучитьЭлементы();
	Для Каждого ЭлементДереваСписка Из ЭлементыДереваСписка Цикл
		Элементы.ОтборыТарифов.Развернуть(ЭлементДереваСписка.ПолучитьИдентификатор());
		ЭлементыУслуги = ЭлементДереваСписка.ПолучитьЭлементы();
		Для Каждого ЭлементДереваСпискаУслуги Из ЭлементыУслуги Цикл
			Если ЭлементДереваСпискаУслуги.Использовать Тогда
				Элементы.ОтборыТарифов.Развернуть(ЭлементДереваСпискаУслуги.ПолучитьИдентификатор());
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеВариантовДоставки()
	
	Элементы.ГруппаОтгрузкаАдрес.Видимость = (СпособОтгрузки = 2);
	Элементы.ГруппаОтгрузкаТерминал.Видимость = (СпособОтгрузки = 1);
	Элементы.ПараметрыТарифаВремяОтгрузки.АвтоОтметкаНезаполненного = (СпособОтгрузки = 2);
	
	Элементы.ГруппаДоставкаАдрес.Видимость = (СпособДоставки = 2);
	Элементы.ГруппаДоставкаТерминал.Видимость = (СпособДоставки = 1);
	Элементы.ПараметрыТарифаВремяДоставки.АвтоОтметкаНезаполненного = (СпособДоставки = 2);
	
КонецПроцедуры

&НаСервере
Процедура ТарифыУстановитьРежимСортировки(ВыбранныйВариантСортировки)
	
	Если Не ЗначениеЗаполнено(ВыбранныйВариантСортировки) Тогда
		ВыбранныйВариантСортировки = "СортироватьТарифыПоЦене";
	КонецЕсли;
	
	Если Не ВариантыСортировки().Свойство(ВыбранныйВариантСортировки) Тогда
		ВыбранныйВариантСортировки = "СортироватьТарифыПоЦене";
	КонецЕсли;
	
	Элементы.ГруппаПодменюТарифыСортировать.Заголовок = Команды[ВыбранныйВариантСортировки].Заголовок;
	
	Для Каждого ТекущийВариант Из ВариантыСортировки() Цикл
		Элементы[ТекущийВариант.Ключ].Пометка = ТекущийВариант.Ключ = ВыбранныйВариантСортировки;
	КонецЦикла;
	
	ТарифыРежимСортировки = ВыбранныйВариантСортировки;
	
	Если Тарифы.Количество() Тогда
		СортироватьТарифы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ВариантыСортировки()
	
	Результат = Новый Структура;
	Результат.Вставить("СортироватьТарифыПоЦене", 					"КартинкаТариф Возр, Стоимость Возр");
	Результат.Вставить("СортироватьТарифыПоСроку", 					"МинимальныйСрок Возр");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СортироватьТарифы()
	
	Тарифы.Сортировать(ВариантыСортировки()[ТарифыРежимСортировки]);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборТарифа(ДанныеПоТарифу)
	
	ГрузоперевозчикИдентификатор = ДанныеПоТарифу.ГрузоперевозчикИдентификатор;
	ГрузоперевозчикНаименование = ДанныеПоТарифу.ГрузоперевозчикНаименование;
	ТарифИдентификатор = ДанныеПоТарифу.ТарифИдентификатор;
	ТарифНаименование = ДанныеПоТарифу.ТарифНаименование;
	ТарифОписание = ДанныеПоТарифу.Описание;
	ТарифНегабарит = ДанныеПоТарифу.Негабарит;
	ТарифТолькоВремяДоставки = ДанныеПоТарифу.ТолькоВремяДоставки;
	ОтправительАдресШирота = ДанныеПоТарифу.ТочкаОтправленияШирота;
	ОтправительАдресДолгота = ДанныеПоТарифу.ТочкаОтправленияДолгота;
	ПолучательАдресШирота = ДанныеПоТарифу.ТочкаДоставкиШирота;
	ПолучательАдресДолгота = ДанныеПоТарифу.ТочкаДоставкиДолгота;
	СуммаДокумента = ДанныеПоТарифу.Стоимость;
	СуммаСкидки = ДанныеПоТарифу.СуммаСкидки;
	
	Если ТарифНегабарит Тогда
		ГрузКоличествоНегабаритныхГрузовыхМест = 1;
		Если ГрузКоличествоГрузовыхМест = 1 Тогда
			ГрузНегабаритныйВес = ГрузВес;
			ГрузНегабаритныйОбъем = ГрузОбъем;
		КонецЕсли;
	КонецЕсли;
	
	ТарифФормаОплаты = ДанныеПоТарифу.ФормаОплаты;
	Если ФормаОплаты = 0 Тогда
		ФормаОплаты = ДанныеПоТарифу.ФормаОплатыПоУмолчанию;
	КонецЕсли;
	
	Если ФормаОплаты = 0
		И ТарифФормаОплаты.Количество() = 1 Тогда
		ФормаОплаты = ТарифФормаОплаты[0].Значение;
	КонецЕсли;
	
	Если ПунктПриемаГрузаИдентификатор = "" Тогда
		Адресат = "ПунктПриемаГруза";
		ЭтотОбъект[Адресат + "Идентификатор"] = ДанныеПоТарифу[Адресат + "Идентификатор"];
		ЭтотОбъект[Адресат + "Наименование"] = ДанныеПоТарифу[Адресат + "Наименование"];
		ЭтотОбъект[Адресат + "Адрес"] = ДанныеПоТарифу[Адресат + "Адрес"];
		ЭтотОбъект[Адресат + "Телефон"] = ДанныеПоТарифу[Адресат + "Телефон"];
		ЭтотОбъект[Адресат + "ТипНаименование"] = ДанныеПоТарифу[Адресат + "ТипНаименование"];
		ЭтотОбъект[Адресат + "ТипИдентификатор"] = ДанныеПоТарифу[Адресат + "ТипИдентификатор"];
	КонецЕсли;
	
	Если ПунктВыдачиГрузаИдентификатор = "" Тогда
		Адресат = "ПунктВыдачиГруза";
		ЭтотОбъект[Адресат + "Идентификатор"] = ДанныеПоТарифу[Адресат + "Идентификатор"];
		ЭтотОбъект[Адресат + "Наименование"] = ДанныеПоТарифу[Адресат + "Наименование"];
		ЭтотОбъект[Адресат + "Адрес"] = ДанныеПоТарифу[Адресат + "Адрес"];
		ЭтотОбъект[Адресат + "Телефон"] = ДанныеПоТарифу[Адресат + "Телефон"];
		ЭтотОбъект[Адресат + "ТипНаименование"] = ДанныеПоТарифу[Адресат + "ТипНаименование"];
		ЭтотОбъект[Адресат + "ТипИдентификатор"] = ДанныеПоТарифу[Адресат + "ТипИдентификатор"];
	КонецЕсли;
	
	Если Не НеЗагружатьУслуги Тогда 
		УслугиТарифа.Очистить();
		Для Каждого ТекущаяСтрокаУслугиТарифа Из УслугиТарифов Цикл
			НоваяСтрока = УслугиТарифа.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаУслугиТарифа,,"Свойства");
			
			СтрокиСвойств = ТекущаяСтрокаУслугиТарифа.Свойства;
			
			Для Каждого ТекущееСвойство Из СтрокиСвойств Цикл
				НовоеСвойство = НоваяСтрока.Свойства.Добавить();
				ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
			КонецЦикла;
			
		КонецЦикла;
	КонецЕсли;
	
	ЗаполнитьИнформациюПоТекущемуТарифу();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюПоТекущемуТарифу()
	
	Элементы.СуммаСкидки.Видимость = СуммаСкидки > 0;
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	
	Если Не ЗначениеЗаполнено(ТарифИдентификатор) Тогда
		ИнформацияПоТекущемуТарифу = ТабличныйДокумент;
		Возврат;
	КонецЕсли;
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("ВыбранныеУслугиТарифа");
	
	ОбластьТабличнаяЧастьУслуги = ТабличнаяЧастьУслуги(Макет, УслугиТарифа);
	
	Если ОбластьТабличнаяЧастьУслуги.ВысотаТаблицы > 3 Тогда
		ТабличныйДокумент.Вывести(ОбластьТабличнаяЧастьУслуги);
	КонецЕсли;
	
	ОбластьПодвалТаблицы = Макет.ПолучитьОбласть("ПодвалТаблицы");
	ТабличныйДокумент.Вывести(ОбластьПодвалТаблицы);
	
	Если ЗначениеЗаполнено(ТарифОписание) Тогда
		ОбластьОписаниеТарифа = Макет.ПолучитьОбласть("ОписаниеТарифа");
		ОбластьОписаниеТарифа.Параметры.ОписаниеТарифа = ТарифОписание;
		ТабличныйДокумент.Вывести(ОбластьОписаниеТарифа);
	КонецЕсли;
	
	ИнформацияПоТекущемуТарифу = ТабличныйДокумент;
	
КонецПроцедуры

&НаСервере
Функция ТабличнаяЧастьУслуги(Макет, Услуги)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	ОбластьМакетаШапкаТалицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
	ТабличныйДокумент.Вывести(ОбластьМакетаШапкаТалицы);
	
	ОбластьМакетаКатегория = Макет.ПолучитьОбласть("СтрокаТаблицыКатегория");
	ОбластьМакетаУслуга = Макет.ПолучитьОбласть("СтрокаТаблицыУслуга");
	ОбластьМакетаСвойство = Макет.ПолучитьОбласть("СтрокаТаблицыСвойство");
	
	ТекущаяКатегория = "";
	Для Каждого ТекущаяУслугаТарифа Из Услуги Цикл
		
		Если (Не ТекущаяУслугаТарифа.Использовать)
			ИЛИ ТекущаяУслугаТарифа.Обязательная Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяКатегория <> ТекущаяУслугаТарифа.Категория Тогда
			ТекущаяКатегория = ТекущаяУслугаТарифа.Категория;
			ОбластьСтрокаТаблицы = ОбластьМакетаКатегория;
			ОбластьСтрокаТаблицы.Параметры.Наименование = ТекущаяУслугаТарифа.Категория;
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЕсли;
		
		ОбластьСтрокаТаблицы = ОбластьМакетаУслуга;
		ОбластьСтрокаТаблицы.Параметры.Наименование = ТекущаяУслугаТарифа.Наименование;
		ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		
		Для Каждого ТекущееСвойство Из ТекущаяУслугаТарифа.Свойства Цикл
			ОбластьСтрокаТаблицы = ОбластьМакетаСвойство;
			ЗаполнитьЗначенияСвойств(ОбластьСтрокаТаблицы.Параметры, ТекущееСвойство);
			ТабличныйДокумент.Вывести(ОбластьСтрокаТаблицы);
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

&НаКлиенте
Процедура СброситьТерминал(ПрефиксТерминала)
	
	ИмяРеквизита = ПрефиксТерминала + "Груза";
	
	ЭтотОбъект[ИмяРеквизита + "Идентификатор"] = Неопределено;
	ЭтотОбъект[ИмяРеквизита + "НаселенныйПунктИдентификатор"] = Неопределено;
	ЭтотОбъект[ИмяРеквизита + "Наименование"] = Неопределено;
	ЭтотОбъект[ИмяРеквизита + "Описание"] = Неопределено;
	ЭтотОбъект[ИмяРеквизита + "Адрес"] = Неопределено;
	ЭтотОбъект[ИмяРеквизита + "Телефон"] = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПрименениеОтборов(СброситьТариф = Истина, ПолучатьТарифы = Истина)
	
	СформироватьПараметрыОтбора();
	ОбновитьЗначенияВУслугахТарифов();
	
	Если СброситьТариф Тогда
		СброситьТариф();
	КонецЕсли;
	
	Если ПолучатьТарифы Тогда
		ПолучитьТарифы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗначенияВУслугахТарифов()
	
	СтрокиОтборовВерхнийУровень = ОтборыТарифов.ПолучитьЭлементы();
	
	ИдентификаторУслугиСпособаОтгрузки = ?(СпособОтгрузки = 1, "000000002", "000000001");
	ИдентификаторУслугиСпособаДоставки = ?(СпособДоставки = 1, "000000004", "000000003");
	
	Для Каждого ТекущаяСтрока Из УслугиТарифов Цикл
		ТекущаяСтрока.Использовать = Ложь;
		
		Если ТекущаяСтрока.Идентификатор = ИдентификаторУслугиСпособаОтгрузки Тогда
			ТекущаяСтрока.Использовать = Истина;
		КонецЕсли;
		
		Если ТекущаяСтрока.Идентификатор = ИдентификаторУслугиСпособаДоставки Тогда
			ТекущаяСтрока.Использовать = Истина;
		КонецЕсли;
		
		Если ТекущаяСтрока.Свойства.Количество() Тогда
			Для Каждого ТекущееСвойство Из ТекущаяСтрока.Свойства Цикл
				ТекущееСвойство.Значение = ТекущееСвойство.ТипЗначения.ПривестиЗначение(ТекущееСвойство.Значение);
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаОтбораВерхнийУровень Из СтрокиОтборовВерхнийУровень Цикл
		
		СтрокиОтборы = СтрокаОтбораВерхнийУровень.ПолучитьЭлементы();
		
		Если СтрокиОтборы.Количество() Тогда
			Для Каждого СтрокаОтборы Из СтрокиОтборы Цикл
				Если СтрокаОтборы.Использовать Тогда
					НайденнаяУслуга = УслугиТарифов.НайтиПоИдентификатору(СтрокаОтборы.ИдентификаторУслугТарифов);
					Если НайденнаяУслуга <> Неопределено Тогда
						НайденнаяУслуга.Использовать = Истина;
						НайденнаяУслуга.Свойства.Очистить();
						Свойства = СтрокаОтборы.ПолучитьЭлементы();
						Для Каждого ТекущееСвойство Из Свойства Цикл
							
							НовоеСвойство = НайденнаяУслуга.Свойства.Добавить();
							ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
							
						КонецЦикла;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОплатуСтоимостьУслуг(ДетализацияСтоимостиПоУслугам)
	
	ОплатаСтоимостьУслуг.Очистить();
	
	Для Каждого ТекущаяСтрока Из ДетализацияСтоимостиПоУслугам Цикл
		Если ТекущаяСтрока.Стоимость = 0 Или ПустаяСтрока(ТекущаяСтрока.Идентификатор) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ОплатаСтоимостьУслуг.Добавить(), ТекущаяСтрока);
	КонецЦикла;
	
	ПлательщикиУслугИзменены = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьПлательщиковУслуг()
	
	Если Не ПлательщикиУслугИзменены Тогда
		Возврат;
	КонецЕсли;

	Для Каждого Тариф Из Тарифы Цикл
		Если Тариф.ТарифИдентификатор = ТарифИдентификатор Тогда
			ЗаполнитьОплатуСтоимостьУслуг(Тариф.ДетализацияСтоимостиПоУслугам);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ТабличныйДокументИтоговаяИнформация()
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Обработка = РеквизитФормыВЗначение("Объект");
	Макет = Обработка.ПолучитьМакет("Заказ");
	
	Если Заблокирован Тогда
		ОбластьМакетаЗаголовокБлокировки = Макет.ПолучитьОбласть("ЗаголовокБлокировки");
		ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокБлокировки);
	КонецЕсли;
	
	ОбластьМакетаШапка = Макет.ПолучитьОбласть("Шапка");
	ПараметрыОбласти = ОбластьМакетаШапка.Параметры;
	
	КоличествоОснований = ДокументыОснования.Количество();
	Если КоличествоОснований = 1 Тогда
		ПараметрыОбласти.РасшифровкаОснование = ДокументыОснования[0].Значение;
		ПараметрыОбласти.ОснованиеПредставление = ДокументыОснования[0].Значение;
	ИначеЕсли КоличествоОснований > 1 Тогда
		ПараметрыОбласти.РасшифровкаОснование = "ОткрытьФормуОснования";
		ПараметрыОбласти.ОснованиеПредставление = СтрШаблон(Нстр("ru = 'Всего документов: %1';
																|en = 'Total documents: %1'"), КоличествоОснований);
	КонецЕсли;
	
	ПараметрыОбласти.ГрузоперевозчикНаименование = ГрузоперевозчикНаименование;
	ПараметрыОбласти.РасшифровкаГрузоперевозчик = "Грузоперевозчик";
	ПараметрыОбласти.ТарифНаименование = ТарифНаименование;
	ПараметрыОбласти.РасшифровкаТариф = "Тариф";
	
	ФормаОплатыЭлемент = Элементы.ФормаОплаты.СписокВыбора.НайтиПоЗначению(ФормаОплаты);
	
	Если ФормаОплатыЭлемент <> Неопределено Тогда
		ПараметрыОбласти.ФормаОплатыНаименование 
			= ФормаОплатыЭлемент.Представление;
	КонецЕсли;
	
	ПараметрыОбласти.ОплатаПредставление = ?(Оплачен, НСтр("ru = 'Оплачено';
															|en = 'Paid'"), НСтр("ru = 'Требует оплаты';
																					|en = 'Required payment'"));
	ПараметрыОбласти.ПлательщикПредставление = 
		?(ПлательщикРоль = 1, НСтр("ru = 'Отправитель';
									|en = 'Sender'"), 
		?(ПлательщикРоль = 2, НСтр("ru = 'Получатель';
									|en = 'Recipient'"), НСтр("ru = 'Третье лицо';
															|en = 'Third party'")));
	
	ПараметрыОбласти.ДатаИВремяОформления = Формат(ДатаСозданияЗаказа,"ДЛФ=DT");
	ПараметрыОбласти.Состояние = ?(ПустаяСтрока(Состояние), СервисДоставки.ПредставлениеСостоянияЧерновик(), Состояние);
	ПараметрыОбласти.ТрекНомер = Новый ФорматированнаяСтрока(ТрекНомер, , , , "ТрекНомер");
	Если Кэш.ЭтоДеловыеЛинии Тогда
		ПараметрыОбласти.ПромокодЗаголовок = "Промокод:";
		ПараметрыОбласти.Промокод = Промокод;
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьМакетаШапка);
	
	Если Кэш.ЭтоЯндексДоставка И (ВодительНаименованиеДоставки <> "" ИЛИ ВодительНомерТелефонаДоставки <> "") Тогда
		ОбластьМакетаИсполнитель = Макет.ПолучитьОбласть("Исполнитель");
		ПараметрыОбласти = ОбластьМакетаИсполнитель.Параметры;
		ПараметрыОбласти.ИсполнительПредставление = ВодительНаименованиеДоставки;
		ПараметрыОбласти.ИсполнительНомерТелефонаПредставление = ВодительНомерТелефонаДоставки;
		ТабличныйДокумент.Вывести(ОбластьМакетаИсполнитель);
	КонецЕсли;
	
	Если Кэш.ЭтоЯндексДоставка И (ТранспортТипДоставки <> "" ИЛИ ТранспортНомерДоставки <> "") Тогда
		ОбластьМакетаАвтомобиль = Макет.ПолучитьОбласть("Автомобиль");
		ПараметрыОбласти = ОбластьМакетаАвтомобиль.Параметры;
		ПараметрыОбласти.АвтомобильПредставление = СокрЛП(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 %2';
				|en = '%1 %2'"),
			ТранспортТипДоставки,
			ТранспортНомерДоставки));
		ТабличныйДокумент.Вывести(ОбластьМакетаАвтомобиль);
	КонецЕсли;
	
	ОбластьМакетаКонтрагенты = Макет.ПолучитьОбласть("Контрагенты");
	ПараметрыОбласти = ОбластьМакетаКонтрагенты.Параметры;
	
	ПараметрыОбласти.ОтправительНаименование = ОтправительКонтрагентНаименование;
	ПараметрыОбласти.РасшифровкаОтправительКонтрагент = "ОтправительКонтрагент";
	ПараметрыОбласти.ОтправительКонтактноеЛицо = ОтправительКонтактноеЛицоНаименование;
	ПараметрыОбласти.ОтправительТелефон = ОтправительКонтактноеЛицоТелефонПредставление;
	
	ПараметрыОбласти.ПолучательНаименование = ПолучательКонтрагентНаименование;
	ПараметрыОбласти.РасшифровкаПолучательКонтрагент = "ПолучательКонтрагент";
	ПараметрыОбласти.ПолучательКонтактноеЛицо = ПолучательКонтактноеЛицоНаименование;
	ПараметрыОбласти.ПолучательТелефон = ПолучательКонтактноеЛицоТелефонПредставление;

	ПараметрыОбласти.Отправитель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отправитель %1';
			|en = 'Sender %1'"),
		?(ПлательщикРоль = 1, "(плательщик)",""));
	ПараметрыОбласти.Получатель = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Получатель %1';
			|en = 'Recipient %1'"),
		?(ПлательщикРоль = 2, "(плательщик)",""));

	ТабличныйДокумент.Вывести(ОбластьМакетаКонтрагенты);

	Если ЗначениеЗаполнено(ОтправительКонтактноеЛицоТелефонДополнительныйПредставление) 
		Или ЗначениеЗаполнено(ПолучательКонтактноеЛицоТелефонДополнительныйПредставление) Тогда
		
		ОбластьМакетаДополнительныйТелефон = Макет.ПолучитьОбласть("ДополнительныйТелефон");
		ПараметрыОбласти = ОбластьМакетаДополнительныйТелефон.Параметры;
	
		Если ЗначениеЗаполнено(ОтправительКонтактноеЛицоТелефонДополнительныйПредставление) Тогда
			ПараметрыОбласти.ТелефонДополнительныйТекст1 = НСтр("ru = 'Доп. телефон:';
																|en = 'Add. phone number:'");
			ПараметрыОбласти.ТелефонДополнительный1 = ОтправительКонтактноеЛицоТелефонДополнительныйПредставление;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПолучательКонтактноеЛицоТелефонДополнительныйПредставление) Тогда
			ПараметрыОбласти.ТелефонДополнительныйТекст2 = НСтр("ru = 'Доп. телефон:';
																|en = 'Add. phone number:'");
			ПараметрыОбласти.ТелефонДополнительный2 = ПолучательКонтактноеЛицоТелефонДополнительныйПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаДополнительныйТелефон);
		
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНаложенныеПлатежиСервис1СКурьер") И НаложенныйПлатежВидОплаты <> 0 Тогда
		ОбластьМакетаНаложенныйПлатеж = Макет.ПолучитьОбласть("НаложенныйПлатеж");
		
		СуммаНаложенногоПлатежа = ПолнаяСтоимость;
		
		ПараметрыОбласти = ОбластьМакетаНаложенныйПлатеж.Параметры;
		
		ПараметрыОбласти.НаложенныйПлатежВидОплаты = ?(НаложенныйПлатежВидОплаты =1, НСтр("ru = 'Картой';
																							|en = 'By card'"),НСтр("ru = 'Наличными';
																												|en = 'Cash'"));		
		ПараметрыОбласти.СуммаНаложенногоПлатежа = СуммаНаложенногоПлатежа;
		ПараметрыОбласти.СуммаНаложенногоПлатежа = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"%1 %2 %3",
			СуммаНаложенногоПлатежа,
			ВалютаНаименование,
			?(НаложенныйПлатежПолучен, НСтр("ru = '(оплачено)';
											|en = '(paid)'"), НСтр("ru = '(ожидает оплаты)';
																	|en = '(pending payment)'")));
		
		ТабличныйДокумент.Вывести(ОбластьМакетаНаложенныйПлатеж);
	Иначе
		СуммаНаложенногоПлатежа = 0;
	КонецЕсли;
	
	Если ПлательщикРоль = 3 Тогда
		
		ОбластьМакетаПлательщик = Макет.ПолучитьОбласть("Плательщик");
		ПараметрыОбласти = ОбластьМакетаПлательщик.Параметры;
		
		ПараметрыОбласти.ПлательщикНаименование = ПлательщикКонтрагентНаименование;
		ПараметрыОбласти.РасшифровкаПлательщикКонтрагент = "ПлательщикКонтрагент";
		ПараметрыОбласти.ПлательщикКонтактноеЛицо = ПлательщикКонтактноеЛицоНаименование;
		ПараметрыОбласти.ПлательщикТелефон = ПлательщикКонтактноеЛицоТелефонПредставление;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаПлательщик);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление) Тогда
		
		ОбластьМакетаДополнительныйТелефон = Макет.ПолучитьОбласть("ДополнительныйТелефон");
		ПараметрыОбласти = ОбластьМакетаДополнительныйТелефон.Параметры;
	
		Если ЗначениеЗаполнено(ОтправительКонтактноеЛицоТелефонДополнительныйПредставление) Тогда
			ПараметрыОбласти.ТелефонДополнительныйТекст1 = НСтр("ru = 'Доп. телефон:';
																|en = 'Add. phone number:'");
			ПараметрыОбласти.ТелефонДополнительный1 = ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление;
		КонецЕсли;
		
		ТабличныйДокумент.Вывести(ОбластьМакетаДополнительныйТелефон);
		
	КонецЕсли;

	ОбластьМакетаАдреса = Макет.ПолучитьОбласть("Адреса");
	ПараметрыОбласти = ОбластьМакетаАдреса.Параметры;
	
	ПараметрыОбласти.СпособОтгрузки = ПредставлениеСпособаОтгрузки(СпособОтгрузки);
	
	Если СпособОтгрузки = 1 Тогда
		ОтправительТерминалОтправительАдрес = ПунктПриемаГрузаНаименование;
		ОтправительТипАдресаНаименование = ?(ПунктПриемаГрузаТипНаименование="", НСтр("ru = 'Адрес';
																						|en = 'Address'"),
			ПунктПриемаГрузаТипНаименование);
		Если ЗначениеЗаполнено(ПунктПриемаГрузаАдрес) Тогда
			ОтправительТерминалОтправительАдрес = ОтправительТерминалОтправительАдрес
												  + "(" + ПунктПриемаГрузаАдрес + ")";
		КонецЕсли;
	Иначе
		ОтправительТерминалОтправительАдрес = ОтправительАдресПредставление;
		ОтправительТипАдресаНаименование = НСтр("ru = 'Адрес';
												|en = 'Address'");
	КонецЕсли;
	
	ПараметрыОбласти.РасшифровкаОтправительТерминалОтправительАдрес = "ОтправительТерминалОтправительАдрес";
	ПараметрыОбласти.ОтправительТипАдресаНаименование = ОтправительТипАдресаНаименование;
	
	Если СпособДоставки = 1 Тогда
		ПолучательТерминалПолучательАдрес = ПунктВыдачиГрузаНаименование;
		ПолучательТипАдресаНаименование = ?(ПунктВыдачиГрузаТипНаименование="", НСтр("ru = 'Адрес';
																					|en = 'Address'"),
			ПунктВыдачиГрузаТипНаименование);
		Если ЗначениеЗаполнено(ПунктВыдачиГрузаАдрес) Тогда
			ПолучательТерминалПолучательАдрес = ПолучательТерминалПолучательАдрес
												+ "(" + ПунктВыдачиГрузаАдрес + ")";
		КонецЕсли;
	Иначе
		ПолучательТерминалПолучательАдрес = ПолучательАдресПредставление;
		ПолучательТипАдресаНаименование = НСтр("ru = 'Адрес';
												|en = 'Address'");
	КонецЕсли;
	
	ПараметрыОбласти.РасшифровкаПолучательТерминалПолучательАдрес = "ПолучательТерминалПолучательАдрес";
	ПараметрыОбласти.ПолучательТипАдресаНаименование = ПолучательТипАдресаНаименование;
	
	ПараметрыОбласти.ОтправительТерминалОтправительАдрес = ОтправительТерминалОтправительАдрес;
	ПараметрыОбласти.ОтправительТерминалТелефон = ПунктПриемаГрузаТелефон;
	ПараметрыОбласти.СпособДоставки = ПредставлениеСпособаДоставки(СпособДоставки);
	ПараметрыОбласти.ПолучательТерминалПолучательАдрес = ПолучательТерминалПолучательАдрес;
	ПараметрыОбласти.ПолучательТерминалТелефон = ПунктВыдачиГрузаТелефон;
	
	ТабличныйДокумент.Вывести(ОбластьМакетаАдреса);
	
	Если Кэш.ЭтоДеловыеЛинии Тогда
		ОбластьМакетаШапкаВремя = Макет.ПолучитьОбласть("ШапкаВремя1");
		ПараметрыОбласти = ОбластьМакетаШапкаВремя.Параметры;
		ПараметрыОбласти.ОтправительДатаИВремяОтгрузкиПредставление = Формат(ДатаОтгрузки,"ДЛФ=D")
			+ " " + ВремяОтгрузкиПредставление;
		ПараметрыОбласти.ПолучательДатаИВремяОтгрузкиПредставление = Формат(ДатаДоставки,"ДЛФ=D")
			+ " " + ВремяДоставкиПредставление;
		
	Иначе
		ОбластьМакетаШапкаВремя = Макет.ПолучитьОбласть("ШапкаВремя1");
		ПараметрыОбласти = ОбластьМакетаШапкаВремя.Параметры;
		
		СтрокиДляПодстановки = Новый Массив();
		СтрокиДляПодстановки.Добавить(Формат(ДатаОтгрузки,"ДЛФ=D"));
		
		Если ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВОпределенноеВремя Тогда
			СтрокиДляПодстановки.Добавить(" ");
			СтрокиДляПодстановки.Добавить(Формат(ВремяОтгрузкиК,"ДФ=ЧЧ:мм; ДП=00:00"));
		ИначеЕсли ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВТечениеДня Тогда
			СтрокиДляПодстановки.Добавить(" (");
			СтрокиДляПодстановки.Добавить(Формат(ВремяОтгрузкиС, "ДФ=ЧЧ:мм; ДП=00:00"));
			СтрокиДляПодстановки.Добавить("-");
			СтрокиДляПодстановки.Добавить(Формат(ВремяОтгрузкиПо, "ДФ=ЧЧ:мм; ДП=00:00"));
			СтрокиДляПодстановки.Добавить(")");
		ИначеЕсли ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.КакМожноСкорее Тогда
			СтрокиДляПодстановки.Добавить(" ");
			СтрокиДляПодстановки.Добавить(НСтр("ru = '(на ближайшее время)';
												|en = '(for the near future)'"));
		КонецЕсли;
		
		ПараметрыОбласти.ОтправительДатаИВремяОтгрузкиПредставление = СтрСоединить(СтрокиДляПодстановки);
		
		ПараметрыОбласти.ПолучательДатаИВремяОтгрузкиПредставление = Формат(ДатаДоставки,"ДЛФ=DT;");
			
	КонецЕсли;
	
	ТабличныйДокумент.Вывести(ОбластьМакетаШапкаВремя);
	
	Если Кэш.ЭтоДеловыеЛинии Тогда
		Если ПропускПредставление <> "Нет" Тогда
			ОбластьМакетаПропуск = Макет.ПолучитьОбласть("Пропуск");
			ОбластьМакетаПропуск.Параметры.ПропускПредставление = ПропускПредставление;
			ТабличныйДокумент.Вывести(ОбластьМакетаПропуск);
		КонецЕсли;
		Если (СервисДоставки.ТранспортЗаполнен(ЭтотОбъект, "Отгрузки") Или СервисДоставки.ТранспортЗаполнен(ЭтотОбъект, "Доставки")) Тогда
			ОбластьМакетаТранспорт = Макет.ПолучитьОбласть("Транспорт");
			ЗаполнитьЗначенияСвойств(ОбластьМакетаТранспорт.Параметры, ЭтотОбъект);
			ТабличныйДокумент.Вывести(ОбластьМакетаТранспорт);
		КонецЕсли;
	КонецЕсли;
	
	ОбластьМакетаПараметрыГруза = Макет.ПолучитьОбласть("ПараметрыГруза");
	
	ПараметрыОбласти = ОбластьМакетаПараметрыГруза.Параметры;
	
	ПараметрыОбласти.Валюта = ВалютаНаименование;
	ПараметрыОбласти.ГрузСодержимое = ГрузСодержимое;
	ПараметрыОбласти.ГрузСтоимость = ГрузСтоимость;
	ПараметрыОбласти.ГрузКоличествоГрузовыхМест = ГрузКоличествоГрузовыхМест;
	ПараметрыОбласти.ГрузВес = ГрузВес;
	ПараметрыОбласти.ГрузОбъем = ГрузОбъем;
	ПараметрыОбласти.ГрузМаксимальныйВес = ГрузМаксимальныйВес;
	ПараметрыОбласти.ГрузМаксимальнаяВысота = ГрузМаксимальнаяВысота;
	ПараметрыОбласти.ГрузМаксимальнаяШирина = ГрузМаксимальнаяШирина;
	ПараметрыОбласти.ГрузМаксимальнаяДлина = ГрузМаксимальнаяДлина;
	
	ТабличныйДокумент.Вывести(ОбластьМакетаПараметрыГруза);
	
	Если ГрузНегабаритныйВес <> 0 
		ИЛИ ГрузНегабаритныйОбъем <> 0 
		ИЛИ ГрузКоличествоНегабаритныхГрузовыхМест <> 0 Тогда 
		ОбластьПараметрыНегабаритногоГруза = Макет.ПолучитьОбласть("ПараметрыНегабаритногоГруза");
		ОбластьПараметрыНегабаритногоГруза.Параметры.ГрузНегабаритныйВес = ГрузНегабаритныйВес;
		ОбластьПараметрыНегабаритногоГруза.Параметры.ГрузНегабаритныйОбъем = ГрузНегабаритныйОбъем;
		ОбластьПараметрыНегабаритногоГруза.Параметры.ГрузКоличествоНегабаритныхГрузовыхМест 
			= ГрузКоличествоНегабаритныхГрузовыхМест;
		
		ТабличныйДокумент.Вывести(ОбластьПараметрыНегабаритногоГруза);
	КонецЕсли;
	
	ОбластьТабличнаяЧастьУслуги = ТабличнаяЧастьУслуги(Макет, УслугиТарифа);
	ТабличныйДокумент.Вывести(ОбластьТабличнаяЧастьУслуги);
	
	Если ОплатаСтоимостьУслуг.Количество() > 0 И Кэш.ЭтоДеловыеЛинии Тогда
		ОбластьМакетаЗаголовокПлательщикиУслуг = Макет.ПолучитьОбласть("ЗаголовокПлательщикиУслуг");
		ТабличныйДокумент.Вывести(ОбластьМакетаЗаголовокПлательщикиУслуг);
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыПлательщикиУслуг");
		Для Каждого ПлательщикУслуги Из ОплатаСтоимостьУслуг Цикл
			ОбластьМакета.Параметры.Наименование = ПлательщикУслуги.Наименование;
			ОбластьМакета.Параметры.ПлательщикРоль = ПредставлениеПоРоли(ПлательщикУслуги.ПлательщикРоль);
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	
	Если СписокДокументов.Количество() > 0 И Кэш.ЭтоДеловыеЛинии Тогда
		ОбластьМакета = Макет.ПолучитьОбласть("ЗаголовокДокументыПеревозчика");
		ТабличныйДокумент.Вывести(ОбластьМакета);
		ОбластьМакета = Макет.ПолучитьОбласть("СтрокаТаблицыДокументыПеревозчика");
		Для Каждого ДокументПеревозчика Из СписокДокументов Цикл
			ОбластьМакета.Параметры.Наименование = СтрШаблон(НСтр("ru = '%1 № %2 от %3';
																	|en = '%1 No. %2 from %3'"),
				ДокументПеревозчика.ТипДокументаНаименование,
				ДокументПеревозчика.Номер,
				Формат(ДокументПеревозчика.Дата, "ДЛФ=D;"));
			ТабличныйДокумент.Вывести(ОбластьМакета);
		КонецЦикла;
	КонецЕсли;
	
	Если СуммаСкидки <> 0 Тогда
		ОбластьМакетаПодвал = Макет.ПолучитьОбласть("ПодвалСоСкидкой");
		ОбластьМакетаПодвал.Параметры.СуммаСкидки = Формат(СуммаСкидки,"ЧДЦ=2; ЧН=-");
	Иначе
		ОбластьМакетаПодвал = Макет.ПолучитьОбласть("Подвал");
	КонецЕсли;
	
	ОбластьМакетаПодвал.Параметры.СуммаДокумента = Формат(СуммаДокумента,"ЧДЦ=2; ЧН=-");
	ОбластьМакетаПодвал.Параметры.Валюта = ВалютаНаименование;
	ТабличныйДокумент.Вывести(ОбластьМакетаПодвал);
	
	Если ДополнительнаяИнформация <> "" Тогда
		ОбластьМакетаДополнительнаяИнформация = Макет.ПолучитьОбласть("ДополнительнаяИнформация");
		ОбластьМакетаДополнительнаяИнформация.Параметры.Комментарий = ДополнительнаяИнформация;
		ТабличныйДокумент.Вывести(ОбластьМакетаДополнительнаяИнформация);
	КонецЕсли;
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован() Тогда
		
		ОбластьМакетаДополнительныеДанныеЗаголовок1 			= Макет.ПолучитьОбласть("ДополнительныеДанныеЗаголовок1");
		ОбластьМакетаДополнительныеДанныеСтрока 				= Макет.ПолучитьОбласть("ДополнительныеДанныеСтрока");
		ОбластьМакетаДополнительныеДанныеСтрокаСНаименованием
			= Макет.ПолучитьОбласть("ДополнительныеДанныеСтрокаСНаименованием");
		
		Для Каждого ТекущиеДополнительныеДанные Из ДополнительныеДанные Цикл
			
			ТекущаяОбласть = ОбластьМакетаДополнительныеДанныеЗаголовок1;
			
			ТекущаяОбласть.Параметры.Наименование = ТекущиеДополнительныеДанные.Наименование;
			ТабличныйДокумент.Вывести(ТекущаяОбласть);
			
			Если ТекущиеДополнительныеДанные.Список.Количество() = 0 Тогда
				ТекущаяОбласть = ОбластьМакетаДополнительныеДанныеСтрока;
				ТекущаяОбласть.Параметры.Значение = ТекущиеДополнительныеДанные.Значение;
				ТабличныйДокумент.Вывести(ТекущаяОбласть);
			Иначе
				Для Каждого ТекущиеДанныеИзСписка Из ТекущиеДополнительныеДанные.Список Цикл
					
					Если ЗначениеЗаполнено(ТекущиеДанныеИзСписка.Наименование) Тогда
						ТекущаяОбласть = ОбластьМакетаДополнительныеДанныеСтрокаСНаименованием;
						ТекущаяОбласть.Параметры.Наименование = ТекущиеДанныеИзСписка.Наименование;
					Иначе
						ТекущаяОбласть = ОбластьМакетаДополнительныеДанныеСтрока;
					КонецЕсли;
					
					ТекущаяОбласть.Параметры.Значение = ТекущиеДанныеИзСписка.Значение;
					ТабличныйДокумент.Вывести(ТекущаяОбласть);
					
				КонецЦикла;
			КонецЕсли;
				
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТабличныйДокумент;

КонецФункции

&НаСервере
Процедура СформироватьИтоговуюИнформацию()
	СохранитьПлательщиковУслуг();
	ИтоговаяИнформация = ТабличныйДокументИтоговаяИнформация();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПолучателяПоОтправителю()
	
	ПараметрыУчастника = СервисДоставки.НовыйПараметрыУчастникаГрузоперевозки();
	ПараметрыКонтрагента = ПараметрыУчастника.Контрагент;
	ПараметрыКонтрагента.Ссылка = ОтправительКонтрагентСсылка;
	ПараметрыКонтрагента.ИНН = ОтправительКонтрагентИНН;
	ПараметрыКонтрагента.КПП = ОтправительКонтрагентКПП;
	ПараметрыКонтрагента.Наименование = ОтправительКонтрагентНаименование;
	ПараметрыКонтрагента.ЮрФизЛицо = ОтправительКонтрагентЮрФизЛицо;
	
	ЮридическийАдрес = ПараметрыКонтрагента.ЮридическийАдрес;
	ЮридическийАдрес.Значение = ОтправительКонтрагентЮридическийАдресЗначение;
	ЮридическийАдрес.Представление = ОтправительКонтрагентЮридическийАдресПредставление;
	
	ОбработатьПараметры(ПараметрыУчастника, "Получатель");
	ОтборыИзменение = Истина;
	УстановитьПризнакОрганизаций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораВремениИДаты(ВариантВыбораВремени, ТолькоПросмотр)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьВыборДатыВремени", ЭтотОбъект);
	
	ПараметрыОткрытияФормы = СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуВыборВремениПередачиГруза();
	
	Если ВариантВыбораВремени = 1 Тогда
		
		ПараметрыОткрытияФормы.Дата = ДатаОтгрузки;
		ПараметрыОткрытияФормы.ВремяРаботыС = ВремяОтгрузкиС;
		ПараметрыОткрытияФормы.ВремяРаботыПо = ВремяОтгрузкиПо;
		ПараметрыОткрытияФормы.ВремяОбедС = ВремяОтгрузкиОбедС;  
		ПараметрыОткрытияФормы.ВремяОбедПо = ВремяОтгрузкиОбедПо;
		ПараметрыОткрытияФормы.ФиксированноеВремя = ВремяОтгрузкиФиксированное;
		ПараметрыОткрытияФормы.ВариантВыбораВремени = 1;
		Если СпособОтгрузки = 1 Тогда
			ПараметрыОткрытияФормы.ВариантОтображения = ЧастиДаты.Дата;
		КонецЕсли;
		
	ИначеЕсли ВариантВыбораВремени = 2 Тогда
		
		ПараметрыОткрытияФормы.Дата = ДатаДоставки;
		ПараметрыОткрытияФормы.ВремяРаботыС = ВремяДоставкиС;
		ПараметрыОткрытияФормы.ВремяРаботыПо = ВремяДоставкиПо;
		ПараметрыОткрытияФормы.ВремяОбедС = ВремяДоставкиОбедС;  
		ПараметрыОткрытияФормы.ВремяОбедПо = ВремяДоставкиОбедПо;
		ПараметрыОткрытияФормы.ФиксированноеВремя = ВремяДоставкиФиксированное;
		ПараметрыОткрытияФормы.ВариантВыбораВремени = 2;
		Если СпособДоставки = 1 Тогда
			ПараметрыОткрытияФормы.ВариантОтображения = ЧастиДаты.Дата;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОткрытияФормы.ТолькоПросмотр = ТолькоПросмотр;
	ПараметрыОткрытияФормы.ТипГрузоперевозки = ТипГрузоперевозки;
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ВыборВремениПередачиГруза", ПараметрыОткрытияФормы, ЭтотОбъект, , , ,
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборДатыВремени(Результат, ДополнительныеПараметры) Экспорт
		
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	ВариантВыбораВремени = 0;
	ЗначенияДляКонтроля = ЗначенияКонтролируемыхРеквизитовДатыВремени();
	Если Результат.Свойство("ВариантВыбораВремени", ВариантВыбораВремени) Тогда
		Если ВариантВыбораВремени = 1 Тогда
			
			Результат.Свойство("Дата", ДатаОтгрузки);
			Результат.Свойство("ВремяРаботыС", ВремяОтгрузкиС);
			Результат.Свойство("ВремяРаботыПо", ВремяОтгрузкиПо);
			Результат.Свойство("ВремяОбедС", ВремяОтгрузкиОбедС);  
			Результат.Свойство("ВремяОбедПо", ВремяОтгрузкиОбедПо);
			Результат.Свойство("ФиксированноеВремя", ВремяОтгрузкиФиксированное);
			
			СформироватьПредставлениеВремениОтгрузки();
			
		ИначеЕсли ВариантВыбораВремени = 2 Тогда

			Результат.Свойство("ВремяРаботыС", ВремяДоставкиС);
			Результат.Свойство("ВремяРаботыПо", ВремяДоставкиПо);
			Результат.Свойство("ВремяОбедС", ВремяДоставкиОбедС);  
			Результат.Свойство("ВремяОбедПо", ВремяДоставкиОбедПо);
			Результат.Свойство("ФиксированноеВремя", ВремяДоставкиФиксированное);
			
			СформироватьПредставлениеВремениДоставки();
			
		КонецЕсли;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	ПроверитьВремяОтгрузки(ЕстьОшибки, Истина);
	Если ИзменилисьЗначенияКонтролируемыхРеквизитовДатыВремени(ЗначенияДляКонтроля) Тогда
		ЗарегистрироватьИзменениеОтборов();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИзменилисьЗначенияКонтролируемыхРеквизитовДатыВремени(ЗначенияДляКонтроля)
	Результат = Ложь;
	Для Каждого ЭлементКонтроля Из ЗначенияДляКонтроля Цикл
		Если ЭлементКонтроля.Значение <> ЭтотОбъект[ЭлементКонтроля.Ключ] Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ЗначенияКонтролируемыхРеквизитовДатыВремени()
	Результат = Новый Структура("ДатаОтгрузки, ВремяОтгрузкиФиксированное, ВремяДоставкиФиксированное");
	ЗаполнитьЗначенияСвойств(Результат, ЭтотОбъект);
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура СформироватьПредставлениеВремениОтгрузки()
	
	ВремяОтгрузкиПредставление = ПредставлениеДатыВремени(ДатаОтгрузки, 
									ВремяОтгрузкиС, 
									ВремяОтгрузкиПо, 
									ВремяОтгрузкиОбедС, 
									ВремяОтгрузкиОбедПо,
									ВремяОтгрузкиФиксированное,
									1);
									
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеВремениДоставки()
	
	ВремяДоставкиПредставление = ПредставлениеДатыВремени(ДатаДоставки, 
									ВремяДоставкиС, 
									ВремяДоставкиПо, 
									ВремяДоставкиОбедС, 
									ВремяДоставкиОбедПо,
									ВремяДоставкиФиксированное,
									2);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеДатыВремени(ДатаДоставки, ВремяРаботыС, ВремяРаботыПо, ВремяОбедС, ВремяОбедПо, Фиксированное, Режим)
	
	СтрокаПредставления = "";
	
	Если ЗначениеЗаполнено(ДатаДоставки) Тогда
		СтрокаПредставления = СтрШаблон("%1, ", Формат(ДатаДоставки, "ДФ=dd.MM.yy"));
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВремяРаботыС) Или ЗначениеЗаполнено(ВремяРаботыПо) Тогда
		СтрокаПредставления = СтрокаПредставления + Формат(ВремяРаботыС, "ДФ=HH:mm; ДП=00:00")
			+ "-" + Формат(ВремяРаботыПо, "ДФ=HH:mm");
	КонецЕсли;
	
	Если Не Фиксированное И (ЗначениеЗаполнено(ВремяОбедС) Или ЗначениеЗаполнено(ВремяОбедПо)) Тогда
		СтрокаПредставления = СтрокаПредставления + " (обед " + Формат(ВремяОбедС, "ДФ=HH:mm; ДП=00:00")
			+ "-" + Формат(ВремяОбедПо, "ДФ=HH:mm") + ")";
	КонецЕсли;
	
	Возврат СтрокаПредставления;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПодборТерминала(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Если ДополнительныеПараметры["Направление"] = 1 Тогда
		Адресат = "ПунктПриемаГруза";
	Иначе
		Адресат = "ПунктВыдачиГруза";
	КонецЕсли;
	
	Если Кэш.ЭтоДеловыеЛинии И Результат.ТерминалИдентификатор <> ЭтотОбъект[Адресат + "Идентификатор"] Тогда
		ЗарегистрироватьИзменениеОтборов(Истина);
	КонецЕсли;
	
	Результат.Свойство("ТерминалИдентификатор", ЭтотОбъект[Адресат + "Идентификатор"]);
	Результат.Свойство("ТерминалИдентификаторВСистемеГрузоперевозчика", ЭтотОбъект[Адресат + "ИдентификаторВСистемеГрузоперевозчика"]);
	Результат.Свойство("Терминал", ЭтотОбъект[Адресат + "Наименование"]);
	Результат.Свойство("Адрес", ЭтотОбъект[Адресат + "Адрес"]);
	Результат.Свойство("Телефон", ЭтотОбъект[Адресат + "Телефон"]);
	Результат.Свойство("Описание", ЭтотОбъект[Адресат + "Описание"]);
	Результат.Свойство("ТипНаименование", ЭтотОбъект[Адресат + "ТипНаименование"]);
	Результат.Свойство("ТипИдентификатор", ЭтотОбъект[Адресат + "ТипИдентификатор"]);
	Результат.Свойство("НаселенныйПунктИдентификатор", ЭтотОбъект[Адресат + "НаселенныйПунктИдентификатор"]);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТерминала(Идентификатор)
	
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("Идентификатор", Идентификатор);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму(
		"Обработка.СервисДоставки.Форма.КарточкаТерминала",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура СброситьВзаимоисключающие(ТекущиеДанные, ИмяЭлементаФормы)
	
	НайденныеСтроки = НесовместимыеУслуги.НайтиСтроки(Новый Структура("Идентификатор", ТекущиеДанные.Идентификатор));
	
	Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
		СброситьВзаимоисключающее(ТекущиеДанные, ТекущаяСтрока, ИмяЭлементаФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьВзаимоисключающее(ТекущиеДанные, СтрокаНесовместимыхУслуг, ИмяЭлементаФормы)
	
	ИдентификаторНесовместимойУслуги = СтрокаНесовместимыхУслуг.ИдентификаторНесовместимойУслуги;
	Если ТекущиеДанные.Идентификатор = СтрокаНесовместимыхУслуг.Идентификатор Тогда
		Родитель = ТекущиеДанные.ПолучитьРодителя();
		СтрокиДерева = Родитель.ПолучитьЭлементы();
		Для Каждого ТекущаяСтрокаДерева Из СтрокиДерева Цикл
			Если ТекущаяСтрокаДерева.Идентификатор = ИдентификаторНесовместимойУслуги Тогда
				ТекущаяСтрокаДерева.Использовать = Ложь;
				Элементы[ИмяЭлементаФормы].Свернуть(ТекущаяСтрокаДерева.ПолучитьИдентификатор());
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьИзменениеОтборов(СброситьТариф = Истина)
	
	ОтборыИзменение = Истина;
	ВыделенныйТариф = "";
	СформироватьНадписьОтбора();
	
	Если СброситьТариф Тогда
		СброситьТариф();
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура СформироватьНадписьОтбора()
	
	ТекстЗаголовка = НСтр("ru = 'Настроить отбор';
							|en = 'Configure filter'");
	Элементы.ГруппаОтборы.Заголовок = ?(ОтборыИзменение, ТекстЗаголовка + "*", ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуГрафикаДвиженияЗаказа()
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("ИдентификаторЗаказа", ИдентификаторЗаказа);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ОтслеживаниеЗаказа",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,
		ИдентификаторЗаказа,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуМультизаказа()
	
	ПараметрыОткрытияФормы = СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуСервисаДоставки();
	ПараметрыОткрытияФормы.ИмяФормы = "КарточкаМультизаказа";
	ПараметрыОткрытияФормы.ТипГрузоперевозки = ТипГрузоперевозки;
	ПараметрыОткрытияФормы.ОрганизацияБизнесСети = ОрганизацияБизнесСетиСсылка;
	ПараметрыОткрытияФормы.ИдентификаторЗаказа = МультизаказИдентификатор;
	ПараметрыОткрытияФормы.Уникальность = МультизаказИдентификатор;
	
	СервисДоставкиКлиент.ОткрытьФормуСервисаДоставки(ПараметрыОткрытияФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;

	Адресат = ОпределитьАдресата(Элемент.Имя);
	ПредставлениеАдресаДо = ЭтотОбъект[Адресат + "АдресПредставление"];

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Структура") И ВыбранноеЗначение.Свойство("Адрес") Тогда

		ЭтотОбъект[Адресат + "АдресЗначение"] = ВыбранноеЗначение.Адрес;
		
		Если Кэш.ЭтоДеловыеЛинии Тогда
			Представление = ПредставлениеАдресаБезМуниципальнойЧасти(ВыбранноеЗначение.Адрес);
			ЭтотОбъект[Адресат + "АдресПредставление"] = ?(ПустаяСтрока(Представление),
				ВыбранноеЗначение.Представление, Представление);
		Иначе
			ЭтотОбъект[Адресат + "АдресПредставление"] = ВыбранноеЗначение.Представление;
		КонецЕсли;
		
	Иначе

		Если ТипЗнч(ВыбранноеЗначение) = Тип("Число") Тогда
			АдресОбработкаВыбораПродолжение(Элемент, ВыбранноеЗначение);
		Иначе

			ПоискЗначения = Элементы[Адресат + "Адрес"].СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
			Если ПоискЗначения <> Неопределено Тогда

				ЭтотОбъект[Адресат + "АдресЗначение"] = ПоискЗначения.Значение;
				
				Если Кэш.ЭтоДеловыеЛинии Тогда
					Представление = ПредставлениеАдресаБезМуниципальнойЧасти(ПоискЗначения.Значение);
					ЭтотОбъект[Адресат + "АдресПредставление"] = ?(ПустаяСтрока(Представление),
						ПредставлениеКонтактнойИнформации(ЭтотОбъект[Адресат + "АдресЗначение"]), Представление);
				Иначе
					ЭтотОбъект[Адресат + "АдресПредставление"] = ПредставлениеКонтактнойИнформации(ЭтотОбъект[Адресат
						+ "АдресЗначение"]);
				КонецЕсли;

				КомментарийКАдресу = КомментарийИзКонтактнойИнформации(ЭтотОбъект[Адресат + "АдресЗначение"]);
				Если ЗначениеЗаполнено(КомментарийКАдресу) Тогда
					ЗадатьВопросДобавлениеКомментария(КомментарийКАдресу);
				КонецЕсли;

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	Если ПредставлениеАдресаДо <> ЭтотОбъект[Адресат + "АдресПредставление"] Тогда
		ЗарегистрироватьИзменениеОтборов(Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АдресОбработкаВыбораПродолжение(Элемент, ВыбранноеЗначение)
	
	ПараметрыОтбора = Новый Структура(); 
	
	ИмяРеквизита = Элемент.Имя;
	
	Префикс = СтрЗаменить(ИмяРеквизита, "Адрес", "");
	ПоследнийСимвол = Прав(Префикс, 1);
	Префикс = ?(ПоследнийСимвол = "1" Или ПоследнийСимвол = "2", Лев(Префикс, СтрДлина(Префикс) - 1), Префикс);
	
	Если ВыбранноеЗначение = 1 Тогда
		
		ЗаполнитьПараметрыОтбораАдресаОрганизации(ПараметрыОтбора, ЭтотОбъект[Префикс + "КонтрагентСсылка"]);
		ОткрытьФормуВыбора("АдресОрганизацииСервисДоставки", Элемент.Имя + "Владелец", ПараметрыОтбора);
		
	ИначеЕсли ВыбранноеЗначение = 2 Тогда
			
		ЗаполнитьПараметрыОтбораАдресаКонтрагента(ПараметрыОтбора, ЭтотОбъект[Префикс + "КонтрагентСсылка"]);
		ОткрытьФормуВыбора("АдресКонтрагентаСервисДоставки", Элемент.Имя + "Владелец", ПараметрыОтбора);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст)
		И ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.КонтактнаяИнформация") Тогда
		МодульУправлениеКонтактнойИнформациейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("УправлениеКонтактнойИнформациейКлиент");
		МодульУправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОтбораАдресаОрганизации(ПараметрыОтбора, ПолучательКонтрагентСсылка)
	СервисДоставкиПереопределяемый.ЗаполнитьПараметрыОтбораАдресаОрганизации(ПараметрыОтбора, ПолучательКонтрагентСсылка);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОтбораАдресаКонтрагента(ПараметрыОтбора, ПолучательКонтрагентСсылка)
	СервисДоставкиПереопределяемый.ЗаполнитьПараметрыОтбораАдресаКонтрагента(ПараметрыОтбора, ПолучательКонтрагентСсылка);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуГрузоперевозчика()
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ПараметрыОткрытияФормы.Вставить("Идентификатор", ГрузоперевозчикИдентификатор);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.КарточкаГрузоперевозчика", ПараметрыОткрытияФормы, ,
		ГрузоперевозчикИдентификатор, , , , РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуУслуги(ИдентификаторУслуги)
		
	ПараметрыОткрытияФормы = Новый Структура();
	
	ПараметрыОткрытияФормы.Вставить("Идентификатор", ИдентификаторУслуги);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.КарточкаУслуги", ПараметрыОткрытияФормы, , , , , ,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуТарифа(Идентификатор = "")
	
	Идентификатор = ?(ПустаяСтрока(Идентификатор), ТарифИдентификатор, Идентификатор);
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("Идентификатор", Идентификатор);
	ПараметрыОткрытияФормы.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	ПараметрыОткрытияФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.КарточкаТарифа", ПараметрыОткрытияФормы, , ТарифИдентификатор, , , ,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

// Возвращает список ошибок заполнения в виде списка значений.
// 
// Параметры:
//  Представление - Строка - Описание ошибки
//  Значение - Строка - XPath для поля
// 
// Возвращаемое значение:
//  СписокЗначений Из Строка - Ошибки заполнения телефона
&НаКлиентеНаСервереБезКонтекста
Функция ОшибкиЗаполненияТелефона(Знач Представление, Знач Значение)
	
	СписокОшибок = Новый СписокЗначений;
	
	Если Представление = "" Тогда
		Возврат СписокОшибок;
	КонецЕсли;
	
	СтруктураТелефона = Неопределено;
	
	Если Значение <> "" Тогда
		
		Отказ = Ложь;
		ТекстОшибки = "";
		СтруктураТелефона = ЗначениеИзСтрокиJSON(Значение, Отказ, ТекстОшибки);
		
	КонецЕсли;
	
	Если СтруктураТелефона = Неопределено Тогда
		
		Значение = СервисДоставкиВызовСервера.КонтактнаяИнформацияПоПредставлению(Представление, "Телефон");
		Отказ = Ложь;
		ТекстОшибки = "";
		СтруктураТелефона = ЗначениеИзСтрокиJSON(Значение, Отказ, ТекстОшибки);
		
	КонецЕсли;
	
	ПолныйНомерТелефона = "";
	Если СтруктураТелефона.Свойство("countryCode") Тогда
		ПолныйНомерТелефона = ПолныйНомерТелефона + СтруктураТелефона.countryCode;
	КонецЕсли;
	
	Если СтруктураТелефона.Свойство("areaCode") Тогда
		ПолныйНомерТелефона = ПолныйНомерТелефона + СтруктураТелефона.areaCode;
	КонецЕсли;
	
	Если СтруктураТелефона.Свойство("number") Тогда
		ПолныйНомерТелефона = ПолныйНомерТелефона + СтруктураТелефона.number;
	КонецЕсли;
	
	НомерТелефонаТолькоЦифры = ТолькоЦифры(ПолныйНомерТелефона);
	
	Если СтрДлина(НомерТелефонаТолькоЦифры) < 10 Тогда
		СписокОшибок.Добавить("НомерТелефона", НСтр("ru = 'Номер телефона слишком короткий (менее 10 цифр)';
													|en = 'Too short phone number (less than 10 digits)'"));
	КонецЕсли;
	
	Если СтрДлина(НомерТелефонаТолькоЦифры) > 15 Тогда
		СписокОшибок.Добавить("НомерТелефона", НСтр("ru = 'Номер телефона слишком длинный';
													|en = 'Phone number is too long.'"));
	КонецЕсли;
	
	Если НомерТелефонаСодержитНедопустимыеСимволы(ПолныйНомерТелефона) Тогда
		СписокОшибок.Добавить("НомерТелефона", НСтр("ru = 'Номер телефона содержит недопустимые символы';
													|en = 'Phone number contains illegal characters.'"));
	КонецЕсли;
	
	Возврат СписокОшибок;
	
КонецФункции

// Проверяет, содержит ли строка только
// 
// Параметры:
//  СтрокаПроверки - Строка - Строка для проверки.
// 
// Возвращаемое значение:
//  Булево - Истина - строка содержит только цифры или пустая, Ложь - строка содержит иные символы.
&НаКлиентеНаСервереБезКонтекста
Функция НомерТелефонаСодержитНедопустимыеСимволы(Знач СтрокаПроверки)
	Возврат СтрРазделить(СтрокаПроверки, "+-.,() wp1234567890", Ложь).Количество() > 0;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТолькоЦифры(Знач СтрокаДляПроверки)
	Возврат СервисДоставкиКлиентСервер.ТолькоЦифры(СтрокаДляПроверки);
КонецФункции

&НаСервереБезКонтекста
Функция ОрганизацииБизнесСетиНаСервере()
	
	Возврат ОбщегоНазначения.ТаблицаЗначенийВМассив(СервисДоставкиСлужебный.ОрганизацииБизнесСети());
	
КонецФункции

&НаКлиенте
Процедура ОрганизацияБизнесСетиСсылкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СписокВыбора = Элементы.ОрганизацияБизнесСетиСсылка.СписокВыбора;
	СписокВыбора.Очистить();
	
	ОрганизацииБизнесСети = ОрганизацииБизнесСетиНаСервере();
	Для Каждого ТекущаяОрганизация Из ОрганизацииБизнесСети Цикл
		СписокВыбора.Добавить(ТекущаяОрганизация.Организация, ТекущаяОрганизация.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначениеИзСтрокиJSON(Значение, Отказ, ТекстОшибки)
	
	Возврат СервисДоставки.ЗначениеИзСтрокиJSON(Значение, Отказ, ТекстОшибки);
	
КонецФункции

&НаКлиенте
Процедура ТоварныйСостав(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТоварныйСостав", ТоварныйСостав);
	ПараметрыФормы.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыФормы.Вставить("НаложенныйПлатежВидОплаты", НаложенныйПлатежВидОплаты);
	
	ОткрытьФорму("Обработка.СервисДоставки.Форма.ТоварныйСостав",ПараметрыФормы, ЭтотОбъект,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыДопУслугПриАктивизацииЯчейки(Элемент)
	
	ТекущиеДанные = Элементы.ТарифДополнительныеУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
	
		Поле = Элемент.ТекущийЭлемент;
		Поле.ТолькоПросмотр = Не ТекущиеДанные.ТребуетсяЗначение;
		
		Если ТекущиеДанные.ТипСтроки = 1 И ТекущиеДанные.Обязательная Тогда
			Элементы.ТарифДополнительныеУслуги.Развернуть(ТекущиеДанные.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТарифДополнительныеУслугиПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.ТарифДополнительныеУслуги.ТекущиеДанные;
	
	Если Элементы.ТарифДополнительныеУслуги.ТекущийЭлемент = Элементы.ОборотыДопУслугПоказыватьИнформацию
		И ТекущиеДанные.ПоказыватьИнформацию Тогда
		ОткрытьФормуУслуги(ТекущиеДанные.Идентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыДопУслугПередРазворачиванием(Элемент, Строка, Отказ)
	
	ТекущиеДанные = ТарифДополнительныеУслуги.НайтиПоИдентификатору(Строка);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если (Не ТекущиеДанные.Использовать) И ТекущиеДанные.ТипСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыДопУслугПередСворачиванием(Элемент, Строка, Отказ)
	
	ТекущиеДанные = ТарифДополнительныеУслуги.НайтиПоИдентификатору(Строка);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Использовать И ТекущиеДанные.ТипСтроки = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыДопУслугЗначениеПриИзменении(Элемент)
	
	СтрокаОтбора = ТарифДополнительныеУслуги.НайтиПоИдентификатору(Элементы.ТарифДополнительныеУслуги.ТекущаяСтрока);
	Если СтрокаОтбора <> Неопределено Тогда
		СтрокаУслуги = СтрокаОтбора.ПолучитьРодителя();
		
		Если ЗначениеЗаполнено(Элементы.ТарифДополнительныеУслуги.ТекущиеДанные.Значение) Тогда
			СтрокаУслуги.Использовать = Истина;
		КонецЕсли;
		ЗарегистрироватьИзменениеДополнительныхУслуг(СтрокаУслуги);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыДопУслугИспользоватьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТарифДополнительныеУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗарегистрироватьИзменениеДополнительныхУслуг(ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьДопУслугиПоТарифу(Знач ТабДопУслуг)

	ДеревоДопУслуг = ТарифДополнительныеУслуги.ПолучитьЭлементы();
	
	ДеревоДопУслуг.Очистить();
	
	ТекущаяКатегория = "";
	
	МассивВыбранныхУслуг = Новый Массив();
	
	Для Каждого ТекущаяУслуга Из ТабДопУслуг Цикл
		
		ЕстьНесовместимаяУслуга = Ложь;
		Для Каждого ИдентификаторВыбраннойУслуги Из МассивВыбранныхУслуг Цикл
			
			Если НесовместимыеУслуги.НайтиСтроки(Новый Структура("Идентификатор,ИдентификаторНесовместимойУслуги",
				ИдентификаторВыбраннойУслуги, ТекущаяУслуга.Идентификатор)).Количество() Тогда
				ЕстьНесовместимаяУслуга = Истина;
				Продолжить;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЕстьНесовместимаяУслуга Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущаяКатегория <> ТекущаяУслуга.Категория Тогда
			ТекущаяКатегория = ТекущаяУслуга.Категория;
			
			СтрокаДерева = ДеревоДопУслуг.Добавить();
			СтрокаДерева.Наименование = ТекущаяУслуга.Категория;
			СтрокаДерева.Использовать = Истина;
			СтрокаДерева.Идентификатор = "";
			СтрокаДерева.Значение = Неопределено;
			СтрокаДерева.ТребуетсяЗначение = Ложь;
			СтрокаДерева.Обязательная = Ложь;
			СтрокаДерева.ТипСтроки = 0;
			
		КонецЕсли;
		
		СтрокаДереваВетка = СтрокаДерева.ПолучитьЭлементы().Добавить();
		СтрокаДереваВетка.Наименование = ТекущаяУслуга.Наименование;
		СтрокаДереваВетка.Использовать = ТекущаяУслуга.Использовать;
		СтрокаДереваВетка.Идентификатор = ТекущаяУслуга.Идентификатор;
		СтрокаДереваВетка.Категория = ТекущаяУслуга.Категория;
		СтрокаДереваВетка.ТипСтроки = 1;
		СтрокаДереваВетка.ИдентификаторУслугТарифов = ТекущаяУслуга.ПолучитьИдентификатор();
		СтрокаДереваВетка.ПоказыватьИнформацию = ТекущаяУслуга.ПоказыватьИнформацию;
		СтрокаДереваВетка.Обязательная = ТекущаяУслуга.Обязательная;
		
		Если СтрокаДереваВетка.Обязательная Тогда
			СтрокаДереваВетка.Использовать = Истина;
			МассивВыбранныхУслуг.Добавить(ТекущаяУслуга.Идентификатор);
		КонецЕсли;
		
		НайденныеСтроки = УслугиТарифа.НайтиСтроки(Новый Структура("Идентификатор", ТекущаяУслуга.Идентификатор));
		
		Свойства = Неопределено;
		Если НайденныеСтроки.Количество() Тогда
			СтрокаДереваВетка.Использовать = Истина;
			Свойства = НайденныеСтроки[0].Свойства;
		КонецЕсли;
		
		СвойстваУслуги = ТекущаяУслуга.Свойства;
		
		Если СвойстваУслуги.Количество() Тогда
			
			СтрокиСвойств = СтрокаДереваВетка.ПолучитьЭлементы();
			СтрокиСвойств.Очистить();
			
			Для Каждого ТекущееСвойство Из СвойстваУслуги Цикл
				
				Если Не Свойства = Неопределено Тогда
					НайденныеСвойства = Свойства.НайтиСтроки(Новый Структура("Идентификатор", ТекущееСвойство.Идентификатор));
				КонецЕсли;
				
				СтрокаДереваСвойство = СтрокиСвойств.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаДереваСвойство, ТекущееСвойство);
				СтрокаДереваСвойство.ТребуетсяЗначение = Истина;
				СтрокаДереваСвойство.ТипСтроки = 2;
				Если Не Свойства = Неопределено И НайденныеСвойства.Количество() Тогда
					СтрокаДереваСвойство.Значение = НайденныеСвойства[0].Значение;
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименитьДополнительныеУслуги(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнениеЗначенийДополнительныхУслуг(ЭтотОбъект) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Есть незаполненные свойства для выбранных дополнительных услуг.';
														|en = 'There are empty properties for selected additional services.'"));
	Иначе
		Модифицированность = Истина;
		Элементы.ГруппаДополнительныхУслугТарифа.Скрыть();
		Элементы.ГруппаПредупрежденияПоДопУслугам.Видимость = Ложь;
		ОбновитьИнформациюПоВыбраннымДопУслугам();
	КонецЕсли;
	
	Если ДополнительныеУслугиИзменение Тогда
		ОбновитьТариф();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПоВыбраннымДопУслугам()
	
	Для Каждого ТекущаяКатегория Из ТарифДополнительныеУслуги.ПолучитьЭлементы() Цикл
		Для Каждого ТекущаяДопУслуга Из ТекущаяКатегория.ПолучитьЭлементы() Цикл
			Если ТекущаяДопУслуга.Использовать Тогда
				ОтборПоИдентификатору = Новый Структура();
				ОтборПоИдентификатору.Вставить("Идентификатор", ТекущаяДопУслуга.Идентификатор);
				НайденныеСтрокиУслуг = УслугиТарифа.НайтиСтроки(ОтборПоИдентификатору);
				Если НайденныеСтрокиУслуг.Количество() Тогда
					НоваяУслуга = НайденныеСтрокиУслуг[0];
				Иначе
					НоваяУслуга = УслугиТарифа.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяУслуга, ТекущаяДопУслуга,"Идентификатор, Наименование, Использовать, Обязательная, Категория");
				КонецЕсли;
				
				НоваяУслуга.Свойства.Очистить();
				СтрокиСвойств = ТекущаяДопУслуга.ПолучитьЭлементы();
				
				Для Каждого ТекущееСвойство Из СтрокиСвойств Цикл
					Если ЗначениеЗаполнено(ТекущееСвойство.Значение) Тогда
						НовоеСвойство = НоваяУслуга.Свойства.Добавить();
						ЗаполнитьЗначенияСвойств(НовоеСвойство, ТекущееСвойство);
					КонецЕсли;
				КонецЦикла;
			Иначе
				ОтборПоИдентификатору = Новый Структура();
				ОтборПоИдентификатору.Вставить("Идентификатор", ТекущаяДопУслуга.Идентификатор);
				НайденныеСтрокиУслуг = УслугиТарифа.НайтиСтроки(ОтборПоИдентификатору);
				Если НайденныеСтрокиУслуг.Количество() Тогда
					УслугиТарифа.Удалить(НайденныеСтрокиУслуг[0]);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗаполнитьИнформациюПоТекущемуТарифу();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьЗаполнениеЗначенийДополнительныхУслуг(Знач Форма)
	
	Отказ = Ложь;
	
	ДеревоДопУслуг = Форма["ТарифДополнительныеУслуги"].ПолучитьЭлементы();
	
	Для Каждого СтрокаДопУслуги Из ДеревоДопУслуг Цикл
		
		СтрокиУслуг = СтрокаДопУслуги.ПолучитьЭлементы();
		
		Для Каждого СтрокаУслуги Из СтрокиУслуг Цикл
			
			Если СтрокаУслуги.Использовать
				Или СтрокаУслуги.Обязательная Тогда
				
				СвойстваУслуги = СтрокаУслуги.ПолучитьЭлементы();
				
				Для Каждого СвойствоУслуги Из СвойстваУслуги Цикл
					
					Если Не ЗначениеЗаполнено(СвойствоУслуги.Значение) Тогда
						Отказ = Истина;
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если Отказ Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Если Отказ Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура ОборотыДопУслугТребуетсяЗначениеПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ТарифДополнительныеУслуги.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Поле = Элемент.ТекущийЭлемент;
		Поле.ТолькоПросмотр = ТекущиеДанные.Обязательная;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РеквизитыДляПересчета()
	Возврат СтрРазделить(
		"Вес,Объем,МаксимальнаяВысота,МаксимальнаяДлина,МаксимальнаяШирина,МаксимальныйВес,Содержимое,СодержимоеИдентификатор,Стоимость", ",");
КонецФункции

&НаКлиенте
Процедура УстановитьКомандыПересчетаПараметровГруза()
	
	Элементы.ОбновитьСтоимость.Видимость = ЗначениеЗаполнено(ГрузСтоимостьДляПересчета);
	Элементы.ОбновитьСодержимое.Видимость = ЗначениеЗаполнено(ГрузСодержимоеДляПересчета) 
		Или ЗначениеЗаполнено(ГрузСодержимоеИдентификаторДляПересчета);
	Элементы.ОбновитьВес.Видимость = ЗначениеЗаполнено(ГрузВесДляПересчета);
	Элементы.ОбновитьОбъем.Видимость = ЗначениеЗаполнено(ГрузОбъемДляПересчета);
	Элементы.ОбновитьОбщийВес.Видимость = ЗначениеЗаполнено(ГрузВесДляПересчета);
	Элементы.ОбновитьОбщийОбъем.Видимость = ЗначениеЗаполнено(ГрузОбъемДляПересчета);
	Элементы.ОбновитьДлину.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяДлинаДляПересчета);
	Элементы.ОбновитьШирину.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяШиринаДляПересчета);
	Элементы.ОбновитьВысоту.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяВысотаДляПересчета);
	Элементы.ОбновитьМаксимальнуюДлину.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяДлинаДляПересчета);
	Элементы.ОбновитьМаксимальнуюШирину.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяШиринаДляПересчета);
	Элементы.ОбновитьМаксимальнуюВысоту.Видимость = ЗначениеЗаполнено(ГрузМаксимальнаяВысотаДляПересчета);
	Элементы.ОбновитьМаксимальныйВес.Видимость = ЗначениеЗаполнено(ГрузМаксимальныйВесДляПересчета);
	
	МассивРеквизитовДляПересчета = РеквизитыДляПересчета();
	
	ГруппаОбщаяПересчетВидимость = Ложь;
	Для Каждого ТекРеквизитДляПересчета Из МассивРеквизитовДляПересчета Цикл
		ИмяРеквизита = "Груз" + ТекРеквизитДляПересчета + "ДляПересчета";
		ГруппаОбщаяПересчетВидимость = ГруппаОбщаяПересчетВидимость 
			ИЛИ ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]);
			
		Если ГруппаОбщаяПересчетВидимость Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ГруппаОбщаяПересчет.Видимость = ГруппаОбщаяПересчетВидимость;
	Элементы.ДекорацияОснованиеПересчет.Видимость = ГруппаОбщаяПересчетВидимость;
	
	Если (Элементы.ГруппаСтраницы.ТекущаяСтраница
		<> Элементы.ГруппаОсновное)
		И ГруппаОбщаяПересчетВидимость Тогда
		ПерейтиКШагуГруз();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеТарифа(Параметры)
	
	Параметры.ТарифПредставление = Параметры.ТарифНаименование 
		+ ?(Параметры.ПоУмолчанию, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			" (%1)",
			НСтр("ru = 'по умолчанию';
				|en = 'default'")), "");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьИзменениеДополнительныхУслуг(ТекущиеДанные)
	
	Модифицированность = Истина;
	ДополнительныеУслугиИзменение = Истина;
	СформироватьЗаголовокГруппыДополнительныхУслуг();

	Если ТекущиеДанные.Использовать Тогда
		СброситьВзаимоисключающие(ТекущиеДанные, "ТарифДополнительныеУслуги");
		Элементы.ТарифДополнительныеУслуги.Развернуть(ТекущиеДанные.ПолучитьИдентификатор());
	Иначе
		Элементы.ТарифДополнительныеУслуги.Свернуть(ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	ОбновитьИнформациюПоВыбраннымДопУслугам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьТарифыАвтоматически()
	
	Если ГрузоперевозчикИдентификатор = ""
		ИЛИ УслугиТарифов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераНовый() Тогда
		ПерейтиКТекущемуШагуМастера(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТариф()
	
	НеЗагружатьУслуги = Истина;
	ПолучитьТариф();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьЗаголовокГруппыДополнительныхУслуг()
	Элементы.ГруппаДополнительныхУслугТарифа.Заголовок = НСтр("ru = 'Дополнительные услуги';
																|en = 'Additional services'") + ?(ДополнительныеУслугиИзменение, " *", "");
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКТекущемуШагуМастера(ВыводитьПредупреждения, ПолучитьТарифыАвтоматически = Ложь)
	
	Отказ = Ложь;
	ПроверитьЗаполнениеОбязательныхРеквизитов(4, Отказ, ВыводитьПредупреждения, ПолучитьТарифыАвтоматически);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПерейтиКШагуПроверка();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьДополнительныхТелефонов(ВидДополнительногоТелефона = "Все")
	
	Если ВидДополнительногоТелефона = "Все" Или ВидДополнительногоТелефона = "Отправитель" Тогда
		Элементы.ГруппаОтправительТелефонДополнительный.Видимость = ОтправительКонтактноеЛицоТелефонДополнительныйДоступен;
		Элементы.ОтправительДобавитьТелефонДополнительный.Видимость = Не ОтправительКонтактноеЛицоТелефонДополнительныйДоступен;
	КонецЕсли;
		
	Если ВидДополнительногоТелефона = "Все" Или ВидДополнительногоТелефона = "Получатель" Тогда
		Элементы.ГруппаПолучательТелефонДополнительный.Видимость = ПолучательКонтактноеЛицоТелефонДополнительныйДоступен;
		Элементы.ПолучательДобавитьТелефонДополнительный.Видимость = Не ПолучательКонтактноеЛицоТелефонДополнительныйДоступен;
	КонецЕсли;
		
	Если ВидДополнительногоТелефона = "Все" Или ВидДополнительногоТелефона = "Плательщик" Тогда
		Элементы.ГруппаПлательщикТелефонДополнительный.Видимость = ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен;
		Элементы.ПлательщикДобавитьТелефонДополнительный.Видимость = Не ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьСпособовОтгрузкиДоставки()
	
	Если ТипГрузоперевозки <> СервисДоставкиКлиентСервер.ТипГрузоперевозкиСервис1СДоставка() Тогда
		Возврат;
	КонецЕсли;
	
	ОтправкаОтАдреса = СпособОтгрузки = 2;
	ДоставкаДоАдреса = СпособДоставки = 2;
	
	Элементы.ОтправительАдрес.Видимость = ОтправкаОтАдреса;
	Элементы.ОтправительАдрес1.Видимость = ОтправкаОтАдреса;
	Элементы.ОтправительТерминал1.Видимость = Не ОтправкаОтАдреса;
	Элементы.ОтправительТерминал2.Видимость = Не ОтправкаОтАдреса;
	
	Элементы.ПолучательАдрес.Видимость = ДоставкаДоАдреса;
	Элементы.ПолучательАдрес1.Видимость = ДоставкаДоАдреса;
	Элементы.ПолучательТерминал1.Видимость = Не ДоставкаДоАдреса;
	Элементы.ПолучательТерминал2.Видимость = Не ДоставкаДоАдреса;
	
	Элементы.ПропускПредставление.Видимость = ОтправкаОтАдреса;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СкопироватьРекурсивно(Источник)

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	Возврат ОбщегоНазначения.СкопироватьРекурсивно(Источник);
#Иначе
	Возврат ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Источник);
#КонецЕсли

КонецФункции

&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитовНаКлиенте(ШагОшибки, Отказ, Знач ВыводитьПредупреждения = Истина)
	
	ЕстьОшибки = Ложь;
	ПроверитьРеквизитыКонтрагентов(ЕстьОшибки, ВыводитьПредупреждения);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 0;
	КонецЕсли;
		
	ЕстьОшибки = Ложь;
	ПроверитьДатуОтгрузки(ЕстьОшибки, ВыводитьПредупреждения);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 0;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	ПроверитьДанныеКонтактныхЛиц(ЕстьОшибки, ВыводитьПредупреждения);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнениеРеквизитовНаСервере(ШагОшибки, Отказ, Знач ВыводитьПредупреждения = Истина)
	
	ЕстьОшибки = Ложь;
	ПроверитьРеквизитыАдресов(ЕстьОшибки, ВыводитьПредупреждения);
	Если ЕстьОшибки Тогда
		Отказ = Истина;
		ШагОшибки = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьРеквизитыАдресов(ЕстьОшибки, Знач ВыводитьПредупреждения)
	
	ПроверитьРеквизитыАдреса("ОтправительАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	ПроверитьРеквизитыАдреса("ПолучательАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	Если ЭтотОбъект["ПлательщикРоль"] = 3 Тогда
		ПроверитьРеквизитыАдреса("ПлательщикАдрес", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьРеквизитыАдреса(Знач ИмяРеквизита, ЕстьОшибки, Знач ВыводитьПредупреждения)
	СервисДоставкиСлужебный.ПроверитьРеквизитыАдреса(ЭтотОбъект, ИмяРеквизита, ЕстьОшибки, ВыводитьПредупреждения);
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРеквизитыКонтрагентов(ЕстьОшибки, Знач ВыводитьПредупреждения = Истина)
	
	ПроверитьРеквизитыКонтрагента(ЭтотОбъект, "ОтправительКонтрагент", ЕстьОшибки, ВыводитьПредупреждения);
	ПроверитьРеквизитыКонтрагента(ЭтотОбъект, "ПолучательКонтрагент", ЕстьОшибки, ВыводитьПредупреждения);
	
	Если ПлательщикРоль = 3 Тогда
		ПроверитьРеквизитыКонтрагента(ЭтотОбъект, "ПлательщикКонтрагент", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьРеквизитыКонтрагента(Знач Форма, Знач Префикс, ЕстьОшибки, Знач ВыводитьПредупреждения = Истина)
	
	ИмяРеквизита = Префикс + "Наименование";
	КонтрагентСсылка = Форма[Префикс + "Ссылка"];
	КонтрагентНаименование = Форма[Префикс + "Наименование"];
	КонтрагентЮрФизЛицо = Форма[Префикс + "ЮрФизЛицо"];
	КонтрагентИНН = Форма[Префикс + "ИНН"];
	КонтрагентКПП = Форма[Префикс + "КПП"];
	
	ЭтоОтправитель = (СтрНайти(Префикс, "Отправитель") > 0);
	
	Если Не ЗначениеЗаполнено(КонтрагентСсылка) 
		И КонтрагентНаименование = "" Тогда
		Возврат;
	КонецЕсли;
	
	СписокОшибок = Новый СписокЗначений;
	УчастникГрузоперевозки = СтрЗаменить(Префикс, "Контрагент", "");
	ТекстОшибки = НСтр("ru = 'Некорректный реквизит ""%1"" у участника грузоперевозки в поле ""%2"".';
						|en = 'Cargo transportation participant has Incorrect attributes ""%1"" in the field ""%2""'");
	
	Если ЗначениеЗаполнено(КонтрагентСсылка) 
		И КонтрагентНаименование = "" Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			НСтр("ru = 'Наименование';
				|en = 'Description'"),
			УчастникГрузоперевозки);
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
	КонецЕсли;
	
	Если КонтрагентЮрФизЛицо = 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			НСтр("ru = 'Вид контрагента';
				|en = 'Counterparty type'"),
			УчастникГрузоперевозки);
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
	КонецЕсли;
	
	Если КонтрагентЮрФизЛицо = 2 Тогда
		Если ЭтоОтправитель Тогда
			ТекстСообщения = НСтр("ru = 'Отправитель не может быть физическим лицом';
									|en = 'Individual cannot be a sender'");
			СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
		КонецЕсли;
		ОбработатьСписокОшибок(Форма.УникальныйИдентификатор, СписокОшибок, ЕстьОшибки, ВыводитьПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтрагентИНН) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			НСтр("ru = 'ИНН';
				|en = 'TIN'"),
			УчастникГрузоперевозки);
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
	Иначе
		
		ТекстСообщения = "";
		Если Не РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(КонтрагентИНН, КонтрагентЮрФизЛицо = 1,
			ТекстСообщения) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				НСтр("ru = 'ИНН';
					|en = 'TIN'"),
				УчастникГрузоперевозки)
				+ Символы.ПС + ТекстСообщения;
			СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверяем юридический адрес только на наличие
	КонтрагентЮридическийАдрес = Форма[Префикс + "ЮридическийАдресПредставление"];
	
	Если СтрДлина(СокрЛП(КонтрагентЮридическийАдрес)) < 5 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			НСтр("ru = 'Юридический адрес';
				|en = 'Legal address'"),
			УчастникГрузоперевозки);
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
	КонецЕсли;
	
	Если КонтрагентЮрФизЛицо = 3 Тогда
		ОбработатьСписокОшибок(Форма.УникальныйИдентификатор, СписокОшибок, ЕстьОшибки, ВыводитьПредупреждения);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтрагентКПП) Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			НСтр("ru = 'КПП';
				|en = 'KPP'"),
			УчастникГрузоперевозки);
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
	Иначе
		
		ТекстСообщения = "";
		Если Не РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КонтрагентКПП, ТекстСообщения) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				НСтр("ru = 'КПП';
					|en = 'KPP'"),
				УчастникГрузоперевозки)
				+ Символы.ПС + ТекстСообщения;
			СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
		КонецЕсли;
		
	КонецЕсли;
		
	ОбработатьСписокОшибок(Форма.УникальныйИдентификатор, СписокОшибок, ЕстьОшибки, ВыводитьПредупреждения);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьСписокОшибок(УникальныйИдентификатор, Знач СписокОшибок, ЕстьОшибки, Знач ВыводитьПредупреждения)
	
	Если СписокОшибок.Количество() > 0 Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ВыводитьПредупреждения Тогда
		Для Каждого Элемент Из СписокОшибок Цикл
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = Элемент.Значение;
			Сообщение.Поле = Элемент.Представление;
			Сообщение.ИдентификаторНазначения = УникальныйИдентификатор;
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДанныеКонтактныхЛиц(ЕстьОшибки, Знач ВыводитьПредупреждения)
	
	ПроверитьНомерТелефона(ЭтотОбъект, "ОтправительКонтактноеЛицоТелефон", ЕстьОшибки, ВыводитьПредупреждения);
	Если ОтправительКонтактноеЛицоТелефонДополнительныйДоступен Тогда
		ПроверитьНомерТелефона(ЭтотОбъект, "ОтправительКонтактноеЛицоТелефонДополнительный", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
	ПроверитьНомерТелефона(ЭтотОбъект, "ПолучательКонтактноеЛицоТелефон", ЕстьОшибки, ВыводитьПредупреждения);
	Если ПолучательКонтактноеЛицоТелефонДополнительныйДоступен Тогда
		ПроверитьНомерТелефона(ЭтотОбъект, "ПолучательКонтактноеЛицоТелефонДополнительный", ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
	Если ПлательщикРоль = 3 Тогда
		ПроверитьНомерТелефона(ЭтотОбъект, "ПлательщикКонтактноеЛицоТелефон", ЕстьОшибки, ВыводитьПредупреждения);
		Если ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен Тогда
			ПроверитьНомерТелефона(ЭтотОбъект, "ПлательщикКонтактноеЛицоТелефонДополнительный", ЕстьОшибки, ВыводитьПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
	ПроверитьEmail(ЭтотОбъект, "ПолучательКонтактноеЛицоEmail", ЕстьОшибки, ВыводитьПредупреждения);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьEmail(Знач Форма, Знач Префикс, ЕстьОшибки, Знач ВыводитьПредупреждения = Истина)
	
	ИмяРеквизита = Префикс;
	ЭтоEmailОтправителя = (СтрНайти(ИмяРеквизита, "Отправитель") > 0);
	
	Представление = Форма[ИмяРеквизита];
	
	Если Не ЗначениеЗаполнено(Представление) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Представление) Тогда 
		СписокОшибок = Новый СписокЗначений;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Адрес электронной почты группы ""%1"" содержит ошибки.';
				|en = 'The group ""%1"" email address contains errors.'"),
			?(ЭтоEmailОтправителя, "Отправитель", "Получатель"));
		СписокОшибок.Добавить(ТекстСообщения, ИмяРеквизита);
		
		ОбработатьСписокОшибок(Форма.УникальныйИдентификатор, СписокОшибок, ЕстьОшибки, ВыводитьПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПроверитьНомерТелефона(Знач Форма, Знач Префикс, ЕстьОшибки, Знач ВыводитьПредупреждения = Истина)
	
	ИмяРеквизита = Префикс + "Представление";
	Представление = Форма[ИмяРеквизита];
	Значение = Форма[Префикс + "Значение"];
	
	Если Не ЗначениеЗаполнено(Представление) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяГруппы = "";
	Если СтрНайти(ИмяРеквизита, "Отправитель") > 0 Тогда
		ИмяГруппы = "Отправитель";
	ИначеЕсли СтрНайти(ИмяРеквизита, "Получатель") > 0 Тогда
		ИмяГруппы = "Получатель";
	ИначеЕсли СтрНайти(ИмяРеквизита, "Плательщик") > 0 Тогда
		ИмяГруппы = "Плательщик";
	КонецЕсли;
	
	СписокОшибок = ОшибкиЗаполненияТелефона(Префикс, Значение);
	
	Если СписокОшибок.Количество() > 0 Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	ТекстСообщения = "";
	Для Каждого ТекущаяОшибка Из СписокОшибок Цикл
		ТекстСообщения = ТекстСообщения + Символы.ПС + ТекущаяОшибка.Представление;
	КонецЦикла;
	
	ТекстСообщения = СокрЛП(ТекстСообщения);
	
	Если ВыводитьПредупреждения И ЕстьОшибки Тогда
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Некорректный формат номера телефона в поле ""Телефон"" группы ""%1"".';
				|en = 'Incorrect format of phone number in the ""Phone"" field of the ""%1"" group.'"), ИмяГруппы);
		ТекстСообщения = ТекстОшибки + Символы.ПС + ТекстСообщения;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Поле = ИмяРеквизита;
		Сообщение.ИдентификаторНазначения = Форма.УникальныйИдентификатор;
		Сообщение.Сообщить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьДатуОтгрузки(ЕстьОшибки, Знач ВыводитьПредупреждения)
	
	Если ЗначениеЗаполнено(ДатаОтгрузки) Тогда
	
		ТекущаяДата = '00010101';
		УстановитьДатуОтгрузки(ТекущаяДата);
		
		Если ТекущаяДата > ДатаОтгрузки Тогда 
			СписокОшибок = Новый СписокЗначений;
			ТекстСообщения = НСтр("ru = 'Выбранная дата отгрузки недоступна. Укажите актуальную дату.';
									|en = 'Selected shipment date is not available. Specify actual date.'");
			СписокОшибок.Добавить(ТекстСообщения, "ДатаОтгрузки");
			
			ОбработатьСписокОшибок(УникальныйИдентификатор, СписокОшибок, ЕстьОшибки, ВыводитьПредупреждения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТоварныйСостав(ЕстьОшибки, ВыводитьПредупреждения)
	
	Если НаложенныйПлатежВидОплаты = 0 Тогда
		Возврат;
	КонецЕсли;
	
	КонтрольныеРеквизиты = Новый Структура("Артикул, ИННВладельцаГруза, СтавкаНДС");
	Для каждого ЭлемСтрока Из ТоварныйСостав Цикл
		Для каждого ЭлемРеквизит Из КонтрольныеРеквизиты Цикл
			
			Если НЕ ЗначениеЗаполнено(ЭлемСтрока[ЭлемРеквизит.Ключ]) Тогда
				ЕстьОшибки = Истина;
				Если ВыводитьПредупреждения Тогда
					ТекстОшибки = НСтр("ru = 'В строке %1 товарного состава для товара ""%2"" не заполнен реквизит ""%3""';
										|en = 'In the %1 item list line, the ""%3"" attribute is not filled in for the ""%2"" item'");
					НомерСтроки = ТоварныйСостав.Индекс(ЭлемСтрока) + 1;
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						ТекстОшибки,
						НомерСтроки,
						ЭлемСтрока["Наименование"], ЭлемРеквизит.Ключ);
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
				Иначе
					Возврат;
				КонецЕсли;
			КонецЕсли;

		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьВремяОтгрузки(ЕстьОшибки, ВыводитьПредупреждения)
	
	ШагОшибки = 0;
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	НачалоТекущегоДня = НачалоДня(ТекущаяДата);
	ТекущееВремя = Дата(1,1,1) + (ТекущаяДата - НачалоТекущегоДня);
	НачалоДняОтгрузки = НачалоДня(ДатаОтгрузки);
	
	Если Кэш.ЭтоЯндексДоставка Тогда
		Если ВариантВремениОтгрузки = Кэш.ВариантыВремениОтгрузки.ВОпределенноеВремя Тогда
			Если НачалоТекущегоДня = НачалоДняОтгрузки
				И ТекущееВремя > ВремяОтгрузкиК
				И ВремяОтгрузкиК <> Дата(1,1,1,0,0,0) Тогда
				ЕстьОшибки = Истина;
				ШагОшибки = 3;
				Если ВыводитьПредупреждения Тогда
					ТекстОшибки = НСтр("ru = 'Выбранное время отгрузки не может быть меньше текущего времени';
										|en = 'The selected shipment time cannot be less than the current time'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,,"ВремяОтгрузкиК");
				КонецЕсли;
			ИначеЕсли НачалоТекущегоДня < НачалоДняОтгрузки И ВремяОтгрузкиК = Дата(1,1,1,0,0,0) Тогда
				ЕстьОшибки = Истина;
				ШагОшибки = 3;
				Если ВыводитьПредупреждения Тогда
					ТекстОшибки = НСтр("ru = 'При отгрузке не на ближайшее время необходимо указать время';
										|en = 'Time is required when shipment is not in the near future'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки,,"ВремяОтгрузкиК");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если НачалоТекущегоДня = НачалоДняОтгрузки И ТекущееВремя > ВремяОтгрузкиПо Тогда
			ЕстьОшибки = Истина;
			ШагОшибки = 3;
			Если ВыводитьПредупреждения Тогда
				ТекстОшибки = НСтр(
					"ru = 'Невозможно оформить доставку на указанное время отгрузки. Время отгрузки меньше, чем текущее.';
					|en = 'Cannot register delivery for the specified shipment time. The shipment time is less than the current one.'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ОтправительВремяПлановойОтгрузки");
				Возврат ШагОшибки;
			КонецЕсли;
		КонецЕсли;
		Если СпособОтгрузки = 2 Тогда
			ПлановоеНачальноеВремяОтгрузки = ВремяОтгрузкиС;
			Если НачалоТекущегоДня = НачалоДняОтгрузки Тогда
				ПлановоеНачальноеВремяОтгрузки = Макс(
					ПолучитьВремяСОкруглением(ВремяОтгрузкиС),
					ПолучитьВремяСОкруглением(ТекущееВремя));
			КонецЕсли;
			Если Не ВремяОтгрузкиФиксированное И ВремяОтгрузкиПо - ПлановоеНачальноеВремяОтгрузки < 4 * 3600 Тогда
				ЕстьОшибки = Истина;
				ШагОшибки = 3;
				Если ВыводитьПредупреждения Тогда
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru = 'Невозможно оформить доставку на указанное время отгрузки. 
						|Разница между начальным %1 и конечным %2 временем отгрузки меньше 4-х часов.
						|Установите конечное время отгрузки не меньше, чем %3.';
						|en = 'Cannot register delivery for the specified shipment time. 
						|The difference between the initial (%1) and the final (%2) shipment time is less than 4 hours.
						|Set the final shipment time to at least %3.'"),
						Формат(ПлановоеНачальноеВремяОтгрузки, "ДФ=ЧЧ:мм"),
						Формат(ВремяОтгрузкиПо, "ДФ=ЧЧ:мм"),
						Формат(ПлановоеНачальноеВремяОтгрузки + 4 * 3600, "ДФ=ЧЧ:мм"));
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "ОтправительВремяПлановойОтгрузки");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ШагОшибки;
	
КонецФункции

&НаСервере
Процедура ПроверитьДозаполнитьАдрес(Префикс, ЕстьОшибки, ЕстьИзменения)
	
	ИмяРеквизитаАдресЗначение = Префикс + "Значение";
	
	СтруктураАдреса = СервисДоставки.ЗначениеИзСтрокиJSON(ЭтотОбъект[ИмяРеквизитаАдресЗначение]);
	
	Если Не ЗначениеЗаполнено(СтруктураАдреса) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизитаАдресЗначение]) Тогда
		
		Если УправлениеКонтактнойИнформацией.АдресВведенВСвободнойФорме(ЭтотОбъект[ИмяРеквизитаАдресЗначение]) Тогда
			ЭтотОбъект[ИмяРеквизитаАдресЗначение] = "";
		Иначе
			СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(ЭтотОбъект[ИмяРеквизитаАдресЗначение],
				Новый Структура("КодыАдреса", Истина));
			ЭтотОбъект[ИмяРеквизитаАдресЗначение] = РаботаСАдресами.ПоляАдресаВJSON(СведенияОбАдресе);
		КонецЕсли;
		
		ЕстьИзменения = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОбязательныеРеквизитыДляПереходаНаСтраницу(НомерШага)
	
	ОбязательныеРеквизиты = Новый СписокЗначений();
	
	Если РежимМастера = СервисДоставкиКлиентСервер.РежимМастераЗарегистрирован()
		ИЛИ НомерШага <= 1 Тогда
		Возврат ОбязательныеРеквизиты;
	КонецЕсли;
	
	ОбязательныеРеквизиты.Добавить("0.ОтправительКонтрагентНаименование", НСтр("ru = 'Отправитель';
																				|en = 'Sender'"));
	
	Если ЗначениеЗаполнено(ОтправительКонтрагентНаименование) Тогда
		Если НомерШага > 3  Тогда
			ОбязательныеРеквизиты.Добавить("0.ОтправительКонтактноеЛицоНаименование",
				НСтр("ru = 'Контактное лицо отправителя';
					|en = 'Sender contact person'"));
			ОбязательныеРеквизиты.Добавить("0.ОтправительКонтактноеЛицоТелефонПредставление",
				НСтр("ru = 'Телефон контактного лица отправителя';
					|en = 'Phone number of sender contact person'"));
				
			Если ОтправительКонтактноеЛицоТелефонДополнительныйДоступен Тогда
				ОбязательныеРеквизиты.Добавить("0.ОтправительКонтактноеЛицоТелефонДополнительныйПредставление",
					НСтр("ru = 'Дополнительный телефон контактного лица отправителя';
						|en = 'Additional phone number of the sender contact person'"));
			КонецЕсли;
		КонецЕсли;
		Если СпособОтгрузки = 2 Или Не Кэш.ЭтоДеловыеЛинии Тогда
			ОбязательныеРеквизиты.Добавить("0.ОтправительАдресПредставление", НСтр("ru = 'Откуда';
																					|en = 'From'"));
		Иначе
			ОбязательныеРеквизиты.Добавить("0.ПунктПриемаГрузаНаименование", НСтр("ru = 'От терминала';
																					|en = 'From terminal'"));
		КонецЕсли;
	КонецЕсли;
	
	ОбязательныеРеквизиты.Добавить("0.ПолучательКонтрагентНаименование", НСтр("ru = 'Получатель';
																				|en = 'Recipient'"));
	
	Если ЗначениеЗаполнено(ПолучательКонтрагентНаименование) Тогда
		Если НомерШага > 3 Тогда
			ОбязательныеРеквизиты.Добавить("0.ПолучательКонтактноеЛицоНаименование",
				НСтр("ru = 'Контактное лицо получателя';
					|en = 'Recipient contact person'"));
			ОбязательныеРеквизиты.Добавить("0.ПолучательКонтактноеЛицоТелефонПредставление",
				НСтр("ru = 'Телефон контактного лица получателя';
					|en = 'Phone number of recipient contact person'"));
			
			Если ПолучательКонтактноеЛицоТелефонДополнительныйДоступен Тогда
				ОбязательныеРеквизиты.Добавить("0.ПолучательКонтактноеЛицоТелефонДополнительныйПредставление",
					НСтр("ru = 'Дополнительный телефон контактного лица получателя';
						|en = 'Additional phone number of the recipient contact person'"));
			КонецЕсли;
		КонецЕсли;
		Если СпособДоставки = 2 Или Не Кэш.ЭтоДеловыеЛинии Тогда
			ОбязательныеРеквизиты.Добавить("0.ПолучательАдресПредставление", НСтр("ru = 'Куда';
																					|en = 'To'"));
		Иначе
			ОбязательныеРеквизиты.Добавить("0.ПунктВыдачиГрузаНаименование", НСтр("ru = 'До терминала';
																					|en = 'To terminal'"));
		КонецЕсли;
	КонецЕсли;
	
	ОбязательныеРеквизиты.Добавить("0.ПлательщикРоль", НСтр("ru = 'Плательщик';
															|en = 'Payer'"));

	Если ПлательщикРоль = 3 Тогда
		ОбязательныеРеквизиты.Добавить("0.ПлательщикКонтрагентНаименование", НСтр("ru = 'Плательщик';
																					|en = 'Payer'"));
		Если ЗначениеЗаполнено(ПлательщикКонтрагентНаименование) Тогда
			Если НомерШага > 3 Тогда
				ОбязательныеРеквизиты.Добавить("0.ПлательщикКонтактноеЛицоНаименование",
					НСтр("ru = 'Контактное лицо плательщика';
						|en = 'Payer contact person'"));
				ОбязательныеРеквизиты.Добавить("0.ПлательщикКонтактноеЛицоТелефонПредставление",
					НСтр("ru = 'Телефон контактного лица плательщика';
						|en = 'Phone number of payer contact person'"));
				Если ПлательщикКонтактноеЛицоТелефонДополнительныйДоступен Тогда
					ОбязательныеРеквизиты.Добавить("0.ПлательщикКонтактноеЛицоТелефонДополнительныйПредставление",
						НСтр("ru = 'Дополнительный телефон контактного лица плательщика';
							|en = 'Additional phone number of payer contact person'"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ОбязательныеРеквизиты.Добавить("0.ПлательщикАдресПредставление", НСтр("ru = 'Где';
																				|en = 'Where'"));
	КонецЕсли;	
	
	ОбязательныеРеквизиты.Добавить("0.ДатаОтгрузки", НСтр("ru = 'Дата отгрузки';
															|en = 'Shipment date'"));
	
	ОбязательныеРеквизиты.Добавить("0.ЗаказчикРоль", НСтр("ru = 'Роль заказчика';
															|en = 'Customer role'"));

	ОбязательныеРеквизиты.Добавить("0.ОрганизацияБизнесСетиСсылка", НСтр("ru = 'Организация БС';
																		|en = 'Company BS'"));
	
	ОбязательныеРеквизиты.Добавить("1.ГрузКоличествоГрузовыхМест", НСтр("ru = 'Количество мест';
																		|en = 'Unit load quantity'"));
	Если ГрузКоличествоГрузовыхМест > 1 Тогда
		ОбязательныеРеквизиты.Добавить("1.ГрузВес", НСтр("ru = 'Общий вес';
														|en = 'Total weight'"));
	КонецЕсли;
	
	ОбязательныеРеквизиты.Добавить("1.ГрузСодержимое", НСтр("ru = 'Описание содержимого';
															|en = 'Content details'"));
	Если Кэш.ЭтоДеловыеЛинии Тогда
		ОбязательныеРеквизиты.Добавить("1.ГрузСодержимоеИдентификатор.ГрузСодержимое", НСтр("ru = 'Описание содержимого';
																							|en = 'Content details'"));
	КонецЕсли;
	ОбязательныеРеквизиты.Добавить("1.ГрузМаксимальныйВес", НСтр("ru = 'Вес';
																|en = 'Weight'"));
	ОбязательныеРеквизиты.Добавить("1.ГрузОбъем", НСтр("ru = 'Объем';
														|en = 'Volume'"));
	ОбязательныеРеквизиты.Добавить("1.ГрузМаксимальнаяВысота", НСтр("ru = 'Высота';
																	|en = 'Height'"));
	ОбязательныеРеквизиты.Добавить("1.ГрузМаксимальнаяШирина", НСтр("ru = 'Ширина';
																	|en = 'Width'"));
	ОбязательныеРеквизиты.Добавить("1.ГрузМаксимальнаяДлина", НСтр("ru = 'Длина';
																	|en = 'Length'"));;
	
	Если НомерШага > 2 Тогда
	
		Если Не ЗначениеЗаполнено(ТарифИдентификатор) Тогда
			ОбязательныеРеквизиты.Добавить("2.ТарифИдентификатор", НСтр("ru = 'Тариф грузоперевозки';
																		|en = 'Cargo transportation service plan'"));
		КонецЕсли;
		
	КонецЕсли;
	
	Если НомерШага > 3 Тогда
		
		Если ЗначениеЗаполнено(ТарифИдентификатор) Тогда
			ОбязательныеРеквизиты.Добавить("3.ФормаОплатыПредставление", НСтр("ru = 'Форма оплаты';
																				|en = 'Payment method'"));
			
			Если Кэш.ЭтоДеловыеЛинии Тогда
				Если СпособОтгрузки = 1 Тогда
					ОбязательныеРеквизиты.Добавить("3.ПунктПриемаГрузаНаименование", НСтр("ru = 'Пункт приема груза';
																							|en = 'Cargo pick up point'"));
				ИначеЕсли СпособОтгрузки = 2 Тогда
					ОбязательныеРеквизиты.Добавить("3.ВремяОтгрузкиПредставление", НСтр("ru = 'Время отгрузки';
																						|en = 'Shipment time'"));
				КонецЕсли;
				
				Если СпособДоставки = 1 Тогда
					ОбязательныеРеквизиты.Добавить("3.ПунктВыдачиГрузаНаименование", НСтр("ru = 'Пункт выдачи груза';
																							|en = 'Cargo issuing point'"));
				ИначеЕсли СпособДоставки = 2 Тогда
					ОбязательныеРеквизиты.Добавить("3.ВремяДоставкиПредставление", НСтр("ru = 'Время доставки';
																						|en = 'Delivery time'"));
				КонецЕсли;
				
				Если ТарифНеГабарит И ГрузКоличествоГрузовыхМест <> 1 Тогда
					ОбязательныеРеквизиты.Добавить("3.ГрузНегабаритныйВес", НСтр("ru = 'Вес негабаритных мест';
																				|en = 'Weight of oversized places'"));
					ОбязательныеРеквизиты.Добавить("3.ГрузНегабаритныйОбъем", НСтр("ru = 'Объем негабаритных мест';
																					|en = 'Volume of oversized places'"));
					ОбязательныеРеквизиты.Добавить("3.ГрузКоличествоНегабаритныхГрузовыхМест",
						НСтр("ru = 'Количество негабаритных грузовых мест';
							|en = 'Number of oversized cargo places'"));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОбязательныеРеквизиты;
	
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросДобавлениеКомментария(ТекстКомментария)
	
	ОбработчикОповещения = Новый ОписаниеОповещения("УстановитьКомментарийИзКонтактнойИнформации", ЭтотОбъект,
		Новый Структура("КомментарийКАдресу", ТекстКомментария));
	
	ПоказатьВопрос(ОбработчикОповещения,
		СтрШаблон(НСтр("ru = 'В выбранном адресе содержится комментарий. Добавить его в заказ?
			|
			|Комментарий: ""%1""';
			|en = 'The selected address contains a comment. Do you want to add it to the order?
			|
			|Comment: ""%1""'"),
			СокрЛП(ТекстКомментария)),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет, НСтр("ru = 'Добавление комментария';
																|en = 'Add comment'"));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомментарийИзКонтактнойИнформации(Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение = КодВозвратаДиалога.Да Тогда
		ДополнительнаяИнформация = СокрЛП(ДополнительнаяИнформация + Символы.ПС + ДополнительныеПараметры.КомментарийКАдресу);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КомментарийИзКонтактнойИнформации(ЗначениеКонтактнойИнформации)
	
	Результат = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ЗначениеКонтактнойИнформации);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьПараметрГруза(ИмяРеквизита)
	
	ЗначениеРеквизитаДляПересчета = ЭтотОбъект[ИмяРеквизита + "ДляПересчета"];
	
	Если ТипЗнч(ЗначениеРеквизитаДляПересчета) = Тип("Число")
		И ЭтотОбъект[ИмяРеквизита] <> ЗначениеРеквизитаДляПересчета 
		И ЗначениеРеквизитаДляПересчета <> 0 Тогда
		ЭтотОбъект[ИмяРеквизита] = ЗначениеРеквизитаДляПересчета;
		Если ГрузКоличествоГрузовыхМест = 1 Тогда
			Если СтрНайти(ИмяРеквизита, "Максимальная") Тогда
				РассчитатьОбъем();
				ЭтотОбъект["ГрузОбъемДляПересчета"] = Неопределено;
			ИначеЕсли ИмяРеквизита = "ГрузВес" Тогда
				ЭтотОбъект["ГрузМаксимальныйВес"] = ЭтотОбъект[ИмяРеквизита];
				ЭтотОбъект["ГрузМаксимальныйВесДляПересчета"] = Неопределено;
			КонецЕсли;
		КонецЕсли;

		ПроверитьВГХ(ИмяРеквизита);
		ЗарегистрироватьИзменениеОтборов();
		
	ИначеЕсли ТипЗнч(ЗначениеРеквизитаДляПересчета) = Тип("Строка")
		И ЗначениеРеквизитаДляПересчета <> "" Тогда
		ЭтотОбъект[ИмяРеквизита] = ЗначениеРеквизитаДляПересчета;
	КонецЕсли;
	
	ЭтотОбъект[ИмяРеквизита + "ДляПересчета"] = Неопределено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеКонтактнойИнформации(ЗначениеКонтактнойИнформации)
	
	Результат = УправлениеКонтактнойИнформацией.ПредставлениеКонтактнойИнформации(ЗначениеКонтактнойИнформации);

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора, КонтрагентСсылка)
	СервисДоставкиПереопределяемый.ЗаполнитьПараметрыОтбораКонтактногоЛицаКонтрагента(ПараметрыОтбора,
		КонтрагентСсылка);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора, КонтрагентСсылка)
	СервисДоставкиПереопределяемый.ЗаполнитьПараметрыОтбораКонтактногоЛицаОрганизации(ПараметрыОтбора,
		КонтрагентСсылка);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСписковВыбораКонтактнойИнформации()
	
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(
		ОтправительКонтрагентСсылка,
		ОтправительКонтрагентЭтоОрганизация,
		ОтправительКонтактноеЛицоСсылка,
		Элементы.ОтправительАдрес,
		Элементы.ОтправительКонтактноеЛицоТелефон,
		Элементы.ОтправительКонтактноеЛицоТелефонДополнительный);
	
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(
		ПолучательКонтрагентСсылка,
		ПолучательКонтрагентЭтоОрганизация,
		ПолучательКонтактноеЛицоСсылка,
		Элементы.ПолучательАдрес,
		Элементы.ПолучательКонтактноеЛицоТелефон,
		Элементы.ПолучательКонтактноеЛицоТелефонДополнительный);
	
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(
		ПлательщикКонтрагентСсылка,
		ПлательщикКонтрагентЭтоОрганизация,
		ПлательщикКонтактноеЛицоСсылка,
		Элементы.ПлательщикАдрес,
		Элементы.ПлательщикКонтактноеЛицоТелефон,
		Элементы.ПлательщикКонтактноеЛицоТелефонДополнительный);
	
	Если Не Кэш.ЭтоДеловыеЛинии Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(
		ОтправительКонтрагентСсылка,
		ОтправительКонтрагентЭтоОрганизация,
		ОтправительКонтактноеЛицоСсылка,
		Элементы.ОтправительАдрес1,
		Элементы.ОтправительКонтактноеЛицоТелефон,
		Элементы.ОтправительКонтактноеЛицоТелефонДополнительный);

	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(
		ПолучательКонтрагентСсылка,
		ПолучательКонтрагентЭтоОрганизация,
		ПолучательКонтактноеЛицоСсылка,
		Элементы.ПолучательАдрес1,
		Элементы.ПолучательКонтактноеЛицоТелефон,
		Элементы.ПолучательКонтактноеЛицоТелефонДополнительный);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСписковВыбораКонтактнойИнформацииУчастника(Контрагент, ЭтоОрганизация, КонтактноеЛицо,
	ЭлементАдрес, ЭлементТелефон, ЭлементТелефонДоп)
	
	ЭлементАдрес.СписокВыбора.Очистить();
	ЭлементТелефон.СписокВыбора.Очистить();
	ЭлементТелефонДоп.СписокВыбора.Очистить();
	
	МассивВладельцевКонтактнойИнформации = Новый Массив;
	МассивВладельцевКонтактнойИнформации.Добавить(Контрагент);
	МассивВладельцевКонтактнойИнформации.Добавить(КонтактноеЛицо);
	
	Если ЗначениеЗаполнено(Контрагент) И Контрагент.Метаданные().Реквизиты.Найти("Партнер") <> Неопределено Тогда
		МассивВладельцевКонтактнойИнформации.Добавить(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "Партнер"));
	КонецЕсли;
	
	СтруктураКонтактнаяИнформация = СервисДоставкиСлужебный.КонтактнаяИнформацияОбъектов(МассивВладельцевКонтактнойИнформации);
	
	Для Каждого ЭлементСоответствия Из СтруктураКонтактнаяИнформация Цикл
		
		Для Каждого СтрокаКонтактнаяИнформация Из ЭлементСоответствия.Значение Цикл
			
			Если СтрокаКонтактнаяИнформация.Значение.Тип = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
				
				ЭлементАдрес.СписокВыбора.Добавить(СтрокаКонтактнаяИнформация.Значение.Значение, СтрШаблон("%1, %2",
					СтрокаКонтактнаяИнформация.Значение.Представление, ЭлементСоответствия.Ключ));
				
			ИначеЕсли СтрокаКонтактнаяИнформация.Значение.Тип = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
				
				ЭлементТелефон.СписокВыбора.Добавить(СтрокаКонтактнаяИнформация.Значение.Значение, СтрШаблон("%1, %2",
					СтрокаКонтактнаяИнформация.Значение.Представление, ЭлементСоответствия.Ключ));
				
				ЭлементТелефонДоп.СписокВыбора.Добавить(СтрокаКонтактнаяИнформация.Значение.Значение, СтрШаблон("%1, %2",
					СтрокаКонтактнаяИнформация.Значение.Представление, ЭлементСоответствия.Ключ));
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЭтоОрганизация Тогда
		ЭлементАдрес.СписокВыбора.Добавить(1, НСтр("ru = 'Наш склад...';
													|en = 'Our warehouse...'"));
	Иначе
		ЭлементАдрес.СписокВыбора.Добавить(2, НСтр("ru = 'Адрес контрагента...';
													|en = 'Address of counterparty...'"));
	КонецЕсли;
	
	Если ЭлементТелефон.СписокВыбора.Количество() > 0 Тогда
		ЭлементТелефон.СписокВыбора.Добавить("ПроизвольныйТелефон", НСтр("ru = 'Произвольный телефон...';
																		|en = 'Custom phone number...'"));
		ЭлементТелефонДоп.СписокВыбора.Добавить("ПроизвольныйТелефон", НСтр("ru = 'Произвольный телефон...';
																			|en = 'Custom phone number...'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеАдресаБезМуниципальнойЧасти(Знач АдресЗначение)

	Представление = "";

	Если УправлениеКонтактнойИнформациейКлиентСервер.ЭтоКонтактнаяИнформацияВJSON(АдресЗначение) Тогда
		СведенияОбАдресе = РаботаСАдресами.СведенияОбАдресе(АдресЗначение);
		Представление = СведенияОбАдресе.Представление;
	КонецЕсли;

	Возврат Представление;

КонецФункции

&НаСервереБезКонтекста
Функция ТаблицаРолейПлательщиков()

	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("Роль", ОбщегоНазначения.ОписаниеТипаЧисло(1));
	Результат.Колонки.Добавить("Представление", ОбщегоНазначения.ОписаниеТипаСтрока(15));
	
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.Роль = 1;
	НоваяСтрока.Представление = "Отправитель";
	
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.Роль = 2;
	НоваяСтрока.Представление = "Получатель";
	
	НоваяСтрока = Результат.Добавить();
	НоваяСтрока.Роль = 3;
	НоваяСтрока.Представление = "Третье лицо";

	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция РольПоПредставлению(Представление)

	Результат = 0;
	
	ТаблицаРолей = ТаблицаРолейПлательщиков();
	НайденнаяСтрока = ТаблицаРолей.Найти(Представление, "Представление");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	Результат = НайденнаяСтрока.Роль;

	Возврат Результат;

КонецФункции

&НаСервереБезКонтекста
Функция ПредставлениеПоРоли(Роль)

	Результат = "";
	
	ТаблицаРолей = ТаблицаРолейПлательщиков();
	НайденнаяСтрока = ТаблицаРолей.Найти(Роль, "Роль");
	Если НайденнаяСтрока = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	Результат = НайденнаяСтрока.Представление;

	Возврат Результат;

КонецФункции

&НаСервере
Функция ПлательщикУслуги(Тариф, Услуга)

	Результат = 0;

	Если Не (ПустаяСтрока(Услуга.Идентификатор) Или Услуга.Стоимость = 0) Тогда
		Результат = ПлательщикРоль; 
		Если Тариф.ТарифИдентификатор = ТарифИдентификатор И Не ЭтоМежтермитальнаяПеревозка(Услуга) Тогда
			Отбор = Новый Структура("Идентификатор", Услуга.Идентификатор);
			НайденныеСтрочки = ОплатаСтоимостьУслуг.НайтиСтроки(Отбор);
			Если НайденныеСтрочки.Количество() = 1 Тогда
				Результат = НайденныеСтрочки[0].ПлательщикРоль;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоМежтермитальнаяПеревозка(ТекущиеДанные)

	Результат = Ложь;
	
	Если ТекущиеДанные.Идентификатор = "000000050" Или СтрНайти(НРег(ТекущиеДанные.Наименование), "межтермитальная перевозка") > 0 Тогда
		Результат = Истина;
    КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ТарифыДетализацияСтоимостиПоУслугамПлательщикРольПредставлениеПриИзмененииЗавершение(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Плательщики = Новый Массив;
	ТекущаяСтрока = Элементы.Тарифы.ТекущиеДанные;
	ТекущиеДанные = Элементы.ТарифыДетализацияСтоимостиПоУслугам.ТекущиеДанные;

	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ЭтоМежтермитальнаяПеревозка = Ложь;
		ПредыдущаяПлательщикРоль = ПлательщикРоль;
		Если ДополнительныеПараметры <> Неопределено И ТипЗнч(ДополнительныеПараметры) = Тип("Структура") Тогда
			ЭтоМежтермитальнаяПеревозка = ДополнительныеПараметры.ЭтоМежтермитальнаяПеревозка;
		КонецЕсли;
		
		ТекущиеДанные.ПлательщикРоль = РольПоПредставлению(ТекущиеДанные.ПлательщикРольПредставление);
		
		Если ЭтоМежтермитальнаяПеревозка Тогда
			ПлательщикРоль = ТекущиеДанные.ПлательщикРоль;
		КонецЕсли;
		
		Плательщики.Добавить(ПлательщикРоль);
			
		Для Каждого Строка Из ТекущаяСтрока.ДетализацияСтоимостиПоУслугам Цикл
			
			Если Строка.ПлательщикРоль > 0 И Плательщики.Найти(Строка.ПлательщикРоль) = Неопределено Тогда
				Плательщики.Добавить(Строка.ПлательщикРоль);
			КонецЕсли;
			
			Если Плательщики.Количество() > 2 Тогда
				
				ТекстПояснения = НСтр("ru = 'Возможно указывать не более двух плательщиков.';
										|en = 'You can specify no more than two payers.'");
				ТипОперации = НСтр("ru = 'Выбран третий плательщик:';
									|en = 'The third payer is selected:'");
				ПоказатьОповещениеПользователя(ТипОперации,, ТекстПояснения, БиблиотекаКартинок.Информация32);
				
				Если ЭтоМежтермитальнаяПеревозка Тогда
					ПлательщикРоль = ПредыдущаяПлательщикРоль;
				КонецЕсли;
				
				ТекущиеДанные.ПлательщикРоль = ПлательщикРоль;
				ТекущиеДанные.ПлательщикРольПредставление = ПредставлениеПоРоли(ТекущиеДанные.ПлательщикРоль);
				
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоМежтермитальнаяПеревозка И ПредыдущаяПлательщикРоль <> ПлательщикРоль Тогда
			ЗначениеСписка = Элементы.ПлательщикРоль.СписокВыбора.НайтиПоЗначению(ПлательщикРоль);
			Если ЗначениеСписка <> Неопределено Тогда
				ПлательщикРольПредставление = ЗначениеСписка.Представление;
				ПлательщикРольОбработкаВыбораНаКлиенте(ПлательщикРоль);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ТекущиеДанные.ПлательщикРольПредставление = ПредставлениеПоРоли(ТекущиеДанные.ПлательщикРоль);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПлательщикРольОбработкаВыбораНаКлиенте(ВыбранноеЗначение)
	
	ЗначениеСписка = Элементы.ПлательщикРоль.СписокВыбора.НайтиПоЗначению(ВыбранноеЗначение);
	
	ПлательщикРольСтарое = ПлательщикРоль;
	Если ЗначениеСписка <> Неопределено Тогда
		ПлательщикРоль = ВыбранноеЗначение;
		Если ПлательщикРольСтарое <> ВыбранноеЗначение Тогда
			ПлательщикКонтрагентСсылка = Неопределено;
			ОчиститьРеквизит(Элементы.ПлательщикКонтрагент, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если ПлательщикРольСтарое <> ПлательщикРоль Тогда
		ПлательщикиУслугИзменены = Истина;
		Модифицированность = Истина;
	КонецЕсли;
	
	ЗаполнитьЗначенияСписковВыбораКонтактнойИнформации();
	УстановитьВидимостьДоступностьНаСтраницеОсновная();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьОбновитьДанныеЗаказаНаДоставку()
	
	ПараметрыСозданияДанныхЗаказа = СервисДоставкиКлиентСервер.НовыйПараметрыЗаписиДанныхЗаказаСервисаДоставки();
	ПараметрыСозданияДанныхЗаказа.ТипГрузоперевозки = Перечисления.ТипыГрузоперевозки.Получить(ТипГрузоперевозки - 1);
	ПараметрыСозданияДанныхЗаказа.Организация = ОрганизацияБизнесСетиСсылка;
	ПараметрыСозданияДанныхЗаказа.ИдентификаторДокумента = Новый УникальныйИдентификатор(ИдентификаторЗаказа);
	ПараметрыСозданияДанныхЗаказа.ДокументыОснования = ДокументыОснования.ВыгрузитьЗначения();
	ПараметрыСозданияДанныхЗаказа.Представление = СервисДоставкиКлиентСервер.ПредставлениеЗаказаНаДоставку(ЭтотОбъект, Истина);
	ПараметрыСозданияДанныхЗаказа.СостояниеИдентификатор = СостояниеИдентификатор;
	ПараметрыСозданияДанныхЗаказа.СостояниеПредставление = СостояниеПредставление;
	ПараметрыСозданияДанныхЗаказа.Номер = НомерЗаказа;
	ПараметрыСозданияДанныхЗаказа.Дата = ДатаЗаказа;
	
	СервисДоставки.СоздатьОбновитьДанныеЗаказа(ПараметрыСозданияДанныхЗаказа);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДанныеТаблицы(Результат, ИмяТаблицыИсточника, ИмяТаблицыПриемника)
	Для Каждого Строка Из ЭтотОбъект[ИмяТаблицыИсточника] Цикл
		ЗаполнитьЗначенияСвойств(Результат[ИмяТаблицыПриемника].Добавить(), Строка);
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ВыбратьТарифНаСервере(ДанныеПоТарифу)
	
	ОбработатьВыборТарифа(ДанныеПоТарифу);
	ЗаполнитьЗначенияСписковВыбора();
	СформироватьДопУслугиПоТарифу(ДанныеПоТарифу.ДополнительныеУслуги);
	ОбновитьИнформациюПоВыбраннымДопУслугам();
	ЗаполнитьОплатуСтоимостьУслуг(ДанныеПоТарифу.ДетализацияСтоимостиПоУслугам);
	
	ДанныеПоТарифу.Вставить("ВидимостьПредупрежденияПоДопУслугам", ПроверитьЗаполнениеЗначенийДополнительныхУслуг(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтгрузкиДоставкиПриИзмененииНаКлиенте()
	
	УстановитьВидимостьСпособовОтгрузкиДоставки();
	СпособОтгрузкиДоставкиПриИзмененииНаСервере();
	РазвернутьРодителейСпискаСВыбраннымиСтроками();
	ЗарегистрироватьИзменениеОтборов();
	
КонецПроцедуры

&НаСервере
Процедура СпособОтгрузкиДоставкиПриИзмененииНаСервере()
	
	СформироватьОтборыТарифов();
	УбратьФиксированноеВремяПриИзмененииСпособаОтгрузкиДоставки();
	ИзменитьПараметрыПропускаПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура УбратьФиксированноеВремяПриИзмененииСпособаОтгрузкиДоставки()
	
	Если СпособОтгрузки = 1 И ВремяОтгрузкиФиксированное Тогда
		ВремяОтгрузкиФиксированное = Ложь;
		ВремяОтгрузкиС = РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяОтгрузкиС", ТипГрузоперевозки);
		ВремяОтгрузкиПо =  РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяОтгрузкиПо", ТипГрузоперевозки);
		ВремяОтгрузкиПредставление = ПредставлениеДатыВремени(ДатаОтгрузки,
			ВремяОтгрузкиС,
			ВремяОтгрузкиПо,
			ВремяОтгрузкиОбедС,
			ВремяОтгрузкиОбедПо,
			ВремяОтгрузкиФиксированное,
			1);
	КонецЕсли;
	
	Если СпособДоставки = 1 И ВремяДоставкиФиксированное Тогда
		ВремяДоставкиФиксированное = Ложь;
		ВремяДоставкиС = РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяДоставкиС", ТипГрузоперевозки);
		ВремяДоставкиПо =  РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяДоставкиПо", ТипГрузоперевозки);
		ВремяДоставкиПредставление = ПредставлениеДатыВремени(ДатаДоставки,
			ВремяДоставкиС,
			ВремяДоставкиПо,
			ВремяДоставкиОбедС,
			ВремяДоставкиОбедПо,
			ВремяДоставкиФиксированное,
			2);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПараметрыПропускаПриНеобходимости()
	Если СпособОтгрузки = 1 И ПропускЗаказывать Тогда
		ПропускЗаказывать = Ложь;
		ДанныеПропуска = Новый Структура;
		СервисДоставкиКлиентСервер.ЗаполнитьЛинейныеДанныеПоСтруктуре(СервисДоставкиКлиентСервер.НовыйПараметрыПропуска(), ДанныеПропуска, "Пропуск");
		ЗаполнитьЗначенияСвойств(ДанныеПропуска, ЭтотОбъект);
		ЗаполнитьПредставлениеПропуска(ДанныеПропуска);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеОтменитьЗаказ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда // Отменить
		ОтменитьЗаказНаДоставку();
	ИначеЕсли Результат = КодВозвратаДиалога.Повторить Тогда // Отменить платно
		ОтменитьЗаказНаДоставку(Истина);
	КонецЕсли;
	
КонецПроцедуры

#Область ОбработкаСобытийЭлементаГрузСодержимое

&НаКлиенте
Процедура ОбработатьХарактерыГрузаАвтоподбор(Элемент, Источник)
	// Восстановление текущего значения в случае отмены ввода
	ТекущийГрузПредставление = Кэш.ОписаниеХарактеровГруза.Получить(ГрузСодержимоеИдентификатор);
	Если ТекущийГрузПредставление <> Неопределено Тогда
		ЗначениеСписка = 
			Новый Структура("Идентификатор, Наименование", ГрузСодержимоеИдентификатор, ТекущийГрузПредставление);
		Источник.Добавить(ЗначениеСписка, ТекущийГрузПредставление);
	КонецЕсли;
	
	Элемент.СписокВыбора.Очистить();
	
	Для Каждого ЗначениеСписка Из Источник Цикл 
		Элемент.СписокВыбора.Добавить(ЗначениеСписка.Значение, Строка(ЗначениеСписка.Представление));
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьХарактерыГрузаАвтоподбор(СтрокаПоиска)
	ПараметрыКэш = Новый Структура("ОрганизацияБизнесСетиСсылка, ТипГрузоперевозки", 
		ОрганизацияБизнесСетиСсылка, ТипГрузоперевозки);
	Возврат ХарактерыГрузаПоСтрокеПоискаНаКлиенте(Кэш, СтрокаПоиска, ПараметрыКэш);
КонецФункции

&НаКлиенте
Функция ХарактерыГрузаПоСтрокеПоискаНаКлиенте(Кэш, СтрокаПоиска, ПараметрыКэш)
	Если ЗначениеЗаполнено(Кэш.СписокХарактеровГруза) Тогда
		СписокХарактеровГруза = Кэш.СписокХарактеровГруза;
	Иначе
		СписокХарактеровГруза = ПолучитьИСохранитьВКэшСписокХарактеровГрузаНаСервере(Кэш, ПараметрыКэш);
	КонецЕсли;
	Возврат СервисДоставкиКлиент.ФорматированныйСписокПоСтрокеПоиска(СписокХарактеровГруза, СтрокаПоиска);
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьИСохранитьВКэшСписокХарактеровГрузаНаСервере(Кэш, Знач ПараметрыКэш)
	Возврат СервисДоставкиСлужебный.СписокКлассификатораХарактерыГруза(Кэш, ПараметрыКэш);
КонецФункции

&НаКлиенте
Процедура ПропускНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Результат);
		ЗаполнитьПредставлениеПропуска(Результат);
		Модифицированность = Истина;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеПропуска(ДанныеПропуска)
	ПропускПредставление = СервисДоставки.ПредставлениеПропуска(ДанныеПропуска);
КонецПроцедуры

#КонецОбласти

#КонецОбласти