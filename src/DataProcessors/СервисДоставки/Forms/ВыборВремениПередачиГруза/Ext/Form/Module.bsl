#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТипГрузоперевозки", ТипГрузоперевозки);
	
	Если Не ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран тип грузоперевозки';
													|en = 'Cargo transportation type is not selected'"));
		Отказ = Истина;
		Возврат;
	ИначеЕсли Не СервисДоставки.ТипГрузоперевозкиДоступен(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Выбранный тип грузоперевозки не доступен';
													|en = 'The selected cargo transportation type is not available'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Дата", Дата);
	Параметры.Свойство("ВремяРаботыС", ВремяРаботыС);
	Параметры.Свойство("ВремяРаботыПо", ВремяРаботыПо);
	Параметры.Свойство("ВремяОбедС", ВремяОбедаС);
	Параметры.Свойство("ВремяОбедПо", ВремяОбедаПо);
	Параметры.Свойство("ВариантВыбораВремени", ВариантВыбораВремени);
	Параметры.Свойство("ФиксированноеВремя", ФиксированноеВремя);
	Параметры.Свойство("ВариантОтображения", ВариантОтображения);
	
	НастроитьФормуПоТипуГрузоперевозки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступностьЭлементов();
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ФиксированноеВремяПриИзменении(Элемент)
	
	Если ФиксированноеВремя Тогда
		ВычислитьВремяРаботыПо();
	Иначе
		ВремяРаботыПо = ПолучитьВремя();
	КонецЕсли;
	
	УстановитьДоступностьЭлементов();

КонецПроцедуры

&НаКлиенте
Процедура ВремяРаботыСРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	Если ФиксированноеВремя Тогда
		СтандартнаяОбработка = Ложь;
		ВремяРаботыС = ВремяРаботыС + Направление * 60 * 30;
		ВычислитьВремяРаботыПо();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВремяРаботыСПриИзменении(Элемент)
	Если ФиксированноеВремя Тогда
		ВычислитьВремяРаботыПо();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если ВремяЗаполнено() Тогда
		
		Если ВремяЗаполненоКорректно() Тогда
			
			ПараметрыЗакрытияФормы = СервисДоставкиКлиент.НовыйПараметрыОткрытьФормуВыборВремениПередачиГруза();
			
			Если СервисДоставкиКлиентСервер.ЭтоСДЭК(ТипГрузоперевозки) Или СервисДоставкиКлиентСервер.ЭтоДеловыеЛинии(ТипГрузоперевозки) Тогда
				ПараметрыЗакрытияФормы.Дата = Дата;
			КонецЕсли;
			ПараметрыЗакрытияФормы.ВремяРаботыС = ВремяРаботыС;
			ПараметрыЗакрытияФормы.ВремяРаботыПо = ВремяРаботыПо;
			ПараметрыЗакрытияФормы.ВремяОбедС = ВремяОбедаС;
			ПараметрыЗакрытияФормы.ВремяОбедПо = ВремяОбедаПо;
			ПараметрыЗакрытияФормы.ВариантВыбораВремени = ВариантВыбораВремени;
			ПараметрыЗакрытияФормы.ФиксированноеВремя = ФиксированноеВремя;
			
			Закрыть(ПараметрыЗакрытияФормы);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
 
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементов()
	
	ОтображениеТолькоДаты = ВариантОтображения = ЧастиДаты.Дата;
	Элементы.ФиксированноеВремя.Доступность = Не (ТолькоПросмотр Или ОтображениеТолькоДаты);
	Элементы.ГруппаВремяСПо.Доступность = Не (ТолькоПросмотр Или ОтображениеТолькоДаты);
	Элементы.ВремяРаботыПо.ТолькоПросмотр = ФиксированноеВремя;
	Элементы.ГруппаОбед.Доступность = Не (ТолькоПросмотр Или ФиксированноеВремя Или ОтображениеТолькоДаты);

	Если ФиксированноеВремя Тогда
		Элементы.ВремяРаботыС.Заголовок = СтрШаблон(НСтр("ru = 'Время %1 с';
														|en = '%1 time from'"), ?(ВариантВыбораВремени = 1, "отгрузки", "доставки"));
	Иначе
		Элементы.ВремяРаботыС.Заголовок = НСтр("ru = 'Время работы с';
												|en = 'Work time from'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоТипуГрузоперевозки()
	
	Заголовок = СтрШаблон(НСтр("ru = '%1: Период времени';
								|en = '%1: Time period'"),
		СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки));
	
	Элементы.Дата.Видимость = ВариантОтображения <> ЧастиДаты.Время;
	Элементы.Дата.Доступность = Не (ТолькоПросмотр Или ВариантВыбораВремени = 2);
	Элементы.ФиксированноеВремя.Видимость = СервисДоставкиКлиентСервер.ЭтоДеловыеЛинии(ТипГрузоперевозки);
	
КонецПроцедуры

&НаКлиенте
Функция ВремяЗаполненоКорректно()
	
	Отказ = Ложь;
	ОписаниеОшибки = "";
	
	Если ВремяРаботыС >= ВремяРаботыПо И (ЗначениеЗаполнено(ВремяРаботыПо))Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru = 'Время окончания работы должно быть больше времени начала работы';
								|en = 'End of working time should be greater than the start of working time'");
	КонецЕсли;
	
	Если Не ФиксированноеВремя И ЗначениеЗаполнено(ВремяОбедаПо)
		И ВремяОбедаС >= ВремяОбедаПо Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru = 'Время окончания обеда должно быть больше времени начала обеда';
								|en = 'End of dinner time should be greater than the start of dinner time'");
	КонецЕсли;
	
	Если Не ФиксированноеВремя И (ЗначениеЗаполнено(ВремяОбедаС) Или ЗначениеЗаполнено(ВремяОбедаПо)) 
		И (ВремяОбедаС < ВремяРаботыС) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru = 'Время начала обеда должно быть больше времени начала работы';
								|en = 'Start of dinner time should be greater than time of work start'");
	КонецЕсли;
	
	Если Не ФиксированноеВремя И (ЗначениеЗаполнено(ВремяОбедаС) Или ЗначениеЗаполнено(ВремяОбедаПо)) 
		И (ВремяОбедаС > ВремяРаботыПо) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru = 'Время начала обеда должно быть больше времени начала работы';
								|en = 'Start of dinner time should be greater than time of work start'");
	КонецЕсли;
	
	Если Не ФиксированноеВремя И ЗначениеЗаполнено(ВремяОбедаПо) И (ВремяОбедаПо > ВремяРаботыПо) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						+ НСтр("ru = 'Время окончания обеда должно быть меньше времени окончания работы';
								|en = 'Time of dinner end should be less than the time of work end'");
	КонецЕсли;
	
	Если ОписаниеОшибки <> "" Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,ОписаниеОшибки);
	КонецЕсли;
		
	Возврат Не Отказ;
	
КонецФункции

&НаКлиенте
Функция ВремяЗаполнено()
	
	Отказ = Ложь;
	
	ОписаниеОшибки = "";
	
	Если (ЗначениеЗаполнено(ВремяРаботыС)) И (Не ЗначениеЗаполнено(ВремяРаботыПо)) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru = 'Не заполнено время окончания работы';
								|en = 'Time of work end is not filled in'");
	КонецЕсли;
	
	Если Не ФиксированноеВремя И ЗначениеЗаполнено(ВремяОбедаС) И Не ЗначениеЗаполнено(ВремяОбедаПо) Тогда
		
		ОписаниеОшибки = ОписаниеОшибки 
						+ ?(ЗначениеЗаполнено(ОписаниеОшибки), Символы.ПС, "")
						 + НСтр("ru = 'Не заполнено время окончания обеда';
								|en = 'Time of dinner end is not filled in'");
	КонецЕсли;
	
	Если ОписаниеОшибки <> "" Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(,ОписаниеОшибки);
	КонецЕсли;
		
	Возврат Не Отказ;
	
КонецФункции

&НаКлиенте
Процедура ВычислитьВремяРаботыПо()
	ВремяРаботыПо = ВремяРаботыС + 60 * 30;
КонецПроцедуры

&НаСервере
Функция ПолучитьВремя()
	ВремяПо = Дата(1, 1, 1, 18, 0, 0);
	Если ВариантВыбораВремени = 1 Тогда
		ВремяПо = РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяОтгрузкиПо", ТипГрузоперевозки);
	Иначе
		ВремяПо = РегистрыСведений.НастройкиОбщиеСервисДоставки.ПолучитьЗначениеНастройки("ВремяДоставкиПо", ТипГрузоперевозки);
	КонецЕсли;
	Возврат ВремяПо;
КонецФункции

#КонецОбласти
