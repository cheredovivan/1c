#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		Если Не СервисДоставки.ПравоРаботыССервисомДоставки(Истина) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СервисДоставки.УстановитьЗначенияРеквизитовПоПараметрам(ЭтотОбъект, Параметры);
	ОрганизацияБизнесСетиСсылка = Параметры.ОрганизацияБизнесСети;

	Если Не ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбран тип грузоперевозки';
													|en = 'Cargo transportation type is not selected'"), , , , Отказ);
		Возврат;
	ИначеЕсли Не СервисДоставки.ТипГрузоперевозкиДоступен(ТипГрузоперевозки) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Выбранный тип грузоперевозки не доступен';
													|en = 'The selected cargo transportation type is not available'"), , , , Отказ);
		Возврат;
	КонецЕсли;
	
	СервисДоставкиСлужебный.ПроверитьОрганизациюБизнесСети(ОрганизацияБизнесСетиСсылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийХарактерГрузаИдентификатор = Параметры.ХарактерГрузаИдентификатор;
	ТекущийХарактерГрузаНаименование = Параметры.ХарактерГрузаСтрокой;
	
	УстановитьКэшированныеЗначения();
	ВыполнитьДлительныеОперацииПриНеобходимости();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций.ФоновоеЗадание) Тогда
		ОжидатьЗавершениеВыполненияЗапроса(Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	Иначе
		Элементы.ГруппаОжидания.Видимость = Ложь;
	КонецЕсли;
	УстановитьТекущуюСтрокуСписка();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеВыбора = Новый Структура("Идентификатор, Наименование", "", "");
	
	ДанныеСтроки = Элементы.Список.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Закрыть(Неопределено);
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеВыбора, ДанныеСтроки);
	
	ОповеститьОВыборе(ДанныеВыбора);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьТекущуюСтрокуСписка()
	Если Список.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущийХарактерГрузаИдентификатор <> "" Или ТекущийХарактерГрузаНаименование <> "" Тогда
		
		УсловиеПоиска = ?(ЗначениеЗаполнено(ТекущийХарактерГрузаИдентификатор), 
			Новый Структура("Идентификатор", ТекущийХарактерГрузаИдентификатор),
			Новый Структура("Наименование", ТекущийХарактерГрузаНаименование));
		НайденныеСтроки = Список.НайтиСтроки(УсловиеПоиска);
		Если НайденныеСтроки.Количество() Тогда
			Элементы.Список.ТекущаяСтрока = НайденныеСтроки[0].ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	КэшГрузы = СервисДоставки.ПолучитьКэшированныеЗначения(ОрганизацияБизнесСетиСсылка, ТипГрузоперевозки, "Грузы");
	Для Каждого КлючИЗначение Из КэшГрузы Цикл
		Кэш.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ОбработатьРезультатПолученияХарактеровГруза(ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Кэш,
		СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза()));
	
КонецПроцедуры

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаСервере
Процедура ВыполнитьДлительныеОперацииПриНеобходимости()
	
	ПараметрыОперации = СервисДоставкиКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	Если Список.Количество() > 0 Тогда
		Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций", ПараметрыОперации);
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ТекстСообщения = НСтр("ru = 'Получение классификатора.';
											|en = 'Get classifier.'");
	ПараметрыОперации.Очередь.Добавить( , СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза());
	
	ВыполнитьЗапросВФоне(Ложь, ПараметрыОперации);
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций", ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не СервисДоставки.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ПараметрыЗапроса = ПараметрыЗапроса(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СервисДоставки.ЗапуститьФоновоеЗадание(ЭтотОбъект, ПараметрыОперации, ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации)
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		Элементы.ДекорацияОжидание.Видимость = Истина;
		Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Выполняется получение данных...';
													|en = 'Receiving data…'");
	Иначе
		Элементы.ДекорацияОжидание.Видимость = Ложь;
	КонецЕсли;
	
	ОповещениеОЗавершении = 
		Новый ОписаниеОповещения(ПараметрыОперации.ИмяПроцедурыЗавершения, ЭтотОбъект, ПараметрыОперации);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ЗаполнитьЗначенияСвойств(ПараметрыОжидания, ПараметрыОперации);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры)
	
	Очередь = ДополнительныеПараметры.Очередь;
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза() Тогда
			
			Элементы.ДекорацияОжидание.Видимость = Ложь;
			Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Обработка результата...';
														|en = 'Result processing...'");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПередЗавершениемДлительнойОперации(Результат, ДополнительныеПараметры);
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	СервисДоставкиКлиент.ОбработатьРезультатФоновогоЗадания(Результат, ДополнительныеПараметры, Отказ);
	
	Если Отказ Или Результат.Статус <> "Выполнено" Или Не ЗначениеЗаполнено(Результат.АдресРезультата)
		Или Не ЭтоАдресВременногоХранилища(Результат.АдресРезультата) Тогда
		СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата);
	Иначе
		РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза() Тогда
			Элементы.ГруппаОжидания.Видимость = Ложь;
			Элементы.ДекорацияСостояние.Заголовок = "";
			УстановитьТекущуюСтрокуСписка();
		КонецЕсли;
		
	КонецЦикла;
	
	СервисДоставкиКлиент.ПослеЗавершенияДлительнойОперации(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(АдресРезультата)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза() Тогда
			ОбработатьРезультатПолученияХарактеровГруза(РезультатОперации);
		КонецЕсли;
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапроса(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = Новый Структура;
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		
		Если ИмяМетода = СервисДоставкиКлиентСервер.ИмяМетодаПолучитьХарактерыГруза() Тогда
			Операция.Значение = СервисДоставки.НовыйПараметрыЗапросаПолучитьХарактерыГруза();
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыЗапроса.Вставить("Очередь", ПараметрыОперации.Очередь);
	ПараметрыЗапроса.Вставить("ТипГрузоперевозки", ТипГрузоперевозки);
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

#КонецОбласти

#Область ОбработкаПолученияКлассификатора

&НаСервере
Процедура ОбработатьРезультатПолученияХарактеровГруза(Результат)
	
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ОперацияВыполнена = Истина;
	СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
	
	Если Не ОперацияВыполнена Тогда
		Возврат;
	КонецЕсли;
	
	Список.Очистить();
	ДанныеКлассификатора = Результат.Список;
	
	Если ТипЗнч(ДанныеКлассификатора) = Тип("Массив") Тогда
		Для Каждого СтрокаКлассификатора Из ДанныеКлассификатора Цикл
			НоваяСтрока = Список.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКлассификатора);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти