
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ДанныеЗаказа", ДанныеЗаказа);
	Параметры.Свойство("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	Параметры.Свойство("ТипГрузоперевозки", ТипГрузоперевозки);
	
	ЗаполнитьФормуПоДаннымЗаказа();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(ДанныеЗаказа.НомерЗаказа) Тогда
		
		ПерейтиПоНавигационнойСсылке(
			СервисДоставкиКлиентСервер.АдресСтраницыЗаказаНаДоставку1СДоставка(ДанныеЗаказа.НомерЗаказа));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Печать(Команда)
	
	СписокВыбора = Новый СписокЗначений();
	ВидыПечатныхФормДокументов = СервисДоставкиКлиентСервер.ВидыПечатныхФормДокументов();
	
	Для Каждого СтрокаДокумент Из ДанныеЗаказа.СписокПечатныхФорм Цикл
		
		Для Каждого СтрокаПечатнаяФорма Из СтрРазделить(СтрокаДокумент.Представление, ",") Цикл
			
			СписокВыбора.Добавить(
				Новый Структура("uid, type",
					СтрокаДокумент.Значение.uid,
					СтрокаПечатнаяФорма),
				СтрШаблон(НСтр("ru = '%1 № %2';
								|en = '%1 No. %2'"),
					ВидыПечатныхФормДокументов.Получить(СтрокаПечатнаяФорма),
					СтрокаДокумент.Значение.id));
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если СписокВыбора.Количество() > 0 Тогда
		
		ОбработчикОповещения = Новый ОписаниеОповещения("ПослеВыбораПечатнойФормы", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОбработчикОповещения,
			СписокВыбора,
			Элементы.ФормаПечать);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФормуПоДаннымЗаказа()
	
	Заголовок = СтрШаблон(НСтр("ru = 'Заказ № %1 от %2';
								|en = 'Order No. %1 from %2'"),
		ДанныеЗаказа.НомерЗаказа,
		Формат(ДанныеЗаказа.ДатаЗаказа, "ДЛФ=Д"));
	
	Элементы.ДекорацияСостояние.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'Состояние заказа:';
																			|en = 'Order state:'") + " ",
		Новый ФорматированнаяСтрока(ДанныеЗаказа.Состояние,,,, "Ссылка"));
	
	Если НРег(ДанныеЗаказа.ВариантОтгрузки) = НСтр("ru = 'от адреса';
													|en = 'from address'") Тогда
		Элементы.ДекорацияОткуда.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'от адреса';
																				|en = 'from address'") + " ",
			Новый ФорматированнаяСтрока(ДанныеЗаказа.АдресОтгрузкиГород,, ЦветаСтиля.ЦветГиперссылкиБЭД));
	Иначе
		Элементы.ДекорацияОткуда.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'от терминала';
																				|en = 'from terminal'") + " ",
			Новый ФорматированнаяСтрока(ДанныеЗаказа.ОтправительТерминал,, ЦветаСтиля.ЦветГиперссылкиБЭД));
	КонецЕсли;
	
	Если НРег(ДанныеЗаказа.ВариантДоставки) = НСтр("ru = 'до адреса';
													|en = 'to address'") Тогда
		Элементы.ДекорацияКуда.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'до адреса';
																			|en = 'to address'") + " ",
			Новый ФорматированнаяСтрока(ДанныеЗаказа.АдресДоставкиГород,, ЦветаСтиля.ЦветГиперссылкиБЭД));
	Иначе
		Элементы.ДекорацияКуда.Заголовок = Новый ФорматированнаяСтрока(НСтр("ru = 'до терминала';
																			|en = 'to terminal'") + " ",
			Новый ФорматированнаяСтрока(ДанныеЗаказа.ПолучательТерминал,, ЦветаСтиля.ЦветГиперссылкиБЭД));
	КонецЕсли;
	
	Элементы.ДекорацияОткуда.Подсказка = ДанныеЗаказа.АдресОтгрузкиПредставление;
	Элементы.ДекорацияКуда.Подсказка = ДанныеЗаказа.АдресДоставкиПредставление;
	
	Элементы.ДекорацияОтправитель.Заголовок = ДанныеЗаказа.ОтправительНаименование;
	Элементы.ДекорацияПолучатель.Заголовок = ДанныеЗаказа.ПолучательНаименование;
	
	Элементы.ДекорацияКонтактыОтправителя.Заголовок = ДанныеЗаказа.ОтправительКонтактноеЛицоТелефонПредставление;
	Элементы.ДекорацияКонтактноеЛицоОтправителя.Заголовок = ДанныеЗаказа.ОтправительКонтактноеЛицоНаименование;

	Элементы.ДекорацияКонтактыПолучателя.Заголовок = ДанныеЗаказа.ПолучательКонтактноеЛицоТелефонПредставление;
	Элементы.ДекорацияКонтактноеЛицоПолучателя.Заголовок = ДанныеЗаказа.ПолучательКонтактноеЛицоНаименование;
	
	Если СервисДоставки.ТранспортЗаполнен(ДанныеЗаказа, "Отгрузки") Или СервисДоставки.ТранспортЗаполнен(ДанныеЗаказа, "Доставки") Тогда
		ЗаполнитьПредставлениеВодителя(ДанныеЗаказа, "Отгрузки");
		ЗаполнитьПредставлениеВодителя(ДанныеЗаказа, "Доставки");
		ЗаполнитьПредставлениеТранспорта(ДанныеЗаказа, "Отгрузки");
		ЗаполнитьПредставлениеТранспорта(ДанныеЗаказа, "Доставки");
	Иначе
		Элементы.ГруппаПанельТранспортОтгрузки.Видимость = Ложь;
		Элементы.ГруппаПанельТранспортДоставки.Видимость = Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаказа.ВнутреннийНомер) Тогда
		Элементы.ДекорацияВнутреннийНомер.Заголовок = СтрШаблон(НСтр("ru = 'Внутренний номер: %1';
																	|en = 'Internal number: %1'"), ДанныеЗаказа.ВнутреннийНомер);
	Иначе
		Элементы.ДекорацияВнутреннийНомер.Видимость = Ложь;
	КонецЕсли;
	

	МассивСклонений = ПолучитьСклоненияСтрокиПоЧислу(НСтр("ru = 'место';
															|en = 'place'"), ДанныеЗаказа.КоличествоГрузовыхМест);
	
	Элементы.ДекорацияОписаниеГруза.Заголовок = СтрШаблон(НСтр("ru = '%1%2, %3 кг, %4 м3.';
																|en = '%1%2, %3 kg, %4 m3.'"),
		?(ЗначениеЗаполнено(ДанныеЗаказа.ГрузОписание),
			ДанныеЗаказа.ГрузОписание + ": ",
			""),
		?(МассивСклонений.Количество() = 0,
			Формат(ДанныеЗаказа.КоличествоГрузовыхМест, "ЧН=") + НСтр("ru = 'мест';
																		|en = 'unit loads'"),
			МассивСклонений.Получить(0)),
		ДанныеЗаказа.ГрузВес,
		ДанныеЗаказа.ГрузОбъем);
		
	Элементы.ДекорацияМаксимальныеРазмерыГруза.Заголовок = СтрШаблон(НСтр("ru = 'Максимальные размеры (ДхШхВ): %1x%2x%3 (м).';
																			|en = 'Maximum dimensions (LxWxH): %1x%2x%3 (m).'"),
		ДанныеЗаказа.ГрузДлина,
		ДанныеЗаказа.ГрузШирина,
		ДанныеЗаказа.ГрузВысота);
		
	Если ДанныеЗаказа.Оплачен Тогда
		
		СостояниеОплаты = НСтр("ru = 'Оплачен';
								|en = 'Paid'");
		Элементы.СостояниеОплаты.ЦветТекста = WebЦвета.Зеленый;
		
	Иначе
		
		СостояниеОплаты = НСтр("ru = 'Не оплачен';
								|en = 'Not paid'");
		Элементы.СостояниеОплаты.ЦветТекста = WebЦвета.Кирпичный;
		
	КонецЕсли;
	
	Для Каждого СтрокаДокументОснование Из ДанныеЗаказа.СписокДокументов Цикл
		
		ДокументПеревозчикаДанные = СтрокаДокументОснование.Значение;
		
		ДокументПеревозчика = СписокДокументов.Добавить();
		ДокументПеревозчика.Тип = ТипДокументаПоИдентификатору(ДокументПеревозчикаДанные.type);
		ДокументПеревозчика.Номер = ДокументПеревозчикаДанные.id;
		ДокументПеревозчика.Дата = СервисДоставки.ПрочитатьДатуJSONБезРазделителя(ДокументПеревозчикаДанные.createDate);
		
		Для Каждого СтрокаУслуга Из ДокументПеревозчикаДанные.services Цикл
			
			НоваяСтрока = СписокУслуг.Добавить();
			НоваяСтрока.Наименование = СтрокаУслуга.name;
			НоваяСтрока.Количество = СтрокаУслуга.quantity;
			НоваяСтрока.Сумма = СтрокаУслуга.sum;
			НоваяСтрока.СуммаСНДС = СтрокаУслуга.totalSum;
			НоваяСтрока.СуммаНДС = СтрокаУслуга.vat;
			НоваяСтрока.СуммаСкидки = СтрокаУслуга.discountSum;
			НоваяСтрока.СтавкаНДС = СтрокаУслуга.vatRate;
			
		КонецЦикла;
		
	КонецЦикла;
	
	ИтогКоличество = СписокУслуг.Итог("Количество");
	ИтогСумма = СписокУслуг.Итог("Сумма");
	ИтогСуммаСНДС = СписокУслуг.Итог("СуммаСНДС");
	ИтогСуммаНДС = СписокУслуг.Итог("СуммаНДС");
	ИтогСуммаСкидки = СписокУслуг.Итог("СуммаСкидки");
	КоличествоУслуг = СписокУслуг.Количество();
	КоличествоДокументов = СписокДокументов.Количество();
	
	Элементы.ФормаПечать.Видимость = ДанныеЗаказа.ДоступнаПечать;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПечатнойФормы(Значение, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Значение) = Тип("ЭлементСпискаЗначений") Тогда
		
		ИдентификаторДокумента = Значение.Значение.uid;
		ИдентификаторПечатнойФормы = Значение.Значение.type;
		ПредставлениеПечатнойФормы = Значение.Представление;
			
		ВыполнитьЗапросПолучитьПечатнуюФормуИзСервиса();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросПолучитьПечатнуюФормуИзСервиса()
	
	ПараметрыОперации = Новый Структура("ИмяПроцедуры, НаименованиеОперации, ВыводитьОкноОжидания");
	ПараметрыОперации.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПечатнуюФормуИзСервиса();
	ПараметрыОперации.НаименованиеОперации = НСтр("ru = 'Получение печатной формы из сервиса.';
													|en = 'Receiving the print form from service.'");
	ПараметрыОперации.ВыводитьОкноОжидания = Истина;
	
	ВыполнитьЗапрос(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапрос(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ОчиститьСообщения();
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	ЭтотОбъект[ИмяФоновогоЗадания] = ФоновоеЗадание;
	
	Если ИнтернетПоддержкаПодключена = Ложь Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВыполнитьЗапросПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		Возврат;
		
	Иначе
		
		ПараметрыОперации.Вставить("ФоновоеЗадание", ФоновоеЗадание);
		ВыполнитьЗапросПродолжение(Истина, ПараметрыОперации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяФоновогоЗадания = "ФоновоеЗадание"+ ДополнительныеПараметры.ИмяПроцедуры;
	
	Если Результат = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
		
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("Логин") Тогда
		
		ИнтернетПоддержкаПодключена = Ложь;
		ЭтотОбъект[ИмяФоновогоЗадания] = ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ДополнительныеПараметры);
		ДополнительныеПараметры.Вставить("ФоновоеЗадание", ЭтотОбъект[ИмяФоновогоЗадания]);
		
	КонецЕсли;
	
	Если ЭтотОбъект[ИмяФоновогоЗадания] = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект[ИмяФоновогоЗадания].Статус = "Выполняется" Тогда
		
		ОжидатьЗавершениеВыполненияЗапроса(ДополнительныеПараметры);
		
	ИначеЕсли ЭтотОбъект[ИмяФоновогоЗадания].Статус = "Выполнено" Тогда
		
		ВыполнитьЗапросЗавершение(ЭтотОбъект[ИмяФоновогоЗадания], ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеВыполненияЗапроса(ПараметрыОперации)
	
	ВыводитьОкноОжидания = ?(ЗначениеЗаполнено(ПараметрыОперации.ВыводитьОкноОжидания), 
		ПараметрыОперации.ВыводитьОкноОжидания,
		Ложь);
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	ПараметрыОперации.Вставить("ФоновоеЗадание", ФоновоеЗадание);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ТекстСообщения = ПараметрыОперации.НаименованиеОперации;
	ПараметрыОжидания.ВыводитьПрогрессВыполнения = Ложь;
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Ложь;
	ПараметрыОжидания.ВыводитьСообщения = Истина;
	ПараметрыОжидания.Вставить("ИдентификаторЗадания", ФоновоеЗадание.ИдентификаторЗадания);
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ВыполнитьЗапросЗавершение",
		ЭтотОбъект, ПараметрыОперации);
		
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ФоновоеЗадание, ОбработкаЗавершения, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗапросЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ДополнительныеПараметры.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	
	СервисДоставкиКлиент.ОбработатьРезультатФоновогоЗадания(Результат, ДополнительныеПараметры, Отказ);
	Если Результат = Неопределено
		Или ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Отказ
		И Результат.Статус = "Выполнено" Тогда
		
		Если ЗначениеЗаполнено(Результат.АдресРезультата)
			И ЭтоАдресВременногоХранилища(Результат.АдресРезультата)
			И ДополнительныеПараметры.ФоновоеЗадание.ИдентификаторЗадания = ЭтотОбъект[ИмяФоновогоЗадания].ИдентификаторЗадания Тогда
			
			Если ДополнительныеПараметры.ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПечатнуюФормуИзСервиса() Тогда
				
				ДвоичныеДанныеBase64 = РезультатПолученияПечатнойФормыИзСервиса(Результат.АдресРезультата);
				Если ЗначениеЗаполнено(ДвоичныеДанныеBase64) Тогда
					
					#Если ВебКлиент Тогда
						
						СодержимоеФайла = Base64Значение(ДвоичныеДанныеBase64);
						АдресХранилища = ПоместитьВоВременноеХранилище(СодержимоеФайла);
						ИмяФайла = СтрШаблон("%1.%2", ПредставлениеПечатнойФормы, "pdf");
						НачатьПолучениеФайлаССервера(АдресХранилища, ИмяФайла);
						
						ПоказатьОповещениеПользователя(НСтр("ru = 'Файл загружен';
															|en = 'File is imported'"),,
							ИмяФайла,
							БиблиотекаКартинок.СохранитьФайлКак);
						
					#Иначе
						
						ПараметрыОткрытияФормы = Новый Структура();
						ПараметрыОткрытияФормы.Вставить("Заголовок", ПредставлениеПечатнойФормы);
						ПараметрыОткрытияФормы.Вставить("ДанныеФайла", ДвоичныеДанныеBase64);
						
						ОткрытьФорму("Обработка.СервисДоставки.Форма.ПросмотрPDF",
							ПараметрыОткрытияФормы,,
							Новый УникальныйИдентификатор(),,,,
							РежимОткрытияОкнаФормы.Независимый);
						
					#КонецЕсли
					
				Иначе
					
					ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Ошибка загрузки печатной формы';
																	|en = 'Error importing the print form'"));
					
				КонецЕсли;
				
				ФоновоеЗаданиеПолучитьПечатнуюФормуИзСервиса = Неопределено;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция РезультатПолученияПечатнойФормыИзСервиса(АдресРезультата, ОперацияВыполнена = Истина)
	
	ДанныеФайла = Неопределено;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		
		Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
		Если ЗначениеЗаполнено(Результат)
			И ТипЗнч(Результат) = Тип("Структура") Тогда
			
			Если Результат.Свойство("Список")
				И ТипЗнч(Результат.Список) = Тип("Массив")
				И Результат.Список.Количество() > 0 Тогда
				
				ДанныеФайла = Результат.Список.Получить(0).base64;
				
			КонецЕсли;
			
			СервисДоставки.ОбработатьБлокОшибок(Результат, ОперацияВыполнена);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ДанныеФайла;
	
КонецФункции

&НаСервере
Функция ВыполнитьЗапросВФоне(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	Если Не ИнтернетПоддержкаПодключена Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Отказ = Ложь;
	ПараметрыЗапроса = ПараметрыЗапроса(ПараметрыОперации, Отказ);
	
	Если ПараметрыЗапроса.Свойство("ОрганизацияБизнесСетиСсылка") Тогда
		СервисДоставкиСлужебный.ПроверитьОрганизациюБизнесСети(ПараметрыЗапроса.ОрганизацияБизнесСетиСсылка, Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ИмяФоновогоЗадания = "ФоновоеЗадание" + ПараметрыОперации.ИмяПроцедуры;
	ФоновоеЗадание = ЭтотОбъект[ИмяФоновогоЗадания];
	Если ФоновоеЗадание <> Неопределено Тогда
		ОтменитьВыполнениеЗадания(ФоновоеЗадание.ИдентификаторЗадания);
	КонецЕсли;
	
	Задание = Новый Структура("ИмяПроцедуры, Наименование, ПараметрыПроцедуры");
	Задание.Наименование = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1. %2';
																						|en = '%1. %2'"),
		СервисДоставкиКлиентСервер.ПредставлениеТипаГрузоперевозки(ТипГрузоперевозки),
		ПараметрыОперации.НаименованиеОперации);
	Задание.ИмяПроцедуры = "СервисДоставки." + ПараметрыОперации.ИмяПроцедуры;
	Задание.ПараметрыПроцедуры = ПараметрыЗапроса;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Задание.Наименование;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Если ПараметрыОперации.Свойство("ЗапуститьВФоне")
		И ТипЗнч(ПараметрыОперации.ЗапуститьВФоне) = Тип("Булево") Тогда
		ПараметрыВыполнения.ЗапуститьВФоне = ПараметрыОперации.ЗапуститьВФоне;
	КонецЕсли; 
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(Задание.ИмяПроцедуры,
		Задание.ПараметрыПроцедуры, ПараметрыВыполнения);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
		ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПараметрыЗапроса(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = Новый Структура();
	
	ИмяПроцедуры = ПараметрыОперации.ИмяПроцедуры;
	
	Если ИмяПроцедуры = СервисДоставкиКлиентСервер.ИмяПроцедурыПолучитьПечатнуюФормуИзСервиса() Тогда
		ПараметрыЗапроса = ПараметрыЗапросаПолучитьПечатнуюФормуИзСервиса(ПараметрыОперации, Отказ);
	КонецЕсли;
	
	ПараметрыЗапроса.Вставить("ОрганизацияБизнесСетиСсылка", ОрганизацияБизнесСетиСсылка);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ПараметрыЗапросаПолучитьПечатнуюФормуИзСервиса(ПараметрыОперации, Отказ)
	
	ПараметрыЗапроса = СервисДоставки.НовыйПараметрыЗапросаПолучитьПечатнуюФормуИзСервиса();
	
	Если ЗначениеЗаполнено(ТипГрузоперевозки) Тогда
		
		ПараметрыЗапроса.ИдентификаторДокумента = ИдентификаторДокумента;
		ПараметрыЗапроса.ИдентификаторПечатнойФормы = ИдентификаторПечатнойФормы;
		ПараметрыЗапроса.ТипГрузоперевозки = ТипГрузоперевозки;
		
	Иначе
		
		ТекстОшибки = НСтр("ru = 'Не выбран тип грузоперевозки.';
							|en = 'Cargo transportation type is not selected.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Отказ = Истина;
		
	КонецЕсли;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

&НаСервере
Функция ТипДокументаПоИдентификатору(Идентификатор) 
	
	Если НРег(Идентификатор) = "request" Тогда
		Результат = НСтр("ru = 'Заявка на доставку груза';
						|en = 'Request for cargo delivery'");
	ИначеЕсли НРег(Идентификатор) = "request_ftl" Тогда
		Результат = НСтр("ru = 'Заявка на междугороднюю перевозку выделенной еврофурой';
						|en = 'Request for intercity transportation with a provided eurotruck'");
	ИначеЕсли НРег(Идентификатор) = "request_sf" Тогда
		Результат = НСтр("ru = 'Заявка на доставку до адреса получателя';
						|en = 'Request for delivery to the recipient address'");
	ИначеЕсли НРег(Идентификатор) = "shipping" Тогда
		Результат = НСтр("ru = 'Накладная';
						|en = 'Invoice'");
	Иначе
		Результат = "";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьПредставлениеВодителя(ДанныеЗаказа, Направление)
	
	ДанныеВодителя = Новый Массив;
	ЗаголовокДекорации = "";
	
	Если ЗначениеЗаполнено(ДанныеЗаказа["ВодительНаименование" + Направление]) Тогда
		ДанныеВодителя.Добавить(ДанныеЗаказа["ВодительНаименование" + Направление]);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеЗаказа["ВодительНомерТелефона" + Направление]) Тогда
		ДанныеВодителя.Добавить(ДанныеЗаказа["ВодительНомерТелефона" + Направление]);
	КонецЕсли;
	
	Если ДанныеВодителя.Количество() Тогда
		ЗаголовокДекорации = СтрШаблон(НСтр("ru = 'Водитель: %1';
											|en = 'Driver: %1'"), СтрСоединить(ДанныеВодителя, ", "));
	КонецЕсли;
	
	Элементы["ДекорацияВодитель" + Направление].Заголовок = ЗаголовокДекорации;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеТранспорта(ДанныеЗаказа, Направление);

	ДанныеТранспорта = Новый Массив;
	ЗаголовокДекорации = "";
	
	Если ЗначениеЗаполнено(ДанныеЗаказа["ТранспортТип" + Направление]) Тогда
		ДанныеТранспорта.Добавить(ДанныеЗаказа["ТранспортТип" + Направление]);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеЗаказа["ТранспортНомер" + Направление]) Тогда
		ДанныеТранспорта.Добавить(ДанныеЗаказа["ТранспортНомер" + Направление]);
	КонецЕсли;
	
	Если ДанныеТранспорта.Количество() Тогда
		ЗаголовокДекорации = СтрШаблон(НСтр("ru = 'Авто: %1';
											|en = 'Auto: %1'"), СтрСоединить(ДанныеТранспорта, ", "));
	КонецЕсли;
	
	Элементы["ДекорацияТранспорт" + Направление].Заголовок = ЗаголовокДекорации;
	
КонецПроцедуры

#КонецОбласти
