#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область Команды

// Определяет список команд создания на основании.
//
// Параметры:
//  КомандыСозданияНаОсновании - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.КомандыСозданияНаОсновании
//  Параметры - см. СозданиеНаОснованииПереопределяемый.ПередДобавлениемКомандСозданияНаОсновании.Параметры
//
Процедура ДобавитьКомандыСозданияНаОсновании(КомандыСозданияНаОсновании, Параметры) Экспорт
	Возврат;
КонецПроцедуры

// Определяет список команд отчетов.
//
// Параметры:
//   КомандыОтчетов - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.КомандыОтчетов
//   Параметры - См. ВариантыОтчетовПереопределяемый.ПередДобавлениемКомандОтчетов.Параметры
//
Процедура ДобавитьКомандыОтчетов(КомандыОтчетов, Параметры) Экспорт
	Возврат;
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - см. УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	Возврат;
КонецПроцедуры

// Возвращает структуру параметров, заданных в настройках открытия формы.
//
// Возвращаемое значение:
//  Структура
//
Функция ПолучитьНастройкиПользователя() Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	НастройкиОткрытияФормПриНачалеРаботыСистемы.Параметры КАК Параметры
		|ИЗ
		|	РегистрСведений.НастройкиОткрытияФормПриНачалеРаботыСистемы КАК НастройкиОткрытияФормПриНачалеРаботыСистемы
		|ГДЕ
		|	НастройкиОткрытияФормПриНачалеРаботыСистемы.Пользователь = &Пользователь
		|	И НастройкиОткрытияФормПриНачалеРаботыСистемы.ОткрываемаяФорма = &ОткрываемаяФорма");
	
	Запрос.УстановитьПараметр("ОткрываемаяФорма",
		Перечисления.ФормыОткрываемыеПриНачалеРаботыСистемы.РабочееМестоВыполнениеОпераций);
	
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		ХранилищеПараметров = Результат.Параметры;// ХранилищеЗначения
		Возврат ХранилищеПараметров.Получить();
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область Прочее

//++ Локализация

// Подобрать исполнителя.
// 
// Параметры:
//  Ссылка - СправочникСсылка.ФизическиеЛица, СправочникСсылка.Сотрудники -
// 
// Возвращаемое значение:
//  СправочникСсылка.Сотрудники, СправочникСсылка.ФизическиеЛица - Подобрать исполнителя
Функция ПодобратьИсполнителя(Ссылка) Экспорт
	
	ТекущаяДата = ТекущаяДатаСеанса();
	
	Исполнитель = Неопределено;
	
	ИспользуетсяУчетТрудозатратВРазрезеСотрудников = ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(
		ТекущаяДата);
	
	Если ИспользуетсяУчетТрудозатратВРазрезеСотрудников И ТипЗнч(Ссылка) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
		
		ОсновныеСотрудники = КадровыйУчет.ОсновныеСотрудникиФизическихЛиц(
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Ссылка), Истина, Неопределено, ТекущаяДата);
			
		Если ОсновныеСотрудники.Количество() > 0 Тогда
			Исполнитель = ОсновныеСотрудники[0].Сотрудник;
		КонецЕсли;
		
	ИначеЕсли НЕ ИспользуетсяУчетТрудозатратВРазрезеСотрудников И ТипЗнч(Ссылка) = Тип("СправочникСсылка.Сотрудники") Тогда
		Исполнитель = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ФизическоеЛицо");
	КонецЕсли;
	
	Возврат Исполнитель;
	
КонецФункции

//-- Локализация

#КонецОбласти

#КонецОбласти

#КонецЕсли
