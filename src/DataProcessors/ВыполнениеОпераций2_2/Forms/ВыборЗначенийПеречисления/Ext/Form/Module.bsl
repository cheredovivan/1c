#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ПараметрПеречисление = Неопределено;
	Параметры.Свойство("Перечисление", ПараметрПеречисление);
	
	ОбъектМетаданных = Метаданные.НайтиПоТипу(ПараметрПеречисление);
	Заголовок = ОбъектМетаданных.Синоним;
	
	ДоступныеЗначения = Неопределено;
	Если НЕ Параметры.Свойство("ДоступныеЗначения", ДоступныеЗначения) ИЛИ НЕ ЗначениеЗаполнено(ДоступныеЗначения) Тогда
		ДоступныеЗначения = Перечисления[ОбъектМетаданных.Имя];
	КонецЕсли;
	
	Для каждого Значение Из ДоступныеЗначения Цикл
		Список.Добавить(Значение);
	КонецЦикла;
		
	ВыбранныеЗначения = Неопределено;
	Если Параметры.Свойство("ВыбранныеЗначения", ВыбранныеЗначения) И ЗначениеЗаполнено(ВыбранныеЗначения) Тогда
		Для каждого Элемент Из Список Цикл
			Если ВыбранныеЗначения.Найти(Элемент.Значение) <> Неопределено Тогда
				Элемент.Пометка = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Массив;
	Для каждого Элемент Из Список Цикл
		Если Элемент.Пометка Тогда
			Результат.Добавить(Элемент.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти