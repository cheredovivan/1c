
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("ТипОтбораПодразделение", ТипОтбораПодразделение);
	Параметры.Свойство("ТипОтбораРабочийЦентр",  ТипОтбораРабочийЦентр);
	Параметры.Свойство("Подразделение",          Подразделение);
	Параметры.Свойство("Участок",                Участок);
	Параметры.Свойство("РабочийЦентр",           РабочийЦентр);
	
	ПрочитатьПараметрыПодразделения();
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	
	УстановитьОтборПоПодразделениюУчастку();
	УстановитьОтборПоРабочемуЦентру(ЭтотОбъект);
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПроизводственнаяОперация2_2"
		И ТипЗнч(Параметр) = Тип("Структура")
		И Параметр.Свойство("Подразделение")
		И Параметр.Подразделение = Подразделение Тогда
		
		Элементы.ОперацииНаКонтроле.Обновить();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	Если ЗначениеЗаполнено(Подразделение) Тогда
		
		// Отборы установлены через параметры, сохраненные настройки не используются
		
		Настройки.Удалить("Подразделение");
		Настройки.Удалить("Участок");
		Настройки.Удалить("РабочийЦентр");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Настройки["Подразделение"] <> Неопределено Тогда
		
		ТипОтбораПодразделение = ?(НЕ Участок.Пустая() И ИспользоватьУчастки, 1, 0);
		
		Если ТипЗнч(РабочийЦентр) = Тип("СправочникСсылка.ВидыРабочихЦентров")
				ИЛИ НЕ ЗначениеЗаполнено(РабочийЦентр) Тогда
			ТипОтбораРабочийЦентр = 0;
		Иначе
			ТипОтбораРабочийЦентр = 1;
		КонецЕсли;
		
		ПодразделениеПриИзмененииНаСервере(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастокПриИзменении(Элемент)
	
	УчастокПриИзмененииНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРабочийЦентрПриИзменении(Элемент)
	
	УстановитьОтборПоРабочемуЦентру(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСменаПриИзменении(Элемент)
	
	УстановитьОтборПоСменномуЗаданию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОтбораПодразделениеПриИзменении(Элемент)
	
	Если ТипОтбораПодразделение = 1 Тогда
		
		Подразделение = Неопределено;
		ПодразделениеПриИзмененииНаСервере(Истина);
		
	Иначе
		
		Участок = Неопределено;
		УчастокПриИзмененииНаСервере(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОтбораРабочийЦентрПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(РабочийЦентр) Тогда
		ЗначениеОтбораДоИзменения = РабочийЦентр;
	Иначе
		ЗначениеОтбораДоИзменения = Неопределено;
	КонецЕсли;
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	
	Если ЗначениеОтбораДоИзменения <> Неопределено
		И ЗначениеОтбораДоИзменения <> РабочийЦентр Тогда
		УстановитьОтборПоРабочемуЦентру(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОперацииНаКонтроле

&НаСервереБезКонтекста
Процедура ОперацииНаКонтролеПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ОперативныйУчетПроизводства.УстановитьОформлениеСтатусаВСпискеПроизводственныхОпераций(Строки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КонтрольОперацийПроконтролировать(Команда)
	
	Если НЕ ОбщегоНазначенияУТКлиент.ПроверитьНаличиеВыделенныхВСпискеСтрок(Элементы.ОперацииНаКонтроле) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ОперацииНаКонтроле.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПроизводственнаяОперация", ТекущиеДанные.Ссылка);
	
	ПараметрыОповещения = Новый Структура("ПроизводственнаяОперация", ТекущиеДанные.Ссылка);
	Оповещение = Новый ОписаниеОповещения("ПроконтролироватьЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ОткрытьФорму("Документ.ПроизводственнаяОперация2_2.Форма.КонтрольОперации",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(ЭтотОбъект, "ОперацииНаКонтроле.Дата", Элементы.ОперацииНаКонтролеДата.Имя);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ИспользоватьУчастки = ПроизводствоСервер.ИспользоватьУчасткиПриПооперационномУправлении();
	
	Элементы.ОтборПодразделениеЗаголовок.Видимость = НЕ ИспользоватьУчастки;
	Элементы.ТипОтбораПодразделение.Видимость = ИспользоватьУчастки;
	
	//

	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Подразделение") ИЛИ Инициализация Тогда
		
		Элементы.ОперацииНаКонтролеСменноеЗадание.Видимость = Форма.ИспользоватьСменныеЗадания;
		Элементы.ОтборСменаЗаголовок.Видимость              = Форма.ИспользоватьСменныеЗадания;
		Элементы.ОтборСмена.Видимость                       = Форма.ИспользоватьСменныеЗадания;
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ТипОтбораПодразделение") ИЛИ Инициализация Тогда
			
		Элементы.ОтборПодразделение.Видимость = (Форма.ТипОтбораПодразделение = 0);
		Элементы.ОтборУчасток.Видимость       = (Форма.ТипОтбораПодразделение = 1);
		
		Связи = Новый Массив;
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Подразделение", "Подразделение"));
		Если Форма.ТипОтбораПодразделение = 1 Тогда
			Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Участок", "Участок"));
		КонецЕсли;
		
		Элементы.ОтборРабочийЦентр.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		Элементы.ОтборСмена.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроконтролироватьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ПроизводственнаяОперация = ДополнительныеПараметры.ПроизводственнаяОперация;
	
	ОперативныйУчетПроизводстваВызовСервера.ЗафиксироватьРезультатКонтроляОперации(
		Результат.ЗаписиПротокола, Результат.ПересчитатьНормативы, ПроизводственнаяОперация);
	
	ОповеститьОбИзменении(ПроизводственнаяОперация);
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("Подразделение", Подразделение);
	ПараметрОповещения.Вставить("Ссылка",        ПроизводственнаяОперация);
	Оповестить("Запись_ПроизводственнаяОперация2_2", ПараметрОповещения, УникальныйИдентификатор);
	
КонецПроцедуры

#Область ОтборыОбщие

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере(ИзмененТипОтбора)
	
	ПрочитатьПараметрыПодразделения();
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	
	УстановитьОтборПоПодразделениюУчастку();
	УстановитьОтборПоРабочемуЦентру(ЭтотОбъект);
	УстановитьОтборПоСменномуЗаданию(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Подразделение" + ?(ИзмененТипОтбора, ",ТипОтбораПодразделение", ""));
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере(ИзмененТипОтбора)
	
	Подразделение = ?(Участок.Пустая(),
		Неопределено,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Участок, "Владелец"));
		
	ПодразделениеПриИзмененииНаСервере(ИзмененТипОтбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоРабочемуЦентру(Форма)
	
	УстановленОтбор = ЗначениеЗаполнено(Форма.РабочийЦентр);
	
	Если Форма.ТипОтбораРабочийЦентр = 1 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.ОперацииНаКонтроле,
			"РабочийЦентр",
			Форма.РабочийЦентр, 
			ВидСравненияКомпоновкиДанных.Равно,
			,
			УстановленОтбор);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.ОперацииНаКонтроле,
			"ВидРабочегоЦентра",
			,
			,
			,
			Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.ОперацииНаКонтроле,
			"РабочийЦентр",
			,
			,
			,
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Форма.ОперацииНаКонтроле,
			"ВидРабочегоЦентра",
			Форма.РабочийЦентр,
			ВидСравненияКомпоновкиДанных.Равно,
			, 
			УстановленОтбор);
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПодразделениюУчастку()
	
	Подразделение = Подразделение;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ОперацииНаКонтроле,
		"Подразделение",
		Подразделение,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ОперацииНаКонтроле,
		"Участок",
		Участок,
		ВидСравненияКомпоновкиДанных.Равно,,
		ТипОтбораПодразделение = 1);
		
	ПараметрыФО = Новый Структура(
		"Подразделение",
		?(ЗначениеЗаполнено(Подразделение), Подразделение, Неопределено));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСменномуЗаданию(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.ОперацииНаКонтроле,
		"СменноеЗадание",
		Форма.Смена,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Форма.Смена));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТипРабочегоЦентра(Форма)
	
	Если Форма.ТипОтбораРабочийЦентр = 0 Тогда
		ДопустимыйТип = Новый ОписаниеТипов("СправочникСсылка.ВидыРабочихЦентров");
	Иначе
		ДопустимыйТип = Новый ОписаниеТипов("СправочникСсылка.РабочиеЦентры");
	КонецЕсли;
	
	Форма.РабочийЦентр = ДопустимыйТип.ПривестиЗначение(Форма.РабочийЦентр);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПрочитатьПараметрыПодразделения()
	
	Если Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект,
		ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение),
		"ИспользоватьСменныеЗадания");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
