#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("Подразделение", Подразделение) Тогда
		ПрочитатьПараметрыПодразделения();
	КонецЕсли;
	
	ИнициализироватьКэш();
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	УстановитьТипИсполнителя();
	
	УстановитьСвойстваДинамическогоСпискаВыполнениеОпераций();
	
	Состояния.ЗагрузитьЗначения(СостоянияНезавершенные(ЭтотОбъект));
	УстановитьОтборПоСостоянию(ЭтотОбъект);
	
	УстановитьОтборПоПодразделениюУчастку();
	УстановитьОтборПоРабочемуЦентру();
	
	УстановитьОтборПоЭтапам(Параметры.ОтборПоЭтапам);
	
	НастроитьЭлементыФормы();
	
	РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций.ЗапуститьЗадания();
	
	#Область СтандартныеМеханизмы
	
	// ПодключаемоеОборудование
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники = Новый ОписаниеТипов("ДокументСсылка.ПроизводственнаяОперация2_2");
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ВыполнениеОпераций.КоманднаяПанель;
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ГруппаГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	#КонецОбласти
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	// ПодключаемоеОборудование
	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			ОбработатьШтрихкоды(МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если (ИмяСобытия = "Запись_ПроизводственнаяОперация2_2"
			ИЛИ ИмяСобытия = "Запись_ЭтапыПроизводства"
			ИЛИ ИмяСобытия = "Запись_СменноеЗадание"
			ИЛИ ИмяСобытия = "Запись_ОчередьПроизводственныхОпераций"
			ИЛИ ИмяСобытия = "Запись_ОперацииКСозданиюСменныхЗаданий") Тогда
		
		ОбновитьСписок = Истина;
		
		Если ТипЗнч(Параметр) = Тип("Структура")
			И Параметр.Свойство("Подразделение")
			И Параметр.Подразделение <> Подразделение Тогда
			
			ОбновитьСписок = Ложь;
			
		КонецЕсли;
		
		Если ОбновитьСписок Тогда
			
			ОбновитьСписокНаСервере();
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)

	Если ЗначениеЗаполнено(Подразделение) Тогда
		
		// Отборы установлены через параметры, сохраненные настройки не используются
		
		Настройки.Удалить("Подразделение");
		Настройки.Удалить("Участок");
		Настройки.Удалить("РабочийЦентр");
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	Если Настройки["Подразделение"] <> Неопределено Тогда
		
		ТипОтбораПодразделение = ?(НЕ Участок.Пустая() И ИспользоватьУчастки, 1, 0);
		
		Если ТипЗнч(РабочийЦентр) = Тип("СправочникСсылка.ВидыРабочихЦентров")
				ИЛИ НЕ ЗначениеЗаполнено(РабочийЦентр) Тогда
			ТипОтбораРабочийЦентр = 0;
		Иначе
			ТипОтбораРабочийЦентр = 1;
		КонецЕсли;
		
		ЗаполнитьСлужебныеРеквизитыРабочегоЦентра();
		
		ПодразделениеПриИзмененииНаСервере(Истина);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипОтбораПодразделениеПриИзменении(Элемент)
	
	Если ТипОтбораПодразделение = 1 Тогда
		
		Подразделение = Неопределено;
		ПодразделениеПриИзмененииНаСервере(Истина);
		
	Иначе
		
		Участок = Неопределено;
		УчастокПриИзмененииНаСервере(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	
	ПодразделениеПриИзмененииНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборУчастокПриИзменении(Элемент)
	
	УчастокПриИзмененииНаСервере(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТипОтбораРабочийЦентрПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(РабочийЦентр) Тогда
		ЗначениеОтбораДоИзменения = РабочийЦентр;
	Иначе
		ЗначениеОтбораДоИзменения = Неопределено;
	КонецЕсли;
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	
	Если ЗначениеОтбораДоИзменения <> Неопределено
		И ЗначениеОтбораДоИзменения <> РабочийЦентр Тогда
		РабочийЦентрПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборРабочийЦентрПриИзменении(Элемент)
	
	РабочийЦентрПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВариантНаладкиПриИзменении(Элемент)
	
	УстановитьОтборПоВариантуНаладки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИсполнительПриИзменении(Элемент)
	
	ИсполнительПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИсполнительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ИсполнительНачалоВыбораЗавершение", ЭтотОбъект);
	
	ПроизводствоКлиент.ОткрытьФормуВыбораИсполнителя(
		Неопределено,
		Подразделение,
		Исполнитель,
		Неопределено,
		,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИсполнительАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборИсполнительОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСменаПриИзменении(Элемент)
	
	УстановитьОтборПоСменномуЗаданию(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйОтбор_НезавершенныеНажатие(Элемент)
	
	Если ТолькоНезавершенныеСостояния(ЭтотОбъект) Тогда
		Состояния.Очистить();
	Иначе
		Состояния.ЗагрузитьЗначения(СостоянияНезавершенные(ЭтотОбъект));
	КонецЕсли;
	УстановитьОтборПоСостоянию(ЭтотОбъект, БыстрыеОтборыПереченьНезавершенные());
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйОтбор_ЗавершенныеНажатие(Элемент)
	
	Если ТолькоЗавершенныеСостояния(ЭтотОбъект) Тогда
		Состояния.Очистить();
	Иначе
		Состояния.ЗагрузитьЗначения(СостоянияЗавершенные(ЭтотОбъект));
	КонецЕсли;
	УстановитьОтборПоСостоянию(ЭтотОбъект, БыстрыеОтборыПереченьНезавершенные());
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйОтбор_Нажатие(Элемент)
	
	ПереключитьБыстрыйОтбор(ЭтотОбъект, Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура БыстрыйОтбор_ВсеНажатие(Элемент)
	
	Состояния.Очистить();
	
	УстановитьОтборПоСостоянию(ЭтотОбъект, БыстрыеОтборыПереченьВсе());
	
КонецПроцедуры

&НаКлиенте
Процедура ИнформационноеСообщениеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОтключитьОтборПоЭтапу" Тогда
		
		СтандартнаяОбработка = Ложь;
		УстановитьОтборПоЭтапам();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходПроизводственныеОперацииНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПодразделениеОтбор", Подразделение);
	
	ОткрытьФорму("Документ.ПроизводственнаяОперация2_2.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходКонтрольВыполненияОперацийНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ТипОтбораПодразделение", ТипОтбораПодразделение);
	ПараметрыФормы.Вставить("ТипОтбораРабочийЦентр",  ТипОтбораРабочийЦентр);
	ПараметрыФормы.Вставить("Подразделение",          Подразделение);
	ПараметрыФормы.Вставить("Участок",                Участок);
	ПараметрыФормы.Вставить("РабочийЦентр",           РабочийЦентр);
	
	ОткрытьФорму("Обработка.ВыполнениеОпераций2_2.Форма.КонтрольВыполненияОпераций", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыполнениеОпераций

&НаКлиенте
Процедура ВыполнениеОперацийПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если СтрНайти("ВыполнениеОперацийОперация,ВыполнениеОперацийНаименованиеОперации,ВыполнениеОперацийПроизводственнаяОперацияНомер", Поле.Имя) > 0 Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.ПроизводственнаяОперация) Тогда
				ПоказатьЗначение(, ТекущиеДанные.ПроизводственнаяОперация);
			ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.Операция) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Операция);
			КонецЕсли;
		ИначеЕсли СтрНайти("ВыполнениеОперацийЭтап,ВыполнениеОперацийПредставлениеЭтапа", Поле.Имя) > 0 Тогда
			ПоказатьЗначение(, ТекущиеДанные.Этап);
		ИначеЕсли СтрНайти("ВыполнениеОперацийСменноеЗаданиеДата", Поле.Имя) > 0 Тогда
			Если ЗначениеЗаполнено(ТекущиеДанные.СменноеЗадание) Тогда
				ПоказатьЗначение(, ТекущиеДанные.СменноеЗадание);
			КонецЕсли;
		ИначеЕсли СтрНайти("ВыполнениеОперацийРаспоряжение", Поле.Имя) > 0 Тогда
			ПоказатьЗначение(, ТекущиеДанные.Распоряжение);
		ИначеЕсли СтрНайти("ВыполнениеОперацийНоменклатура,ВыполнениеОперацийХарактеристика", Поле.Имя) > 0 Тогда
			ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнениеОперацийПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ТаблицаОпераций = Новый ТаблицаЗначений;
	ТаблицаОпераций.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
	ТаблицаОпераций.Колонки.Добавить("СтрокаСписка");
	ТаблицаОпераций.Колонки.Добавить("Этап", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
	ТаблицаОпераций.Колонки.Добавить("Операция", Новый ОписаниеТипов("СправочникСсылка.ТехнологическиеОперации"));
	ТаблицаОпераций.Колонки.Добавить("ИдентификаторОперации", Новый ОписаниеТипов("Число"));
	ТаблицаОпераций.Колонки.Добавить("ПроизводственнаяОперация", Новый ОписаниеТипов("ДокументСсылка.ПроизводственнаяОперация2_2"));
	ТаблицаОпераций.Колонки.Добавить("СостояниеТекст", Новый ОписаниеТипов("Строка"));
	
	Для каждого КлючИЗначение Из Строки Цикл
		ДанныеСтроки = КлючИЗначение.Значение.Данные;
		Если ДанныеСтроки.Состояние = Перечисления.СостоянияВыполненияОпераций.КНазначению
				ИЛИ ДанныеСтроки.Состояние = Перечисления.СостоянияВыполненияОпераций.ОжиданиеПредшествующих
				ИЛИ ДанныеСтроки.Состояние = Перечисления.СостоянияВыполненияОпераций.Выполнена Тогда
			НоваяСтрока = ТаблицаОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
			НоваяСтрока.НомерСтроки  = ТаблицаОпераций.Количество() -1;
			НоваяСтрока.СтрокаСписка = КлючИЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если ТаблицаОпераций.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Настройки.ДополнительныеСвойства.ИспользоватьСменныеЗадания Тогда
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	Операции.НомерСтроки              КАК НомерСтроки,
		|	Операции.Этап                     КАК Этап,
		|	Операции.Операция                 КАК Операция,
		|	Операции.ИдентификаторОперации    КАК ИдентификаторОперации,
		|	Операции.ПроизводственнаяОперация КАК ПроизводственнаяОперация
		|ПОМЕСТИТЬ ВТОперации
		|ИЗ
		|	&Операции КАК Операции
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Этап,
		|	Операция,
		|	ИдентификаторОперации
		|;
		|
		|ВЫБРАТЬ
		|	Операции.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	ВТОперации КАК Операции
		|ГДЕ
		|	ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Регистр
		|		ГДЕ
		|			Регистр.Подразделение = &Подразделение
		|			И Регистр.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|			И Операции.Этап = Регистр.Этап
		|			И Операции.Операция = Регистр.Операция
		|			И Операции.ИдентификаторОперации = Регистр.ИдентификаторОперации)
		|";
		
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Операции", ТаблицаОпераций);
		Запрос.УстановитьПараметр("Подразделение", Настройки.ДополнительныеСвойства.Подразделение);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаТаблицы = ТаблицаОпераций[Выборка.НомерСтроки];
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ПроизводственнаяОперация) Тогда
				СтрокаСписка  = СтрокаТаблицы.СтрокаСписка;
				СтрокаТаблицы.СостояниеТекст = СтрШаблон(НСтр("ru = '%1 (уже назначена)';
																|en = '%1 (already assigned)'"), СтрокаСписка.Данные.Состояние);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаОпераций Цикл
		СтрокаСписка = СтрокаТаблицы.СтрокаСписка;
		Если ПустаяСтрока(СтрокаТаблицы.СостояниеТекст)
			И СтрокаСписка.Данные.Состояние = Перечисления.СостоянияВыполненияОпераций.Выполнена
			И СтрокаСписка.Данные.ТребуетПовторения Тогда
			СтрокаТаблицы.СостояниеТекст = СтрШаблон(НСтр("ru = '%1 (требует повторения)';
															|en = '%1 (requires repetition)'"), СтрокаСписка.Данные.Состояние);
		КонецЕсли;
		Если НЕ ПустаяСтрока(СтрокаТаблицы.СостояниеТекст) Тогда
			СтрокаСписка.Оформление["Состояние"].УстановитьЗначениеПараметра("Текст", СтрокаТаблицы.СостояниеТекст);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнениеОпераций_ИзменитьОперацию(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы", Ложь);
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПоказатьЗначение(, ВыделенныеОперации.ОперацииСсылки[0]);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_Назначить(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Неназначенные");
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОткрытьФормуНазначениеОпераций(ВыделенныеОперации.КлючиОпераций, "Назначить");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_ПринятьВРаботу(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации(?(ИспользоватьСменныеЗадания, "Документы", ""));
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если ВыделенныеОперации.ДанныеСтрок.Количество() <> ВыделенныеОперации.Счетчики.МожноПринятьВРаботу Тогда
		ПоказатьПредупреждение(,
			РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена(ВыделенныеОперации.КлючиОпераций));
	ИначеЕсли ВыделенныеОперации.Счетчики.Неназначенные > 0 Тогда
		ОткрытьФормуНазначениеОпераций(
			ВыделенныеОперации.КлючиОпераций, "ПринятьВРаботу");
	Иначе
		ОперативныйУчетПроизводстваКлиент.УстановитьСтатусПроизводственнойОперации(
			ВыделенныеОперации.ОперацииСсылки, "Выполняется", Подразделение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_ОтметитьВыполнение(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации(?(ИспользоватьСменныеЗадания, "Документы", ""));
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если ВыделенныеОперации.ДанныеСтрок.Количество() <> ВыделенныеОперации.Счетчики.МожноОтметитьВыполнение Тогда
		ПоказатьПредупреждение(,
			РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена(ВыделенныеОперации.КлючиОпераций));
	ИначеЕсли ВыделенныеОперации.Счетчики.Неназначенные > 0 Тогда
		ОткрытьФормуНазначениеОпераций(
			ВыделенныеОперации.КлючиОпераций, "ОтметитьВыполнение");
	Иначе
		ОперативныйУчетПроизводстваКлиент.УстановитьСтатусПроизводственнойОперации(
			ВыделенныеОперации.ОперацииСсылки, "Выполнена", Подразделение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_НеВыполнена(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы");
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	НовыйСтатус = "НеВыполнена";
	
	Для каждого ДанныеСтроки Из ВыделенныеОперации.ДанныеСтрок Цикл
		ПараметрыПодтверждения = ОперативныйУчетПроизводстваКлиент.ПодтверждениеПриУстановкеСтатусаОперации(
			ДанныеСтроки, НовыйСтатус, ВыделенныеОперации.Количество() > 0);
		Если ПараметрыПодтверждения <> Неопределено Тогда
			ПараметрыПодтверждения.Вставить("КлючиОпераций", ВыделенныеОперации.КлючиОпераций);
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПодтверждениеПриУстановкеСтатусаОперацийЗавершение", ЭтотОбъект, ПараметрыПодтверждения);
			ПоказатьВопрос(ОписаниеОповещения, ПараметрыПодтверждения.ТекстВопроса, ПараметрыПодтверждения.РежимДиалогаВопрос);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	СформироватьУстановитьСтатусОпераций(ВыделенныеОперации.КлючиОпераций, НовыйСтатус);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_Пропустить(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации(?(ИспользоватьСменныеЗадания, "Документы", ""));
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	НовыйСтатус = "Пропущена";
	ПараметрыПодтверждения = Неопределено;
	
	Для каждого ДанныеСтроки Из ВыделенныеОперации.ДанныеСтрок Цикл
		Если НЕ ДанныеСтроки.МожноПропустить
			ИЛИ (НЕ ЗначениеЗаполнено(ДанныеСтроки.ПроизводственнаяОперация)
				И ДанныеСтроки.Состояние <> ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.КВыполнению")) Тогда
			ПоказатьПредупреждение(,
				РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена(ВыделенныеОперации.КлючиОпераций));
			Возврат;
		КонецЕсли;
		Если ПараметрыПодтверждения = Неопределено Тогда
			ПараметрыПодтверждения = ОперативныйУчетПроизводстваКлиент.ПодтверждениеПриУстановкеСтатусаОперации(
				ДанныеСтроки, НовыйСтатус, ВыделенныеОперации.Количество() > 0);
		КонецЕсли;
	КонецЦикла;
	
	Если ПараметрыПодтверждения <> Неопределено Тогда
		ПараметрыПодтверждения.Вставить("КлючиОпераций", ВыделенныеОперации.КлючиОпераций);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПодтверждениеПриУстановкеСтатусаОперацийЗавершение", ЭтотОбъект, ПараметрыПодтверждения);
		ПоказатьВопрос(ОписаниеОповещения, ПараметрыПодтверждения.ТекстВопроса, ПараметрыПодтверждения.РежимДиалогаВопрос);
		Возврат;
	КонецЕсли;
	
	СформироватьУстановитьСтатусОпераций(ВыделенныеОперации.КлючиОпераций, НовыйСтатус);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_ИзменитьТехпроцесс(Команда)
	
	ТекущиеДанные = Элементы.ВыполнениеОпераций.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Этап", ТекущиеДанные.Этап);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуРедактированияТехпроцессаЗавершение",
		ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.РедакторПроизводственногоПроцесса.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		ТекущиеДанные.Этап,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_НазначитьРабочийЦентр(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы");
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекВидРабочегоЦентра = Неопределено;
	ТекУчасток           = Неопределено;
	
	Для каждого ДанныеСтроки Из ВыделенныеОперации.ДанныеСтрок Цикл
			
		Если ЗначениеЗаполнено(ДанныеСтроки.ВидРабочегоЦентра) Тогда
			
			Если ТекВидРабочегоЦентра = Неопределено Тогда
				
				ТекВидРабочегоЦентра = ДанныеСтроки.ВидРабочегоЦентра;
				
			ИначеЕсли ТекВидРабочегоЦентра <> ДанныеСтроки.ВидРабочегоЦентра Тогда
				
				ПоказатьПредупреждение(, НСтр("ru = 'Выбраны операции с различными видами рабочих центров.';
												|en = 'Operations with different work center types are selected.'"));
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеСтроки.Участок) Тогда
			
			Если ТекУчасток = Неопределено Тогда
				
				ТекУчасток = ДанныеСтроки.Участок;
				
			ИначеЕсли ТекУчасток <> ДанныеСтроки.Участок Тогда
				
				ПоказатьПредупреждение(, НСтр("ru = 'Выбраны операции с различными участками.';
												|en = 'Operations with different areas are selected.'"));
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Подразделение", Подразделение);
	Если ТекУчасток <> Неопределено Тогда
		Отбор.Вставить("Участок", ТекУчасток);
	КонецЕсли;
	Если ТекВидРабочегоЦентра <> Неопределено Тогда
		Отбор.Вставить("ВидРабочегоЦентра", ТекВидРабочегоЦентра);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("ВыбиратьВариантНаладки", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Операции", ВыделенныеОперации.ОперацииСсылки);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОперацииНазначитьРабочийЦентрЗавершение",
		ЭтотОбъект, ДопПараметры);
	
	ОткрытьФорму("Справочник.РабочиеЦентры.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца); 
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_ОтменитьНазначениеРабочегоЦентра(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы");
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ОперацииНазначитьРабочийЦентрНаСервере(
		ВыделенныеОперации.ОперацииСсылки,
		ПредопределенноеЗначение("Справочник.РабочиеЦентры.ПустаяСсылка"));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнениеОпераций_Проконтролировать(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы", Ложь);
	
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = ВыделенныеОперации.ДанныеСтрок[0];
	
	Если ДанныеСтроки.КоличествоНаКонтроле = 0 Тогда
		ПоказатьПредупреждение(,
			РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена());
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПроизводственнаяОперация", ДанныеСтроки.ПроизводственнаяОперация);
	
	ПараметрыОповещения = Новый Структура("ПроизводственнаяОперация", ДанныеСтроки.ПроизводственнаяОперация);
	Оповещение = Новый ОписаниеОповещения("ПроконтролироватьЗавершение", ЭтотОбъект, ПараметрыОповещения);
	
	ОткрытьФорму("Документ.ПроизводственнаяОперация2_2.Форма.КонтрольОперации",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	ВыделенныеОперации = ПроверитьПолучитьВыделенныеВСпискеОперации("Документы");
		
	Если ВыделенныеОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, ВыделенныеОперации.ОперацииСсылки);
	
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.ВыполнениеОпераций, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.ВыполнениеОпераций);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Элементы.ВыполнениеОпераций);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)
	
	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыполнениеОпераций.Имя);
	
	ГруппаИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВыполнениеОпераций.НачатыПредшествующие");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	ОтборЭлемента = ГруппаИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВыполнениеОпераций.ОжиданиеПредшествующих");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВыполнениеОперацийВремяЕдИзм.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВыполнениеОпераций.ВремяВыполнения");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);

КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ИспользоватьУчастки = ПроизводствоСервер.ИспользоватьУчасткиПриПооперационномУправлении();
	
	Элементы.ОтборПодразделениеЗаголовок.Видимость = НЕ ИспользоватьУчастки;
	Элементы.ТипОтбораПодразделение.Видимость = ИспользоватьУчастки;
	
	ПравоДобавлениеОпераций = ПравоДоступа("Добавление", Метаданные.Документы.ПроизводственнаяОперация2_2);
	ПравоИзменениеОпераций  = ПравоДоступа("Изменение", Метаданные.Документы.ПроизводственнаяОперация2_2);
	
	Элементы.ГруппаКомандыВыполнения.Доступность           = ПравоДобавлениеОпераций И ПравоИзменениеОпераций;
	Элементы.ВыполнениеОперацийГруппаДействие1.Доступность = ПравоДобавлениеОпераций И ПравоИзменениеОпераций;
	Элементы.ВыполнениеОперацийГруппаДействие2.Доступность = ПравоДобавлениеОпераций И ПравоИзменениеОпераций;
	Элементы.ГруппаКомандыРабочийЦентр.Доступность         = ПравоДобавлениеОпераций И ПравоИзменениеОпераций;
	
	ИспользоватьКонтрольВыполненияОпераций = ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольВыполненияОпераций");
	Элементы.ПереходКонтрольВыполненияОпераций.Видимость = ИспользоватьКонтрольВыполненияОпераций;
	
	ИспользуетсяУчетТрудозатратВРазрезеСотрудников = ПроизводствоСервер.ИспользуетсяУчетТрудозатратВРазрезеСотрудников(ТекущаяДатаСеанса());
	
	УстановитьКлючСохраненияПоложенияОкна();
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("Подразделение") Тогда
		
		Элементы.ВыполнениеОперацийСменноеЗаданиеДата.Видимость      = Форма.ИспользоватьСменныеЗадания;
		Элементы.ВыполнениеОперацийСменноеЗадание.Видимость          = Форма.ИспользоватьСменныеЗадания;
		
		Элементы.ОтборСменаЗаголовок.Видимость                       = Форма.ИспользоватьСменныеЗадания;
		Элементы.ОтборСмена.Видимость                                = Форма.ИспользоватьСменныеЗадания;
		Элементы.БыстрыйОтбор_КНазначению.Видимость                  = Форма.ИспользоватьСменныеЗадания;
		
		Элементы.ВыполнениеОпераций_Назначить.Видимость              = НЕ Форма.ИспользоватьСменныеЗадания;
		Элементы.ВыполнениеОперацийДействие_Назначить.Видимость      = НЕ Форма.ИспользоватьСменныеЗадания;
		
	КонецЕсли;
	
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("ТипОтбораПодразделение") Тогда
		
		Элементы.ОтборПодразделение.Видимость = (Форма.ТипОтбораПодразделение = 0);
		Элементы.ОтборУчасток.Видимость       = (Форма.ТипОтбораПодразделение = 1);
		
		Связи = Новый Массив;
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Подразделение", "Подразделение"));
		Если Форма.ТипОтбораПодразделение = 1 Тогда
			Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Участок", "Участок"));
		КонецЕсли;
		
		Элементы.ОтборРабочийЦентр.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		Элементы.ОтборСмена.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
		
	КонецЕсли;
	
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("Состояние") Тогда
		
		УстановитьСостояниеГиперссылки(Форма, Элементы.БыстрыйОтбор_Незавершенные, Ложь);
		УстановитьСостояниеГиперссылки(Форма, Элементы.БыстрыйОтбор_Завершенные, Ложь);
		
		Если ТолькоНезавершенныеСостояния(Форма) Тогда
			Форма.СостоянияСтрока = СостоянияНезавершенныеСтрока();
			УстановитьСостояниеГиперссылки(Форма, Элементы.БыстрыйОтбор_Незавершенные, Истина);
		ИначеЕсли ТолькоЗавершенныеСостояния(Форма) Тогда
			Форма.СостоянияСтрока = СостоянияЗавершенныеСтрока();
			УстановитьСостояниеГиперссылки(Форма, Элементы.БыстрыйОтбор_Завершенные, Истина);
		Иначе
			Форма.СостоянияСтрока = СтрСоединить(Форма.Состояния.ВыгрузитьЗначения(), ";");
		КонецЕсли;
		
		Для каждого Отбор Из ГруппаБыстрыхОтборов(Форма, Форма.ИспользоватьСменныеЗадания) Цикл // ОтборКомпоновкиДанных
			Для каждого ЭлементОтбора Из Отбор.Элементы Цикл
				УстановитьСостояниеГиперссылки(Форма, Элементы[ЭлементОтбора.Представление], ЭлементОтбора.Использование);
			КонецЦикла;
		КонецЦикла;
		
		УстановитьСостояниеГиперссылки(Форма, Элементы.БыстрыйОтбор_Все, ТекущийОтборВсе(Форма));
		
	КонецЕсли;
		
	Если Инициализация ИЛИ СтруктураРеквизитов.Свойство("ВариантНаладки") Тогда
		
		ПараметрыВыбора = Новый Массив();
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Форма.ВидРабочегоЦентра));
		
		Элементы.ГруппаОтборВариантНаладки.Видимость = Форма.ИспользуютсяВариантыНаладки;
		Элементы.ОтборВариантНаладки.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
			
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("ИнформационноеСообщение") Тогда
		
		ЗаполненоИнформационноеСообщение = ЗначениеЗаполнено(Форма.ИнформационноеСообщение);
		
		Элементы.ИнформационноеСообщение.Видимость = ЗаполненоИнформационноеСообщение;
		
	КонецЕсли;
	
КонецПроцедуры

#Область СвойстваСпискаВыполнениеОпераций

&НаСервере
Процедура УстановитьСвойстваДинамическогоСпискаВыполнениеОпераций()

	Если СтрНайти(ВыполнениеОпераций.ТекстЗапроса, "&ПредставлениеЭтапа") <> 0
		ИЛИ СтрНайти(ВыполнениеОпераций.ТекстЗапроса, "&Участок") <> 0
			И ТипОтбораПодразделение = 0
		ИЛИ СтрНайти(ВыполнениеОпераций.ТекстЗапроса, "&Участок") = 0
			И ТипОтбораПодразделение = 1 Тогда
			
		ТекстЗапроса = ТекстЗапросаСпискаВыполнениеОпераций();
		
		Документы.ЭтапПроизводства2_2.ВыполнитьПодстановкуПоляПредставлениеЭтапа(
			ТекстЗапроса, "&ПредставлениеЭтапа", "Очередь.Этап");
		
		СвойстваСписка              = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
		СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
		ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.ВыполнениеОпераций, СвойстваСписка);
		
		ВыполнениеОпераций.Параметры.УстановитьЗначениеПараметра(
			"ИспользуетсяГрафикПроизводства",
			УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства());
			
		ВыполнениеОпераций.Параметры.УстановитьЗначениеПараметра(
			"СтатусРабочийГрафик",
			РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
			
	КонецЕсли;
	
	ВыполнениеОперацийСоздатьЭлементыОтбора();
	
	ВыполнениеОперацийСоздатьЭлементыПорядка();

КонецПроцедуры

&НаСервере
Функция ТекстЗапросаСпискаВыполнениеОпераций()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Очередь.Распоряжение                                           КАК Распоряжение,
	|	Очередь.Этап                                                   КАК Этап,
	|	Очередь.Операция                                               КАК Операция,
	|	Очередь.ИдентификаторОперации                                  КАК ИдентификаторОперации,
	|	Очередь.ПроизводственнаяОперация                               КАК ПроизводственнаяОперация,
	|	Очередь.ПроизводственнаяОперация.Номер                         КАК ПроизводственнаяОперацияНомер,
	|	Очередь.ПроизводственнаяОперация.Статус                        КАК ПроизводственнаяОперацияСтатус,
	|	Очередь.ПроизводственнаяОперация.НаОснованииНСИ                КАК ПроизводственнаяОперацияНаОснованииНСИ,
	|	Очередь.ПроизводственнаяОперация.ОперацияКакРаспоряжениеВыработки КАК ПроизводственнаяОперацияКакРаспоряжениеВыработки,
	|	Очередь.ПроизводственнаяОперация <> ЗНАЧЕНИЕ(Документ.ПроизводственнаяОперация2_2.ПустаяСсылка) КАК ПроизводственнаяОперацияНазначена,
	|	Очередь.Состояние                                              КАК Состояние,
	|	Очередь.Исполнитель                                            КАК Исполнитель,
	|	Очередь.СменноеЗадание                                         КАК СменноеЗадание,
	|	Очередь.СменноеЗадание.Дата                                    КАК СменноеЗаданиеДата,
	|	Очередь.НомерОперации                                          КАК НомерОперации,
	|	Очередь.НомерСледующейОперации                                 КАК НомерСледующейОперации,
	|
	|	Очередь.Запланировано                                          КАК КоличествоПлан,
	|	Очередь.Выполняется + Очередь.НаКонтроле + Очередь.НаДоработке КАК КоличествоВРаботе,
	|	Очередь.Выполнено                                              КАК КоличествоГотово,
	|	Очередь.Брак                                                   КАК КоличествоБрак,
	|	Очередь.НаКонтроле                                             КАК КоличествоНаКонтроле,
	|	Очередь.НаДоработке                                            КАК КоличествоНаДоработке,
	|	Очередь.МожноВыполнять                                         КАК МожноВыполнять,
	|	Очередь.ОжиданиеПредшествующих                                 КАК ОжиданиеПредшествующих,
	|	Очередь.НачатыПредшествующие                                   КАК НачатыПредшествующие,
	|
	|	ВЫБОР
	|		КОГДА Очередь.РабочийЦентр ССЫЛКА Справочник.ВидыРабочихЦентров
	|			ТОГДА Очередь.РабочийЦентр
	|		ИНАЧЕ
	|			Очередь.РабочийЦентр.ВидРабочегоЦентра
	|	КОНЕЦ                                                          КАК ВидРабочегоЦентра,
	|	ВЫБОР
	|		КОГДА Очередь.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА Очередь.РабочийЦентр
	|		ИНАЧЕ
	|			ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|	КОНЕЦ                                                          КАК РабочийЦентр,
	|	ВЫБОР
	|		КОГДА Очередь.ПроизводственнаяОперация <> ЗНАЧЕНИЕ(Документ.ПроизводственнаяОперация2_2.ПустаяСсылка)
	|			ТОГДА Очередь.ПроизводственнаяОперация.Участок
	|		КОГДА Очередь.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|				И НЕ Очередь.РабочийЦентр = ЗНАЧЕНИЕ(Справочник.РабочиеЦентры.ПустаяСсылка)
	|			ТОГДА ВЫРАЗИТЬ(Очередь.РабочийЦентр КАК Справочник.РабочиеЦентры).Участок
	|		ИНАЧЕ
	|			ЕСТЬNULL(Очередь.ПроизводственнаяОперация.Участок, Очередь.Операция.Участок)
	|	КОНЕЦ                                                                            КАК Участок,
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.МожноПропустить, Очередь.Операция.МожноПропустить)    КАК МожноПропустить,
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.МожноПовторить, Очередь.Операция.МожноПовторить)      КАК МожноПовторить,
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.Наименование, Очередь.Операция.Наименование)          КАК НаименованиеОперации,
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.ЕдиницаИзмерения, Очередь.Операция.ЕдиницаИзмерения)  КАК ЕдиницаИзмерения,
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.ВариантНаладки, Очередь.Операция.ВариантНаладки)      КАК ВариантНаладки,
	|
	|	ЕСТЬNULL(Очередь.ПроизводственнаяОперация.ТребуетПовторения, ЛОЖЬ) КАК ТребуетПовторения,
	|
	|	&ПредставлениеЭтапа                                            КАК ПредставлениеЭтапа,
	|	Очередь.Этап.ПартияПроизводства.ОсновноеИзделиеНоменклатура    КАК Номенклатура,
	|	Очередь.Этап.ПартияПроизводства.ОсновноеИзделиеХарактеристика  КАК Характеристика,
	|
	|	Очередь.ВремяОбщее                                             КАК ВремяВыполнения,
	|	Очередь.ВремяЕдИзм                                             КАК ВремяЕдИзм,
	|	Очередь.Порядок                                                КАК Порядок,
	|	График.Начало                                                  КАК НачалоЭтапа,
	|	График.НачалоСледующегоЭтапа                                   КАК НачалоСледующегоЭтапа,
	|	ВЫБОР
	|		КОГДА &ИспользуетсяГрафикПроизводства
	|				И НЕ График.Этап ЕСТЬ NULL
	|			ТОГДА График.Начало
	|		КОГДА НормативныйГрафикСтруктурыЗаказа.Начало ЕСТЬ НЕ NULL
	|			ТОГДА НормативныйГрафикСтруктурыЗаказа.Начало
	|		ИНАЧЕ ДОБАВИТЬКДАТЕ(Очередь.Распоряжение.НачатьНеРанее, СЕКУНДА, ЕСТЬNULL(НормативныйГрафик.ДлительностьДоЗапуска, 0))
	|	КОНЕЦ                                                                                 КАК ДатаНачала,
	|	ЕСТЬNULL(НормативныйГрафик.ДлительностьДоВыпуска, Очередь.Этап.ДлительностьДоВыпуска) КАК ДлительностьДоВыпуска
	|ИЗ
	|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
	|		ПО Очередь.Распоряжение = График.ЗаказНаПроизводство
	|			И Очередь.Этап = График.Этап
	|			И (График.СтатусГрафика = &СтатусРабочийГрафик)
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикЭтаповПроизводства КАК НормативныйГрафик
	|		ПО Очередь.Этап = НормативныйГрафик.ЭтапПроизводства
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НормативныйГрафикСтруктурыЗаказа КАК НормативныйГрафикСтруктурыЗаказа
	|		ПО Очередь.Этап = НормативныйГрафикСтруктурыЗаказа.Этап
	|
	|ГДЕ
	|	НЕ Очередь.ВАрхиве
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура ВыполнениеОперацийСоздатьЭлементыОтбора()
	
	ЭлементыОтбора = ВыполнениеОпераций.КомпоновщикНастроек.Настройки.Отбор.Элементы;
	ЭлементыОтбора.Очистить();
	
	ГруппаОтборовКорневая = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭлементыОтбора,,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборовКорневая,
		"Состояние",
		ВидСравненияКомпоновкиДанных.ВСписке,,
		"Отбор_Состояние",
		Ложь);
		
	ГруппаОтборовБезСЗ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовКорневая,
		"БыстрыйОтбор_БезСЗ",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	ГруппаОтборовСЗ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовКорневая,
		"БыстрыйОтбор_СЗ",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	// Без СЗ
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовБезСЗ,
		Элементы.БыстрыйОтбор_КВыполнению.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	СписокСостояний = Новый СписокЗначений();
	СписокСостояний.Добавить(Перечисления.СостоянияВыполненияОпераций.КВыполнению);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"Состояние",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокСостояний);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовБезСЗ,
		Элементы.БыстрыйОтбор_ВРаботе.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоВРаботе",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовБезСЗ,
		Элементы.БыстрыйОтбор_НаКонтроле.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоНаКонтроле",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовБезСЗ,
		Элементы.БыстрыйОтбор_НаДоработке.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоНаДоработке",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовБезСЗ,
		Элементы.БыстрыйОтбор_Брак.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоБрак",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
	
	// СЗ
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_КНазначению.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	СписокСостояний = Новый СписокЗначений();
	СписокСостояний.Добавить(Перечисления.СостоянияВыполненияОпераций.КНазначению);
	СписокСостояний.Добавить(Перечисления.СостоянияВыполненияОпераций.ОжиданиеПредшествующих);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"Состояние",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокСостояний);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"ПроизводственнаяОперация",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_КВыполнению.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	СписокСостояний = Новый СписокЗначений();
	СписокСостояний.Добавить(Перечисления.СостоянияВыполненияОпераций.Назначена);
	СписокСостояний.Добавить(Перечисления.СостоянияВыполненияОпераций.КВыполнению);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"Состояние",
		ВидСравненияКомпоновкиДанных.ВСписке,
		СписокСостояний);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_ВРаботе.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоВРаботе",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_НаКонтроле.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоНаКонтроле",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_НаДоработке.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоНаДоработке",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ГруппаОтборовСЗ,
		Элементы.БыстрыйОтбор_Брак.Имя,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтборов,
		"КоличествоБрак",
		ВидСравненияКомпоновкиДанных.Больше,
		0);
		
	СброситьБыстрыеОтборы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнениеОперацийСоздатьЭлементыПорядка()
	
	ЭлементыПорядка = ВыполнениеОпераций.Порядок.Элементы;
	ЭлементыПорядка.Очистить();
	
	Если УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства() Тогда
	
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("Порядок");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НачалоЭтапа");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НачалоСледующегоЭтапа");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НомерОперации");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НаименованиеОперации");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("ПроизводственнаяОперацияНазначена");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("ПроизводственнаяОперация.Дата");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
	Иначе
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("Порядок");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("Распоряжение.Приоритет");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("Распоряжение.Подразделение.РеквизитДопУпорядочивания");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("Распоряжение.Очередь");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("ДлительностьДоВыпуска");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НомерОперации");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("НаименованиеОперации");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("ПроизводственнаяОперацияНазначена");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Убыв;
		ПорядокСписка.Использование = Истина;
		
		ПорядокСписка = ЭлементыПорядка.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ПорядокСписка.Поле = Новый ПолеКомпоновкиДанных("ПроизводственнаяОперация.Дата");
		ПорядокСписка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ПорядокСписка.Использование = Истина;
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Элементы.ВыполнениеОпераций.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область ОтборыОбщие

&НаСервере
Процедура ПодразделениеПриИзмененииНаСервере(ИзмененТипОтбора)
	
	ПрочитатьПараметрыПодразделения();
	
	УстановитьОтборПоСостояниюПриИзмененииПодразделения(ЭтотОбъект);
	
	УстановитьТипРабочегоЦентра(ЭтотОбъект);
	УстановитьТипИсполнителя();
	
	УстановитьОтборПоПодразделениюУчастку();
	УстановитьОтборПоРабочемуЦентру();
	УстановитьОтборПоИсполнителю(ЭтотОбъект);
	УстановитьОтборПоСменномуЗаданию(ЭтотОбъект);
	
	УстановитьКлючСохраненияПоложенияОкна();
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Подразделение,ВариантНаладки,Состояние" + ?(ИзмененТипОтбора, ",ТипОтбораПодразделение", ""));
	
КонецПроцедуры

&НаСервере
Процедура УчастокПриИзмененииНаСервере(ИзмененТипОтбора)
	
	Подразделение = ?(Участок.Пустая(),
		Неопределено,
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Участок, "Владелец"));
		
	ПодразделениеПриИзмененииНаСервере(ИзмененТипОтбора);
	
КонецПроцедуры

&НаСервере
Процедура РабочийЦентрПриИзмененииНаСервере()
	
	ЗаполнитьСлужебныеРеквизитыРабочегоЦентра();
	
	УстановитьОтборПоРабочемуЦентру();
	
	ВариантНаладки = ПредопределенноеЗначение("Справочник.ВариантыНаладки.ПустаяСсылка");
	УстановитьОтборПоВариантуНаладки(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ВариантНаладки");
	
КонецПроцедуры

&НаСервере
Процедура ИсполнительПриИзмененииНаСервере()
	
	Если ИспользуетсяУчетТрудозатратВРазрезеСотрудников Тогда
	
		ИсполнительФизЛицо = ?(ЗначениеЗаполнено(Исполнитель),
			ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Исполнитель, "ФизическоеЛицо"),
			Неопределено);
		
	КонецЕсли;
	
	УстановитьОтборПоИсполнителю(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыРабочегоЦентра()
	
	Если НЕ ЗначениеЗаполнено(РабочийЦентр) Тогда
		ВидРабочегоЦентра = Справочники.ВидыРабочихЦентров.ПустаяСсылка();
		ИспользуютсяВариантыНаладки = Ложь;
	ИначеЕсли ТипОтбораРабочийЦентр = 1 Тогда
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РабочийЦентр,
			"ВидРабочегоЦентра,ВидРабочегоЦентра.ИспользуютсяВариантыНаладки");
		ВидРабочегоЦентра = ЗначенияРеквизитов.ВидРабочегоЦентра;
		ИспользуютсяВариантыНаладки = ЗначенияРеквизитов.ВидРабочегоЦентраИспользуютсяВариантыНаладки;
	Иначе
		ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РабочийЦентр,
			"ИспользуютсяВариантыНаладки");
		ВидРабочегоЦентра = РабочийЦентр;
		ИспользуютсяВариантыНаладки = ЗначенияРеквизитов.ИспользуютсяВариантыНаладки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоИсполнителю(Форма)
	
	ОтборПоИсполнителю = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
		Форма.ВыполнениеОпераций.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
		НСтр("ru = 'Отбор по исполнителю';
			|en = 'Filter by assignee'"),
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
		
	Если Форма.ИспользуетсяУчетТрудозатратВРазрезеСотрудников Тогда
		
		Исполнители = Новый Массив;
		Исполнители.Добавить(Форма.Исполнитель);
		Исполнители.Добавить(Форма.ИсполнительФизЛицо);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ОтборПоИсполнителю,
			"ПроизводственнаяОперация.Исполнитель",
			ВидСравненияКомпоновкиДанных.ВСписке,
			Исполнители,
			,
			ЗначениеЗаполнено(Форма.Исполнитель));
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ОтборПоИсполнителю,
			"ПроизводственнаяОперация.Трудозатраты.Исполнитель",
			ВидСравненияКомпоновкиДанных.ВСписке,
			Исполнители,
			,
			ЗначениеЗаполнено(Форма.Исполнитель));
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ОтборПоИсполнителю,
			"ПроизводственнаяОперация.Исполнитель",
			ВидСравненияКомпоновкиДанных.Равно,
			Форма.Исполнитель,
			,
			ЗначениеЗаполнено(Форма.Исполнитель));
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ОтборПоИсполнителю,
			"ПроизводственнаяОперация.Трудозатраты.Исполнитель",
			ВидСравненияКомпоновкиДанных.Равно,
			Форма.Исполнитель,
			,
			ЗначениеЗаполнено(Форма.Исполнитель));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСменномуЗаданию(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.ВыполнениеОпераций,
		"СменноеЗадание",
		Форма.Смена,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Форма.Смена));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоЭтапам(СписокЭтапов = Неопределено)
	
	УстановленОтбор = ЗначениеЗаполнено(СписокЭтапов);
	
	Если УстановленОтбор Тогда
		
		ЗначениеОтбора  = ?(СписокЭтапов.Количество() = 1, СписокЭтапов[0], СписокЭтапов);
		ВидСравненияСКД = ?(СписокЭтапов.Количество() = 1,
			ВидСравненияКомпоновкиДанных.Равно,
			ВидСравненияКомпоновкиДанных.ВСписке);
			
	Иначе
		
		ЗначениеОтбора = Неопределено;
		ВидСравненияСКД = ВидСравненияКомпоновкиДанных.Равно;
		
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ВыполнениеОпераций,
			"Этап",
			ЗначениеОтбора,
			ВидСравненияСКД,
			,
			УстановленОтбор);
	
	Если УстановленОтбор Тогда
		СформироватьНадписьУстановленОтборПоЭтапам(СписокЭтапов);
	Иначе
		ОчиститьИнформационноеСообщение();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоРабочемуЦентру()
	
	УстановленОтбор = ЗначениеЗаполнено(РабочийЦентр);
	
	Если ТипОтбораРабочийЦентр = 1 Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ВыполнениеОпераций,
				"РабочийЦентр",
				РабочийЦентр, 
				ВидСравненияКомпоновкиДанных.Равно,
				,
				УстановленОтбор);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ВыполнениеОпераций,
				"ВидРабочегоЦентра",
				,
				,
				,
				Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ВыполнениеОпераций,
				"РабочийЦентр",
				,
				,
				,
				Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				ВыполнениеОпераций,
				"ВидРабочегоЦентра",
				РабочийЦентр,
				ВидСравненияКомпоновкиДанных.Равно,
				, 
				УстановленОтбор);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоВариантуНаладки(Форма)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.ВыполнениеОпераций,
		"ВариантНаладки",
		Форма.ВариантНаладки,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Форма.ВариантНаладки));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПодразделениюУчастку()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ВыполнениеОпераций,
		"Подразделение",
		Подразделение,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ВыполнениеОпераций,
		"Участок",
		Участок,
		ВидСравненияКомпоновкиДанных.Равно,,
		ТипОтбораПодразделение = 1);
		
	ПараметрыФО = Новый Структура(
		"Подразделение",
		?(ЗначениеЗаполнено(Подразделение), Подразделение, Неопределено));
	УстановитьПараметрыФункциональныхОпцийФормы(ПараметрыФО);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТипРабочегоЦентра(Форма)
	
	Если Форма.ТипОтбораРабочийЦентр = 0 Тогда
		ДопустимыйТип = Новый ОписаниеТипов("СправочникСсылка.ВидыРабочихЦентров");
	Иначе
		ДопустимыйТип = Новый ОписаниеТипов("СправочникСсылка.РабочиеЦентры");
	КонецЕсли;
	
	Форма.РабочийЦентр = ДопустимыйТип.ПривестиЗначение(Форма.РабочийЦентр);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипИсполнителя()
	
	УправлениеПроизводствомКлиентСервер.УстановитьТипИсполнителя(Исполнитель,
		ИспользоватьБригадныеНаряды,
		ИспользуетсяУчетТрудозатратВРазрезеСотрудников);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНачалоВыбораЗавершение(Результат, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		Исполнитель = Результат;
		УстановитьОтборПоИсполнителю(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИсполнительПолучениеДанныхВыбора(ДанныеВыбора, Текст, Подразделение)
	
	ДанныеВыбора = Новый СписокЗначений;
	ПараметрыОтбора = Новый Структура("Организация, Подразделение, Дата");
	ПараметрыОтбора.Подразделение = Подразделение;
	ПараметрыОтбора.Дата = ТекущаяДатаСеанса();
	ПроизводствоСервер.ЗаполнитьДанныеВыбораПриВводеИсполнителя(ДанныеВыбора, Текст, ПараметрыОтбора);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьНадписьУстановленОтборПоЭтапам(СписокЭтапов)
	
	МассивСтрок = Новый Массив;
	
	Если СписокЭтапов.Количество() > 1 Тогда
		
		МассивСтрок.Добавить(НСтр("ru = 'Установлен отбор по списку этапов';
									|en = 'Filter by stage list is set'"));
		
	Иначе
		
		РеквизитыЭтапа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СписокЭтапов[0], "Номер, НаименованиеЭтапа"); 
		ПредставлениеЭтапа = Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(РеквизитыЭтапа);
		
		МассивСтрок.Добавить(НСтр("ru = 'Установлен отбор по этапу';
									|en = 'Stage filter is set'"));
		МассивСтрок.Добавить(" ");
		МассивСтрок.Добавить(Новый ФорматированнаяСтрока(ПредставлениеЭтапа,,,, ПолучитьНавигационнуюСсылку(СписокЭтапов[0])));
		
	КонецЕсли;
	
	МассивСтрок.Добавить(" (");
	МассивСтрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'отключить';
															|en = 'disable'"),,,, "ОтключитьОтборПоЭтапу"));
	МассивСтрок.Добавить(") ");
	
	ИнформационноеСообщение = Новый ФорматированнаяСтрока(МассивСтрок);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ИнформационноеСообщение");
	
КонецПроцедуры

#КонецОбласти

#Область ОтборыБыстрые

&НаКлиентеНаСервереБезКонтекста
Функция ТекущийОтборВсе(Форма)
	
	Возврат Форма.Состояния.Количество() = 0
		И БыстрыеОтборыСброшены(Форма);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСостоянию(Форма, СброситьБыстрыеОтборы = Неопределено)
	
	Состояния = Форма.Состояния;
	
	Если СброситьБыстрыеОтборы <> Неопределено Тогда
		
		СброситьБыстрыеОтборы(Форма, СброситьБыстрыеОтборы);
		
	КонецЕсли;
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		Форма.ВыполнениеОпераций.КомпоновщикНастроек.Настройки.Отбор,, "Отбор_Состояние");
	Если ГруппаОтборов <> Неопределено И ГруппаОтборов.Количество() > 0 Тогда
		ЭлементОтбора = ГруппаОтборов[0];
		ЭлементОтбора.Использование = Состояния.Количество() > 0;
		ЭлементОтбора.ПравоеЗначение = Состояния;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(Форма, "Состояние");
	 
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТолькоЗавершенныеСостояния(Форма, Состояния = Неопределено)
	
	Если Состояния = Неопределено Тогда
		Состояния = Форма.Состояния;
	КонецЕсли;
	
	Возврат СпискиСостоянийОдинаковы(Состояния.ВыгрузитьЗначения(), СостоянияЗавершенные(Форма));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТолькоНезавершенныеСостояния(Форма, Состояния = Неопределено)
	
	Если Состояния = Неопределено Тогда
		Состояния = Форма.Состояния;
	КонецЕсли;
	
	Возврат СпискиСостоянийОдинаковы(Состояния.ВыгрузитьЗначения(), СостоянияНезавершенные(Форма));
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СпискиСостоянийОдинаковы(Состояния1, Состояния2)
	
	Если Состояния1.Количество() <> Состояния2.Количество() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ОбщегоНазначенияКлиентСервер.СпискиЗначенийИдентичны(Состояния1, Состояния2);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСостояниюПриИзмененииПодразделения(Форма)
	
	Если Форма.СостоянияСтрока = СостоянияНезавершенныеСтрока() Тогда
		Форма.Состояния.ЗагрузитьЗначения(СостоянияНезавершенные(Форма));
	ИначеЕсли Форма.СостоянияСтрока = СостоянияЗавершенныеСтрока() Тогда
		Форма.Состояния.ЗагрузитьЗначения(СостоянияЗавершенные(Форма));
	ИначеЕсли Форма.Состояния.Количество() > 0 Тогда
		Состояния = Новый Массив;
		Для каждого Состояние Из СостоянияДоступные(Форма) Цикл
			Если Форма.Состояния.НайтиПоЗначению(Состояние) <> Неопределено Тогда
				Состояния.Добавить(Состояние);
			КонецЕсли;
		КонецЦикла;
		Форма.Состояния.ЗагрузитьЗначения(Состояния);
	КонецЕсли;
	
	УстановитьОтборПоСостоянию(Форма, БыстрыеОтборыПереченьНезавершенные());
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСостояниеГиперссылки(Форма, Элемент, Нажата)
	
	Если Нажата Тогда
		Элемент.ЦветФона = Форма.Кэш.ЦветНажатогоОтбора;
	Иначе
		Элемент.ЦветФона = Новый Цвет();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьБыстрыйОтбор(Форма, ИмяОтбора)
	
	Для каждого ГруппаОтборов Из ГруппаБыстрыхОтборов(Форма, Форма.ИспользоватьСменныеЗадания) Цикл
		ГруппаОтборов.Использование = Истина;
		Для каждого ЭлементОтбора Из ГруппаОтборов.Элементы Цикл
			Если ЭлементОтбора.Представление = ИмяОтбора Тогда
				ЭлементОтбора.Использование = НЕ ЭлементОтбора.Использование;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Если ИмяОтбора <> "БыстрыйОтбор_Брак" Тогда
		Форма.Состояния.Очистить();
	КонецЕсли;
	
	УстановитьОтборПоСостоянию(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СброситьБыстрыеОтборы(Форма, ЭлементыОтбора = Неопределено)
	
	Для Ит = 0 По 1 Цикл
		Для каждого ГруппаОтборов Из ГруппаБыстрыхОтборов(Форма, Булево(Ит)) Цикл
			ВсеСброшены = Истина;
			Для каждого ЭлементОтбора Из ГруппаОтборов.Элементы Цикл
				Если Форма.ИспользоватьСменныеЗадания = Булево(Ит)
						И ЭлементыОтбора <> Неопределено
						И ЭлементыОтбора.Найти(ЭлементОтбора.Представление) = Неопределено Тогда
					ВсеСброшены = Ложь;
					Продолжить;
				КонецЕсли;
				ЭлементОтбора.Использование = Ложь;
			КонецЦикла;
			ГруппаОтборов.Использование = НЕ ВсеСброшены;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция БыстрыеОтборыСброшены(Форма)
	
	Для Ит = 0 По 1 Цикл
		Для каждого ГруппаОтборов Из ГруппаБыстрыхОтборов(Форма, Булево(Ит)) Цикл // ГруппаЭлементовОтбораКомпоновкиДанных
			Для каждого ЭлементОтбора Из ГруппаОтборов.Элементы Цикл
				Если ЭлементОтбора.Использование Тогда
					Возврат Ложь;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГруппаБыстрыхОтборов(Форма, ИспользоватьСменныеЗадания)
	
	ГруппаОтборов = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(
		Форма.ВыполнениеОпераций.КомпоновщикНастроек.Настройки.Отбор,,
		?(ИспользоватьСменныеЗадания, "БыстрыйОтбор_СЗ", "БыстрыйОтбор_БезСЗ"));
	
	Возврат ГруппаОтборов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияДоступные(Форма)
	
	Состояния = Новый Массив;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Состояния, СостоянияНезавершенные(Форма));
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Состояния, СостоянияЗавершенные(Форма));
	
	Возврат Состояния;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияНезавершенные(Форма)
	
	Состояния = Новый Массив;
	
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.ОжиданиеПредшествующих"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.КВыполнению"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Выполняется"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.НаКонтроле"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.НаДоработке"));
	
	Если Форма.ИспользоватьСменныеЗадания Тогда
		Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.КНазначению"));
		Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Назначена"));
	КонецЕсли;
	
	Возврат Состояния;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияНезавершенныеСтрока()
	
	Возврат НСтр("ru = 'Незавершенные';
				|en = 'Unfinished'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияЗавершенные(Форма)
	
	Состояния = Новый Массив;
	
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Выполнена"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Пропущена"));
	Состояния.Добавить(ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Отменена"));
	
	Возврат Состояния;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияЗавершенныеСтрока()
	
	Возврат НСтр("ru = 'Завершенные';
				|en = 'Finished'");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция БыстрыеОтборыПереченьКонтроль()
	
	Результат = Новый Массив;
	
	Результат.Добавить("БыстрыйОтбор_НаКонтроле");
	Результат.Добавить("БыстрыйОтбор_НаДоработке");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция БыстрыеОтборыПереченьВРаботе()
	
	Результат = БыстрыеОтборыПереченьКонтроль();
	Результат.Добавить("БыстрыйОтбор_ВРаботе");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция БыстрыеОтборыПереченьНезавершенные()
	
	Результат = БыстрыеОтборыПереченьВРаботе();
	Результат.Добавить("БыстрыйОтбор_КНазначению");
	Результат.Добавить("БыстрыйОтбор_КВыполнению");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция БыстрыеОтборыПереченьВсе()
	
	Результат = БыстрыеОтборыПереченьНезавершенные();
	Результат.Добавить("БыстрыйОтбор_Брак");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДействияСОперациями

// Проверить получить выделенные в списке операции.
// 
// Параметры:
//  Вариант - Строка - допустимый вариант выбора
//  МножественныйВыбор - Булево
// 
// Возвращаемое значение:
//  Неопределено, Структура - Проверить получить выделенные в списке операции:
// * КлючиОпераций - Массив из РегистрСведенийКлючЗаписи.ОчередьПроизводственныхОпераций
// * ДанныеСтрок - Массив из ДанныеФормыСтруктура
// * ОперацииСсылки - Массив из ДокументСсылка.ПроизводственнаяОперация2_2
&НаКлиенте
Функция ПроверитьПолучитьВыделенныеВСпискеОперации(Вариант = "", МножественныйВыбор = Истина)
	
	Результат = Новый Структура;
	Результат.Вставить("КлючиОпераций",  Новый Массив);
	Результат.Вставить("ДанныеСтрок",    Новый Массив);
	Результат.Вставить("ОперацииСсылки", Новый Массив);
	
	Счетчики = Новый Структура;
	Счетчики.Вставить("Обычные", 0);
	Счетчики.Вставить("ОбычныеНазначенные", 0);
	Счетчики.Вставить("ОбычныеНеназначенные", 0);
	Счетчики.Вставить("Индивидуальные", 0);
	Счетчики.Вставить("ИндивидуальныеНазначенные", 0);
	Счетчики.Вставить("ИндивидуальныеНеназначенные", 0);
	Счетчики.Вставить("Назначенные", 0);
	Счетчики.Вставить("Неназначенные", 0);
	Счетчики.Вставить("Документы", 0);
	
	Счетчики.Вставить("МожноПринятьВРаботу", 0);
	Счетчики.Вставить("МожноОтметитьВыполнение", 0);
	
	ВыделенныеСтроки = Элементы.ВыполнениеОпераций.ВыделенныеСтроки;
	
	АктивныеСостояния = ОперативныйУчетПроизводстваКлиентСервер.АктивныеСостоянияОперации();
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбраны операции для выполнения действия';
										|en = 'Operations to execute the action are not selected'"));
		Возврат Неопределено;
		
	ИначеЕсли ВыделенныеСтроки.Количество() > 1 И НЕ МножественныйВыбор Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для нескольких строк.';
										|en = 'Command cannot be executed for multiple lines.'"));
		Возврат Неопределено;
		
	КонецЕсли;
		
	Для каждого КлючОперации Из ВыделенныеСтроки Цикл
		
		Если ТипЗнч(КлючОперации) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Действие не может быть выполнено для строки группировки списка';
											|en = 'Action cannot be executed for the list grouping row'"));
			Возврат Неопределено;
		КонецЕсли;
		
		ДанныеСтроки = Элементы.ВыполнениеОпераций.ДанныеСтроки(КлючОперации);
		
		Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
			Счетчики.Индивидуальные = Счетчики.Индивидуальные + 1;
			Счетчики.Документы = Счетчики.Документы + 1;
			Если ДанныеСтроки.ПроизводственнаяОперацияСтатус = ПредопределенноеЗначение("Перечисление.СтатусыПроизводственныхОпераций.Формируется") Тогда
				Счетчики.Неназначенные = Счетчики.Неназначенные + 1;
				Счетчики.ИндивидуальныеНеназначенные = Счетчики.ИндивидуальныеНеназначенные + 1;
			Иначе
				Счетчики.Назначенные = Счетчики.Назначенные + 1;
				Счетчики.ИндивидуальныеНазначенные = Счетчики.ИндивидуальныеНазначенные + 1;
			КонецЕсли;
		Иначе
			Счетчики.Обычные = Счетчики.Обычные + 1;
			Если ЗначениеЗаполнено(ДанныеСтроки.ПроизводственнаяОперация) Тогда
				Счетчики.ОбычныеНазначенные = Счетчики.ОбычныеНазначенные + 1;
				Счетчики.Назначенные = Счетчики.Назначенные + 1;
				Счетчики.Документы = Счетчики.Документы + 1;
			ИначеЕсли АктивныеСостояния.Найти(ДанныеСтроки.Состояние) <> Неопределено Тогда
				Счетчики.ОбычныеНазначенные = Счетчики.ОбычныеНазначенные + 1;
				Счетчики.Назначенные = Счетчики.Назначенные + 1;
			Иначе
				Счетчики.ОбычныеНеназначенные = Счетчики.ОбычныеНеназначенные + 1;
				Счетчики.Неназначенные = Счетчики.Неназначенные + 1;
			КонецЕсли;
		КонецЕсли;
		
		Если ДанныеСтроки.МожноВыполнять > 0 Тогда
			Счетчики.МожноПринятьВРаботу = Счетчики.МожноПринятьВРаботу + 1;
		КонецЕсли;
		
		Если ДанныеСтроки.МожноВыполнять + ДанныеСтроки.КоличествоВРаботе - ДанныеСтроки.КоличествоНаКонтроле > 0 Тогда
			Счетчики.МожноОтметитьВыполнение = Счетчики.МожноОтметитьВыполнение + 1;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ПроизводственнаяОперация) Тогда
			Если КлючОперации = Элементы.ВыполнениеОпераций.ТекущаяСтрока Тогда
				Результат.ОперацииСсылки.Вставить(0, ДанныеСтроки.ПроизводственнаяОперация);
			Иначе
				Результат.ОперацииСсылки.Добавить(ДанныеСтроки.ПроизводственнаяОперация);
			КонецЕсли;
		КонецЕсли;
		
		Результат.ДанныеСтрок.Добавить(ДанныеСтроки);
		Результат.КлючиОпераций.Добавить(КлючОперации);
		
	КонецЦикла;
		
	Если НЕ ПустаяСтрока(Вариант) Тогда
		
		Если (Вариант = "Документы" И Счетчики.ОбычныеНеназначенные > 0)
			ИЛИ (Вариант = "Индивидуальные" И Счетчики.Обычные > 0)
			ИЛИ (Вариант = "Обычные" И Счетчики.Индивидуальные > 0)
			ИЛИ (Вариант = "Назначенные" И Счетчики.Неназначенные > 0)
			ИЛИ (Вариант = "Неназначенные" И Счетчики.Назначенные > 0)
			ИЛИ (Вариант = "ОдинаковоеНазначение" И Счетчики.Назначенные > 0 И Счетчики.Неназначенные > 0)
			ИЛИ (Вариант = "ОдинаковыйТип" И Счетчики.Индивидуальные > 0 И Счетчики.Обычные > 0) Тогда
			ПоказатьПредупреждение(, РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена(ВыделенныеСтроки));
			Возврат Неопределено;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.Вставить("Счетчики", Счетчики);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПодтверждениеПриУстановкеСтатусаОперацийЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
		
	Если ДополнительныеПараметры.ПодтверждаемоеСобытие = ОперативныйУчетПроизводстваКлиентСервер.Подтверждение_АннулированиеПоказателей()
		И РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		СформироватьУстановитьСтатусОпераций(ДополнительныеПараметры.КлючиОпераций, ДополнительныеПараметры.НовыйСтатус);
		
	ИначеЕсли ДополнительныеПараметры.ПодтверждаемоеСобытие = ОперативныйУчетПроизводстваКлиентСервер.Подтверждение_СозданиеИндивидуальнойОперации() Тогда
		
		Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
			Для каждого КлючОперации Из ДополнительныеПараметры.КлючиОпераций Цикл
				ДанныеСтроки = Элементы.ВыполнениеОпераций.ДанныеСтроки(КлючОперации);
				Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
					ОткрытьФорму("Документ.ПроизводственнаяОперация2_2.Форма.ФормаДокумента",
						Новый Структура("ЗначениеКопирования", ДанныеСтроки.ПроизводственнаяОперация),
						ЭтотОбъект, ДанныеСтроки.ПроизводственнаяОперация);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		СформироватьУстановитьСтатусОпераций(ДополнительныеПараметры.КлючиОпераций, ДополнительныеПараметры.НовыйСтатус);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьУстановитьСтатусОпераций(КлючиОпераций, НовыйСтатус)
	
	ОперацииСоздать = Новый Массив;
	ОперацииСсылки  = Новый Массив;
	
	Для каждого КлючОперации Из КлючиОпераций Цикл
		
		ДанныеСтроки = Элементы.ВыполнениеОпераций.ДанныеСтроки(КлючОперации);
		
		Если ЗначениеЗаполнено(ДанныеСтроки.ПроизводственнаяОперация) Тогда
			ОперацииСсылки.Добавить(ДанныеСтроки.ПроизводственнаяОперация);
		Иначе
			ОперацииСоздать.Добавить(КлючОперации);
		КонецЕсли;
		
	КонецЦикла;
	
	ОчиститьСообщения();
	
	Если ОперацииСоздать.Количество() > 0 Тогда
		ОперативныйУчетПроизводстваКлиент.СформироватьПроизводственныеОперации(
			Подразделение,
			ОперацииСоздать,
			Новый Структура,
			ОперативныйУчетПроизводстваКлиентСервер.СтатусОперацииСсылка(НовыйСтатус));
	КонецЕсли;
	
	Если ОперацииСсылки.Количество() > 0 Тогда
		ОперативныйУчетПроизводстваКлиент.УстановитьСтатусПроизводственнойОперации(
			ОперацииСсылки,
			НовыйСтатус,
			Подразделение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНазначениеОпераций(КлючиОпераций, РежимРаботы)
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("Подразделение", Подразделение);
	ПараметрыФормы.Вставить("РежимРаботы", РежимРаботы);
	ПараметрыФормы.Вставить("КлючиОпераций", КлючиОпераций);
	
	Для каждого ИмяПараметра Из СтрРазделить("РабочийЦентр,ВариантНаладки,Участок", ",") Цикл
		Если ЗначениеЗаполнено(ЭтотОбъект[ИмяПараметра]) Тогда
			ПараметрыФормы.Вставить(ИмяПараметра, ЭтотОбъект[ИмяПараметра]);
		ИначеЕсли КлючиОпераций.Количество() = 1 Тогда
			ТекущиеДанные = Элементы.ВыполнениеОпераций.ТекущиеДанные;
			Если ТекущиеДанные <> Неопределено Тогда
				ПараметрыФормы.Вставить(ИмяПараметра, ТекущиеДанные[ИмяПараметра])
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СтатусОперации = Неопределено;
	Если РежимРаботы = "Назначить" Тогда
		СтатусОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроизводственныхОпераций.Создана");
	ИначеЕсли РежимРаботы = "ПринятьВРаботу" Тогда
		СтатусОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроизводственныхОпераций.Выполняется");
	ИначеЕсли РежимРаботы = "ОтметитьВыполнение" Тогда
		СтатусОперации = ПредопределенноеЗначение("Перечисление.СтатусыПроизводственныхОпераций.Выполнена");
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("КлючиОпераций", КлючиОпераций);
	ДопПараметры.Вставить("СтатусОперации", СтатусОперации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуНазначениеОперацийЗавершение",
		ЭтотОбъект,
		ДопПараметры);
	
	ОткрытьФорму("Обработка.ВыполнениеОпераций2_2.Форма.НазначениеОпераций",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНазначениеОперацийЗавершение(ПараметрыНазначения, ДополнительныеПараметры) Экспорт
	
	Если ПараметрыНазначения <> Неопределено Тогда
		
		ОперативныйУчетПроизводстваКлиент.СформироватьПроизводственныеОперации(
			Подразделение,
			ДополнительныеПараметры.КлючиОпераций,
			ПараметрыНазначения,
			ДополнительныеПараметры.СтатусОперации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииНазначитьРабочийЦентрЗавершение(РезультатЗакрытия, ДопПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		
		ОчиститьСообщения();
		
		Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
			ОперацииНазначитьРабочийЦентрНаСервере(ДопПараметры.Операции,
				РезультатЗакрытия.РабочийЦентр,
				РезультатЗакрытия.ВариантНаладки);
		Иначе
			ОперацииНазначитьРабочийЦентрНаСервере(ДопПараметры.Операции, РезультатЗакрытия);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОперацииНазначитьРабочийЦентрНаСервере(Операции, РабочийЦентр, ВариантНаладки = Неопределено)
	
	Документы.ПроизводственнаяОперация2_2.НазначитьРабочийЦентрОперациям(Операции, РабочийЦентр, ВариантНаладки);
	
	Элементы.ВыполнениеОпераций.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроконтролироватьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ПроизводственнаяОперация = ДополнительныеПараметры.ПроизводственнаяОперация;
	
	ОперативныйУчетПроизводстваВызовСервера.ЗафиксироватьРезультатКонтроляОперации(
		Результат.ЗаписиПротокола, Результат.ПересчитатьНормативы, ПроизводственнаяОперация);
	
	ОповеститьОбИзменении(ПроизводственнаяОперация);
	
	ПараметрОповещения = Новый Структура;
	ПараметрОповещения.Вставить("Подразделение", Подразделение);
	ПараметрОповещения.Вставить("Ссылка",        ПроизводственнаяОперация);
	Оповестить("Запись_ПроизводственнаяОперация2_2", ПараметрОповещения, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область КлючиЗаписей

&НаКлиентеНаСервереБезКонтекста
Функция КлючЗаписиОчередиКонструктор(Назначена = Ложь, Состояние = Ложь)
	
	Результат = Новый Структура;
	
	Результат.Вставить("Подразделение");
	Результат.Вставить("Этап");
	Результат.Вставить("Операция");
	Результат.Вставить("ИдентификаторОперации");
	
	Если Назначена Тогда
		Результат.Вставить("ПроизводственнаяОперация");
	КонецЕсли;
	
	Если Состояние Тогда
		Результат.Вставить("Состояние");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция СформироватьКлючЗаписиОчереди(ДанныеКлюча, Изменения = Неопределено)
	
	КлючСтрокиСтруктура = КлючЗаписиОчередиКонструктор(Истина, Истина);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	Для каждого КлючИЗначение Из ДанныеКлюча Цикл
		Если КлючСтрокиСтруктура.Свойство(КлючИЗначение.Ключ) Тогда
			Набор.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	Если Изменения <> Неопределено Тогда
		Для каждого КлючИЗначение Из Изменения Цикл
			Если КлючСтрокиСтруктура.Свойство(КлючИЗначение.Ключ) Тогда
				Набор.Отбор[КлючИЗначение.Ключ].Установить(КлючИЗначение.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Набор.Прочитать();
	
	Если Набор.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(КлючСтрокиСтруктура, Набор[0]);
		КлючЗаписи = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьКлючЗаписи(КлючСтрокиСтруктура);
	Иначе
		КлючЗаписи = Неопределено;
	КонецЕсли;
	
	Возврат КлючЗаписи;
	
КонецФункции

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ИнициализироватьКэш()
	
	Кэш = Новый Структура;
	
	Кэш.Вставить("ЦветНажатогоОтбора", ЦветаСтиля.БыстрыеОтборыФонГиперссылки);
	Кэш.Вставить("ДанныеТекущейСтроки", Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьПараметрыПодразделения()
	
	Если Подразделение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(
		ЭтотОбъект,
		ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Подразделение),
		"ИспользоватьСменныеЗадания,ИспользоватьБригадныеНаряды");
		
	НастройкиСписка = ВыполнениеОпераций.КомпоновщикНастроек.Настройки;
	НастройкиСписка.ДополнительныеСвойства.Вставить("Подразделение", Подразделение);
	НастройкиСписка.ДополнительныеСвойства.Вставить("ИспользоватьСменныеЗадания", ИспользоватьСменныеЗадания);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКлючСохраненияПоложенияОкна()
	
	КлючСохраненияПоложенияОкна = СтрШаблон("%1_%2", ИмяФормы, ?(ИспользоватьСменныеЗадания,"СЗ","БезСЗ"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкоды(Данные)
	
	МассивСсылок = СсылкаНаЭлементСпискаПоШтрихкоду(Данные.Штрихкод);
	Если МассивСсылок.Количество() > 0 Тогда
		ДанныеКлюча = Новый Структура("ПроизводственнаяОперация", МассивСсылок[0]);
		Элементы.ВыполнениеОпераций.ТекущаяСтрока = СформироватьКлючЗаписиОчереди(ДанныеКлюча);
		Если Элементы.ВыполнениеОпераций.ТекущаяСтрока = Неопределено Тогда
			ПоказатьЗначение(Неопределено, МассивСсылок[0]);
		КонецЕсли;
	Иначе
		ШтрихкодированиеПечатныхФормКлиент.ОбъектНеНайден(Данные.Штрихкод);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СсылкаНаЭлементСпискаПоШтрихкоду(Штрихкод)
	
	Менеджеры = Новый Массив();
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ПроизводственнаяОперация2_2.ПустаяСсылка"));
	Возврат ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);
	
КонецФункции

&НаСервере
Процедура ОчиститьИнформационноеСообщение()
	
	ИнформационноеСообщение = Неопределено;
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ИнформационноеСообщение");
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияТехпроцессаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = Истина Тогда
		Элементы.ВыполнениеОпераций.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти