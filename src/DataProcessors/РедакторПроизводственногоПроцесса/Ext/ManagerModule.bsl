#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

#Область ДлительныеОперации

Процедура ЗаписатьДанныеРедактораВФоне(Параметры, АдресХранилища) Экспорт
	
	Результат = ЗаписатьДанныеРедактора(Параметры);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Процедура СоздатьТиповойТехпроцессВФоне(Параметры, АдресХранилища) Экспорт
	
	Результат = СоздатьТиповойТехпроцесс(Параметры);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

Процедура ГрупповаяЗаменаВФоне(Параметры, АдресХранилища) Экспорт
	
	Результат = ГрупповаяЗамена(Параметры);
	
	ПоместитьВоВременноеХранилище(Результат, АдресХранилища);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РедакторИндивидуальный

Функция ЗаписатьДанныеРедактора(Параметры)
	
	Результат = РедакторПроизводственногоПроцессаКлиентСервер.РезультатЗаписиТехпроцессаКонструктор();
	
	Этап = Параметры.Этап;
	ПараметрыТехпроцесса = Параметры.ПараметрыТехпроцесса;
	
	Операции = РегистрыСведений.ОчередьПроизводственныхОпераций.ОчередьОперацийКонструктор();
	ПроизводственныеОперации = Новый Массив;
	
	Если ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		Для каждого ДанныеОперации Из Параметры.ТаблицаОпераций Цикл
			НоваяСтрока = Операции.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Параметры);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеОперации);
			Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеОперации) Тогда
				Если ЗначениеЗаполнено(ДанныеОперации.ПроизводственнаяОперация) Тогда
					НормативыСсылка = ДанныеОперации.ПроизводственнаяОперация;
					НормативыОбъект = ДанныеОперации.ПроизводственнаяОперация.ПолучитьОбъект();
				Иначе
					НормативыСсылка = Документы.ПроизводственнаяОперация2_2.ПолучитьСсылку();
					НормативыОбъект = Документы.ПроизводственнаяОперация2_2.СоздатьДокумент();
					НормативыОбъект.УстановитьСсылкуНового(НормативыСсылка);
				КонецЕсли;
				Если ДанныеОперации.Модифицированность Тогда
					ЗаполнитьДокументХраненияНормативов(НормативыОбъект, ДанныеОперации, Параметры);
					ПроизводственныеОперации.Добавить(НормативыОбъект);
				КонецЕсли;
				НоваяСтрока.ПроизводственнаяОперация = НормативыСсылка;
			КонецЕсли;
		КонецЦикла;
		
		//++ Локализация
		Если ПолучитьФункциональнуюОпцию("НормированиеИРасценкаРаботПоТарифнымСеткам") Тогда
			
			Трудозатраты = Новый ТаблицаЗначений();
			
			Трудозатраты.Колонки.Добавить("ИдентификаторОперации", Новый ОписаниеТипов("Число"));
			Трудозатраты.Колонки.Добавить("НомерСтроки", Новый ОписаниеТипов("Число"));
			Трудозатраты.Колонки.Добавить("ВидРабот", Новый ОписаниеТипов("СправочникСсылка.ВидыРаботСотрудников"));
			
			Для Каждого Ресурс Из Параметры.РесурсыОпераций Цикл
				Если Ресурс.Значение.Трудозатраты.Количество() <> 0 Тогда
					Строка = Трудозатраты.Добавить();
					Строка.ИдентификаторОперации = Ресурс.Ключ;
					Строка.НомерСтроки = 1;
					Строка.ВидРабот = Ресурс.Значение.Трудозатраты[0].ВидРабот;
				КонецЕсли;
			КонецЦикла;
			
			РегистрыСведений.ОчередьПроизводственныхОпераций.ДополнитьОперацииДолжностьюРазрядом(Операции, Трудозатраты, "ИдентификаторОперации");
			
		КонецЕсли;
		//-- Локализация
		
	КонецЕсли;
	ПроизводственныеОперацииУдалить = ДокументыКУдалению(Операции, Параметры);
	
	НачатьТранзакцию();
	Попытка
		
		БлокировкаДанных = РегистрыСведений.ОчередьПроизводственныхОпераций.БлокировкаДанныхДляРасчетаОчереди(Этап);
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства");
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Если Параметры.Свойство("ДанныеЭтапаДляОбновления") Тогда
			
			Результат.ЕстьОшибкиЗаписиЭтапа = Истина;
			
			ЭтапОбъект = Этап.ПолучитьОбъект();
			Для каждого КлючИЗначение Из Параметры.ДанныеЭтапаДляОбновления Цикл
				ЭтапОбъект[КлючИЗначение.Ключ].Загрузить(КлючИЗначение.Значение);
			КонецЦикла;
			Если ЭтапОбъект.ПроверитьЗаполнение() Тогда
				ЭтапОбъект.Записать(РежимЗаписиДокумента.Проведение);
			Иначе
				ВызватьИсключение НСтр("ru = 'Ошибка записи этапа';
										|en = 'An error occurred when saving the stage'");
			КонецЕсли;
			
			Результат.ЕстьОшибкиЗаписиЭтапа = Ложь;
			
		КонецЕсли;
		
		Результат.ЕстьОшибкиЗаписиТехпроцесса = Истина;
		
		РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ЗаписатьПараметрыТехпроцесса(
			Этап, ПараметрыТехпроцесса, Параметры.СохранитьОграниченияТехпроцесса);
		
		Для каждого ПроизводственнаяОперация Из ПроизводственныеОперации Цикл
			ПроизводственнаяОперация.Записать(
				?(ПроизводственнаяОперация.ОбменДанными.Загрузка, РежимЗаписиДокумента.Запись, РежимЗаписиДокумента.Проведение));
		КонецЦикла;
		
		Для каждого ПроизводственнаяОперация Из ПроизводственныеОперацииУдалить Цикл
			ПроизводственнаяОперация.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
		Если ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
			Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
			Набор.Отбор.Этап.Установить(Этап);
			Набор.Загрузить(Операции);
			Набор.Записать();
		КонецЕсли;
		
		РегистрыСведений.ОчередьПроизводственныхОпераций.ДобавитьЗаданиеКРасчетуОчереди(Этап);
		
		Результат.ЕстьОшибкиЗаписиТехпроцесса = Ложь;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ЗаполнитьРезультатЗаписиТехпроцессаПриОшибке(Результат, ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,Результат.ТекстОшибкиЖР);
		
	КонецПопытки;
	
	Если ПустаяСтрока(Результат.КраткоеПредставлениеОшибки) Тогда
		РегистрыСведений.СостоянияЭтаповПроизводства.ОтразитьСостояниеЭтапов(Этап);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СоздатьТиповойТехпроцесс(Параметры)
	
	Результат = РедакторПроизводственногоПроцессаКлиентСервер.РезультатСозданияТиповогоТехпроцессаКонструктор();
	Результат.ТехнологическийПроцесс = Справочники.ТехнологическиеПроцессы.ПолучитьСсылку();
	
	Объект = Справочники.ТехнологическиеПроцессы.СоздатьЭлемент();
	Объект.Наименование = Параметры.ПредставлениеЭтапа;
	Объект.Подразделение = Параметры.Подразделение;
	Объект.ДляВидаИзделий = Параметры.ВидНоменклатуры;
	Объект.Статус = Перечисления.СтатусыТехнологическихПроцессов.ВРазработке;
	Объект.Ответственный = Пользователи.ТекущийПользователь();
	Объект.УстановитьСсылкуНового(Результат.ТехнологическийПроцесс);
	
	Операции = Новый Массив();
	Для каждого ДанныеОперации Из Параметры.ТаблицаОпераций Цикл
		ОперацияСсылка = Справочники.ТехнологическиеОперации.ПолучитьСсылку();
		ОперацияОбъект = Справочники.ТехнологическиеОперации.СоздатьЭлемент();
		ОперацияОбъект.УстановитьСсылкуНового(ОперацияСсылка);
		ОперацияОбъект.Владелец = Результат.ТехнологическийПроцесс;
		ЗаполнитьЗначенияСвойств(ОперацияОбъект, ДанныеОперации);
		РесурсыОперации = Параметры.РесурсыОпераций.Получить(ДанныеОперации.ИдентификаторОперации);
		Если РесурсыОперации <> Неопределено Тогда
			Для каждого ИмяТЧ Из СтрРазделить("МатериалыИУслуги,Трудозатраты", ",") Цикл
				Если НЕ РесурсыОперации.Свойство(ИмяТЧ) Тогда
					Продолжить;
				КонецЕсли;
				Для каждого Строка Из РесурсыОперации[ИмяТЧ] Цикл
					Таблица = Объект[ИмяТЧ]; // ТабличнаяЧасть
					НоваяСтрока = Таблица.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
					НоваяСтрока.ОперацияРедактирование = ОперацияСсылка;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
		Операции.Добавить(ОперацияОбъект);
	КонецЦикла;
	
	НачатьТранзакцию();
	Попытка
		
		Объект.Записать();
		
		Для каждого ОперацияОбъект Из Операции Цикл
			ОперацияОбъект.Записать();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать технологический процесс по причине: %1';
									|en = 'Cannot create a technological process. Reason: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,ТекстОшибки);
			
		Результат.ЕстьОшибки = Истина;
		Результат.ТехнологическийПроцесс = Неопределено;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьДокументХраненияНормативов(НормативыОбъект, ДанныеОперации, Параметры)
	
	Если НормативыОбъект.ЭтоНовый() Тогда
		НормативыОбъект.Заполнить(Параметры.Этап);
		НормативыОбъект.Дата   = ТекущаяДатаСеанса();
		НормативыОбъект.Подразделение = Параметры.Подразделение;
		НормативыОбъект.Ответственный = Пользователи.ТекущийПользователь();
	Иначе
		НормативыОбъект.ПометкаУдаления = Ложь;
	КонецЕсли;
	
	Если ДанныеОперации.ТолькоПросмотр Тогда
		
		ЗаполнитьЗначенияСвойств(НормативыОбъект, ДанныеОперации, "НомерОперации,НомерСледующейОперации");
		
	Иначе
		
		НормативыОбъект.Статус = Перечисления.СтатусыПроизводственныхОпераций.Формируется;
		
		ЗаполнитьЗначенияСвойств(НормативыОбъект, ОперативныйУчетПроизводстваКлиентСервер.СтруктураИтоговКонструктор());
		НормативыОбъект.Протокол.Очистить();
		
		ЗаполнитьЗначенияСвойств(НормативыОбъект, ДанныеОперации);
		НормативыОбъект.Комментарий = ДанныеОперации["Описание"];
		НормативыОбъект.Количество  = ДанныеОперации["Требуется"];
		
		РесурсыОперации = Параметры.РесурсыОпераций.Получить(ДанныеОперации.ИдентификаторОперации);
		Если РесурсыОперации <> Неопределено Тогда
			СоответствиеРесурсов = Новый Структура;
			СоответствиеРесурсов.Вставить("ВыходныеИзделия",  "ВыходныеИзделия");
			СоответствиеРесурсов.Вставить("МатериалыИРаботы", "МатериалыИУслуги");
			СоответствиеРесурсов.Вставить("Трудозатраты",     "Трудозатраты");
			Для каждого КлючИЗначение Из СоответствиеРесурсов Цикл
				Таблица = Неопределено;
				Если РесурсыОперации.Свойство(КлючИЗначение.Значение, Таблица) Тогда
					НормативыОбъект[КлючИЗначение.Ключ].Загрузить(Таблица);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Документы.ПроизводственнаяОперация2_2.ПересчитатьВремяВыполнения(НормативыОбъект);
		
	КонецЕсли;
	
	НормативыОбъект.ВключенаВТехпроцесс = Истина;
	
	Если НЕ НормативыОбъект.ПроверитьЗаполнение() Тогда
		НормативыОбъект.ОбменДанными.Загрузка = Истина;
	КонецЕсли;
	
	НормативыОбъект.УстановитьРежимГрупповойОбработки();
	НормативыОбъект.УстановитьРежимРедактораПроизводственногоПроцесса();
	
КонецПроцедуры

Функция ДокументыКУдалению(Операции, Параметры)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Операции.Ссылка КАК ПроизводственнаяОперация
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2 КАК Операции
	|ГДЕ
	|	НЕ Операции.НаОснованииНСИ
	|	И Операции.ВключенаВТехпроцесс
	|	И НЕ Операции.ПометкаУдаления
	|	И Операции.Этап = &Этап";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Параметры.Этап);
	
	Результат = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ПроизводственнаяОперация");
	Результат = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Результат, Операции.ВыгрузитьКолонку("ПроизводственнаяОперация"));
	
	Для Индекс = 0 По Результат.ВГраница() Цикл
		Результат[Индекс] = Результат[Индекс].ПолучитьОбъект();
		Результат[Индекс].УстановитьРежимГрупповойОбработки();
		Результат[Индекс].УстановитьРежимРедактораПроизводственногоПроцесса();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Имя события журнала регистрации.
// 
// Возвращаемое значение:
//  Строка
Функция ИмяСобытияЖурналаРегистрации() Экспорт
	
	Возврат НСтр("ru = 'Редактор производственного процесса';
				|en = 'Production process editor'");
	
КонецФункции

#КонецОбласти

#Область ЗаменаГрупповая

Функция ГрупповаяЗамена(Параметры)
	
	Результат = Новый Структура;
	Результат.Вставить("Успешно",    Новый Массив);
	Результат.Вставить("Неудачно",   Новый Соответствие);
	Результат.Вставить("ЕстьОшибки", Ложь);
	
	ДанныеОсновногоИзделия = Неопределено;
	ПараметрыТехпроцесса   = Параметры.ПараметрыТехпроцесса;
	Индивидуальный         = ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный;
	
	ДанныеЭтапов = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Параметры.Этапы, Ложь);
	ДанныеРаспоряжений = ДанныеРаспоряжений(Параметры.Этапы);
	
	Для каждого Этап Из Параметры.Этапы Цикл
		
		ДанныеЭтапа = ДанныеЭтапов[Этап];
		
		ПараметрыЗапроса = РедакторПроизводственногоПроцесса.ПараметрыЗапросаСопоставлениеРесурсовТехпроцессЭтап();
		
		ПараметрыЗапроса.РесурсыТехпроцесса = ТаблицаРесурсовТехпроцесса(Параметры, ДанныеЭтапа, ДанныеРаспоряжений[Этап]);
		ПараметрыЗапроса.Этап               = ДанныеЭтапа.Ссылка;
		ПараметрыЗапроса.Подразделение      = ДанныеЭтапа.Подразделение;
		ПараметрыЗапроса.Распоряжение       = ДанныеЭтапа.Распоряжение;
		ПараметрыЗапроса.ИспользоватьВыходныеИзделия = Ложь;
		
		ТаблицаСопоставления = РедакторПроизводственногоПроцесса.ТаблицаСопоставленияРесурсовТехпроцессЭтап(ПараметрыЗапроса);
		
		НачатьТранзакцию();
		Попытка
			
			РезультатЭтап = РедакторПроизводственногоПроцессаКлиентСервер.РезультатЗаписиТехпроцессаКонструктор();
			
			РезультатЭтап.ЕстьОшибкиЗаписиЭтапа = Истина;
			
			ОбновитьЭтап(Этап, ТаблицаСопоставления);
			
			РезультатЭтап.ЕстьОшибкиЗаписиЭтапа = Ложь;
			
			РезультатЭтап.ЕстьОшибкиЗаписиТехпроцесса = Истина;
			
			ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса = ДанныеЭтапа.КоэффициентТехнологическогоПроцесса;
			РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ЗаписатьПараметрыТехпроцесса(Этап, ПараметрыТехпроцесса, Ложь);
		
			Если Индивидуальный Тогда
				ТаблицаОпераций = Параметры.ТаблицыОпераций.Получить(Этап);
				Если ТаблицаОпераций = Неопределено Тогда
					ТаблицаОпераций = Параметры.ТаблицыОпераций.Получить(Документы.ЭтапПроизводства2_2.ПустаяСсылка());
				КонецЕсли;
				Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
				Набор.Отбор.Этап.Установить(Этап);
				Для каждого ДанныеОперации Из ТаблицаОпераций Цикл
					Если Параметры.ТехпроцессТекущий = ДанныеЭтапа.Спецификация Тогда
						Если ДанныеОсновногоИзделия = Неопределено Тогда
							ДанныеОсновногоИзделия = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(
									ДанныеЭтапа.Спецификация,
									ДанныеЭтапа.Номенклатура,
									ДанныеЭтапа.Характеристика);
						КонецЕсли;
						Коэффициент = ДанныеОперации.КоэффициентТехнологическогоПроцесса
								* ДанныеЭтапа.КоличествоУпаковокПлан
								* ДанныеЭтапа.КоэффициентУпаковки
								/ ДанныеОсновногоИзделия.Количество;
					Иначе
						Коэффициент = ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса;
					КонецЕсли;
					Запись = Набор.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, ДанныеОперации);
					Запись.Этап = Этап;
					Запись.Подразделение = ДанныеЭтапа.Подразделение;
					Запись.Распоряжение  = ДанныеЭтапа.Распоряжение;
					Запись.КоэффициентТехнологическогоПроцесса = Коэффициент;
					Запись.Требуется = ДанныеОперации.Количество * Коэффициент;
				КонецЦикла;
				Набор.Записать();
			КонецЕсли;
			
			РегистрыСведений.ОчередьПроизводственныхОпераций.ДобавитьЗаданиеКРасчетуОчереди(Этап);
			
			РезультатЭтап.ЕстьОшибкиЗаписиТехпроцесса = Ложь;
			
			Результат.Успешно.Добавить(Этап);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ЗаполнитьРезультатЗаписиТехпроцессаПриОшибке(РезультатЭтап, ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,РезультатЭтап.ТекстОшибкиЖР);
			
			Результат.Неудачно.Вставить(Этап, РезультатЭтап);
			
		КонецПопытки;
	
	КонецЦикла;
	
	Если Результат.Успешно.Количество() > 0 Тогда
		
		РегистрыКОтражению = СостоянияДокументов.РегистрыКОтражениюСостоянияЗаказов();
		РегистрыКОтражению.СостоянияЭтаповПроизводства = Истина;
		
		СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов(
			Неопределено,
			Результат.Успешно,
			"ДЕЙСТВИЕ_ОБНОВИТЬ_СОСТОЯНИЕ_ОПЕРАЦИЙ",
			РегистрыКОтражению);
		
	КонецЕсли;
	
	Результат.ЕстьОшибки = Результат.Неудачно.Количество() > 0;
	Возврат Результат;
	
КонецФункции

Функция ГрупповаяЗаменаПереченьРесурсов(ВМассив = Ложь)
	
	Результат = "МатериалыИУслуги,Трудозатраты";
	Возврат ?(ВМассив, СтрРазделить(Результат, ","), Результат);
	
КонецФункции

Функция ТаблицаРесурсовТехпроцесса(Параметры, ДанныеЭтапа, ДанныеРаспоряжения)
	
	ТаблицаРесурсов = РедакторПроизводственногоПроцесса.ТаблицаРесурсовТехпроцесса();
	
	ДанныеТехпроцессаТекущего = Неопределено;
	ДанныеТехпроцессаНового   = ДанныеТехпроцесса(Параметры.ТехпроцессНовый, ДанныеЭтапа, ДанныеРаспоряжения);
	
	Если Параметры.ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		
		ДанныеТехпроцессаТекущего = ДанныеТехпроцесса(Параметры.ТехпроцессТекущий, ДанныеЭтапа, ДанныеРаспоряжения);
		ПрименитьОтборКДаннымТехпроцесса(Параметры.ТехпроцессТекущий, ДанныеТехпроцессаТекущего, "Трудозатраты", , Параметры.ОперацияПрерватьИдентификатор);
		ПрименитьОтборКДаннымТехпроцесса(Параметры.ТехпроцессНовый, ДанныеТехпроцессаНового, "Трудозатраты", Параметры.ОперацияПродолжитьИдентификатор);
		
	КонецЕсли;
	
	ДобавитьДанныеТехпроцессаВТаблицу(ТаблицаРесурсов, ДанныеТехпроцессаТекущего);
	ДобавитьДанныеТехпроцессаВТаблицу(ТаблицаРесурсов, ДанныеТехпроцессаНового);
	ПроизводствоСервер.СвернутьТаблицуЗначений(ТаблицаРесурсов,,"Количество");
	
	Возврат ТаблицаРесурсов;
	
КонецФункции

Функция ДанныеРаспоряжений(Этапы)
	
	Результат = Новый Соответствие;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Этапы.Ссылка                     КАК Этап,
	|	Этапы.Распоряжение.НачатьНеРанее КАК НачалоПроизводства,
	|	Этапы.Распоряжение.Подразделение КАК ПодразделениеДиспетчер
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Этапы
	|ГДЕ
	|	Этапы.Ссылка В (&Этапы)";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этапы", Этапы);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДанныеРаспоряжения = Новый Структура("НачалоПроизводства,ПодразделениеДиспетчер");
		ЗаполнитьЗначенияСвойств(ДанныеРаспоряжения, Выборка);
		Результат.Вставить(Выборка.Этап, ДанныеРаспоряжения);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеТехпроцесса(ТехнологическийПроцесс, ДанныеЭтапа, ДанныеРаспоряжения)
	
	Результат = Неопределено;
	
	ПереченьДанных = ГрупповаяЗаменаПереченьРесурсов()+",Операции";
	
	Если ТипЗнч(ТехнологическийПроцесс) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
		ДанныеПоНоменклатуре.Номенклатура = ДанныеЭтапа.Номенклатура;
		ДанныеПоНоменклатуре.Характеристика = ДанныеЭтапа.Характеристика;
		ДанныеПоНоменклатуре.Спецификация = ДанныеЭтапа.ТехнологическийПроцесс;
		ДанныеПоНоменклатуре.НачалоПроизводства = ДанныеРаспоряжения.НачалоПроизводства;
		ДанныеПоНоменклатуре.ПодразделениеДиспетчер = ДанныеРаспоряжения.ПодразделениеДиспетчер;
		ДанныеПоНоменклатуре.НаправлениеДеятельности = ДанныеЭтапа.НаправлениеДеятельности;
		ДанныеПоНоменклатуре.Количество = ДанныеЭтапа.КоличествоУпаковокПлан * ДанныеЭтапа.КоэффициентУпаковки;
		
		ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных(ПереченьДанных);
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.ВыпускПроизвольнымиПорциями = Истина;
		ПараметрыВыборки.РассчитыватьПризнакЗапланироватьПроизводство = Ложь;
		
		Результат = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоНоменклатуре(
			ДанныеПоНоменклатуре,
			ПараметрыВыборки,
			Новый Структура("Этап", ДанныеЭтапа.ЭтапСпецификации));
	
	ИначеЕсли ТипЗнч(ТехнологическийПроцесс) = Тип("СправочникСсылка.ТехнологическиеПроцессы") Тогда
		
		ДанныеПоНоменклатуре = Справочники.ТехнологическиеПроцессы.ДанныеПоНоменклатуре();
		ДанныеПоНоменклатуре.Номенклатура = ДанныеЭтапа.Номенклатура;
		ДанныеПоНоменклатуре.Характеристика = ДанныеЭтапа.Характеристика;
		ДанныеПоНоменклатуре.Спецификация = ДанныеЭтапа.Спецификация;
		ДанныеПоНоменклатуре.НачалоПроизводства = ДанныеРаспоряжения.НачалоПроизводства;
		ДанныеПоНоменклатуре.ПодразделениеДиспетчер = ДанныеРаспоряжения.ПодразделениеДиспетчер;
		ДанныеПоНоменклатуре.НаправлениеДеятельности = ДанныеЭтапа.НаправлениеДеятельности;
		ДанныеПоНоменклатуре.Количество = ДанныеЭтапа.КоличествоУпаковокПлан * ДанныеЭтапа.КоэффициентУпаковки;
		
		ПараметрыВыборки = Справочники.ТехнологическиеПроцессы.ПараметрыВыборкиДанных(ПереченьДанных);
		
		Результат = Справочники.ТехнологическиеПроцессы.ДанныеТехнологическогоПроцессаПоНоменклатуре(
			ТехнологическийПроцесс,
			ДанныеЭтапа.КоэффициентТехнологическогоПроцесса,
			ДанныеПоНоменклатуре,
			ПараметрыВыборки);
		
	ИначеЕсли ТипЗнч(ТехнологическийПроцесс) = Тип("СправочникСсылка.МаршрутныеКарты") Тогда
	
		Результат = Справочники.МаршрутныеКарты.ДанныеМаршрутнойКарты(
			ТехнологическийПроцесс,
			ДанныеЭтапа.КоэффициентТехнологическогоПроцесса,
			Неопределено,
			Неопределено,
			"Материалы,Трудозатраты,Операции");
		Результат.Вставить("МатериалыИУслуги", Результат.Материалы);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьДанныеТехпроцессаВТаблицу(ТаблицаРесурсов, ДанныеТехпроцесса)
	
	Если ДанныеТехпроцесса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ИмяТЧ Из ГрупповаяЗаменаПереченьРесурсов(Истина) Цикл
		
		Для каждого Строка Из ДанныеТехпроцесса[ИмяТЧ] Цикл
			НоваяСтрока = ТаблицаРесурсов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ИмяТЧ = ИмяТЧ;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПрименитьОтборКДаннымТехпроцесса(ТехнологическийПроцесс, ДанныеТехпроцесса, ПереченьДанных, ИдентификаторНачальный = Неопределено, ИдентификаторКонечный = Неопределено)
	
	Используется = (ИдентификаторНачальный = Неопределено);
	
	Для Индекс = 0 По ДанныеТехпроцесса.Операции.Количество() -1 Цикл
		
		ДанныеОперации = ДанныеТехпроцесса.Операции[Индекс];
		
		Если ДанныеОперации.ИдентификаторОперации = ИдентификаторНачальный Тогда
			Используется = НЕ Используется;
		КонецЕсли;
		
		Если НЕ Используется Тогда
			
			Для каждого ИмяТЧ Из СтрРазделить(ПереченьДанных, ",") Цикл
				
				МассивСтрок = Новый Массив;
				
				Если ТипЗнч(ТехнологическийПроцесс) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
					Для каждого СтрокаНСИ Из ДанныеТехпроцесса[ИмяТЧ] Цикл
						Если Справочники.РесурсныеСпецификации.СтрокаДанныхСпецификацииПринадлежитОперации(ДанныеОперации, СтрокаНСИ) Тогда
							МассивСтрок.Добавить(СтрокаНСИ);
						КонецЕсли;
					КонецЦикла;
				Иначе
					МассивСтрок = ДанныеТехпроцесса[ИмяТЧ].НайтиСтроки(Новый Структура("Операция", ДанныеОперации.Операция));
				КонецЕсли;
			
				Для каждого Строка Из МассивСтрок Цикл
					ДанныеТехпроцесса[ИмяТЧ].Удалить(Строка);
				КонецЦикла;
			
			КонецЦикла;
			
		КонецЕсли;
		
		Если ДанныеОперации.ИдентификаторОперации = ИдентификаторКонечный Тогда
			Используется = НЕ Используется;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьЭтап(Этап, ТаблицаСопоставления)
	
	Если ТаблицаСопоставления.Найти(Истина, "Изменено") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтапОбъект                    = Неопределено;
	ПараметрыЗаполненияНазначений = Неопределено;
	КэшированныеЗначения          = Неопределено;
	
	Для каждого СтрокаДействие Из ТаблицаСопоставления Цикл
		
		ИмяТЧ = СтрокаДействие.ИмяТЧ;
		ИмяТЧЭтапа = ?(ИмяТЧ = "МатериалыИУслуги", "ОбеспечениеМатериаламиИРаботами", ИмяТЧ);
		ПоляПоиска = ?(ИмяТЧ = "МатериалыИУслуги", "Номенклатура,Характеристика", "ВидРабот");
		
		РедакторПроизводственногоПроцессаКлиентСервер.ЗаполнитьПараметрыДействияПоУмолчанию(СтрокаДействие, ИмяТЧЭтапа);
		
		Если НЕ СтрокаДействие.Отметка Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтапОбъект = Неопределено Тогда
			ЭтапОбъект = Этап.ПолучитьОбъект();
		КонецЕсли;
		
		ТаблицаДокумента = ЭтапОбъект[ИмяТЧЭтапа]; // ТабличнаяЧасть
		
		СтруктураПоиска1 = Новый Структура(ПоляПоиска);
		СтруктураПоиска2 = Новый Структура(ПоляПоиска);
		ЗаполнитьЗначенияСвойств(СтруктураПоиска1, СтрокаДействие);
		
		СтрокиОбработать = Новый Массив;
		СтрокиУдалить    = Новый Массив;
		
		Если СтрокаДействие.Действие = РедакторПроизводственногоПроцессаКлиентСервер.ДействиеДобавить() Тогда
			
			НоваяСтрока = ТаблицаДокумента.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДействие);
			
			НоваяСтрока.Количество    = СтрокаДействие.КоличествоТехпроцесс - СтрокаДействие.КоличествоЭтап;
			НоваяСтрока.Подразделение = ЭтапОбъект.Подразделение;
		
			Если ИмяТЧЭтапа = "ОбеспечениеМатериаламиИРаботами" Тогда
				Если ПараметрыЗаполненияНазначений = Неопределено Тогда
					ПараметрыЗаполненияНазначений = ОбеспечениеПроизводства.ПараметрыДействияЗаполнитьНазначениеОбеспеченияВЭтапеПроизводства(
						ЭтапОбъект, ЭтапОбъект.ВыходныеИзделия);
				КонецЕсли;
				НоваяСтрока.НазначениеОбеспечения = ПараметрыЗаполненияНазначений.НазначениеМатериалы;
				Если НоваяСтрока.Обособленно Тогда
					НоваяСтрока.Назначение = ПараметрыЗаполненияНазначений.НазначениеМатериалы;
				КонецЕсли;
				Если СтрокаДействие.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
					НоваяСтрока.ВариантОбеспечения = ?(НоваяСтрока.Обособленно, Перечисления.ВариантыОбеспечения.КОбеспечению, Перечисления.ВариантыОбеспечения.НеТребуется);
				Иначе
					НоваяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.КОбеспечению;
				КонецЕсли;
			КонецЕсли;
			
			СтрокиОбработать.Добавить(НоваяСтрока);
		
		ИначеЕсли СтрокаДействие.Действие = РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУвеличить() Тогда
			
			КоличествоУвеличитьВсего = СтрокаДействие.КоличествоТехпроцесс - СтрокаДействие.КоличествоЭтап;
			
			Для Индекс = 0 По ТаблицаДокумента.Количество() - 1 Цикл
				Строка = ТаблицаДокумента[Индекс];
				ЗаполнитьЗначенияСвойств(СтруктураПоиска2, Строка);
				Если ОбщегоНазначения.ДанныеСовпадают(СтруктураПоиска1, СтруктураПоиска2)
					И РедакторПроизводственногоПроцессаКлиентСервер.МожноИзменитьСтрокуЭтапа(ИмяТЧЭтапа, Строка, СтрокаДействие.Действие) Тогда
					Строка.Количество = Строка.Количество + КоличествоУвеличитьВсего;
					СтрокиОбработать.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли СтрокаДействие.Действие = РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУменьшить() Тогда
			
			КоличествоУменьшитьВсего = СтрокаДействие.КоличествоЭтап - СтрокаДействие.КоличествоТехпроцесс;
			
			Для Индекс = 0 По ТаблицаДокумента.Количество() - 1 Цикл
				Строка = ТаблицаДокумента[Индекс];
				ЗаполнитьЗначенияСвойств(СтруктураПоиска2, Строка);
				Если ОбщегоНазначения.ДанныеСовпадают(СтруктураПоиска1, СтруктураПоиска2)
						И РедакторПроизводственногоПроцессаКлиентСервер.МожноИзменитьСтрокуЭтапа(ИмяТЧЭтапа, Строка, СтрокаДействие.Действие) Тогда
					Если КоличествоУменьшитьВсего > 0 Тогда
						КоличествоУменьшить = Мин(Строка.Количество, КоличествоУменьшитьВсего);
						Строка.Количество = Строка.Количество - КоличествоУменьшить;
						КоличествоУменьшитьВсего = КоличествоУменьшитьВсего - КоличествоУменьшить;
						Если Строка.Количество > 0 Тогда
							СтрокиОбработать.Добавить(Строка);
						Иначе
							СтрокиУдалить.Добавить(Строка);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		
		ИначеЕсли СтрокаДействие.Действие = РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУдалить() Тогда
			
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СтрокиУдалить, ТаблицаДокумента.НайтиСтроки(СтруктураПоиска1));
		
		КонецЕсли;
		
		Если ИмяТЧ = "МатериалыИУслуги" Тогда
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
			Для каждого СтрокаТабличнойЧасти Из СтрокиОбработать Цикл
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
			КонецЦикла;
		КонецЕсли;
		
		Для каждого Строка Из СтрокиУдалить Цикл
			ТаблицаДокумента.Удалить(Строка);
		КонецЦикла;
		
	КонецЦикла;
	
	Если ЭтапОбъект <> Неопределено
		И ЭтапОбъект.Модифицированность() Тогда
		
		Если ЭтапОбъект.ПроверитьЗаполнение() Тогда
			ЭтапОбъект.Записать(РежимЗаписиДокумента.Проведение);
		Иначе
			ВызватьИсключение СтрШаблон(НСтр("ru = 'Ошибка записи этапа %1';
											|en = 'An error occurred when saving the ""%1"" stage'"), Этап);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаполнитьРезультатЗаписиТехпроцессаПриОшибке(РезультатЗаписи, ИнформацияОбОшибке)
	
	РезультатЗаписи.КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	РезультатЗаписи.ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	
	МассивТекстов = Новый Массив;
	МассивТекстов.Добавить(РезультатЗаписи.КраткоеПредставлениеОшибки);
	Для каждого Сообщение Из ПолучитьСообщенияПользователю(Ложь) Цикл
		МассивТекстов.Добавить(Сообщение.Текст);
	КонецЦикла;
	МассивТекстов.Добавить(НСтр("ru = 'Подробнее см. в журнале регистрации.';
								|en = 'For more information, see the event log.'"));
	РезультатЗаписи.ТекстОшибки = СтрСоединить(МассивТекстов, Символы.ПС);
	
	МассивТекстов.Добавить("");
	МассивТекстов.Добавить(РезультатЗаписи.ПодробноеПредставлениеОшибки);
	МассивТекстов.Добавить(СтрШаблон(НСтр("ru = 'Ошибки записи этапа: %1';
											|en = 'An error occurred when saving the stage: %1'"), РезультатЗаписи.ЕстьОшибкиЗаписиЭтапа));
	МассивТекстов.Добавить(СтрШаблон(НСтр("ru = 'Ошибки записи техпроцесса: %1';
											|en = 'An error occurred when saving the technological process: %1'"), РезультатЗаписи.ЕстьОшибкиЗаписиТехпроцесса));
	РезультатЗаписи.ТекстОшибкиЖР = СтрСоединить(МассивТекстов, Символы.ПС);
	
КонецПроцедуры

// Данные коэффициента технологического процесса.
// 
// Параметры:
//  ТехнологическийПроцесс - СправочникСсылка.ТехнологическиеПроцессы
//  ДанныеОсновногоИзделия - см. Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации
//  Сократить - Булево - сократить дробь
// 
// Возвращаемое значение:
//  см. ПроизводствоСервер.Дробь
Функция ДанныеКоэффициентаТехнологическогоПроцесса(ТехнологическийПроцесс, ДанныеОсновногоИзделия, Сократить = Ложь) Экспорт
	
	Результат = ПроизводствоСервер.Дробь(1, 1);
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ТехнологическийПроцесс,
		"КоэффициентЧислитель,КоэффициентЗнаменатель,РассчитыватьКоэффициент");
		
	Если ЗначенияРеквизитов.РассчитыватьКоэффициент Тогда
		
		Результат.Числитель   = ЗначенияРеквизитов.КоэффициентЧислитель;
		Результат.Знаменатель = ЗначенияРеквизитов.КоэффициентЗнаменатель;
		
	ИначеЕсли ДанныеОсновногоИзделия.Количество > 0 Тогда
		
		Результат.Числитель   = 1;
		Результат.Числитель = ДанныеОсновногоИзделия.Количество;
		
	КонецЕсли;
	
	Если Сократить Тогда
		ПроизводствоСервер.СократитьДробь(Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Инициализирует параметры, обслуживающие выбор назначений в формах документа.
// 
//  Возвращаемое значение:
//  См. Справочники.Назначения.МакетФормыВыбораНазначений
//
Функция МакетФормыВыбораНазначений() Экспорт
	
	МакетФормы = Справочники.Назначения.МакетФормыВыбораНазначений();
	
	#Область ОбеспечениеМатериаламиИРаботами
	
	//
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	ШаблонНазначения.УсловиеИспользования     = "&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СобственноеПроизводство)";
	ШаблонНазначения.НаправлениеДеятельности  = "&НаправлениеДеятельности";
	Если Не Пользователи.РолиДоступны("ВыборПроизвольныхНазначенийВЭтапеПроизводства") Тогда
		ШаблонНазначения.Ссылка = "&ОтборСсылокВФормеВыбораНазначений";
	КонецЕсли;
	ШаблонНазначения.ИсключатьНазначениеСсылки = Ложь;
	ШаблонНазначения.ТипыНазначений.Очистить();
	ШаблонНазначения.ТипыНазначений.Добавить(Перечисления.ТипыНазначений.Собственное);
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	ШаблонНазначения.УсловиеИспользования     = "&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья)";
	ШаблонНазначения.НаправлениеДеятельности  = "&НаправлениеДеятельности";
	ШаблонНазначения.Партнер                  = "&Партнер";
	ШаблонНазначения.Договор                  = "&Договор";
	Если Не Пользователи.РолиДоступны("ВыборПроизвольныхНазначенийВЭтапеПроизводства") Тогда
		ШаблонНазначения.Ссылка = "&ОтборСсылокВФормеВыбораНазначений";
	КонецЕсли;
	ШаблонНазначения.ИсключатьНазначениеСсылки = Ложь;
	ШаблонНазначения.ТипыНазначений.Очистить();
	ШаблонНазначения.ТипыНазначений.Добавить(Перечисления.ТипыНазначений.ДавальческоеМатериалы22);
	ШаблонНазначения.ТипыНазначений.Добавить(Перечисления.ТипыНазначений.ДавальческоеМатериалыПодЭтап22);
	
	ШаблонНазначения = Справочники.Назначения.ДобавитьШаблонНазначений(МакетФормы, "ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	ШаблонНазначения.УсловиеИспользования     = "&ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья2_5)";
	ШаблонНазначения.НаправлениеДеятельности  = "&НаправлениеДеятельности";
	ШаблонНазначения.Партнер                  = "&Партнер";
	ШаблонНазначения.Договор                  = "&Договор";
	Если Не Пользователи.РолиДоступны("ВыборПроизвольныхНазначенийВЭтапеПроизводства") Тогда
		ШаблонНазначения.Ссылка = "&ОтборСсылокВФормеВыбораНазначений";
	КонецЕсли;
	ШаблонНазначения.ИсключатьНазначениеСсылки = Ложь;
	ШаблонНазначения.ТипыНазначений.Очистить();
	ШаблонНазначения.ТипыНазначений.Добавить(Перечисления.ТипыНазначений.Давальческое2_5);
	ШаблонНазначения.ТипыНазначений.Добавить(Перечисления.ТипыНазначений.ДавальческоеМатериалыПодЭтап22);
	
	//
	ОписаниеКолонок = Справочники.Назначения.ДобавитьОписаниеКолонок(МакетФормы, "ОбеспечениеЗаказов", Ложь, "ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	
	ОписаниеКолонок.ПутиКДанным.Номенклатура     = "ОбеспечениеМатериаламиИРаботами.Номенклатура";
	ОписаниеКолонок.ПутиКДанным.Характеристика   = "ОбеспечениеМатериаламиИРаботами.Характеристика";
	ОписаниеКолонок.ПутиКДанным.Склад            = "ОбеспечениеМатериаламиИРаботами.Склад";
	
	#КонецОбласти
	
	Возврат МакетФормы;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли