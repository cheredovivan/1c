
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Этап   = Параметры.Этап;
	Режим  = Параметры.Режим;
	
	Параметры.Свойство("АдресПараметровЗаписиТехпроцесса", АдресПараметровЗаписиТехпроцесса);
	Параметры.Свойство("АдресПараметровОбновленияЭтапа",   АдресПараметровОбновленияЭтапа);
	Параметры.Свойство("АдресДанныхЭтапаДляОбновления",    АдресДанныхЭтапаДляОбновления);
	
	ЗаполнитьСлужебныеРеквизиты();
	
	ЗаполнитьДанные();
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УстановитьОтборОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоИзмененные(Элемент)
	
	УстановитьОтборИзмененныхСтрок(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбеспечениеМатериаламиИРаботами

&НаКлиенте
Процедура ОбеспечениеМатериаламиИРаботамиПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечениеМатериаламиИРаботамиОтметкаПриИзменении(Элемент)
	
	ЗаполнитьКоличествоРезультат(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыходныеИзделия

&НаКлиенте
Процедура ВыходныеИзделияПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияОтметкаПриИзменении(Элемент)
	
	ЗаполнитьКоличествоРезультат(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияТипСтоимостиПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВыходныеИзделия.ТекущиеДанные;
	Если ТекущиеДанные.ТипСтоимости = ПредопределенноеЗначение("Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается") Тогда
		ТекущиеДанные.СтатьяКалькуляции = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТрудозатраты

&НаКлиенте
Процедура ТрудозатратыПриАктивизацииСтроки(Элемент)
	
	УстановитьОтборОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыОтметкаПриИзменении(Элемент)
	
	ЗаполнитьКоличествоРезультат(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьВсе(Команда)
	
	ОчиститьСообщения();
	
	ДанныеДляЗавершенияРедактирования = ПодготовитьДанныеДляЗавершенияРедактирования();
	
	Если НЕ ДанныеДляЗавершенияРедактирования.ПроверкаНастроекПройдена Тогда
		
		Возврат;
	
	ИначеЕсли НЕ ДанныеДляЗавершенияРедактирования.ЕстьДействияКВыполнению Тогда
		
		Закрыть(ПараметрыЗакрытия(Истина));
		
	ИначеЕсли Режим = РедакторПроизводственногоПроцессаКлиентСервер.РежимИзФормыЭтапа() Тогда
		
		Закрыть(ПараметрыЗакрытия(Истина, Истина));
		
	Иначе
		
		НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТолькоТехпроцесс(Команда)
	
	Закрыть(ПараметрыЗакрытия(Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьСнятьФлажки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Обеспечение_ЗаполнитьСтатьюКалькуляции(Команда)
	
	ЗаполнитьСтатьюКалькуляцииВСтроках("ОбеспечениеМатериаламиИРаботами");
	
КонецПроцедуры

&НаКлиенте
Процедура Выпуск_ЗаполнитьСтатьюКалькуляции(Команда)
	
	ЗаполнитьСтатьюКалькуляцииВСтроках("ВыходныеИзделия", "ПобочныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты_ЗаполнитьСтатьюКалькуляции(Команда)
	
	ЗаполнитьСтатьюКалькуляцииВСтроках("Трудозатраты");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Для каждого КлючИЗначение Из ОписаниеТаблиц(ЭтотОбъект) Цикл
		
		ОписаниеТЧ = КлючИЗначение.Значение; // см. ОписаниеТаблицыКонструктор
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ЭлементДействие);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Отметка");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Ложь;
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".ТолькоПросмотр");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", КомментарийНеИзменять());
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ЭлементДействие);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".ТолькоПросмотр");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
		Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Комментарий"));
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".ТолькоПросмотр");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Истина;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ЭлементКоличествоТехпроцесс);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".КоличествоТехпроцесс");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		Отбор.ПравоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".КоличествоЭтап");
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ЭлементКоличествоРезультат);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".КоличествоЭтап");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
		Отбор.ПравоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".КоличествоРезультат");
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
		
		//
		Элемент = УсловноеОформление.Элементы.Добавить();
		Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ЭлементКартинкаСтрелка);
		
		Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Отметка");
		Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		Отбор.ПравоеЗначение = Ложь;
		
		Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
		
		Для каждого КлючИЗначение Из ПараметрыПроверкиЗаполненияРеквизитов(ОписаниеТЧ.ИмяТЧ) Цикл
			
			Настройка = КлючИЗначение.Значение;
			
			//
			Элемент = УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Настройка.Элемент);
			
			Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Действие");
			Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
			Отбор.ПравоеЗначение = РедакторПроизводственногоПроцессаКлиентСервер.ДействияИзменениеСуществующих();
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
			Если Настройка.СкрытьЗначение Тогда
				Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
				Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<из документа>';
																			|en = '<from document>'"));
			КонецЕсли;
			
			//
			Элемент = УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Настройка.Элемент);
			
			Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Действие");
			Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			Отбор.ПравоеЗначение = ДействиеДобавить();
			
			Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+"."+КлючИЗначение.Ключ);
			Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
			
			Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".Отметка");
			Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			Отбор.ПравоеЗначение = Истина;
			
			Если ОписаниеТЧ.ИмяТЧ = "ВыходныеИзделия"
				И КлючИЗначение.Ключ = "СтатьяКалькуляции" Тогда
				
				Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".ТипСтоимости");
				Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				Отбор.ПравоеЗначение = Перечисления.ТипыСтоимостиВыходныхИзделий.Фиксированная;
				
			КонецЕсли;
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
			
			//
			Если ОписаниеТЧ.ИмяТЧ = "ВыходныеИзделия"
				И КлючИЗначение.Ключ = "СтатьяКалькуляции" Тогда
				
				Элемент = УсловноеОформление.Элементы.Добавить();
				Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Настройка.Элемент);
					
				Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОписаниеТЧ.ИмяТЧ+".ТипСтоимости");
				Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
				Отбор.ПравоеЗначение = Перечисления.ТипыСтоимостиВыходныхИзделий.Рассчитывается;
					
				Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
			
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	//
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ВыходныеИзделияНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "ВыходныеИзделия.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
	
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ОбеспечениеМатериаламиИРаботамиНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "ОбеспечениеМатериаламиИРаботами.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
	
	//
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "ВыходныеИзделияХарактеристика", "ВыходныеИзделия.ХарактеристикиИспользуются");
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "ОбеспечениеМатериаламиИРаботамиХарактеристика", "ОбеспечениеМатериаламиИРаботами.ХарактеристикиИспользуются");
	
	УстановитьУсловноеОформлениеОбеспеченияМатериаламиИРаботами();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеОбеспеченияМатериаламиИРаботами()
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения.Имя);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Назначение");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Текущий этап';
																|en = 'Current step'"));
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения.Имя);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.ТипНоменклатуры");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Перечисления.ТипыНоменклатуры.МногооборотнаяТара;
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.КоличествоЭтап");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<для товаров и работ>';
																|en = '<for goods and labor>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	ОбеспечениеВДокументахСервер.УстановитьУсловноеОформлениеОбособленно(
		УсловноеОформление,
		Элементы.ОбеспечениеМатериаламиИРаботамиОбособленно,
		"ОбеспечениеМатериаламиИРаботами.ВариантОбеспечения",
		"ОбеспечениеМатериаламиИРаботами.ТипНоменклатуры");
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения.Имя);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.Обособленно");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Ложь;
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ДинамическаяСтруктура");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения.Имя);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.НазначениеОбеспечения");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Назначение");
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ОбеспечениеМатериаламиИРаботами.Обособленно");
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстПредопределенногоЗначения);
	
	// Склад можно выбрать только для товаров
	УстановитьУсловноеОформлениеСклада("ОбеспечениеМатериаламиИРаботами", Элементы.ОбеспечениеМатериаламиИРаботамиСклад.Имя);
	УстановитьУсловноеОформлениеСклада("ВыходныеИзделия", Элементы.ВыходныеИзделияСклад.Имя);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеСклада(ИмяТаблицы, ИмяЭлемента)
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ИмяЭлемента);
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрШаблон("%1.ТипНоменклатуры", ИмяТаблицы));
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = Перечисления.ТипыНоменклатуры.Работа;
	
	Отбор = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СтрШаблон("%1.КоличествоЭтап", ИмяТаблицы));
	Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	Отбор.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаОтмененнойСтрокиДокумента);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Заголовок = СтрШаблон("%1 %2", Заголовок, Параметры.ПредставлениеЭтапа);
	
	Справочники.Назначения.ФормаДокументаПриСозданииНаСервере(ЭтотОбъект);
	
	Элементы.ТрудозатратыСтатьяКалькуляции.Видимость = НЕ Объект.ОперацияКакРаспоряжениеВыработки;
	Элементы.Трудозатраты_ЗаполнитьСтатьюКалькуляции.Видимость = НЕ Объект.ОперацияКакРаспоряжениеВыработки;
	
	ТолькоИзмененные = Истина;
	ОтображаемыеСтроки = УстановитьОтборИзмененныхСтрок(ЭтотОбъект);
	
	УстановитьТекущуюСтраницу(ОтображаемыеСтроки);

	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("ИнформационнаяПанель") Тогда
		
		Элементы.ГруппаИнформационнаяПанель.Видимость = ЗначениеЗаполнено(Элементы.ИнформационнаяПанельНадпись.Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекущуюСтраницу(ОтображаемыеСтроки = Неопределено)
	
	СтраницаЕстьСтроки = Неопределено;
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(ЭтотОбъект) Цикл
		ОписаниеТЧ = КлючИЗначение.Значение;
		Если ЕстьДействияКВыполнению(ЭтотОбъект, ОписаниеТЧ.ИмяТЧ) Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы[ОписаниеТЧ.ЭлементСтраница];
			Возврат;
		КонецЕсли;
		Если СтраницаЕстьСтроки = Неопределено
			И ОтображаемыеСтроки <> Неопределено
			И ОтображаемыеСтроки[ОписаниеТЧ.ИмяТЧ] > 0 Тогда
			СтраницаЕстьСтроки = Элементы[ОписаниеТЧ.ЭлементСтраница]
		КонецЕсли;
	КонецЦикла;
	
	Если СтраницаЕстьСтроки <> Неопределено Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = СтраницаЕстьСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ДлительныеОперацииСтруктура = Новый Структура("ЗаписьДанных");
	
	ЭтапОбъект = Этап.ПолучитьОбъект();
	ЗначениеВРеквизитФормы(ЭтапОбъект, "Объект");
	
	СписокРеквизитов = Новый Массив;
	СписокРеквизитов.Добавить("Договор");
	СписокРеквизитов.Добавить("Партнер");
	СписокРеквизитов.Добавить("НаправлениеДеятельности");
	СписокРеквизитов.Добавить("ХозяйственнаяОперация");
	
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Объект.Подразделение);
	
	ПараметрыЗаполненияНазначений = ОбеспечениеПроизводства.ПараметрыДействияЗаполнитьНазначениеОбеспеченияВЭтапеПроизводства(
		Объект, Объект.ВыходныеИзделия);
		
	Если Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья2_5
		//++ Устарело_Переработка24
		Или Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья
		//-- Устарело_Переработка24
		Или Ложь Тогда
		НазначениеДавальческогоВыпуска = Документы.ЭтапПроизводства2_2.НазначениеПоУмолчаниюДляПобочныхИзделий(Объект.Распоряжение);
	КонецЕсли;
	
	ПараметрыРаспределенияЗатрат = Новый ФиксированнаяСтруктура(Документы.ЭтапПроизводства2_2.ПараметрыРаспределенияЗатрат(Объект));
	
	ОбеспечениеПроизводства.ЗаполнитьСписокВыбораНазначенийВЭтапеПроизводства(
		Объект, Объект.ВыходныеИзделия, ОтборСсылокВФормеВыбораНазначений);
	
	Элементы.ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения.СписокВыбора.ЗагрузитьЗначения(
		ОтборСсылокВФормеВыбораНазначений.ВыгрузитьЗначения());
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Объект, СтрСоединить(СписокРеквизитов, ","));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанные()
	
	АдресПараметровОбновленияЭтапа = Неопределено;
	Если НЕ Параметры.Свойство("АдресПараметровОбновленияЭтапа", АдресПараметровОбновленияЭтапа)
		ИЛИ НЕ ЭтоАдресВременногоХранилища(АдресПараметровОбновленияЭтапа) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОбновления = ПолучитьИзВременногоХранилища(АдресПараметровОбновленияЭтапа);
	
	ОписанияТЧ = ОписаниеТаблиц(ЭтотОбъект);
	
	Для каждого Строка Из ПараметрыОбновления.ТаблицаСопоставления Цикл
		ОписаниеТЧ = ОписанияТЧ[Строка.ИмяТЧ];
		Если ОписаниеТЧ.Использовать Тогда
			Таблица = ЭтотОбъект[ОписаниеТЧ.ИмяТЧ]; // ТаблицаФормы
			НоваяСтрока = Таблица.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			ЗаполнитьСлужебныеРеквизитыСтрокиРесурсов(ЭтотОбъект, НоваяСтрока, ОписаниеТЧ);
			НоваяСтрока.НомерСтроки = Таблица.Количество();
		КонецЕсли;
	КонецЦикла;
	
	ПроверитьЗаполнитьТипСтоимостиВыходныхИзделий();
	
	ПараметрыОбновления.РесурсыТехпроцесса.Свернуть(
		"Номенклатура,Характеристика,ВидРабот,НомерОперации,НаименованиеОперации", "Количество");
	ПараметрыОбновления.РесурсыТехпроцесса.Сортировать("НомерОперации,НаименованиеОперации");
	РесурсыПоОперациям.Загрузить(ПараметрыОбновления.РесурсыТехпроцесса);
	
	СтруктураДействий = Новый Структура;
	СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(ВыходныеИзделия, СтруктураДействий);
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(ОбеспечениеМатериаламиИРаботами, СтруктураДействий);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьОтборИзмененныхСтрок(Форма)
	
	Результат = Новый Структура;
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(Форма) Цикл
		
		ОписаниеТЧ = КлючИЗначение.Значение; // см. ОписаниеТаблицыКонструктор
		
		Если Форма.ТолькоИзмененные Тогда
			Отбор = Новый Структура("Изменено", Истина);
			ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
			КоличествоСтрок = Форма[ОписаниеТЧ.ИмяТЧ].НайтиСтроки(Отбор).Количество();
		Иначе
			ОтборСтрок = Неопределено;
			КоличествоСтрок = Форма[ОписаниеТЧ.ИмяТЧ].Количество();
		КонецЕсли;
		
		Таблица = Форма.Элементы[ОписаниеТЧ.ИмяТЧ]; // ТаблицаФормы
		Таблица.ОтборСтрок = ОтборСтрок;
		
		Страница = Форма.Элементы[ОписаниеТЧ.ЭлементСтраница]; // ГруппаФормы
		Страница.Заголовок = ОписаниеТЧ.Заголовок + ?(КоличествоСтрок > 0, " ("+КоличествоСтрок+")", "");
		
		Результат.Вставить(ОписаниеТЧ.ИмяТЧ, КоличествоСтрок);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборОпераций()
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	Отбор.Вставить("Характеристика", ПредопределенноеЗначение("Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка"));
	Отбор.Вставить("ВидРабот", ПредопределенноеЗначение("Справочник.ВидыРаботСотрудников.ПустаяСсылка"));
	
	ТекущиеДанные = Элементы[ТаблицаТекущая(ЭтотОбъект).ИмяТЧ].ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(Отбор, ТекущиеДанные);
		
	КонецЕсли;
	
	КоличествоОпераций = РесурсыПоОперациям.НайтиСтроки(Отбор).Количество();
	Элементы.РесурсыПоОперациям.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
	
	ЧастиЗаголовка = Новый Массив;
	ЧастиЗаголовка.Добавить(НСтр("ru = 'Операции';
								|en = 'Operations'"));
	Если КоличествоОпераций > 0 Тогда
		ЧастиЗаголовка.Добавить(" (");
		ЧастиЗаголовка.Добавить(КоличествоОпераций);
		ЧастиЗаголовка.Добавить(")");
	КонецЕсли;
	Элементы.ГруппаРесурсыПоОперациям.Заголовок  = СтрСоединить(ЧастиЗаголовка);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыСтрокиРесурсов(Форма, ДанныеСтроки, ОписаниеТЧ)
	
	ДанныеСтроки.Картинка = ОписаниеТЧ.Картинка;
	
	РедакторПроизводственногоПроцессаКлиентСервер.ЗаполнитьПараметрыДействияПоУмолчанию(ДанныеСтроки, ОписаниеТЧ.ИмяТЧ);
	
	Если НЕ ДанныеСтроки.ТолькоПросмотр Тогда;
		Если ДанныеСтроки.Действие = ДействиеДобавить()
				ИЛИ ДанныеСтроки.Действие = ДействиеУвеличить() Тогда
			ДанныеСтроки.КартинкаСтрелка = БиблиотекаКартинок.СтрелкаВверх;
		Иначе
			ДанныеСтроки.КартинкаСтрелка = БиблиотекаКартинок.СтрелкаВниз;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьНазначение(Форма, ДанныеСтроки, ОписаниеТЧ);
	
	ЗаполнитьКоличествоРезультат(Форма, ДанныеСтроки, ОписаниеТЧ);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКоличествоРезультат(Форма, ДанныеСтроки = Неопределено, ОписаниеТЧ = Неопределено)
	
	Если ОписаниеТЧ = Неопределено Тогда
		ОписаниеТЧ = ТаблицаТекущая(Форма);
	КонецЕсли;
	
	Если ДанныеСтроки = Неопределено Тогда
		ТекущаяСтрока = Форма.Элементы[ОписаниеТЧ.ИмяТЧ].ТекущаяСтрока;
		ДанныеСтроки = Форма[ОписаниеТЧ.ИмяТЧ].НайтиПоИдентификатору(ТекущаяСтрока);
	КонецЕсли;
	
	Если НЕ ДанныеСтроки.Отметка Тогда
		ДанныеСтроки.КоличествоРезультат = ДанныеСтроки.КоличествоЭтап;
		Если ОписаниеТЧ.ЕстьУпаковки Тогда
			ДанныеСтроки.КоличествоУпаковокРезультат = ДанныеСтроки.КоличествоУпаковокЭтап;
		КонецЕсли;
	Иначе
		ДанныеСтроки.КоличествоРезультат = ДанныеСтроки.КоличествоТехпроцесс;
		Если ОписаниеТЧ.ЕстьУпаковки Тогда
			ДанныеСтроки.КоличествоУпаковокРезультат = ДанныеСтроки.КоличествоУпаковокТехпроцесс;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНазначение(Форма, ДанныеСтроки, ОписаниеТЧ)
	
	Если НЕ ДанныеСтроки.Изменено
			ИЛИ ДанныеСтроки.ТолькоПросмотр Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеТЧ.ИмяТЧ = "ОбеспечениеМатериаламиИРаботами"
		И ДанныеСтроки.Действие = ДействиеДобавить() Тогда
		ДанныеСтроки.НазначениеОбеспечения = Форма.ПараметрыЗаполненияНазначений.НазначениеМатериалы;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнитьТипСтоимостиВыходныхИзделий()
	
	КоличествоИзделий  = ВыходныеИзделия.Количество();
	
	ЕстьРассчитываемая = ВыходныеИзделия.НайтиСтроки(
		Новый Структура("ТипСтоимости", Перечисления.ТипыСтоимостиВыходныхИзделий.Рассчитывается)).Количество();
	
	Для Индекс = 0 По КоличествоИзделий - 1 Цикл
		Строка = ВыходныеИзделия[КоличествоИзделий - 1 - Индекс];
		Если ЗначениеЗаполнено(Строка.ТипСтоимости) Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ЕстьРассчитываемая
			И УправлениеПроизводствомКлиентСервер.ЭтоВыпускающийЭтап(Объект) Тогда
			Строка.ТипСтоимости = Перечисления.ТипыСтоимостиВыходныхИзделий.Рассчитывается;
			ЕстьРассчитываемая = Истина;
		Иначе
			Строка.ТипСтоимости = Перечисления.ТипыСтоимостиВыходныхИзделий.Фиксированная;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьНастройки()
	
	Отказ = Ложь;
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""%3""';
							|en = 'Column ""%1"" in line #%2, list ""%3"" cannot be empty.'");
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(ЭтотОбъект) Цикл
		
		ОписаниеТЧ = КлючИЗначение.Значение; // см. ОписаниеТаблицыКонструктор
		
		Для каждого Строка Из ЭтотОбъект[ОписаниеТЧ.ИмяТЧ] Цикл
			Если НЕ Строка.Отметка Тогда
				Продолжить;
			КонецЕсли;
			Для каждого КлючИЗначение Из ПараметрыПроверкиЗаполненияРеквизитов(ОписаниеТЧ.ИмяТЧ) Цикл
				
 				Если Строка.КоличествоЭтап <> 0
 					ИЛИ ЗначениеЗаполнено(Строка[КлючИЗначение.Ключ])
 					ИЛИ (ОписаниеТЧ.ИмяТЧ = "ВыходныеИзделия"
							И КлючИЗначение.Ключ = "СтатьяКалькуляции"
							И Строка.ТипСтоимости = ПредопределенноеЗначение("Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается"))
					ИЛИ (ОписаниеТЧ.ИмяТЧ = "Трудозатраты"
							И КлючИЗначение.Ключ = "СтатьяКалькуляции"
							И Объект.ОперацияКакРаспоряжениеВыработки)
					ИЛИ (КлючИЗначение.Ключ = "Склад"
							И Строка.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Работа")) Тогда
					Продолжить;
 				КонецЕсли;
 				
				ТекстСообщения = СтрШаблон(ШаблонСообщения,
					Элементы[КлючИЗначение.Значение.Элемент].Заголовок, Строка.НомерСтроки, ОписаниеТЧ.Заголовок);
				ПутьКДанным = СтрШаблон("%1[%2].%3", ОписаниеТЧ.ИмяТЧ, Строка.НомерСтроки-1, КлючИЗначение.Ключ);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, ПутьКДанным,, Отказ);
				
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат НЕ Отказ;
	
КонецФункции

#Область ОбновлениеДанных

&НаКлиенте
Функция ПодготовитьДанныеДляЗавершенияРедактирования()
	
	Результат = Новый Структура;
	Результат.Вставить("ПроверкаНастроекПройдена", ПроверитьНастройки());
	Результат.Вставить("ЕстьДействияКВыполнению",  ЕстьДействияКВыполнению(ЭтотОбъект));
	
	Если Результат.ПроверкаНастроекПройдена
		И Результат.ЕстьДействияКВыполнению Тогда
		
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ПодготовитьДанныеДляЗавершенияРедактированияНаСервере());
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПодготовитьДанныеДляЗавершенияРедактированияНаСервере()
	
	Результат = Новый Структура;
	
	ЭтапОбъект               = Этап.ПолучитьОбъект();
	ДанныеЭтапаДляОбновления = Новый Структура;
	КэшированныеЗначения     = Неопределено;
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(ЭтотОбъект) Цикл
		
		ОписаниеТЧ = КлючИЗначение.Значение; // см. ОписаниеТаблицыКонструктор
		ИмяТЧ = ОписаниеТЧ.ИмяТЧ;
		
		Если НЕ ЕстьДействияКВыполнению(ЭтотОбъект, ИмяТЧ) Тогда
			Продолжить;
		КонецЕсли;
		
		Для каждого ИмяТЧЭтапа Из СтрРазделить(ОписаниеТЧ.ИмяТЧЭтапа, ",") Цикл
			
			РезультатОбработки = Новый Структура;
			РезультатОбработки.Вставить("ИзмененныеСтроки",  Новый Массив);
			РезультатОбработки.Вставить("ДобавленныеСтроки", Новый Массив);
			
			СтруктураПоиска1     = Новый Структура(ОписаниеТЧ.ПоляПоиска);
			СтруктураПоиска2     = Новый Структура(ОписаниеТЧ.ПоляПоиска);
			
			ТаблицаДокумента = ЭтапОбъект[ИмяТЧЭтапа].Выгрузить(); // ТаблицаЗначений
			ТаблицаДокумента.Колонки.Добавить("ТипНоменклатуры", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыНоменклатуры"));
			ТаблицаДокумента.Индексы.Добавить("Отменено");
			ТаблицаДокумента.Индексы.Добавить(ОписаниеТЧ.ПоляПоиска);
			
			Для каждого СтрокаДействие Из ЭтотОбъект[ИмяТЧ] Цикл
				
				Если НЕ СтрокаДействие.Отметка Тогда
					Продолжить;
				КонецЕсли;
				
				Если (ИмяТЧЭтапа = "ВыходныеИзделия"
						И СтрокаДействие.ТипСтоимости = Перечисления.ТипыСтоимостиВыходныхИзделий.Фиксированная)
					ИЛИ (ИмяТЧЭтапа = "ПобочныеИзделия"
						И СтрокаДействие.ТипСтоимости = Перечисления.ТипыСтоимостиВыходныхИзделий.Рассчитывается) Тогда
					Продолжить;
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска1, СтрокаДействие);
				
				Если СтрокаДействие.Действие = ДействиеДобавить() Тогда
					
					НоваяСтрока = ТаблицаДокумента.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаДействие);
					НоваяСтрока.НомерСтроки = ТаблицаДокумента.Количество();
					
					НоваяСтрока.Количество    = СтрокаДействие.КоличествоТехпроцесс - СтрокаДействие.КоличествоЭтап;
					НоваяСтрока.Подразделение = Объект.Подразделение;
					
					Если ИмяТЧЭтапа = "ОбеспечениеМатериаламиИРаботами" Тогда
						Если НоваяСтрока.Обособленно Тогда
							НоваяСтрока.Назначение = НоваяСтрока.НазначениеОбеспечения;
						КонецЕсли;
						Если СтрокаДействие.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
							НоваяСтрока.ВариантОбеспечения = ?(НоваяСтрока.Обособленно, Перечисления.ВариантыОбеспечения.КОбеспечению, Перечисления.ВариантыОбеспечения.НеТребуется);
						Иначе
							НоваяСтрока.ВариантОбеспечения = Перечисления.ВариантыОбеспечения.КОбеспечению;
						КонецЕсли;
					КонецЕсли;
					
					Если ОписаниеТЧ.ИмяТЧ = "ВыходныеИзделия" Тогда
						НоваяСтрока.Получатель = ?(СтрокаДействие.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа, Объект.Подразделение, СтрокаДействие.Склад);
						НоваяСтрока.ДатаПроизводства = ?(ЭтапОбъект.ПроизводствоОднойДатой, ЭтапОбъект.ДатаПроизводства, ТекущаяДатаСеанса());
					КонецЕсли;
					
					РезультатОбработки.ДобавленныеСтроки.Добавить(НоваяСтрока);
					
				ИначеЕсли СтрокаДействие.Действие = ДействиеУвеличить() Тогда
					
					КоличествоУвеличитьВсего = СтрокаДействие.КоличествоТехпроцесс - СтрокаДействие.КоличествоЭтап;
					
					ТаблицаДокумента.Сортировать("Отменено,НомерСтроки УБЫВ");
					
					Для Индекс = 0 По ТаблицаДокумента.Количество() - 1 Цикл
						Строка = ТаблицаДокумента[Индекс];
						ЗаполнитьЗначенияСвойств(СтруктураПоиска2, Строка);
						Если ОбщегоНазначения.ДанныеСовпадают(СтруктураПоиска1, СтруктураПоиска2)
								И РедакторПроизводственногоПроцессаКлиентСервер.МожноИзменитьСтрокуЭтапа(ИмяТЧЭтапа, Строка, СтрокаДействие.Действие) Тогда
							Строка.Количество = Строка.Количество + КоличествоУвеличитьВсего;
							РезультатОбработки.ИзмененныеСтроки.Добавить(Строка);
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
				ИначеЕсли СтрокаДействие.Действие = ДействиеУменьшить() Тогда
					
					КоличествоУменьшитьВсего = СтрокаДействие.КоличествоЭтап - СтрокаДействие.КоличествоТехпроцесс;
					
					ТаблицаДокумента.Сортировать("Отменено,НомерСтроки УБЫВ");
					
					Для Индекс = 0 По ТаблицаДокумента.Количество() - 1 Цикл
						Строка = ТаблицаДокумента[Индекс];
						ЗаполнитьЗначенияСвойств(СтруктураПоиска2, Строка);
						Если ОбщегоНазначения.ДанныеСовпадают(СтруктураПоиска1, СтруктураПоиска2)
								И РедакторПроизводственногоПроцессаКлиентСервер.МожноИзменитьСтрокуЭтапа(ИмяТЧЭтапа, Строка, СтрокаДействие.Действие) Тогда
							Если КоличествоУменьшитьВсего > 0 Тогда
								КоличествоУменьшить = Мин(Строка.Количество, КоличествоУменьшитьВсего);
								Строка.Количество = Строка.Количество - КоличествоУменьшить;
								КоличествоУменьшитьВсего = КоличествоУменьшитьВсего - КоличествоУменьшить;
								Если Строка.Количество > 0 Тогда
									РезультатОбработки.ИзмененныеСтроки.Добавить(Строка);
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
				ИначеЕсли СтрокаДействие.Действие = ДействиеУдалить() Тогда
					
					Для каждого Строка Из ТаблицаДокумента.НайтиСтроки(СтруктураПоиска1) Цикл
						Строка.Количество = 0;
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для каждого НайденнаяСтрока Из ТаблицаДокумента.НайтиСтроки(Новый Структура("Количество", 0)) Цикл
				ТаблицаДокумента.Удалить(НайденнаяСтрока);
			КонецЦикла;
			
			ВыполнитьДействияДляМассиваСтрок(РезультатОбработки.ИзмененныеСтроки, ОписаниеТЧ, ИмяТЧЭтапа, КэшированныеЗначения);
			ВыполнитьДействияДляМассиваСтрок(РезультатОбработки.ДобавленныеСтроки, ОписаниеТЧ, ИмяТЧЭтапа, КэшированныеЗначения, Истина);
			
			ПроверитьЗаполнитьДолиСтоимости(ТаблицаДокумента, ИмяТЧЭтапа);
			
			ТаблицаДокумента.Сортировать("НомерСтроки");
			
			ДанныеЭтапаДляОбновления.Вставить(ИмяТЧЭтапа, ТаблицаДокумента);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ДанныеЭтапаДляОбновления, АдресДанныхЭтапаДляОбновления);
	
	ПараметрыЗаписиТехпроцесса = ОбщегоНазначения.СкопироватьРекурсивно(ПолучитьИзВременногоХранилища(АдресПараметровЗаписиТехпроцесса));
	ПараметрыЗаписиТехпроцесса.Вставить("ДанныеЭтапаДляОбновления", ДанныеЭтапаДляОбновления);
	
	Результат.Вставить("АдресПараметровЗаписиТехпроцесса", ПоместитьВоВременноеХранилище(ПараметрыЗаписиТехпроцесса, УникальныйИдентификатор));
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования, ЗакрытьПриЗавершении = Истина)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Этап", Этап);
	ДополнительныеПараметры.Вставить("Подразделение", Объект.Подразделение);
	ДополнительныеПараметры.Вставить("ЗакрытьПриЗавершении", ЗакрытьПриЗавершении);
	
	Оповещение = Новый ОписаниеОповещения("ЗаписьДанныхТехпроцессаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	РедакторПроизводственногоПроцессаКлиент.НачатьЗаписьДанныхТехпроцесса(
		ЭтотОбъект, ДанныеДляЗавершенияРедактирования, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьДанныхТехпроцессаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Результат = РедакторПроизводственногоПроцессаКлиент.ЗаписьДанныхТехпроцессаЗавершение(
		ЭтотОбъект, ДлительнаяОперация, ДополнительныеПараметры);
	
	Если Результат.ЕстьОшибкиЗаписиЭтапа Тогда
		
		МассивТекстов = Новый Массив;
		МассивТекстов.Добавить(Результат.ТекстОшибки);
		МассивТекстов.Добавить("");
		МассивТекстов.Добавить(НСтр("ru = 'Выполнить запись техпроцесса без обновления этапа?';
									|en = 'Save the technological process without updating the stage?'"));
		
		Оповещение = Новый ОписаниеОповещения("ЗаписатьТехпроцессБезОбновленияЭтапаЗавершение", ЭтотОбъект);
		
		ПоказатьВопрос(Оповещение,
			СтрСоединить(МассивТекстов, Символы.ПС), РежимДиалогаВопрос.ДаНет,,, НСтр("ru = 'Ошибка записи данных';
																						|en = 'Data saving error'"));
		
	ИначеЕсли Результат.ЗакрытьПриЗавершении Тогда
		
		Модифицированность = Ложь;
		
		Закрыть(ПараметрыЗакрытия(,,Истина));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьТехпроцессБезОбновленияЭтапаЗавершение(РезультатВопроса, ДанныеДляСозданияТехпроцесса) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Закрыть(ПараметрыЗакрытия(Истина));
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействияДляМассиваСтрок(МассивСтрок, ОписаниеТЧ, ИмяТЧЭтапа, КэшированныеЗначения, ДобавленныеСтроки = Ложь)
	
	СтруктураДействий = Новый Структура;
	
	Если ОписаниеТЧ.ЕстьУпаковки Тогда
		СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
	КонецЕсли;
	
	Если ДобавленныеСтроки Тогда
		
		Если ИмяТЧЭтапа = "ВыходныеИзделия" Тогда
			СтруктураДействий.Вставить("ПроверитьЗаполнитьДолюСтоимости", ПараметрыРаспределенияЗатрат);
			СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение", НазначениеДавальческогоВыпуска);
		ИначеЕсли ИмяТЧЭтапа = "ПобочныеИзделия" Тогда
			СтруктураДействий.Вставить("ПроверитьЗаполнитьНазначение", НазначениеДавальческогоВыпуска);
		КонецЕсли;
		
	КонецЕсли;
	
	Для каждого СтрокаТабличнойЧасти Из МассивСтрок Цикл
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаТабличнойЧасти, СтруктураДействий, КэшированныеЗначения);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьЗаполнитьДолиСтоимости(ТаблицаДокумента, ИмяТЧЭтапа)
	
	Если ИмяТЧЭтапа <> "ВыходныеИзделия"
		ИЛИ ТаблицаДокумента.Количество() < 2
		ИЛИ Режим = РедакторПроизводственногоПроцессаКлиентСервер.РежимИзФормыЭтапа()
		ИЛИ Объект.СпособРаспределенияЗатратНаВыходныеИзделия <> Перечисления.СпособыРаспределенияЗатратНаВыходныеИзделия.ПоДолямСтоимости Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого Строка Из ТаблицаДокумента Цикл
		Если НЕ ЗначениеЗаполнено(Строка.ДоляСтоимости) Тогда
			Строка.ДоляСтоимости = 1;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура УстановитьСнятьФлажки(Значение)
	
	ОписаниеТЧ = ТаблицаТекущая(ЭтотОбъект);
	
	Для каждого Строка Из ЭтотОбъект[ОписаниеТЧ.ИмяТЧ] Цикл
		Если НЕ Строка.ТолькоПросмотр Тогда
			Строка.Отметка = Значение;
			ЗаполнитьКоличествоРезультат(ЭтотОбъект, Строка, ОписаниеТЧ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТаблицаТекущая(Форма)
	
	Результат = ОписаниеТаблицыКонструктор();
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(Форма) Цикл
		ОписаниеТЧ = КлючИЗначение.Значение;
		Если Форма.Элементы[ОписаниеТЧ.ЭлементСтраница] = Форма.Элементы.ГруппаСтраницы.ТекущаяСтраница Тогда
			Результат = КлючИЗначение.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблиц(Форма)
	
	Результат = Новый Структура();
	
	Описание = ОписаниеТаблицыКонструктор("ОбеспечениеМатериаламиИРаботами");
	Описание.Картинка   = 2;
	Описание.Заголовок  = НСтр("ru = 'Обеспечение';
								|en = 'Supply'");
	Описание.ПоляПоиска = "Номенклатура,Характеристика";
	Результат.Вставить("МатериалыИУслуги", Описание);
	
	Описание = ОписаниеТаблицыКонструктор("ВыходныеИзделия");
	Описание.ИмяТЧЭтапа = "ВыходныеИзделия,ПобочныеИзделия";
	Описание.Картинка   = 4;
	Описание.Заголовок  = НСтр("ru = 'Выпуск';
								|en = 'Release'");
	Описание.ПоляПоиска = "Номенклатура,Характеристика";
	Результат.Вставить(Описание.ИмяТЧ, Описание);
	
	Описание = ОписаниеТаблицыКонструктор("Трудозатраты");
	Описание.Картинка   = 3;
	Описание.Заголовок  = НСтр("ru = 'Трудозатраты';
								|en = 'Labor costs'");
	Описание.ЕстьУпаковки = Ложь;
	Описание.ПоляПоиска   = "ВидРабот";
	Результат.Вставить(Описание.ИмяТЧ, Описание);
	
	Возврат Результат;
	
КонецФункции

// Описание таблицы конструктор.
// 
// Параметры:
//  ИмяТЧ - Строка - Имя таблицы
// 
// Возвращаемое значение:
//  Структура - Описание таблицы ресурсов:
// * ИмяТЧ - Строка - Имя таблицы
// * ИмяТЧЭтапа - Строка - Перечень таблиц этапа
// * Картинка - Число - индекса картинки
// * ЕстьУпаковки - Булево - есть ли упаковки в таблице
// * Заголовок - Строка - заголовок
// * ПоляПоиска - Строка
// * ПоляСортировки - Строка
&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеТаблицыКонструктор(ИмяТЧ = "")
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяТЧ", ИмяТЧ);
	Результат.Вставить("ИмяТЧЭтапа", ИмяТЧ);
	Результат.Вставить("Картинка", 1);
	Результат.Вставить("ЕстьУпаковки", Истина);
	Результат.Вставить("Заголовок", "");
	Результат.Вставить("ПоляПоиска", "");
	Результат.Вставить("Использовать", Истина);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, СоответствиеЭлементов(ИмяТЧ));
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СоответствиеЭлементов(ИмяТЧ)
	
	Результат = Новый Структура;
	
	Если ИмяТЧ = "ОбеспечениеМатериаламиИРаботами" Тогда
		
		Результат.Вставить("ЭлементСтраница",             "СтраницаОбеспечениеМатериаламиИРаботами");
		Результат.Вставить("ЭлементОтметка",              "ОбеспечениеМатериаламиИРаботамиОтметка");
		Результат.Вставить("ЭлементДействие",             "ОбеспечениеМатериаламиИРаботамиДействие");
		Результат.Вставить("ЭлементКоличествоТехпроцесс", "ОбеспечениеМатериаламиИРаботамиКоличествоУпаковокТехпроцесс");
		Результат.Вставить("ЭлементКоличествоЭтап",       "ОбеспечениеМатериаламиИРаботамиКоличествоУпаковокЭтап");
		Результат.Вставить("ЭлементКоличествоРезультат",  "ОбеспечениеМатериаламиИРаботамиКоличествоУпаковокРезультат");
		Результат.Вставить("ЭлементКартинкаСтрелка",      "ОбеспечениеМатериаламиИРаботамиКартинкаСтрелка");
		
	ИначеЕсли ИмяТЧ = "Трудозатраты" Тогда
		
		Результат.Вставить("ЭлементСтраница",             "СтраницаТрудозатраты");
		Результат.Вставить("ЭлементОтметка",              "ТрудозатратыОтметка");
		Результат.Вставить("ЭлементДействие",             "ТрудозатратыДействие");
		Результат.Вставить("ЭлементКоличествоТехпроцесс", "ТрудозатратыКоличествоТехпроцесс");
		Результат.Вставить("ЭлементКоличествоЭтап",       "ТрудозатратыКоличествоЭтап");
		Результат.Вставить("ЭлементКоличествоРезультат",  "ТрудозатратыКоличествоРезультат");
		Результат.Вставить("ЭлементКартинкаСтрелка",      "ТрудозатратыКартинкаСтрелка");
		
	ИначеЕсли ИмяТЧ = "ВыходныеИзделия" Тогда
		
		Результат.Вставить("ЭлементСтраница",             "СтраницаВыходныеИзделия");
		Результат.Вставить("ЭлементОтметка",              "ВыходныеИзделияОтметка");
		Результат.Вставить("ЭлементДействие",             "ВыходныеИзделияДействие");
		Результат.Вставить("ЭлементКоличествоТехпроцесс", "ВыходныеИзделияКоличествоУпаковокТехпроцесс");
		Результат.Вставить("ЭлементКоличествоЭтап",       "ВыходныеИзделияКоличествоУпаковокЭтап");
		Результат.Вставить("ЭлементКоличествоРезультат",  "ВыходныеИзделияКоличествоУпаковокРезультат");
		Результат.Вставить("ЭлементКартинкаСтрелка",      "ВыходныеИзделияКартинкаСтрелка");
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыПроверкиЗаполненияРеквизитов(ИмяТЧ)
	
	Результат = Новый Структура;
	
	СписокРеквизитов = "Элемент,СкрытьЗначение";
	
	Если ИмяТЧ = "ОбеспечениеМатериаламиИРаботами" Тогда
		
		Результат.Вставить("Склад",                 Новый Структура(СписокРеквизитов, "ОбеспечениеМатериаламиИРаботамиСклад", Истина));
		Результат.Вставить("Обособленно",           Новый Структура(СписокРеквизитов, "ОбеспечениеМатериаламиИРаботамиОбособленно", Ложь));
		Результат.Вставить("НазначениеОбеспечения", Новый Структура(СписокРеквизитов, "ОбеспечениеМатериаламиИРаботамиНазначениеОбеспечения", Истина));
		Результат.Вставить("СтатьяКалькуляции",     Новый Структура(СписокРеквизитов, "ОбеспечениеМатериаламиИРаботамиСтатьяКалькуляции", Истина));
		
	ИначеЕсли ИмяТЧ = "ВыходныеИзделия" Тогда
		
		Результат.Вставить("ТипСтоимости",       Новый Структура(СписокРеквизитов, "ВыходныеИзделияТипСтоимости", Ложь));
		Результат.Вставить("Склад",              Новый Структура(СписокРеквизитов, "ВыходныеИзделияСклад", Истина));
		Результат.Вставить("СтатьяКалькуляции",  Новый Структура(СписокРеквизитов, "ВыходныеИзделияСтатьяКалькуляции", Истина));
		
	ИначеЕсли ИмяТЧ = "Трудозатраты" Тогда
		
		Результат.Вставить("СтатьяКалькуляции",  Новый Структура(СписокРеквизитов, "ТрудозатратыСтатьяКалькуляции", Истина));
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьДействияКВыполнению(Форма, ИмяТЧ = Неопределено)
	
	Для каждого КлючИЗначение Из ОписаниеТаблиц(Форма) Цикл
		ОписаниеТЧ = КлючИЗначение.Значение;
		Если ИмяТЧ = Неопределено ИЛИ ОписаниеТЧ.ИмяТЧ = ИмяТЧ Тогда
			Для каждого Строка Из Форма[ОписаниеТЧ.ИмяТЧ] Цикл
				Если Строка.Отметка Тогда
					Возврат Истина;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Функция ПараметрыЗакрытия(ЗаписатьТехпроцесс = Ложь, ОбновитьЭтап = Ложь, ЗакрытьПриЗавершении = Ложь)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЗаписатьТехпроцесс",   ЗаписатьТехпроцесс);
	Результат.Вставить("ЗакрытьПриЗавершении", ЗакрытьПриЗавершении);
	
	Если НЕ ОбновитьЭтап Тогда
		ПоместитьВоВременноеХранилище(Неопределено, АдресДанныхЭтапаДляОбновления);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьСтатьюКалькуляцииВСтроках(ИмяТЧ, ИмяТЧЭтапа = "")
	
	Если ПустаяСтрока(ИмяТЧЭтапа) Тогда
		ИмяТЧЭтапа = ИмяТЧ;
	КонецЕсли;
	
	Если НЕ РаботаСТабличнымиЧастямиКлиент.ВыбранаСтрокаДляВыполненияКоманды(Элементы[ИмяТЧ]) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипЗатрат", ПроизводствоКлиент.ТипыЗатратДляВыбораСтатьиКалькуляции(ИмяТЧЭтапа));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСтатьюКалькуляцииВСтрокахЗавершение", ЭтотОбъект, ИмяТЧ);
	
	ОткрытьФорму(
		"Справочник.СтатьиКалькуляции.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтатьюКалькуляцииВСтрокахЗавершение(РезультатЗакрытия, ИмяТЧ) Экспорт

	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы[ИмяТЧ].ВыделенныеСтроки;
	
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ТекущиеДанные = ЭтотОбъект[ИмяТЧ].НайтиПоИдентификатору(ИдентификаторСтроки);
		
		Если ТекущиеДанные.ТолькоПросмотр
			ИЛИ НЕ ТекущиеДанные.Действие = ДействиеДобавить()
			ИЛИ (ИмяТЧ = "ВыходныеИзделия" И ТекущиеДанные.ТипСтоимости = ПредопределенноеЗначение("Перечисление.ТипыСтоимостиВыходныхИзделий.Рассчитывается"))  Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТекущиеДанные.СтатьяКалькуляции <> РезультатЗакрытия Тогда
			ТекущиеДанные.СтатьяКалькуляции = РезультатЗакрытия;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Константы

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеДобавить()
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ДействиеДобавить();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеУдалить()
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУдалить();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеУвеличить()
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУвеличить();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДействиеУменьшить()
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ДействиеУменьшить();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КомментарийНеИзменять()
	Возврат НСтр("ru = 'Не изменять';
				|en = 'Do not change'");
КонецФункции

#КонецОбласти

#КонецОбласти
