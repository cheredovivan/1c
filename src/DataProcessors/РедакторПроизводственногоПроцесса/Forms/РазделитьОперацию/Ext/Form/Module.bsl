
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Подразделение      = Параметры.Подразделение;
	Наименование       = Параметры.Наименование;
	Участок            = Параметры.Участок;
	УчастокНов         = Параметры.Участок;
	КоличествоИсходное = Параметры.Количество;
	Количество         = Параметры.Количество;
	ЕдиницаИзмерения   = Параметры.ЕдиницаИзмерения;
	
	Если ЗначениеЗаполнено(Параметры.РабочийЦентр) Тогда
		Если ТипЗнч(Параметры.РабочийЦентр) = Тип("СправочникСсылка.ВидыРабочихЦентров") Тогда
			ВидРабочегоЦентра    = Параметры.РабочийЦентр;
			ВидРабочегоЦентраНов = Параметры.РабочийЦентр;
		Иначе
			ВидРабочегоЦентра    = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.РабочийЦентр, "ВидРабочегоЦентра");
			ВидРабочегоЦентраНов = ВидРабочегоЦентра;
			РабочийЦентр         = Параметры.РабочийЦентр;
			РабочийЦентрНов      = Параметры.РабочийЦентр;
		КонецЕсли;
	КонецЕсли;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Деление операции ""%1""';
								|en = '""%1"" operation division'"), Наименование);
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КоличествоНовПриИзменении(Элемент)
	
	Количество = КоличествоИсходное - КоличествоНов;
	
	ЗаполнитьПредставлениеЕдиницыИзмерения(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Разделить(Команда)
	
	Если КоличествоНов = 0
			ИЛИ Количество = КоличествоИсходное Тогда
		
		ТекстСообщения = НСтр("ru = 'Введены некорректные данные';
								|en = 'Incorrect data entered'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"КоличествоНов");
		
		Возврат;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Участок", УчастокНов);
	Результат.Вставить("РабочийЦентр", ?(ЗначениеЗаполнено(РабочийЦентрНов), РабочийЦентрНов, ВидРабочегоЦентраНов));
	Результат.Вставить("Количество", КоличествоНов);
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.КоличествоНов.МаксимальноеЗначение = Количество;
	
	ОтборФункциональныхОпций = Новый Структура("Подразделение", Подразделение);
	УстановитьПараметрыФункциональныхОпцийФормы(ОтборФункциональныхОпций);
	Элементы.ДекорацияУчасток.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьПроизводственныеУчастки", ОтборФункциональныхОпций);
	
	Связи = Новый Массив;
	Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.ВидРабочегоЦентра", "ВидРабочегоЦентраНов"));
	Если Элементы.ДекорацияУчасток.Видимость Тогда
		Связи.Добавить(Новый СвязьПараметраВыбора("Отбор.Участок", "УчастокНов"));
	КонецЕсли;
	Элементы.РабочийЦентрНов.СвязиПараметровВыбора = Новый ФиксированныйМассив(Связи);
	
	ЗаполнитьПредставлениеЕдиницыИзмерения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПредставлениеЕдиницыИзмерения(Форма)

	Форма.ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Форма.ЕдиницаИзмерения,
		Форма.Количество);
		
	Форма.ЕдиницаИзмеренияПредставлениеНов = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
		Форма.ЕдиницаИзмерения,
		Форма.КоличествоНов);
	
КонецПроцедуры

#КонецОбласти