#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИмяСправочника = Параметры.ИмяСправочника;
	ЗакрыватьПриВыборе = Ложь;
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)
	
	УстановитьОтборПоСтатусу(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПодразделениеПриИзменении(Элемент)
	
	УстановитьОтборПоПодразделению();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНоменклатураПриИзменении(Элемент)
	
	УстановитьОтборПоНоменклатуре();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено ИЛИ ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_СписокПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаОпераций

&НаКлиенте
Процедура ТаблицаОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ПеренестиВыбранныеОперации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВыбранные(Команда)
	
	ПеренестиВыбранныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВсе(Команда)
	
	ПеренестиОперации();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	Список.УсловноеОформление.Элементы.Очистить();
	
	Если ПоМаршрутнойКарте(Параметры) Тогда
		Справочники.МаршрутныеКарты.УстановитьУсловноеОформлениеСпискаМаршрутныхКарт(
			Список.УсловноеОформление);
	Иначе
		Справочники.ТехнологическиеПроцессы.УстановитьУсловноеОформлениеСпискаТехнологическихПроцессов(
			Список.УсловноеОформление);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	ТекстЗапроса = СтрЗаменить("
	|ВЫБРАТЬ
	|	ТаблицаПереопределяемый.Ссылка          КАК Ссылка,
	|	ТаблицаПереопределяемый.ПометкаУдаления КАК ПометкаУдаления,
	|	ТаблицаПереопределяемый.Родитель        КАК Родитель,
	|	ТаблицаПереопределяемый.ЭтоГруппа       КАК ЭтоГруппа,
	|	ТаблицаПереопределяемый.Код             КАК Код,
	|	ТаблицаПереопределяемый.Наименование    КАК Наименование,
	|	ТаблицаПереопределяемый.Статус          КАК Статус,
	|	ТаблицаПереопределяемый.Подразделение   КАК Подразделение,
	|	ТаблицаПереопределяемый.ДляВидаИзделий  КАК ДляВидаИзделий
	|ИЗ 
	|	&ИмяСправочника КАК ТаблицаПереопределяемый", "&ИмяСправочника", ИмяСправочника);
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса    = ТекстЗапроса;
	СвойстваСписка.ОсновнаяТаблица = ИмяСправочника;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	Если Параметры.Свойство("Подразделение", ОтборПодразделение) Тогда
		УстановитьОтборПоПодразделению();
		Если Параметры.Свойство("ПодразделениеТолькоПросмотр") Тогда
			Элементы.ОтборПодразделение.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если ПоМаршрутнойКарте(ЭтотОбъект) Тогда
		Элементы.ОтборСтатус.ОграничениеТипа = Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыМаршрутныхКарт");
	Иначе
		Элементы.ОтборСтатус.ОграничениеТипа = Новый ОписаниеТипов("ПеречислениеСсылка.СтатусыТехнологическихПроцессов");
	КонецЕсли;
	
	Если Параметры.Свойство("Статус", ОтборСтатус) Тогда
		УстановитьОтборПоСтатусу(ЭтотОбъект);
		Если Параметры.Свойство("СтатусТолькоПросмотр") Тогда
			Элементы.ОтборСтатус.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусу(Форма)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Статус",
		Форма.ОтборСтатус,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Форма.ОтборСтатус));

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоПодразделению()
	
	ОтборУстановлен = ЗначениеЗаполнено(ОтборПодразделение);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Подразделение",
		ОтборПодразделение,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ОтборУстановлен);
	
	Элементы.ТаблицаОперацийУчасток.Видимость = ?(ОтборУстановлен,
		ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ОтборПодразделение).ИспользоватьУчастки,
		Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоНоменклатуре()
	
	Если ПоМаршрутнойКарте(ЭтотОбъект) Тогда
		УправлениеДаннымиОбИзделияхКлиентСервер.УстановитьОтборПоНоменклатуреВСпискеМаршрутныхКарт(
			Список, ОтборНоменклатура);
	Иначе
		Справочники.ТехнологическиеПроцессы.УстановитьОтборПоНоменклатуреВСпискеТехнологическихПроцессов(
			Список, ОтборНоменклатура);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СписокПриАктивизацииСтроки()
	
	СписокПриАктивизацииСтрокиНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоМаршрутнойКарте(Форма)
	
	Возврат СтрНайти(Форма.ИмяСправочника,"МаршрутныеКарты") > 0;
	
КонецФункции

&НаСервере
Процедура СписокПриАктивизацииСтрокиНаСервере()
	
	ЗаполнитьТаблицуОпераций();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуОпераций()
	
	ТаблицаОпераций.Очистить();
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПоМаршрутнойКарте(ЭтотОбъект) Тогда
		Операции = Справочники.МаршрутныеКарты.ДанныеМаршрутнойКарты(
			ТекущаяСтрока, 1, Неопределено, Неопределено, "Операции").Операции;
	Иначе
		Операции = Справочники.ТехнологическиеПроцессы.ДанныеТехнологическогоПроцесса(
			ТекущаяСтрока, Справочники.ТехнологическиеПроцессы.ПараметрыВыборкиДанных("Операции")).Операции;
	КонецЕсли;
	
	Для каждого Строка Из Операции Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаОпераций.Добавить(), Строка);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиВыбранныеОперации()
	
	МассивОпераций = Новый Массив;
	Для каждого ИдентификаторСтроки Из Элементы.ТаблицаОпераций.ВыделенныеСтроки Цикл
		МассивОпераций.Добавить(ТаблицаОпераций.НайтиПоИдентификатору(ИдентификаторСтроки).Операция);
	КонецЦикла;
	
	ПеренестиОперации(МассивОпераций);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиОперации(МассивОпераций = Неопределено)
	
	Результат = Новый Структура("ТехнологическийПроцесс,Операции");
	
	Результат.ТехнологическийПроцесс = Элементы.Список.ТекущаяСтрока;
	Если ЗначениеЗаполнено(МассивОпераций) Тогда
		Результат.Операции = МассивОпераций;
	КонецЕсли;
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры

#КонецОбласти