
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизиты();
	
	ЗаполнитьФормуПоПараметрам();

	ЗаполнитьТаблицуЭтапов();
	
	НастроитьЭлементыФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ЭтапыПроизводства"
			ИЛИ ИмяСобытия = "Запись_ПроизводственнаяОперация2_2" Тогда
		ОбновитьДанные();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСостояниеОперацийПриИзменении(Элемент)
	
	Если ОтборСостояниеОпераций = ОтборСостояниеОперацийНеНачаты() Тогда
		ОперацияПрервать = Неопределено;
		ОперацияПродолжить = Неопределено;
	КонецЕсли;
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ТехпроцессТекущийПриИзменении(Элемент)
	
	ОтборЭтапСпецификации = Неопределено;
	
	ТехпроцессТекущийПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТехпроцессТекущийОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	НастроитьПараметрыВыбораТехпроцесса(ЭтотОбъект, Элемент, ВыбранноеЗначение);
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение)
		И ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", ВыбранноеЗначение));
		
		Оповещение = Новый ОписаниеОповещения("ОбработкаВыбораЭтапаСпецификацииЗавершение", ЭтотОбъект);
		
		ОткрытьФорму("Справочник.ЭтапыПроизводства.ФормаВыбора", ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТехпроцессТекущийОчистка(Элемент, СтандартнаяОбработка)
	
	ТехпроцессТекущий = Элемент.ОграничениеТипа.ПривестиЗначение(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ТехпроцессНовыйПриИзменении(Элемент)
	
	ТехпроцессНовыйПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ТехпроцессНовыйОчистка(Элемент, СтандартнаяОбработка)
	
	ТехпроцессНовый = Элемент.ОграничениеТипа.ПривестиЗначение(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПрерватьПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацияПродолжитьПриИзменении(Элемент)
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПриИзменении(Элемент)
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЭтапСпецификацииПриИзменении(Элемент)
	
	ТехпроцессТекущийПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапы

&НаКлиенте
Процедура ЭтапыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Значение = Неопределено;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если Поле.Имя = "ЭтапыЭтап" Тогда
		Значение = ТекущиеДанные.Этап;
	ИначеЕсли Поле.Имя = "ЭтапыРаспоряжение" Тогда
		Значение = ТекущиеДанные.Распоряжение;
	ИначеЕсли Поле.Имя = "ЭтапыИзделие" Тогда
		Значение = ТекущиеДанные.Изделие;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Значение) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьЗначение(,Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыПриАктивизацииСтроки(Элемент)
	
	НастроитьПараметрыВыбораЭтапа(ЭтотОбъект, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗамену(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьНастройкиЗаменыТехпроцессов() Тогда
		
		НачатьЗаменуТехпроцесса();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НайтиЭтапы(Команда)
	
	ОбновитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ЭтапыЕдиницаИзмерения");
	
	ЭлементОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Этапы.Упаковка");
	ЭлементОтбора.ВидСравнения  = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	НастроитьЭлементыВыбораТехпроцесса();
	
	НастроитьПараметрыВыбораЭтапа(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	Если СтруктураРеквизитов.Свойство("Операции") ИЛИ Инициализация Тогда
		
		Элементы.ГруппаОперации.Видимость = Форма.ОтборСостояниеОпераций = ОтборСостояниеОперацийНачаты();
		
		НастроитьТекстПояснения(Форма);
		
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("Техпроцесс") ИЛИ Инициализация Тогда
		
		ТекущийТехпроцессСпецификация = ТипЗнч(Форма.ТехпроцессТекущий) = Тип("СправочникСсылка.РесурсныеСпецификации");
		
		Элементы.ОтборСостояниеОпераций.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.ОтборПодразделение)
			ИЛИ Форма.ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
		
		Элементы.ОтборЭтапСпецификации.АвтоОтметкаНезаполненного = ТекущийТехпроцессСпецификация;
		Элементы.ОтборЭтапСпецификации.ОтметкаНезаполненного = Элементы.ОтборЭтапСпецификации.АвтоОтметкаНезаполненного
			И НЕ ЗначениеЗаполнено(Форма.ТехпроцессТекущий);
		Элементы.ОперацияПрервать.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.ТехпроцессТекущий)
			ИЛИ НЕ ЗначениеЗаполнено(Форма.ОтборЭтапСпецификации) И ТекущийТехпроцессСпецификация;
		Элементы.ОперацияПродолжить.ТолькоПросмотр = НЕ ЗначениеЗаполнено(Форма.ТехпроцессНовый);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыВыбораТехпроцесса()
	
	Типы = Новый Массив;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьТехнологическиеПроцессы") Тогда
		Типы.Добавить(Тип("СправочникСсылка.ТехнологическиеПроцессы"));
	КонецЕсли;
	Элементы.ТехпроцессНовый.ОграничениеТипа = Новый ОписаниеТипов(Типы);
	ТехпроцессНовый = Элементы.ТехпроцессНовый.ОграничениеТипа.ПривестиЗначение(ТехпроцессНовый);
	
	Типы.Добавить(Тип("СправочникСсылка.РесурсныеСпецификации"));
	Элементы.ТехпроцессТекущий.ОграничениеТипа = Новый ОписаниеТипов(Типы);
	ТехпроцессТекущий = Элементы.ТехпроцессТекущий.ОграничениеТипа.ПривестиЗначение(ТехпроцессТекущий);
	
	НастроитьПараметрыВыбораТехпроцесса(ЭтотОбъект, Элементы.ТехпроцессТекущий, ТипЗнч(ТехпроцессТекущий));
	НастроитьПараметрыВыбораТехпроцесса(ЭтотОбъект, Элементы.ТехпроцессНовый, ТипЗнч(ТехпроцессНовый));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПараметрыВыбораТехпроцесса(Форма, Элемент, ВыбранноеЗначение)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Тип") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	
	Если ВыбранноеЗначение = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ОтборСтатус", ПредопределенноеЗначение("Перечисление.СтатусыСпецификаций.Действует")));
	ИначеЕсли ВыбранноеЗначение = Тип("СправочникСсылка.ТехнологическиеПроцессы") Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыТехнологическихПроцессов.Действует")));
	ИначеЕсли ВыбранноеЗначение = Тип("СправочникСсылка.МаршрутныеКарты") Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Статус", ПредопределенноеЗначение("Перечисление.СтатусыМаршрутныхКарт.Действует")));
	КонецЕсли;
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("ПодразделениеТолькоПросмотр", Истина));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("СтатусТолькоПросмотр", Истина));
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьПараметрыВыбораЭтапа(Форма, ПриАктивизацииСтроки = Ложь)
	
	ПараметрыВыбора = Новый Массив;
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	
	Если ЗначениеЗаполнено(Форма.ТехпроцессТекущий) И ТипЗнч(Форма.ТехпроцессТекущий) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", Форма.ТехпроцессТекущий));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("РазрешитьСменуВладельца", Ложь));
	ИначеЕсли ЗначениеЗаполнено(Форма.ОтборЭтапСпецификации) И ПриАктивизацииСтроки Тогда
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(Форма.ОтборЭтапСпецификации) Тогда
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", СпецификацияЭтапа(Форма.ОтборЭтапСпецификации)));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("РазрешитьСменуВладельца", Истина));
	ИначеЕсли Форма.Элементы.Этапы.ТекущаяСтрока <> Неопределено Тогда
		ТекущиеДанные = Форма.Этапы.НайтиПоИдентификатору(Форма.Элементы.Этапы.ТекущаяСтрока);
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.Владелец", ТекущиеДанные.Спецификация));
		ПараметрыВыбора.Добавить(Новый ПараметрВыбора("РазрешитьСменуВладельца", Истина));
	КонецЕсли;
	
	Форма.Элементы.ОтборЭтапСпецификации.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьТекстПояснения(Форма)
	
	ТекстПояснения = "";
	
	Операция1 = ОперацияПрерватьПредставление(Форма);
	Операция2 = ОперацияПродолжитьПредставление(Форма);
	
	Если Форма.ОтборСостояниеОпераций = ОтборСостояниеОперацийНеНачаты() Тогда
		ТекстПояснения = НСтр("ru = 'Текущий техпроцесс будет полностью заменен новым.';
								|en = 'The current technological process will be completely replaced with a new one.'");
	ИначеЕсли НЕ ЗначениеЗаполнено(Операция1) И НЕ ЗначениеЗаполнено(Операция2) Тогда
		ТекстПояснения = НСтр("ru = 'После последней операции текущего техпроцесса выполнение будет продолжено с первой операции нового техпроцесса.';
								|en = 'After the last operation of the current technological process, execution will continue from the first operation of the new technological process.'");
	ИначеЕсли ЗначениеЗаполнено(Операция1) И НЕ ЗначениеЗаполнено(Операция2) Тогда
		ТекстПояснения = СтрШаблон(
			НСтр("ru = 'Выполнение текущего техпроцесса будет прервано после операции ""%1"" и продолжено с первой операции нового техпроцесса.';
				|en = 'The current technological process will be interrupted after the ""%1"" operation and continued from the first operation of the new technological process.'"),
				Операция1);
	ИначеЕсли НЕ ЗначениеЗаполнено(Операция1) И ЗначениеЗаполнено(Операция2) Тогда
		ТекстПояснения = СтрШаблон(
			НСтр("ru = 'После последней операции текущего техпроцесса выполнение будет продолжено с операции ""%1"" нового техпроцесса.';
				|en = 'After the last operation of the current technological process, execution will continue from the ""%1"" operation of the new technological process.'"),
				Операция2);
	ИначеЕсли ЗначениеЗаполнено(Операция1) И ЗначениеЗаполнено(Операция2) Тогда
		ТекстПояснения = СтрШаблон(
			НСтр("ru = 'Выполнение текущего техпроцесса будет прервано после операции ""%1"" и продолжено с операции ""%2"" нового техпроцесса.';
				|en = 'The current technological process will be interrupted after the ""%1"" operation and continued from the ""%2"" operation of the new technological process.'"),
				Операция1, Операция2);
	КонецЕсли;
	
	Форма.ТекстПояснения = ТекстПояснения;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ДлительныеОперацииСтруктура = Новый Структура("ЗаменаТехпроцесса");
	
	АдресДополнительныхДанных = ПоместитьВоВременноеХранилище(Новый Структура, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборПоПодразделению()
	
	ВладелецОпераций = ВладелецОперацийТекущегоТехпроцесса(ЭтотОбъект);
	Если ЗначениеЗаполнено(ВладелецОпераций) Тогда
		ОтборПодразделение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВладелецОпераций, "Подразделение");
	Иначе
		ОтборПодразделение = Справочники.СтруктураПредприятия.ПустаяСсылка();
	КонецЕсли;
	
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ОтборПодразделение);
	
	Если ПараметрыПодразделения.ИспользоватьПооперационноеПланирование Тогда
		ОтборСостояниеОпераций = ОтборСостояниеОперацийНеНачаты();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ВыбранныеЭтапы(Форма)
	
	Результат = Новый Массив;
	
	Для каждого ИдентификаторСтроки Из Форма.Элементы.Этапы.ВыделенныеСтроки Цикл
		Результат.Добавить(Форма.Этапы.НайтиПоИдентификатору(ИдентификаторСтроки).Этап);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

#Область Отборы

&НаСервереБезКонтекста
Функция СпецификацияЭтапа(ЭтапСпецификации)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтапСпецификации, "Владелец");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВладелецОперацийТекущегоТехпроцесса(Форма)
	
	Возврат ?(ТипЗнч(Форма.ТехпроцессТекущий) = Тип("СправочникСсылка.РесурсныеСпецификации"),
		Форма.ОтборЭтапСпецификации, Форма.ТехпроцессТекущий);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораЭтапаСпецификацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		ОтборЭтапСпецификации = Результат;
		
		ОбработкаВыбораЭтапаСпецификацииЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораЭтапаСпецификацииЗавершениеНаСервере()
	
	ЗаполнитьОтборПоПодразделению();
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОтборСостояниеОперацийНеНачаты()
	Возврат 0;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтборСостояниеОперацийНачаты()
	Возврат 1;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтборВидТехпроцессаТекущий()
	Возврат "ТехпроцессТекущий";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтборВидТехпроцессаНовый()
	Возврат "ТехпроцессНовый";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭлементВыбораОперации(ВидТехпроцесса)
	Если ВидТехпроцесса = ОтборВидТехпроцессаТекущий() Тогда
		Возврат "ОперацияПрервать";
	ИначеЕсли ВидТехпроцесса = ОтборВидТехпроцессаНовый() Тогда
		Возврат "ОперацияПродолжить";
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОперацияПрерватьПредставление(Форма)
	ЭлементСписка = Форма.Элементы.ОперацияПрервать.СписокВыбора.НайтиПоЗначению(Форма.ОперацияПрервать);
	Возврат ?(ЭлементСписка <> Неопределено, ЭлементСписка.Представление, "");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОперацияПродолжитьПредставление(Форма)
	ЭлементСписка = Форма.Элементы.ОперацияПродолжить.СписокВыбора.НайтиПоЗначению(Форма.ОперацияПродолжить);
	Возврат ?(ЭлементСписка <> Неопределено, ЭлементСписка.Представление, "");
КонецФункции

#КонецОбласти

#Область ЗаполнениеДанных

&НаСервере
Процедура ЗаполнитьФормуПоПараметрам()
	
	МассивОтборов = Новый Массив;
	
	Этап = Неопределено;
	Если Параметры.Свойство("Этап", Этап) Тогда
		МассивОтборов.Добавить("Ссылка = &Этап");
		МассивОтборов.Добавить("&ТехнологическийПроцесс_.Статус В (
		|ЗНАЧЕНИЕ(Перечисление.СтатусыСпецификаций.Действует),
		|ЗНАЧЕНИЕ(Перечисление.СтатусыТехнологическихПроцессов.Действует),
		|ЗНАЧЕНИЕ(Перечисление.СтатусыМаршрутныхКарт.Действует))");
		МассивОтборов.Добавить("&ТипТехнологическогоПроцесса_ <> ЗНАЧЕНИЕ(Перечисление.ТипыТехнологическихПроцессовЭтапа.Индивидуальный)");
	КонецЕсли;
	
	ТехнологическийПроцесс = Неопределено;
	Если Параметры.Свойство("ТехнологическийПроцесс", ТехнологическийПроцесс) Тогда
		МассивОтборов.Добавить("&ТехнологическийПроцесс_ = &ТехпроцессТекущий");
		ТехпроцессТекущий = ТехнологическийПроцесс;
	КонецЕсли;
	
	Если МассивОтборов.ВГраница() = -1 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаЭтапы(МассивОтборов);
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("ТехпроцессТекущий", ТехнологическийПроцесс);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ТехпроцессТекущий     = Выборка.ТехнологическийПроцесс;
		ОтборЭтапСпецификации = Выборка.ТехнологическийПроцессЭтап;
		ПоместитьДанныеТехпроцесса(ОтборВидТехпроцессаТекущий(), ТехпроцессТекущий, ОтборЭтапСпецификации);
		ЗаполнитьОтборПоПодразделению();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЭтапов()
	
	ТекущаяСтрока = Элементы.Этапы.ТекущаяСтрока;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущийЭтап = Этапы.НайтиПоИдентификатору(ТекущаяСтрока).Этап;
	КонецЕсли;
	
	Этапы.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ТехпроцессТекущий) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТехпроцессТекущий", ТехпроцессТекущий);
	Запрос.УстановитьПараметр("ОтборЭтапСпецификации", ОтборЭтапСпецификации);
	Запрос.УстановитьПараметр("ОтборПодразделение", ОтборПодразделение);
	
	МассивОтборов = Новый Массив;
	МассивОтборов.Добавить("НЕ &ТипТехнологическогоПроцесса_ = ЗНАЧЕНИЕ(Перечисление.ТипыТехнологическихПроцессовЭтапа.Индивидуальный)");
	МассивОтборов.Добавить("НЕ Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)");
	МассивОтборов.Добавить("&ТехнологическийПроцесс_ = &ТехпроцессТекущий");
	МассивОтборов.Добавить("ВЫБОР
	|КОГДА &ТехнологическийПроцесс_ ССЫЛКА Справочник.РесурсныеСпецификации
	|	ТОГДА &ТехнологическийПроцессЭтап_ = &ОтборЭтапСпецификации
	|КОГДА &ОтборЭтапСпецификации <> ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
	|	ТОГДА Этапы.Этап = &ОтборЭтапСпецификации
	|ИНАЧЕ
	|	ИСТИНА
	|КОНЕЦ");
	
	Если ЗначениеЗаполнено(ОтборИзделие) ИЛИ ЗначениеЗаполнено(ОтборНазначение) Тогда
		
		Подгруппа1 = Новый Массив;
		Подгруппа2 = Новый Массив;
		
		Если ЗначениеЗаполнено(ОтборИзделие) Тогда
			Подгруппа1.Добавить("Этапы.ПартияПроизводства.ОсновноеИзделиеНоменклатура = &ОтборИзделие");
			Подгруппа2.Добавить("Т.Номенклатура = &ОтборИзделие");
			Запрос.УстановитьПараметр("ОтборИзделие", ОтборИзделие);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ОтборНазначение) Тогда
			Подгруппа1.Добавить("Этапы.ПартияПроизводства.Назначение = &ОтборНазначение");
			Подгруппа2.Добавить("Т.Назначение = &ОтборНазначение");
			Запрос.УстановитьПараметр("ОтборНазначение", ОтборНазначение);
		КонецЕсли;
		
		ТекстШаблона = "((%1)
		|ИЛИ ИСТИНА В (
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА
		|	ИЗ
		|		Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК Т
		|	ГДЕ
		|		Этапы.Ссылка = Т.Ссылка
		|		И %2
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ ПЕРВЫЕ 1
		|		ИСТИНА
		|	ИЗ
		|		Документ.ЭтапПроизводства2_2.ПобочныеИзделия КАК Т
		|	ГДЕ
		|		Этапы.Ссылка = Т.Ссылка
		|		И %2))
		|";
		
		МассивОтборов.Добавить(СтрШаблон(ТекстШаблона, СтрСоединить(Подгруппа1," И "), СтрСоединить(Подгруппа2," И ")));
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ОтборМатериал) Тогда
		
		МассивОтборов.Добавить("ИСТИНА В (
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Т
		|ГДЕ
		|	Этапы.Ссылка = Т.Ссылка
		|	И Т.Номенклатура = &ОтборМатериал
		|)");
		Запрос.УстановитьПараметр("ОтборМатериал", ОтборМатериал);
		
	КонецЕсли;
	
	Если ОтборСостояниеОпераций = ОтборСостояниеОперацийНеНачаты() Тогда
		
		МассивОтборов.Добавить("НЕ ИСТИНА В (
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Т
		|ГДЕ
		|	Этапы.Ссылка = Т.Этап
		|	И Т.Создано > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т
		|ГДЕ
		|	Этапы.Ссылка = Т.Этап
		|	И Т.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|)");
		
	Иначе
		
		МассивОтборов.Добавить("ИСТИНА В (
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Т
		|ГДЕ
		|	Этапы.Ссылка = Т.Этап
		|	И Т.Создано > 0
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т
		|ГДЕ
		|	Этапы.Ссылка = Т.Этап
		|	И Т.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|)");
		
		Если ЗначениеЗаполнено(ОперацияПрервать) Тогда
			
			МассивОтборов.Добавить("НЕ ИСТИНА В (
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Т
			|ГДЕ
			|	Этапы.Ссылка = Т.Этап
			|	И Т.Создано > 0
			|	И Т.НомерОперации > &ОперацияПрерватьНомер
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т
			|ГДЕ
			|	Этапы.Ссылка = Т.Этап
			|	И Т.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
			|	И т.НомерОперации > &ОперацияПрерватьНомер)
			|");
			ДанныеОперации = ДанныеОперацииТехпроцессаПомещенного(ОтборВидТехпроцессаТекущий(), ОперацияПрервать);
			Запрос.УстановитьПараметр("ОперацияПрерватьНомер", ?(ДанныеОперации = Неопределено, 0, ДанныеОперации.НомерОперации));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапросаЭтапы(МассивОтборов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = Этапы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		Если Выборка.Назначена Тогда
			НоваяСтрока.ТекущаяОперацияСостояние = Перечисления.СостоянияВыполненияОпераций.Назначена;
		КонецЕсли;
		
		Если НоваяСтрока.Этап = ТекущийЭтап Тогда
			Элементы.Этапы.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ТехпроцессТекущийПриИзмененииНаСервере()
	
	ОперацияПрервать = Неопределено;
	
	ЗаполнитьОтборПоПодразделению();
	
	НастроитьПараметрыВыбораТехпроцесса(ЭтотОбъект, Элементы.ТехпроцессТекущий, ТипЗнч(ТехпроцессТекущий));
	
	НастроитьПараметрыВыбораЭтапа(ЭтотОбъект);
	
	ПоместитьДанныеТехпроцесса(ОтборВидТехпроцессаТекущий(), ТехпроцессТекущий, ОтборЭтапСпецификации);
	
	ОбновитьДанные();
	
КонецПроцедуры

&НаСервере
Процедура ТехпроцессНовыйПриИзмененииНаСервере()
	
	ОперацияПродолжить = Неопределено;
	
	НастроитьПараметрыВыбораТехпроцесса(ЭтотОбъект, Элементы.ТехпроцессНовый, ТипЗнч(ТехпроцессНовый));
	
	ПоместитьДанныеТехпроцесса(ОтборВидТехпроцессаНовый(), ТехпроцессНовый);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанные()
	
	ЗаполнитьТаблицуЭтапов();
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьДанныеТехпроцесса(ВидТехпроцесса, ТехнологическийПроцесс, ЭтапСпецификации = Неопределено)
	
	ДанныеЭтапа = Новый Структура("ТехнологическийПроцесс");
	
	Если ЗначениеЗаполнено(ТехнологическийПроцесс) Тогда
		ДанныеЭтапа.Вставить("ТехнологическийПроцесс", ТехнологическийПроцесс);
		ДанныеЭтапа.Вставить("Номенклатура",           Справочники.Номенклатура.ПустаяСсылка());
		ДанныеЭтапа.Вставить("Характеристика",         Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
		ДанныеЭтапа.Вставить("ЭтапСпецификации",       ЭтапСпецификации);
		ДанныеЭтапа.Вставить("КоличествоУпаковокПлан", 1);
		ДанныеЭтапа.Вставить("КоэффициентУпаковки",    1);
		ДанныеЭтапа.Вставить("КоэффициентТехнологическогоПроцесса", 1);
	КонецЕсли;
	
	ТаблицаОпераций = РегистрыСведений.ОчередьПроизводственныхОпераций.СписокОперацийПоДаннымЭтапа(ДанныеЭтапа);
	
	Элемент = Элементы[ЭлементВыбораОперации(ВидТехпроцесса)]; // ПолеВыбора
	Элемент.СписокВыбора.Очистить();
	Для каждого Строка Из ТаблицаОпераций Цикл
		Элемент.СписокВыбора.Добавить(Формат(Строка.ИдентификаторОперации,"ЧГ=;"), Строка.ОперацияПредставление);
	КонецЦикла;
	
	ДополнительныеДанные = ПолучитьИзВременногоХранилища(АдресДополнительныхДанных);
	ДанныеТехпроцессов   = Неопределено;
	Если НЕ ДополнительныеДанные.Свойство("ДанныеТехпроцессов", ДанныеТехпроцессов) Тогда
		ДанныеТехпроцессов = Новый Структура;
		ДополнительныеДанные.Вставить("ДанныеТехпроцессов", ДанныеТехпроцессов);
	КонецЕсли;
	ДанныеТехпроцессов.Вставить(ВидТехпроцесса, ТаблицаОпераций);
	
	АдресДополнительныхДанных = ПоместитьВоВременноеХранилище(ДополнительныеДанные, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ДанныеТехпроцессаПомещенные(ВидТехпроцесса)
	
	ДополнительныеДанные = ПолучитьИзВременногоХранилища(АдресДополнительныхДанных);
	Если НЕ ДополнительныеДанные.Свойство("ДанныеТехпроцессов")
		ИЛИ НЕ ДополнительныеДанные.ДанныеТехпроцессов.Свойство(ВидТехпроцесса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДополнительныеДанные.ДанныеТехпроцессов[ВидТехпроцесса];
	
КонецФункции

&НаСервере
Функция ДанныеОперацииТехпроцессаПомещенного(ВидТехпроцесса, ИдентификаторОперации)
	
	СтрокаТехпроцесса = Неопределено;
	ДанныеТехпроцесса = ДанныеТехпроцессаПомещенные(ВидТехпроцесса); // см. Справочники.ТехнологическиеОперации.ТаблицаОперацииКонструктор
	
	Если НЕ ЗначениеЗаполнено(ДанныеТехпроцесса) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторОперации) Тогда
		СтрокаТехпроцесса = ДанныеТехпроцесса.Найти(Число(ИдентификаторОперации), "ИдентификаторОперации");
	ИначеЕсли ВидТехпроцесса = ОтборВидТехпроцессаТекущий() Тогда
		СтрокаТехпроцесса = ДанныеТехпроцесса[ДанныеТехпроцесса.Количество()-1];
	ИначеЕсли ВидТехпроцесса = ОтборВидТехпроцессаНовый() Тогда
		СтрокаТехпроцесса = ДанныеТехпроцесса[0];
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТехпроцесса) Тогда
		Возврат Неопределено;
	Иначе
		Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(СтрокаТехпроцесса);
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаЭтапы(МассивОтборов = Неопределено)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Этапы.Дата                                             КАК Дата,
	|	Этапы.ПартияПроизводства.ОсновноеИзделиеНоменклатура   КАК Изделие,
	|	Этапы.КоличествоУпаковокПлан                           КАК Количество,
	|	Этапы.УпаковкаПлан                                     КАК Упаковка,
	|	Этапы.Ссылка                                           КАК Этап,
	|	Этапы.Статус                                           КАК Статус,
	|	Этапы.Спецификация                                     КАК Спецификация,
	|	Этапы.Этап                                             КАК ЭтапСпецификации,
	|	Этапы.Распоряжение                                     КАК Распоряжение,
	|	Очередь.Операция                                       КАК ТекущаяОперация,
	|	Очередь.Состояние                                      КАК ТекущаяОперацияСостояние,
	|	НЕ Назначенные.Операция IS NULL                        КАК Назначена,
	|	ЕСТЬNULL(Техпроцесс.ИдентификаторПервойОперации, 0)    КАК ИдентификаторПервойОперации,
	|	ЕСТЬNULL(Техпроцесс.ИдентификаторПоследнейОперации, 0) КАК ИдентификаторПоследнейОперации,
	|	&ПараметрыТехпроцесса
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК Этапы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
	|	ПО Этапы.Ссылка = Техпроцесс.Этап
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
	|	ПО Этапы.Ссылка = Очередь.Этап
	|		И Очередь.ОсновнаяЗапись
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Назначенные
	|	ПО Этапы.Ссылка = Назначенные.Этап
	|		И Очередь.Операция = Назначенные.Операция
	|		И Очередь.ИдентификаторОперации = Назначенные.ИдентификаторОперации
	|		И Назначенные.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
	|
	|ГДЕ
	|	Этапы.Проведен 
	|
	|	И (Очередь.Операция IS NULL
	|		ИЛИ Очередь.ИдентификаторОперации В (
	|				ВЫБРАТЬ ПЕРВЫЕ 1
	|					Т1.ИдентификаторОперации
	|				ИЗ
	|					РегистрСведений.ОчередьПроизводственныхОпераций КАК Т1
	|				
	|					ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Т2
	|					ПО Т1.Этап = Т2.Этап
	|						И Т1.Операция = Т2.Операция
	|						И Т1.ИдентификаторОперации = Т2.ИдентификаторОперации
	|						И Т2.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
	|				
	|				ГДЕ
	|					Этапы.Ссылка = Т1.Этап
	|					И Т1.ОсновнаяЗапись
	|					И НЕ (Т1.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияВыполненияОпераций.Выполнена)
	|							И Т1.НомерСледующейОперации > 0)
	|					И (НЕ Т1.Состояние В (
	|							ЗНАЧЕНИЕ(Перечисление.СостоянияВыполненияОпераций.ОжиданиеПредшествующих),
	|							ЗНАЧЕНИЕ(Перечисление.СостоянияВыполненияОпераций.НачатаПредшествующая))
	|						ИЛИ НЕ Т2.ИдентификаторОперации IS NULL)
	|			УПОРЯДОЧИТЬ ПО
	|				ISNULL(Т2.Порядок, Т1.Порядок) УБЫВ,
	|				Т1.НомерОперации УБЫВ))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Этапы.МоментВремени
	|";
	
	ПереченьДанных = РедакторПроизводственногоПроцесса.ПараметрыТехпроцессаПереченьДанных("ТехнологическийПроцессЭтап");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПараметрыТехпроцесса",
		РедакторПроизводственногоПроцесса.ТекстЗапросаПараметрыТехпроцессаЭтапа("Этапы",,ПереченьДанных));
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Пакет = СхемаЗапроса.ПакетЗапросов[0];
	
	Если ЗначениеЗаполнено(МассивОтборов) Тогда
		Для каждого ТекстОтбора Из МассивОтборов Цикл
			Для каждого КолонкаПакета Из Пакет.Колонки Цикл
				ТекстОтбора = СтрЗаменить(ТекстОтбора, "&"+КолонкаПакета.Псевдоним+"_", "("+КолонкаПакета.Поля[0]+")");
			КонецЦикла;
			Пакет.Операторы[0].Отбор.Добавить(ТекстОтбора);
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

#Область ВыполнениеЗамены

&НаСервере
Функция ТаблицаОперацийДляЗамены(ОграниченияТехпроцесса = Неопределено)
	
	ТаблицаОпераций1 = ДанныеТехпроцессаПомещенные(ОтборВидТехпроцессаТекущий());
	ТаблицаОпераций2 = ДанныеТехпроцессаПомещенные(ОтборВидТехпроцессаНовый());
	
	Если НЕ ЗначениеЗаполнено(ТаблицаОпераций1)
		ИЛИ НЕ ЗначениеЗаполнено(ТаблицаОпераций2) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеОперации1 = Неопределено;
	ДанныеОперации2 = ДанныеОперацииТехпроцессаПомещенного(ОтборВидТехпроцессаНовый(), ОперацияПродолжить);
	
	Если ОграниченияТехпроцесса <> Неопределено Тогда
		РегистрыСведений.ОчередьПроизводственныхОпераций.ПрименитьОграниченияТехпроцесса(ТаблицаОпераций1, ОграниченияТехпроцесса);
		Если ТаблицаОпераций1.Количество() > 0 Тогда
			ТекущееСмещение = 1 - ТаблицаОпераций1[0].НомерОперации;
			Для каждого ДанныеСтроки Из ТаблицаОпераций1 Цикл
				ДанныеСтроки.НомерОперации          = ДанныеСтроки.НомерОперации + ТекущееСмещение;
				ДанныеСтроки.НомерСледующейОперации = ДанныеСтроки.НомерСледующейОперации + ТекущееСмещение;
				Если ДанныеОперации1 = Неопределено
						И (ЗначениеЗаполнено(ОперацияПрервать) И ДанныеСтроки.ИдентификаторОперации = Число(ОперацияПрервать)
							ИЛИ ДанныеСтроки = ТаблицаОпераций1[ТаблицаОпераций1.Количество()-1]) Тогда
					ДанныеОперации1 = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ДанныеСтроки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	Иначе
		ДанныеОперации1 = ДанныеОперацииТехпроцессаПомещенного(ОтборВидТехпроцессаТекущий(), ОперацияПрервать);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДанныеОперации1)
			ИЛИ НЕ ЗначениеЗаполнено(ДанныеОперации2) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ТекущийИдентификатор = 0;
	ТекущееСмещение = ДанныеОперации1.НомерОперации - ДанныеОперации2.НомерОперации + 1;
	ТаблицаОпераций = Справочники.ТехнологическиеОперации.ТаблицаОперацииКонструктор();
	
	Для Индекс = 0 По ТаблицаОпераций1.Количество() - 1 Цикл
		ДанныеСтроки = ТаблицаОпераций1[Индекс];
		Если ДанныеСтроки.НомерОперации <= ДанныеОперации1.НомерОперации Тогда
			НоваяСтрока = ТаблицаОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
			ТекущийИдентификатор = Макс(ТекущийИдентификатор, ДанныеСтроки.ИдентификаторОперации);
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтрока.НомерСледующейОперации = НоваяСтрока.НомерОперации + 1;
	
	Для Индекс = 0 По ТаблицаОпераций2.Количество() - 1 Цикл
		ДанныеСтроки = ТаблицаОпераций2[Индекс];
		Если ДанныеСтроки.НомерОперации >= ДанныеОперации2.НомерОперации Тогда
			ТекущийИдентификатор = ТекущийИдентификатор + 1;
			НоваяСтрока = ТаблицаОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
			НоваяСтрока.ИдентификаторОперации  = ТекущийИдентификатор;
			НоваяСтрока.НомерОперации          = ДанныеСтроки.НомерОперации + ТекущееСмещение;
			НоваяСтрока.НомерСледующейОперации = ДанныеСтроки.НомерСледующейОперации + ТекущееСмещение;
		КонецЕсли;
	КонецЦикла;
	
	НоваяСтрока.НомерСледующейОперации = 0;
	
	Возврат ТаблицаОпераций;
	
КонецФункции

&НаКлиенте
Функция ПроверитьНастройкиЗаменыТехпроцессов()
	
	Если ВыбранныеЭтапы(ЭтотОбъект).ВГраница() = -1 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо выделить этапы производства.';
														|en = 'Select production stages.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТехпроцессТекущий)
		ИЛИ НЕ ЗначениеЗаполнено(ТехпроцессНовый) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо указать текущий и новый техпроцессы.';
														|en = 'Specify the current and new technological processes.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Если ТехпроцессТекущий = ТехпроцессНовый Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Текущий и новый техпроцессы совпадают.';
														|en = 'The current and new technological processes match.'"));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ПроверитьНастройкиЗаменыТехпроцессовНаСервере();
	
КонецФункции

&НаСервере
Функция ПроверитьНастройкиЗаменыТехпроцессовНаСервере()
	
	Отказ = Ложь;
	
	Ссылки = Новый Массив;
	Ссылки.Добавить(ВладелецОперацийТекущегоТехпроцесса(ЭтотОбъект));
	Ссылки.Добавить(ТехпроцессНовый);
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(Ссылки, "Подразделение");
	
	Если ЗначенияРеквизитов[Ссылки[0]] <> ЗначенияРеквизитов[Ссылки[1]] Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не совпадают подразделения текущего и нового техпроцессов (""%1"", ""%2"").';
										|en = 'The business units of the current and new technological processes do not match (""%1"", ""%2"").'"),
			ЗначенияРеквизитов[Ссылки[0]], ЗначенияРеквизитов[Ссылки[1]]);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
		Возврат НЕ Отказ;
	КонецЕсли;
	
	Если ОтборСостояниеОпераций = ОтборСостояниеОперацийНачаты() Тогда
		
		ТаблицаОпераций = ТаблицаОперацийДляЗамены();
		
		Если ЗначениеЗаполнено(ТаблицаОпераций) Тогда 
			
			ТаблицаПроверкиПорядка = РедакторПроизводственногоПроцесса.ТаблицаПроверкиПорядкаЭлементовПроизводственногоПроцессаКонструктор();
			
			Для каждого Строка Из ТаблицаОпераций Цикл
				
				НоваяСтрока = ТаблицаПроверкиПорядка.Добавить();
				НоваяСтрока.Объект                 = Строка;
				НоваяСтрока.Операция               = Строка.Операция;
				НоваяСтрока.Представление          = Строка.ОперацияПредставление;
				НоваяСтрока.НомерОперации          = Строка.НомерОперации;
				НоваяСтрока.НомерСледующейОперации = Строка.НомерСледующейОперации;
				
			КонецЦикла; 
			
			РедакторПроизводственногоПроцесса.ПроверитьПорядокЭлементовПроизводственногоПроцесса(
				ТаблицаПроверкиПорядка,
				РедакторПроизводственногоПроцесса.СтруктураПроверокПоследовательностиОпераций(, ПараметрыПодразделения),
				Отказ);
			
		Иначе
			
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Ошибка формирования индивидуального техпроцесса.';
														|en = 'An error occurred when generating an individual technological process.'"));
			Возврат НЕ Отказ;
			
		КонецЕсли;
	
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ФоноваяОперацияЗаменаТехпроцесса()
	
	Возврат НСтр("ru = 'Замена технологического процесса в этапах.';
				|en = 'Replacing the technological process in stages.'");
	
КонецФункции

&НаКлиенте
Процедура НачатьЗаменуТехпроцесса()
	
	Если ДлительныеОперацииСтруктура.ЗаменаТехпроцесса <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачатьЗаменуТехпроцессаНаСервере();
	
	Если ДлительныеОперацииСтруктура.ЗаменаТехпроцесса <> Неопределено Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		ПараметрыОжидания.ТекстСообщения = ФоноваяОперацияЗаменаТехпроцесса();
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительныеОперацииСтруктура.ЗаменаТехпроцесса,
			Новый ОписаниеОповещения("ЗаменаТехпроцессаЗавершение", ЭтотОбъект),
			ПараметрыОжидания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьЗаменуТехпроцессаНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне    = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = ФоноваяОперацияЗаменаТехпроцесса();
	
	ДлительныеОперацииСтруктура.ЗаменаТехпроцесса = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.РедакторПроизводственногоПроцесса.ГрупповаяЗаменаВФоне",
		ПараметрыПроцедурыЗаменыТехпроцесса(),
		ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Функция ПараметрыПроцедурыЗаменыТехпроцесса()
	
	Результат = Новый Структура;
	Результат.Вставить("ТехпроцессТекущий", ТехпроцессТекущий);
	Результат.Вставить("ТехпроцессНовый", ТехпроцессНовый);
	Результат.Вставить("Этапы", ВыбранныеЭтапы(ЭтотОбъект));
	Результат.Вставить("ПараметрыТехпроцесса", РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор());
	Результат.Вставить("ПараметрыПодразделения", ПараметрыПодразделения);
	
	ДанныеОперации = ДанныеОперацииТехпроцессаПомещенного(ОтборВидТехпроцессаТекущий(), ОперацияПрервать);
	Результат.Вставить("ОперацияПрерватьИдентификатор", ?(ДанныеОперации = Неопределено, Неопределено, ДанныеОперации.ИдентификаторОперации));
	
	ДанныеОперации = ДанныеОперацииТехпроцессаПомещенного(ОтборВидТехпроцессаНовый(), ОперацияПродолжить);
	Результат.Вставить("ОперацияПродолжитьИдентификатор", ?(ДанныеОперации = Неопределено, Неопределено, ДанныеОперации.ИдентификаторОперации));
	
	Если ОтборСостояниеОпераций = ОтборСостояниеОперацийНеНачаты() Тогда
		Результат.ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Альтернативный;
		Результат.ПараметрыТехпроцесса.ТехнологическийПроцесс      = ТехпроцессНовый;
	Иначе
		Результат.ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный;
		ТаблицыОпераций = Новый Соответствие();
		ТаблицыОпераций.Вставить(Документы.ЭтапПроизводства2_2.ПустаяСсылка(), ТаблицаОперацийДляЗамены());
		Для каждого ИдентификаторСтроки Из Элементы.Этапы.ВыделенныеСтроки Цикл
			ДанныеСтроки = Этапы.НайтиПоИдентификатору(ИдентификаторСтроки);
			Ограничения = РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ОграниченияТехпроцессаКонструктор();
			ЗаполнитьЗначенияСвойств(Ограничения, ДанныеСтроки);
			Если РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ЗаписьСодержитОграниченияТехпроцесса(Ограничения) Тогда
				ТаблицыОпераций.Вставить(ДанныеСтроки.Этап, ТаблицаОперацийДляЗамены(Ограничения));
			КонецЕсли;
		КонецЦикла;
		Результат.Вставить("ТаблицыОпераций", ТаблицыОпераций);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗаменаТехпроцессаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	ДлительныеОперацииСтруктура.ЗаменаТехпроцесса = Неопределено;
	
	Если ДлительнаяОперация <> Неопределено Тогда
		
		РезультатЗадания = ПолучитьИзВременногоХранилища(ДлительнаяОперация.АдресРезультата);
		
		Если ДлительнаяОперация.Статус = "Ошибка"
				ИЛИ РезультатЗадания = Неопределено
				ИЛИ РезультатЗадания.ЕстьОшибки Тогда
			ТекстСообщения = НСтр("ru = 'Не удалось заменить технологический процесс выбранных этапов. Подробности см. в журнале регистрации.';
									|en = 'Cannot replace a technological process in the selected stages. For more information, see the Event log.'");
			Картинка = БиблиотекаКартинок.Ошибка32;
			Если РезультатЗадания <> Неопределено И РезультатЗадания.ЕстьОшибки Тогда
				Для каждого КлючИЗначение Из РезультатЗадания.Неудачно Цикл
					ОбщегоНазначенияКлиент.СообщитьПользователю(КлючИЗначение.Значение.ТекстОшибки, КлючИЗначение.Ключ);
				КонецЦикла;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'Технологический процесс этапов заменен.';
									|en = 'Technological process of the stages is replaced.'");
			Картинка = БиблиотекаКартинок.Информация32;
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(ФоноваяОперацияЗаменаТехпроцесса(),,ТекстСообщения, Картинка);
		
		Оповестить("ИзменениеТехпроцесса");
		Оповестить("Запись_ОчередьПроизводственныхОпераций", Новый Структура("Подразделение", ОтборПодразделение));
		
		ОбновитьДанные();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
