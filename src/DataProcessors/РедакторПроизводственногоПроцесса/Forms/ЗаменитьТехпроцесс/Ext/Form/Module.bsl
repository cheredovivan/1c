
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Спецификация           = Параметры.Спецификация;
	КоличествоПлан         = Параметры.КоличествоПлан;
	ПараметрыТехпроцесса   = Параметры.ПараметрыТехпроцесса;
	
	ТехнологическийПроцесс = ПараметрыТехпроцесса.ТехнологическийПроцесс; // СправочникСсылка.ТехнологическиеПроцессы
	
	ДанныеОсновногоИзделия = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(
		Спецификация);
	
	ДанныеКоэффициента = Обработки.РедакторПроизводственногоПроцесса.ДанныеКоэффициентаТехнологическогоПроцесса(
		ТехнологическийПроцесс, ДанныеОсновногоИзделия);
		
	КоэффициентЧислитель   = ДанныеКоэффициента.Числитель;
	КоэффициентЗнаменатель = ДанныеКоэффициента.Знаменатель;
	
	ЕдиницаИзмеренияПредставление = Строка(ДанныеОсновногоИзделия.ЕдиницаИзмерения);
	Элементы.ДекорацияЗапланировано.Заголовок = ЕдиницаИзмеренияПредставление;
	Элементы.ДекорацияКоэффициент2.Заголовок  = ЕдиницаИзмеренияПредставление;
	
	Элементы.ДекорацияТекст.Заголовок = СтрШаблон(НСтр("ru = 'Будет выполнена замена на %1: %2.';
														|en = 'Will be replaced with %1: %2.'"),
		?(ТипЗнч(ТехнологическийПроцесс) = Тип("СправочникСсылка.МаршрутныеКарты"),
			НСтр("ru = 'маршрутную карту';
				|en = 'route sheet'"),
			НСтр("ru = 'технологический процесс';
				|en = 'technological process'")),
		ТехнологическийПроцесс);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КнопкаПродолжить(Команда)
	
	Если ПроверитьЗаполнение() Тогда
		
		ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса = КоличествоПлан * КоэффициентЧислитель / КоэффициентЗнаменатель;
		
		Закрыть(ПараметрыТехпроцесса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КнопкаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти
