
#Область ОписаниеПеременных

&НаКлиенте
Перем КэшированныеЗначения;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();

	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("Этап", Объект.Этап) И ЗначениеЗаполнено(Объект.Этап) Тогда
		ПредставлениеЭтапа = Документы.ЭтапПроизводства2_2.ПредставлениеЭтапа(Объект.Этап);
		Заголовок = СтрШаблон("%1 %2", НСтр("ru = 'Технологический процесс этапа';
											|en = 'Technological process of the stage'"), ПредставлениеЭтапа);
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Режим", Режим) Тогда
		Режим = РедакторПроизводственногоПроцессаКлиентСервер.РежимИзСписка();
	КонецЕсли;
	
	Попытка
		ЗаблокироватьДанныеДляРедактирования(Объект.Этап, , УникальныйИдентификатор);
	Исключение
		Элементы.ИнформационнаяПанельНадпись.Заголовок =
			СтрШаблон(НСтр("ru = 'Не удалось заблокировать этап ""%1"", возможно он в настоящий момент редактируется.';
							|en = 'Cannot lock the ""%1"" stage. It might be locked for editing.'"), ПредставлениеЭтапа);
		ТолькоПросмотр = Истина;
	КонецПопытки;
	
	СлужебныеРеквизитыОперацийСоздать();
	
	Инициализировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность
		И НЕ ЗавершениеРаботы Тогда
		
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'The data has changed. Do you want to save the changes?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СсылкиРазблокировать = Новый Массив;
	СсылкиРазблокировать.Добавить(Объект.Этап);
	Для каждого ПроизводственнаяОперация Из Кэш.ПроизводственныеОперации Цикл
		СсылкиРазблокировать.Добавить(ПроизводственнаяОперация);
	КонецЦикла;
	РазблокироватьДанные(СсылкиРазблокировать, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора <> Неопределено 
		И ИсточникВыбора.ИмяФормы = "Обработка.РедакторПроизводственногоПроцесса.Форма.ПодборОпераций" Тогда
		
		Модифицированность = Истина;
		ОперацииДобавитьПодбором(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора <> Неопределено
		И ИсточникВыбора.ИмяФормы = "Справочник.КлассификаторТехнологическихОпераций.Форма.ФормаСписка" Тогда
		
		Модифицированность = Истина;
		ОбработкаВыбораОперацииПоКлассификатору(ВыбранноеЗначение, НЕ ИсточникВыбора.ЗакрыватьПриВыборе);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ДеревоОперацийСвертка(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "КопированиеСтрокВБуферОбмена" Тогда
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Операция"); 
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НадписьТехнологическийПроцессОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "#ОткрытьТехнологическийПроцесс" Тогда
		ПоказатьЗначение(, Объект.ТехнологическийПроцесс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНаладкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Операции.ТекущиеДанные;
	Если ЗначениеЗаполнено(ТекущиеДанные.РабочийЦентр) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		РедакторПроизводственногоПроцессаКлиент.ОткрытьФормуВыбораВариантаНаладки(
			ТекущиеДанные.РабочийЦентр,
			ТекущиеДанные.ВариантНаладки,
			Элемент);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КТОПриИзменении(Элемент)
	
	ВыбранноеЗначение = Элементы.Операции.ТекущиеДанные.КТО;
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		ОбработкаВыбораОперацииПоКлассификатору(ВыбранноеЗначение);
	Иначе
		ПриИзмененииОперации(Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантНаладкиПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередаточнаяПартияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Операции.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РедакторПроизводственногоПроцессаКлиентСервер.ЗаполнитьЕдиницуИзмеренияПередаточнойПартии(
		ТекущиеДанные, ТекущиеДанные.ЕдиницаИзмеренияПередаточнойПартии);
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрольПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура МожноПовторитьПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура МожноПропуститьПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура НепрерывнаяПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОперации

&НаКлиенте
Процедура ОперацииПриИзменении(Элемент)
	
	ПриИзмененииОперации(Элемент.ТекущийЭлемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПриАктивизацииСтроки(Элемент)
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	
	Если ТекущаяСтрока = ИдентификаторТекущейСтроки(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущаяСтрока = Неопределено Тогда
		ОчиститьТаблицы(ПереченьРесурсов(ЭтотОбъект));
		Возврат;
	КонецЕсли;
	
	Кэш.АктивизацияСтроки = Истина;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОперацииПриАктивизацииСтроки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТолькоПросмотр Тогда
	
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если Поле.Имя = "Наименование" И ЗначениеЗаполнено(ТекущиеДанные.Операция) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Операция);
			ИначеЕсли Поле.Имя = "РабочийЦентр" И ЗначениеЗаполнено(ТекущиеДанные.РабочийЦентр) Тогда
				ПоказатьЗначение(, ТекущиеДанные.РабочийЦентр);
			ИначеЕсли Поле.Имя = "ТехнологическийПроцесс" И ЗначениеЗаполнено(ТекущиеДанные.ТехнологическийПроцесс) Тогда
				ПоказатьЗначение(, ТекущиеДанные.ТехнологическийПроцесс);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
	ОперацииДобавитьНовую(Копирование);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ВыделенныеСтроки = Элементы.Операции.ВыделенныеСтроки;
	
	Если ПроверитьВозможностьРедактированияОпераций(ВыделенныеСтроки, "Все") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИдентификаторыСтрок", ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ВыделенныеСтроки));
		
		Если ВыделенныеСтроки.Количество() = 1 Тогда
			ДанныеСтроки = Операции.НайтиПоИдентификатору(ВыделенныеСтроки[0]);
			ТекстВопроса = СтрШаблон(НСтр("ru = 'Удалить операцию ""%1""?';
											|en = 'Delete the ""%1"" operation?'"), ДанныеСтроки.Наименование);
		Иначе
			ТекстВопроса = НСтр("ru = 'Удалить выделенные операции?';
								|en = 'Delete the selected operations?'");
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОперацииПередУдалениемЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПередНачаломИзменения(Элемент, Отказ)
	
	Если Кэш.АктивизацияСтроки Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Если Строка = Элементы.Операции.ТекущаяСтрока Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(Строка);
	Если ДанныеСтроки = Неопределено
		ИЛИ ДанныеСтроки.ТолькоПросмотр
		ИЛИ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Значение = ПараметрыПеретаскивания.Значение;
	
	Если НЕ ТипЗнч(Значение) = Тип("Структура") Тогда
		ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
		Возврат;
	КонецЕсли;
	
	Значение.Вставить("ИдентификаторПриемника", Строка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если Строка = Неопределено
		ИЛИ НЕ ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	Перетаскивание(Строка(ПараметрыПеретаскивания.Действие), ПараметрыПеретаскивания.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыходныеИзделия

&НаКлиенте
Процедура ВыходныеИзделияПослеУдаления(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ПриИзмененииОперации(Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТолькоПросмотр Тогда
	
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если Поле.Имя = "ВыходныеИзделияНоменклатура" И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
			ИначеЕсли Поле.Имя = "ВыходныеИзделияХарактеристика" И ЗначениеЗаполнено(ТекущиеДанные.Характеристика) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Характеристика);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияНоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзмененииНаСервере("ВыходныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияКоличествоУпаковокПриИзменении(Элемент)
	
	ПриИзмененииКоличестваУпаковок("ВыходныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияУпаковкаПриИзменении(Элемент)
	
	ПриИзмененииКоличестваУпаковок("ВыходныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделияНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	НачалоПеретаскивания(Элемент.Имя, ПараметрыПеретаскивания, Выполнение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМатериалыИУслуги

&НаКлиенте
Процедура МатериалыИУслугиПослеУдаления(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ПриИзмененииОперации(Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТолькоПросмотр Тогда
	
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если Поле.Имя = "МатериалыИУслугиНоменклатура" И ЗначениеЗаполнено(ТекущиеДанные.Номенклатура) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
			ИначеЕсли Поле.Имя = "МатериалыИУслугиХарактеристика" И ЗначениеЗаполнено(ТекущиеДанные.Характеристика) Тогда
				ПоказатьЗначение(, ТекущиеДанные.Характеристика);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиНоменклатураПриИзменении(Элемент)
	
	НоменклатураПриИзмененииНаСервере("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиКоличествоУпаковокПриИзменении(Элемент)
	
	ПриИзмененииКоличестваУпаковок("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиУпаковкаПриИзменении(Элемент)
	
	ПриИзмененииКоличестваУпаковок("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслугиНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	НачалоПеретаскивания(Элемент.Имя, ПараметрыПеретаскивания, Выполнение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТрудозатраты

&НаКлиенте
Процедура ТрудозатратыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТолькоПросмотр Тогда
	
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Если Поле.Имя = "ТрудозатратыВидРабот" И ЗначениеЗаполнено(ТекущиеДанные.ВидРабот) Тогда
				ПоказатьЗначение(, ТекущиеДанные.ВидРабот);
			КонецЕсли;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыПослеУдаления(Элемент)
	
	ПриИзмененииОперации(Элемент.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НЕ ОтменаРедактирования Тогда
		ПриИзмененииОперации(Элемент.Имя);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТрудозатратыНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	НачалоПеретаскивания(Элемент.Имя, ПараметрыПеретаскивания, Выполнение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗавершитьРедактирование();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьНаАльтернативный(Команда)
	
	ВыбратьТехнологическийПроцесс(Новый ОписаниеОповещения("ЗаменитьНаАльтернативныйЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьНаСтандартный(Команда)
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаВызовСервера.СтандартныйТехпроцессЭтапа(Объект.Этап);
	
	ТекстВопроса = НСтр("ru = 'Будет выполнена замена на стандартный технологический процесс этапа. Продолжить?';
						|en = 'The technological process will be replaced with the standard technological process of the stage. Continue?'");
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения("ЗаменитьНаТехпроцессЗавершение", ЭтотОбъект, ПараметрыТехпроцесса),
		ТекстВопроса,
		РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьНаИндивидуальный(Команда)
	
	ЗаменитьНаИндивидуальныйНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТиповойТехпроцесс(Команда)
	
	ОчиститьСообщения();
	
	ДанныеДляСозданияТехпроцесса = ПодготовитьДанныеДляСозданияТиповогоТехпроцесса();
	
	Если НЕ ДанныеДляСозданияТехпроцесса.ПроверкаТехпроцессаПройдена Тогда
		
		Возврат;
		
	Иначе
		
		ТекстВопроса = НСтр("ru = 'Будет создан и записан новый технологический процесс. Продолжить?';
							|en = 'A new technological process will be created and saved. Continue?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ВопросСоздатьТиповойТехпроцессЗавершение", ЭтотОбъект, ДанныеДляСозданияТехпроцесса);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоэффициент(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Спецификация", Объект.Спецификация);
	ПараметрыФормы.Вставить("ТехнологическийПроцесс", Объект.ТехнологическийПроцесс);
	ПараметрыФормы.Вставить("КоэффициентТехнологическогоПроцесса", Объект.КоэффициентТехнологическогоПроцесса);
	ПараметрыФормы.Вставить("КоличествоПлан", Объект.Количество);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОткрытьФорму("Обработка.РедакторПроизводственногоПроцесса.Форма.ИзменитьКоэффициент",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		Новый ОписаниеОповещения("ИзменитьКоэффициентЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура Перечитать(Команда)
	
	Если Модифицированность Тогда
		
		ТекстВопроса = НСтр("ru = 'Внесенные изменения будут потеряны. Продолжить?';
							|en = 'Changes will be lost. Continue?'");
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПеречитатьЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		Инициализировать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПереместитьВверх(Команда)
	
	СдвинутьОперации(Элементы.Операции.ВыделенныеСтроки, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПереместитьВниз(Команда)
	
	СдвинутьОперации(Элементы.Операции.ВыделенныеСтроки, Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПодбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Подразделение", Объект.Подразделение);
	ПараметрыФормы.Вставить("ПодразделениеТолькоПросмотр");
	
	Если ХранитьОперацииВРесурсныхСпецификациях Тогда
		ПараметрыФормы.Вставить("ИмяСправочника", "Справочник.ТехнологическиеПроцессы");
		ПараметрыФормы.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыТехнологическихПроцессов.Действует"));
	Иначе
		ПараметрыФормы.Вставить("ИмяСправочника", "Справочник.МаршрутныеКарты");
		ПараметрыФормы.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыМаршрутныхКарт.Действует")); 
	КонецЕсли;
	
	ОткрытьФорму("Обработка.РедакторПроизводственногоПроцесса.Форма.ПодборОпераций",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПодборПоКТО(Команда)
	
	РедакторПроизводственногоПроцессаКлиент.ОткрытьФормуВыбораОперацииПоКлассификатору(
		ЭтотОбъект,,
		Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииУдалитьНеначатые(Команда)
	
	Идентификаторы = Новый Массив;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
		Если НЕ ДанныеСтроки.ТолькоПросмотр Тогда
			Идентификаторы.Добавить(ДанныеСтроки.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;
	
	Если Идентификаторы.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Нет неначатых операций, которые можно удалить.';
									|en = 'There are no unstarted operations that can be deleted.'"));
	Иначе
		ДополнительныеПараметры = Новый Структура("ИдентификаторыСтрок", Идентификаторы);
		ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьОперацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Будут удалены все неначатые операции. Продолжить?';
												|en = 'All unstarted operations will be deleted. Continue?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииУдалитьСВыделенной(Команда)
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Идентификаторы = Новый Массив;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки,,Ложь) Цикл
		Идентификатор = ДанныеСтроки.ПолучитьИдентификатор();
		Если Идентификатор = ТекущаяСтрока ИЛИ Идентификаторы.Количество() > 0 Тогда
			Идентификаторы.Добавить(Идентификатор);
		КонецЕсли;
	КонецЦикла;
	
	Если ПроверитьВозможностьРедактированияОпераций(Идентификаторы, "Все") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИдентификаторыСтрок", Идентификаторы);
		
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Будут удалены все операции, начиная с ""%1"". Продолжить?';
										|en = 'All operations starting from ""%1"" will be deleted. Continue?'"),
			Элементы.Операции.ТекущиеДанные.Наименование);
			
		ОписаниеОповещения = Новый ОписаниеОповещения("УдалитьОперацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииПеренумеровать(Команда)
	
	ВыделенныеСтроки = Элементы.Операции.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВозможностьРедактированияОпераций(ВыделенныеСтроки, "Все") Тогда

		ИндексыСтрок = ИндексыОпераций(ЭтотОбъект, ВыделенныеСтроки);
		ИндексыСтрок.СортироватьПоПредставлению();
		
		Если ИндексыСтрок.Количество() = 0 И ВыделенныеСтроки.Количество() > 0 Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Необходимо выделить строки верхнего уровня.';
											|en = 'Select the top-level lines.'"));
			Возврат;
		КонецЕсли;
		
		Для Индекс = 0 По ИндексыСтрок.Количество()-1 Цикл
			Если Число(ИндексыСтрок[0].Представление)+Индекс <> Число(ИндексыСтрок[Индекс].Представление) Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Выделенные строки должны располагаться последовательно.';
												|en = 'The selected lines must be placed sequentially.'"));
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИндексыСтрок", ИндексыСтрок);
		
		ПоказатьВводЧисла(Новый ОписаниеОповещения("ОперацииПеренумероватьЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			Операции.НайтиПоИдентификатору(ИндексыСтрок[0].Значение).НомерОперации,
			НСтр("ru = 'Введите начальный номер';
				|en = 'Enter the initial number'"), 5, 0);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииРазделить(Команда)
		
	ВыделенныеСтроки = Элементы.Операции.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Команда не может быть выполнена для нескольких строк.';
										|en = 'Command cannot be executed for multiple lines.'"));
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВозможностьРедактированияОпераций(ВыделенныеСтроки, "Детальные") Тогда
		
		ИдентификаторСтроки = ВыделенныеСтроки[0];
		ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		Оповещение = Новый ОписаниеОповещения("ОперацииРазделитьЗавершение",
			ЭтотОбъект,
			Новый Структура("ИдентификаторСтроки", ИдентификаторСтроки));
		
		ПараметрыФормы = СтрокаДереваСтруктуройКонструктор(ЭтотОбъект);
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, ДанныеСтроки,,"Количество");
		ПараметрыФормы.Вставить("Количество", ДанныеСтроки.Требуется);
		ПараметрыФормы.Вставить("Подразделение", Объект.Подразделение);
		
		ОткрытьФорму("Обработка.РедакторПроизводственногоПроцесса.Форма.РазделитьОперацию",
			ПараметрыФормы, ЭтотОбъект,,,, Оповещение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииДеревоСвернуть(Команда)
	
	ДеревоОперацийСвертка(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииДеревоРазвернуть(Команда)
	
	ДеревоОперацийСвертка(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииУстановитьКоличество(Команда)
	
	ВыделенныеСтроки = Элементы.Операции.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВозможностьРедактированияОпераций(ВыделенныеСтроки, "Все") Тогда
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИдентификаторыСтрок", ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ВыделенныеСтроки));
		
		ПоказатьВводЧисла(Новый ОписаниеОповещения("УстановитьКоличествоЗавершение", ЭтотОбъект, ДополнительныеПараметры),
			Операции.НайтиПоИдентификатору(ВыделенныеСтроки[0]).Требуется,
			НСтр("ru = 'Введите новое количество';
				|en = 'Enter the new quantity'"), 10, 3);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслуги_ЗаполнитьСтатьюКалькуляции(Команда)
	
	ЗаполнитьСтатьюКалькуляцииВСтроках("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты_ЗаполнитьСтатьюКалькуляции(Команда)
	
	ЗаполнитьСтатьюКалькуляцииВСтроках("Трудозатраты");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделия_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("ВыходныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура ВыходныеИзделия_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("ВыходныеИзделия");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслуги_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура МатериалыИУслуги_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("МатериалыИУслуги");
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты_СкопироватьСтроки(Команда)
	
	СкопироватьСтрокиТЧ("Трудозатраты");
	
КонецПроцедуры

&НаКлиенте
Процедура Трудозатраты_ВставитьСтроки(Команда)
	
	ПолучитьСтрокиИзБуфераОбмена("Трудозатраты");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСТехпроцессом

&НаСервере
Процедура ЗаполнитьДанныеТехпроцесса(Инициализация = Ложь)
	
	ОчиститьТаблицы();
	
	НаОснованииОчереди = Инициализация
		И (ЭтоИндивидуальныйТехпроцесс(Объект)
			ИЛИ УправлениеПроизводствомКлиентСервер.СтатусыДоступноПооперационноеУправление(ПараметрыПодразделения).Найти(Объект.Статус) <> Неопределено);
	
	Если НаОснованииОчереди Тогда
		
		ПараметрыТекстаЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ПараметрыТекстаЗапросаОчередьОпераций();
		ПараметрыТекстаЗапроса.ПоказателиВыполнения = Истина;
		ПараметрыТекстаЗапроса.РеквизитыИсполнителяОперации = Истина;
		ПараметрыТекстаЗапроса.РеквизитыРасчетаВремениВыполненияОперации = Истина;
		ПараметрыТекстаЗапроса.РеквизитыОперацииРасширенные = Истина;
		ПараметрыТекстаЗапроса.ПоляВыборки = ДополнительныеПоляОчередиОпераций();
		ПараметрыТекстаЗапроса.ИмяВыходнойТаблицы = "ВТОчередь";
		ПараметрыТекстаЗапроса.Отборы.Добавить("Очередь.Этап = &Этап");
		
		ТекстЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ТекстЗапросаОчередьОпераций(ПараметрыТекстаЗапроса) + "
		|
		|;
		|
		|ВЫБРАТЬ
		|	ВТОчередь.*,
		|	ВТОчередь.Загрузка                                                                            КАК ЗагрузкаНорматив,
		|	Состояния.Ссылка                                                                              КАК Состояние,
		|	ЕСТЬNULL(ВТОчередь.Операция.Описание, ВТОчередь.ПроизводственнаяОперация.Комментарий)         КАК Описание,
		|	ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Назначенные
		|			ГДЕ
		|				Назначенные.Этап = &Этап
		|				И ВтОчередь.Операция = Назначенные.Операция
		|				И ВтОчередь.ИдентификаторОперации = Назначенные.ИдентификаторОперации
		|				И НЕ Назначенные.СменноеЗадание = ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)) КАК Назначена
		|ИЗ
		|	ВТОчередь КАК ВТОчередь
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ Перечисление.СостоянияВыполненияОпераций КАК Состояния
		|	ПО ВТОчередь.СостояниеПорядок = Состояния.Порядок
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерОперации,
		|	Наименование
		|
		|";
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("Этап", Объект.Этап);
		
		СписокОпераций = Операции.ПолучитьЭлементы();
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = СписокОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			НоваяСтрока.ПоказателиВыполнения = ПоказателиВыполнения(Выборка);
			Если ЭтоОперацияИндивидуальная(НоваяСтрока) Тогда
				ПроизводственныеОперации = Кэш.ПроизводственныеОперации; // Массив
				ПроизводственныеОперации.Добавить(НоваяСтрока.ПроизводственнаяОперация);
				Попытка
					ЗаблокироватьДанныеДляРедактирования(НоваяСтрока.ПроизводственнаяОперация, , УникальныйИдентификатор);
				Исключение
					НоваяСтрока.ТолькоПросмотр = Истина;
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
		
	Иначе
		
		СохранитьКэшРесурсов(Неопределено);
		
		ДобавитьОперацииТехпроцесса();
		
	КонецЕсли;
	
	ТаблицаРесурсов.Очистить();
	ЗаполнитьСлужебныеРеквизитыСтрокОперацийНаСервере();
	
	ДеревоСортироватьСгруппировать(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьНаАльтернативныйЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		
		ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
		ПараметрыТехпроцесса.ТипТехнологическогоПроцесса         = АльтернативныйТехпроцесс();
		ПараметрыТехпроцесса.ТехнологическийПроцесс              = Результат;
		ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса = Объект.Количество;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Спецификация",         Объект.Спецификация);
		ПараметрыФормы.Вставить("КоличествоПлан",       Объект.Количество);
		ПараметрыФормы.Вставить("ПараметрыТехпроцесса", ПараметрыТехпроцесса);
		
		ОткрытьФорму("Обработка.РедакторПроизводственногоПроцесса.Форма.ЗаменитьТехпроцесс",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			Новый ОписаниеОповещения("ЗаменитьНаТехпроцессЗавершение", ЭтотОбъект, ПараметрыТехпроцесса));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьНаТехпроцессЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			ПараметрыТехпроцесса = Результат;
		Иначе
			ПараметрыТехпроцесса = ДополнительныеПараметры;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(Объект, ПараметрыТехпроцесса);
		Объект.СохранитьОграниченияТехпроцесса = Ложь;
		
		ЗаполнитьДанныеТехпроцесса();
		
		Модифицированность = Истина;
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьКоэффициентЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено
			И Объект.КоэффициентТехнологическогоПроцесса <> Результат Тогда
		
		Объект.КоэффициентТехнологическогоПроцесса = Результат;
		
		ЗаполнитьДанныеТехпроцесса();
		
		Модифицированность = Истина;
		
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ТехнологическийПроцесс");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьТехнологическийПроцесс(ОписаниеОповещения)
	
	ВидыНоменклатуры = Новый Массив;
	ВидыНоменклатуры.Добавить(Объект.ВидНоменклатуры);
	ВидыНоменклатуры.Добавить(ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"));
	
	Отбор = Новый Структура;
	Отбор.Вставить("Подразделение",  Объект.Подразделение);
	Отбор.Вставить("ДляВидаИзделий", ВидыНоменклатуры);
	
	Если ХранитьОперацииВРесурсныхСпецификациях Тогда
		ИмяФормыВыбора = "Справочник.ТехнологическиеПроцессы.ФормаВыбора";
		Отбор.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыТехнологическихПроцессов.Действует"));
	Иначе
		ИмяФормыВыбора = "Справочник.МаршрутныеКарты.ФормаВыбора";
		Отбор.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыМаршрутныхКарт.Действует")); 
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("ЗапретитьВыбор", Объект.ТехнологическийПроцесс);
	ПараметрыФормы.Вставить("ПодразделениеТолькоПросмотр");
	ПараметрыФормы.Вставить("ДляВидаИзделийТолькоПросмотр");
	ПараметрыФормы.Вставить("СтатусТолькоПросмотр");
	
	ОткрытьФорму(ИмяФормыВыбора,
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция ДополнительныеПоляОчередиОпераций()
	
	Результат = Новый Массив;
	
	Поле = "
	|МИНИМУМ(ВЫБОР
	|		КОГДА ЕСТЬNULL(Очередь.ПроизводственнаяОперация.ТребуетПовторения, ЛОЖЬ) = ИСТИНА
	|			ТОГДА 10
	|		ИНАЧЕ
	|			Очередь.Состояние.Порядок
	|	КОНЕЦ) КАК СостояниеПорядок
	|";
	
	Результат.Добавить(Поле);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПроверитьЗаполнениеТехпроцесса(Режим = "ЗаписьТехпроцесса")
	
	Отказ = Ложь;
	Сообщения = Новый Массив;
	МетаданныеОперации = Метаданные.Документы.ПроизводственнаяОперация2_2;
	ТаблицаПроверкиПорядка = РедакторПроизводственногоПроцесса.ТаблицаПроверкиПорядкаЭлементовПроизводственногоПроцессаКонструктор();
	Обработка = Обработки.РедакторПроизводственногоПроцесса.Создать();
	ЕстьРедактируемыеСтроки = Ложь;
	КонтрольРеквизитов = Неопределено;
	
	СохранитьДанныеТекущейОперации();
	
	КэшРесурсов = ПолучитьКэшРесурсов();
	
	ДанныеСтроки = Неопределено; // ДанныеФормыЭлементДерева
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
		
		ДобавитьОперациюВТаблицуПроверкиПорядка(ТаблицаПроверкиПорядка, ДанныеСтроки);
		
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			
			КонтрольРеквизитов = КонтролируемыеРеквизитыОперацииВГруппе();
			
		Иначе
			
			ПроверитьЗаполнениеРеквизитовШапкиОперации(ДанныеСтроки, Отказ);
			
			СтруктураДанных = СтруктураХраненияРесурсовОперации(ДанныеСтроки, КэшРесурсов);
			Если СтруктураДанных <> Неопределено Тогда
				
				Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
					Обработка[ИмяТЧ].Загрузить(СтруктураДанных[ИмяТЧ]);
				КонецЦикла;
				
				ПроверитьЗаполнениеТабличныхЧастей(Обработка, ДанныеСтроки, Отказ);
				
			КонецЕсли;
			
			ПривязатьСообщенияПользователюКОперации(Сообщения, ДанныеСтроки);
			
			Если ЭтоОперацияИндивидуальнаяВГруппе(ДанныеСтроки) И КонтрольРеквизитов <> Неопределено Тогда
				ГруппаОпераций = ДанныеСтроки.ПолучитьРодителя();
				Для каждого КлючИЗначение Из КонтрольРеквизитов Цикл
					ИмяРеквизита = КлючИЗначение.Ключ;
					Если КлючИЗначение.Значение = Неопределено Тогда
						КонтрольРеквизитов[ИмяРеквизита] = ДанныеСтроки[ИмяРеквизита];
					ИначеЕсли КонтрольРеквизитов[ИмяРеквизита] <> ДанныеСтроки[ИмяРеквизита] Тогда
						ТекстСообщения = СтрШаблон(НСтр("ru = 'Различаются значения реквизита ""%1"" в операциях группы';
														|en = 'Values of the ""%1"" attribute differ in the group operations'"),
							МетаданныеОперации.Реквизиты[ИмяРеквизита].Синоним);
						ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
						КонтрольРеквизитов.Удалить(ИмяРеквизита);
					КонецЕсли;
				КонецЦикла;
				ПривязатьСообщенияПользователюКОперации(Сообщения, ГруппаОпераций);
			КонецЕсли;
			
		КонецЕсли;
		
		ЕстьРедактируемыеСтроки = ЕстьРедактируемыеСтроки ИЛИ НЕ ДанныеСтроки.ТолькоПросмотр;
		
	КонецЦикла;
	
	Если НЕ Объект.СохранитьОграниченияТехпроцесса И ЕстьРедактируемыеСтроки Тогда
		
		СтруктураПроверок = РедакторПроизводственногоПроцесса.СтруктураПроверокПоследовательностиОпераций(, ПараметрыПодразделения);
		
		Если Режим = "СозданиеТехпроцесса"
			И СтруктураПроверок.Свойство("КонтрольныеОперации") Тогда
			СтруктураПроверок.КонтрольныеОперации.Удалить("КонтрольИндивидуальныеОперацииВПоследовательности");
		КонецЕсли;
		
		РедакторПроизводственногоПроцесса.ПроверитьПорядокЭлементовПроизводственногоПроцесса(
			ТаблицаПроверкиПорядка,
			СтруктураПроверок,
			Отказ,
			Сообщения);
	КонецЕсли;
	
	Для каждого Сообщение Из Сообщения Цикл
		ОбщегоНазначения.СообщитьПользователю(Сообщение.Текст);
	КонецЦикла;
	
	Возврат НЕ Отказ;
	
КонецФункции

&НаСервере
Процедура ПроверитьЗаполнениеРеквизитовШапкиОперации(ДанныеСтроки, Отказ)
	
	ВладелецОперации = ?(
		ЗначениеЗаполнено(Объект.ЭтапСпецификации), Объект.ЭтапСпецификации, Справочники.ЭтапыПроизводства.ПолучитьСсылку());
	
	ОперацияОбъект = Справочники.ТехнологическиеОперации.СоздатьЭлемент();
	ОперацияОбъект.Владелец = ВладелецОперации;
	ЗаполнитьЗначенияСвойств(ОперацияОбъект, ДанныеСтроки);
	
	Если НЕ ОперацияОбъект.ПроверитьЗаполнение() Тогда
		Отказ = Истина;
	КонецЕсли;
	
	РедакторПроизводственногоПроцесса.ПроверитьЗаполнениеРабочихЦентровОперации(ОперацияОбъект, Объект.Подразделение, Неопределено, Отказ);
	
	РедакторПроизводственногоПроцесса.ПроверитьКорректностьВариантаКонтроляОперации(ОперацияОбъект, ПараметрыПодразделения, Неопределено, Отказ);
	
	Если ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		
		ПроверяемыеРеквизиты = ПроверяемыеРеквизитыШапкиОперации();
		
		Для каждого ИмяРеквизита Из ПроверяемыеРеквизиты Цикл
			Если НЕ ЗначениеЗаполнено(ДанныеСтроки[ИмяРеквизита]) Тогда
				ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" не заполнено';
												|en = '""%1"" is required'"), Элементы[ИмяРеквизита].Заголовок);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПроверяемыеРеквизитыШапкиОперации()
	
	Результат = Новый Массив;
	
	Результат.Добавить("Требуется");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КонтролируемыеРеквизитыОперацииВГруппе()
	
	Результат = Новый Структура;
	
	Результат.Вставить("КТО");
	Результат.Вставить("ВидОперации");
	Результат.Вставить("Контроль");
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПроверитьЗаполнениеТабличныхЧастей(Обработка, ДанныеСтроки, Отказ)
	
	ШаблонСообщения = НСтр("ru = 'Не заполнена колонка ""%1"" в строке %2 списка ""%3""';
							|en = 'Column ""%1"" in line #%2, list ""%3"" cannot be empty.'");
	
	МетаданныеТЧ = Метаданные.Обработки.РедакторПроизводственногоПроцесса.ТабличныеЧасти;
	
	ПереченьРесурсов = ПереченьРесурсов(ЭтотОбъект, Истина);
	Для Индекс = 0 По ПереченьРесурсов.Количество() - 1 Цикл
		ИмяТЧ = ПереченьРесурсов[Индекс];
		Для ИндексСтроки = 0 По Обработка[ИмяТЧ].Количество() - 1 Цикл
			Строка = Обработка[ИмяТЧ][ИндексСтроки];
			Для каждого ИмяРеквизита Из ПереченьРесурсовПроверкаЗаполнения(ИмяТЧ, ДанныеСтроки) Цикл
				Если НЕ ЗначениеЗаполнено(Строка[ИмяРеквизита]) Тогда
					ТекстСообщения = СтрШаблон(ШаблонСообщения,
						МетаданныеТЧ[ИмяТЧ].Реквизиты[ИмяРеквизита].Синоним, ИндексСтроки+1, МетаданныеТЧ[ИмяТЧ].Синоним);
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		Если ЕстьНоменклатураУпаковкаВТЧ(ИмяТЧ) И Обработка[ИмяТЧ].Количество() > 0 Тогда
			ПараметрыПроверки = НоменклатураСервер.ПараметрыПроверкиЗаполненияХарактеристик();
			ПараметрыПроверки.ИмяТЧ = ИмяТЧ;
			НоменклатураСервер.ПроверитьЗаполнениеХарактеристик(Обработка, Новый Массив(), Отказ, ПараметрыПроверки);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПривязатьСообщенияПользователюКОперации(Сообщения, ДанныеСтроки)
	
	ИндексСтроки = КоллекцияСтрок(ЭтотОбъект, ДанныеСтроки).Индекс(ДанныеСтроки);
	
	Если ЗначениеЗаполнено(ДанныеСтроки.Наименование) Тогда
		Наименование = ДанныеСтроки.Наименование;
	Иначе
		Наименование = СтрШаблон(НСтр("ru = 'Строка %1';
										|en = 'Row %1'"), ИндексСтроки+1);
	КонецЕсли;
	
	Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
		Сообщение.Текст = СтрШаблон("%1: %2", Наименование, Сообщение.Текст);
		Сообщения.Добавить(Сообщение);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаменитьНаИндивидуальныйНаСервере()
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
	ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = ИндивидуальныйТехпроцесс();
	ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса = Объект.КоэффициентТехнологическогоПроцесса;
	
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыТехпроцесса);
	Объект.СохранитьОграниченияТехпроцесса = Ложь;
	
	ТаблицаРесурсов.Очистить();
	ЗаполнитьСлужебныеРеквизитыСтрокОперацийНаСервере();
	
	ИнициализироватьНовуюОперацию();
	
	Модифицированность = Истина;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОперациюВТаблицуПроверкиПорядка(ТаблицаПроверкиПорядка, ДанныеСтроки)
	
	Если ЭтоОперацияИндивидуальнаяВГруппе(ДанныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаПроверкиПорядка.Добавить();
	НоваяСтрока.Объект                 = ДанныеСтроки;
	НоваяСтрока.Операция               = ДанныеСтроки.Операция;
	НоваяСтрока.Представление          = ДанныеСтроки.Наименование;
	НоваяСтрока.НомерОперации          = ДанныеСтроки.НомерОперации;
	НоваяСтрока.НомерСледующейОперации = ДанныеСтроки.НомерСледующейОперации; 
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСОперациями

&НаКлиенте
Процедура Подключаемый_ОперацииПриАктивизацииСтроки()
	
	ОперацииПриАктивизацииСтрокиНаСервере();
	
	Кэш.АктивизацияСтроки = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ОперацииПриАктивизацииСтрокиНаСервере()
	
	СохранитьДанныеТекущейОперации();
	
	ИнициализироватьНовуюОперацию();
	
	СохранитьТекущуюСтроку(ЭтотОбъект);
	
КонецПроцедуры
	
&НаСервере
Процедура СохранитьДанныеТекущейОперации()
	
	ИдентификаторСтроки = ИдентификаторТекущейСтроки(ЭтотОбъект);
	
	СохранитьРесурсыИндивидуальнойОперации(ИдентификаторСтроки);
	
КонецПроцедуры
	
&НаСервере
Процедура ИнициализироватьНовуюОперацию()
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Операции.НайтиПоИдентификатору(ТекущаяСтрока);
	
	РедакторПроизводственногоПроцесса.НастроитьПараметрыВыбораРабочихЦентров(
		ТекущиеДанные, ЭтотОбъект, Объект.Подразделение);
	
	РедакторПроизводственногоПроцесса.НастроитьПараметрыВыбораУчасток(
		ТекущиеДанные, ЭтотОбъект, Объект.Подразделение);
	
	Элементы.Участок.ПодсказкаВвода = РедакторПроизводственногоПроцесса.ПредставлениеУчастка(
		ТекущиеДанные, Элементы.Участок.Видимость);
	
	ЗагрузитьРесурсыОперацииВОбъект(ТекущаяСтрока);
	
	ОбновитьИндикаторыОбеспеченияТекущие();
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Операция");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииОперации(СписокРеквизитов)
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	ТекущиеДанные = Операции.НайтиПоИдентификатору(ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СортироватьСгруппировать = Ложь;
	ОбработатьНаСервере      = Ложь;
	
	Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
		
		ЗаполнитьРеквизитыОперацийГруппы(ЭтотОбъект, ТекущиеДанные, ИмяРеквизита);
		
		Если ИмяРеквизита = "НомерОперации"
			ИЛИ ИмяРеквизита = "НомерСледующейОперации" Тогда
			СортироватьСгруппировать = Истина;
		Иначе
			ОбработатьНаСервере = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ОбработатьНаСервере Тогда
		ИндивидуальнаяДо = ЭтоОперацияИндивидуальная(ТекущиеДанные);
		ПриИзмененииОперацииНаСервере(СписокРеквизитов, ТекущаяСтрока);
		СортироватьСгруппировать = СортироватьСгруппировать ИЛИ ИндивидуальнаяДо <> ЭтоОперацияИндивидуальная(ТекущиеДанные);
	КонецЕсли;
	
	Если НЕ СортироватьСгруппировать ИЛИ НЕ ДеревоСортироватьСгруппироватьКлиент() Тогда
		ЗаполнитьСлужебныеРеквизитыСтрокиОперации(ЭтотОбъект, ТекущиеДанные);
		ЗаполнитьРеквизитыГруппыОпераций(ЭтотОбъект, ТекущиеДанные.ПолучитьРодителя());
	КонецЕсли;
	
	Модифицированность = Истина;
	ТекущиеДанные.Модифицированность = Истина;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "Операция");
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОперацииНаСервере(СписокРеквизитов, ИдентификаторСтроки)
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СписокРеквизитов = "Требуется" И НЕ ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		
		ДанныеСтроки.КоэффициентТехнологическогоПроцесса = ДанныеСтроки.Требуется / ДанныеСтроки.Количество;
		ЗагрузитьРесурсыОперацииВОбъект(ИдентификаторСтроки);
		ОтразитьИзменениеРесурсовОперации(ИдентификаторСтроки, Объект);
		
	Иначе
		
		Если ПреобразоватьОперациюВИндивидуальную(ИдентификаторСтроки, Ложь)
			ИЛИ СтрНайти(СписокРеквизитов, "ВыходныеИзделия") > 0
			ИЛИ СтрНайти(СписокРеквизитов, "МатериалыИУслуги") > 0
			ИЛИ СтрНайти(СписокРеквизитов, "Трудозатраты") > 0 Тогда
			СохранитьРесурсыИндивидуальнойОперации(ИдентификаторСтроки);
		КонецЕсли;
	
		Если СтрНайти(СписокРеквизитов, "Участок") > 0
			ИЛИ СтрНайти(СписокРеквизитов, "РабочийЦентр") > 0
			ИЛИ СтрНайти(СписокРеквизитов, "ВариантНаладки") > 0 Тогда
			
			МассивСтрок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеСтроки);
			Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
				Для каждого ДанныеПодчиненнойСтроки Из ДанныеСтроки.ПолучитьЭлементы() Цикл
					МассивСтрок.Добавить(ДанныеПодчиненнойСтроки);
				КонецЦикла;
			КонецЕсли;
			
			Для каждого ДанныеСтроки Из МассивСтрок Цикл
				
				Если СтрНайти(СписокРеквизитов, "Участок") > 0 Тогда
				
					РедакторПроизводственногоПроцесса.УчастокПриИзменении(ДанныеСтроки, ЭтотОбъект);
					
				КонецЕсли;
					
				РедакторПроизводственногоПроцесса.ПроверитьКорректностьВариантаНаладки(ДанныеСтроки);
				
				РедакторПроизводственногоПроцесса.ПрочитатьРеквизитыРабочегоЦентра(ДанныеСтроки, ДанныеСтроки);
				
				Если НЕ ДанныеСтроки.ПараллельнаяЗагрузка Тогда
					ДанныеСтроки.Загрузка = 0;
				КонецЕсли;
				
				Если ДанныеСтроки.СинхроннаяЗагрузка Тогда
					ДанныеСтроки.ВремяШтучное = 0;
					ДанныеСтроки.ВремяПЗ = 0;
				КонецЕсли;
				
				РедакторПроизводственногоПроцесса.ВариантНаладкиПриИзменении(ДанныеСтроки, ДанныеСтроки);
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьИндикаторыОбеспеченияОбщие();
	ОбновитьИндикаторыОбеспеченияТекущие();
	
КонецПроцедуры

// Проверить корректность выделения строк.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИдентификаторыСтрок - Массив из Число
//  Режим - Строка, Число - режим работы проверки (
//    "Все" - без ограничений,
//    "Индивидуальные" - только индивидуальные,
//    "Одноуровневые" - у которых уровень одинаковый,
//    "Детальные" - только исполняемые операции, без группировочных строк,
//    0...N - только конкретного уровня).
// Возвращаемое значение:
//  Булево
&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьКорректностьВыделенияСтрок(Форма, ИдентификаторыСтрок, Режим = "Все")
	
	ТекстСообщения = "";
	
	Если Режим = "Все" Тогда
		Возврат ТекстСообщения;
	КонецЕсли;
	
	МассивУровней = Новый Массив;
	МассивГрупп   = Новый Массив;
	
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ДанныеСтроки = Форма.Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
		Уровень = УровеньСтроки(ДанныеСтроки);
		Если МассивУровней.Найти(Уровень) = Неопределено Тогда
			МассивУровней.Добавить(Уровень);
		КонецЕсли;
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			МассивГрупп.Добавить(ИдентификаторСтроки);
		КонецЕсли;
		Если (ТипЗнч(Режим) = Тип("Число") И Уровень <> Режим)
				ИЛИ (Режим = "Одноуровневые" И МассивУровней.Количество() > 1)
				ИЛИ (Режим = "Индивидуальные" И НЕ ЭтоОперацияИндивидуальная(ДанныеСтроки))
				ИЛИ (Режим = "Детальные" И МассивГрупп.Количество() > 0) Тогда
			ТекстСообщения = РедакторПроизводственногоПроцессаКлиентСервер.ТекстПредупрежденияКомандаНеМожетБытьВыполнена(МассивУровней);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ТекстСообщения;
	
КонецФункции

// Проверить возможность редактирования операций.
// 
// Параметры:
//  ИдентификаторыСтрок - Массив из Число, Неопределено - идентификаторы
//  Режим - Строка, Число - см. ПроверитьКорректностьВыделенияСтрок
// Возвращаемое значение:
//  Булево - Проверить возможность редактирования операций
&НаКлиенте
Функция ПроверитьВозможностьРедактированияОпераций(ИдентификаторыСтрок = Неопределено, Режим = 0)
	
	ТекстПредупреждения = "";
	
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ДанныеСтроки.ТолькоПросмотр Тогда
			Если Кэш.МассивСостояний.Найти(ДанныеСтроки.Состояние) <= ГраницаРедактируемыхСостояний()
				ИЛИ ДанныеСтроки.ЕстьСозданные Тогда
				ТекстПредупреждения = НСтр("ru = 'Нельзя изменять и удалять выполненные или находящиеся в работе операции.';
											|en = 'Cannot edit or delete operations that are completed or in progress.'");
				Прервать;
			Иначе
				ТекстПредупреждения = НСтр("ru = 'Нельзя изменять и удалять операции, предшествующие выполненным или находящимся в работе.';
											|en = 'Cannot edit or delete operations that precede the completed operations or operations in progress.'");
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(ТекстПредупреждения) Тогда
		ТекстПредупреждения = ПроверитьКорректностьВыделенияСтрок(ЭтотОбъект, ИдентификаторыСтрок, Режим);
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(, ТекстПредупреждения);
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПреобразоватьОперациюВИндивидуальную(ИдентификаторСтроки, СохранитьРесурсы = Истина)
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено
		ИЛИ ДанныеСтроки.ТолькоПросмотр
		ИЛИ ЭтоОперацияИндивидуальная(ДанныеСтроки)
		ИЛИ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ДанныеСтроки.Операция                 = Неопределено;
	ДанныеСтроки.ТехнологическийПроцесс   = Неопределено;
	ДанныеСтроки.КоэффициентТехнологическогоПроцесса = Объект.КоэффициентТехнологическогоПроцесса;
	ДанныеСтроки.ПроизводственнаяОперация = Неопределено;
	ДанныеСтроки.МожноПовторить           = Ложь;
	Если ДанныеСтроки.Контроль = Перечисления.ВариантыКонтроляТехнологическихОпераций.ТребуетсяНаПоследующих Тогда
		ДанныеСтроки.Контроль  = Перечисления.ВариантыКонтроляТехнологическихОпераций.НеТребуется;
	КонецЕсли;
	
	Если ДанныеСтроки.Количество > 1 Тогда
		ДанныеСтроки.ВремяШтучное = ДанныеСтроки.ВремяШтучное / ДанныеСтроки.Количество;
		ДанныеСтроки.Количество   = 1;
	КонецЕсли;
	 
	 ДанныеСтроки.Модифицированность = Истина;
	
	Если СохранитьРесурсы Тогда
		СохранитьРесурсыИндивидуальнойОперации(ИдентификаторСтроки);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция УровеньСтроки(ДанныеСтроки)
	
	Результат = 0;
	Родитель  = ДанныеСтроки.ПолучитьРодителя();
	
	Пока Родитель <> Неопределено Цикл
		Результат = Результат + 1;
		Родитель  = Родитель.ПолучитьРодителя();
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КоллекцияСтрок(Форма, ДанныеСтроки)
	
	Родитель = ?(ДанныеСтроки <> Неопределено, ДанныеСтроки.ПолучитьРодителя(), Неопределено);
	Если Родитель <> Неопределено Тогда
		Возврат Родитель.ПолучитьЭлементы(); // ДанныеФормыКоллекцияЭлементовДерева
	Иначе
		Возврат Форма.Операции.ПолучитьЭлементы(); // ДанныеФормыКоллекцияЭлементовДерева
	КонецЕсли;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СледующаяСтрока(Форма, Курсор = Неопределено, ВложенныеУровни = Истина, ПриоритетПотомков = Истина)
	
	СписокОпераций = Форма.Операции.ПолучитьЭлементы();
	
	Если ВложенныеУровни И ПриоритетПотомков Тогда
		
		Если Курсор = Неопределено Тогда
			ТекущийИндекс = 0;
		Иначе
			Родитель = Курсор.ПолучитьРодителя();
			Если Родитель <> Неопределено Тогда
				СписокОпераций = Родитель.ПолучитьЭлементы();
			КонецЕсли;
			ТекущийИндекс = СписокОпераций.Индекс(Курсор)+1;
		КонецЕсли;
		
		Если ТекущийИндекс <= СписокОпераций.Количество()-1 Тогда
			Пока СписокОпераций.Количество() > 0 Цикл
				Курсор = СписокОпераций[ТекущийИндекс];
				СписокОпераций = Курсор.ПолучитьЭлементы();
				ТекущийИндекс = 0;
			КонецЦикла;
		ИначеЕсли Родитель <> Неопределено Тогда
			Курсор = Родитель;
		Иначе
			Курсор = Неопределено;
		КонецЕсли;
		
	Иначе
		
		Если Курсор = Неопределено Тогда
			Если СписокОпераций.Количество() > 0 Тогда
				Курсор = СписокОпераций[0];
			КонецЕсли;
			Возврат Курсор <> Неопределено; // Булево
		КонецЕсли;
		
		СписокОперацийВложенный = Курсор.ПолучитьЭлементы();
		Если СписокОперацийВложенный.Количество() > 0 И ВложенныеУровни Тогда
			Курсор = СписокОперацийВложенный[0];
		Иначе
			Родитель = Курсор.ПолучитьРодителя();
			Если Родитель <> Неопределено Тогда
				СписокОпераций = Родитель.ПолучитьЭлементы();
			КонецЕсли;
			ТекущийИндекс = СписокОпераций.Индекс(Курсор);
			Если ТекущийИндекс < СписокОпераций.Количество()-1 Тогда
				Курсор = СписокОпераций[ТекущийИндекс+1];
			ИначеЕсли Родитель <> Неопределено Тогда
				Курсор = Родитель;
				Возврат СледующаяСтрока(Форма, Курсор, Ложь, Ложь);
			Иначе
				Курсор = Неопределено;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Курсор <> Неопределено; // Булево
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьКартинкуСтроки(Форма, ДанныеСтроки)
	
	КартинкаОсновная = Неопределено;
	
	Если ЭтоОперацияПоНСИ(ДанныеСтроки) Тогда
		КартинкаОсновная = БиблиотекаКартинок.РедакторПроизводственногоПроцессаОперация;
	ИначеЕсли ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		КартинкаОсновная = БиблиотекаКартинок.МетаданныеДокументы;
	ИначеЕсли ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		КартинкаОсновная = БиблиотекаКартинок.РедакторПроизводственногоПроцессаДокумент;
	КонецЕсли;
	
	ДанныеСтроки.КартинкаСтроки = КартинкаОсновная;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьРеквизитСтроки(Форма, ДанныеСтроки, ИмяРеквизита, Значение, ВключаяПодчиненные = Истина)
	
	Если ДанныеСтроки.ТолькоПросмотр
		И НЕ ИмяРеквизита = "НомерСледующейОперации"
		И НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеСтроки[ИмяРеквизита] <> Значение Тогда
		Форма.Модифицированность = Истина;
		ДанныеСтроки.Модифицированность = Истина;
		ДанныеСтроки[ИмяРеквизита] = Значение;
		Если ВключаяПодчиненные Тогда
			ЗаполнитьРеквизитыОперацийГруппы(Форма, ДанныеСтроки, ИмяРеквизита);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючГруппировкиСтрок(ДанныеСтроки)
	
	Часть1 = Формат(ДанныеСтроки.НомерОперации, "ЧЦ=5; ЧВН=; ЧГ=;");
	Часть2 = Формат(ДанныеСтроки.НомерСледующейОперации, "ЧЦ=5; ЧВН=; ЧГ=;");
	Часть3 = ?(ЭтоОперацияИндивидуальная(ДанныеСтроки), 0, Формат(ДанныеСтроки.ИдентификаторОперации,"ЧЦ=5; ЧВН=; ЧГ=;"));
	
	Результат = СтрШаблон("%1_%2_%3", Часть1, Часть2, Часть3);
	Возврат Результат;
	
КонецФункции

// Ключ сортировки строк.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ДанныеСтроки - СтрокаДереваЗначений
// 
// Возвращаемое значение:
//  Строка - Ключ сортировки строк
&НаКлиентеНаСервереБезКонтекста
Функция КлючСортировкиСтрок(Форма, ДанныеСтроки)
	
	Часть1 = Формат(ДанныеСтроки.НомерОперации, "ЧЦ=5; ЧВН=; ЧГ=;");
	Часть2 = ДанныеСтроки.Наименование;
	Часть3 = Формат(ДанныеСтроки.ИдентификаторОперации, "ЧЦ=5; ЧВН=; ЧГ=;");
	
	Результат = СтрШаблон("%1_%2_%3", Часть1, Часть2, Часть3);
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СохранитьТекущуюСтроку(Форма, ИдентификаторСтроки = Неопределено)
	
	СохраненноеЗначение = Новый Структура;
	
	ТекущаяСтрока = ?(ИдентификаторСтроки = Неопределено, Форма.Элементы.Операции.ТекущаяСтрока, ИдентификаторСтроки);
	ТекущиеДанные = ?(ТекущаяСтрока = Неопределено, Неопределено, Форма.Операции.НайтиПоИдентификатору(ТекущаяСтрока));
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		СохраненноеЗначение.Вставить("ИдентификаторСтроки",   ТекущаяСтрока);
		СохраненноеЗначение.Вставить("ИдентификаторОперации", ТекущиеДанные.ИдентификаторОперации);
		СохраненноеЗначение.Вставить("КлючГруппировки",       КлючГруппировкиСтрок(ТекущиеДанные));
		
		Форма.Кэш.Вставить("Состояние_ТекущаяСтрока", СохраненноеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВосстановитьТекущуюСтроку(Форма)
	
	СохраненноеЗначение = Неопределено;
	Идентификаторы = Новый Массив(3);
	
	Если НЕ Форма.Кэш.Свойство("Состояние_ТекущаяСтрока", СохраненноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(Форма, ДанныеСтроки) Цикл
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			Если СохраненноеЗначение.КлючГруппировки = КлючГруппировкиСтрок(ДанныеСтроки) Тогда
				Идентификаторы[1] = ДанныеСтроки.ПолучитьИдентификатор();
			КонецЕсли;
			Если СохраненноеЗначение.ИдентификаторОперации = 0 Тогда
				Прервать;
			КонецЕсли;
		ИначеЕсли СохраненноеЗначение.ИдентификаторОперации = ДанныеСтроки.ИдентификаторОперации Тогда
			Идентификаторы[0] = ДанныеСтроки.ПолучитьИдентификатор();
		ИначеЕсли СохраненноеЗначение.КлючГруппировки = КлючГруппировкиСтрок(ДанныеСтроки) Тогда
			Идентификаторы[2] = ДанныеСтроки.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЦикла;
	
	Для каждого Идентификатор Из Идентификаторы Цикл
		Если Идентификатор <> Неопределено Тогда
			Форма.Элементы.Операции.ТекущаяСтрока = Идентификатор;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИдентификаторТекущейСтроки(Форма)
	
	СохраненноеЗначение = Неопределено;
	Если НЕ Форма.Кэш.Свойство("Состояние_ТекущаяСтрока", СохраненноеЗначение) Тогда
		Возврат -1;
	Иначе
		Возврат СохраненноеЗначение.ИдентификаторСтроки;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура СохранитьСостояниеУровнейДерева()
	
	СохраненноеЗначение = Новый Массив;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки, Ложь) Цикл
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			Развернут = Элементы.Операции.Развернут(ДанныеСтроки.ПолучитьИдентификатор());
			Если Развернут Тогда
				СохраненноеЗначение.Добавить(КлючГруппировкиСтрок(ДанныеСтроки));
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Кэш.Вставить("Состояние_УровниДерева", СохраненноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьСостояниеУровнейДерева()
	
	СохраненноеЗначение = Неопределено;
	
	Если НЕ Кэш.Свойство("Состояние_УровниДерева", СохраненноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки, Ложь) Цикл
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки)
				И СохраненноеЗначение.Найти(КлючГруппировкиСтрок(ДанныеСтроки)) <> Неопределено
				И НЕ Элементы.Операции.Развернут(ИдентификаторСтроки) Тогда
			Элементы.Операции.Развернуть(ИдентификаторСтроки, Ложь);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОперацийСвертка(Свернуть = Истина, СтрокиДерева = Неопределено)
	
	Если СтрокиДерева = Неопределено Тогда
		СтрокиДерева = Операции;
	КонецЕсли;
	
	Для каждого СтрокаДерева Из СтрокиДерева.ПолучитьЭлементы() Цикл
		Если Свернуть Тогда
			ДеревоОперацийСвертка(Свернуть, СтрокаДерева);
			Элементы.Операции.Свернуть(СтрокаДерева.ПолучитьИдентификатор());
		Иначе
			Элементы.Операции.Развернуть(СтрокаДерева.ПолучитьИдентификатор(), Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ДеревоСортироватьСгруппироватьКлиент()
	
	Результат = Ложь;
	
	СохранитьСостояниеУровнейДерева();
	
	Если ДеревоСортироватьСгруппировать(ЭтотОбъект) Тогда
		ВосстановитьСостояниеУровнейДерева();
		ВосстановитьТекущуюСтроку(ЭтотОбъект);
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ДеревоСортироватьСгруппировать(Форма)
	
	СписокКлючей   = Новый СписокЗначений();
	ДанныеГрупп    = Новый Соответствие();
	ДанныеОпераций = Новый Соответствие();
	
	ДанныеСтроки = Неопределено; // Форма.Операции
	Пока СледующаяСтрока(Форма, ДанныеСтроки) Цикл
		КлючГруппировки = КлючГруппировкиСтрок(ДанныеСтроки);
		КлючСортировки  = КлючСортировкиСтрок(Форма, ДанныеСтроки);
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			ДанныеГрупп.Вставить(КлючГруппировки, ДанныеСтроки.ПолучитьЭлементы().Количество());
		Иначе
			СписокСтрок = ДанныеОпераций.Получить(КлючГруппировки);
			Если СписокСтрок = Неопределено Тогда
				СписокСтрок = Новый СписокЗначений;
				ДанныеОпераций.Вставить(КлючГруппировки, СписокСтрок);
			КонецЕсли;
			ДанныеСтрокиСтруктурой = СтрокаДереваСтруктуройКонструктор(Форма);
			ЗаполнитьЗначенияСвойств(ДанныеСтрокиСтруктурой, ДанныеСтроки);
			СписокСтрок.Добавить(ДанныеСтрокиСтруктурой, КлючСортировки);
		КонецЕсли;
		Если СписокКлючей.НайтиПоЗначению(КлючГруппировки) = Неопределено Тогда
			СписокКлючей.Добавить(КлючГруппировки, КлючСортировки);
		КонецЕсли;
	КонецЦикла;
	
	Порядок1 = СтрСоединить(СписокКлючей.ВыгрузитьЗначения(), ";");
	СписокКлючей.СортироватьПоПредставлению();
	Порядок2 = СтрСоединить(СписокКлючей.ВыгрузитьЗначения(), ";");
	
	ПерестроитьДерево = Порядок1 <> Порядок2; // Булево
	Если НЕ ПерестроитьДерево Тогда
		Для каждого ЭлементСписка Из СписокКлючей Цикл
			ДанныеГруппы   = ДанныеГрупп.Получить(ЭлементСписка.Значение);
			ДанныеОперации = ДанныеОпераций.Получить(ЭлементСписка.Значение);
			Если ((ДанныеГруппы = Неопределено) <> (ДанныеОперации = Неопределено))
				ИЛИ (ДанныеГруппы <> ДанныеОперации.Количество()) Тогда
				ПерестроитьДерево = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ПерестроитьДерево Тогда
		СтрокиДерева = Форма.Операции.ПолучитьЭлементы();
		СтрокиДерева.Очистить();
		Для каждого ЭлементСписка Из СписокКлючей Цикл
			СписокСтрок = ДанныеОпераций.Получить(ЭлементСписка.Значение);
			Если СписокСтрок <> Неопределено Тогда
				СписокСтрок.СортироватьПоПредставлению();
				Если СписокСтрок.Количество() = 1 Тогда
					НоваяСтрока = СтрокиДерева.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, СписокСтрок[0].Значение);
				Иначе
					НоваяГруппировка = СтрокиДерева.Добавить();
					Для каждого Элемент Из СписокСтрок Цикл
						НоваяСтрока = НоваяГруппировка.ПолучитьЭлементы().Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Элемент.Значение);
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ЗаполнитьСлужебныеРеквизитыСтрокОпераций(Форма);
	КонецЕсли;
	
	Возврат ПерестроитьДерево; // Булево
	
КонецФункции

&НаКлиенте
Процедура УстановитьКоличествоЗавершение(НовоеКоличество, ДополнительныеПараметры) Экспорт
	
	Если НовоеКоличество <> Неопределено Тогда
		
		УстановитьКоличествоНаСервере(НовоеКоличество, ДополнительныеПараметры.ИдентификаторыСтрок);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКоличествоНаСервере(Количество, ИдентификаторыСтрок)
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
		
		ДанныеСтрокиРодителя = ДанныеСтроки.ПолучитьРодителя();
		
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		ИдентификаторСтрокиРодителя = ?(ДанныеСтрокиРодителя <> Неопределено, ДанныеСтрокиРодителя.ПолучитьИдентификатор(), Неопределено);
		
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки)
			ИЛИ (ИдентификаторыСтрок.Найти(ИдентификаторСтроки) = Неопределено
				И ИдентификаторыСтрок.Найти(ИдентификаторСтрокиРодителя) = Неопределено) Тогда
			Продолжить;
		КонецЕсли;
		
		УстановитьРеквизитСтроки(
			ЭтотОбъект, ДанныеСтроки, "Требуется", Количество, Ложь);
		УстановитьРеквизитСтроки(
			ЭтотОбъект, ДанныеСтроки, "КоэффициентТехнологическогоПроцесса", ДанныеСтроки.Требуется / ДанныеСтроки.Количество, Ложь);
		ОтразитьИзменениеРесурсовОперации(ИдентификаторСтроки, СтруктураХраненияРесурсовОперации(ДанныеСтроки));
		
		ЗаполнитьРеквизитыГруппыОпераций(ЭтотОбъект, ДанныеСтрокиРодителя);
		
	КонецЦикла;
	
	ЗагрузитьРесурсыОперацииВОбъект(Элементы.Операции.ТекущаяСтрока);
	
	ОбновитьИндикаторыОбеспеченияОбщие();
	ОбновитьИндикаторыОбеспеченияТекущие();
	
КонецПроцедуры

#Область СлужебныеРеквизиты

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыСтрокОперацийНаСервере(ИдентификаторыСтрок = Неопределено)
	
	КэшРесурсов  = ПолучитьКэшРесурсов();
	ДанныеСтроки = Неопределено;
	
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
		ИдентификаторСтроки = ДанныеСтроки.ПолучитьИдентификатор();
		Если ИдентификаторыСтрок = Неопределено
				ИЛИ ИдентификаторыСтрок.Найти(ИдентификаторСтроки) <> Неопределено Тогда
			СтруктураДанных = СтруктураХраненияРесурсовОперации(ДанныеСтроки, КэшРесурсов);
			ОтразитьИзменениеРесурсовОперации(ИдентификаторСтроки, СтруктураДанных);
			РедакторПроизводственногоПроцесса.ПрочитатьРеквизитыРабочегоЦентра(
				ДанныеСтроки, ДанныеСтроки);
			РедакторПроизводственногоПроцессаКлиентСервер.ЗаполнитьЕдиницуИзмеренияПередаточнойПартии(
				ДанныеСтроки, ДанныеСтроки.ЕдиницаИзмеренияПередаточнойПартии);
			ЗаполнитьСлужебныеРеквизитыСтрокиОперации(ЭтотОбъект, ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьИндикаторыОбеспеченияОбщие();
	
КонецПроцедуры
	
&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеРеквизитыСтрокОпераций(Форма, ИдентификаторыСтрок = Неопределено)
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(Форма, ДанныеСтроки) Цикл
		Если ИдентификаторыСтрок = Неопределено
				ИЛИ ИдентификаторыСтрок.Найти(ДанныеСтроки.ПолучитьИдентификатор()) <> Неопределено Тогда
			ЗаполнитьСлужебныеРеквизитыСтрокиОперации(Форма, ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	СброситьСлужебныеПризнакиКэша(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСлужебныеРеквизитыСтрокиОперации(Форма, ДанныеСтроки)
	
	ЗаполнитьСостояниеОперации(Форма, ДанныеСтроки);
	
	СлужебныйРеквизитУстановить(Форма, ДанныеСтроки, "ЭтоГруппаИндивидуальныхОпераций",  ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки));
	СлужебныйРеквизитУстановить(Форма, ДанныеСтроки, "ЭтоОперацияИндивидуальная",        ЭтоОперацияИндивидуальная(ДанныеСтроки));
	СлужебныйРеквизитУстановить(Форма, ДанныеСтроки, "ЭтоОперацияИндивидуальнаяВГруппе", ЭтоОперацияИндивидуальнаяВГруппе(ДанныеСтроки));
	
	ЗаполнитьРеквизитыГруппыОпераций(Форма, ДанныеСтроки);
	
	ЗаполнитьКартинкуСтроки(Форма, ДанныеСтроки);
	
	ЗаполнитьПризнакТолькоПросмотрОперации(Форма, ДанныеСтроки);
	
	ЗаполнитьЗагрузку(Форма, ДанныеСтроки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыГруппыОпераций(Форма, ДанныеСтрокиГруппы)
	
	Если НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтрокиГруппы) Тогда
		Возврат;
	КонецЕсли;
	
	ТребуетсяГруппы          = 0;
	МодифицированностьГруппы = Ложь;
	ТолькоПросмотрГруппы     = Ложь;
	СостояниеГруппы          = Форма.Кэш.МассивСостояний.ВГраница();
	ТребуетОбеспечения       = Ложь;
	ИндикаторыРесурсов       = ИндикаторыРесурсовКонструктор(Истина);
	
	ПодчиненныеЭлементы = ДанныеСтрокиГруппы.ПолучитьЭлементы();
	СлужебныеРеквизиты = СлужебныеРеквизитыОпераций(Форма);
	
	Для Индекс = 0 По ПодчиненныеЭлементы.Количество() - 1 Цикл
		ДанныеСтроки = ПодчиненныеЭлементы[Индекс];
		Для каждого КлючИЗначение Из СлужебныеРеквизиты Цикл
			Описание = КлючИЗначение.Значение;
			Если Описание.ГрупповойРасчет Тогда
				Если Индекс= 0 Тогда
					Описание.ТекущееЗначение = ДанныеСтроки[Описание.ИмяРеквизитаОсновного];
					ДанныеСтрокиГруппы[Описание.ИмяРеквизитаСлужебного] = Истина;
				ИначеЕсли (ЗначениеЗаполнено(Описание.ТекущееЗначение) ИЛИ ЗначениеЗаполнено(ДанныеСтроки[Описание.ИмяРеквизитаОсновного]))
						И Описание.ТекущееЗначение <> ДанныеСтроки[Описание.ИмяРеквизитаОсновного] Тогда
					ДанныеСтрокиГруппы[Описание.ИмяРеквизитаСлужебного] = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ТребуетсяГруппы = ТребуетсяГруппы + ДанныеСтроки.Требуется;
	КонецЦикла;
	
	Для Индекс = 0 По ПодчиненныеЭлементы.Количество() - 1 Цикл
		ДанныеСтроки = ПодчиненныеЭлементы[Индекс];
		Для каждого КлючИЗначение Из СлужебныеРеквизиты Цикл
			Описание = КлючИЗначение.Значение;
			Если Описание.ГрупповойРасчет Тогда
				Если НЕ ПустаяСтрока(Описание.РеквизитыКонтроляРазличий) Тогда
					Синхронизировано = Истина;
					Для каждого ИмяРеквизита Из СтрРазделить(Описание.РеквизитыКонтроляРазличий, ",") Цикл
						Если НЕ ДанныеСтрокиГруппы[СлужебныеРеквизиты[ИмяРеквизита].ИмяРеквизитаСлужебного] Тогда
							Синхронизировано = Ложь;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					ДанныеСтрокиГруппы[Описание.ИмяРеквизитаСлужебного] = Синхронизировано;
				КонецЕсли;
				Если ДанныеСтрокиГруппы[Описание.ИмяРеквизитаСлужебного] Тогда
					ДанныеСтрокиГруппы[Описание.ИмяРеквизитаОсновного] = Описание.ТекущееЗначение;
					ДанныеСтроки[Описание.ИмяРеквизитаСлужебного] = Ложь;
				Иначе
					Если Описание.БлокироватьПриРазличиях ИЛИ Описание.КонтролироватьПриРазличиях Тогда
						ДанныеСтрокиГруппы[Описание.ИмяРеквизитаОсновного] = Неопределено;
					Иначе
						ДанныеСтрокиГруппы[Описание.ИмяРеквизитаОсновного] = Описание.ТекущееЗначение;
					КонецЕсли;
					ДанныеСтроки[Описание.ИмяРеквизитаСлужебного] = Истина;
				КонецЕсли;
			КонецЕсли
		КонецЦикла;
		МодифицированностьГруппы = Макс(МодифицированностьГруппы, ДанныеСтроки.Модифицированность);
		ТолькоПросмотрГруппы = Макс(ТолькоПросмотрГруппы, ДанныеСтроки.ТолькоПросмотр);
		СостояниеГруппы = Мин(СостояниеГруппы, Форма.Кэш.МассивСостояний.Найти(ДанныеСтроки.Состояние));
		ТребуетОбеспечения = Макс(ТребуетОбеспечения, ДанныеСтроки.ТребуетОбеспечения);
		Для каждого КлючИЗначение Из ИндикаторыРесурсов Цикл
			ИндикаторыРесурсов[КлючИЗначение.Ключ] = Макс(ИндикаторыРесурсов[КлючИЗначение.Ключ], ДанныеСтроки[КлючИЗначение.Ключ]);
		КонецЦикла;
	КонецЦикла;
	
	ДанныеСтрокиГруппы.Количество         = 1;
	ДанныеСтрокиГруппы.Требуется          = ?(СлужебныйРеквизитЗначение(Форма, ДанныеСтрокиГруппы, "Требуется"), ТребуетсяГруппы, 0);
	ДанныеСтрокиГруппы.Модифицированность = МодифицированностьГруппы;
	ДанныеСтрокиГруппы.ТолькоПросмотр     = ТолькоПросмотрГруппы;
	ДанныеСтрокиГруппы.Состояние          = Форма.Кэш.МассивСостояний[СостояниеГруппы];
	ДанныеСтрокиГруппы.ТребуетОбеспечения = ТребуетОбеспечения;
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиГруппы, ИндикаторыРесурсов);
	ИндикаторыРесурсовЗаполнитьКартинки(ДанныеСтрокиГруппы);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьРеквизитыОперацийГруппы(Форма, ДанныеСтрокиГруппы, ИмяРеквизита)
	
	Если НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтрокиГруппы) Тогда
		Возврат;
	КонецЕсли;
	
	Описание = СлужебныйРеквизитОписание(Форма, ИмяРеквизита);
	Если Описание = Неопределено
		ИЛИ НЕ Описание.ГрупповоеРедактирование Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтрокиГруппы[Описание.ИмяРеквизитаСлужебного] = Истина;
	Для каждого ДанныеСтроки Из ДанныеСтрокиГруппы.ПолучитьЭлементы() Цикл
		УстановитьРеквизитСтроки(Форма, ДанныеСтроки, ИмяРеквизита, ДанныеСтрокиГруппы[ИмяРеквизита], Ложь);
		ДанныеСтроки[Описание.ИмяРеквизитаСлужебного] = Ложь;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСостояниеОперации(Форма, ДанныеСтроки)
	
	Если ДанныеСтроки.ТолькоПросмотр И ЗначениеЗаполнено(ДанныеСтроки.Состояние)
			ИЛИ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Показатели = ПоказателиВыполнения(ДанныеСтроки.ПоказателиВыполнения);
	Состояние  = ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.ОжиданиеПредшествующих");
	
	Если ЗначениеЗаполнено(Показатели.Состояние) Тогда
		Состояние = Показатели.Состояние;
		Если Форма.Кэш.МассивСостояний.Найти(Состояние) > ГраницаРедактируемыхСостояний() И Показатели.Назначена = Истина Тогда
			Состояние = ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.Назначена");
		КонецЕсли; 
	Иначе
		МожноВыполнять = Истина;
		Для каждого Строка Из ПредыдущиеОперации(Форма, ДанныеСтроки).ПоНомеру Цикл
			МожноВыполнять = Мин(МожноВыполнять, Форма.Кэш.МассивСостояний.Найти(Строка.Состояние) < ГраницаКонечныхСостояний());
		КонецЦикла;
		Если МожноВыполнять ИЛИ Показатели.МожноВыполнять > 0 Тогда
			Состояние = ?(Форма.ПараметрыПодразделения.ИспользоватьСменныеЗадания,
				ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.КНазначению"),
				ПредопределенноеЗначение("Перечисление.СостоянияВыполненияОпераций.КВыполнению"));
		КонецЕсли;
	КонецЕсли;
	
	ДанныеСтроки.Состояние = Состояние;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПризнакТолькоПросмотрОперации(Форма, ДанныеСтроки)
	
	Если НЕ ДанныеСтроки.ТолькоПросмотр
		И НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		
		ДанныеСтроки.ЕстьСозданные  = ПоказателиВыполнения(ДанныеСтроки.ПоказателиВыполнения).Создано > 0;
		ДанныеСтроки.ТолькоПросмотр = Форма.Кэш.МассивСостояний.Найти(ДанныеСтроки.Состояние) < ГраницаРедактируемыхСостояний()
			ИЛИ ДанныеСтроки.ЕстьСозданные;
		
		Если ДанныеСтроки.ТолькоПросмотр Тогда
			ДанныеПредшественника = Неопределено;
			Пока СледующаяСтрока(Форма, ДанныеПредшественника) Цикл
				Если ДанныеПредшественника.НомерОперации < ДанныеСтроки.НомерОперации Тогда
					Если НЕ ДанныеПредшественника.ТолькоПросмотр Тогда
						ДанныеПредшественника.ТолькоПросмотр = Истина;
					КонецЕсли;
				Иначе
					Прервать;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ДанныеСтроки.ПоказателиВыполнения = Неопределено;
		КонецЕсли;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьЗагрузку(Форма, ДанныеСтроки)
	
	Если ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		
		ДанныеСтроки.ЗагрузкаНорматив = ?(ДанныеСтроки.Требуется > 0, ДанныеСтроки.Загрузка / ДанныеСтроки.Требуется, 0);
		
	Иначе
		
		ДанныеСтроки.Загрузка = ДанныеСтроки.ЗагрузкаНорматив * ДанныеСтроки.Требуется / ДанныеСтроки.Количество;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СлужебныеРеквизитыОперацийСоздать()
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для каждого КлючИзЗначение Из СлужебныеРеквизитыОпераций(ЭтотОбъект) Цикл
		
		Описание = КлючИзЗначение.Значение;
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(Описание.ИмяРеквизитаСлужебного, Описание.Тип, "Операции"));
		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СлужебныеРеквизитыОпераций(Форма)
	
	Если Форма.Кэш <> Неопределено
		И Форма.Кэш.Свойство("СлужебныеРеквизитыОпераций") Тогда
			
		Результат = Форма.Кэш.СлужебныеРеквизитыОпераций; // Структура
		
	Иначе
		
		Результат = Новый Структура;
		
		//
		СписокРеквизитов = "ЭтоОперацияИндивидуальная,ЭтоГруппаИндивидуальныхОпераций,ЭтоОперацияИндивидуальнаяВГруппе";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповойРасчет = Ложь;
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = "Наименование,НомерОперации,НомерСледующейОперации";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповоеРедактирование = Истина;
			Описание.ВКолонкеСписка = Истина;
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = "ЕдиницаИзмерения,Участок,РабочийЦентр";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповоеРедактирование = Истина;
			Описание.БлокироватьПриРазличиях = Истина;
			Описание.ВКолонкеСписка = Истина;
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(КонтролируемыеРеквизитыОперацииВГруппе());
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповоеРедактирование = Истина;
			Описание.КонтролироватьПриРазличиях = Истина;
			Описание.ТекстГруппыПриРазличиях = НСтр("ru = '<должен совпадать у операций группы>';
													|en = '<must match for group operations>'");
			Описание.ТекстОперацииПриРазличиях = НСтр("ru = '<задается на уровне группы>';
														|en = '<set at the group level>'");
			Описание.НаПанелиСвойств = Истина;
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = "ВариантНаладки,Загрузка,ПередаточнаяПартия,МожноПовторить,МожноПропустить,Непрерывная,Описание";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповоеРедактирование = Истина;
			Описание.БлокироватьПриРазличиях = Истина;
			Описание.НаПанелиСвойств = Истина;
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = "ВремяШтучное,ВремяШтучноеЕдИзм,ВремяПЗ,ВремяПЗЕдИзм,ВремяРаботыПриСинхроннойЗагрузке,ЕдиницаИзмеренияПриСинхроннойЗагрузке";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита);
			Описание.ГрупповоеРедактирование = Истина;
			Описание.БлокироватьПриРазличиях = Истина;
			Описание.ВКолонкеСписка = Истина;
			Описание.РеквизитыКонтроляРазличий = ИмяРеквизита+",ЕдиницаИзмерения,Участок,РабочийЦентр,ВариантНаладки";
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		СписокРеквизитов = "ВремяРаботыПриСинхроннойЗагрузке,ЕдиницаИзмеренияЗагрузки,ЕдиницаИзмеренияПередаточнойПартии,
		|ЕдиницаИзмеренияПриСинхроннойЗагрузке,ИспользуютсяВариантыНаладки,ПараллельнаяЗагрузка,СинхроннаяЗагрузка";
		Для каждого ИмяРеквизита Из СтрРазделить(СписокРеквизитов,",") Цикл
			Описание = ОписаниеСлужебногоРеквизитаКонструктор(СтрЗаменить(ИмяРеквизита, Символы.ПС, ""));
			Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		КонецЦикла;
		
		//
		Описание = ОписаниеСлужебногоРеквизитаКонструктор("Требуется");
		Описание.БлокироватьПриРазличиях = Истина;
		Описание.ВКолонкеСписка = Истина;
		Описание.РеквизитыКонтроляРазличий = "ЕдиницаИзмерения";
		Результат.Вставить(Описание.ИмяРеквизитаОсновного, Описание);
		
		Если Форма.Кэш <> Неопределено Тогда
			Форма.Кэш.Вставить("СлужебныеРеквизитыОпераций", Результат);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СлужебныйРеквизитОписание(Форма, ИмяРеквизита)
	
	Описание = Неопределено;
	СлужебныеРеквизитыОпераций(Форма).Свойство(ИмяРеквизита, Описание);
	Возврат Описание;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СлужебныйРеквизитУстановить(Форма, ДанныеСтроки, ИмяРеквизита, Значение)
	
	Описание = СлужебныйРеквизитОписание(Форма, ИмяРеквизита); // см. ОписаниеСлужебногоРеквизитаКонструктор
	ДанныеСтроки[Описание.ИмяРеквизитаСлужебного] = Значение;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СлужебныйРеквизитЗначение(Форма, ДанныеСтроки, ИмяРеквизита)
	
	Описание = СлужебныйРеквизитОписание(Форма, ИмяРеквизита); // см. ОписаниеСлужебногоРеквизитаКонструктор
	Значение = ДанныеСтроки[Описание.ИмяРеквизитаСлужебного]; // Булево
	Возврат Значение;
	
КонецФункции

// Нормативное количество операций с учетом коэффициента применения техпроцесса
// 
// Параметры:
//  ПараметрыТехпроцесса - см. ПараметрыТехпроцессаКонструктор
//  ДанныеСтроки - ДанныеФормыЭлементДерева
// 
// Возвращаемое значение:
//  Число
&НаСервере
Функция КоличествоОперацийНормативное(ДанныеСтроки)
	
	Результат = ДанныеСтроки.Требуется * Объект.КоличествоНаЕдиницуПартииВыпуска / Объект.Количество;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДобавлениеУдалениеРазделение

&НаСервере
Функция ДобавитьОперацииТехпроцесса(ПараметрыТехпроцесса = Неопределено, ОтборОпераций = Неопределено, Нумерация = 0)
	
	ИдентификаторыСтрок = Новый Массив();
	ПоследнийНомер = ПоследнийНомер(ЭтотОбъект);
	СохранитьИдентификаторыОпераций = Ложь;
	
	Если ПараметрыТехпроцесса = Неопределено Тогда
		ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
		ЗаполнитьЗначенияСвойств(ПараметрыТехпроцесса, Объект);
		СохранитьИдентификаторыОпераций = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ПараметрыТехпроцесса.ТехнологическийПроцесс) Тогда
		Возврат ИдентификаторыСтрок;
	КонецЕсли;
	
	ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Объект.Этап, Ложь)[Объект.Этап];
	ЗаполнитьЗначенияСвойств(ДанныеЭтапа, ПараметрыТехпроцесса);
	
	ОперацииРедактора   = Операции.ПолучитьЭлементы();
	ОперацииТехпроцесса = РегистрыСведений.ОчередьПроизводственныхОпераций.СписокОперацийПоДаннымЭтапа(ДанныеЭтапа, Объект.СохранитьОграниченияТехпроцесса);
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОперацииТехпроцесса.ВыгрузитьКолонку("Операция"), "КТО,Участок,Описание");
	
	Для каждого Строка Из ОперацииТехпроцесса Цикл
		Если ОтборОпераций = Неопределено ИЛИ ОтборОпераций.Найти(Строка.Операция) <> Неопределено Тогда
			НоваяСтрока = ОперацииРедактора.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Требуется = Строка.КоличествоНаПартию;
			НоваяСтрока.КоэффициентТехнологическогоПроцесса = ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса;
			НоваяСтрока.Наименование = Строка.ОперацияПредставление;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗначенияРеквизитов[НоваяСтрока.Операция]);
			НоваяСтрока.ЗагрузкаНорматив = Строка.Загрузка;
			Если Нумерация = 1 Тогда // 0 - не изменять, 1 - сместить, 2 - дополнить
				НоваяСтрока.НомерОперации = НоваяСтрока.НомерОперации + ПоследнийНомер;
				НоваяСтрока.НомерСледующейОперации = НоваяСтрока.НомерСледующейОперации + ПоследнийНомер;
			ИначеЕсли Нумерация = 2 Тогда
				НоваяСтрока.НомерОперации = ПоследнийНомер + 1;
				НоваяСтрока.НомерСледующейОперации = НоваяСтрока.НомерОперации + 1;
				ПоследнийНомер = ПоследнийНомер + 1;
			КонецЕсли;
			Если НЕ СохранитьИдентификаторыОпераций Тогда
				НоваяСтрока.ИдентификаторОперации = НовыйИдентификаторОперации(ЭтотОбъект);
			КонецЕсли;
			Если ЗначениеЗаполнено(Строка.МаршрутнаяКарта) Тогда
				НоваяСтрока.ТехнологическийПроцесс = Строка.МаршрутнаяКарта;
			КонецЕсли;
			НоваяСтрока.Модифицированность = Нумерация <> 0;
			ИдентификаторыСтрок.Добавить(НоваяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ Объект.СохранитьОграниченияТехпроцесса Тогда
		СкорректироватьНумерациюОпераций(ЭтотОбъект);
	КонецЕсли;
	
	Возврат ИдентификаторыСтрок;
	
КонецФункции

&НаСервере
Процедура ОперацииДобавитьПодбором(РезультатПодбора)
	
	Если НЕ ЗначениеЗаполнено(РезультатПодбора) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
	ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = АльтернативныйТехпроцесс();
	ПараметрыТехпроцесса.ТехнологическийПроцесс      = РезультатПодбора.ТехнологическийПроцесс;
	ПараметрыТехпроцесса.КоэффициентТехнологическогоПроцесса = Объект.КоэффициентТехнологическогоПроцесса;
	
	ИдентификаторыСтрок = ДобавитьОперацииТехпроцесса(ПараметрыТехпроцесса, РезультатПодбора.Операции, 2);
	ЗаполнитьСлужебныеРеквизитыСтрокОперацийНаСервере(ИдентификаторыСтрок);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораОперацииПоКлассификатору(ВыбранноеЗначение, ДобавитьНовую = Ложь)
	
	Если НЕ ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДобавитьНовую Тогда
		
		НоваяСтрока = ОперацииДобавитьНовую();
		Если НоваяСтрока <> Неопределено Тогда
			ИдентификаторСтроки = НоваяСтрока.ПолучитьИдентификатор()
		КонецЕсли;
		
	Иначе
		
		ИдентификаторСтроки = Элементы.Операции.ТекущаяСтрока;
		
	КонецЕсли;
	
	Если ИдентификаторСтроки <> Неопределено Тогда
		
		СписокРеквизитов = ОбработкаВыбораОперацииПоКлассификаторуНаСервере(ИдентификаторСтроки, ВыбранноеЗначение);
		
		ПриИзмененииОперации(СписокРеквизитов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработкаВыбораОперацииПоКлассификаторуНаСервере(ИдентификаторСтроки, ВыбранноеЗначение)
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ВыбранноеЗначение,
		Новый Структура("КТО,Наименование,ВидОперации", "Ссылка"));
	
	Если НЕ ЗначениеЗаполнено(ЗначенияРеквизитов.ВидОперации) Тогда
		ЗначенияРеквизитов.Удалить("ВидОперации");
	КонецЕсли;
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Для каждого КлючИЗначение Из ЗначенияРеквизитов Цикл
		УстановитьРеквизитСтроки(ЭтотОбъект, ДанныеСтроки, КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Возврат ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(ЗначенияРеквизитов);
	
КонецФункции

&НаКлиенте
Функция ОперацииДобавитьКопию(ИдентификаторИсточника, СохранитьНумерацию = Ложь)
	
	ТекстСообщения = ПроверитьКорректностьВыделенияСтрок(ЭтотОбъект,
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторИсточника),
		"Детальные");
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		ПоказатьПредупреждение(, ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	
	НоваяСтрока = ОперацииДобавитьКопированиемКлиент(ИдентификаторИсточника, СохранитьНумерацию);
	
	ОперацииДобавитьКопированиемНаСервере(НоваяСтрока.ПолучитьИдентификатор());
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция ОперацииДобавитьКопированиемКлиент(ИдентификаторИсточника, СохранитьНумерацию = Ложь)
	
	ПоследнийНомер = ПоследнийНомер(ЭтотОбъект);
	СтрокаИсточник = Операции.НайтиПоИдентификатору(ИдентификаторИсточника);
	
	НоваяСтрока = КоллекцияСтрок(ЭтотОбъект, ?(СохранитьНумерацию, СтрокаИсточник, Неопределено)).Добавить();
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаИсточник);
	НоваяСтрока.ИдентификаторОперации    = НовыйИдентификаторОперации(ЭтотОбъект);
	НоваяСтрока.ПроизводственнаяОперация = Неопределено;
	НоваяСтрока.ТолькоПросмотр           = Ложь;
	НоваяСтрока.Состояние                = Неопределено;
	НоваяСтрока.ПоказателиВыполнения     = Неопределено;
	
	Если НЕ СохранитьНумерацию Тогда
		НоваяСтрока.НомерОперации = ПоследнийНомер + 1;
		НоваяСтрока.НомерСледующейОперации = 0;
	КонецЕсли;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Процедура ОперацииДобавитьКопированиемНаСервере(ИдентификаторСтроки)
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоОперацияПоСпецификации(ДанныеСтроки) Тогда
		ПреобразоватьОперациюВИндивидуальную(ИдентификаторСтроки);
	ИначеЕсли ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		СохранитьРесурсыИндивидуальнойОперации(ИдентификаторСтроки);
	КонецЕсли;
	
	ЗаполнитьСлужебныеРеквизитыСтрокОперацийНаСервере(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторСтроки));
	
КонецПроцедуры

&НаКлиенте
Функция ОперацииДобавитьНовую(Копирование = Ложь)
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	
	Если Копирование И ТекущаяСтрока <> Неопределено Тогда
		НоваяСтрока = ОперацииДобавитьКопию(ТекущаяСтрока);
	Иначе
		ПоследнийНомер = ПоследнийНомер(ЭтотОбъект);
		СписокОпераций = Операции.ПолучитьЭлементы();
		
		НоваяСтрока = СписокОпераций.Добавить();
		НоваяСтрока.ИдентификаторОперации = НовыйИдентификаторОперации(ЭтотОбъект);
		НоваяСтрока.Наименование          = СтрШаблон(НСтр("ru = 'Новая операция %1';
															|en = 'New operation %1'"), СписокОпераций.Количество());
		НоваяСтрока.НомерОперации         = ПоследнийНомер + 1;
		НоваяСтрока.ВремяШтучноеЕдИзм     = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Минута");
		НоваяСтрока.ВремяПЗЕдИзм          = ПредопределенноеЗначение("Перечисление.ЕдиницыИзмеренияВремени.Минута");
		НоваяСтрока.Контроль              = ПредопределенноеЗначение("Перечисление.ВариантыКонтроляТехнологическихОпераций.НеТребуется");
		НоваяСтрока.Количество            = 1;
		НоваяСтрока.Требуется             = Объект.КоэффициентТехнологическогоПроцесса;
		НоваяСтрока.КоэффициентТехнологическогоПроцесса = Объект.КоэффициентТехнологическогоПроцесса;
	КонецЕсли;
	
	Если НоваяСтрока = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СкорректироватьНумерациюОпераций(ЭтотОбъект);
	
	ЗаполнитьСлужебныеРеквизитыСтрокиОперации(ЭтотОбъект, НоваяСтрока);
	ЗаполнитьРеквизитыГруппыОпераций(ЭтотОбъект, НоваяСтрока.ПолучитьРодителя());
	
	Модифицированность = Истина;
	НоваяСтрока.Модифицированность = Истина;
	
	Элементы.Операции.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ОперацииПередУдалениемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		УдалитьОперации(ДополнительныеПараметры.ИдентификаторыСтрок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОперацииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		УдалитьОперации(ДополнительныеПараметры.ИдентификаторыСтрок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОперации(ИдентификаторыСтрок)
	
	УдалитьОперацииНаСервере(ИдентификаторыСтрок);
	
	ДеревоСортироватьСгруппироватьКлиент();
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОперацииНаСервере(ИдентификаторыСтрок)
	
	ИндексыДоИзменения = ИндексыОпераций(ЭтотОбъект);
	
	МассивСтрок = Новый Массив;
	МассивГрупп = Новый Массив;
		
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			Для каждого ДанныеПодчиненнойСтроки Из ДанныеСтроки.ПолучитьЭлементы() Цикл
				МассивСтрок.Добавить(ДанныеПодчиненнойСтроки);
			КонецЦикла;
			МассивГрупп.Добавить(ДанныеСтроки);
		Иначе
			МассивСтрок.Добавить(ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого ДанныеСтроки Из МассивСтрок Цикл
		ОтразитьИзменениеРесурсовОперации(ДанныеСтроки.ПолучитьИдентификатор());
		КоллекцияСтрок(ЭтотОбъект, ДанныеСтроки).Удалить(ДанныеСтроки);
	КонецЦикла;
	
	Для каждого ДанныеСтроки Из МассивГрупп Цикл
		КоллекцияСтрок(ЭтотОбъект, ДанныеСтроки).Удалить(ДанныеСтроки);
	КонецЦикла;
	
	СкорректироватьНумерациюОпераций(ЭтотОбъект, ИндексыДоИзменения);
	
	ОбновитьИндикаторыОбеспеченияОбщие();
	
КонецПроцедуры

&НаКлиенте
Процедура ОперацииРазделитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ИдентификаторИсточника = ДополнительныеПараметры.ИдентификаторСтроки;
		
		ИдентификаторНовый = ОперацииДобавитьКопированиемКлиент(ИдентификаторИсточника, Истина).ПолучитьИдентификатор();
		
		ОперацииРазделитьЗавершениеНаСервере(Результат, ИдентификаторИсточника, ИдентификаторНовый);
		
		СохранитьТекущуюСтроку(ЭтотОбъект, ИдентификаторНовый);
		
		ДеревоСортироватьСгруппироватьКлиент();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОперацииРазделитьЗавершениеНаСервере(Результат, ИдентификаторИсточника, ИдентификаторПриемника)
	
	ПреобразоватьОперациюВИндивидуальную(ИдентификаторИсточника);
	ПреобразоватьОперациюВИндивидуальную(ИдентификаторПриемника);
	
	СтрокаИсточник = Операции.НайтиПоИдентификатору(ИдентификаторИсточника);
	СтрокаПриемник = Операции.НайтиПоИдентификатору(ИдентификаторПриемника);
	
	УстановитьРеквизитСтроки(ЭтотОбъект, СтрокаИсточник, "Требуется", СтрокаИсточник.Требуется - Результат.Количество, Ложь);
	УстановитьРеквизитСтроки(ЭтотОбъект, СтрокаПриемник, "Участок", Результат.Участок, Ложь);
	УстановитьРеквизитСтроки(ЭтотОбъект, СтрокаПриемник, "РабочийЦентр", Результат.РабочийЦентр, Ложь);
	УстановитьРеквизитСтроки(ЭтотОбъект, СтрокаПриемник, "Требуется", Результат.Количество, Ложь);
	
	ОбработатьНаСервере = Новый Массив;
	Если СтрокаИсточник.РабочийЦентр <> СтрокаПриемник.РабочийЦентр Тогда
		ОбработатьНаСервере.Добавить("РабочийЦентр");
	КонецЕсли;
	Если СтрокаИсточник.Участок <> СтрокаПриемник.Участок Тогда
		ОбработатьНаСервере.Добавить("Участок");
	КонецЕсли;
	
	СтруктураДанных = СтруктураРесурсовТекущая(ЭтотОбъект);
	
	РесурсыИсточника = ПересчитатьСтруктуруРесурсовПоКоэффициенту(СтруктураДанных, СтрокаИсточник.Требуется / (СтрокаИсточник.Требуется + СтрокаПриемник.Требуется));
	СохранитьРесурсыИндивидуальнойОперации(ИдентификаторИсточника, РесурсыИсточника);
	
	РесурсыПриемника = ПересчитатьСтруктуруРесурсовПоКоэффициенту(СтруктураДанных, СтрокаПриемник.Требуется / (СтрокаИсточник.Требуется + СтрокаПриемник.Требуется));
	СохранитьРесурсыИндивидуальнойОперации(ИдентификаторПриемника, РесурсыПриемника);
	
	ПриИзмененииОперацииНаСервере(СтрСоединить(ОбработатьНаСервере,","), ИдентификаторПриемника);
	
КонецПроцедуры

#КонецОбласти

#Область ПеремещениеНумерация

&НаКлиенте
Процедура СдвинутьОперации(ИдентификаторыСтрок, ИмяДействия)
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторыСтрок) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьВозможностьРедактированияОпераций(ИдентификаторыСтрок) Тогда
		
		СписокОпераций = Операции.ПолучитьЭлементы();
		
		Индексы1 = ИндексыОпераций(ЭтотОбъект, ИдентификаторыСтрок);
		Если ИмяДействия = "ОперацииСдвинутьВниз" Тогда
			Смещение = 1;
			Индексы1.СортироватьПоПредставлению(НаправлениеСортировки.Убыв);
			Если Число(Индексы1[0].Представление) + Смещение > СписокОпераций.Количество() - 1 Тогда
				Смещение = 0;
			КонецЕсли;
		ИначеЕсли ИмяДействия = "ОперацииСдвинутьВверх" Тогда
			Смещение = -1;
			Индексы1.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
			Если Число(Индексы1[0].Представление) + Смещение < НачальныйИндексРедактирования(ЭтотОбъект) Тогда
				Смещение = 0;
			КонецЕсли;
		Иначе
			Возврат;
		КонецЕсли;
		
		Если Смещение = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Для каждого ЭлементСписка Из Индексы1 Цикл
			ТекущийИндекс = Число(ЭлементСписка.Представление);
			ДанныеСтроки = СписокОпераций[ТекущийИндекс];
			УстановитьРеквизитСтроки(ЭтотОбъект, ДанныеСтроки, "НомерОперации", ДанныеСтроки.НомерОперации + Смещение);
			УстановитьРеквизитСтроки(ЭтотОбъект, ДанныеСтроки, "НомерСледующейОперации", ДанныеСтроки.НомерСледующейОперации + Смещение);
			ВыполнитьСдвиг = ?(Смещение > 0,
				ДанныеСтроки.НомерОперации >= СписокОпераций[ТекущийИндекс+1].НомерОперации,
				ДанныеСтроки.НомерОперации <= СписокОпераций[ТекущийИндекс-1].НомерОперации);
			Если ВыполнитьСдвиг Тогда
				Индексы2 = ИндексыОпераций(ЭтотОбъект, ИдентификаторыСтрок, Истина);
				СписокОпераций.Сдвинуть(ТекущийИндекс, Смещение);
				СкорректироватьНумерациюОпераций(ЭтотОбъект, Индексы2);
			КонецЕсли;
		КонецЦикла;
		
		ОбновитьИндикаторыОбеспеченияОбщие();
		
		ЗаполнитьСлужебныеРеквизитыСтрокОпераций(ЭтотОбъект);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкорректироватьНумерациюОпераций(Форма, ИндексыДоИзменения = Неопределено)
	
	СписокОпераций = Форма.Операции.ПолучитьЭлементы();
	
	Для Индекс = 0 По СписокОпераций.Количество() - 1 Цикл
		ДанныеСтроки = СписокОпераций[Индекс];
		Если ИндексыДоИзменения <> Неопределено Тогда
			НайденныйЭлемент = ИндексыДоИзменения.НайтиПоЗначению(ДанныеСтроки.ПолучитьИдентификатор());
			Если НайденныйЭлемент <> Неопределено Тогда
				Дельта = Индекс - Число(НайденныйЭлемент.Представление);
				Если Дельта <> 0 Тогда
					УстановитьРеквизитСтроки(Форма, ДанныеСтроки, "НомерОперации", ДанныеСтроки.НомерОперации + Дельта);
					УстановитьРеквизитСтроки(Форма, ДанныеСтроки, "НомерСледующейОперации", ДанныеСтроки.НомерСледующейОперации + Дельта);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		Если Индекс = СписокОпераций.Количество() - 1 Тогда
			Если ДанныеСтроки.НомерСледующейОперации <> 0 Тогда
				УстановитьРеквизитСтроки(Форма, ДанныеСтроки, "НомерСледующейОперации", 0);
			КонецЕсли;
		Иначе
			Если ДанныеСтроки.НомерСледующейОперации = 0 Тогда
				УстановитьРеквизитСтроки(Форма, ДанныеСтроки, "НомерСледующейОперации", ДанныеСтроки.НомерОперации + 1);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИндексыОпераций(Форма, ИдентификаторыСтрок = Неопределено, Исключить = Ложь)
	
	Результат = Новый СписокЗначений();
	СписокОпераций = Форма.Операции.ПолучитьЭлементы();
	
	Для каждого Строка Из СписокОпераций Цикл
		ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
		Если ИдентификаторыСтрок = Неопределено
				ИЛИ (ИдентификаторыСтрок.Найти(ИдентификаторСтроки) <> Неопределено И Исключить = Ложь)
				ИЛИ (ИдентификаторыСтрок.Найти(ИдентификаторСтроки) = Неопределено И Исключить = Истина) Тогда
			Результат.Добавить(ИдентификаторСтроки, ИндексПредставление(СписокОпераций.Индекс(Строка)));
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИндексПредставление(Индекс)
	
	Возврат Прав("000000"+Формат(Индекс,"ЧГ=;"), 6);
	
КонецФункции

&НаКлиенте
Процедура ОперацииПеренумероватьЗавершение(НачальныйНомер, ДополнительныеПараметры) Экспорт
	
	Если НачальныйНомер <> Неопределено Тогда
		
		МинимальныйНомер = МинимальныйРедактируемыйНомер(ЭтотОбъект);
		Если НачальныйНомер < МинимальныйНомер Тогда
			ПоказатьПредупреждение(, СтрШаблон(НСтр("ru = 'Номер должен быть не меньше %1';
													|en = 'Number must be not less than %1'"), МинимальныйНомер));
			Возврат;
		КонецЕсли;
		
		Номер = НачальныйНомер;
		Для каждого ЭлементСписка Из ДополнительныеПараметры.ИндексыСтрок Цикл
			ДанныеСтроки = Операции.НайтиПоИдентификатору(ЭлементСписка.Значение);
			УстановитьРеквизитСтроки(ЭтотОбъект, ДанныеСтроки, "НомерОперации", Номер);
			УстановитьРеквизитСтроки(ЭтотОбъект, ДанныеСтроки, "НомерСледующейОперации", Номер + 1);
			Номер = Номер + 1;
		КонецЦикла;
		
		СкорректироватьНумерациюОпераций(ЭтотОбъект);
		
		ДеревоСортироватьСгруппироватьКлиент();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПоследнийНомер(Форма)
	
	СписокОпераций     = Форма.Операции.ПолучитьЭлементы();
	КоличествоОпераций = СписокОпераций.Количество();
	
	Возврат ?(КоличествоОпераций > 0, СписокОпераций[КоличествоОпераций-1].НомерОперации, 0);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПредыдущиеОперации(Форма, Курсор)
	
	КоллекцияСтрок = Форма.Операции.ПолучитьЭлементы();
	
	ПредыдущиеПоНомеру  = Новый Массив;
	ПредыдущиеПоИндексу = Новый Массив;
	
	ИндексКурсора = ?(ЭтоОперацияИндивидуальнаяВГруппе(Курсор),
		КоллекцияСтрок.Индекс(Курсор.ПолучитьРодителя()),
		КоллекцияСтрок.Индекс(Курсор));
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(Форма, ДанныеСтроки) Цикл
		Если ДанныеСтроки.НомерСледующейОперации = Курсор.НомерОперации
			И НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			ПредыдущиеПоНомеру.Добавить(ДанныеСтроки);
		КонецЕсли;
		ИндексТекущий = ?(ЭтоОперацияИндивидуальнаяВГруппе(ДанныеСтроки),
			КоллекцияСтрок.Индекс(ДанныеСтроки.ПолучитьРодителя()),
			КоллекцияСтрок.Индекс(ДанныеСтроки));
		Если ИндексТекущий = ИндексКурсора -1
			И НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			ПредыдущиеПоИндексу.Добавить(ДанныеСтроки);
		КонецЕсли;
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ПоНомеру", ПредыдущиеПоНомеру);
	Результат.Вставить("ПоИндексу", ПредыдущиеПоИндексу);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МинимальныйРедактируемыйНомер(Форма)
	
	Если ЕстьЗаблокированныеОперации(Форма) Тогда
		СписокОпераций = Форма.Операции.ПолучитьЭлементы();
		Результат = СписокОпераций[НачальныйИндексРедактирования(Форма)-1].НомерОперации+1;
	Иначе
		Результат = 1;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РесурсыОпераций

// Перечень ресурсов.
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
//  ВМассив - Булево - необходимость свернуть в массив
// 
// Возвращаемое значение:
//  Строка, Массив из Строка - перечень ресурсов
&НаКлиентеНаСервереБезКонтекста
Функция ПереченьРесурсов(Форма, ВМассив = Ложь)
	
	ПереченьДанных = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(ПереченьРесурсовДляЗаполненияПоНСИ(Форма));
	
	Если ВМассив Тогда
		Возврат СтрРазделить(ПереченьДанных, ",");
	Иначе
		Возврат ПереченьДанных;
	КонецЕсли;
	
КонецФункции

// Перечень ресурсов для переноса данных НСИ (спецификации, маршрутной карты) в редактор операций и ПО
// 
// Параметры:
// 	Форма - ФормаКлиентскогоПриложения
// 	УчитыватьПараметрыПодразделения - Булево
// 
// Возвращаемое значение:
//  Структура
&НаКлиентеНаСервереБезКонтекста
Функция ПереченьРесурсовДляЗаполненияПоНСИ(Форма, УчитыватьПараметрыПодразделения = Ложь)
	
	Результат = Новый Структура;
	
	Если Форма.ПараметрыПодразделения.ИспользоватьВыходныеИзделияВОперациях
		ИЛИ НЕ УчитыватьПараметрыПодразделения Тогда
		Результат.Вставить("ВыходныеИзделия", "ВыходныеИзделия,ВозвратныеОтходы");
	КонецЕсли;
	Если Форма.ПараметрыПодразделения.ИспользоватьМатериалыВОперациях
		ИЛИ НЕ УчитыватьПараметрыПодразделения Тогда
		Результат.Вставить("МатериалыИУслуги", "МатериалыИУслуги");
	КонецЕсли;
	Результат.Вставить("Трудозатраты", "Трудозатраты");
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПереченьРесурсовПараметрыСвертки(ИмяТЧ)
	
	Результат = Новый Структура("КолонкиГруппировок,КолонкиСуммирования");
	
	Если ИмяТЧ = "ВыходныеИзделия" Тогда
		Результат.КолонкиГруппировок = "Номенклатура,Характеристика,Упаковка";
		Результат.КолонкиСуммирования = "КоличествоУпаковок,Количество";
	ИначеЕсли ИмяТЧ = "МатериалыИУслуги" Тогда
		Результат.КолонкиГруппировок = "Номенклатура,Характеристика,Упаковка,СтатьяКалькуляции,ТипНоменклатуры";
		Результат.КолонкиСуммирования = "КоличествоУпаковок,Количество";
	ИначеЕсли ИмяТЧ = "Трудозатраты" Тогда
		Результат.КолонкиГруппировок = "ВидРабот,СтатьяКалькуляции";
		Результат.КолонкиСуммирования = "Количество";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПереченьРесурсовПроверкаЗаполнения(ИмяТЧ, ДанныеСтроки)
	
	Результат = Новый Массив;
	
	Если ИмяТЧ = "ВыходныеИзделия" Тогда
		Результат.Добавить("Номенклатура");
		Результат.Добавить("КоличествоУпаковок");
	ИначеЕсли ИмяТЧ = "МатериалыИУслуги" Тогда
		Результат.Добавить("Номенклатура");
		Результат.Добавить("КоличествоУпаковок");
		Если ЭтоОперацияИндивидуальная(ДанныеСтроки) И НЕ ДанныеСтроки.ТолькоПросмотр Тогда
			Результат.Добавить("СтатьяКалькуляции");
		КонецЕсли;
	ИначеЕсли ИмяТЧ = "Трудозатраты" Тогда
		Результат.Добавить("ВидРабот");
		Результат.Добавить("Количество");
		Если ЭтоОперацияИндивидуальная(ДанныеСтроки) И НЕ ДанныеСтроки.ТолькоПросмотр Тогда
			Результат.Добавить("СтатьяКалькуляции");
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьНоменклатураУпаковкаВТЧ(ИмяТЧ)
	
	Возврат ИмяТЧ = "ВыходныеИзделия" ИЛИ ИмяТЧ = "МатериалыИУслуги";
	
КонецФункции

&НаСервере
Функция КлючХраненияРесурсов(ДанныеСтроки)
	
	Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	
	Если ЭтоСтандартныйТехпроцесс(Объект) ИЛИ ЭтоАльтернативныйТехпроцесс(Объект) Тогда
		ХешированиеДанных.Добавить(Строка(Объект.ТехнологическийПроцесс.УникальныйИдентификатор()));
		ХешированиеДанных.Добавить(Строка(Объект.КоэффициентТехнологическогоПроцесса));
	ИначеЕсли ЗначениеЗаполнено(ДанныеСтроки.ТехнологическийПроцесс) Тогда
		ХешированиеДанных.Добавить(Строка(ДанныеСтроки.ТехнологическийПроцесс.УникальныйИдентификатор()));
		ХешированиеДанных.Добавить(Строка(ДанныеСтроки.КоэффициентТехнологическогоПроцесса)); 
	ИначеЕсли ЭтоОперацияПоСпецификации(ДанныеСтроки) Тогда
		ХешированиеДанных.Добавить(Строка(Объект.Спецификация.УникальныйИдентификатор()));
	Иначе
		ХешированиеДанных.Добавить(Строка(ДанныеСтроки.ИдентификаторОперации));
	КонецЕсли;
	
	Возврат ХешированиеДанных.ХешСумма;
	
КонецФункции

&НаСервере
Функция СтруктураХраненияРесурсов(ДанныеСтроки, КэшРесурсов)
	
	Если КэшРесурсов = Неопределено Тогда
		КэшРесурсов = Новый Соответствие();
	КонецЕсли;
	
	ТехнологическийПроцесс    = ДанныеСтроки.ТехнологическийПроцесс;
	ТехнологическийПроцессТип = ТипЗнч(ДанныеСтроки.ТехнологическийПроцесс);
	
	ДанныеСпецификации = КэшРесурсов.Получить("ДанныеСпецификации");
	Если ДанныеСпецификации = Неопределено
		И ЗначениеЗаполнено(Объект.Спецификация) Тогда
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
		ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Объект);
		ПереченьДанных = "ВыходныеИзделия,ВозвратныеОтходы,МатериалыИУслуги,Трудозатраты,Операции";
		ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных(ПереченьДанных);
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.ВыпускПроизвольнымиПорциями = Истина;
		ПараметрыВыборки.ПолучитьПромежуточныйВыпуск = Истина;
		ДанныеСпецификации = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоНоменклатуре(
								ДанныеПоНоменклатуре, ПараметрыВыборки, Новый Структура("Этап", Объект.ЭтапСпецификации));
		КэшРесурсов.Вставить("ДанныеСпецификации", ДанныеСпецификации);
	КонецЕсли;
	
	Если ЭтоСтандартныйТехпроцессПоСпецификации(Объект)
		ИЛИ ЭтоОперацияПоСпецификации(ДанныеСтроки)
		ИЛИ ЗначениеЗаполнено(ТехнологическийПроцесс)
			И ТехнологическийПроцессТип = Тип("СправочникСсылка.ТехнологическиеПроцессы")
			И ДанныеСпецификации <> Неопределено
			И ДанныеСпецификации.Операции.НайтиСтроки(ОтборОперации(ДанныеСтроки)).Количество() > 0 Тогда
		
		СтруктураДанных = ДанныеСпецификации;
		
	Иначе
		
		Ключ = КлючХраненияРесурсов(ДанныеСтроки);
		Если Ключ <> Неопределено Тогда
			СтруктураДанных = КэшРесурсов.Получить(Ключ);
			Если СтруктураДанных <> Неопределено Тогда
				Возврат СтруктураДанных;
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТехнологическийПроцесс)
			И ТехнологическийПроцессТип = Тип("СправочникСсылка.ТехнологическиеПроцессы") Тогда
			
			ДанныеПоНоменклатуре = Справочники.ТехнологическиеПроцессы.ДанныеПоНоменклатуре();
			ЗаполнитьЗначенияСвойств(ДанныеПоНоменклатуре, Объект);
			СтруктураДанных = Справочники.ТехнологическиеПроцессы.ДанныеТехнологическогоПроцессаПоНоменклатуре(
				ТехнологическийПроцесс,
				ДанныеСтроки.КоэффициентТехнологическогоПроцесса,
				ДанныеПоНоменклатуре,
				Справочники.ТехнологическиеПроцессы.ПараметрыВыборкиДанных("МатериалыИУслуги,Трудозатраты"));
			
		ИначеЕсли ЗначениеЗаполнено(ТехнологическийПроцесс)
			И ТехнологическийПроцессТип = Тип("СправочникСсылка.МаршрутныеКарты") Тогда
			
			СтруктураДанных = Справочники.МаршрутныеКарты.ДанныеМаршрутнойКарты(
				ТехнологическийПроцесс,
				ДанныеСтроки.КоэффициентТехнологическогоПроцесса,
				Объект.Номенклатура,
				Объект.Характеристика);
			СтруктураДанных.Вставить("МатериалыИУслуги", СтруктураДанных.Материалы);
			
		ИначеЕсли ЭтоОперацияИндивидуальная(ДанныеСтроки)
			И ЗначениеЗаполнено(ДанныеСтроки.ПроизводственнаяОперация) Тогда
			
			ДанныеТЧ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
				ДанныеСтроки.ПроизводственнаяОперация, Новый Структура(ПереченьРесурсов(ЭтотОбъект),,"МатериалыИРаботы"));
			СтруктураДанных = ОбщегоНазначения.СкопироватьРекурсивно(ПолучитьИзВременногоХранилища(Кэш.ОписаниеРесурсов));
			Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
				Для каждого Строка Из ДанныеТЧ[ИмяТЧ].Выгрузить() Цикл
					Таблица = СтруктураДанных[ИмяТЧ]; // ТаблицаЗначений
					ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка);
				КонецЦикла;
				Если ИмяТЧ = "МатериалыИУслуги" Тогда
					СтруктураДействий = Новый Структура;
					СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
					НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(СтруктураДанных[ИмяТЧ], СтруктураДействий);
				КонецЕсли;
			КонецЦикла;
			
		ИначеЕсли ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			
			СтруктураДанных = Новый Структура;
			Для каждого ДанныеПодчиненнойСтроки Из ДанныеСтроки.ПолучитьЭлементы() Цикл
				ДополнитьСтруктуруРесурсов(СтруктураДанных, СтруктураХраненияРесурсов(ДанныеПодчиненнойСтроки, КэшРесурсов));
			КонецЦикла;
			
		Иначе
			
			СтруктураДанных = Неопределено;
			
		КонецЕсли;
		
		Если Ключ <> Неопределено Тогда
			КэшРесурсов.Вставить(Ключ, СтруктураДанных);
			СохранитьКэшРесурсов(КэшРесурсов);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервере
Функция СтруктураХраненияРесурсовОперации(ДанныеСтроки, КэшРесурсов = Неопределено, ПересчитатьНаКоличество = Неопределено)
	
	Результат = Новый Структура;
	
	Если КэшРесурсов = Неопределено Тогда
		КэшРесурсов = ПолучитьКэшРесурсов();
	КонецЕсли;
	
	СтруктураДанных = СтруктураХраненияРесурсов(ДанныеСтроки, КэшРесурсов);
	
	Если СтруктураДанных = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Коэффициент = 1;
	
	Если КэшРесурсов.Получить("ДанныеСпецификации") = СтруктураДанных Тогда
		
		НайденныеСтроки = СтруктураДанных.Операции.НайтиСтроки(ОтборОперации(ДанныеСтроки));
		Если НайденныеСтроки.Количество() = 0 Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Для каждого КлючИЗначение Из ПереченьРесурсовДляЗаполненияПоНСИ(ЭтотОбъект) Цикл
			ИмяТЧ    = КлючИЗначение.Ключ;
			МассивТЧ = СтрРазделить(КлючИЗначение.Значение,",");
			Таблица  = Неопределено; // ТаблицаЗначений
			Если НЕ Результат.Свойство(ИмяТЧ, Таблица) Тогда
				Таблица = ПолучитьИзВременногоХранилища(Кэш.ОписаниеРесурсов)[ИмяТЧ].Скопировать();
			КонецЕсли;
			Для каждого ИмяТЧНСИ Из МассивТЧ Цикл
				Если СтруктураДанных.Свойство(ИмяТЧНСИ) Тогда
					Для каждого Строка Из СтруктураДанных[ИмяТЧНСИ] Цикл
						Если Справочники.РесурсныеСпецификации.СтрокаДанныхСпецификацииПринадлежитОперации(
							НайденныеСтроки[0], Строка, ИмяТЧНСИ = "ВыходныеИзделия" ИЛИ ИмяТЧНСИ ="ВозвратныеОтходы") Тогда
							ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			Результат.Вставить(ИмяТЧ, Таблица);
		КонецЦикла;
		
		Если НайденныеСтроки[0].КоличествоНаПартию > 0 Тогда
			Коэффициент = ?(ПересчитатьНаКоличество <> Неопределено, ПересчитатьНаКоличество, ДанныеСтроки.Требуется) / НайденныеСтроки[0].КоличествоНаПартию;
		КонецЕсли;
		
	Иначе
		
		ОтборОперации = Новый Структура("Операция", ДанныеСтроки.Операция);
		
		Для каждого КлючИЗначение Из ПереченьРесурсовДляЗаполненияПоНСИ(ЭтотОбъект) Цикл
			ИмяТЧ    = КлючИЗначение.Ключ;
			МассивТЧ = СтрРазделить(КлючИЗначение.Значение,",");
			Таблица  = Неопределено; // ТаблицаЗначений
			Если НЕ Результат.Свойство(ИмяТЧ, Таблица) Тогда
				Таблица = ПолучитьИзВременногоХранилища(Кэш.ОписаниеРесурсов)[ИмяТЧ].Скопировать();
			КонецЕсли;
			Для каждого ИмяТЧНСИ Из МассивТЧ Цикл
				ТаблицаНСИ = Неопределено; // ТаблицаЗначений
				Если СтруктураДанных.Свойство(ИмяТЧНСИ, ТаблицаНСИ) Тогда
					Для каждого Строка Из ?(
							ТаблицаНСИ.Колонки.Найти("Операция") <> Неопределено, ТаблицаНСИ.НайтиСтроки(ОтборОперации), ТаблицаНСИ) Цикл
						ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			Результат.Вставить(ИмяТЧ, Таблица);
		КонецЦикла;
		
		Если ЭтоАльтернативныйТехпроцесс(Объект) И ЭтоОперацияПоТехнологическомуПроцессу(ДанныеСтроки) Тогда
			РедакторПроизводственногоПроцесса.ДобавитьРесурсыСпецификации(
				Результат,
				КэшРесурсов.Получить("ДанныеСпецификации"),
				РедакторПроизводственногоПроцесса.ПереченьДанныхПриДобавленииРесурсовСпецификации(ДанныеСтроки));
		КонецЕсли;
		
		Если ПересчитатьНаКоличество <> Неопределено Тогда
			Коэффициент = ПересчитатьНаКоличество / ДанныеСтроки.Требуется;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ПересчитатьСтруктуруРесурсовПоКоэффициенту(Результат, Коэффициент);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтруктураРесурсовТекущая(Форма)
	
	СтруктураДанных = Новый Структура;
	
	Для каждого ИмяТЧ Из ПереченьРесурсов(Форма, Истина) Цикл
		СтруктураДанных.Вставить(ИмяТЧ, Форма.Объект[ИмяТЧ].Выгрузить());
	КонецЦикла;
	
	Возврат СтруктураДанных;
	
КонецФункции

&НаСервере
Процедура ДополнитьСтруктуруРесурсов(СтруктураПриемник, СтруктураИсточник)
	
	Если СтруктураИсточник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого КлючИЗначение Из СтруктураИсточник Цикл
		Таблица = Неопределено; // ТаблицаЗначений
		Если НЕ СтруктураПриемник.Свойство(КлючИЗначение.Ключ, Таблица) Тогда
			СтруктураПриемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение.Скопировать());
		Иначе
			Для каждого Строка Из КлючИЗначение.Значение Цикл
				ЗаполнитьЗначенияСвойств(Таблица.Добавить(), Строка);
			КонецЦикла;
			ПараметрыСвертки = ПереченьРесурсовПараметрыСвертки(КлючИЗначение.Ключ);
			Таблица.Свернуть(ПараметрыСвертки.КолонкиГруппировок, ПараметрыСвертки.КолонкиСуммирования);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПересчитатьСтруктуруРесурсовПоКоэффициенту(СтруктураДанных, Коэффициент)
	
	Если Коэффициент = 1 Тогда
		Возврат СтруктураДанных; // Структура
	КонецЕсли;
	
	Результат = Новый Структура;
	
	Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
		Таблица1 = Новый ТаблицаЗначений;
		Если СтруктураДанных.Свойство(ИмяТЧ, Таблица1) Тогда
			Таблица2 = Таблица1.Скопировать();
			Для каждого Строка Из Таблица2 Цикл
				Для каждого ИмяРеквизита Из СтрРазделить(ПереченьРесурсовПараметрыСвертки(ИмяТЧ).КолонкиСуммирования, ",") Цикл
					Строка[ИмяРеквизита] = Строка[ИмяРеквизита] * Коэффициент;
				КонецЦикла;
			КонецЦикла;
			Результат.Вставить(ИмяТЧ, Таблица2);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьРесурсыОперацииВОбъект(ИдентификаторСтроки)
	
	ОчиститьТаблицы(ПереченьРесурсов(ЭтотОбъект));
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = СтруктураХраненияРесурсовОперации(ДанныеСтроки);
	
	Если СтруктураДанных <> Неопределено Тогда
		Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
			Таблица = Неопределено; // ТаблицаЗначений
			Если СтруктураДанных.Свойство(ИмяТЧ, Таблица) Тогда
				Объект[ИмяТЧ].Загрузить(Таблица);
			КонецЕсли;
			Если ЕстьНоменклатураУпаковкаВТЧ(ИмяТЧ) Тогда
				ЗаполнитьСлужебныеРеквизитыНоменклатурыВСтроках(ИмяТЧ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьРесурсыИндивидуальнойОперации(ИдентификаторСтроки, СтруктураДанных = Неопределено)
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено
		ИЛИ НЕ ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Ресурсы = ?(СтруктураДанных = Неопределено, СтруктураРесурсовТекущая(ЭтотОбъект), СтруктураДанных);
	
	КэшРесурсов = ПолучитьКэшРесурсов();
	
	КэшРесурсов.Вставить(КлючХраненияРесурсов(ДанныеСтроки), Ресурсы);
	
	СохранитьКэшРесурсов(КэшРесурсов);
	
	ОтразитьИзменениеРесурсовОперации(ИдентификаторСтроки, Ресурсы);
	
КонецПроцедуры

&НаСервере
Процедура ОтразитьИзменениеРесурсовОперации(ИдентификаторСтроки, СтруктураДанных = Неопределено)
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
		
		Отбор = Новый Структура("ИдентификаторОперации", ДанныеСтроки.ИдентификаторОперации);
		Для каждого НайденнаяСтрока Из ТаблицаРесурсов.НайтиСтроки(Отбор) Цикл
			ТаблицаРесурсов.Удалить(НайденнаяСтрока);
		КонецЦикла;
		
		Если СтруктураДанных <> Неопределено Тогда
			Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
				Таблица = Неопределено; // ТаблицаЗначений
				Если СтруктураДанных.Свойство(ИмяТЧ, Таблица) Тогда
					Для Индекс = 0 По Таблица.Количество() - 1 Цикл
						НоваяСтрока = ТаблицаРесурсов.Добавить();
						НоваяСтрока.ИдентификаторОперации = ДанныеСтроки.ИдентификаторОперации;
						НоваяСтрока.ИмяТЧ = ИмяТЧ;
						НоваяСтрока.Порядок = Индекс + 1;
						ЗаполнитьЗначенияСвойств(НоваяСтрока, Таблица[Индекс]);
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбновитьИндикаторыНаличияРесурсовОбщие(ЭтотОбъект, ИдентификаторСтроки, СтруктураДанных);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИндикаторыРесурсовКонструктор(РасчетИтогов = Ложь)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ЕстьВыходныеИзделия",  ?(РасчетИтогов, Ложь, "ВыходныеИзделия"));
	Результат.Вставить("ЕстьМатериалыИУслуги", ?(РасчетИтогов, Ложь, "МатериалыИУслуги"));
	Результат.Вставить("ЕстьТрудозатраты",     ?(РасчетИтогов, Ложь, "Трудозатраты"));
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ИндикаторыРесурсовЗаполнитьКартинки(ДанныеСтроки)
	
	ДанныеСтроки.ЕстьВыходныеИзделияКартинка = ?(ДанныеСтроки.ЕстьВыходныеИзделия,
		БиблиотекаКартинок.РедакторПроизводственногоПроцессаФлажокСерый, Неопределено);
	ДанныеСтроки.ЕстьМатериалыИУслугиКартинка = ?(ДанныеСтроки.ЕстьМатериалыИУслуги,
		БиблиотекаКартинок.РедакторПроизводственногоПроцессаФлажокСерый, Неопределено);
	ДанныеСтроки.ЕстьМатериалыИУслугиКартинка = ?(ДанныеСтроки.ТребуетОбеспечения,
		БиблиотекаКартинок.ТребуетсяОбеспечение, ДанныеСтроки.ЕстьМатериалыИУслугиКартинка);
	ДанныеСтроки.ЕстьТрудозатратыКартинка= ?(ДанныеСтроки.ЕстьТрудозатраты,
		БиблиотекаКартинок.РедакторПроизводственногоПроцессаФлажокСерый, Неопределено);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИндикаторыНаличияРесурсовОбщие(Форма, ИдентификаторСтроки, СтруктураДанных)
	
	ДанныеСтроки = Форма.Операции.НайтиПоИдентификатору(ИдентификаторСтроки);
	Если ДанныеСтроки = Неопределено
		ИЛИ СтруктураДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого КлючИЗначение Из ИндикаторыРесурсовКонструктор() Цикл
		
		ИмяТЧ = КлючИЗначение.Значение;
		
		ДанныеСтроки[КлючИЗначение.Ключ] = Ложь;
		Если СтруктураДанных.Свойство(ИмяТЧ) Тогда
			ДанныеСтроки[КлючИЗначение.Ключ] = СтруктураДанных[ИмяТЧ].Количество();
		КонецЕсли;
		
	КонецЦикла;
	
	ИндикаторыРесурсовЗаполнитьКартинки(ДанныеСтроки);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИндикаторыОбеспеченияОбщие()
	
	ТаблицаОбеспечения.Загрузить(ПолучитьИзВременногоХранилища(Кэш.АдресТаблицыОбеспечениеЭтапа));
	
	ОтборОбеспечение = Новый Структура("Номенклатура,Характеристика");
	ОтборРесурсы     = Новый Структура("ИмяТЧ,ИдентификаторОперации", "МатериалыИУслуги");
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки, Истина) Цикл
		
		Если ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеСтроки.ТребуетОбеспечения = Ложь;
		
		ОтборРесурсы.ИдентификаторОперации = ДанныеСтроки.ИдентификаторОперации;
		Для каждого СтрокаМатериалОперации Из ТаблицаРесурсов.НайтиСтроки(ОтборРесурсы) Цикл
			ЗаполнитьЗначенияСвойств(ОтборОбеспечение, СтрокаМатериалОперации);
			НайденныеСтрокиОбеспечение = ТаблицаОбеспечения.НайтиСтроки(ОтборОбеспечение);
			Если НайденныеСтрокиОбеспечение.Количество() > 0 Тогда
				СтрокаОбеспечение = НайденныеСтрокиОбеспечение[0];
			Иначе
				СтрокаОбеспечение = ТаблицаОбеспечения.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаОбеспечение, ОтборОбеспечение);
			КонецЕсли;
			СтрокаОбеспечение.КоличествоВТехпроцессе = СтрокаОбеспечение.КоличествоВТехпроцессе + СтрокаМатериалОперации.Количество;
			Дельта = Мин(СтрокаОбеспечение.КоличествоВТехпроцессе - СтрокаОбеспечение.КоличествоВЭтапе, СтрокаМатериалОперации.Количество);
			Если Дельта > 0 Тогда
				Если СтрокаМатериалОперации.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа
					ИЛИ СтрокаМатериалОперации.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга Тогда
					СтрокаОбеспечение.НеОбеспечивать = СтрокаОбеспечение.НеОбеспечивать + Дельта;
				Иначе
					СтрокаОбеспечение.Обеспечить = СтрокаОбеспечение.Обеспечить + Дельта;
				КонецЕсли;
			КонецЕсли;
			Если СтрокаОбеспечение.КоличествоВТехпроцессе > Макс(СтрокаОбеспечение.КоличествоВЭтапе, СтрокаОбеспечение.КоличествоВТехпроцессе) - СтрокаОбеспечение.Обеспечить Тогда
				ДанныеСтроки.ТребуетОбеспечения = Истина;
				Если СтрокаОбеспечение.НомерОперацииПревышения = 0 Тогда
					СтрокаОбеспечение.НомерОперацииПревышения = ДанныеСтроки.НомерОперации;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ИндикаторыРесурсовЗаполнитьКартинки(ДанныеСтроки);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИндикаторыОбеспеченияТекущие()
	
	ТекущаяСтрока = Элементы.Операции.ТекущаяСтрока;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Операции.НайтиПоИдентификатору(ТекущаяСтрока);
	
	Отбор = Новый Структура("Номенклатура,Характеристика");
	Для каждого Строка Из Объект.МатериалыИУслуги Цикл
		ЗаполнитьЗначенияСвойств(Отбор, Строка);
		НайденныеСтроки = ТаблицаОбеспечения.НайтиСтроки(Отбор);
		Если ЗначениеЗаполнено(НайденныеСтроки)
			И НайденныеСтроки[0].НомерОперацииПревышения <> 0
			И ДанныеСтроки.НомерОперации >= НайденныеСтроки[0].НомерОперацииПревышения Тогда
			Строка.ТребуетОбеспечения = Истина;
		Иначе
			Строка.ТребуетОбеспечения = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтатьюКалькуляцииВСтроках(ИмяТЧ)
	
	Если НЕ РаботаСТабличнымиЧастямиКлиент.ВыбранаСтрокаДляВыполненияКоманды(Элементы[ИмяТЧ]) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("ТипЗатрат", ПроизводствоКлиент.ТипыЗатратДляВыбораСтатьиКалькуляции(ИмяТЧ));
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСтатьюКалькуляцииВСтрокахЗавершение", ЭтотОбъект, ИмяТЧ);
	
	ОткрытьФорму(
		"Справочник.СтатьиКалькуляции.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,,,,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСтатьюКалькуляцииВСтрокахЗавершение(РезультатЗакрытия, ИмяТЧ) Экспорт

	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы[ИмяТЧ].ВыделенныеСтроки;
	ТабличнаяЧасть = Объект[ИмяТЧ];
	
	ЕстьИзменения = Ложь;
	Для каждого ИдентификаторСтроки Из ВыделенныеСтроки Цикл
		
		ТекущиеДанные = ТабличнаяЧасть.НайтиПоИдентификатору(ИдентификаторСтроки);
		
		Если ТекущиеДанные.СтатьяКалькуляции <> РезультатЗакрытия Тогда
			ТекущиеДанные.СтатьяКалькуляции = РезультатЗакрытия;
			ЕстьИзменения = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЕстьИзменения Тогда
		ПриИзмененииОперации(ИмяТЧ);
	КонецЕсли;
	
КонецПроцедуры

#Область Перетаскивание

&НаКлиенте
Процедура НачалоПеретаскивания(ИмяТЧ, ПараметрыПеретаскивания, Выполнение)
	
	ТекущиеДанные = Элементы.Операции.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
		ИЛИ ЭтоГруппаИндивидуальныхОпераций(ТекущиеДанные) Тогда
		Выполнение = Ложь;
	КонецЕсли;
	
	Значение = Новый Структура;
	Значение.Вставить("ИдентификаторИсточника", ТекущиеДанные.ПолучитьИдентификатор());
	Значение.Вставить("ИмяТЧ", ИмяТЧ);
	Значение.Вставить("Строки", ПараметрыПеретаскивания.Значение);
	
	ПараметрыПеретаскивания.Значение = Значение;
	Если ТекущиеДанные.ТолькоПросмотр Тогда
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.Копирование;
	Иначе
		ПараметрыПеретаскивания.ДопустимыеДействия = ДопустимыеДействияПеретаскивания.КопированиеИПеремещение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура Перетаскивание(Действие, Значение)
	
	ИмяТЧ            = Значение.ИмяТЧ;
	ПараметрыСвертки = ПереченьРесурсовПараметрыСвертки(ИмяТЧ);
	
	СтрокаИсточник = Операции.НайтиПоИдентификатору(Значение.ИдентификаторИсточника);
	СтрокаПриемник = Операции.НайтиПоИдентификатору(Значение.ИдентификаторПриемника);
	
	Если СтрокаИсточник = Неопределено ИЛИ СтрокаПриемник = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РесурсыПриемника = СтруктураХраненияРесурсовОперации(СтрокаПриемник);
	Если РесурсыПриемника = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если НЕ СтрокаПриемник.ТолькоПросмотр Тогда
		СтрокаПриемник.Модифицированность = Истина;
		Таблица = РесурсыПриемника[ИмяТЧ]; // ТаблицаЗначений
		Для каждого ИдентификаторСтроки Из Значение.Строки Цикл
			ДанныеСтроки = Объект[ИмяТЧ].НайтиПоИдентификатору(ИдентификаторСтроки);
			ЗаполнитьЗначенияСвойств(Таблица.Добавить(), ДанныеСтроки);
		КонецЦикла;
		Таблица.Свернуть(ПараметрыСвертки.КолонкиГруппировок, ПараметрыСвертки.КолонкиСуммирования);
		ПреобразоватьОперациюВИндивидуальную(Значение.ИдентификаторПриемника, Ложь);
		СохранитьРесурсыИндивидуальнойОперации(Значение.ИдентификаторПриемника, РесурсыПриемника);
	КонецЕсли;
	
	Если Действие = "Перемещение" И НЕ СтрокаИсточник.ТолькоПросмотр Тогда
		СтрокаИсточник.Модифицированность = Истина;
		Для каждого ИдентификаторСтроки Из Значение.Строки Цикл
			ДанныеСтроки = Объект[ИмяТЧ].НайтиПоИдентификатору(ИдентификаторСтроки);
			Объект[ИмяТЧ].Удалить(ДанныеСтроки);
		КонецЦикла;
		РесурсыИсточника = СтруктураРесурсовТекущая(ЭтотОбъект);
		ПреобразоватьОперациюВИндивидуальную(Значение.ИдентификаторИсточника, Ложь);
		СохранитьРесурсыИндивидуальнойОперации(Значение.ИдентификаторИсточника, РесурсыИсточника);
	КонецЕсли;
	
	ОбновитьИндикаторыОбеспеченияОбщие();
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(СтрокаИсточник);
	МассивСтрок.Добавить(СтрокаПриемник);
	
	Для каждого ДанныеСтроки Из МассивСтрок Цикл
		Если ДанныеСтроки.Модифицированность Тогда
			ЗаполнитьСлужебныеРеквизитыСтрокиОперации(ЭтотОбъект, ДанныеСтроки);
			ЗаполнитьРеквизитыГруппыОпераций(ЭтотОбъект, ДанныеСтроки.ПолучитьРодителя());
			Модифицированность = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Обеспечение

&НаСервере
Функция ТаблицаОбеспечениеЭтапа()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Обеспечение.Номенклатура           КАК Номенклатура,
	|	Обеспечение.Характеристика         КАК Характеристика,
	|	СУММА(Обеспечение.Количество)      КАК Количество
	|ПОМЕСТИТЬ ВТОбеспечение
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.ОбеспечениеМатериаламиИРаботами КАК Обеспечение
	|ГДЕ
	|	Обеспечение.Ссылка = &Этап
	|	И НЕ Обеспечение.Отменено
	|СГРУППИРОВАТЬ ПО
	|	Обеспечение.Номенклатура,
	|	Обеспечение.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расход.Номенклатура                КАК Номенклатура,
	|	Расход.Характеристика              КАК Характеристика,
	|	СУММА(Расход.Количество)           КАК Количество
	|ПОМЕСТИТЬ ВТРасход
	|ИЗ
	|	Документ.ЭтапПроизводства2_2.РасходМатериаловИРабот КАК Расход
	|ГДЕ
	|	Расход.Ссылка = &Этап
	|СГРУППИРОВАТЬ ПО
	|	Расход.Номенклатура,
	|	Расход.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ISNULL(ВТОбеспечение.Номенклатура, ВТРасход.Номенклатура)          КАК Номенклатура,
	|	ISNULL(ВТОбеспечение.Характеристика, ВТРасход.Характеристика)      КАК Характеристика,
	|	ISNULL(ВТОбеспечение.Количество, 0)                                КАК КоличествоВЭтапе,
	|	0                                                                  КАК КоличествоВТехпроцессе,
	|	ISNULL(ВТРасход.Количество, 0)                                     КАК Израсходовано,
	|	0                                                                  КАК Обеспечить,
	|	0                                                                  КАК НеОбеспечивать
	|ИЗ
	|	ВТОбеспечение КАК ВТОбеспечение
	|		ПОЛНОЕ СОЕДИНЕНИЕ ВТРасход КАК ВТРасход
	|		ПО ВТОбеспечение.Номенклатура = ВТРасход.Номенклатура
	|			И ВТОбеспечение.Характеристика = ВТРасход.Характеристика
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Объект.Этап);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	ПоляОтбора = "Номенклатура,Характеристика";
	Отбор = Новый Структура(ПоляОтбора);
	
	РаспределенияЗапасовЭтапа = РаспределенияЗапасовЭтапа();
	РаспределенияЗапасовЭтапа.Индексы.Добавить(ПоляОтбора);
	
	Для каждого Строка Из Результат Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, Строка);
		Для каждого НайденнаяСтрока Из РаспределенияЗапасовЭтапа.НайтиСтроки(Отбор) Цикл
			Если НайденнаяСтрока.Состояние = Перечисления.РаспределениеЗапасовСостояния.Обеспечить Тогда
				Строка.Обеспечить = Строка.Обеспечить + НайденнаяСтрока.Количество;
			ИначеЕсли НайденнаяСтрока.Состояние = Перечисления.РаспределениеЗапасовСостояния.НеОбеспечивать Тогда
				Строка.НеОбеспечивать = Строка.НеОбеспечивать + НайденнаяСтрока.Количество;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РаспределенияЗапасовЭтапа()
	
	ЭтапОбъект = Объект.Этап.ПолучитьОбъект();
	
	ПараметрыВстраивания = Документы.ЭтапПроизводства2_2.ДоступныеОстаткиПараметрыВстраивания(ЭтапОбъект);
	
	СлужебныеПараметры = Новый Структура("ЭтоДокумент", Истина);
	ПараметрыВстраивания.Вставить("СлужебныеПараметры", СлужебныеПараметры);
	
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("Объект",                  ЭтапОбъект);
	ДанныеФормы.Вставить("УникальныйИдентификатор", ЭтапОбъект.УникальныйИдентификатор);
	ДанныеФормы.Вставить("Модифицированность",      Истина);
	
	ДанныеФормы.Вставить(
		"ДанныеПартииПроизводства",
		Документы.ЭтапПроизводства2_2.ДанныеПартииПроизводства(
			ЭтапОбъект.Распоряжение,
			ЭтапОбъект.НазначениеПродукция,
			ЭтапОбъект.ПартияПроизводства));
	
	ДанныеФормы.Вставить(
		"ДоступныеОстаткиПараметрыВстраивания",
		ПараметрыВстраивания);
		
	ОтборСостояний = Новый Массив;
	ОтборСостояний.Добавить(Перечисления.РаспределениеЗапасовСостояния.НеОбеспечивать);
	ОтборСостояний.Добавить(Перечисления.РаспределениеЗапасовСостояния.Обеспечить);
		
	ТаблицаРаспределенияЗапасов = ОбеспечениеВДокументахСервер.РаспределениеЗапасовДляРедактируемогоДокумента(
		Ложь, ДанныеФормы, ОтборСостояний);
		
	Возврат ТаблицаРаспределенияЗапасов;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РаботаСБуферомОбмена

&НаКлиенте
Процедура СкопироватьСтрокиТЧ(ИмяТЧ)
	
	Если РаботаСТабличнымиЧастямиКлиент.ВыбранаСтрокаДляВыполненияКоманды(Элементы[ИмяТЧ]) Тогда
		СкопироватьСтрокиНаСервере(ИмяТЧ);
		РаботаСТабличнымиЧастямиКлиент.ОповеститьПользователяОКопированииСтрок(Элементы[ИмяТЧ].ВыделенныеСтроки.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СкопироватьСтрокиНаСервере(ИмяТЧ)
	
	РаботаСТабличнымиЧастями.СкопироватьСтрокиВБуферОбмена(Объект[ИмяТЧ], Элементы[ИмяТЧ].ВыделенныеСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСтрокиИзБуфераОбмена(ИмяТЧ)
	
	КоличествоСтрокДоВставки = Объект[ИмяТЧ].Количество();
	
	ПолучитьСтрокиИзБуфераОбменаНаСервере(ИмяТЧ);
	
	КоличествоВставленных = Объект[ИмяТЧ].Количество() - КоличествоСтрокДоВставки;
	РаботаСТабличнымиЧастямиКлиент.ОповеститьПользователяОВставкеСтрок(КоличествоВставленных);
	
	Если КоличествоВставленных > 0 Тогда
		ПриИзмененииОперации(ИмяТЧ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСтрокиИзБуфераОбменаНаСервере(ИмяТЧ)
	
	СоставСтрок     = Неопределено;
	ПараметрыОтбора = Неопределено;
	
	Если ИмяТЧ = "ВыходныеИзделия" Тогда
		СоставСтрок = "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок";
	ИначеЕсли ИмяТЧ = "МатериалыИУслуги" Тогда
		СоставСтрок = "Номенклатура,Характеристика,Упаковка,КоличествоУпаковок,СтатьяКалькуляции";
	ИначеЕсли ИмяТЧ = "Трудозатраты" Тогда
		СоставСтрок = "ВидРабот,Количество,СтатьяКалькуляции";
	КонецЕсли;
	
	ПодборТоваров = ИмяТЧ <> "Трудозатраты";
	
	СтруктураДействий = Новый Структура;
	
	Если ПодборТоваров Тогда
		
		МассивТиповНоменклатуры = УправлениеДаннымиОбИзделияхКлиентСервер.МассивДоступныхТиповНоменклатуры(ЭтотОбъект);
		ПараметрыОтбора = Новый Соответствие;
		ПараметрыОтбора.Вставить("Номенклатура.ТипНоменклатуры", МассивТиповНоменклатуры);
		
		СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
		СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
		СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());
		
	КонецЕсли;
	
	СтрокиИзБуфера = РаботаСТабличнымиЧастями.СтрокиИзБуфераОбмена(ПараметрыОтбора, СоставСтрок);
	
	Если Не ЗначениеЗаполнено(СтрокиИзБуфера) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаОбъекта = Объект[ИмяТЧ]; // ДанныеФормыКоллекция
	ОбрабатываемыеСтроки = Новый Массив();
	
	Для каждого СтрокаИзБуфера Из СтрокиИзБуфера Цикл
		
		ТекущаяСтрока = ТаблицаОбъекта.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтрокаИзБуфера);
		
		Если ПодборТоваров Тогда
			ОбрабатываемыеСтроки.Добавить(ТекущаяСтрока);
		КонецЕсли;
		
	КонецЦикла;
	
	ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
		ОбрабатываемыеСтроки,
		СтруктураДействий,
		ТаблицаОбъекта);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕстьСтрокиВБуфереОбмена()
	
	Возврат РаботаСТабличнымиЧастями.ЕстьСтрокиВБуфереОбмена();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьКомандБуфераОбмена(Форма, РеквизитыДоступны)
	
	Форма.Элементы.ВыходныеИзделия_ВставитьСтроки.Доступность = РеквизитыДоступны;
	Форма.Элементы.КонтекстноеМенюВыходныеИзделия_ВставитьСтроки.Доступность = РеквизитыДоступны;
	
	Форма.Элементы.МатериалыИУслуги_ВставитьСтроки.Доступность = РеквизитыДоступны;
	Форма.Элементы.КонтекстноеМенюМатериалыИУслуги_ВставитьСтроки.Доступность = РеквизитыДоступны;
	
	Форма.Элементы.Трудозатраты_ВставитьСтроки.Доступность = РеквизитыДоступны;
	Форма.Элементы.КонтекстноеМенюТрудозатраты_ВставитьСтроки.Доступность = РеквизитыДоступны;
	
КонецПроцедуры

#КонецОбласти


#Область ЧтениеЗаписьДанных

&НаКлиенте
Функция ПодготовитьДанныеДляЗавершенияРедактирования()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Подразделение",      Объект.Подразделение);
	Результат.Вставить("Этап",               Объект.Этап);
	Результат.Вставить("ПредставлениеЭтапа", ПредставлениеЭтапа);
	Результат.Вставить("Режим",              Режим);
	
	АдресДанныхЭтапаДляОбновления = ПоместитьВоВременноеХранилище(Неопределено,
		?(Режим = РедакторПроизводственногоПроцессаКлиентСервер.РежимИзФормыЭтапа(),
				ВладелецФормы.УникальныйИдентификатор,
				УникальныйИдентификатор));
	Результат.Вставить("АдресДанныхЭтапаДляОбновления", АдресДанныхЭтапаДляОбновления);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ПодготовитьДанныеДляЗавершенияРедактированияНаСервере());
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПодготовитьДанныеДляЗавершенияРедактированияНаСервере()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПроверкаТехпроцессаПройдена", ПроверитьЗаполнениеТехпроцесса());
	
	Если Результат.ПроверкаТехпроцессаПройдена Тогда
		
		ПараметрыЗаписиТехпроцесса = ПараметрыПроцедурыЗаписиТехпроцесса();
		Результат.Вставить("АдресПараметровЗаписиТехпроцесса",
			ПоместитьВоВременноеХранилище(ПараметрыЗаписиТехпроцесса, УникальныйИдентификатор));
			
		ПараметрыОбновленияЭтапа   = ПараметрыОбновленияДанныхЭтапа();
		Результат.Вставить("АдресПараметровОбновленияЭтапа",
			ПоместитьВоВременноеХранилище(ПараметрыОбновленияЭтапа, УникальныйИдентификатор));
		Результат.Вставить("ТребуетсяОбновитьЭтап", ПараметрыОбновленияЭтапа.ТребуетсяОбновитьЭтап);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПодготовитьДанныеДляСозданияТиповогоТехпроцесса()
	
	Результат = Новый Структура;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ПодготовитьДанныеДляСозданияТиповогоТехпроцессаНаСервере());
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПодготовитьДанныеДляСозданияТиповогоТехпроцессаНаСервере()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПроверкаТехпроцессаПройдена", ПроверитьЗаполнениеТехпроцесса("СозданиеТехпроцесса"));
	
	Если Результат.ПроверкаТехпроцессаПройдена Тогда
		
		ПараметрыЗаписиТехпроцесса = ПараметрыПроцедурыСозданияТиповогоТехпроцесса();
		Результат.Вставить("АдресПараметровСозданияТиповогоТехпроцесса",
			ПоместитьВоВременноеХранилище(ПараметрыЗаписиТехпроцесса, УникальныйИдентификатор));
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПараметрыПроцедурыЗаписиТехпроцесса()
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
	ЗаполнитьЗначенияСвойств(ПараметрыТехпроцесса, Объект);
	
	Результат = Новый Структура;
	Результат.Вставить("Этап", Объект.Этап);
	Результат.Вставить("Подразделение", Объект.Подразделение);
	Результат.Вставить("Распоряжение", Объект.Распоряжение);
	Результат.Вставить("ПараметрыТехпроцесса", ПараметрыТехпроцесса);
	Результат.Вставить("ПроизводственныеОперации", Кэш.ПроизводственныеОперации);
	Результат.Вставить("СохранитьОграниченияТехпроцесса", Объект.СохранитьОграниченияТехпроцесса);
	
	Если ЭтоИндивидуальныйТехпроцесс(ПараметрыТехпроцесса) Тогда
		
		КэшРесурсов = ПолучитьКэшРесурсов();
		
		ТаблицаОпераций = ТаблицаОперацийКонструктор();
		РесурсыОпераций = Новый Соответствие;
		
		ДанныеСтроки = Неопределено;
		Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
			Если НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
				Если ЭтоОперацияИндивидуальная(ДанныеСтроки) Тогда
					РесурсыОперации = СтруктураХраненияРесурсовОперации(ДанныеСтроки, КэшРесурсов);
					Если РесурсыОперации <> Неопределено Тогда
						РесурсыОпераций.Вставить(ДанныеСтроки.ИдентификаторОперации, РесурсыОперации);
					КонецЕсли;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(ТаблицаОпераций.Добавить(), ДанныеСтроки);
			КонецЕсли;
		КонецЦикла;
		
		Результат.Вставить("ТаблицаОпераций", ТаблицаОпераций);
		Результат.Вставить("РесурсыОпераций", РесурсыОпераций);
	
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПараметрыПроцедурыСозданияТиповогоТехпроцесса()
	
	ПараметрыТехпроцесса = РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор();
	ЗаполнитьЗначенияСвойств(ПараметрыТехпроцесса, Объект);
	
	Результат = Новый Структура;
	Результат.Вставить("ПараметрыТехпроцесса", ПараметрыТехпроцесса);
	Результат.Вставить("ПредставлениеЭтапа", ПредставлениеЭтапа);
	Результат.Вставить("Подразделение", Объект.Подразделение);
	Результат.Вставить("ВидНоменклатуры", Объект.ВидНоменклатуры);
	
	КэшРесурсов = ПолучитьКэшРесурсов();
	
	ТаблицаОпераций = ТаблицаОперацийКонструктор();
	РесурсыОпераций = Новый Соответствие;
	
	ДанныеСтроки = Неопределено;
	Пока СледующаяСтрока(ЭтотОбъект, ДанныеСтроки) Цикл
		Если НЕ ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки) Тогда
			НоваяСтрока = ТаблицаОпераций.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеСтроки);
			НоваяСтрока.Количество = КоличествоОперацийНормативное(ДанныеСтроки);
			НоваяСтрока.Загрузка   = ДанныеСтроки.ЗагрузкаНорматив;
			РесурсыОперации = СтруктураХраненияРесурсовОперации(ДанныеСтроки, КэшРесурсов, НоваяСтрока.Количество);
			Если РесурсыОперации <> Неопределено Тогда
				РесурсыОпераций.Вставить(ДанныеСтроки.ИдентификаторОперации, РесурсыОперации);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Результат.Вставить("ТаблицаОпераций", ТаблицаОпераций);
	Результат.Вставить("РесурсыОпераций", РесурсыОпераций);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПараметрыОбновленияДанныхЭтапа()
	
	ДеревоОпераций = РеквизитФормыВЗначение("Операции");
	
	РесурсыТехпроцесса = РедакторПроизводственногоПроцесса.ТаблицаРесурсовТехпроцесса();
	Для каждого Строка Из ТаблицаРесурсов Цикл
		НоваяСтрока = РесурсыТехпроцесса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		ДанныеОперации = ДеревоОпераций.Строки.Найти(Строка.ИдентификаторОперации, "ИдентификаторОперации", Истина);
		Если ДанныеОперации <> Неопределено Тогда
			НоваяСтрока.НаименованиеОперации = ДанныеОперации.Наименование;
			НоваяСтрока.НомерОперации        = ДанныеОперации.НомерОперации;
		КонецЕсли;
	КонецЦикла;
	ПроизводствоСервер.СвернутьТаблицуЗначений(РесурсыТехпроцесса,,"Количество"); 
	
	ПараметрыЗапроса = РедакторПроизводственногоПроцесса.ПараметрыЗапросаСопоставлениеРесурсовТехпроцессЭтап();
	
	ПараметрыЗапроса.РесурсыТехпроцесса = РесурсыТехпроцесса;
	ПараметрыЗапроса.Этап               = Объект.Этап;
	ПараметрыЗапроса.Подразделение      = Объект.Подразделение;
	ПараметрыЗапроса.Распоряжение       = Объект.Распоряжение;
	
	ТаблицаСопоставления = РедакторПроизводственногоПроцесса.ТаблицаСопоставленияРесурсовТехпроцессЭтап(ПараметрыЗапроса);
	
	Результат = Новый Структура("ТребуетсяОбновитьЭтап", ТаблицаСопоставления.Найти(Истина, "Изменено") <> Неопределено И ХранитьОперацииВРесурсныхСпецификациях);
	
	Если Результат.ТребуетсяОбновитьЭтап Тогда
		
		Результат.Вставить("РесурсыТехпроцесса", РесурсыТехпроцесса);
		Результат.Вставить("ТаблицаОбеспечения", ТаблицаОбеспечения.Выгрузить());
		Результат.Вставить("ТаблицаСопоставления", ТаблицаСопоставления);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьРедактирование()
	
	ОчиститьСообщения();
	
	Если Модифицированность Тогда
		
		ДанныеДляЗавершенияРедактирования = ПодготовитьДанныеДляЗавершенияРедактирования();
		
		Если НЕ ДанныеДляЗавершенияРедактирования.ПроверкаТехпроцессаПройдена Тогда
			
			Возврат;
			
		ИначеЕсли ДанныеДляЗавершенияРедактирования.ТребуетсяОбновитьЭтап Тогда
			
			ОткрытьФорму("Обработка.РедакторПроизводственногоПроцесса.Форма.ОбновлениеДанныхЭтапа",
				ДанныеДляЗавершенияРедактирования,
				ЭтотОбъект,,,,
				Новый ОписаниеОповещения("ОбновлениеДанныхЭтапаЗавершение", ЭтотОбъект, ДанныеДляЗавершенияРедактирования));
			
		Иначе
			
			НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования);
			
		КонецЕсли;
	
	Иначе
		
		Закрыть(Неопределено);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования, ЗакрытьПриЗавершении = Истина)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Этап", Объект.Этап);
	ДополнительныеПараметры.Вставить("Подразделение", Объект.Подразделение);
	ДополнительныеПараметры.Вставить("ЗакрытьПриЗавершении", ЗакрытьПриЗавершении);
	
	Оповещение = Новый ОписаниеОповещения("ЗаписьДанныхТехпроцессаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	РедакторПроизводственногоПроцессаКлиент.НачатьЗаписьДанныхТехпроцесса(
		ЭтотОбъект, ДанныеДляЗавершенияРедактирования, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписьДанныхТехпроцессаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	Результат = РедакторПроизводственногоПроцессаКлиент.ЗаписьДанныхТехпроцессаЗавершение(
		ЭтотОбъект, ДлительнаяОперация, ДополнительныеПараметры);
	
	Если Результат.ЗакрытьПриЗавершении Тогда
		
		Модифицированность = Ложь;
		
		Закрыть(ПараметрыЗакрытия());
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСоздатьТиповойТехпроцессЗавершение(РезультатВопроса, ДанныеДляСозданияТехпроцесса) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		НачатьСозданиеТиповогоТехпроцесса(ДанныеДляСозданияТехпроцесса);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьСозданиеТиповогоТехпроцесса(ДанныеДляСозданияТехпроцесса)
	
	Оповещение = Новый ОписаниеОповещения("СозданиеТиповогоТехпроцессаЗавершение", ЭтотОбъект);
	
	РедакторПроизводственногоПроцессаКлиент.НачатьСозданиеТиповогоТехпроцесса(
		ЭтотОбъект, ДанныеДляСозданияТехпроцесса, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеТиповогоТехпроцессаЗавершение(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	
	РедакторПроизводственногоПроцессаКлиент.СозданиеТиповогоТехпроцессаЗавершение(ЭтотОбъект, ДлительнаяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПеречитатьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Инициализировать();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеДанныхЭтапаЗавершение(Результат, ДанныеДляЗавершенияРедактирования) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЗаписатьТехпроцесс Тогда
		
		НачатьЗаписьДанныхТехпроцесса(ДанныеДляЗавершенияРедактирования);
		
	ИначеЕсли Результат.Свойство("ЗакрытьПриЗавершении") Тогда
		
		Модифицированность = Ложь;
		
		Закрыть(ПараметрыЗакрытия());
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.Операции.Имя);
	
	ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.ТолькоПросмотр");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ТипТехнологическогоПроцесса");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ИндивидуальныйТехпроцесс();
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ЦветФонаНедоступнойОперации);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяШтучное.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяШтучноеЕдИзм.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяПЗ.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяПЗЕдИзм.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ВремяРаботыПриСинхроннойЗагрузке.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаИзмеренияПриСинхроннойЗагрузке.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.СинхроннаяЗагрузка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ТехнологическийПроцесс.Имя);
	
	ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.ТехнологическийПроцесс");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.Операция");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстТехпроцессИндивидуальный());
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ТехнологическийПроцесс.Имя);
	
	ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.ТехнологическийПроцесс");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.Операция");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", ТекстТехпроцессСпецификация());
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ЕдиницаИзмерения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.ЕдиницаИзмерения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(Неопределено,,Истина));
	
	//
	СлужебныеРеквизиты = СлужебныеРеквизитыОпераций(ЭтотОбъект);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.НомерОперации.Имя);
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.НомерСледующейОперации.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СлужебныеРеквизиты["ЭтоОперацияИндивидуальнаяВГруппе"].ПутьСлужебного);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.Требуется.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СлужебныеРеквизиты["ЭтоГруппаИндивидуальныхОпераций"].ПутьСлужебного);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	Для каждого КлючИЗначение Из СлужебныеРеквизиты Цикл
		Описание = КлючИЗначение.Значение;
			
		Если Описание.БлокироватьПриРазличиях Тогда
		
			//
			Элемент = УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы[Описание.ИмяРеквизитаОсновного].Имя);
			
			ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
			ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СлужебныеРеквизиты["ЭтоГруппаИндивидуальныхОпераций"].ПутьСлужебного);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Истина;
			
			ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Описание.ПутьСлужебного);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Ложь;
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
			Элемент.Оформление.УстановитьЗначениеПараметра("Текст", Описание.ТекстГруппыПриРазличиях);
		
		КонецЕсли;
		
		Если Описание.ГрупповоеРедактирование Тогда
		
			//
			Элемент = УсловноеОформление.Элементы.Добавить();
			Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы[Описание.ИмяРеквизитаОсновного].Имя);
			
			ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
			ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СлужебныеРеквизиты["ЭтоОперацияИндивидуальнаяВГруппе"].ПутьСлужебного);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Истина;
			
			ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Описание.ПутьСлужебного);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			ОтборЭлемента.ПравоеЗначение = Ложь;
			
			ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Описание.ПутьОсновного);
			ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
			
			Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
		
		КонецЕсли;
		
	КонецЦикла;
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.Требуется.Имя);
	
	ГруппаИ = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаИ.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Операции.Требуется");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = ГруппаИ.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(СлужебныеРеквизиты["ЭтоГруппаИндивидуальныхОпераций"].ПутьСлужебного);
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	//
	Перечисления.СостоянияВыполненияОпераций.УстановитьУсловноеОформлениеСостояния(
		ЭтотОбъект, "Состояние", "Операции.Состояние", "Операции.ЕстьСозданные");
	
	//
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ВыходныеИзделияНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Объект.ВыходныеИзделия.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
	
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "МатериалыИУслугиНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Объект.МатериалыИУслуги.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтотОбъект, ПараметрыУсловногоОформления);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.МатериалыИУслугиТребуетОбеспечения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.МатериалыИУслуги.ТребуетОбеспечения");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.СветлоСерый);
	
	//
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "ВыходныеИзделияХарактеристика", "Объект.ВыходныеИзделия.ХарактеристикиИспользуются");
	НоменклатураСервер.УстановитьУсловноеОформлениеХарактеристикНоменклатуры(
		ЭтотОбъект, "МатериалыИУслугиХарактеристика", "Объект.МатериалыИУслуги.ХарактеристикиИспользуются");

КонецПроцедуры

&НаСервере
Процедура Инициализировать()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Статус                                                            КАК Статус,
	|	РеквизитыДокумента.Распоряжение                                                      КАК Распоряжение,
	|	РеквизитыДокумента.Подразделение                                                     КАК Подразделение,
	|	РеквизитыДокумента.Спецификация                                                      КАК Спецификация,
	|	РеквизитыДокумента.Этап                                                              КАК ЭтапСпецификации,
	|	РеквизитыДокумента.НаправлениеДеятельности                                           КАК НаправлениеДеятельности,
	|
	|	РеквизитыДокумента.ПартияПроизводства.ОсновноеИзделиеНоменклатура                    КАК Номенклатура,
	|	РеквизитыДокумента.ПартияПроизводства.ОсновноеИзделиеХарактеристика                  КАК Характеристика,
	|	РеквизитыДокумента.КоличествоУпаковокПлан                                            КАК КоличествоУпаковокПлан,
	|	РеквизитыДокумента.УпаковкаПлан                                                      КАК УпаковкаПлан,
	|	РеквизитыДокумента.КоличествоУпаковокПлан * ЕСТЬNULL(&КоэффициентУпаковки, 1)        КАК Количество,
	|	РеквизитыДокумента.КоличествоНаЕдиницуПартииВыпуска                                  КАК КоличествоНаЕдиницуПартииВыпуска,
	|
	|	РеквизитыДокумента.ПартияПроизводства.ОсновноеИзделиеНоменклатура.ВидНоменклатуры    КАК ВидНоменклатуры,
	|	РеквизитыДокумента.Распоряжение.НачатьНеРанее                                        КАК НачалоПроизводства,
	|	РеквизитыДокумента.Распоряжение.Подразделение                                        КАК ПодразделениеДиспетчер,
	|
	|	&ПараметрыТехпроцесса
	|
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК РеквизитыДокумента
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
	|	ПО РеквизитыДокумента.Ссылка = Техпроцесс.Этап
	|
	|ГДЕ
	|	РеквизитыДокумента.Ссылка = &Этап
	|";
	
	ТекстЗапросаКоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"РеквизитыДокумента.УпаковкаПлан",
		"РеквизитыДокумента.ПартияПроизводства.ОсновноеИзделиеНоменклатура");
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&КоэффициентУпаковки",
		ТекстЗапросаКоэффициентУпаковки);
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ПараметрыТехпроцесса",
		РедакторПроизводственногоПроцесса.ТекстЗапросаПараметрыТехпроцессаЭтапа());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этап", Объект.Этап);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ЗаполнитьЗначенияСвойств(Объект, Выборка);
	
	ЗаполнитьСлужебныеРеквизиты();
	
	ИнициализироватьКэш();
	
	ЗаполнитьДанныеТехпроцесса(Истина);
	
	НастроитьЭлементыФормы();
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизиты()
	
	ХранитьОперацииВРесурсныхСпецификациях = ПолучитьФункциональнуюОпцию("ХранитьОперацииВРесурсныхСпецификациях");
	ИспользоватьКонтрольВыполненияОпераций = ПолучитьФункциональнуюОпцию("ИспользоватьКонтрольВыполненияОпераций");
	
	ДлительныеОперацииСтруктура = Новый Структура("ЗаписьДанных,СозданиеТиповогоТехпроцесса");
	
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(Объект.Подразделение);
	
	Объект.СохранитьОграниченияТехпроцесса = НЕ ЭтоИндивидуальныйТехпроцесс(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеРеквизитыНоменклатурыВСтроках(ИмяТЧ, СтрокиЗаполнения = Неопределено)
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ЗаполнитьПризнакХарактеристикиИспользуются", Новый Структура("Номенклатура", "ХарактеристикиИспользуются"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакАртикул", Новый Структура("Номенклатура", "Артикул"));
	СтруктураДействий.Вставить("ЗаполнитьПризнакТипНоменклатуры", Новый Структура("Номенклатура", "ТипНоменклатуры"));
	
	НоменклатураСервер.ЗаполнитьСлужебныеРеквизитыПоНоменклатуреВКоллекции(
		Объект[ИмяТЧ], СтруктураДействий, СтрокиЗаполнения);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	Элементы.КТО.Видимость      = ХранитьОперацииВРесурсныхСпецификациях;
	Элементы.Участок.Видимость  = ПараметрыПодразделения.ИспользоватьУчастки;
	Элементы.Контроль.Видимость = ИспользоватьКонтрольВыполненияОпераций И НЕ ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
	Элементы.ГруппаПередаточнаяПартия.Видимость = ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
	Элементы.Непрерывная.Видимость = ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
	
	ТолькоПросмотр = ТолькоПросмотр
		ИЛИ (Объект.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен)
		ИЛИ НЕ ПравоДоступа("Изменение", Метаданные.Документы.ЭтапПроизводства2_2);
	
	Элементы.ФормаЗаписатьИЗакрыть.Доступность = НЕ ТолькоПросмотр;
	
	Элементы.НомерОперации.МинимальноеЗначение = МинимальныйРедактируемыйНомер(ЭтотОбъект);
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);
	
	ДоступностьЭлементов = НЕ Форма.ТолькоПросмотр;
	Стандартный    = ЭтоСтандартныйТехпроцесс(Объект);
	Альтернативный = ЭтоАльтернативныйТехпроцесс(Объект);
	Индивидуальный = ЭтоИндивидуальныйТехпроцесс(Объект);
	
	ПараметрыПодразделения = Форма.ПараметрыПодразделения;
	
	Если СтруктураРеквизитов.Свойство("ТехнологическийПроцесс")
		ИЛИ Инициализация Тогда
			
			ПоМаршрутнойКарте    = ТипЗнч(Объект.ТехнологическийПроцесс) = Тип("СправочникСсылка.МаршрутныеКарты");
			ЕстьЗаблокированные  = ЕстьЗаблокированныеОперации(Форма);
			
			Элементы.ФормаЗаменитьНаАльтернативный.Доступность = ДоступностьЭлементов И НЕ ЕстьЗаблокированные;
			Элементы.ФормаЗаменитьНаСтандартный.Доступность    = ДоступностьЭлементов И НЕ Стандартный И НЕ ЕстьЗаблокированные;
			Элементы.ФормаЗаменитьНаИндивидуальный.Доступность = ДоступностьЭлементов И НЕ Индивидуальный И НЕ ПараметрыПодразделения.ИспользоватьПооперационноеПланирование;
			Элементы.ФормаИзменитьКоэффициент.Доступность      = ДоступностьЭлементов И НЕ ЕстьЗаблокированные И (Альтернативный ИЛИ ПоМаршрутнойКарте);
			Элементы.ФормаСоздатьТиповойТехпроцесс.Доступность = Индивидуальный;
			Элементы.ПанельРедактированияГруппа1.Доступность   = ДоступностьЭлементов;
			
			Элементы.ГруппаПанельРедактирования.Видимость      = Индивидуальный;
			Элементы.ДополнениеПоиск1.Видимость                = НЕ Индивидуальный;
			Элементы.Операции.ТолькоПросмотр                   = НЕ Индивидуальный ИЛИ НЕ ДоступностьЭлементов;
			
			Элементы.Операции.РазрешитьПеретаскивание          = Индивидуальный;
			Для каждого ИмяТЧ Из ПереченьРесурсов(Форма, Истина) Цикл
				Элементы[ИмяТЧ].РазрешитьНачалоПеретаскивания  = Индивидуальный;
			КонецЦикла;
			
			НастроитьНадписьПараметрыТехпроцесса(Форма);
			
	КонецЕсли;
	
	Если СтруктураРеквизитов.Свойство("Операция")
		ИЛИ Инициализация Тогда
		
		ТекущаяСтрока  = Элементы.Операции.ТекущаяСтрока;
		ТекущиеДанные  = ?(ТекущаяСтрока = Неопределено, Неопределено, Форма.Операции.НайтиПоИдентификатору(ТекущаяСтрока));
		ЭтоГруппа      = ЭтоГруппаИндивидуальныхОпераций(ТекущиеДанные);
		Индивидуальная = ЭтоОперацияИндивидуальная(ТекущиеДанные);
		ИндивидуальнаяВГруппе = ЭтоОперацияИндивидуальнаяВГруппе(ТекущиеДанные);
		
		ДоступностьЭлементовОперации = ДоступностьЭлементов И Индивидуальный
			И ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ТолькоПросмотр;
		
		Элементы.ГруппаСтраницаОсновное.ТолькоПросмотр         = НЕ ДоступностьЭлементовОперации;
		Элементы.ГруппаСтраницаВыходныеИзделия.ТолькоПросмотр  = НЕ ДоступностьЭлементовОперации ИЛИ ЭтоГруппа;
		Элементы.ГруппаСтраницаМатериалыИРаботы.ТолькоПросмотр = НЕ ДоступностьЭлементовОперации ИЛИ ЭтоГруппа;
		Элементы.ГруппаСтраницаТрудозатраты.ТолькоПросмотр     = НЕ ДоступностьЭлементовОперации ИЛИ ЭтоГруппа;
		Элементы.ГруппаСтраницаДополнительно.ТолькоПросмотр    = НЕ ДоступностьЭлементовОперации;
		
		Элементы.КТО.ТолькоПросмотр                = НЕ ДоступностьЭлементовОперации;
		Элементы.ВидОперации.ТолькоПросмотр        = НЕ ДоступностьЭлементовОперации;
		Элементы.ВариантНаладки.ТолькоПросмотр     = НЕ ДоступностьЭлементовОперации ИЛИ НЕ ТекущиеДанные.ИспользуютсяВариантыНаладки;
		Элементы.Загрузка.ТолькоПросмотр           = НЕ ДоступностьЭлементовОперации ИЛИ НЕ ТекущиеДанные.ПараллельнаяЗагрузка;
		Элементы.ПередаточнаяПартия.ТолькоПросмотр = НЕ ДоступностьЭлементовОперации;
		Элементы.Контроль.ТолькоПросмотр           = НЕ ДоступностьЭлементовОперации;
		Элементы.МожноПовторить.Видимость          = НЕ Индивидуальный;
		Элементы.МожноПовторить.ТолькоПросмотр     = НЕ ДоступностьЭлементовОперации;
		Элементы.МожноПропустить.ТолькоПросмотр    = НЕ ДоступностьЭлементовОперации;
		Элементы.Непрерывная.ТолькоПросмотр        = Истина;
		Элементы.Описание.ТолькоПросмотр           = НЕ ДоступностьЭлементовОперации;
		
		Если Индивидуальный Тогда
			Для каждого КлючИЗначение Из СлужебныеРеквизитыОпераций(Форма) Цикл
				Описание = КлючИЗначение.Значение;
				Если НЕ Описание.НаПанелиСвойств Тогда
					Продолжить;
				КонецЕсли;
				Если Описание.КонтролироватьПриРазличиях
					И Элементы[Описание.ИмяРеквизитаОсновного].Вид = ВидПоляФормы.ПолеВвода Тогда
					Если ЭтоГруппа И НЕ ТекущиеДанные[Описание.ИмяРеквизитаСлужебного] Тогда
						Элементы[Описание.ИмяРеквизитаОсновного].ПодсказкаВвода = Описание.ТекстГруппыПриРазличиях;
					ИначеЕсли ИндивидуальнаяВГруппе И НЕ ТекущиеДанные[Описание.ИмяРеквизитаСлужебного] Тогда
						Элементы[Описание.ИмяРеквизитаОсновного].ПодсказкаВвода = Описание.ТекстОперацииПриРазличиях;
					Иначе
						Элементы[Описание.ИмяРеквизитаОсновного].ПодсказкаВвода = "";
					КонецЕсли;
				КонецЕсли;
				Элементы[Описание.ИмяРеквизитаОсновного].ТолькоПросмотр = НЕ ДоступностьЭлементовОперации
					ИЛИ Элементы[Описание.ИмяРеквизитаОсновного].ТолькоПросмотр
					ИЛИ ?(ЭтоГруппа, НЕ ТекущиеДанные[Описание.ИмяРеквизитаСлужебного] И Описание.БлокироватьПриРазличиях, Ложь)
					ИЛИ ?(ИндивидуальнаяВГруппе, НЕ ТекущиеДанные[Описание.ИмяРеквизитаСлужебного] И Описание.КонтролироватьПриРазличиях, Ложь);
			КонецЦикла;
		КонецЕсли;
		
		Элементы.ВариантНаладки.ОтметкаНезаполненного = ДоступностьЭлементовОперации И НЕ ЭтоГруппа
			И НЕ Элементы.ВариантНаладки.ТолькоПросмотр И НЕ ЗначениеЗаполнено(ТекущиеДанные.ВариантНаладки);
		Элементы.Загрузка.ОтметкаНезаполненного = ДоступностьЭлементовОперации И НЕ ЭтоГруппа
			И НЕ Элементы.Загрузка.ТолькоПросмотр И НЕ ЗначениеЗаполнено(ТекущиеДанные.Загрузка);
		Элементы.Контроль.ОтметкаНезаполненного = ДоступностьЭлементовОперации И НЕ ЭтоГруппа
			И НЕ ЗначениеЗаполнено(ТекущиеДанные.Контроль);
			
		Элементы.МатериалыИУслугиСтатьяКалькуляции.АвтоОтметкаНезаполненного = Индивидуальная;
		Элементы.ТрудозатратыСтатьяКалькуляции.АвтоОтметкаНезаполненного = Индивидуальная;
		
		Элементы.МатериалыИУслуги_ЗаполнитьСтатьюКалькуляции.Доступность = ДоступностьЭлементовОперации И НЕ ЭтоГруппа;
		Элементы.Трудозатраты_ЗаполнитьСтатьюКалькуляции.Доступность = ДоступностьЭлементовОперации И НЕ ЭтоГруппа;
		
		УстановитьДоступностьКомандБуфераОбмена(Форма, ДоступностьЭлементовОперации И ЕстьСтрокиВБуфереОбмена());
		
		Если Форма.ИспользоватьКонтрольВыполненияОпераций Тогда
			
			МассивВариантов = ОперативныйУчетПроизводстваКлиентСервер.ВариантыКонтроляИндивидуальнойОперации(
				?(ТекущиеДанные <> Неопределено, ТекущиеДанные.Контроль, Неопределено));
			Элементы.Контроль.СписокВыбора.ЗагрузитьЗначения(МассивВариантов);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Инициализация
		Или СтруктураРеквизитов.Свойство("ИнформационнаяПанель") Тогда
		
		Элементы.ГруппаИнформационнаяПанель.Видимость = ЗначениеЗаполнено(Элементы.ИнформационнаяПанельНадпись.Заголовок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьНадписьПараметрыТехпроцесса(Форма)
	
	Объект = Форма.Объект;
	
	МассивСтрок = Новый Массив();
	
	МассивСтрок.Добавить(Строка(Объект.ТипТехнологическогоПроцесса));
	Если НЕ ЭтоИндивидуальныйТехпроцесс(Объект) Тогда
		МассивСтрок.Добавить(",");
		Если ЭтоСтандартныйТехпроцессПоСпецификации(Объект) Тогда
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(НСтр("ru = 'по спецификации';
										|en = 'by bill of materials'"));
		ИначеЕсли НЕ ЗначениеЗаполнено(Объект.ТехнологическийПроцесс) Тогда
			МассивСтрок.Добавить(" ");
			МассивСтрок.Добавить(НСтр("ru = 'не задан';
										|en = 'not specified'"));
		КонецЕсли;
		МассивСтрок.Добавить(" ");
		Если ЗначениеЗаполнено(Объект.ТехнологическийПроцесс) Тогда
			СтрокаСсылка = Строка(Объект.ТехнологическийПроцесс);
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(СтрокаСсылка, Форма.Кэш.ВажнаяНадписьШрифт,,,"#ОткрытьТехнологическийПроцесс"));
		КонецЕсли;
	КонецЕсли;
	
	Форма.НадписьПараметрыТехпроцесса = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактирование();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьТаблицы(ИменаТаблиц = "")
	
	Таблицы = Новый Структура;
	Таблицы.Вставить("Операции", Операции.ПолучитьЭлементы());
	Таблицы.Вставить("ВыходныеИзделия", Объект.ВыходныеИзделия);
	Таблицы.Вставить("МатериалыИУслуги", Объект.МатериалыИУслуги);
	Таблицы.Вставить("Трудозатраты", Объект.Трудозатраты);
	
	Для каждого КлючИЗначение Из Таблицы Цикл
		Если ПустаяСтрока(ИменаТаблиц) ИЛИ СтрНайти(ИменаТаблиц, КлючИЗначение.Ключ) > 0 Тогда
			Таблицы[КлючИЗначение.Ключ].Очистить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура РазблокироватьДанные(Ссылки, ИдентификаторФормы)
	
	Для каждого Ссылка Из Ссылки Цикл
		РазблокироватьДанныеДляРедактирования(Ссылка, ИдентификаторФормы);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НоменклатураПриИзмененииНаСервере(ИмяТЧ)
	
	ТекущиеДанные = Объект[ИмяТЧ].НайтиПоИдентификатору(Элементы[ИмяТЧ].ТекущаяСтрока);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	МассивСтрок.Добавить(ТекущиеДанные);
	ЗаполнитьСлужебныеРеквизитыНоменклатурыВСтроках(ИмяТЧ, МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКоличестваУпаковок(ИмяТЧ)
	
	СтруктураДействий = Новый Структура;
	
	СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц", ПроизводствоКлиентСервер.ПараметрыПересчетаКоличестваЕдиниц());
	
	ПакетнаяОбработкаТабличнойЧастиКлиент.ОбработатьСтрокуТЧ(Элементы[ИмяТЧ].ТекущиеДанные, СтруктураДействий, КэшированныеЗначения);
	
КонецПроцедуры

&НаКлиенте
Функция ПараметрыЗакрытия()
	
	Результат = Новый Структура;
	
	Если Режим = РедакторПроизводственногоПроцессаКлиентСервер.РежимИзФормыЭтапа() Тогда
		Результат.Вставить("АдресДанныхЭтапаДляОбновления", АдресДанныхЭтапаДляОбновления);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#Область Кэш

&НаСервере
Процедура ИнициализироватьКэш()
	
	Кэш = Новый Структура;
	Кэш.Вставить("АдресДополнительныхДанных");
	Кэш.Вставить("ВажнаяНадписьШрифт", ШрифтыСтиля.ВажнаяНадписьШрифт);
	Кэш.Вставить("ПроизводственныеОперации", Новый Массив);
	Кэш.Вставить("АктивизацияСтроки", Ложь);
	Кэш.Вставить("БлокироватьСтроки", Новый Массив);
	
	СтрокаДереваСтруктурой = Новый Структура;
	Для каждого Колонка Из РеквизитФормыВЗначение("Операции").Колонки Цикл
		СтрокаДереваСтруктурой.Вставить(Колонка.Имя, Колонка.ТипЗначения.ПривестиЗначение(Неопределено));
	КонецЦикла;
	Для каждого КлючИЗначение Из СлужебныеРеквизитыОпераций(ЭтотОбъект) Цикл
		ИмяРеквизита = КлючИЗначение.Значение.ИмяРеквизитаСлужебного;
		Если СтрокаДереваСтруктурой.Свойство(ИмяРеквизита) Тогда
			СтрокаДереваСтруктурой.Удалить(ИмяРеквизита);
		КонецЕсли;
	КонецЦикла;
	Кэш.Вставить("СтрокаДереваСтруктурой", СтрокаДереваСтруктурой);
	
	ОбработкаОбъект  = РеквизитФормыВЗначение("Объект");
	ОписаниеРесурсов = Новый Структура;
	Для каждого ИмяТЧ Из ПереченьРесурсов(ЭтотОбъект, Истина) Цикл
		ОписаниеРесурсов.Вставить(ИмяТЧ, ОбработкаОбъект[ИмяТЧ].ВыгрузитьКолонки());
	КонецЦикла;
	Кэш.Вставить("ОписаниеРесурсов", ПоместитьВоВременноеХранилище(ОписаниеРесурсов, УникальныйИдентификатор));
	
	МассивСостояний = Новый Массив;
	Для каждого Значение Из Перечисления.СостоянияВыполненияОпераций Цикл
		МассивСостояний.Добавить(Значение);
	КонецЦикла;
	Кэш.Вставить("МассивСостояний", МассивСостояний);
	
	ТаблицаОбеспечениеЭтапа = ТаблицаОбеспечениеЭтапа();
	Кэш.Вставить("АдресТаблицыОбеспечениеЭтапа", ПоместитьВоВременноеХранилище(ТаблицаОбеспечениеЭтапа, УникальныйИдентификатор));
	
	СохранитьКэшРесурсов(Неопределено);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьЗначениеИзКэша(Ключ)
	
	Результат = Неопределено;
	
	ПолучитьКэшСерверный().Свойство(Ключ, Результат);
	
	Возврат Результат
	
КонецФункции

&НаСервере
Процедура ПоместитьЗначениеВКэш(Ключ, Значение)
	
	КэшСерверный = ПолучитьКэшСерверный();
	
	КэшСерверный.Вставить(Ключ, Значение);
	
	СохранитьКэшСерверный(КэшСерверный);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКэшСерверный()
	
	Если ЭтоАдресВременногоХранилища(Кэш.АдресДополнительныхДанных) Тогда
		Возврат ПолучитьИзВременногоХранилища(Кэш.АдресДополнительныхДанных);
	Иначе
		СтруктураДанных = Новый Структура;
		Возврат СтруктураДанных;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура СохранитьКэшСерверный(СтруктураДанных)
	
	Кэш.АдресДополнительныхДанных = ПоместитьВоВременноеХранилище(СтруктураДанных, УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКэшРесурсов()
	
	КэшРесурсов = ПолучитьЗначениеИзКэша("Ресурсы");
	
	Если КэшРесурсов = Неопределено Тогда
		КэшРесурсов = Новый Соответствие();
	КонецЕсли;
	
	Возврат КэшРесурсов;
	
КонецФункции

&НаСервере
Процедура СохранитьКэшРесурсов(КэшРесурсов)
	
	ПоместитьЗначениеВКэш("Ресурсы", КэшРесурсов);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НовыйИдентификаторОперации(Форма)
	
	Результат = Неопределено;
	
	Если НЕ Форма.Кэш.Свойство("НовыйИдентификаторОперации", Результат) Тогда
		Результат = 0;
		ДанныеСтроки = Неопределено;
		Пока СледующаяСтрока(Форма, ДанныеСтроки, Истина) Цикл
			Результат = Макс(ДанныеСтроки.ИдентификаторОперации, Результат);
		КонецЦикла;
	КонецЕсли;
	
	Результат = Результат + 1;
	Форма.Кэш.Вставить("НовыйИдентификаторОперации", Результат);
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НачальныйИндексРедактирования(Форма)
	
	Результат = Неопределено;
	СписокОпераций = Форма.Операции.ПолучитьЭлементы();
	
	Если НЕ Форма.Кэш.Свойство("НачальныйИндексРедактирования", Результат) Тогда
		Результат = 0;
		ДанныеСтроки = Неопределено;
		Пока СледующаяСтрока(Форма, ДанныеСтроки, Ложь) Цикл
			Если ДанныеСтроки.ТолькоПросмотр Тогда
				Результат = Макс(СписокОпераций.Индекс(ДанныеСтроки)+1, Результат);
			КонецЕсли;
		КонецЦикла;
		Форма.Кэш.Вставить("НачальныйИндексРедактирования", Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЕстьЗаблокированныеОперации(Форма)
	
	Результат = Неопределено;
	
	Если НЕ Форма.Кэш.Свойство("ЕстьЗаблокированныеОперации", Результат) Тогда
		Результат = Ложь;
		ДанныеСтроки = Неопределено;
		Пока СледующаяСтрока(Форма, ДанныеСтроки) Цикл
			Если ДанныеСтроки.ТолькоПросмотр Тогда
				Результат = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Форма.Кэш.Вставить("ЕстьЗаблокированныеОперации", Результат);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СброситьСлужебныеПризнакиКэша(Форма)
	
	Для каждого Ключ Из СтрРазделить("НовыйИдентификаторОперации,НачальныйИндексРедактирования,ЕстьЗаблокированныеОперации",",") Цикл
		Если Форма.Кэш.Свойство(Ключ) Тогда
			Форма.Кэш.Удалить(Ключ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область Конструкторы

// Таблица операций конструктор.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица операций конструктор
&НаСервере
Функция ТаблицаОперацийКонструктор()
	
	Результат = Новый ТаблицаЗначений;
	
	Для каждого Колонка Из РеквизитФормыВЗначение("Операции").Колонки Цикл
		Результат.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтрокаДереваСтруктуройКонструктор(Форма)
	
	Результат = Новый Структура;
	
	Для каждого КлючИЗначение Из Форма.Кэш.СтрокаДереваСтруктурой Цикл
		Результат.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПоказателиВыполнения(Значения = Неопределено)
	
	Результат = Новый Структура;
	
	Ключи = "Состояние,Назначена,Запланировано,Создано,Выполняется,Выполнено,НаКонтроле,НаДоработке,Брак,Пропущено,МожноВыполнять";
	Для каждого Ключ Из СтрРазделить(Ключи, ",") Цикл
		Результат.Вставить(Ключ, 0);
	КонецЦикла;
	
	Если Значения <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(Результат, Значения);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Описание служебного реквизита конструктор.
// 
// Параметры:
//  ИмяРеквизита - Строка - Имя реквизита
// 
// Возвращаемое значение:
//  Структура - Описание служебного реквизита конструктор:
// * ИмяРеквизитаОсновного - Строка
// * ИмяРеквизитаСлужебного - Строка
// * Путь - Строка
// * Тип - ОписаниеТипов
// * ГрупповоеРедактирование - Булево
// * БлокироватьПриРазличиях - Булево
// * КонтролироватьПриРазличиях - Булево
// * ТекстГруппыПриРазличиях - Строка
// * ТекстОперацииПриРазличиях - Строка
// * РеквизитыКонтроляРазличий - Строка
// * ВКолонкеСписка - Булево
// * НаПанелиСвойств - Булево
&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеСлужебногоРеквизитаКонструктор(ИмяРеквизита)
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИмяРеквизитаОсновного", ИмяРеквизита);
	Результат.Вставить("ПутьОсновного", "Операции."+Результат.ИмяРеквизитаОсновного);
	Результат.Вставить("ИмяРеквизитаСлужебного", ИмяРеквизита+"_");
	Результат.Вставить("ПутьСлужебного", "Операции."+Результат.ИмяРеквизитаСлужебного);
	
	Результат.Вставить("ГрупповойРасчет", Истина);
	Результат.Вставить("ГрупповоеРедактирование", Ложь);
	
	Результат.Вставить("БлокироватьПриРазличиях", Ложь);
	Результат.Вставить("РеквизитыКонтроляРазличий", "");
	Результат.Вставить("КонтролироватьПриРазличиях", Ложь);
	Результат.Вставить("ТекстГруппыПриРазличиях", НСтр("ru = '-';
														|en = '-'"));
	Результат.Вставить("ТекстОперацииПриРазличиях", НСтр("ru = '-';
														|en = '-'"));
	
	Результат.Вставить("ВКолонкеСписка", Ложь);
	Результат.Вставить("НаПанелиСвойств", Ложь);
	
	Результат.Вставить("Тип", Новый ОписаниеТипов("Булево"));
	Результат.Вставить("ТекущееЗначение", Неопределено);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Константы

&НаКлиентеНаСервереБезКонтекста
Функция СтандартныйТехпроцесс()
	Возврат ПредопределенноеЗначение("Перечисление.ТипыТехнологическихПроцессовЭтапа.Стандартный");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСтандартныйТехпроцесс(Объект)
	Возврат Объект.ТипТехнологическогоПроцесса = СтандартныйТехпроцесс();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоСтандартныйТехпроцессПоСпецификации(Объект)
	Возврат Объект.ТипТехнологическогоПроцесса = СтандартныйТехпроцесс()
		И ТипЗнч(Объект.ТехнологическийПроцесс) = Тип("СправочникСсылка.РесурсныеСпецификации")
		И ЗначениеЗаполнено(Объект.ТехнологическийПроцесс);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция АльтернативныйТехпроцесс()
	Возврат ПредопределенноеЗначение("Перечисление.ТипыТехнологическихПроцессовЭтапа.Альтернативный");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоАльтернативныйТехпроцесс(Объект)
	Возврат Объект.ТипТехнологическогоПроцесса = АльтернативныйТехпроцесс();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИндивидуальныйТехпроцесс()
	Возврат ПредопределенноеЗначение("Перечисление.ТипыТехнологическихПроцессовЭтапа.Индивидуальный");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоИндивидуальныйТехпроцесс(Объект)
	Возврат Объект.ТипТехнологическогоПроцесса = ИндивидуальныйТехпроцесс();
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстТехпроцессИндивидуальный()
	Возврат НСтр("ru = 'Индивидуальный';
				|en = 'Individual'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстТехпроцессСпецификация()
	Возврат НСтр("ru = 'Спецификация';
				|en = 'Bill of materials'");
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОперацияПоНСИ(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияПоНСИ(ДанныеСтроки);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОперацияПоСпецификации(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияПоСпецификации(ДанныеСтроки);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОперацияПоТехнологическомуПроцессу(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияПоТехнологическомуПроцессу(ДанныеСтроки);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОперацияИндивидуальная(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеСтроки);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоГруппаИндивидуальныхОпераций(ДанныеСтроки);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЭтоОперацияИндивидуальнаяВГруппе(ДанныеСтроки)
	Возврат РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеСтроки)
		И ДанныеСтроки.ПолучитьРодителя() <> Неопределено;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ОтборОперации(ДанныеСтроки)
	Возврат Новый Структура("Операция,ИдентификаторОперации", ДанныеСтроки.Операция, ДанныеСтроки.ИдентификаторОперации);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГраницаРедактируемыхСостояний()
	Возврат 7;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ГраницаКонечныхСостояний()
	Возврат 3;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецОбласти
