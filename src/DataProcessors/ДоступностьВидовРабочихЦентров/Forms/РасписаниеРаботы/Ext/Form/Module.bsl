#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ВидРабочегоЦентра = Параметры.ВидРабочегоЦентра;
	ДатаГрафика = Параметры.ДатаГрафика;
	ВнесеныРучныеИзменения = Параметры.ВнесеныРучныеИзменения;
	АдресДанныхГрафикиРаботыРЦ = Параметры.АдресДанныхГрафикиРаботыРЦ;
	
	ПереходящиеСмены = (Параметры.ИнтервалПланирования = Перечисления.ТочностьГрафикаПроизводства.Смена);
	
	Если ТипЗнч(Параметры.ГрафикиРаботы) = Тип("Массив") И Параметры.ГрафикиРаботы.Количество() > 0 Тогда
		АдресГрафикиРаботыРЦ = ПоместитьВоВременноеХранилище(Параметры.ГрафикиРаботы, УникальныйИдентификатор);
	КонецЕсли;
	
	Если Параметры.ДанныеИзменены Тогда
		РасписаниеГрафиковРаботыРЦ = ПолучитьИзВременногоХранилища(АдресДанныхГрафикиРаботыРЦ);
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("РабочийЦентр", Параметры.РабочийЦентр);
		СтруктураПоиска.Вставить("Смена", Параметры.Смена);
		СтруктураПоиска.Вставить("ДатаГрафика", ДатаГрафика);
		
		РасписаниеНаДатуГрафика = РасписаниеГрафиковРаботыРЦ.НайтиСтроки(СтруктураПоиска);
	Иначе
		РасписаниеНаДатуГрафика = РасписаниеНаДатуГрафика(Параметры.РабочийЦентр, Параметры.Смена, ДатаГрафика);
	КонецЕсли; 
	
	Для каждого РасписаниеДня Из РасписаниеНаДатуГрафика Цикл
		Если РасписаниеДня.Количество <> 0 Тогда
			СтрокаИнтервал = Интервалы.Добавить();
			СтрокаИнтервал.ВремяНачала    = РасписаниеДня.ВремяНачала;
			СтрокаИнтервал.ВремяОкончания = РасписаниеДня.ВремяОкончания;
		КонецЕсли; 
	КонецЦикла;
	
	Если Не ПереходящиеСмены Тогда
		Интервалы.Сортировать("ВремяНачала");
	КонецЕсли;
	
	Элементы.Интервалы.ИзменятьПорядокСтрок = ПереходящиеСмены;
	
	ЗаполнитьОбщееВремя(ЭтотОбъект);
	
	Если Параметры.ТолькоПросмотрГрафика Тогда
		Элементы.Интервалы.ТолькоПросмотр = Истина;
		Элементы.ДатыГрафикаЗаполнитьПоГрафикуРаботы.Видимость = Ложь;
		Элементы.КоманднаяПанельНиз.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ОповещениеЗакрытия = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
							|en = 'Data has changed. Save the changes?'");
		
		ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ОповещениеЗакрытия, Отказ, ЗавершениеРаботы, ТекстВопроса,
			ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Календари") Тогда
		ЗаполнитьПоГрафикуРаботыНаСервере(ВыбранноеЗначение);
		ВнесеныРучныеИзменения = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИнтервалы

&НаКлиенте
Процедура ИнтервалыПриИзменении(Элемент)

	ВнесеныРучныеИзменения = Истина;
	
	ЗаполнитьОбщееВремя(ЭтотОбъект);
	
	Если Не ПереходящиеСмены Тогда
		УправлениеПроизводствомКлиент.ВосстановитьПорядокСтрокКоллекцииПослеРедактирования(
			Интервалы,
			"ВремяНачала",
			Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если ОтменаРедактирования Тогда
		
		ЗаполнитьОбщееВремя(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеИнтервалаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Интервалы.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И НачалоМинуты(ТекущиеДанные.ВремяОкончания) <> ТекущиеДанные.ВремяОкончания Тогда
		ТекущиеДанные.ВремяОкончания = НачалоМинуты(ТекущиеДанные.ВремяОкончания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаполнитьПоГрафикуРаботы(Команда)
	
	Если ЭтоАдресВременногоХранилища(АдресГрафикиРаботыРЦ) Тогда
		
		ЗаполнитьПоГрафикуРаботыНаСервере(ПолучитьИзВременногоХранилища(АдресГрафикиРаботыРЦ));
		ВнесеныРучныеИзменения = Ложь;
		
	Иначе
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоГрафикуРаботыЗавершение", ЭтотОбъект);
		ОткрытьФорму("Справочник.Календари.ФормаВыбора", 
						ПараметрыФормы, 
						ЭтаФорма,,,, 
						ОписаниеОповещения, 
						РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗавершитьРедактирование();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПоГрафикуРаботыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт

	Если РезультатЗакрытия <> Неопределено Тогда
		ЗаполнитьПоГрафикуРаботыНаСервере(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоГрафикуРаботыНаСервере(ГрафикРаботы)
	
	Интервалы.Очистить();
	
	РасписанияРаботыНаПериод = ПланированиеПроизводства.РасписаниеРаботыПоГрафику(
		ГрафикРаботы,
		НачалоДня(ДатаГрафика),
		КонецДня(ДатаГрафика),
		ПереходящиеСмены);
	Для каждого СтрокаРасписание Из РасписанияРаботыНаПериод Цикл
		СтрокаИнтервал = Интервалы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаИнтервал, СтрокаРасписание);
	КонецЦикла; 
	
	ЗаполнитьОбщееВремя(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗавершитьРедактирование();
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактирование()

	ОчиститьСообщения();
	
	РасписаниеДня = РасписаниеДня();
	Если РасписаниеДня = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РезультатРедактирования = Новый Структура;
	РезультатРедактирования.Вставить("ВнесеныРучныеИзменения", ВнесеныРучныеИзменения);
	РезультатРедактирования.Вставить("РасписаниеДня", РасписаниеДня);
	
	Модифицированность = Ложь;
	
	Закрыть(РезультатРедактирования);

КонецПроцедуры

&НаКлиенте
Функция РасписаниеДня()
	
	Отказ = Ложь;
	
	РасписаниеДня = Новый Массив;
	
	Если ПереходящиеСмены Тогда
		НомерДня = 1;
		ОкончаниеДня = '00010101';
		Для ИндексСтроки = 0 По Интервалы.Количество()-1 Цикл
			СтрокаРасписания = Интервалы[ИндексСтроки];
			
			Если СтрокаРасписания.ВремяНачала < ОкончаниеДня Тогда
				НомерДня = НомерДня + 1;
			КонецЕсли;
			
			Если СтрокаРасписания.ВремяНачала = СтрокаРасписания.ВремяОкончания
				Или Не ЗначениеЗаполнено(СтрокаРасписания.ВремяОкончания)
				Или СтрокаРасписания.ВремяНачала > СтрокаРасписания.ВремяОкончания Тогда
				НомерДня = НомерДня + 1;
			КонецЕсли;
			
			ОкончаниеДня = СтрокаРасписания.ВремяОкончания;
			
			Если НомерДня >= 3 И ЗначениеЗаполнено(ОкончаниеДня) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Обнаружены пересекающиеся интервалы';
						|en = 'Overlapping intervals are detected'"), ,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Интервалы[%1].ВремяНачала", ИндексСтроки), ,
					Отказ);
				Прервать;
			КонецЕсли;
			
			Длительность = УправлениеПроизводствомКлиентСервер.ДлительностьИнтервалаРасписания(
				СтрокаРасписания.ВремяНачала, СтрокаРасписания.ВремяОкончания);
			
			РасписаниеДня.Добавить(Новый Структура("ВремяНачала, ВремяОкончания, Длительность", 
									СтрокаРасписания.ВремяНачала, СтрокаРасписания.ВремяОкончания, Длительность));
		КонецЦикла;
	Иначе
		ОкончаниеДня = Неопределено;
	
		Для Каждого СтрокаРасписания Из Интервалы Цикл
			ИндексСтроки = Интервалы.Индекс(СтрокаРасписания);
			Если СтрокаРасписания.ВремяНачала > СтрокаРасписания.ВремяОкончания 
				И ЗначениеЗаполнено(СтрокаРасписания.ВремяОкончания) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Время начала больше времени окончания';
						|en = 'Start time is greater than the end time.'"), ,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Интервалы[%1].ВремяОкончания", ИндексСтроки), ,
					Отказ);
			КонецЕсли;
			Если СтрокаРасписания.ВремяНачала = СтрокаРасписания.ВремяОкончания Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Длительность интервала не определена';
						|en = 'Interval length is not specified'"), ,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Интервалы[%1].ВремяОкончания", ИндексСтроки), ,
					Отказ);
			КонецЕсли;
			Если ОкончаниеДня <> Неопределено Тогда
				Если ОкончаниеДня > СтрокаРасписания.ВремяНачала 
					Или Не ЗначениеЗаполнено(ОкончаниеДня) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
						НСтр("ru = 'Обнаружены пересекающиеся интервалы';
							|en = 'Overlapping intervals are detected'"), ,
						СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Интервалы[%1].ВремяНачала", ИндексСтроки), ,
						Отказ);
				КонецЕсли;
			КонецЕсли;
			ОкончаниеДня = СтрокаРасписания.ВремяОкончания;
			
			Длительность = УправлениеПроизводствомКлиентСервер.ДлительностьИнтервалаРасписания(
				СтрокаРасписания.ВремяНачала, СтрокаРасписания.ВремяОкончания);
			
			РасписаниеДня.Добавить(Новый Структура("ВремяНачала, ВремяОкончания, Длительность", 
									СтрокаРасписания.ВремяНачала, СтрокаРасписания.ВремяОкончания, Длительность));
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат РасписаниеДня;
	
КонецФункции

&НаСервере
Функция РасписаниеНаДатуГрафика(РабочийЦентр, Смена, ДатаГрафика)

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоступностьРабочихЦентровИнтервалы.ВремяНачала КАК ВремяНачала,
	|	ДоступностьРабочихЦентровИнтервалы.ВремяОкончания КАК ВремяОкончания,
	|	ДоступностьРабочихЦентровИнтервалы.Количество
	|ИЗ
	|	Документ.ДоступностьРабочихЦентров.Интервалы КАК ДоступностьРабочихЦентровИнтервалы
	|ГДЕ
	|	ДоступностьРабочихЦентровИнтервалы.Ссылка.Проведен
	|	И ДоступностьРабочихЦентровИнтервалы.РабочийЦентр = &РабочийЦентр
	|	И ДоступностьРабочихЦентровИнтервалы.Смена = &Смена
	|	И ДоступностьРабочихЦентровИнтервалы.ДатаГрафика = &ДатаГрафика
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("РабочийЦентр", РабочийЦентр);
	Запрос.УстановитьПараметр("Смена", Смена);
	Запрос.УстановитьПараметр("ДатаГрафика", ДатаГрафика);
	
	РасписаниеНаДатуГрафика = Запрос.Выполнить().Выгрузить();

	Возврат РасписаниеНаДатуГрафика;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьОбщееВремя(Форма)
	
	Секунд = 0;
	Для каждого Строка Из Форма.Интервалы Цикл
		Секунд = Секунд + УправлениеПроизводствомКлиентСервер.ДлительностьИнтервалаРасписания(
			Строка.ВремяНачала, Строка.ВремяОкончания);
	КонецЦикла;
	
	Часов = Окр(Секунд / 3600, 1);
	
	Форма.Элементы.ОбщееВремя.Заголовок = СтрШаблон(НСтр("ru = 'Всего: %1 ч.';
														|en = 'Total: %1 h.'"), Часов);
	
КонецПроцедуры

#КонецОбласти
