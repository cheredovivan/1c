
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ОбособитьВыпускПолуфабрикатов.Видимость = Не Параметры.ПолуфабрикатыПроизводитьОбособленно;
	Элементы.ЗаменятьМатериалыНаАналоги.Видимость = Параметры.ИспользоватьАналоги;
	
	ПараметрыСозданияДокументов = Обработки.ФормированиеЭтаповПроизводства.ПараметрыСозданияДокументовПоУмолчанию();
	
	ЗаполнитьЗначенияСвойств(ПараметрыСозданияДокументов, Параметры.ПараметрыСозданияДокументов);	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПараметрыСозданияДокументов);	
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РезервироватьМатериалыПриИзменении(Элемент)
	
	Если Не РезервироватьМатериалы Тогда
		ЗаменятьМатериалыНаАналоги = Ложь;
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "РезервироватьМатериалы");

КонецПроцедуры
 
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ПараметрыСозданияДокументов, ЭтотОбъект);
	
	Закрыть(ПараметрыСозданияДокументов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);

	Если СтруктураРеквизитов.Свойство("РезервироватьМатериалы")
		ИЛИ Инициализация Тогда
		Элементы.ЗаменятьМатериалыНаАналоги.Доступность = Форма.РезервироватьМатериалы;
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

