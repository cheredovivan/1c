#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

 // Получение количества элементов справочников с группировкой по Ставкам НДС.
// 
// Параметры:
//  ПараметрыПолученияДанных - Структура - См. ПараметрыПолученияДанных()
// 
// Возвращаемое значение:
//  Структура - См. СтруктураИтоговыхДанных() 
Функция ПолучениеДанныхПоСтавкахНДС(ПараметрыПолученияДанных) Экспорт
	
	ДанныеСправочниковВРазрезеСтавокНДС = СтруктураИтоговыхДанных();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ТекущаяСтавка",     ПараметрыПолученияДанных.ТекущаяСтавка);
	Запрос.УстановитьПараметр("НоваяСтавка",       ПараметрыПолученияДанных.НоваяСтавка);
	Запрос.УстановитьПараметр("Период",            ПараметрыПолученияДанных.Период);
	Запрос.УстановитьПараметр("СтранаРегистрации", ПараметрыПолученияДанных.Страна);
	Запрос.УстановитьПараметр("ПустаяДата",        Дата(1,1,1));
	
	ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, Справочники.ВидыНоменклатуры.ПустаяСсылка().Метаданные().Имя);
	ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, Справочники.ДоговорыКонтрагентов.ПустаяСсылка().Метаданные().Имя);
	
	//++ НЕ УТ
	ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, Справочники.ДоговорыАренды.ПустаяСсылка().Метаданные().Имя);
	//-- НЕ УТ
	
	ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, Справочники.Номенклатура.ПустаяСсылка().Метаданные().Имя);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыМеждуОрганизациями") Тогда
		ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, Справочники.ДоговорыМеждуОрганизациями.ПустаяСсылка().Метаданные().Имя);
	КонецЕсли;
	
	Возврат ДанныеСправочниковВРазрезеСтавокНДС;
	
КонецФункции

// Параметры получения данных справочников по ставкам НДС.
// 
// Возвращаемое значение:
//  Структура - Параметры получения данных подготовки первичной декларации:
//   * ТекущаяСтавка - СправочникСсылка.СтавкиНДС
//   * НоваяСтавка - СправочникСсылка.СтавкиНДС
//   * Страна - СправочникСсылка.СтраныМира
//   * Период - Дата
//   * СписокСправочников - СписокЗначений из Строка - список названий справочников к обработке
Функция ПараметрыПолученияДанных() Экспорт
	
	ПараметрыПолученияДанных = Новый Структура;
	ПараметрыПолученияДанных.Вставить("ТекущаяСтавка",      Справочники.СтавкиНДС.ПустаяСсылка());
	ПараметрыПолученияДанных.Вставить("НоваяСтавка",        Справочники.СтавкиНДС.ПустаяСсылка());
	ПараметрыПолученияДанных.Вставить("Страна",             Справочники.СтраныМира.ПустаяСсылка());
	ПараметрыПолученияДанных.Вставить("Период",             Дата(1, 1, 1)); 
	ПараметрыПолученияДанных.Вставить("СписокСправочников", Новый СписокЗначений());
	
	Возврат ПараметрыПолученияДанных;
	
КонецФункции

// Структура итоговых данных.
// 
// Возвращаемое значение:
//  Структура - Структура итоговых данных:
// * Номенклатура - Массив из Структура - см. СтруктураВозвращаемыхДанных() 
// * ВидыНоменклатуры - Массив из Структура - см. СтруктураВозвращаемыхДанных()  
// * ДоговорыКонтрагентов - Массив из Структура - см. СтруктураВозвращаемыхДанных() 
// * ДоговорыАренды - Массив из Структура - см. СтруктураВозвращаемыхДанных()  
// * ДоговорыМеждуОрганизациями - Массив из Структура - см. СтруктураВозвращаемыхДанных()  
Функция СтруктураИтоговыхДанных() Экспорт
	
	ДанныеСправочниковВРазрезеСтавокНДС = Новый Структура;
	ДанныеСправочниковВРазрезеСтавокНДС.Вставить("Номенклатура", Новый Массив());
	ДанныеСправочниковВРазрезеСтавокНДС.Вставить("ВидыНоменклатуры", Новый Массив());
	ДанныеСправочниковВРазрезеСтавокНДС.Вставить("ДоговорыКонтрагентов", Новый Массив());
	ДанныеСправочниковВРазрезеСтавокНДС.Вставить("ДоговорыАренды", Новый Массив());
	ДанныеСправочниковВРазрезеСтавокНДС.Вставить("ДоговорыМеждуОрганизациями", Новый Массив());
	
	Возврат ДанныеСправочниковВРазрезеСтавокНДС;
	
КонецФункции

// Структура итоговых данных.
// 
// Возвращаемое значение:
//  Структура:
//	 * СтавкаНДС - СправочникСсылка.СтавкиНДС
//	 * Количество - Число
//	 * УИД - Строка
Функция СтруктураВозвращаемыхДанных() Экспорт
	
	СтруктураСтроки = Новый Структура();
	
	СтруктураСтроки.Вставить("СтавкаНДС",  Справочники.СтавкиНДС.ПустаяСсылка());
	СтруктураСтроки.Вставить("Количество", 0);
	СтруктураСтроки.Вставить("УИД",        "");
	
	Возврат СтруктураСтроки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьКоличествоЭлементовСправочника(Запрос, ДанныеСправочниковВРазрезеСтавокНДС, ВидСправочника)
	
	МассивДанных = Новый Массив;
	
	Если ВидСправочника = "ВидыНоменклатуры" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Справочник.СтавкаНДС КАК СтавкаНДС,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник.Ссылка) КАК Количество,
		|	СтавкиНДС.Ставка КАК Ставка
		|ИЗ
		|	Справочник.ВидыНоменклатуры КАК Справочник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
		|		ПО Справочник.СтавкаНДС = СтавкиНДС.Ссылка
		|ГДЕ
		|	НЕ Справочник.ПометкаУдаления
		|
		|СГРУППИРОВАТЬ ПО
		|	Справочник.СтавкаНДС,
		|	СтавкиНДС.Ставка
		|
		|";
	
	//++ НЕ УТ	
	ИначеЕсли ВидСправочника = "ДоговорыАренды" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ УсловияДоговоровАренды.Договор) КАК Количество,
		|	УсловияДоговоровАренды.СтавкаНДС КАК СтавкаНДС,
		|	СтавкиНДС.Ставка КАК Ставка
		|ИЗ
		|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Период, ) КАК УсловияДоговоровАренды
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
		|		ПО УсловияДоговоровАренды.СтавкаНДС = СтавкиНДС.Ссылка
		|ГДЕ
		|	УсловияДоговоровАренды.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Закрыт)
		|
		|СГРУППИРОВАТЬ ПО
		|	УсловияДоговоровАренды.СтавкаНДС,
		|	СтавкиНДС.Ставка
		|
		|";
	//-- НЕ УТ
		
	ИначеЕсли ВидСправочника = "Номенклатура" Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СтавкиНДСНоменклатурыСрезПоследних.Номенклатура) КАК Количество,
		|	СтавкиНДСНоменклатурыСрезПоследних.СтавкаНДС КАК СтавкаНДС,
		|	СтавкиНДС.Ставка КАК Ставка
		|ИЗ
		|	РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК СтавкиНДСНоменклатурыСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
		|		ПО СтавкиНДСНоменклатурыСрезПоследних.СтавкаНДС = СтавкиНДС.Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	СтавкиНДСНоменклатурыСрезПоследних.СтавкаНДС,
		|	СтавкиНДС.Ставка
		|
		|";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Справочник.СтавкаНДС КАК СтавкаНДС,
		|	СтавкиНДС.Ставка КАК Ставка,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Справочник.Ссылка) КАК Количество
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК Справочник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтавкиНДС КАК СтавкиНДС
		|		ПО Справочник.СтавкаНДС = СтавкиНДС.Ссылка
		|ГДЕ
		|	НЕ Справочник.ПометкаУдаления
		|	И Справочник.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Закрыт)
		|	И (Справочник.ДатаОкончанияДействия >= &Период
		|			ИЛИ Справочник.ДатаОкончанияДействия = &ПустаяДата)
		|
		|СГРУППИРОВАТЬ ПО
		|	Справочник.СтавкаНДС,
		|	СтавкиНДС.Ставка
		|
		|";
			
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ДоговорыКонтрагентов", ВидСправочника);
			
	КонецЕсли;
	
	Результат = Запрос.Выполнить().Выгрузить();
	Результат.Сортировать("Ставка Убыв");
	Для Каждого Строка Из Результат Цикл
		СтруктураСтроки = СтруктураВозвращаемыхДанных();
		СтруктураСтроки.Вставить("СтавкаНДС",  Строка.СтавкаНДС);
		СтруктураСтроки.Вставить("Количество", Строка.Количество);
		СтруктураСтроки.Вставить("УИД",        ?(ЗначениеЗаполнено(Строка.СтавкаНДС),Строка.СтавкаНДС.УникальныйИдентификатор(),""));
		МассивДанных.Добавить(СтруктураСтроки);
	КонецЦикла;
	
	ДанныеСправочниковВРазрезеСтавокНДС[ВидСправочника] = МассивДанных;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
