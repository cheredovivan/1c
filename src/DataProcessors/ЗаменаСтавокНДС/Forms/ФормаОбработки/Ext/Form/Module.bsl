&НаКлиенте
Перем ЭлементыФормыСправочников;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(СтранаРегистрации) Тогда
		СтранаРегистрации = Константы.ОсновнаяСтрана.Получить();
	КонецЕсли;
	
	ЭтоУТ = ПолучитьФункциональнуюОпцию("УправлениеТорговлей");
	Элементы.ГруппаДоговорыАренды.Видимость = Не ЭтоУТ;
	Элементы.ПерейтиДокументы.Видимость     = Не ЭтоУТ;
	Элементы.ПредупреждениеАренда.Видимость = Не ЭтоУТ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭлементыФормыСправочников = СоответствиеЭлементовФормы();
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если Не ЗначениеЗаполнено(СтранаРегистрации) Тогда
		СтранаРегистрации = Константы.ОсновнаяСтрана.Получить();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекущаяСтавкаПриИзменении(Элемент)
	
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаРегистрацииПриИзменении(Элемент)
	
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеНоменклатураОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтавкаНДС = ДанныеНоменклатураОбработкаНавигационнойСсылкиНаСервере(НавигационнаяСсылкаФорматированнойСтроки);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Страна",    СтранаРегистрации);
	ПараметрыФормы.Вставить("СтавкаНДС", СтавкаНДС);
	ПараметрыФормы.Вставить("Период",    Период);
	
	ОткрытьФорму("РегистрСведений.СтавкиНДСНоменклатуры.Форма.РабочееМесто", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеВидыНоменклатурыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуСправочника(НавигационнаяСсылкаФорматированнойСтроки, "Справочник.ВидыНоменклатуры.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеДоговорыКонтрагентовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуСправочника(НавигационнаяСсылкаФорматированнойСтроки, "Справочник.ДоговорыКонтрагентов.Форма.ФормаСписка", Истина);
	
КонецПроцедуры 

&НаКлиенте
Процедура ДанныеДоговорыМеждуорганизациямиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуСправочника(НавигационнаяСсылкаФорматированнойСтроки, "Справочник.ДоговорыМеждуОрганизациями.Форма.ФормаСписка", Истина);

КонецПроцедуры

&НаКлиенте
Процедура ДанныеДоговорыАрендыОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОткрытьФормуСправочникаДоговорыАренды(НавигационнаяСсылкаФорматированнойСтроки);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВезде(Команда)
	
	Если Не ПроверитьЗаполнениеФормы(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	СписокСправочников = Новый СписокЗначений;
	СписокСправочников.Добавить("Номенклатура");
	СписокСправочников.Добавить("ВидыНоменклатуры");
	СписокСправочников.Добавить("ДоговорыКонтрагентов");
	СписокСправочников.Добавить("ДоговорыМеждуОрганизациями");
	
	ДлительнаяОперация = НачатьЗаменуСтавкиНаСервере(СписокСправочников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаменаСтавкиЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВВидеНоменклатуры(Команда)
	
	Если Не ПроверитьЗаполнениеФормы(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	СписокСправочников = Новый СписокЗначений;
	СписокСправочников.Добавить("ВидыНоменклатуры");
	
	ДлительнаяОперация = НачатьЗаменуСтавкиНаСервере(СписокСправочников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаменаСтавкиЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВНоменклатуре(Команда)
	
	Если Не ПроверитьЗаполнениеФормы(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	СписокСправочников = Новый СписокЗначений;
	СписокСправочников.Добавить("Номенклатура");
	
	ДлительнаяОперация = НачатьЗаменуСтавкиНаСервере(СписокСправочников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаменаСтавкиЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаменитьВДоговорахСКонтрагентами(Команда) 
	
	Если Не ПроверитьЗаполнениеФормы(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	СписокСправочников = Новый СписокЗначений;
	СписокСправочников.Добавить("ДоговорыКонтрагентов");
	
	ДлительнаяОперация = НачатьЗаменуСтавкиНаСервере(СписокСправочников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаменаСтавкиЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьВДоговорахМеждуОрганизациями(Команда)
	
	Если Не ПроверитьЗаполнениеФормы(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	СписокСправочников = Новый СписокЗначений;
	СписокСправочников.Добавить("ДоговорыМеждуОрганизациями");
	
	ДлительнаяОперация = НачатьЗаменуСтавкиНаСервере(СписокСправочников);

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаменаСтавкиЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокДоговорыАренды(Команда)
	
	//++ НЕ УТ
	ОткрытьФорму("Документ.ИзменениеУсловийДоговораАренды.Форма.ФормаСписка");
	//-- НЕ УТ
	
	Возврат ; // В УТ не используется
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокДокументы(Команда)
	
	//++ НЕ УТ
	ОткрытьФорму("Документ.ИзменениеУсловийПередачиВАренду.Форма.ФормаСписка");
	//-- НЕ УТ
	
	Возврат ; // В УТ не используется
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НачатьОбновлениеФормы()

	ДлительнаяОперация = НачатьОбновлениеФормыНаСервере();

	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновлениеФормыЗавершение", ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция НачатьОбновлениеФормыНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	ПараметрыПолученияДанных = Обработки.ЗаменаСтавокНДС.ПараметрыПолученияДанных();
	ПараметрыПолученияДанных.ТекущаяСтавка = ТекущаяСтавка;
	ПараметрыПолученияДанных.НоваяСтавка   = НоваяСтавка;
	ПараметрыПолученияДанных.Период        = Период;
	ПараметрыПолученияДанных.Страна        = СтранаРегистрации;
	
	ИмяФункции = "Обработки.ЗаменаСтавокНДС.ПолучениеДанныхПоСтавкахНДС";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		ИмяФункции, ПараметрыПолученияДанных);
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	ДанныеСправочниковПоСтавкам = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	УдалитьИзВременногоХранилища(Результат.АдресРезультата);
	
	СтрокаШаблона             = "%4 %1 - <a href = ""%2""> %3 </a>";
	СтрокаШаблонаПервойСтроки = "%1 - <a href = ""%2""> %3 </a>";
	Разделитель = ","; 
	ВыводитьКнопкуИзменитьВсе = Ложь;
	
	Для Каждого ТипСправочника Из ДанныеСправочниковПоСтавкам Цикл
		
		ЭлементФормыСправочника = ЭлементыФормыСправочников.Получить(ТипСправочника.Ключ);
		ЭлементФормыСправочника[0] = Истина;
		Массив = Новый Массив();
		
		Если ЗначениеЗаполнено(ТипСправочника.Значение) Тогда
			
			ЭлементФормыСправочника[2].Видимость = Истина;
			ВыводитьКнопкуИзменитьВсе = Истина;
			ПерваяСтрока = Истина;
			Для Каждого СтавкаНДС Из ТипСправочника.Значение Цикл
				Если ЗначениеЗаполнено(СтавкаНДС.СтавкаНДС) Тогда
					Если СтавкаНДС.СтавкаНДС = ТекущаяСтавка Тогда
						
						ЭлементФормыСправочника[0] = Ложь;
						Если ЭлементФормыСправочника[1] <> Неопределено Тогда
							ЭлементЗаменитьСтавку = ЭлементФормыСправочника[1]; // ПолеФормы
							ЭлементЗаменитьСтавку.Заголовок = НСтр("ru = 'Заменить ставку';
																	|en = 'Replace the rate'") + " (" + СтавкаНДС.Количество+")";
						КонецЕсли;
						
					КонецЕсли;
					СтрокаСДаннымиПоставке = СтроковыеФункцииКлиент.ФорматированнаяСтрока(?(ПерваяСтрока, СтрокаШаблонаПервойСтроки, СтрокаШаблона),
																		Строка(СтавкаНДС.СтавкаНДС), СтавкаНДС.УИД, СтавкаНДС.Количество, Разделитель);
					
					Массив.Добавить(СтрокаСДаннымиПоставке);
					ПерваяСтрока = Ложь;
					
				КонецЕсли;
			КонецЦикла;
			ЭлементЗаменитьСтавку = ЭлементФормыСправочника[3]; // ПолеФормы
			ЭлементЗаменитьСтавку.Заголовок = Новый ФорматированнаяСтрока(Массив);
		Иначе
			ЭлементФормыСправочника[2].Видимость = Ложь;
		КонецЕсли;

	КонецЦикла;

	Элементы.ФормаЗаменитьВезде.Доступность = ВыводитьКнопкуИзменитьВсе;
	УстановитьВидимость(ДанныеСправочниковПоСтавкам);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменаСтавкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	НачатьОбновлениеФормы();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеНоменклатураОбработкаНавигационнойСсылкиНаСервере(УИД)
	
	Возврат Справочники.СтавкиНДС.ПолучитьСсылку(Новый УникальныйИдентификатор(УИД));
	
КонецФункции

&НаКлиенте
Процедура УстановитьВидимость(АнализируемыеСправочники) 
	
	Для Каждого Справочник Из АнализируемыеСправочники Цикл
		ЭлементФормыСправочника = ЭлементыФормыСправочников.Получить(Справочник.Ключ);
		ЭлементФормыСправочника[4].Видимость = Не ЭлементФормыСправочника[0];
		ЭлементФормыСправочника[5].Видимость = ЭлементФормыСправочника[0];
		Если ЭлементФормыСправочника[1] <> Неопределено Тогда
			ЭлементФормыСправочника[1].Доступность = Не ЭлементФормыСправочника[0];
			Если ЭлементФормыСправочника[0] Тогда
				ЭлементЗаменитьСтавку = ЭлементФормыСправочника[1]; // ПолеФормы
				ЭлементЗаменитьСтавку.Заголовок = НСтр("ru = 'Заменить ставку';
														|en = 'Replace the rate'") + " (" + 0 +")";
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьФормуСправочника(УИД, ИмяФормы, ОтборПоПериоду = Ложь)
	
	СтавкаНДС = ДанныеНоменклатураОбработкаНавигационнойСсылкиНаСервере(УИД);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("СтавкаНДС", СтавкаНДС);
	Если ОтборПоПериоду Тогда
		ПараметрыФормы.Вставить("Статус", ПредопределенноеЗначение("Перечисление.СтатусыДоговоровКонтрагентов.Закрыт"));
		ПараметрыФормы.Вставить("ВидСравненияСтатуса", ВидСравненияКомпоновкиДанных.НеРавно);
		ДоступныеПериоды = Новый СписокЗначений();
		ДоступныеПериоды.Добавить(Дата(1,1,1));
		ДоступныеПериоды.Добавить(Период);
		ПараметрыФормы.Вставить("ДатаОкончанияДействия", ДоступныеПериоды);
		ПараметрыФормы.Вставить("ВидСравненияДаты", ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСправочникаДоговорыАренды(УИД)
	
	// ++ НЕ УТ
	СписокСсылок = СписокДоговоровАренды(УИД, Период);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Ссылки", СписокСсылок);
	
	ОткрытьФорму("Справочник.ДоговорыАренды.Форма.ФормаСписка", ПараметрыФормы);
	// -- НЕ УТ
	
КонецПроцедуры

// ++ НЕ УТ
&НаСервереБезКонтекста
Функция СписокДоговоровАренды(УИД, Период)
	
	СтавкаНДС = ДанныеНоменклатураОбработкаНавигационнойСсылкиНаСервере(УИД);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Период",    Период);
	Запрос.УстановитьПараметр("СтавкаНДС", СтавкаНДС);
	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	УсловияДоговоровАрендыСрезПоследних.Договор КАК Договор
	|ИЗ
	|	РегистрСведений.УсловияДоговоровАренды.СрезПоследних(&Период, ) КАК УсловияДоговоровАрендыСрезПоследних
	|ГДЕ
	|	УсловияДоговоровАрендыСрезПоследних.СтавкаНДС = &СтавкаНДС
	|	И УсловияДоговоровАрендыСрезПоследних.Состояние <> ЗНАЧЕНИЕ(Перечисление.СостоянияДоговоровКонтрагентов.Закрыт)";
	
	Результат = Запрос.Выполнить().Выбрать();
	Ссылки = Новый СписокЗначений();
	
	Пока Результат.Следующий() Цикл
		Ссылки.Добавить(Результат.Договор);
	КонецЦикла;
		
	Возврат Ссылки;
	
КонецФункции
// -- НЕ УТ

&НаСервере
Функция НачатьЗаменуСтавкиНаСервере(СписокСправочников)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	
	ПараметрыПолученияДанных = Обработки.ЗаменаСтавокНДС.ПараметрыПолученияДанных();
	ПараметрыПолученияДанных.ТекущаяСтавка      = ТекущаяСтавка;
	ПараметрыПолученияДанных.НоваяСтавка        = НоваяСтавка;
	ПараметрыПолученияДанных.Период             = Период;
	ПараметрыПолученияДанных.Страна             = СтранаРегистрации;
	ПараметрыПолученияДанных.СписокСправочников = СписокСправочников;
	
	ИмяФункции = "УчетНДСУП.ЗаменитьСтавкуНДС";
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения,
		ИмяФункции, ПараметрыПолученияДанных);
	
КонецФункции

&НаКлиенте
Функция ПроверитьЗаполнениеФормы(ПроверятьПериод)
	
	ОчиститьСообщения();
	
	ФормаЗаполнена = Истина;
	
	Если Не ЗначениеЗаполнено(ТекущаяСтавка) Тогда
		ФормаЗаполнена = Ложь;
		ТекстОшибки = НСтр("ru = 'Поле ""Ставка"" не заполнено';
							|en = 'The ""Rate"" field is required'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки,, "ТекущаяСтавка");
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НоваяСтавка) Тогда
		ФормаЗаполнена = Ложь;
		ТекстОшибки = НСтр("ru = 'Поле ""Новая ставка"" не заполнено';
							|en = 'The ""New rate"" field is required'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки,, "НоваяСтавка");
	КонецЕсли;
	
	Если ПроверятьПериод И Не ЗначениеЗаполнено(Период) Тогда
		ФормаЗаполнена = Ложь;
		ТекстОшибки = НСтр("ru = 'Поле ""Период"" не заполнено';
							|en = 'Period is not filled in'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ТекстОшибки,, "Период");

	КонецЕсли;
	
	Возврат ФормаЗаполнена;
	
КонецФункции

&НаКлиенте
Функция СоответствиеЭлементовФормы()
	
	// Для устаранения ошибок перевода
	ЭлементыФормыСправочников = Новый Соответствие();
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(УспешноВидыНоменклатуры);
	МассивЭлементов.Добавить(Элементы.ЗаменитьВидыНоменклатуры);
	МассивЭлементов.Добавить(Элементы.ГруппаВидыНоменклатуры);
	МассивЭлементов.Добавить(Элементы.ДанныеВидыНоменклатуры);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокВидыНоменклатуры);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокУспешноВидыНоменклатуры);
	
	ЭлементыФормыСправочников.Вставить("ВидыНоменклатуры", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(УспешноНоменклатура);
	МассивЭлементов.Добавить(Элементы.ЗаменитьНоменклатура);
	МассивЭлементов.Добавить(Элементы.ГруппаНоменклатура);
	МассивЭлементов.Добавить(Элементы.ДанныеНоменклатура);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокНоменклатура);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокУспешноНоменклатура);
	
	ЭлементыФормыСправочников.Вставить("Номенклатура", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(УспешноДоговорыКонтрагентов);
	МассивЭлементов.Добавить(Элементы.ЗаменитьДоговорыКонтрагентов);
	МассивЭлементов.Добавить(Элементы.ГруппаДоговорыКонтрагентов);
	МассивЭлементов.Добавить(Элементы.ДанныеДоговорыКонтрагентов);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокДоговорыКонтрагентов);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокУспешноДоговорыКонтрагентов);
	
	ЭлементыФормыСправочников.Вставить("ДоговорыКонтрагентов", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(УспешноДоговорыМеждуОрганизациями);
	МассивЭлементов.Добавить(Элементы.ЗаменитьДоговорыМеждуОрганизациями);
	МассивЭлементов.Добавить(Элементы.ГруппаДоговорыМеждуОрганизациями);
	МассивЭлементов.Добавить(Элементы.ДанныеДоговорыМеждуОрганизациями);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокДоговорыМеждуОрганизациями);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокУспешноДоговорыМеждуОрганизациями);
	
	ЭлементыФормыСправочников.Вставить("ДоговорыМеждуОрганизациями", МассивЭлементов);
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(УспешноДоговорыАренды);
	МассивЭлементов.Добавить(Неопределено);
	МассивЭлементов.Добавить(Элементы.ГруппаДоговорыАренды);
	МассивЭлементов.Добавить(Элементы.ДанныеДоговорыАренды);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокДоговорыАренды);
	МассивЭлементов.Добавить(Элементы.ДекорацияЗаменаСтавокУспешноДоговорыАренды);
	
	ЭлементыФормыСправочников.Вставить("ДоговорыАренды", МассивЭлементов);
		
	Возврат ЭлементыФормыСправочников;
	
КонецФункции

#КонецОбласти
