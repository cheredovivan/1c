
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	СтруктураРеквизитовФормы = ПолучитьИзВременногоХранилища(Параметры.СтруктураРеквизитовФормы);
	мСохраненныйДок = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураРеквизитовФормы, "мСохраненныйДок");
	Если Не ЗначениеЗаполнено(мСохраненныйДок) Тогда 
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(мСохраненныйДок,
		"Организация,ДатаНачала,ДатаОкончания,ИсточникОтчета,Ссылка,Периодичность,ВыбраннаяФорма,Вид,НаименованиеОтчета");
	РеквизитыДокумента.Вставить("ИмяФормыОтчета", Параметры.ИмяФормыОтчета); 
	
	ТекстЗаголовок = "Форма не является актуальной<br>";
	МенеджерОтчета = ?(СтрНачинаетсяС(НРег(РеквизитыДокумента.ИмяФормыОтчета), "внешний"),
		РегламентированнаяОтчетностьВызовСервера.ОбъектОтчета(РеквизитыДокумента.ИмяФормыОтчета), Отчеты[РеквизитыДокумента.ИсточникОтчета]);
	ТаблицаФормОтчета = МенеджерОтчета.ТаблицаФормОтчета();
	Для Каждого Стр Из ТаблицаФормОтчета.НайтиСтроки(Новый Структура("ФормаОтчета", РеквизитыДокумента.ВыбраннаяФорма)) Цикл 
		ТекстЗаголовок = "Форма в редакции " + ПреобразованиеПоляРедакция(Стр.РедакцияФормы) + " не является актуальной<br>";
		Прервать;
	КонецЦикла;
	
	СписокАктуальныхРедакций = "";
	Для Каждого Стр Из ТаблицаФормОтчета Цикл 
		Если НачалоДня(Стр.ДатаНачалоДействия) <= РеквизитыДокумента.ДатаНачала 
			И (КонецДня(Стр.ДатаКонецДействия) >= РеквизитыДокумента.ДатаОкончания Или Не ЗначениеЗаполнено(Стр.ДатаКонецДействия)) Тогда 
			
			СписокАктуальныхРедакций = СписокАктуальныхРедакций + "<br>" + ПреобразованиеПоляРедакция(Стр.РедакцияФормы);
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(СписокАктуальныхРедакций) Тогда 
		ТекстЗаголовок = ТекстЗаголовок + "Список актуальных редакций:" + СписокАктуальныхРедакций;
	КонецЕсли;
	
	ТекстHTML = "<!DOCTYPE html PUBLIC ""-//W3C//DTD HTML 4.0 Transitional//EN""><html><head><meta" 
		+ " content=""text/html; charset=utf-8"" http-equiv=""Content-Type""></meta><link rel=""stylesheet"" type=""text/css"" "
		+ "href=""__STYLE__""></link><meta name=""GENERATOR"" content=""MSHTML 9.00.8112.16553""></meta><base "
		+ "href=""v8config://v8cfgHelp/mdobject/idcd45707e-7c21-43b8-a9fe-e51edf46aa9b/8eb4fad1-1fa6-403e-970f-2c12dbb43e23"">"
		+ "</base></head><body scroll=""auto"">";
	ТекстHTML = ТекстHTML + "<p align=""left""><font size=""2"" face=""Microsoft Sans Serif""><strong><img "
		+ "align=""left"" src=""" + АдресКартинки(Ложь) + """ "
		+ "width=""48"" height=""48""><font color=""#ff0000"" face=""Verdana"">"
		+ ТекстЗаголовок + "</font></strong></font></p><font size=""2"" face=""Verdana""><br>";
	ТекстHTML = ТекстHTML + "<p align=""left"" ><font size=""2"" face=""Microsoft Sans Serif"">"
			+ "Можно <a href=""ПродолжитьРаботу"">продолжить работать</a> с текущим отчетом";
			
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураРеквизитовФормы, "ПредлагатьКонвертацию", Ложь) Тогда
		ТекстHTML = ТекстHTML + ", либо<a href=""СконвертироватьОтчет""> сконвертировать отчет</a> до актуальной редакции<br></p>";
	Иначе
		ТекстHTML = ТекстHTML + "<br></p>";
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураРеквизитовФормы, "ЕстьАктуальныеФормы", Ложь) Тогда 
		АктуальныеФормы = Новый Массив;
		Для Каждого Элт Из ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(СтруктураРеквизитовФормы, "АктуальныеФормы", Новый Массив) Цикл 
			АктуальныеФормы.Добавить(Элт.ФормаОтчета);
		КонецЦикла;
		Запрос = РегламентированнаяОтчетность.ЗапросАктуальныхФорм(РеквизитыДокумента, АктуальныеФормы);
		РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		Если ЗначениеЗаполнено(РезультатЗапроса) Тогда 
			ТекстHTML = ТекстHTML + "<p align=""left""><font color=""#0000d8"" size=""2"" face=""Verdana"">" 
				+ "<strong>Сохраненные отчеты актуальной редакции</strong></font><br></p>";
			ТекстHTML = ТекстHTML + "<p align=""left""><font size=""2"" face=""Microsoft Sans Serif"">";
			Для Каждого Стр Из РезультатЗапроса Цикл
				ТекстHTML = ТекстHTML + "";
				ТекстHTML = ТекстHTML + "<a href=""" 
					+ ПолучитьНавигационнуюСсылку(Стр.Ссылка) + """>" + Строка(Стр.Ссылка) + "</a><br>";
			КонецЦикла;
			ТекстHTML = ТекстHTML + "</p>";
		КонецЕсли;
	КонецЕсли;
	ТекстHTML = ТекстHTML + "</body></html>";
КонецПроцедуры

&НаСервере
Функция ПреобразованиеПоляРедакция(Редакция)
	Возврат ?(СтрЗаканчиваетсяНа(Редакция, "."), Лев(Редакция, СтрДлина(Редакция) - 1), Редакция);
КонецФункции

&НаКлиенте
Процедура ПолеHTMLДокументаИнформацияПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Href = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСобытия, "Href");
	Если ЗначениеЗаполнено(Href) Тогда
		СтандартнаяОбработка = Ложь;
		Если ЗначениеЗаполнено(СтрНайти(Href, "e1cib/data/Документ.РегламентированныйОтчет")) Тогда 
			ПерейтиПоНавигационнойСсылке(Сред(Href, СтрНайти(Href, "e1cib/")));
			ВладелецФормы.Закрыть();
		ИначеЕсли ЗначениеЗаполнено(СтрНайти(Href, "СконвертироватьОтчет")) Тогда 
			Конвертировать();
		ИначеЕсли ЗначениеЗаполнено(СтрНайти(Href, "ПродолжитьРаботу")) Тогда 
			Закрыть();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Процедура Конвертировать()
	Ждать ПредупреждениеАсинх("Будет открыта форма актуальной редакции." + Символы.ПС 
		+ "После открытия необходимо дождаться завершения выполнения конвертации данных и работать с новой формой");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонвертацияИзФормыОнлайнБлокировок", Истина);
	ПараметрыФормы.Вставить("мДатаНачалаПериодаОтчета", НачалоДня(РеквизитыДокумента.ДатаНачала));
	ПараметрыФормы.Вставить("мСкопированаФорма", РеквизитыДокумента.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("мДатаКонцаПериодаОтчета", КонецДня(РеквизитыДокумента.ДатаОкончания));
	ПараметрыФормы.Вставить("мПериодичность", РеквизитыДокумента.Периодичность);
	ПараметрыФормы.Вставить("Организация", РеквизитыДокумента.Организация);
	ПараметрыФормы.Вставить("мВыбраннаяФорма", РеквизитыДокумента.ВыбраннаяФорма);
	ПараметрыФормы.Вставить("ВидДокумента", РеквизитыДокумента.Вид);
	ПараметрыФормы.Вставить("ПредставлениеВидаОтчета", РеквизитыДокумента.НаименованиеОтчета);
	ПараметрыФормы.Вставить("мСохраненныйДок", РеквизитыДокумента.Ссылка);
	ПараметрыФормы.Вставить("ЗначениеКопирования", РеквизитыДокумента.Ссылка);
	ПараметрыФормы.Вставить("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417",
		РегламентированнаяОтчетностьКлиент.ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417());
		
	ПрефиксИмени = ?(СтрНачинаетсяС(РеквизитыДокумента.ИмяФормыОтчета, "Внешний"), "Внешний", "");
	ОткрытьФорму(ПрефиксИмени + "Отчет." + РеквизитыДокумента.ИсточникОтчета + ".ФормаОбъекта", ПараметрыФормы, ЭтотОбъект,,,,
		Новый ОписаниеОповещения("КнопкаКонвертироватьЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура КнопкаКонвертироватьЗавершение(Результат, Парам) Экспорт 
	Если Результат = "КонвертацияИзФормыОнлайнБлокировок" Тогда 
		ВладелецФормы.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция АдресКартинки(ЗапретНаПродолжение)
	Возврат ?(ЗапретНаПродолжение, 
		"../../mdpicture/idd9859dd1-911d-4c5b-8ab4-868b1b1640f7/00000000-0000-0000-0000-000000000000",
		"../../mdpicture/id5e4a21d9-e796-40fa-9832-fbc2c352020b/00000000-0000-0000-0000-000000000000");
КонецФункции
