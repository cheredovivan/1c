#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "Показатель, НаименованиеПоказателя, ГруппыПоказателей, ЗачетПоказателей");
	
	Если ЗачетПоказателей = "Показатель" Тогда
		ТекущийЭлемент = Элементы.ЗачетПоказателейПоказатель;
	ИначеЕсли ЗачетПоказателей = "СтатьиОбъекты" Тогда
		ТекущийЭлемент = Элементы.ЗачетПоказателейСтатьиОбъекты;
	КонецЕсли;
	
	// Презюмируем, что доходы в отчете предшествуют расходам
	Для Каждого ГруппаПоказателя Из ГруппыПоказателей Цикл
		
		НомерСтроки = ГруппыПоказателей.Индекс(ГруппаПоказателя) + 1; // Используем номер, а не индекс или идентификатор, чтобы значение по умолчанию заведомо соответствовало не заполненному
		
		Элементы.ГруппаДоходы.СписокВыбора.Добавить(НомерСтроки,  ГруппаПоказателя.Представление);
		Элементы.ГруппаРасходы.СписокВыбора.Добавить(НомерСтроки, ГруппаПоказателя.Представление);
		
		Если ГруппаПоказателя.Пометка Тогда
			
			Если ГруппаДоходы = 0 Тогда
				ГруппаДоходы = НомерСтроки;
			ИначеЕсли ГруппаРасходы = 0 Тогда
				ГруппаРасходы = НомерСтроки;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ГруппыПоказателей) Тогда
		Возврат;
	КонецЕсли;
	
	Если ГруппаРасходы = 0 Тогда
		ГруппаРасходы = ГруппаДоходы;
	КонецЕсли;
	
	НастроитьГруппыДоходовРасходов(Элементы.ГруппаДоходы, Элементы.ГруппаРасходы, Элементы.ГруппыДоходовРасходов, ЗачетПоказателей);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗачетПоказателейПриИзменении(Элемент)
	
	НастроитьГруппыДоходовРасходов(Элементы.ГруппаДоходы, Элементы.ГруппаРасходы, Элементы.ГруппыДоходовРасходов, ЗачетПоказателей);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Если Не ПроверитьРезультат() Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ЗачетПоказателей", ЗачетПоказателей);
	Результат.Вставить("Группы",           Новый Соответствие);
	
	Если ГруппаДоходы > 0 Тогда
		Результат.Группы.Вставить(ГруппыПоказателей[ГруппаДоходы - 1].Значение, Истина);
	КонецЕсли;
	
	Если ГруппаРасходы > 0 Тогда
		Результат.Группы.Вставить(ГруппыПоказателей[ГруппаРасходы - 1].Значение, Истина);
	КонецЕсли;
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПроверитьРезультат()
	
	Ошибка = "";
	
	Если ЗачетПоказателей <> "Показатель" Тогда
		
		ОшибкаЗаполненыГруппы = Новый Массив;
		Если ГруппаДоходы = 0 И ГруппаРасходы = 0 Тогда
			ОшибкаЗаполненыГруппы.Добавить(НСтр("ru = 'Не заполнены группы доходов и расходов.';
												|en = 'Income and expense groups are not filled.'"));
		ИначеЕсли ГруппаДоходы = 0 Тогда
			ОшибкаЗаполненыГруппы.Добавить(НСтр("ru = 'Не заполнена группа доходов.';
												|en = 'The income group is not filled.'"));
		ИначеЕсли ГруппаРасходы = 0 Тогда
			ОшибкаЗаполненыГруппы.Добавить(НСтр("ru = 'Не заполнена группа расходов.';
												|en = 'The expense group is not filled.'"));
		ИначеЕсли ГруппаДоходы = ГруппаРасходы Тогда
			ОшибкаЗаполненыГруппы.Добавить(НСтр("ru = 'Указана одна и та же группа для доходов и расходов.';
												|en = 'The same group is specified for income and expenses.'"));
		КонецЕсли;
		Если ЗначениеЗаполнено(ОшибкаЗаполненыГруппы) Тогда
			ОшибкаЗаполненыГруппы.Добавить(
				НСтр("ru = 'Если доходы и расходы не зачитываются по показателю в целом, то следует указать группу, в которую будет включена строка с доходами и группу, в которую будет включена строка с расходами.';
					|en = 'If income and expenses are not offset under the whole indicator, specify the group to which the income row will be included and the group to which the expense row will be included.'"));
        	Ошибка = СтрСоединить(ОшибкаЗаполненыГруппы, Символы.ПС + Символы.ПС);
		КонецЕсли;
			
	КонецЕсли;
	
	Если ПустаяСтрока(Ошибка) Тогда
		
		Если ГруппаДоходы > ГруппаРасходы Тогда
			ДопустимыеГруппыРасходов = НСтр("ru = 'одну из групп, следующих за группой доходов';
											|en = 'one of the groups following the income group'");
			Если ЗачетПоказателей = "Показатель" Тогда
				ДопустимыеГруппыРасходов = НСтр("ru = 'одну из групп, следующих за группой доходов, или совпадающую с группой доходов';
												|en = 'one of the groups following the income group, or coinciding with the income group'");
			КонецЕсли;
			Ошибка = СтрШаблон(НСтр("ru = 'Группы доходов и расходов указаны в неверном порядке.
                          |
                          |В качестве группы расходов следует указать %1.';
                          |en = 'Income and expense groups are specified in the wrong order.
                          |
                          |%1 must be specified as the expense group.'"), ДопустимыеГруппыРасходов);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(Ошибка) Тогда
		
		ПоказатьПредупреждение(, Ошибка);
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьГруппыДоходовРасходов(ГруппаДоходы, ГруппаРасходы, ГруппыДоходовРасходов, ЗачетПоказателей)
	
	Перем ОписаниеЗачета, ШаблонПодсказки;
	
	Если ЗачетПоказателей = "Показатель" Тогда
		
		ГруппаДоходы.Заголовок  = НСтр("ru = 'Группа с доходом';
										|en = 'Group with income'");
		ГруппаРасходы.Заголовок = НСтр("ru = 'Группа с расходом';
										|en = 'Group with expense'");
		ГруппыДоходовРасходов.Подсказка = НСтр("ru = 'Когда доходы и расходы зачитываются по показателю в целом, то в конкретном отчете может быть отражен либо только доход, либо только расход.
                                               |Разные группы может быть уместным указать на случай, если в разных отчетах (например, за разные периоды) может быть когда-то доход, а когда-то расход.';
                                               |en = 'When income and expenses are offset under the whole indicator, either only income or only expense can be displayed in a specific report.
                                               |It may be appropriate to specify different groups in case there may be sometimes income and sometimes expense in different reports (for example, for different periods).'");
		
		ГруппаДоходы.АвтоотметкаНезаполненного = Ложь;
		ГруппаДоходы.ОтметкаНезаполненного = Ложь;
		
		ГруппаРасходы.АвтоотметкаНезаполненного = Ложь;
		ГруппаРасходы.ОтметкаНезаполненного = Ложь;
		
	Иначе
		
		ГруппаДоходы.Заголовок  = НСтр("ru = 'Группа с доходами';
										|en = 'Group with income'");
		ГруппаРасходы.Заголовок = НСтр("ru = 'Группа с расходами';
										|en = 'Group with expenses'");
		
		ШаблонПодсказки = НСтр("ru = 'Так как %1, то в отчете могут быть отражены одновременно и доходы и расходы: в одной строке доходы, в другой - расходы.
                                |Доходы и расходы указываются в строках разных групп.';
                                |en = 'Since it is %1, both income and expenses can be recorded in the report at the same time: income in one row, and expenses in another row.
                                |Income and expenses are shown in rows of different groups.'");
		
		ОписаниеЗачета = НСтр("ru = 'не все доходы и расходы по показателю зачитываются';
								|en = 'not all incomes and expenses under the indicator are offset'");
		
		Если ЗачетПоказателей = "Нет" Тогда
			ОписаниеЗачета = НСтр("ru = 'доходы и расходы не зачитываются';
									|en = 'income and expenses are not offset'");
		КонецЕсли;
		ГруппыДоходовРасходов.Подсказка = СтрШаблон(ШаблонПодсказки, ОписаниеЗачета);
		
		ГруппаДоходы.АвтоотметкаНезаполненного = Истина;
		ГруппаРасходы.АвтоотметкаНезаполненного = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

