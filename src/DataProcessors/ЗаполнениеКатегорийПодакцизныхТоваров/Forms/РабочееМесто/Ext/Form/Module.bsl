#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	Период = ТекущаяДатаСеанса;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Страна", Страна);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Период", ?(Не ЗначениеЗаполнено(Период), ТекущаяДатаСеанса, КонецДня(Период)));
		
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, СписокНоменклатуры);
	
	СвойстваСписка.ТекстЗапроса = СтрЗаменить(СписокНоменклатуры.ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"КатегорииПереопределяемый.КатегорияПодакцизногоТовара.ЕдиницаИзмерения",
			"НоменклатураПереопределяемый.Ссылка"));
		
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокНоменклатуры, СвойстваСписка);
	
	ОбновитьИнформациюПоКатегориям();
	ОбновитьСписокКатегорий();
	
	ПодакцизныйТовар = 0;
	МногострановойУчет = ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет");
	
	Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Очистить();
	Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Добавить("<Все>", НСтр("ru = '<Любой>';
																			|en = '<Any>'"));
	Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Добавить("<Указан>", НСтр("ru = '<Указан>';
																				|en = '<Specified>'"));
	Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Добавить("<Не указан>", НСтр("ru = '<Не указан>';
																				|en = '<not specified>'"));
	Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Добавить("<Выбрать>", НСтр("ru = '<Указать коэффициент...>';
																				|en = '<Specify the ratio...>'"));
	КоэффициентПересчетаОтбор = "<Все>";
	КоэффициентПересчетаОтборДо = КоэффициентПересчетаОтбор;
	
	УстановитьОтборПоПризнаку();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Страна", Страна);
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Номенклатура" Тогда
		ОбновитьИнформациюПоКатегориям();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		Период = ТекущаяДатаСеанса;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Период", КонецДня(Период));
	ОбновитьИнформациюПоКатегориям();
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		СписокНоменклатуры, "Страна", Страна);
	ОбновитьИнформациюПоКатегориям();
	ОбновитьСписокКатегорий();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодакцизныйТоварПриИзменении(Элемент)
	
	УстановитьОтборПоПризнаку();
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияОтбораПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(КатегорияОтбора) Тогда
		КатегорияОтбора = 0;
	КонецЕсли;
	
	Если КатегорияОтбора = 3 Тогда
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Страна", Страна);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора);
		ПараметрыФормы.Вставить("РежимВыбора", Истина);
		ПараметрыФормы.Вставить("МножественныйВыбор", Ложь);
		
		ОткрытьФорму("Справочник.КатегорииПодакцизныхТоваров.ФормаВыбора", ПараметрыФормы,
			ЭтотОбъект, , , ,Новый ОписаниеОповещения("ВыборПодакцизнойКатегорииЗавершение", ЭтотОбъект));
	Иначе
		УстановитьОтборПоПризнаку();
		КатегорияОтбораДо = КатегорияОтбора;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КатегорияНазначенияПриИзменении(Элемент)
	
	УстановитьТипЕдиницыИзмеренияАкциза();
	
КонецПроцедуры

&НаКлиенте
Процедура КоэффициентПересчетаОтборПриИзменении(Элемент)
	
	Если ТипЗнч(КоэффициентПересчетаОтбор) = Тип("Строка")
		И КоэффициентПересчетаОтбор = "<Выбрать>" Тогда
		ЗапроситьКоэффициент(Неопределено);
	Иначе
		Если Не ЗначениеЗаполнено(КоэффициентПересчетаОтбор) Тогда
			КоэффициентПересчетаОтбор = "<Все>";
		КонецЕсли;
		УстановитьОтборПоПризнаку();
		КоэффициентПересчетаОтборДо = КоэффициентПересчетаОтбор;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПодакцизнуюКатегориюНоменклатурыПоТекущемуОтбору(Команда)
	
	ОчиститьСообщения();
	Если ПроверитьПараметрыЗаполнения(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПоИзменению = ЗаполнитьДанныеДляИзмененияНоменклатуры(Истина);
	
	Если ДанныеПоИзменению.АдресСписка = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'В списке нет товара.';
										|en = 'There are no goods in the list.'"));
		Возврат;
	КонецЕсли;
	
	ДанныеПоИзменению.Вставить("КатегорияПодакцизногоТовара", КатегорияНазначения);
	ДанныеПоИзменению.Вставить("Период", Период);
	ДанныеПоИзменению.Вставить("Страна", Страна);
	
	Если ДанныеПоИзменению.ЕстьУжеУстановленнаяКатегория Тогда
		
		Если МногострановойУчет Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Нстр("ru = 'Уже есть действующие подакцизные категории. Установить категорию ""%1"" для товара из списка по стране %2 с %3?';
					|en = 'There are already active excise categories. Set the ""%1"" category for the product from the list for the ""%2"" country from %3?'"),
				КатегорияНазначения,
				Страна,
				Формат(Период,"ДЛФ=D"));
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Нстр("ru = 'Уже есть действующие подакцизные категории. Установить категорию ""%1"" для товара из списка с %2?';
					|en = 'There are already active excise categories. Set the ""%1"" category for the item from the list from %2?'"),
				КатегорияНазначения,
				Формат(Период,"ДЛФ=D"));
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьПодакцизнуюКатегориюНоменклатурыЗавершение", ЭтотОбъект, ДанныеПоИзменению),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
			
	Иначе
		УстановитьПодакцизнуюКатегориюНоменклатурыНаКлиенте(ДанныеПоИзменению);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПодакцизнуюКатегориюНоменклатурыПоВыделеннымСтрокам(Команда)
	
	ОчиститьСообщения();
	ВыделенныеСтроки = Элементы.СписокНоменклатуры.ВыделенныеСтроки;

	Если ПроверитьПараметрыЗаполнения(Истина) Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатураДляИзменения = Новый Массив;
	ЕстьУжеУстановленнаяКатегория = Ложь;
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		ТекущаяСтрока = Элементы.СписокНоменклатуры.ДанныеСтроки(Элемент);
		Если ТекущаяСтрока.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТекущаяСтрока.КатегорияПодакцизногоТовара) Тогда
			ЕстьУжеУстановленнаяКатегория = Истина;
		КонецЕсли;
		Если НоменклатураДляИзменения.Найти(ТекущаяСтрока.Ссылка) = Неопределено Тогда
			НоменклатураДляИзменения.Добавить(ТекущаяСтрока.Ссылка);
		КонецЕсли;
	КонецЦикла;

	Если НоменклатураДляИзменения.Количество() = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для продолжения необходимо выделить строки.';
										|en = 'To continue, select lines.'"));
		Возврат;
	КонецЕсли;
	
	ДанныеПоИзменению = Новый Структура();
	ДанныеПоИзменению.Вставить("АдресСписка", Неопределено);
	ДанныеПоИзменению.Вставить("СписокНоменклатуры", НоменклатураДляИзменения);
	ДанныеПоИзменению.Вставить("КатегорияПодакцизногоТовара", КатегорияНазначения);
	ДанныеПоИзменению.Вставить("Период", Период);
	ДанныеПоИзменению.Вставить("Страна", Страна);
	
	Если ЕстьУжеУстановленнаяКатегория Тогда
		
		Если МногострановойУчет Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Нстр("ru = 'Уже есть действующие подакцизные категории. Установить категорию ""%1"" для выделенного товара по стране %2 с %3?';
					|en = 'There are already active excise categories. Set the ""%1"" category for the selected product for the ""%2"" country from %3?'"),
				КатегорияНазначения,
				Страна,
				Формат(Период,"ДЛФ=D"));
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Нстр("ru = 'Уже есть действующие подакцизные категории. Установить категорию ""%1"" для выделенного товара с %2?';
					|en = 'There are already active excise categories. Set the ""%1"" category for the selected item from %2?'"),
				КатегорияНазначения,
				Формат(Период,"ДЛФ=D"));
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("УстановитьПодакцизнуюКатегориюНоменклатурыЗавершение", ЭтотОбъект, ДанныеПоИзменению),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
			
	Иначе
		УстановитьПодакцизнуюКатегориюНоменклатурыНаКлиенте(ДанныеПоИзменению);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКоэффициентПоВыделеннымСтрокам(Команда)
	
	ОчиститьСообщения();
	Если ПроверитьПараметрыЗаполнения(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы.СписокНоменклатуры.ВыделенныеСтроки;
		
	НоменклатураДляИзменения = Новый Массив;
	ЕстьТоварыБезКатегории = Ложь;
	АвтоЗаполнениеКоэффициентаПересчета = Ложь;
	ЗапрашиватьКоэффициент = Ложь; 
	ЕстьРазныеЕдиницыИзмерения = Ложь;
	ЕдиницаИзмеренияНоменклатуры = Неопределено;
	ЕстьАдвалорнаяСтавка = Ложь;
	ЕдиницаИзмеренияКатегории = Неопределено;
	ЕстьРазныеЕдиницыИзмеренияКатегории = Ложь;
	
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		ТекущаяСтрока = Элементы.СписокНоменклатуры.ДанныеСтроки(Элемент);
		Если ТекущаяСтрока.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.КатегорияПодакцизногоТовара) Тогда
			ЕстьТоварыБезКатегории = Истина;
			Прервать;
		КонецЕсли;
		Если Не ТекущаяСтрока.ЭтоТвердаяСтавка Тогда
			ЕстьАдвалорнаяСтавка = Истина;
			Прервать;
		КонецЕсли;
		
		Если ЕдиницаИзмеренияКатегории = Неопределено Тогда
			ЕдиницаИзмеренияКатегории = ТекущаяСтрока.КатегорияЕдиницаИзмерения;
		ИначеЕсли ЕдиницаИзмеренияКатегории <> ТекущаяСтрока.КатегорияЕдиницаИзмерения Тогда
			ЕстьРазныеЕдиницыИзмеренияКатегории = Истина;
			Прервать;
		КонецЕсли;
		
		Если ТекущаяСтрока.АвтозаполнениеКоэффициентаПересчета Тогда
		    АвтоЗаполнениеКоэффициентаПересчета = Истина;
		Иначе
			ЗапрашиватьКоэффициент = Истина;
		КонецЕсли;
		Если ЕдиницаИзмеренияНоменклатуры = Неопределено Тогда
			ЕдиницаИзмеренияНоменклатуры = ТекущаяСтрока.ЕдиницаИзмерения;
		КонецЕсли;
		Если Не ЕстьРазныеЕдиницыИзмерения
			И ТекущаяСтрока.ЕдиницаИзмерения <> ЕдиницаИзмеренияНоменклатуры Тогда
			ЕстьРазныеЕдиницыИзмерения = Истина;
		КонецЕсли;
		НоменклатураДляИзменения.Добавить(Новый Структура("Номенклатура, КатегорияПодакцизногоТовара, АвтоКоэффициентПересчета",
			ТекущаяСтрока.Ссылка, ТекущаяСтрока.КатегорияПодакцизногоТовара, ТекущаяСтрока.АвтоКоэффициентПересчета));
	КонецЦикла;

	ПараметрыЗаполненияКоэффициента = Новый Структура;
	ПараметрыЗаполненияКоэффициента.Вставить("АдресСписка", Неопределено);
	ПараметрыЗаполненияКоэффициента.Вставить("Период", Период);
	ПараметрыЗаполненияКоэффициента.Вставить("Страна", Страна);	
	ПараметрыЗаполненияКоэффициента.Вставить("СписокНоменклатуры", НоменклатураДляИзменения);
	ПараметрыЗаполненияКоэффициента.Вставить("АвтоЗаполнениеКоэффициентаПересчета", АвтоЗаполнениеКоэффициентаПересчета);
	ПараметрыЗаполненияКоэффициента.Вставить("ЗапрашиватьКоэффициент", ЗапрашиватьКоэффициент);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕстьРазныеЕдиницыИзмерения", ЕстьРазныеЕдиницыИзмерения);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АвтоЗаполнениеКоэффициентаПересчета", АвтоЗаполнениеКоэффициентаПересчета);
	ПараметрыОткрытия.Вставить("ЗапрашиватьКоэффициент", ЗапрашиватьКоэффициент);
	ПараметрыОткрытия.Вставить("ЕстьРазныеЕдиницыИзмерения", ЕстьРазныеЕдиницыИзмерения);
	ПараметрыОткрытия.Вставить("ЕдиницаИзмеренияКатегории", ЕдиницаИзмеренияКатегории);
	
	Если Не ВывестиПредупреждениеПриУстановкеКоэффициента(ЕстьТоварыБезКатегории,
			ЕстьАдвалорнаяСтавка,
			АвтоЗаполнениеКоэффициентаПересчета И ЗапрашиватьКоэффициент,
			ЕстьРазныеЕдиницыИзмеренияКатегории,
			НоменклатураДляИзменения.Количество() = 0) Тогда
		ОткрытьФорму("РегистрСведений.КатегорииПодакцизныхТоваров.Форма.ПараметрыУстановкиКоэффициента", ПараметрыОткрытия,,,,,
			Новый ОписаниеОповещения("УстановкаКоэффициентаЗавершение", ЭтотОбъект, ПараметрыЗаполненияКоэффициента),);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКоэффициентПоТекущемуОтбору(Команда)
	
	ОчиститьСообщения();	
	Если ПроверитьПараметрыЗаполнения(Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполненияКоэффициента = ПолучитьПараметрыДляПересчетаКоэффициента();
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("АвтоЗаполнениеКоэффициентаПересчета", ПараметрыЗаполненияКоэффициента.АвтоЗаполнениеКоэффициентаПересчета);
	ПараметрыОткрытия.Вставить("ЗапрашиватьКоэффициент", ПараметрыЗаполненияКоэффициента.ЗапрашиватьКоэффициент);
	ПараметрыОткрытия.Вставить("ЕстьРазныеЕдиницыИзмерения", ПараметрыЗаполненияКоэффициента.ЕстьРазныеЕдиницыИзмерения);
	ПараметрыОткрытия.Вставить("ЕдиницаИзмеренияКатегории", ПараметрыЗаполненияКоэффициента.ЕдиницаИзмеренияКатегории);
	ПараметрыОткрытия.Вставить("АдресСписка", ПараметрыЗаполненияКоэффициента.АдресСписка);
	
	Если Не ВывестиПредупреждениеПриУстановкеКоэффициента(ПараметрыЗаполненияКоэффициента.ЕстьТоварыБезКатегории,
			ПараметрыЗаполненияКоэффициента.ЕстьАдвалорнаяСтавка,
			ПараметрыЗаполненияКоэффициента.АвтоЗаполнениеКоэффициентаПересчета И ПараметрыЗаполненияКоэффициента.ЗапрашиватьКоэффициент,
			ПараметрыЗаполненияКоэффициента.ЕстьРазныеЕдиницыИзмеренияКатегории,
			ПараметрыЗаполненияКоэффициента.АдресСписка = Неопределено) Тогда
		ОткрытьФорму("РегистрСведений.КатегорииПодакцизныхТоваров.Форма.ПараметрыУстановкиКоэффициента", ПараметрыОткрытия,,,,,
			Новый ОписаниеОповещения("УстановкаКоэффициентаЗавершение", ЭтотОбъект, ПараметрыЗаполненияКоэффициента),);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодакцизныйПризнакПоВыделеннымСтрокам(Команда)
	
	ОчиститьСообщения();
	
	ВыделенныеСтроки = Элементы.СписокНоменклатуры.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для продолжения необходимо выделить строки';
														|en = 'Select lines to continue'"));
		Возврат;
	КонецЕсли;
	
	Если ПроверитьПараметрыЗаполнения(Ложь) Тогда
		Возврат;
	КонецЕсли;

	НоменклатураДляИзменения = Новый Массив;
	Для Каждого Элемент Из ВыделенныеСтроки Цикл
		ТекущаяСтрока = Элементы.СписокНоменклатуры.ДанныеСтроки(Элемент);
		Если ТекущаяСтрока.ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		Если Не ТекущаяСтрока.ПодакцизныйТовар Тогда
			Продолжить;
		КонецЕсли;
		Если НоменклатураДляИзменения.Найти(ТекущаяСтрока.Ссылка) = Неопределено Тогда
			НоменклатураДляИзменения.Добавить(ТекущаяСтрока.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	Если НоменклатураДляИзменения.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для продолжения необходимо выделить строки с подакцизной номенклатурой.';
														|en = 'To continue, select lines with excisable items.'"));
	Иначе
		
		ДанныеДляИзменения = Новый Структура();
		ДанныеДляИзменения.Вставить("СписокНоменклатуры", НоменклатураДляИзменения);
		ДанныеДляИзменения.Вставить("Период", Период);
		ДанныеДляИзменения.Вставить("Страна", Страна);
		ДанныеДляИзменения.Вставить("АдресСписка", Неопределено);
		
		Если МногострановойУчет Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						Нстр("ru = 'Сделать выбранный товар неподакцизным для страны ""%1"" с %2?';
							|en = 'Make the selected product non-excisable for the ""%1"" country from %2?'"),
						Страна,
						Формат(Период,"ДЛФ=D"));
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						Нстр("ru = 'Сделать выбранный товар неподакцизным с %1?';
							|en = 'Make the selected item non-excisable from %1?'"),
						Формат(Период,"ДЛФ=D"));
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьПодакцизныйПризнакНоменклатурыЗавершение", ЭтотОбъект, ДанныеДляИзменения),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодакцизныйПризнакПоТекущемуОтбору(Команда)
	
	Если ПроверитьПараметрыЗаполнения(Ложь) Тогда
		Возврат;
	КонецЕсли;

	ДанныеДляИзменения = ЗаполнитьДанныеДляИзмененияНоменклатуры(Ложь, Истина);
	
	Если ДанныеДляИзменения.АдресСписка <> Неопределено Тогда
		
		ДанныеДляИзменения.Вставить("Период", Период);
		ДанныеДляИзменения.Вставить("Страна", Страна);
		
		Если МногострановойУчет Тогда
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Нстр("ru = 'Сделать весь товар в списке неподакцизным для страны ""%1"" с %2?';
						|en = 'Make all products in the list non-excisable for the ""%1"" country from %2?'"),
					Страна,
					Формат(Период,"ДЛФ=D"));
		Иначе
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					Нстр("ru = 'Сделать весь товар в списке неподакцизным с %1?';
						|en = 'Make all the items in the list non-excisable from %1?'"),
					Формат(Период,"ДЛФ=D"));
		КонецЕсли;
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьПодакцизныйПризнакНоменклатурыЗавершение", ЭтотОбъект, ДанныеДляИзменения),
			ТекстВопроса,
			РежимДиалогаВопрос.ДаНет);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКатегория.Имя);
	
	ГруппаОтбораИли = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбораИли.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
	
	ОтборЭлемента = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ГруппаОтбораИли.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ПодакцизныйТовар");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>';
																|en = '<not required>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	// 
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКатегория.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ПодакцизныйТовар");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.КатегорияПодакцизногоТовара");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не указана>';
																|en = '<not specified>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКоэффициентОбъема.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыОбъемЕдиницаИзмерения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ОбъемИспользовать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКоэффициентВеса.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыВесЕдиницаИзмерения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ВесИспользовать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКоэффициентПересчета.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКатегорияЕдиницаИзмерения.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоТвердаяСтавка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненноеПолеТаблицы);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокНоменклатурыКоэффициентПересчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.ЭтоТвердаяСтавка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("СписокНоменклатуры.КоэффициентПересчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не задан>';
																|en = '<not specified>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОсобогоТекста);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокКатегорий()

	Элементы.КатегорияОтбора.СписокВыбора.Очистить();
	Элементы.КатегорияОтбора.СписокВыбора.Добавить(0, НСтр("ru = '<Любая>';
															|en = '<Any>'"));
	Элементы.КатегорияОтбора.СписокВыбора.Добавить(1, НСтр("ru = '<Указана>';
															|en = '<Specified>'"));
	Элементы.КатегорияОтбора.СписокВыбора.Добавить(2, НСтр("ru = '<Не указана>';
															|en = '<Not specified>'"));

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 10
	|	КатегорииПодакцизныхТоваров.Ссылка КАК Категория
	|ИЗ
	|	Справочник.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|ГДЕ
	|	КатегорииПодакцизныхТоваров.Страна = &Страна
	|	И НЕ КатегорииПодакцизныхТоваров.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("Страна", Страна);
	
	СтрокиКатегорий = Запрос.Выполнить().Выбрать();
	
	Пока СтрокиКатегорий.Следующий() Цикл
		Элементы.КатегорияОтбора.СписокВыбора.Добавить(СтрокиКатегорий.Категория);
	КонецЦикла;

	Если СтрокиКатегорий.Количество() > 10 Тогда
		Элементы.КатегорияОтбора.СписокВыбора.Добавить(3, "...");
	КонецЕсли;

	КатегорияОтбора = 0;
	КатегорияОтбораДо = КатегорияОтбора;

КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюПоКатегориям()

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(КатегорииНоменклатуры.КатегорияПодакцизногоТовара, ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка)) КАК Категория,
	|	КОЛИЧЕСТВО(1) КАК Количество
	|ИЗ
	|	Справочник.Номенклатура КАК СправочникНоменклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, Страна = &Страна) КАК КатегорииНоменклатуры
	|		ПО СправочникНоменклатура.Ссылка = КатегорииНоменклатуры.Номенклатура
	|ГДЕ
	|	СправочникНоменклатура.ЭтоГруппа = ЛОЖЬ
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСТЬNULL(КатегорииНоменклатуры.КатегорияПодакцизногоТовара, ЗНАЧЕНИЕ(Справочник.КатегорииПодакцизныхТоваров.ПустаяСсылка))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Категория";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Страна", Страна);
	
	СписокСтрок = Новый Массив;
	СписокСтрок.Добавить(НСтр("ru = 'Всего номенклатуры с категориями:';
								|en = 'Total items with categories:'"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Не ЗначениеЗаполнено(Выборка.Категория) Тогда
			Категория = НСтр("ru = '<не указана>';
							|en = '<not specified>'");
		Иначе
			Категория = Выборка.Категория;
		КонецЕсли;
		СписокСтрок.Добавить(СтрШаблон(НСтр("ru = '%1 - %2 шт.';
											|en = '%1 - %2 pcs.'"), Категория, Выборка.Количество));
	КонецЦикла;
		
	ИнформацияПоСтавкам = Новый ФорматированнаяСтрока(СтрСоединить(СписокСтрок, " "), , ЦветаСтиля.ТекстИнформационнойНадписи);
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьПараметрыЗаполнения(ПроверятьКатегориюНазначения = Ложь)
	
	Отказ = Ложь;
	
	Если ПроверятьКатегориюНазначения И Не ЗначениеЗаполнено(КатегорияНазначения) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана категория подакцизных товаров';
							|en = 'The excisable goods category is not specified'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки, , "КатегорияНазначения", , Отказ);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не указан период';
														|en = 'Period is not specified'"), , "Период", , Отказ);
	КонецЕсли;
	
	Возврат Отказ;
	
КонецФункции

&НаКлиенте
Процедура УстановитьПодакцизнуюКатегориюНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		УстановитьПодакцизнуюКатегориюНоменклатурыНаКлиенте(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПодакцизнуюКатегориюНоменклатурыНаКлиенте(ДополнительныеПараметры)
	
	ДлительнаяОперация = УстановитьПодакцизнуюКатегориюНоменклатурыНаСервере(ДополнительныеПараметры);
	
	Если ДлительнаяОперация <> Неопределено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияФоновойОперации", ЭтотОбъект,
			НСтр("ru = 'Подакцизные категории номенклатуры установлены';
				|en = 'Excisable item categories are set'"));
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьСообщения = Истина;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановкаКоэффициентаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		ДополнительныеПараметры.Вставить("КоэффициентПересчета", Результат.КоэффициентПересчета);
		
		ДлительнаяОперация = УстановитьКоэффициентНаСервере(ДополнительныеПараметры);
	
		Если ДлительнаяОперация <> Неопределено Тогда
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияФоновойОперации", ЭтотОбъект,
				НСтр("ru = 'Коэффициенты пересчета номенклатуры установлены';
					|en = 'Item conversion ratios are set'"));
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьСообщения = Истина;
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьПодакцизныйПризнакНоменклатурыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ДлительнаяОперация = ОчиститьПодакцизныйПризнакНоменклатурыНаСервере(ДополнительныеПараметры);
	
		Если ДлительнаяОперация <> Неопределено Тогда
			ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗавершенияФоновойОперации", ЭтотОбъект,
			НСтр("ru = 'Подакцизный признак сброшен для номенклатуры';
				|en = 'Excisable flag is reset for the item'"));
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьСообщения = Истина;
			ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УстановитьПодакцизнуюКатегориюНоменклатурыНаСервере(ДополнительныеПараметры)

	Если ДополнительныеПараметры.АдресСписка <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("СписокНоменклатуры", ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресСписка));
	КонецЕсли;
	
	ИмяМетода = "РегистрыСведений.КатегорииПодакцизныхТоваров.УстановитьПодакцизнуюКатегориюНоменклатуры";
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Установка подакцизных категорий номенклатуры';
														|en = 'Set excise item categories'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(НастройкиЗапуска, ИмяМетода, ДополнительныеПараметры);
	
КонецФункции

&НаСервере
Функция ОчиститьПодакцизныйПризнакНоменклатурыНаСервере(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.АдресСписка <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("СписокНоменклатуры", ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресСписка));
	КонецЕсли;
	
	ИмяМетода = "РегистрыСведений.КатегорииПодакцизныхТоваров.ОчиститьПодакцизныйПризнакНоменклатуры";
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Очистка подакцизных категорий номенклатуры';
														|en = 'Clear excisable item categories'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(НастройкиЗапуска, ИмяМетода, ДополнительныеПараметры);
	
КонецФункции

&НаСервере
Функция УстановитьКоэффициентНаСервере(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.АдресСписка <> Неопределено Тогда
		ДополнительныеПараметры.Вставить("СписокНоменклатуры", ПолучитьИзВременногоХранилища(ДополнительныеПараметры.АдресСписка));
	КонецЕсли;
		
	ИмяМетода = "РегистрыСведений.КатегорииПодакцизныхТоваров.УстановитьКоэффициентПересчетаПоСписку";
	
	НастройкиЗапуска = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	НастройкиЗапуска.НаименованиеФоновогоЗадания = НСтр("ru = 'Установка коэффициента пересчета';
														|en = 'Set conversion ratio'");
	НастройкиЗапуска.ОжидатьЗавершение = 0;
	
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(НастройкиЗапуска, ИмяМетода, ДополнительныеПараметры);

КонецФункции

&НаКлиенте
Процедура ПослеЗавершенияФоновойОперации(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Сообщение Из Результат.Сообщения Цикл
		Сообщение.Сообщить();
	КонецЦикла;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		ПоказатьОповещениеПользователя(ДополнительныеПараметры);
		Элементы.СписокНоменклатуры.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДанныеДляИзмененияНоменклатуры(ПроверятьКатегорию = Ложь, ТолькоПодакцизный = Ложь);
	
	ДанныеДляИзменения = Новый Структура;
	ДанныеДляИзменения.Вставить("АдресСписка", Неопределено);
	ДанныеДляИзменения.Вставить("ЕстьУжеУстановленнаяКатегория", Ложь);
	
	ДанныеПоНоменклатуре = ПолучитьВыборкуПоНоменклатуре();
	
	НоменклатураДляИзменения = Новый Массив;
	Пока ДанныеПоНоменклатуре.Следующий() Цикл
		Если ПроверятьКатегорию И ЗначениеЗаполнено(ДанныеПоНоменклатуре.КатегорияПодакцизногоТовара) Тогда
			ДанныеДляИзменения.ЕстьУжеУстановленнаяКатегория = Истина;
		КонецЕсли;
		Если ТолькоПодакцизный И Не ДанныеПоНоменклатуре.ПодакцизныйТовар Тогда
			Продолжить;
		КонецЕсли;
		НоменклатураДляИзменения.Добавить(ДанныеПоНоменклатуре.Ссылка);
	КонецЦикла;
	
	Если НоменклатураДляИзменения.Количество() > 0 Тогда
		ДанныеДляИзменения.АдресСписка = ПоместитьВоВременноеХранилище(НоменклатураДляИзменения, Новый УникальныйИдентификатор);
	КонецЕсли;
	
	Возврат ДанныеДляИзменения;
	
КонецФункции

&НаСервере
Функция ПолучитьПараметрыДляПересчетаКоэффициента()
	
	ТекущаяСтрока = ПолучитьВыборкуПоНоменклатуре();

	НоменклатураДляИзменения = Новый Массив;
	ЕстьТоварыБезКатегории = Ложь;
	АвтоЗаполнениеКоэффициентаПересчета = Ложь;
	ЗапрашиватьКоэффициент = Ложь; 
	ЕстьРазныеЕдиницыИзмерения = Ложь;
	ЕдиницаИзмеренияНоменклатуры = Неопределено;
	ЕстьАдвалорнаяСтавка = Ложь;
	ЕдиницаИзмеренияКатегории = Неопределено;
	ЕстьРазныеЕдиницыИзмеренияКатегории = Ложь;

	Пока ТекущаяСтрока.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ТекущаяСтрока.КатегорияПодакцизногоТовара) Тогда
			ЕстьТоварыБезКатегории = Истина;
			Прервать;
		КонецЕсли;
		Если Не ТекущаяСтрока.ЭтоТвердаяСтавка Тогда
			ЕстьАдвалорнаяСтавка = Истина;
			Прервать;
		КонецЕсли;
		
		Если ЕдиницаИзмеренияКатегории = Неопределено Тогда
			ЕдиницаИзмеренияКатегории = ТекущаяСтрока.КатегорияЕдиницаИзмерения;
		ИначеЕсли ЕдиницаИзмеренияКатегории <> ТекущаяСтрока.КатегорияЕдиницаИзмерения Тогда
			ЕстьРазныеЕдиницыИзмеренияКатегории = Истина;
			Прервать;
		КонецЕсли;
	
		Если ТекущаяСтрока.АвтозаполнениеКоэффициентаПересчета = Истина Тогда
		    АвтоЗаполнениеКоэффициентаПересчета = Истина;
		Иначе
			ЗапрашиватьКоэффициент = Истина;
		КонецЕсли;
		Если ЕдиницаИзмеренияНоменклатуры = Неопределено Тогда
			ЕдиницаИзмеренияНоменклатуры = ТекущаяСтрока.ЕдиницаИзмерения;
		КонецЕсли;
		Если Не ЕстьРазныеЕдиницыИзмерения
			И ТекущаяСтрока.ЕдиницаИзмерения <> ЕдиницаИзмеренияНоменклатуры Тогда
			ЕстьРазныеЕдиницыИзмерения = Истина;
		КонецЕсли;
		НоменклатураДляИзменения.Добавить(Новый Структура("Номенклатура, КатегорияПодакцизногоТовара, АвтоКоэффициентПересчета",
			ТекущаяСтрока.Ссылка, ТекущаяСтрока.КатегорияПодакцизногоТовара, ТекущаяСтрока.АвтоКоэффициентПересчета));
	КонецЦикла;
	
	Если НоменклатураДляИзменения.Количество() > 0 Тогда
		АдресДанныхПоНоменклатуре = ПоместитьВоВременноеХранилище(НоменклатураДляИзменения, Новый УникальныйИдентификатор);
	Иначе
		АдресДанныхПоНоменклатуре = Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполненияКоэффициента = Новый Структура;
	ПараметрыЗаполненияКоэффициента.Вставить("Период", Период);
	ПараметрыЗаполненияКоэффициента.Вставить("Страна", Страна);	
	ПараметрыЗаполненияКоэффициента.Вставить("АдресСписка", АдресДанныхПоНоменклатуре);
	ПараметрыЗаполненияКоэффициента.Вставить("АвтоЗаполнениеКоэффициентаПересчета", АвтоЗаполнениеКоэффициентаПересчета);
	ПараметрыЗаполненияКоэффициента.Вставить("ЗапрашиватьКоэффициент", ЗапрашиватьКоэффициент);
	ПараметрыЗаполненияКоэффициента.Вставить("АвтоЗаполнениеКоэффициентаПересчета", АвтоЗаполнениеКоэффициентаПересчета);
	ПараметрыЗаполненияКоэффициента.Вставить("ЗапрашиватьКоэффициент", ЗапрашиватьКоэффициент);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕстьРазныеЕдиницыИзмерения", ЕстьРазныеЕдиницыИзмерения);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕстьТоварыБезКатегории", ЕстьТоварыБезКатегории);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕстьАдвалорнаяСтавка", ЕстьАдвалорнаяСтавка);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕдиницаИзмеренияКатегории", ЕдиницаИзмеренияКатегории);
	ПараметрыЗаполненияКоэффициента.Вставить("ЕстьРазныеЕдиницыИзмеренияКатегории", ЕстьРазныеЕдиницыИзмеренияКатегории);
	
	Возврат ПараметрыЗаполненияКоэффициента;
	
КонецФункции

&НаСервере
Функция ПолучитьВыборкуПоНоменклатуре()
	
	Схема = Элементы.СписокНоменклатуры.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки = Элементы.СписокНоменклатуры.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
		
	КомпоновкаДанныхКлиентСервер.ДобавитьОтбор(Настройки, "ЭтоГруппа", Ложь);
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Схема, Настройки, , ,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	Запрос = Новый Запрос;
	Запрос.Текст = МакетКомпоновки.НаборыДанных.НаборДанныхДинамическогоСписка.Запрос;	
	Для Каждого ЗначениеПараметра Из МакетКомпоновки.ЗначенияПараметров Цикл
		Запрос.УстановитьПараметр(ЗначениеПараметра.Имя,ЗначениеПараметра.Значение);
	КонецЦикла;
	
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

&НаСервере
Процедура УстановитьОтборПоПризнаку()
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
		СписокНоменклатуры,"ПодакцизныйТовар");

	Если ПодакцизныйТовар <> 0 Тогда
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		СписокНоменклатуры,
		"ПодакцизныйТовар",
		?(ПодакцизныйТовар = 1, Истина, Ложь),
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
	КонецЕсли;
	
	Если ((ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И КатегорияОтбора = 0)
		Или ПодакцизныйТовар = 2 Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
		СписокНоменклатуры,"КатегорияПодакцизногоТовара");
	ИначеЕсли (ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И КатегорияОтбора = 1 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокНоменклатуры,
			"КатегорияПодакцизногоТовара",
			,
			ВидСравненияКомпоновкиДанных.Заполнено);
	ИначеЕсли (ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И КатегорияОтбора = 2 Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокНоменклатуры,
			"КатегорияПодакцизногоТовара",
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ИначеЕсли (ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И ЗначениеЗаполнено(КатегорияОтбора) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			СписокНоменклатуры,
			"КатегорияПодакцизногоТовара",
			КатегорияОтбора,
			ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	Если (ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И ТипЗнч(КоэффициентПересчетаОтбор) = Тип("Строка") Тогда
		Если КоэффициентПересчетаОтбор = "<Все>" Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				СписокНоменклатуры,"КоэффициентПересчета");
			ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
				СписокНоменклатуры,"ЭтоТвердаяСтавка");
		ИначеЕсли КоэффициентПересчетаОтбор = "<Указан>" Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокНоменклатуры,
				"КоэффициентПересчета",
				,
				ВидСравненияКомпоновкиДанных.Заполнено);
		ИначеЕсли КоэффициентПересчетаОтбор = "<Не указан>" Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокНоменклатуры,
				"КоэффициентПересчета",
				,
				ВидСравненияКомпоновкиДанных.НеЗаполнено);
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокНоменклатуры,
				"ЭтоТвердаяСтавка",
				Истина,
				ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	ИначеЕсли (ПодакцизныйТовар = 1 Или ПодакцизныйТовар = 0) И ТипЗнч(КоэффициентПересчетаОтбор) = Тип("Число") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
				СписокНоменклатуры,
				"КоэффициентПересчета",
				КоэффициентПересчетаОтбор,
				ВидСравненияКомпоновкиДанных.Равно);
	Иначе
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокНоменклатуры,"КоэффициентПересчета");
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(
			СписокНоменклатуры,"ЭтоТвердаяСтавка");
	КонецЕсли;
	
	ДоступностьЭлементов = ПодакцизныйТовар <> 2;
	Элементы.КатегорияОтбора.Доступность = ДоступностьЭлементов;
	Элементы.КоэффициентПересчетаОтбор.Доступность = ДоступностьЭлементов;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТипЕдиницыИзмеренияАкциза()
	
	СкрытьВесИОбъемНоменклатуры = Истина;
	
	Если ЗначениеЗаполнено(КатегорияНазначения) Тогда
		РеквизитыКатегории = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(КатегорияНазначения, "ТипСтавки, ЕдиницаИзмерения");
		Если РеквизитыКатегории.ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая
			И ЗначениеЗаполнено(РеквизитыКатегории.ЕдиницаИзмерения) Тогда
			ТипИзмеряемойВеличины = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РеквизитыКатегории.ЕдиницаИзмерения, "ТипИзмеряемойВеличины");
			Если ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Объем Тогда
				СкрытьВесИОбъемНоменклатуры = Ложь;
				Элементы.СписокНоменклатурыКоэффициентОбъема.Видимость = Истина;
				Элементы.СписокНоменклатурыОбъемЕдиницаИзмерения.Видимость = Истина;
			ИначеЕсли ТипИзмеряемойВеличины = Перечисления.ТипыИзмеряемыхВеличин.Вес Тогда
				СкрытьВесИОбъемНоменклатуры = Ложь;
				Элементы.СписокНоменклатурыКоэффициентВеса.Видимость = Истина;
				Элементы.СписокНоменклатурыВесЕдиницаИзмерения.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СкрытьВесИОбъемНоменклатуры Тогда
		Элементы.СписокНоменклатурыКоэффициентОбъема.Видимость = Ложь;
		Элементы.СписокНоменклатурыОбъемЕдиницаИзмерения.Видимость = Ложь;
		Элементы.СписокНоменклатурыКоэффициентВеса.Видимость = Ложь;
		Элементы.СписокНоменклатурыВесЕдиницаИзмерения.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьОтборПоПризнаку();

КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьКоэффициент(Параметры) Экспорт
	
	ПоказатьВводЧисла(Новый ОписаниеОповещения("ВыбратьКоэффициентЗавершение", ЭтотОбъект), 1, 
			НСтр("ru = 'Введите коэффициент пересчета';
				|en = 'Enter conversion ratio'"), 10, 6);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьКоэффициентЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если Результат < 0 Тогда
			ТекстОшибки = НСтр("ru = 'Коэффициент не должен быть отрицательным числом.';
								|en = 'The ratio cannot be a negative number.'");
			ЗаголовокПредупреждения = НСтр("ru = 'Ошибка ввода коэффициента';
											|en = 'Ratio input error'");
			ПоказатьПредупреждение(Новый ОписаниеОповещения("ЗапроситьКоэффициент", ЭтотОбъект),
				ТекстОшибки,
				,
				ЗаголовокПредупреждения);
		Иначе
			ЭлементВыбора = Элементы.КоэффициентПересчетаОтбор.СписокВыбора.НайтиПоЗначению(Результат);
			Если ЭлементВыбора = Неопределено Тогда
				КоэффициентСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСТр("ru = 'Коэффициент ""%1""';
						|en = 'The ""%1"" ratio'"), Формат(Результат, "ЧЦ=10; ЧДЦ=6"));
				Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Добавить(Результат, КоэффициентСтрока);
				Если Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Количество() > 10 Тогда
					Для Каждого КоэффициентПересчетаСтрока Из Элементы.КоэффициентПересчетаОтбор.СписокВыбора Цикл
						Если ТипЗнч(КоэффициентПересчетаСтрока.Значение) = Тип("Число") Тогда
							Элементы.КоэффициентПересчетаОтбор.СписокВыбора.Удалить(КоэффициентПересчетаСтрока);
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
			КоэффициентПересчетаОтбор = Результат;
			КоэффициентПересчетаОтборДо = КоэффициентПересчетаОтбор;
			
			УстановитьОтборПоПризнаку();
		КонецЕсли;
	Иначе
		КоэффициентПересчетаОтбор = КоэффициентПересчетаОтборДо;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборПодакцизнойКатегорииЗавершение(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		КатегорияОтбора = Результат;
		УстановитьОтборПоПризнаку();
		КатегорияОтбораДо = КатегорияОтбора;
	Иначе
		КатегорияОтбора = КатегорияОтбораДо;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВывестиПредупреждениеПриУстановкеКоэффициента(ЕстьТоварыБезКатегории, ЕстьАдвалорнаяСтавка, АвтоЗаполнениеИЗапрашиватьКоэффициент, ЕстьРазныеЕдиницыИзмеренияКатегории, НетВыбраннойНоменклатуры)
	
	Отказ = Истина;
	
	Если ЕстьТоварыБезКатегории Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Нельзя указать коэффициент пересчета, если для номенклатуры не задана категория.';
										|en = 'Cannot specify a conversion ratio if the category is not specified for the item.'"));
	ИначеЕсли ЕстьАдвалорнаяСтавка Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выбраны подакцизные товары с категориями, в которых указаны адвалорные ставки.
			|Для таких категорий не поддерживается автоматический расчет сумм акциза. Вводить коэффициент пересчета не надо.';
			|en = 'Excisable goods with ad valorem rates are selected.
			|Automatic calculation of excise amounts is not supported for these categories. There is no need to enter a conversion ratio.'"));
	ИначеЕсли АвтоЗаполнениеИЗапрашиватьКоэффициент Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выбраны подакцизные товары, для которых возможен авторасчет коэффициента и для которых необходимо задавать коэффициент вручную.';
										|en = 'Excisable goods for which automatic ratio calculation is available and for which it is necessary to set the ratio manually are selected.'"));
	ИначеЕсли ЕстьРазныеЕдиницыИзмеренияКатегории Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выбраны подакцизные товары с разными единицами измерения подакцизных категорий.
			|Установить коэффициент пересчета можно для товаров с одинаковыми единицами измерения подакцизных категорий.';
			|en = 'Excisable goods with different units of measure of excisable categories are selected.
			|You can set the conversion ratio for goods with the same units of measure of excisable categories.'"));
	ИначеЕсли НетВыбраннойНоменклатуры Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для продолжения необходимо выделить строки.';
										|en = 'To continue, select lines.'"));
	Иначе
		Отказ = Ложь;
	КонецЕсли;
	
	Возврат Отказ;

КонецФункции

#КонецОбласти
