// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// см. ТрансграничныйЭДО.ПриФормированииТаблицыТиповЭлементовРегламента
Процедура ПриФормированииТаблицыТиповЭлементовРегламента(Таблица) Экспорт
	
	ДобавитьВТаблицуТипЭлементаРегламента(Таблица,
		"RBMainDocumentRejectPostDateConfirmation", Перечисления.ТипыЭлементовРегламентаЭДО.УОУ_ПДП);
	ДобавитьВТаблицуТипЭлементаРегламента(Таблица,
		"BillLadingRepublicBelarusSender", Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя);
	
КонецПроцедуры

// см. ТрансграничныйЭДО.ДанныеСлужебногоСообщения
Функция ДанныеСлужебногоСообщения(ТипДокумента, ДанныеЭД) Экспорт
	
	ИдентификаторПолучателя = ДанныеЭД.Участники.Получатель.Абонент.Идентификатор; // Строка
	ПоддерживаемыеТипы = Новый Соответствие();
	ПоддерживаемыеТипы.Вставить(Перечисления.ТипыДокументовЭДО.УведомлениеОбУточнении, Истина);
	
	Если ПоддерживаемыеТипы.Получить(ТипДокумента) = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(ИдентификаторПолучателя)
		ИЛИ НЕ ЭтоОператорРБ(ИдентификаторПолучателя) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТипДокумента = Перечисления.ТипыДокументовЭДО.УведомлениеОбУточнении Тогда
		Возврат УведомлениеОбУточнении_Данные(ДанныеЭД);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Проверяет доступность отклонения сообщения
// 
// Параметры:
//  ИмяФайла - Строка
// 
// Возвращаемое значение:
//  Булево -
//
Функция ОтклонениеДоступно(ИмяФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ИмяФайла); // Строка
	Если НЕ ЗначениеЗаполнено(Формат) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ФорматыЭД = ФорматыЭД();
	Возврат Формат = ФорматыЭД.ТоварнаяНакладная.Ключ;
	
КонецФункции

// Извлекает 13-значный GLN контрагента
// 
// Параметры:
//  Идентификатор - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция ПолучитьGLN(Идентификатор) Экспорт
	
	ДлинаИдентификатораТЭДО = 17;
	Если СтрДлина(Идентификатор) <> ДлинаИдентификатораТЭДО Тогда
		Возврат "";
	КонецЕсли;
	
	Префикс = ВРег(Лев(Идентификатор, 4));
	
	Для Каждого Оператор Из Операторы() Цикл
		Если Префикс = Врег(Оператор.Префикс) Тогда
			ДлинаИдентификатора = 13;
			Возврат Прав(Идентификатор, ДлинаИдентификатора);
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

// см. ТрансграничныйЭДО.КодТранзакции
Функция КодТранзакции(ОписаниеОбъекта) Экспорт
	
	Если ОписаниеОбъекта.ТипРегламента <> Перечисления.ТипыРегламентовЭДО.Формализованный Тогда
		Возврат "";
	КонецЕсли;
	
	ОписаниеДанных = ОписаниеОбъекта["ОписаниеДанных"]; // см. СинхронизацияЭДО.НовоеОписаниеДанныхОбъекта
	Формат = ОпределитьФорматЭД(ОписаниеДанных.ИмяФайла); // Строка
	Если НЕ ЗначениеЗаполнено(Формат) Тогда
		Возврат "";
	КонецЕсли;
	
	Если НЕ ЭтоОператорРБ(ОписаниеОбъекта.ИдентификаторПолучателя)
		И НЕ ЭтоОператорРБ(ОписаниеОбъекта.ИдентификаторОтправителя) Тогда
		Возврат "";
	КонецЕсли;
	
	ФорматыЭД = ФорматыЭД();
	ТипЭлементаРегламента = ОписаниеОбъекта.ТипЭлементаРегламента;
	
	Если ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияОтправителя Тогда
		Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
			Возврат "BillLadingRepublicBelarusSender";
		КонецЕсли;
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя Тогда
		Если Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
			Возврат "CustomerTitle";
		КонецЕсли;
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИОП Тогда
		Возврат "RBReceiveNotice";
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.УОУ Тогда
		Возврат "RBMainDocumentReject";
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.УОУ_ПДО Тогда
		Возврат "RBMainDocumentRejectSendConfirmation";
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.УОУ_ПДП Тогда
		Возврат "RBMainDocumentRejectPostDateConfirmation";
	ИначеЕсли ТипЭлементаРегламента = Перечисления.ТипыЭлементовРегламентаЭДО.ИнформацияПолучателя_ИОП Тогда
		Возврат "RBCustomerInformationReceiveNotice";
	КонецЕсли;
		
	Возврат "";
	
КонецФункции

// см. ТрансграничныйЭДО.ТипДокументаСтрокой
Функция ТипДокументаСтрокой(ОписаниеОбъекта) Экспорт
	
	ОписаниеДанных = ОписаниеОбъекта["ОписаниеДанных"]; // см. СинхронизацияЭДО.НовоеОписаниеДанныхОбъекта
	Формат = ОпределитьФорматЭД(ОписаниеДанных.ИмяФайла); // Строка
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат "BillLadingRepublicBelarusRecipient";
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Данные ЭД.
// 
// Параметры:
//  ОписаниеФайла - см. РаботаСФайламиБЭД.НовоеОписаниеФайла
// 
// Возвращаемое значение:
//  см. ТрансграничныйЭДО.ДанныеЭД
//
Функция ДанныеЭД(ОписаниеФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ОписаниеФайла.ИмяФайла);
	ФорматыЭД = ФорматыЭД();
	Результат = Новый Структура();
	Результат.Вставить("ОбъектXDTO", Неопределено);
	Результат.Вставить("ПространствоИмен", "");
	Результат.Вставить("Сопоставление", Новый Соответствие());
	Результат.Вставить("Номенклатура", Новый Соответствие());
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		
		РезультатЧтения = ПрочитатьТоварнуюНакладную(ОписаниеФайла.ДвоичныеДанные);
		
		Если РезультатЧтения <> Неопределено Тогда
			
			Результат.ОбъектXDTO = РезультатЧтения;
			Результат.ПространствоИмен = ФорматыЭД.ТоварнаяНакладная.ПространствоИмен;
			
			ТоварыИзФайла = ТоварыИзФайла(ОписаниеФайла);
			Если ТоварыИзФайла <> Неопределено Тогда
				
				НомерСтроки = 1;
				Для Каждого ДанныеТовара Из ТоварыИзФайла Цикл
					Результат.Сопоставление.Вставить(НомерСтроки, ДанныеТовара.Сопоставление);
					НомерСтроки = НомерСтроки + 1;
				КонецЦикла;
				
			КонецЕсли;
			
			Возврат Результат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// см. ТрансграничныйЭДО.ТоварыИзФайла
Функция ТоварыИзФайла(ОписаниеФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ОписаниеФайла.ИмяФайла);
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		Возврат ТоварыИзТоварнойНакладной(ОписаниеФайла.ДвоичныеДанные);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает коллекцию поддерживаемых форматов.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - Строка
//
Функция ПоддерживаемыеФорматы() Экспорт
	
	Результат = Новый Соответствие();
	
	Для Каждого Формат Из ФорматыЭД() Цикл
		ОписаниеФормата = Формат.Значение; // см. ТрансграничныйЭДО.ОписаниеФорматаЭД
		Результат.Вставить(ОписаниеФормата.Ключ, ОписаниеФормата.Код);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращает код страны по ОКСМ, поддерживаемой данным обработчиком
// 
// Возвращаемое значение:
//  Строка -
//  
Функция КодСтраны() Экспорт
	Возврат "112";
КонецФункции

// Возвращает альфа-3 код страны по ОКСМ, поддерживаемой данным обработчиком
// 
// Возвращаемое значение:
//  Строка -
//  
Функция КодАльфа3Страны() Экспорт
	Возврат "BLR";
КонецФункции

// Формирует электронный документ.
//
// Параметры:
//  Формат - Строка
//  Данные - см. ТрансграничныйЭДО.ДанныеОбъектаУчета
//
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НовыйРезультатФормированияДокумента
//
Функция СформироватьДокумент(Формат, Данные) Экспорт
	
	ФорматЭД = ОпределитьФорматЭД(Формат);
	ФорматыЭД = ФорматыЭД();
	
	Если ФорматЭД = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат СформироватьBLRDNR(Данные);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет форму ручного формирования ответного титула.
// 
// Параметры:
//  ПолноеИмяФайла - Строка
// 
// Возвращаемое значение:
//  Неопределено, Строка -
//
Функция ФормаРучногоФормированияОтветногоТитула(ПолноеИмяФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ПолноеИмяФайла);
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		Возврат "Обработка.ТрансграничныйОбменРБ.Форма.ФормированиеBLRDNR";
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Возвращает описание данных объекта учета.
// 
// Параметры:
//  ДанныеУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//              - Неопределено
//  Параметры - см. ИнтеграцияЭДО.НовыеПараметрыФормированияДанныхОбъектаУчета
// 
// Возвращаемое значение:
//  - Неопределено
//  - см. ИнтеграцияЭДО.НовыйРезультатФормированияДанныхОбъектаУчета
//
Функция ОписаниеДанныхОбъектаУчета(ДанныеУчета, Параметры) Экспорт
	
	Формат = ОпределитьФорматЭД(Параметры.Формат);
	
	Если НЕ ЗначениеЗаполнено(Формат) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат ТоварнаяНакладнаяТитулПокупателя_ДанныеУчета(ДанныеУчета, Параметры);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Описывает соответствие титулов ЭД
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - Строка
//
Функция СоответствиеТитулов() Экспорт
	
	Результат = Новый Соответствие();
	ФорматыЭД = ФорматыЭД();
	Результат.Вставить(ФорматыЭД.ТоварнаяНакладная.Код, ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Код);
	Возврат Результат;
	
КонецФункции

// Формирует представление ЭД.
// 
// Параметры:
//  ИмяФайла - Строка
//  ДвоичныеДанныеФайла - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Неопределено, ТабличныйДокумент -
//
Функция ПредставлениеДанных(ИмяФайла, ДвоичныеДанныеФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ИмяФайла);
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		Возврат ПредставлениеТоварнойНакладной(ДвоичныеДанныеФайла);
	ИначеЕсли Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат ПредставлениеТоварнойНакладной_ТитулПокупателя(ДвоичныеДанныеФайла);
	ИначеЕсли Формат = ФорматыЭД.УведомлениеОбУточнении.Ключ Тогда
		Возврат ПредставлениеУведомленияОбУточнении(ДвоичныеДанныеФайла);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Читает параметры файла произвольного документа.
// 
// Параметры:
//  ПолноеИмяФайла - Строка
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. ТрансграничныйЭДО.РаспознатьСообщение
//
Функция РаспознатьСообщение(ПолноеИмяФайла, ДвоичныеДанные) Экспорт
	
	Формат = ОпределитьФорматЭД(ПолноеИмяФайла);
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		Возврат ПараметрыТоварнойНакладной(ДвоичныеДанные);
	ИначеЕсли Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат ПараметрыТоварнойНакладной_ТитулПокупателя(ДвоичныеДанные);
	ИначеЕсли Формат = ФорматыЭД.УведомлениеОбУточнении.Ключ Тогда
		Возврат ПараметрыУведомленияОбУточнении(ДвоичныеДанные);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// См. ТрансграничныйЭДО.ПрочитатьСодержаниеДокумента
Функция ПрочитатьСодержаниеДокумента(ОписаниеФайла) Экспорт
	
	Формат = ОпределитьФорматЭД(ОписаниеФайла.ИмяФайла);
	ФорматыЭД = ФорматыЭД();
	
	Если Формат = ФорматыЭД.ТоварнаяНакладная.Ключ Тогда
		Возврат СодержаниеТоварнойНакладной(ОписаниеФайла.ДвоичныеДанные);
	ИначеЕсли Формат = ФорматыЭД.ТоварнаяНакладнаяТитулПокупателя.Ключ Тогда
		Возврат СодержаниеТоварнойНакладной_ТитулПокупателя(ОписаниеФайла.ДвоичныеДанные);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// см. ТрансграничныйЭДО.КодСтраныАбонента
Функция КодСтраныАбонента(ИдентификаторУчастника) Экспорт
	Возврат ?(ЭтоОператорРБ(ИдентификаторУчастника), КодСтраны(), "");
КонецФункции

// Возвращает код страны участника ТЭДО.
// 
// Параметры:
//  ИдентификаторУчастника - Строка
// 
// Возвращаемое значение:
//  Строка -
//  
Функция КодАльфа3СтраныУчастника(ИдентификаторУчастника) Экспорт
	Возврат ?(ЭтоОператорРБ(ИдентификаторУчастника), КодАльфа3Страны(), "");
КонецФункции

// Возвращает массив операторов ТЭДО
// 
// Возвращаемое значение:
//  Массив из см. ТрансграничныйЭДО.ОписаниеОператора
//  
Функция Операторы() Экспорт
	
	Результат = Новый Массив(); // Массив из см. ТрансграничныйЭДО.ОписаниеОператора
	
	Оператор = ТрансграничныйЭДО.ОписаниеОператора();
	Оператор.Префикс = "ABY-";
	Оператор.КодАльфа3Страны = КодАльфа3Страны();
	Оператор.Наименование = НСтр("ru = 'ООО ""Современные технологии торговли (тестовый контур)""';
								|en = 'Modern Trading Technologies (test circuit) LLC'");
	Результат.Добавить(Оператор);
	
	Оператор = ТрансграничныйЭДО.ОписаниеОператора();
	Оператор.Префикс = "ARB-";
	Оператор.КодАльфа3Страны = КодАльфа3Страны();
	Оператор.Наименование = НСтр("ru = 'ООО ""Современные технологии торговли""';
								|en = 'Modern Trading Technologies LLC'");
	Результат.Добавить(Оператор);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ФорматыЭД

// Проверяет оператора РБ.
// 
// Параметры:
//  ИдентификаторУчастника - Строка
// 
// Возвращаемое значение:
//  Булево -
//
Функция ЭтоОператорРБ(ИдентификаторУчастника)
	
	Для Каждого Оператор Из Операторы() Цикл
		Если СтрНачинаетсяС(ВРег(ИдентификаторУчастника), ВРег(Оператор.Префикс)) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Возвращает коллекцию поддерживаемых форматов ЭД.
// 
// Возвращаемое значение:
//  Структура:
//  * ТоварнаяНакладная - см. ТрансграничныйЭДО.ОписаниеФорматаЭД
//  * ТоварнаяНакладнаяТитулПокупателя - см. ТрансграничныйЭДО.ОписаниеФорматаЭД
//  * УведомлениеОбУточнении - см. ТрансграничныйЭДО.ОписаниеФорматаЭД
//  
Функция ФорматыЭД() Экспорт
	
	ПространстваИмен = ТрансграничныйЭДО.ПространстваИмен();
	Результат = Новый Структура();
	
	ФорматЭД = ТрансграничныйЭДО.ОписаниеФорматаЭД();
	ФорматЭД.Ключ = "ТоварнаяНакладная";
	ФорматЭД.Код = "BLRDLN";
	ФорматЭД.Наименование = НСтр("ru = 'Товарная накладная Республики Беларусь';
								|en = 'Invoice of the Republic of Belarus'");
	ФорматЭД.ПространствоИмен = ПространстваИмен.ТоварнаяНакладнаяРБ;
	ФорматЭД.ТипДанных = "DeliveryNote";
	ФорматЭД.Макет = "BLRDLN";
	Результат.Вставить("ТоварнаяНакладная", ФорматЭД);
	
	ФорматЭД = ТрансграничныйЭДО.ОписаниеФорматаЭД();
	ФорматЭД.Ключ = "ТоварнаяНакладнаяТитулПокупателя";
	ФорматЭД.Код = "BLRDNR";
	ФорматЭД.Наименование = НСтр("ru = 'Товарная накладная Республики Беларусь, титул покупателя';
								|en = 'Invoice of the Republic of Belarus, customer title'");
	ФорматЭД.ПространствоИмен = ПространстваИмен.ТоварнаяНакладнаяРБТитулПокупателя;
	ФорматЭД.ТипДанных = "DeliveryNote";
	ФорматЭД.Макет = "BLRDNR";
	Результат.Вставить("ТоварнаяНакладнаяТитулПокупателя", ФорматЭД);
	
	ФорматЭД = ТрансграничныйЭДО.ОписаниеФорматаЭД();
	ФорматЭД.Ключ = "УведомлениеОбУточнении";
	ФорматЭД.Код = "BLRAPN";
	ФорматЭД.Наименование = НСтр("ru = 'Уведомление об уточнении Республики Беларусь';
								|en = 'Notification of specification of the Republic of Belarus'");
	ФорматЭД.ПространствоИмен = ПространстваИмен.УведомлениеОбУточненииРБ;
	ФорматЭД.ТипДанных = "Acknowledgement";
	ФорматЭД.Макет = "BLRAPN";
	Результат.Вставить("УведомлениеОбУточнении", ФорматЭД);
	
	Возврат Результат;
	
КонецФункции

// Возвращает описание формата ЭД.
// 
// Параметры:
//  ИмяФайла - Строка
// 
// Возвращаемое значение:
//  - Неопределено
//  - см. ТрансграничныйЭДО.ОписаниеФорматаЭД
//
Функция ФорматЭД(ИмяФайла) Экспорт
	
	Для Каждого КлючИЗначение Из ФорматыЭД() Цикл
		
		Формат = КлючИЗначение.Значение; // см. ТрансграничныйЭДО.ОписаниеФорматаЭД
		Если СтрНачинаетсяС(ВРег(ИмяФайла), ВРег(Формат.Код)) Тогда
			Возврат Формат;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

// Определяет формат ЭД.
// 
// Параметры:
//  ПолноеИмяФайла - Строка
// 
// Возвращаемое значение:
//  Строка -
//
Функция ОпределитьФорматЭД(ПолноеИмяФайла) Экспорт
	
	Для Каждого КлючИЗначение Из ФорматыЭД() Цикл
		
		Формат = КлючИЗначение.Значение; // см. ТрансграничныйЭДО.ОписаниеФорматаЭД
		Если СтрНачинаетсяС(ВРег(ПолноеИмяФайла), ВРег(Формат.Код)) Тогда
			Возврат Формат.Ключ;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

#КонецОбласти

#Область BLRDLN

// Извлекает товары из товарной накладной.
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. ТрансграничныйЭДО.ТоварыИзФайла
//
Функция ТоварыИзТоварнойНакладной(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную(ДвоичныеДанные);
	
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ФорматыЭДО.НоваяТаблицаТоваров(); // ТаблицаЗначений
	Товары = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DespatchAdviceLogisticUnitLineItem.LineItem");
	
	Для Каждого Товар Из Товары Цикл
		
		СтрокаТаблицы = Результат.Добавить();
		СтрокаТаблицы.Штрихкод = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Товар, "LineItemID");
		СтрокаТаблицы.Наименование = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Товар, "LineItemName");
		СтрокаТаблицы.БазоваяЕдиницаКод = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Товар, "LineItemQuantityUOM");
		
		ЧастиКлюча = Новый Массив(); // Массив из Строка
		ЧастиКлюча.Добавить(ВРег(СтрЗаменить(СтрокаТаблицы.Наименование, " ", "")));
		ЧастиКлюча.Добавить(СтрокаТаблицы.Штрихкод);
		ЧастиКлюча.Добавить(СтрокаТаблицы.БазоваяЕдиницаКод);
		Ключ = СтрСоединить(ЧастиКлюча, "#");
		ИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоСтроке(Ключ);
		СтрокаТаблицы.ИсторияИдентификаторов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИД);
		
		Сопоставление = ФорматыЭДО.НовыеДанныеДляСопоставленияТоваров();
		Сопоставление.ШтрихкодыНоменклатуры = СтрокаТаблицы.Штрихкод;
		Сопоставление.Наименование = СтрокаТаблицы.Наименование;
		Сопоставление.ЕдиницаИзмеренияКод = СтрокаТаблицы.БазоваяЕдиницаКод;
		Сопоставление.Идентификатор =
			СопоставлениеНоменклатурыКонтрагентов.ИдентификаторНоменклатурыКонтрагентаПоНатуральнымКлючам(Сопоставление);
		ИД = СопоставлениеНоменклатурыКонтрагентов.ИдентификаторТовараПоДаннымНоменклатуры(
			Сопоставление.Наименование, Сопоставление.Характеристика, Сопоставление.ЕдиницаИзмерения);
		Сопоставление.ИсторияИдентификаторов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИД);
		Сопоставление.Идентификатор = СтрЗаменить(Сопоставление.Идентификатор, Символы.ПС, "");
		СтрокаТаблицы.Сопоставление = Сопоставление;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Проверяет аннулирование.
// 
// Параметры:
//  ФункцияНакладной - Строка
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоАннулирование(ФункцияНакладной)
	Возврат ФункцияНакладной = "1";
КонецФункции

// Возвращает содержание товарной накладной
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
//
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НовоеОписаниеФайлаДокумента
//
Функция СодержаниеТоварнойНакладной(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную(ДвоичныеДанные);
	
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ФорматыЭДО.НовоеОписаниеФайлаДокумента();
	
	Результат.ТипДокумента = Перечисления.ТипыДокументовЭДО.ТоварнаяНакладная;
	НомерДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteID");
	Результат.НомерДокумента = Прав(НомерДокумента, ТрансграничныйЭДО.МаксимальнаяДлинаНомераЭД());
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteDate");
	Результат.ДатаДокумента = ДатаИзЧисла(DeliveryNoteDate);
	Результат.СуммаДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Total.TotalAmount");
	Результат.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный;
	Результат.Отправитель.Наименование = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Name");
	НалоговыйНомер = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.VATRegistrationNumber");
	УчастникТЭДО = ТрансграничныйЭДО.ОписаниеУчастника();
	УчастникТЭДО.НалоговыйНомер = НалоговыйНомер;
	УчастникТЭДО.КодАльфа3Страны = КодАльфа3Страны();
	Результат.Отправитель.УчастникТЭДО = УчастникТЭДО;
	Результат.Получатель.Наименование = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.Name");
	Результат.Получатель.ИНН = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.VATRegistrationNumber");
	Результат.Формат = ФорматыЭД().ТоварнаяНакладная.Код;
	FunctionCode = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "FunctionCode", "9");
	ФорматЭД = ФорматыЭД().ТоварнаяНакладная;
	Если НЕ ЭтоАннулирование(FunctionCode) Тогда
		ОтражениеВУчете = Новый Структура("ВариантЗаполнения, Формат", ФорматЭД.Ключ, ФорматЭД.Код);
		Результат.ОтражениеВУчете = ОтражениеВУчете;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Читает товарную накладную и возвращает XDTO-объект
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Неопределено, ОбъектXDTO -
//  
Функция ПрочитатьТоварнуюНакладную(ДвоичныеДанные)
	
	Формат = ФорматыЭД().ТоварнаяНакладная;
	ТипДанных = РаботаСФайламиБЭД.ПолучитьТипЗначенияCML(Формат.ТипДанных, Формат.ПространствоИмен);
	
	Попытка
		
		ДвоичныеДанныеФайла = ОбщегоНазначенияБЭД.ДобавитьПространствоИмен(
			ДвоичныеДанные, Формат.ПространствоИмен, Кодировка());
		Возврат РаботаСФайламиБЭД.ПрочитатьXDTO(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения(), ТипДанных);
		
	Исключение
		
		Операция = НСтр("ru = 'Чтение товарной накладной Республики Беларусь';
						|en = 'Read the invoice of the Republic of Belarus'");
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ТЭДО, ТекстОшибки, ТекстОшибки);
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Читает параметры товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. РаспознатьСообщение
//
Функция ПараметрыТоварнойНакладной(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную(ДвоичныеДанные);
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ФорматЭД = ФорматыЭД().ТоварнаяНакладная;
	Результат = ТрансграничныйЭДО.НовыеПараметрыДокумента();
	Результат.ТипДокумента = ФорматЭД.Ключ;
	
	FunctionCode = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "FunctionCode", "9");
	Если НЕ ЭтоАннулирование(FunctionCode) Тогда
		Результат.ОтражениеВУчете.ВариантЗаполнения = ФорматЭД.Ключ;
		Результат.ОтражениеВУчете.Формат = ФорматЭД.Код;
	КонецЕсли;
	
	Результат.ИсходныйФормат = ФорматЭД.Код;
	Возврат Результат;
	
КонецФункции

// Формирует представление товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. ПредставлениеДанных
//
Функция ПредставлениеТоварнойНакладной(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную(ДвоичныеДанные);
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый ТабличныйДокумент();
	Макет = ПолучитьМакет(ФорматыЭД().ТоварнаяНакладная.Макет);
	Шапка = Новый Структура();
	
	FunctionCode = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "FunctionCode", "9");
	Шапка.Вставить("НакладнаяОтменена", ?(ЭтоАннулирование(FunctionCode), НСтр("ru = 'Накладная отменена';
																				|en = 'The invoice is canceled'"), ""));
	
	УНПГрузоотправителя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.VATRegistrationNumber", "");
	Шапка.Вставить("УНПГрузоотправителя", УНПГрузоотправителя);
	
	ShipperGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.GLN", "");
	Шапка.Вставить("GLNГрузоотправителя", ShipperGLN);
	
	УНПГрузополучателя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.VATRegistrationNumber", "");
	Шапка.Вставить("УНПГрузополучателя", УНПГрузополучателя);
	
	ReceiverGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.GLN", "");
	Шапка.Вставить("GLNГрузополучателя", ReceiverGLN);
	
	НомерЭлектроннойНакладной = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteID", "");
	Шапка.Вставить("НомерЭлектроннойНакладной", НомерЭлектроннойНакладной);
	
	ДатаЧислом = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteDate");
	Шапка.Вставить("ДатаЭлектроннойНакладной", ПредставлениеДатыИзЧисла(ДатаЧислом));
	
	ПолноеНаименованиеГрузоотправителя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Name", "");
	Шапка.Вставить("ПолноеНаименованиеГрузоотправителя", ПолноеНаименованиеГрузоотправителя);
	
	ЮридическийАдресГрузоотправителя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Address", "");
	Шапка.Вставить("ЮридическийАдресГрузоотправителя", ЮридическийАдресГрузоотправителя);
	
	ПолноеНаименованиеГрузополучателя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.Name", "");
	Шапка.Вставить("ПолноеНаименованиеГрузополучателя", ПолноеНаименованиеГрузополучателя);
	
	ЮридическийАдресГрузополучателя = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.Address", "");
	Шапка.Вставить("ЮридическийАдресГрузополучателя", ЮридическийАдресГрузополучателя);
	
	НаименованиеДоговора = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ContractName", "");
	НомерДоговора = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ContractID", "");
	ОснованиеОтпуска = СтрШаблон(НСтр("ru = '%1 №%2';
										|en = '%1 No.%2'"), НаименованиеДоговора, НомерДоговора);
	ДатаЧислом = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ContractDate");
	Если ЗначениеЗаполнено(ДатаЧислом) Тогда
		ОснованиеОтпуска = СтрШаблон(НСтр("ru = '%1 от %2';
											|en = '%1 from %2'"), ОснованиеОтпуска, ПредставлениеДатыИзЧисла(ДатаЧислом));
	КонецЕсли;
	Шапка.Вставить("ОснованиеОтпуска", ОснованиеОтпуска);
	
	ПунктПогрузки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipFrom.GLN", "");
	Шапка.Вставить("ПунктПогрузки", ПунктПогрузки);
	
	АдресПунктаПогрузки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipFrom.Address", "");
	Шапка.Вставить("АдресПунктаПогрузки", АдресПунктаПогрузки);
	
	ПунктРазгрузки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.GLN", ReceiverGLN);
	Шапка.Вставить("ПунктРазгрузки", ПунктРазгрузки);
	
	АдресПунктаРазгрузки = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(
		DeliveryNote, "ShipTo.Address", ЮридическийАдресГрузополучателя);
	Шапка.Вставить("АдресПунктаРазгрузки", АдресПунктаРазгрузки);
	
	Currency = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Currency", "");
	Шапка.Вставить("Валюта", Currency);
	
	ШапкаДокумента = Макет.ПолучитьОбласть("Шапка");
	ШапкаДокумента.Параметры.Заполнить(Шапка);
	Результат.Вывести(ШапкаДокумента);
	ШапкаТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");
	Результат.Вывести(ШапкаТаблицы);
	LineItems = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DespatchAdviceLogisticUnitLineItem.LineItem");
	
	Для Каждого LineItem Из LineItems Цикл
		
		ДанныеСтроки = Новый Структура();
		ДанныеСтроки.Вставить("НомерСтроки", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemNumber"));
		ДанныеСтроки.Вставить("GTINТовара", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemID"));
		ДанныеСтроки.Вставить("НаименованиеТовара", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemName"));
		ЕдиницаИзмерения = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemQuantitySPT");
		ДанныеСтроки.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
		ДанныеСтроки.Вставить("Количество", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "QuantityDespatched"));
		ДанныеСтроки.Вставить("Цена", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemPrice"));
		Стоимость = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemAmount");
		ДанныеСтроки.Вставить("Стоимость", Стоимость);
		ДанныеСтроки.Вставить("СтавкаНДС", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "TaxRate"));
		СтоимостьБезНалогов = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(LineItem, "LineItemAmountWithoutCharges");
		ДанныеСтроки.Вставить("СуммаНДС", Макс(0, Стоимость - СтоимостьБезНалогов));
		ДанныеСтроки.Вставить("СтоимостьСНДС", Стоимость);
		СтрокаДокумента = Макет.ПолучитьОбласть("Строка");
		СтрокаДокумента.Параметры.Заполнить(ДанныеСтроки);
		Результат.Вывести(СтрокаДокумента);
		
	КонецЦикла;
	
	Total = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Total");
	Итоги = Новый Структура();
	Итоги.Вставить("ИтогоКоличество", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Total, "TotalLineItemQuantity"));
	Итоги.Вставить("ИтогоСтоимость", РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Total, "TotalAmount"));
	TotalAmount = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Total, "TotalAmount");
	TotalAmountWithoutCharges = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Total, "TotalAmountWithoutCharges");
	Итоги.Вставить("ИтогоНДС", Макс(0, TotalAmount - TotalAmountWithoutCharges));
	Итоги.Вставить("ИтогоСтоимостьСНДС", TotalAmount);
	
	ИтогиПоТаблице = Макет.ПолучитьОбласть("Итого");
	ИтогиПоТаблице.Параметры.Заполнить(Итоги);
	Результат.Вывести(ИтогиПоТаблице);
	
	Подвал = Новый Структура();
	
	ShipperContact = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Contact");
	Подвал.Вставить("ОтпускРазрешил", ShipperContact);
	
	ShipFromContact = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipFrom.Contact");
	Подвал.Вставить("СдалГрузоотправитель", ShipFromContact);
	
	DeliveryContact = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.Carrier.DeliveryContact", "");
	Подвал.Вставить("ТоварКПеревозкеПринял", DeliveryContact);
	
	ДанныеДоверенности = Новый Массив(); // Массив из Строка
	ДоверенностьНомер = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.Carrier.ProxyID");
	Если ЗначениеЗаполнено(ДоверенностьНомер) Тогда
		ДанныеДоверенности.Добавить(ДоверенностьНомер);
	КонецЕсли;
	ДатаЧислом = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.Carrier.ProxyDate");
	Если ЗначениеЗаполнено(ДатаЧислом) Тогда
		ДанныеДоверенности.Добавить(ПредставлениеДатыИзЧисла(ДатаЧислом));
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеДоверенности) Тогда
		Подвал.Вставить("ДоверенностьНомерДата", СтрСоединить(ДанныеДоверенности, ", "));
	КонецЕсли;
	
	IssuingProxyName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.Carrier.PartyIssuingProxyName", "");
	Подвал.Вставить("ДоверенностьВыдал", IssuingProxyName);
	
	МассивДокументов = Новый Массив(); // Массив из Строка
	СопроводительныеДокументы = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Document");
	
	Для Каждого ДанныеДокумента Из СопроводительныеДокументы Цикл
		
		Наименование = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ДанныеДокумента, "DocumentName");
		
		НомерДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ДанныеДокумента, "DocumentID", "");
		Если ЗначениеЗаполнено(НомерДокумента) Тогда
			Наименование = СтрШаблон(НСтр("ru = '%1 №%2';
											|en = '%1 No.%2'"), Наименование, НомерДокумента); 
		КонецЕсли;
		
		ДатаЧислом = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ДанныеДокумента, "DocumentDate");
		Если ЗначениеЗаполнено(ДатаЧислом) Тогда
			Наименование = СтрШаблон(НСтр("ru = '%1 от %2';
											|en = '%1 from %2'"), Наименование, ПредставлениеДатыИзЧисла(ДатаЧислом)); 
		КонецЕсли;
		
		МассивДокументов.Добавить(Наименование);
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(МассивДокументов) Тогда
		Подвал.Вставить("ДокументыСТоваром", СтрСоединить(МассивДокументов, "; "));
	КонецЕсли;
	
	ПодвалДокумента = Макет.ПолучитьОбласть("Подвал");
	ПодвалДокумента.Параметры.Заполнить(Подвал);
	Результат.Вывести(ПодвалДокумента);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область BLRAPN

// Читает параметры титула покупателя товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. РаспознатьСообщение
//
Функция ПараметрыУведомленияОбУточнении(ДвоичныеДанные)
	
	Результат = ТрансграничныйЭДО.НовыеПараметрыДокумента();
	Результат.ТипДокумента = ФорматыЭД().УведомлениеОбУточнении.Ключ;
	Результат.ОтражениеВУчете = Неопределено;
	Возврат Результат;
	
КонецФункции

// Формирует представление титула покупателя товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. ПредставлениеДанных
//
Функция ПредставлениеУведомленияОбУточнении(ДвоичныеДанные)
	
	Acknowledgement = ПрочитатьУведомленияОбУточнении(ДвоичныеДанные);
	Если Acknowledgement = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый ТабличныйДокумент();
	
	Макет = ПолучитьМакет(ФорматыЭД().УведомлениеОбУточнении.Макет);
	Результат.Вывести(Макет.ПолучитьОбласть("Отступ"));
	РамкаВерх = Макет.ПолучитьОбласть("РамкаВерх");
	Результат.Вывести(РамкаВерх);
	
	Документ = Новый Структура();
	
	CreationDateTime = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "CreationDateTime");
	Документ.Вставить("CreationDateTime", ПредставлениеДатыИзЧисла(CreationDateTime));
	
	Description = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "ErrorOrAcknowledgement.Description", "");
	Документ.Вставить("Description", Description);
	
	ОбластьДокумент = Макет.ПолучитьОбласть("Документ");
	ОбластьДокумент.Параметры.Заполнить(Документ);
	Результат.Вывести(ОбластьДокумент);
	
	Разделитель = Макет.ПолучитьОбласть("Разделитель");
	Результат.Вывести(Разделитель);
	Основание = Новый Структура();
	
	DeliveryNoteID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "DeliveryNoteID");
	Основание.Вставить("DeliveryNoteID", DeliveryNoteID);
	
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "DeliveryNoteDate");
	Основание.Вставить("DeliveryNoteDate", ПредставлениеДатыИзЧисла(DeliveryNoteDate));
	
	ОбластьОснование = Макет.ПолучитьОбласть("Основание");
	ОбластьОснование.Параметры.Заполнить(Основание);
	Результат.Вывести(ОбластьОснование);
	
	Результат.Вывести(Разделитель);
	Отправитель = Новый Структура();
	
	ShipperGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "Shipper.GLN");
	Отправитель.Вставить("ShipperGLN", ShipperGLN);
	
	ОбластьОтправитель = Макет.ПолучитьОбласть("Отправитель");
	ОбластьОтправитель.Параметры.Заполнить(Отправитель);
	Результат.Вывести(ОбластьОтправитель);
	
	Результат.Вывести(Разделитель);
	Получатель = Новый Структура();
	
	ReceiverGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "Receiver.GLN");
	Получатель.Вставить("ReceiverGLN", ReceiverGLN);
	
	ОбластьПолучатель = Макет.ПолучитьОбласть("Получатель");
	ОбластьПолучатель.Параметры.Заполнить(Получатель);
	Результат.Вывести(ОбластьПолучатель);
	
	РамкаНиз = Макет.ПолучитьОбласть("РамкаНиз");
	Результат.Вывести(РамкаНиз);
	
	Возврат Результат;
	
КонецФункции

// Возвращает содержание BLRAPN
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
//
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НовоеОписаниеФайлаДокумента
//
Функция СодержаниеУведомленияОбУточнении(ДвоичныеДанные)
	
	Acknowledgement = ПрочитатьУведомленияОбУточнении(ДвоичныеДанные);
	
	Если Acknowledgement = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ФорматыЭДО.НовоеОписаниеФайлаДокумента();
	
	Результат.ТипДокумента = Перечисления.ТипыДокументовЭДО.УведомлениеОбУточнении;
	Результат.НомерДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "DeliveryNoteID");
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "DeliveryNoteDate");
	Результат.ДатаДокумента = ДатаИзЧисла(DeliveryNoteDate);
	Результат.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный;
	УчастникТЭДО = ТрансграничныйЭДО.ОписаниеУчастника();
	УчастникТЭДО.КодАльфа3Страны = КодАльфа3Страны();
	УчастникТЭДО.GLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "Shipper.GLN");
	Результат.Получатель.УчастникТЭДО = УчастникТЭДО;
	УчастникТЭДО = ТрансграничныйЭДО.ОписаниеУчастника();
	УчастникТЭДО.GLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Acknowledgement, "Receiver.GLN");
	Результат.Отправитель.УчастникТЭДО = УчастникТЭДО;
	Результат.Формат = ФорматыЭД().УведомлениеОбУточнении.Код;
	
	Возврат Результат;
	
КонецФункции

// Читает товарную накладную и возвращает XDTO-объект
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Неопределено, ОбъектXDTO -
//  
Функция ПрочитатьУведомленияОбУточнении(ДвоичныеДанные)
	
	Формат = ФорматыЭД().УведомлениеОбУточнении;
	ТипДанных = РаботаСФайламиБЭД.ПолучитьТипЗначенияCML(Формат.ТипДанных, Формат.ПространствоИмен);
	
	Попытка
		
		ДвоичныеДанныеФайла = ОбщегоНазначенияБЭД.ДобавитьПространствоИмен(
			ДвоичныеДанные, Формат.ПространствоИмен, Кодировка());
		Возврат РаботаСФайламиБЭД.ПрочитатьXDTO(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения(), ТипДанных);
		
	Исключение
		
		Операция = НСтр("ru = 'Чтение товарной накладной Республики Беларусь';
						|en = 'Read the invoice of the Republic of Belarus'");
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ТЭДО, ТекстОшибки, ТекстОшибки);
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Формирует данные уведомления об уточнении
// 
// Параметры:
//  ДанныеЭД - см. ФорматыЭДО.НовыеДанныеДляФормированияСлужебногоСообщения
// 
// Возвращаемое значение:
// - см. ФорматыЭДО.НовыйРезультатФормированияДокументаПоУчету
//
Функция УведомлениеОбУточнении_Данные(ДанныеЭД)
	
	Результат = ФорматыЭДО.НовыйРезультатФормированияДокументаПоУчету();
	ДвоичныеДанные = РаботаСФайлами.ДвоичныеДанныеФайла(ДанныеЭД.Основание.Ссылка);
	DeliveryNote = ПрочитатьТоварнуюНакладную(ДвоичныеДанные);
	Ошибки = Результат.Ошибки.ЗаполнениеДанных;
	
	Если DeliveryNote = Неопределено Тогда
		Результат.ЕстьОшибки = Истина;
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Ошибки, ОшибкаФормированияДокументаПриЧтенииОснования());
		Возврат Результат;
	КонецЕсли;
	
	РезультатФормирования = СформироватьBLRAPN(ДанныеЭД.ТекстУточнения, DeliveryNote);
	
	Если ЗначениеЗаполнено(РезультатФормирования.Ошибки) Тогда
		Результат.Ошибки.ЗаполнениеДанных = РезультатФормирования.Ошибки;
		Результат.ЕстьОшибки = Истина;
		Возврат Результат;
	КонецЕсли;
	
	ФорматЭД = ФорматыЭД().УведомлениеОбУточнении;
	ДвоичныеДанные = ОбщегоНазначенияБЭДКлиентСервер.ПустыеДвоичныеДанные();
	ТекстОшибки = "";
	
	Попытка
		
		ДвоичныеДанные = ДвоичныеДанныеОбъектаXDTO(РезультатФормирования.Объект, , , ФорматЭД.ТипДанных);
		
	Исключение
		
		Операция = НСтр("ru = 'Формирование уведомления об уточнении накладной Республики Беларусь';
						|en = 'Generate the notification on specification of the invoice of the Republic of Belarus'");
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ТЭДО, ТекстОшибки, ТекстОшибки);
			
	КонецПопытки;
	
	Если НЕ ЗначениеЗаполнено(ДвоичныеДанные) Тогда
		
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Результат.Ошибки.ЗаполнениеДанных, ТекстОшибки);
		Результат.ЕстьОшибки = Истина;
		Возврат Результат;
		
	КонецЕсли;
	
	Результат.Документ.ДвоичныеДанные = УдалитьПространствоИмен(ДвоичныеДанные, ФорматЭД.ТипДанных);
	Результат.Содержание = СодержаниеУведомленияОбУточнении(Результат.Документ.ДвоичныеДанные);
	
	ИдентификаторДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(РезультатФормирования.Объект, "DocumentID");
	Результат.Документ.ИмяФайла = ИмяФайлаBLRAPN(ИдентификаторДокумента,
		ДанныеЭД.Участники.Отправитель.Идентификатор, ДанныеЭД.Участники.Получатель.Абонент.Идентификатор);
	
	Возврат Результат;
	
КонецФункции

// Формирует XDTO-объект BLRAPN.
// 
// Параметры:
//  ТекстУточнения - Строка
//  DeliveryNote - ОбъектXDTO
// 
// Возвращаемое значение:
//  Структура:
//  * Объект - Неопределено, ОбъектXDTO -
//  * Ошибки - Массив из см. НовоеОписаниеОшибокФормированияДокумента
//
Функция СформироватьBLRAPN(ТекстУточнения, DeliveryNote)
	
	Ошибки = Новый Массив();
	Результат = Новый Структура("Объект, Ошибки", Неопределено, Ошибки);
	ФорматЭД = ФорматыЭД().УведомлениеОбУточнении;
	Acknowledgement = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных, ФорматЭД.ПространствоИмен);
	
	DocumentID = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "DocumentID", DocumentID, Истина, Ошибки);
	
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "FunctionCode", "27", Истина, Ошибки);
	
	CreationDateTime = ДатаВФорматеTopby(ТекущаяДатаСеанса());
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "CreationDateTime", CreationDateTime, Истина, Ошибки);
	
	DeliveryNoteID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteID");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "DeliveryNoteID", DeliveryNoteID, Истина, Ошибки);
	
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteDate");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "DeliveryNoteDate", DeliveryNoteDate, Истина, Ошибки);
	
	ReferenceDocument = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(
		ФорматЭД.ТипДанных + ".ReferenceDocument", ФорматЭД.ПространствоИмен);
	
	ReferenceDocumentID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DocumentID");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ReferenceDocument, "ID", ReferenceDocumentID, Истина, Ошибки);
	
	ReferenceDocumentDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "CreationDateTime");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ReferenceDocument, "Date", ReferenceDocumentDate, Истина, Ошибки);
	
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ReferenceDocument, "Type", ФорматыЭД().ТоварнаяНакладная.Код, Истина, Ошибки);
	
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "ReferenceDocument", ReferenceDocument, Истина, Ошибки);
	
	Shipper = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".Shipper", ФорматЭД.ПространствоИмен);
	ShipperGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.GLN");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Shipper, "GLN", ShipperGLN, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "Shipper", Shipper, Истина, Ошибки);
	
	Receiver = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".Receiver", ФорматЭД.ПространствоИмен);
	ReceiverGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.GLN");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Receiver, "GLN", ReceiverGLN, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Acknowledgement, "Receiver", Receiver, Истина, Ошибки);
	
	ErrorOrAcknowledgement = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(
		ФорматЭД.ТипДанных + ".ErrorOrAcknowledgement", ФорматЭД.ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ErrorOrAcknowledgement, "Code", "2750", Истина, Ошибки);
	Если ЗначениеЗаполнено(ТекстУточнения) Тогда
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ErrorOrAcknowledgement, "Description", ТекстУточнения, Истина, Ошибки);
	КонецЕсли;
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(
		Acknowledgement, "ErrorOrAcknowledgement", ErrorOrAcknowledgement, Истина, Ошибки);
	
	Если НЕ ЗначениеЗаполнено(Ошибки) Тогда
		Результат.Объект = Acknowledgement;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Формирует имя файла BLRAPN.
// 
// Параметры:
//  ИдентификаторДокумента - Строка
//  ИдентификаторОтправителя - Строка
//  ИдентификаторПолучателя - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяФайлаBLRAPN(ИдентификаторДокумента, ИдентификаторОтправителя, ИдентификаторПолучателя)
	
	Возврат СтрШаблон("%1_%2_%3_%4.xml", ФорматыЭД().УведомлениеОбУточнении.Код,
		ИдентификаторПолучателя, ИдентификаторОтправителя, ИдентификаторДокумента);
	
КонецФункции

#КонецОбласти

#Область BLRDNR

// Читает параметры титула покупателя товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. РаспознатьСообщение
//
Функция ПараметрыТоварнойНакладной_ТитулПокупателя(ДвоичныеДанные)
	
	Результат = ТрансграничныйЭДО.НовыеПараметрыДокумента();
	Результат.ТипДокумента = ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя.Ключ;
	Результат.ОтражениеВУчете = Неопределено;
	Возврат Результат;
	
КонецФункции

// Формирует представление титула покупателя товарной накладной
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  см. ПредставлениеДанных
//
Функция ПредставлениеТоварнойНакладной_ТитулПокупателя(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную_ТитулПокупателя(ДвоичныеДанные);
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = Новый ТабличныйДокумент();
	
	Макет = ПолучитьМакет(ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя.Макет);
	Результат.Вывести(Макет.ПолучитьОбласть("Отступ"));
	РамкаВерх = Макет.ПолучитьОбласть("РамкаВерх");
	Результат.Вывести(РамкаВерх);
	
	Документ = Новый Структура();
	
	DeliveryNoteType = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteType");
	Если DeliveryNoteType = "270" Тогда
		DeliveryNoteType = НСтр("ru = 'Товарная накладная';
								|en = 'Invoice'");
	ИначеЕсли DeliveryNoteType = "701" Тогда
		DeliveryNoteType = НСтр("ru = 'Иной документ';
								|en = 'Other document'");
	КонецЕсли;
	Документ.Вставить("DeliveryNoteType", DeliveryNoteType);
	
	DocumentID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DocumentID");
	Документ.Вставить("DocumentID", DocumentID);
	
	CreationDateTime = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "CreationDateTime");
	Документ.Вставить("CreationDateTime", ПредставлениеДатыИзЧисла(CreationDateTime));
	
	FunctionCode = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "FunctionCode");
	Если FunctionCode = "11" Тогда
		FunctionCode = НСтр("ru = 'Ответ на накладную';
							|en = 'Response to the invoice'");
	ИначеЕсли FunctionCode = "31" Тогда
		FunctionCode = НСтр("ru = 'Копия';
							|en = 'Copy'");
	ИначеЕсли FunctionCode = "21" Тогда
		FunctionCode = НСтр("ru = 'Подтверждение электронной накладной грузополучателем';
							|en = 'Confirmation of the electronic waybill by the recipient'");
	КонецЕсли;
	Документ.Вставить("FunctionCode", FunctionCode);
	
	ОбластьДокумент = Макет.ПолучитьОбласть("Документ");
	ОбластьДокумент.Параметры.Заполнить(Документ);
	Результат.Вывести(ОбластьДокумент);
	
	Разделитель = Макет.ПолучитьОбласть("Разделитель");
	Результат.Вывести(Разделитель);
	Основание = Новый Структура();
	
	ReferenceDocumentID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ReferenceDocument.ID");
	Основание.Вставить("ReferenceDocumentID", ReferenceDocumentID);
	
	ReferenceDocumentDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ReferenceDocument.Date");
	Основание.Вставить("ReferenceDocumentDate", ПредставлениеДатыИзЧисла(ReferenceDocumentDate));
	
	DeliveryNoteID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteID");
	Основание.Вставить("DeliveryNoteID", DeliveryNoteID);
	
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "DeliveryNoteDate");
	Основание.Вставить("DeliveryNoteDate", ПредставлениеДатыИзЧисла(DeliveryNoteDate));
	
	ОбластьОснование = Макет.ПолучитьОбласть("Основание");
	ОбластьОснование.Параметры.Заполнить(Основание);
	Результат.Вывести(ОбластьОснование);
	
	Результат.Вывести(Разделитель);
	Отправитель = Новый Структура();
	
	ShipperGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.GLN");
	Отправитель.Вставить("ShipperGLN", ShipperGLN);
	
	ShipperName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Name");
	Отправитель.Вставить("ShipperName", ShipperName);
	
	ShipperAddress = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.Address");
	Отправитель.Вставить("ShipperAddress", ShipperAddress);
	
	ShipperVATRegistrationNumber = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Shipper.VATRegistrationNumber");
	Отправитель.Вставить("ShipperVATRegistrationNumber", ShipperVATRegistrationNumber);
	
	ОбластьОтправитель = Макет.ПолучитьОбласть("Отправитель");
	ОбластьОтправитель.Параметры.Заполнить(Отправитель);
	Результат.Вывести(ОбластьОтправитель);
	
	Результат.Вывести(Разделитель);
	Получатель = Новый Структура();
	
	ReceiverGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.GLN");
	Получатель.Вставить("ReceiverGLN", ReceiverGLN);
	
	ReceiverName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.Name");
	Получатель.Вставить("ReceiverName", ReceiverName);
	
	ReceiverAddress = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.Address");
	Получатель.Вставить("ReceiverAddress", ReceiverAddress);
	
	ReceiverVATRegistrationNumber = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Receiver.VATRegistrationNumber");
	Получатель.Вставить("ReceiverVATRegistrationNumber", ReceiverVATRegistrationNumber);
	
	ShipToContact = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "ShipTo.Contact");
	Получатель.Вставить("ShipToContact", ShipToContact);
	
	ОбластьПолучатель = Макет.ПолучитьОбласть("Получатель");
	ОбластьПолучатель.Параметры.Заполнить(Получатель);
	Результат.Вывести(ОбластьПолучатель);
	
	Report = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Report");
	
	Если Report <> Неопределено Тогда
		
		Результат.Вывести(Разделитель);
		Акт = Новый Структура();
		
		ReportName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Report.ReportName");
		Акт.Вставить("ReportName", ReportName);
		
		ReportID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Report.ReportID");
		Акт.Вставить("ReportID", ReportID);
		
		ReportDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(DeliveryNote, "Report.ReportDate");
		Акт.Вставить("ReportDate", ПредставлениеДатыИзЧисла(ReportDate));
		
		ОбластьАкт = Макет.ПолучитьОбласть("АктПриемки");
		ОбластьАкт.Параметры.Заполнить(Акт);
		Результат.Вывести(ОбластьАкт);
		
	КонецЕсли;
	
	РамкаНиз = Макет.ПолучитьОбласть("РамкаНиз");
	Результат.Вывести(РамкаНиз);
	
	Возврат Результат;
	
КонецФункции

// Читает титул покупателя товарной накладной и возвращает XDTO-объект
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
// 
// Возвращаемое значение:
//  Неопределено, ОбъектXDTO -
//  
Функция ПрочитатьТоварнуюНакладную_ТитулПокупателя(ДвоичныеДанные)
	
	Формат = ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя;
	ТипДанных = РаботаСФайламиБЭД.ПолучитьТипЗначенияCML(Формат.ТипДанных, Формат.ПространствоИмен);
	
	Попытка
		
		ДвоичныеДанныеФайла = ОбщегоНазначенияБЭД.ДобавитьПространствоИмен(
			ДвоичныеДанные, Формат.ПространствоИмен, Кодировка());
		Возврат РаботаСФайламиБЭД.ПрочитатьXDTO(ДвоичныеДанныеФайла.ОткрытьПотокДляЧтения(), ТипДанных);
		
	Исключение
		
		Операция = НСтр("ru = 'Чтение титула покупателя товарной накладной Республики Беларусь';
						|en = 'Read the customer title of the invoice of the Republic of Belarus'");
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбработкаНеисправностейБЭД.ОбработатьОшибку(Операция,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ТЭДО, ТекстОшибки, ТекстОшибки);
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Возвращает содержание титула покупателя товарной накладной
//
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
//
// Возвращаемое значение:
//  - Неопределено
//  - см. ФорматыЭДО.НовоеОписаниеФайлаДокумента
//
Функция СодержаниеТоварнойНакладной_ТитулПокупателя(ДвоичныеДанные)
	
	DeliveryNote = ПрочитатьТоварнуюНакладную_ТитулПокупателя(ДвоичныеДанные);
	
	Если DeliveryNote = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ФорматыЭДО.НовоеОписаниеФайлаДокумента();
	Результат.ТипРегламента = Перечисления.ТипыРегламентовЭДО.Формализованный;
	Результат.Формат = ФорматыЭД().ТоварнаяНакладная.Код;
	
	Возврат Результат;
	
КонецФункции

// Формирует данные документа BLRDNR.
// 
// Параметры:
//  Данные - см. ТрансграничныйЭДО.ДанныеОбъектаУчета
// 
// Возвращаемое значение:
//  см. ФорматыЭДО.НовыйРезультатФормированияДокумента
//
Функция СформироватьBLRDNR(Данные)
	
	Результат = ФорматыЭДО.НовыйРезультатФормированияДокумента();
	
	Если Данные.ДанныеXDTO = Неопределено Тогда
		
		РезультатФормирования = СформироватьОбъектBLRDNR(Данные);
		
		Если ЗначениеЗаполнено(РезультатФормирования.Ошибки) Тогда
			Результат.Ошибки = РезультатФормирования.Ошибки;
			Возврат Результат;
		КонецЕсли;
		
		Данные.ДанныеXDTO = РезультатФормирования.Объект;
		
	КонецЕсли;
	
	ФорматЭД = ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя;
	ДвоичныеДанные = ДвоичныеДанныеОбъектаXDTO(Данные.ДанныеXDTO, , , ФорматЭД.ТипДанных);
	ДвоичныеДанные = УдалитьПространствоИмен(ДвоичныеДанные, ФорматЭД.ТипДанных);
	Результат.Документ.ДвоичныеДанные = ДвоичныеДанные;
	ИдентификаторДокумента = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(Данные.ДанныеXDTO, "DocumentID");
	Результат.Документ.ИмяФайла = ИмяФайлаBLRDNR(
		ИдентификаторДокумента, Данные.ИдентификаторОтправителя, Данные.ИдентификаторПолучателя);
	
	Возврат Результат;
	
КонецФункции

// Формирует имя файла BLRDNR.
// 
// Параметры:
//  ИдентификаторДокумента - Строка
//  ИдентификаторОтправителя - Строка
//  ИдентификаторПолучателя - Строка
// 
// Возвращаемое значение:
//  Строка
//
Функция ИмяФайлаBLRDNR(ИдентификаторДокумента, ИдентификаторОтправителя, ИдентификаторПолучателя)
	
	Возврат СтрШаблон("%1_%2_%3_%4.xml", ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя.Код,
		ИдентификаторПолучателя, ИдентификаторОтправителя, ИдентификаторДокумента);
	
КонецФункции

// Формирует XDTO-объект BLRDNR.
// 
// Параметры:
//  Данные - см. ТрансграничныйЭДО.ДанныеОбъектаУчета
// 
// Возвращаемое значение:
//  Структура:
//  * Объект - Неопределено, ОбъектXDTO -
//  * Ошибки - Массив из см. ФорматыЭДО.НовоеОписаниеОшибокФормированияДокумента
//
Функция СформироватьОбъектBLRDNR(Данные)
	
	Ошибки = Новый Массив();
	Результат = Новый Структура("Объект, Ошибки", Неопределено, Ошибки);
	ИсходныйДокумент = ПрочитатьТоварнуюНакладную(Данные.Основание.ДвоичныеДанные);
		
	Если ИсходныйДокумент = Неопределено Тогда
		
		ТекстОшибки = ОшибкаФормированияДокументаПриЧтенииОснования();
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Ошибки, ТекстОшибки);
		Возврат Неопределено;
		
	КонецЕсли;
	
	Если ИсходныйДокумент = Неопределено Тогда
		
		ТекстОшибки = НСтр("ru = 'Не удалось прочитать исходный электронный документ';
							|en = 'Cannot read the original electronic document'");
		ОбщегоНазначенияБЭД.ДобавитьОшибку(Ошибки, ТекстОшибки);
		Возврат Результат;
		
	КонецЕсли;
	
	ФорматЭД = ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя;
	DeliveryNote = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных, ФорматЭД.ПространствоИмен);
	
	DeliveryNoteType = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "DeliveryNoteType", "701");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "DeliveryNoteType", DeliveryNoteType, Истина, Ошибки);
	
	DocumentID = СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", "");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "DocumentID", DocumentID, Истина, Ошибки);
	
	CreationDateTime = ДатаВФорматеTopby(ТекущаяДатаСеанса());
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "CreationDateTime", CreationDateTime, Истина, Ошибки);
	
	FunctionCode = ?(ЗначениеЗаполнено(Данные.ДанныеРучногоФормированияТитула), "21", "11");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "FunctionCode", FunctionCode, Истина, Ошибки);
	
	ReferenceDocument = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(
		ФорматЭД.ТипДанных + ".ReferenceDocument", ФорматЭД.ПространствоИмен);
	
	ReferenceDocumentID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "DocumentID");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ReferenceDocument, "ID", ReferenceDocumentID, Истина, Ошибки);
	
	ReferenceDocumentDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "CreationDateTime");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ReferenceDocument, "Date", ReferenceDocumentDate, Истина, Ошибки);
	
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "ReferenceDocument", ReferenceDocument, Истина, Ошибки);
	
	DeliveryNoteID = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "DeliveryNoteID");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "DeliveryNoteID", DeliveryNoteID, Истина, Ошибки);
	
	DeliveryNoteDate = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "DeliveryNoteDate");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "DeliveryNoteDate", DeliveryNoteDate, Истина, Ошибки);
	
	ДанныеТитула = Данные.ДанныеРучногоФормированияТитула; // см. ДанныеРучногоФормированияТитулаТоварнойНакладной
	АктЗаполненКорректно = ЗначениеЗаполнено(ДанныеТитула.НазваниеАкта)
		И ЗначениеЗаполнено(ДанныеТитула.НомерАкта) И ЗначениеЗаполнено(ДанныеТитула.ДатаСоставленияАкта);
	
	Если АктЗаполненКорректно Тогда
		
		Report = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".Report", ФорматЭД.ПространствоИмен);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Report, "ReportID", ДанныеТитула.НомерАкта, Истина, Ошибки);
		ReportDate = ДатаВФорматеTopby(ДанныеТитула.ДатаСоставленияАкта, Истина);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Report, "ReportDate", ReportDate, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Report, "ReportName", ДанныеТитула.НазваниеАкта, Истина, Ошибки);
		РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "Report", Report, Истина, Ошибки);
		
	КонецЕсли;
	
	Shipper = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".Shipper", ФорматЭД.ПространствоИмен);
	ShipperGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Shipper.GLN");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Shipper, "GLN", ShipperGLN, Истина, Ошибки);
	ShipperName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Shipper.Name");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Shipper, "Name", ShipperName, Истина, Ошибки);
	ShipperAddress = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Shipper.Address");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Shipper, "Address", ShipperAddress, Истина, Ошибки);
	ShipperVATRegistrationNumber = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(
		ИсходныйДокумент, "Shipper.VATRegistrationNumber");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Shipper,
		"VATRegistrationNumber", ShipperVATRegistrationNumber, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "Shipper", Shipper, Истина, Ошибки);
	
	Receiver = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".Receiver", ФорматЭД.ПространствоИмен);
	ReceiverGLN = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Receiver.GLN");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Receiver, "GLN", ReceiverGLN, Истина, Ошибки);
	ReceiverName = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Receiver.Name");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Receiver, "Name", ReceiverName, Истина, Ошибки);
	ReceiverAddress = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(ИсходныйДокумент, "Receiver.Address");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Receiver, "Address", ReceiverAddress, Истина, Ошибки);
	ReceiverVATRegistrationNumber = РаботаСФайламиБЭД.ЗначениеСвойстваXDTO(
		ИсходныйДокумент, "Receiver.VATRegistrationNumber");
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(Receiver,
		"VATRegistrationNumber", ReceiverVATRegistrationNumber, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "Receiver", Receiver, Истина, Ошибки);
	
	ShipToContact = СтрШаблон("%1, %2", ДанныеТитула.ДолжностьЛицаПринявшегоГруз, ДанныеТитула.ФИОЛицаПринявшегоГруз);
	ShipTo = РаботаСФайламиБЭД.ПолучитьОбъектТипаCML(ФорматЭД.ТипДанных + ".ShipTo", ФорматЭД.ПространствоИмен);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(ShipTo, "Contact", ShipToContact, Истина, Ошибки);
	РаботаСФайламиБЭД.ЗаполнитьСвойствоXDTO(DeliveryNote, "ShipTo", ShipTo, Истина, Ошибки);
	
	Если НЕ ЗначениеЗаполнено(Ошибки) Тогда
		Результат.Объект = DeliveryNote;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Описывает данные ручного формирования титула BLRDNR.
// 
// Возвращаемое значение:
//  Структура:
//  * ДолжностьЛицаПринявшегоГруз - Строка
//  * ФИОЛицаПринявшегоГруз - Строка
//  * НазваниеАкта - Строка
//  * НомерАкта - Строка
//  * ДатаСоставленияАкта - Дата
//
Функция ДанныеРучногоФормированияТитулаТоварнойНакладной() Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ДолжностьЛицаПринявшегоГруз", "");
	Результат.Вставить("ФИОЛицаПринявшегоГруз", "");
	Результат.Вставить("НазваниеАкта", "");
	Результат.Вставить("НомерАкта", "");
	Результат.Вставить("ДатаСоставленияАкта", '00010101');
	Возврат Результат;
	
КонецФункции

// см. ОписаниеДанныхОбъектаУчета
Функция ТоварнаяНакладнаяТитулПокупателя_ДанныеУчета(ДанныеУчета, Параметры)
	
	Результат = ИнтеграцияЭДО.НовыйРезультатФормированияДанныхОбъектаУчета();
	ФорматЭД = ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя;
	Результат.Формат = ФорматЭД.Код;
	ДанныеТЭДО = ТрансграничныйЭДО.ДанныеОбъектаУчета();
	Результат.ДанныеТЭДО = ДанныеТЭДО;
	
	Если ЗначениеЗаполнено(ДанныеУчета) Тогда
		
		Данные = Неопределено;
		Отказ = Ложь;
		ПараметрыЗаполнения = ТрансграничныйЭДО.НовыеПараметрыЗаполненияПакетаПоОбъектуУчета();
		ЗаполнитьЗначенияСвойств(ПараметрыЗаполнения, ФорматЭД, "ПространствоИмен, ТипДанных");
		ТрансграничныйЭДО.ЗаполнитьДанныеПакетаПоОбъектуУчета(Данные, ДанныеУчета, ПараметрыЗаполнения, Отказ);
		
		Если Отказ Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если Данные <> Неопределено Тогда
			ДанныеТЭДО.ДанныеXDTO = Данные;
			Возврат Результат;
		КонецЕсли;
		
	КонецЕсли;
	
	ДанныеТитула = Параметры.ДанныеРучногоФормированияТитула; // см. ДанныеРучногоФормированияТитулаТоварнойНакладной
	Если НЕ ЗначениеЗаполнено(ДанныеТитула) Тогда
		Результат.ОшибкаФормированияВПрикладнойЧасти = Истина;
		Возврат Результат;
	КонецЕсли;
	
	ДанныеТЭДО.ДанныеРучногоФормированияТитула = ДанныеТитула;
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

// Добавляет и заполняет строку в таблице типов элементов регламента.
//
// Параметры:
//  Таблица - ТаблицаЗначений:
//   * КодТранзакции - Строка
//   * ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//   * ТипДокумента - Строка
//  КодТранзакции - Строка
//  ТипЭлементаРегламента - ПеречислениеСсылка.ТипыЭлементовРегламентаЭДО
//  ТипДокумента - Строка
//
Процедура ДобавитьВТаблицуТипЭлементаРегламента(Таблица, КодТранзакции, ТипЭлементаРегламента, ТипДокумента = "")
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.КодТранзакции = КодТранзакции;
	НоваяСтрока.ТипЭлементаРегламента = ТипЭлементаРегламента;
	НоваяСтрока.ТипДокумента = ТипДокумента;
	
КонецПроцедуры

// Описывает ошибку формирования документа, связанную с чтением основания
// 
// Возвращаемое значение:
//  Строка -
//
Функция ОшибкаФормированияДокументаПриЧтенииОснования()
	Возврат НСтр("ru = 'Произошла ошибка получения данных для формирования электронного документа по причине:
			|Не удалось прочитать документ-основание. Подробности в журнале регистрации';
			|en = 'An error occurred when obtaining data for generating an electronic document. Reason:
			|Cannot read the base document. See the event log for details'");
КонецФункции

// Преобразует дату в формат topby.
// 
// Параметры:
//  ДатаДляПреобразования - Дата
//  КраткийФормат - Булево
// 
// Возвращаемое значение:
//  Строка
//  
Функция ДатаВФорматеTopby(ДатаДляПреобразования, КраткийФормат = Ложь)
	Возврат Формат(ДатаДляПреобразования, ?(КраткийФормат, "ДФ=yyyyMMdd", "ДФ=yyyyMMddHHmmss"));
КонецФункции

// Читает дату из числа см. ДатаВФорматеTopby
// 
// Параметры:
//  ДатаЧислом - Число
// 
// Возвращаемое значение:
//  Дата -
//
Функция ДатаИзЧисла(ДатаЧислом)
	Возврат Дата(Формат(ДатаЧислом, "ЧГ=;"));
КонецФункции

// Формирует представление даты из см. ДатаВФорматеTopby
// 
// Параметры:
//  ДатаЧислом - Число
//  ФорматДаты - Строка
// 
// Возвращаемое значение:
//  Строка -
//
Функция ПредставлениеДатыИзЧисла(ДатаЧислом, ФорматДаты = "ДЛФ=D;")
	Возврат Формат(ДатаИзЧисла(ДатаЧислом), ФорматДаты);
КонецФункции

// Получает двоичные данные объекта XDTO.
// 
// Параметры:
//  ОбъектXDTO - ОбъектXDTO
//  Кодировка - Строка
//  УказаниеТипа - Булево
//  ЛокальноеИмя - Строка
// 
// Возвращаемое значение:
//  ДвоичныеДанные - Двоичные данные объекта XDTO
//
Функция ДвоичныеДанныеОбъектаXDTO(ОбъектXDTO, Кодировка = "", УказаниеТипа = Ложь, ЛокальноеИмя = "") Экспорт
	
	Кодировка = ?(ЗначениеЗаполнено(Кодировка), Кодировка, Кодировка());
	НазначениеТипа = ?(УказаниеТипа, НазначениеТипаXML.Явное, НазначениеТипаXML.Неявное);
	Поток = Новый ПотокВПамяти();
	
	НоваяЗапись = Новый ЗаписьXML;
	НоваяЗапись.ОткрытьПоток(Поток, Кодировка);
	
	Если ПустаяСтрока(ЛокальноеИмя) Тогда
		ФабрикаXDTO.ЗаписатьXML(НоваяЗапись, ОбъектXDTO, , , , НазначениеТипа);
	Иначе
		ФабрикаXDTO.ЗаписатьXML(НоваяЗапись, ОбъектXDTO, ЛокальноеИмя, , , НазначениеТипа);
	КонецЕсли;
	
	НоваяЗапись.Закрыть();
	Возврат Поток.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

// Удаляет пространство имен из двоичных данных XML
// 
// Параметры:
//  ДвоичныеДанные - ДвоичныеДанные
//  ТипДанных - Строка
//  Кодировка - Строка
// 
// Возвращаемое значение:
//  ДвоичныеДанные
//
Функция УдалитьПространствоИмен(ДвоичныеДанные, ТипДанных, Кодировка = "") Экспорт
	
	Кодировка = ?(ЗначениеЗаполнено(Кодировка), Кодировка, Кодировка());
	ПотокЧтения = ДвоичныеДанные.ОткрытьПотокДляЧтения();
	Чтение = Новый ЧтениеТекста(ПотокЧтения, Кодировка);
	ТекстДокумента = Чтение.Прочитать();
	Чтение.Закрыть();
	
	Текст = Новый ТекстовыйДокумент;
	Текст.УстановитьТекст(ТекстДокумента);
	Текст.ЗаменитьСтроку(1, СтрШаблон("<%1>", ТипДанных));
	ТекстДокумента = Текст.ПолучитьТекст();
	
	ПотокЗаписи = Новый ПотокВПамяти();
	Запись = Новый ЗаписьТекста(ПотокЗаписи, Кодировка);
	Запись.Записать(ТекстДокумента);
	Запись.Закрыть();
	
	Возврат ПотокЗаписи.ЗакрытьИПолучитьДвоичныеДанные();
	
КонецФункции

// Определяет кодировку текстов
// 
// Возвращаемое значение:
//  Строка
//
Функция Кодировка() Экспорт
	Возврат "utf-8";
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
