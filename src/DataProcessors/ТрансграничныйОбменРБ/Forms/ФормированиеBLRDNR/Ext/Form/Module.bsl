// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Формат = Обработки.ТрансграничныйОбменРБ.ФорматыЭД().ТоварнаяНакладнаяТитулПокупателя.Код;
	ОшибкиПриФормированииДокументов = ПолучитьИзВременногоХранилища(
		Параметры.АдресСведенийОбОшибках); // Массив из см. ЭлектронныеДокументыЭДО.НовоеОписаниеОшибкиФормирования
	ЭлектронныйДокумент = ОшибкиПриФормированииДокументов[0].ОшибкаФормированияВПрикладнойЧасти.ЭлектронныйДокумент;
	Данные = Обработки.ТрансграничныйОбменРБ.ДанныеРучногоФормированияТитулаТоварнойНакладной();

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив(); // Массив Из Строка
	
	Если СоставленАктПриемки Тогда
		
		ШаблонСообщения = НСтр("ru = 'Поле ""%1"" не заполнено';
								|en = 'Field %1 is required'");
		МассивЭлементов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Элементы.НазваниеАкта);
		МассивЭлементов.Добавить(Элементы.НомерАкта);
		МассивЭлементов.Добавить(Элементы.ДатаСоставленияАкта);
		
		Для Каждого Элемент Из МассивЭлементов Цикл
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект[Элемент.ПутьКДанным]) Тогда
				Отказ = Истина;
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Элемент.Заголовок);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Элемент.Имя);
				НепроверяемыеРеквизиты.Добавить(Элемент.Имя);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НепроверяемыеРеквизиты.Количество() > 0 Тогда
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СоставленАктПриемкиПриИзменении(Элемент)
	Элементы.ГруппаАкт.Видимость = СоставленАктПриемки;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодписатьИОтправить(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеТитула = Данные; // см. Обработки.ТрансграничныйОбменРБ.ДанныеРучногоФормированияТитулаТоварнойНакладной
	
	Если СоставленАктПриемки Тогда
		ДанныеТитула.НазваниеАкта = НазваниеАкта;
		ДанныеТитула.НомерАкта = НомерАкта;
		ДанныеТитула.ДатаСоставленияАкта = ДатаСоставленияАкта;
	КонецЕсли;
	
	ДанныеТитула.ДолжностьЛицаПринявшегоГруз = ДолжностьЛицаПринявшегоГруз;
	ДанныеТитула.ФИОЛицаПринявшегоГруз = ФИОЛицаПринявшегоГруз;
	ПодписатьИОтправитьДокумент(ДанныеТитула);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПодписатьИОтправитьДокумент(ДанныеТитула)
	
	НаборДействий = Новый Соответствие();
	ДействиеУтвердить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Утвердить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеУтвердить);
	ДействиеСформироватьОтвет = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.СформироватьОтвет");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеСформироватьОтвет);
	ДействиеПодписать = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Подписать");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеПодписать);
	ДействиеПодготовитьКОтправке = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ПодготовитьКОтправке");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеПодготовитьКОтправке);
	ДействиеОтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеОтправить);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект);
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы.Добавить(ЭлектронныйДокумент);
	ПараметрыВыполненияДействийПоЭДО.ДанныеРучногоФормированияТитула = ДанныеТитула;
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

// Выполняется после выполнения действий по ЭДО.
// 
// Параметры:
//  Результат - см. ЭлектронныеДокументыЭДОКлиент.НовыйРезультатОбработкиДействийИнтеграцииЭДО
//  ДополнительныеПараметры - Структура
&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда 
		
		Если Результат.Свойство("ОшибкиФормирования") И Результат.ОшибкиФормирования.Количество() Тогда
			
			Для Каждого Ошибка Из Результат.ОшибкиФормирования[0].ОшибкиДанных.ЗаполнениеДанных Цикл
				// см. ЭлектронныеДокументыЭДО.НовоеОписаниеОшибкиФормирования
				ОбщегоНазначенияКлиент.СообщитьПользователю(Ошибка.ТекстОшибки);
			КонецЦикла;
			
		ИначеЕсли Результат.Свойство("КонтекстДиагностики")
			И ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(Результат.КонтекстДиагностики) Тогда
			
			ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
			
		Иначе
			
			Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());
			Оповестить("УдалитьИзСпискаДокументовДляФормированияОтветногоТитула", ЭлектронныйДокумент);
			ПараметрыСобытия = ИнтерфейсДокументовЭДОКлиент.НовыеПараметрыСобытияОбновленияСостоянияЭДО();
			ПараметрыСобытия.ЭлектронныеДокументы.Добавить(ЭлектронныйДокумент);
			Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО(), ПараметрыСобытия);
			
			Если Открыта() Тогда
				Закрыть(Истина);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти