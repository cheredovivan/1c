///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИдентификаторТокена = Строка(УникальныйИдентификатор);
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Получение списка доступных банков. Пожалуйста, подождите...';
												|en = 'Getting a list of available banks. Please wait...'");
	
	ОтображатьОписаниеСервиса = (Параметры.ОтображатьОписаниеСервиса
		И Не Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ЕстьНастройкиПодключения());
	
	ОтображатьОписаниеСервиса = Ложь;
	
	ЗаполненыДанныеАутентификации =
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	БИК = Параметры.БИК;
	
	Если ЗначениеЗаполнено(Параметры.НастройкаДляКопирования) Тогда
		ДанныеНастройки = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.ДанныеНастройкиПодключения(
			Параметры.НастройкаДляКопирования);
		Наименование = ДанныеНастройки.Наименование;
		ВыбранныйУчастник = ДанныеНастройки.ИдентификаторУчастника;
	КонецЕсли;
	
	Если ОтображатьОписаниеСервиса Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация;
	ИначеЕсли Не ЗаполненыДанныеАутентификации Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания(
		"ЗаполнитьУчастников",
		0.1,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Не НастройкаСохранена И ЗначениеЗаполнено(ТокенСервиса) Тогда
		Отказ = Истина;
		ОтозватьТокен();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	УстановитьВидимостьДоступность(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ПарольИПППриИзменении(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольИППНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		"ПарольИПП");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПоясненияПодключенияАвторизацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура НадписьВыборУчастникаОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
		
		ОбработкаНавигационныхСсылок(
			Элемент,
			НавигационнаяСсылкаФорматированнойСтроки,
			СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПользовательскоеСоглашениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеРезультатаОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодсказкаОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)

	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодсказкаНастройкиОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДополнительнаяИнформацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиОплаты(Элемент)
	
	ПриИзмененииПрикладныхНастроекНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиАутентификации(
		Элемент,
		Текст,
		СтандартнаяОбработка)
	
	Если ЭтоПолеСПаролем(Элемент) Тогда
		ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
			Элемент);
	КонецЕсли;
	
	Эталон = Новый Структура("РеквизитПоИмениЭлемента", Неопределено);
		
	Если ТипЗнч(ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ДинамическиеРеквизиты);
	КонецЕсли;

	Если ТипЗнч(Эталон.РеквизитПоИмениЭлемента) <> Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяРеквизита = Эталон.РеквизитПоИмениЭлемента.Получить(Элемент.Имя);
		
	Если ИмяРеквизита = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗначениеНастройки = ЭтотОбъект[ИмяРеквизита];
	Если ТипЗнч(ЗначениеНастройки) = Тип("Строка") Тогда
		ЗначениеНастройки = НастройкиПлатежныхСистемКлиентСервер.УдалитьНечитаемыеСимволы(ЗначениеНастройки);
		ЭтотОбъект[ИмяРеквизита] = ЗначениеНастройки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_НачалоВыбораНастройкиАутентификации(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	Если ЭтоПолеСПаролем(Элемент) Тогда
		
		ИмяРеквизита = ДинамическиеРеквизиты.РеквизитПоИмениЭлемента.Получить(
				Элемент.Имя);
		Если Не ЗначениеЗаполнено(ИмяРеквизита) Тогда
			СтандартнаяОбработка = Ложь;
			Возврат;
		КонецЕсли;
		
		ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
			ЭтотОбъект,
			Элемент,
			ИмяРеквизита);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДоступныеУчастники

&НаКлиенте
Процедура ДоступныеУчастникиВыбор(
		Элемент,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПереходДалее();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	ПереходДалее();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
		ВыделитьВыбранногоУчастника();
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТребуемыеНастройки Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки;
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокУчастников(Команда)

	ОбновлениеСпискаУчастников = Истина;
	ЗаполнитьУчастников();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьОтображениеИнформацииОбОшибке(
		Знач ИнформацияОбОшибке,
		Знач Ошибка = Истина,
		Знач ОтображатьЖР = Истина)
	
	Если ОтображатьЖР Тогда
		ПредставлениеОшибки = СтроковыеФункции.ФорматированнаяСтрока(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1
					|
					|
					|Подробную информацию см. в <a href = ""open:log"">Журнале регистрации</a>.';
					|en = '%1
					|
					|
					|For more information see the <a href = ""open:log"">Event log</a>.'"),
				ИнформацияОбОшибке));
		
	Иначе
		ПредставлениеОшибки = ИнформацияОбОшибке;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница          = Элементы.ГруппаРезультат;
	Элементы.ДекорацияОписаниеРезультата.Заголовок   = ПредставлениеОшибки;
	Элементы.ДекорацияКартинкаРезультат.Картинка     = ?(Ошибка,
		БиблиотекаКартинок.Ошибка32,
		БиблиотекаКартинок.Предупреждение32);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеУспешногоЗавершения()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат;
	Элементы.ДекорацияКартинкаРезультат.Картинка = БиблиотекаКартинок.Успешно32;
	ПодключениеЗавершено = Истина;
	Элементы.ДекорацияОписаниеРезультата.Заголовок = НСтр("ru = 'Настройка подключения создана.';
															|en = 'The connection setting is created.'");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)

	Элементы = Форма.Элементы;
	
	Элементы.Назад.Видимость = Ложь;
	Элементы.Далее.Видимость = Истина;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Если Форма.ПодключениеЗавершено Тогда
			Элементы.Далее.Заголовок  = НСтр("ru = 'Завершить';
											|en = 'Exit'");
		ИначеЕсли Не Форма.ДанныеУчастниковЗагружены Тогда
			Элементы.Далее.Видимость  = Ложь;
		Иначе
			Элементы.Назад.Видимость  = Истина;
			Элементы.Далее.Видимость  = Ложь;
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТребуемыеНастройки Тогда
		Элементы.Назад.Видимость = Истина;
	
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация Тогда
		Элементы.Далее.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходДалее()
	
	Отказ = Ложь;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация Тогда
		
		Если Не ЗаполненыДанныеАутентификации Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
		Иначе
			ЗаполнитьУчастников();
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу Тогда
		
		Результат = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
			Новый Структура("Логин, Пароль",
			ЛогинИПП, ПарольИПП));
		
		Если Результат.Отказ Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				Результат.СообщениеОбОшибке,
				,
				Результат.Поле);
			Возврат;
		КонецЕсли;
		
		ПроверитьПодключениеКПорталу1СИТС(
			ЛогинИПП,
			ПарольИПП,
			Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ЛогинИПП = "";
		ПарольИПП = "";
		
		ЗаполнитьУчастников();

	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника Тогда
		
		ТекущиеДанные = Элементы.ДоступныеУчастники.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не выбран банк или платежный агрегатор для подключения к Интернет-эквайрингу.';
					|en = 'A bank or a payment aggregator to connect to Online acquiring is not selected.'"),
				,
				,
				"ДоступныеУчастники");
			Возврат;
		КонецЕсли;
		
		ОбработатьВыборУчастника(ТекущиеДанные);
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТребуемыеНастройки Тогда
		
		ИсходнаяСтраница = Элементы.ГруппаСтраницы.ТекущаяСтраница;
		
		ЗапросТокена(Отказ);
		ПроверитьОтказДлительнаяОперация(
			Отказ,
			ИсходнаяСтраница);
	
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки Тогда
		
		ПроверитьПрикладныеПараметры(Отказ);
		ПроверитьОтказДлительнаяОперация(
			Отказ,
			Элементы.ГруппаПрикладныеНастройки);
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Закрыть(Истина);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоПолеСПаролем(Элемент)
	
	Эталон = Новый Структура(
		"НастройкиАутентификации, ИдентификаторПоИмениЭлемента",
		Неопределено,
		Неопределено);
	Если ТипЗнч(ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ДинамическиеРеквизиты);
	Иначе
		Возврат Ложь;
	КонецЕсли;

	Если (ТипЗнч(Эталон.НастройкиАутентификации) <> Тип("Массив")
		Или ТипЗнч(Эталон.ИдентификаторПоИмениЭлемента) <> Тип("Соответствие")) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Идентификатор = Эталон.ИдентификаторПоИмениЭлемента.Получить(Элемент.Имя);
	Если Не ЗначениеЗаполнено(Идентификатор) Тогда
		Возврат Ложь;
	КонецЕсли;

	Для Каждого ОписаниеРеквизита Из Эталон.НастройкиАутентификации Цикл
		Если ОписаниеРеквизита.Идентификатор = Идентификатор Тогда
			Возврат ОписаниеРеквизита.РежимПароля;
		КонецЕсли;
	КонецЦикла;

	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область Участники

&НаКлиенте
Процедура ЗаполнитьУчастников()
	
	Если ДанныеУчастниковЗагружены И Не ОбновлениеСпискаУчастников Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
		УстановитьВидимостьДоступность(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	Элементы.ДекорацияСостояние.Заголовок   = НСтр("ru = 'Получение списка доступных участников. Пожалуйста, подождите....';
													|en = 'Getting a list of available participants. Please wait....'");
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ПолучитьУчастниковЗапускЗадания(УникальныйИдентификатор);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПолучитьУчастниковЗапускЗаданияЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ПолучитьУчастниковЗапускЗаданияЗавершение(
			РезультатВыполнения,
			Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьУчастниковЗапускЗадания(Знач УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение актуального списка участников Интернет-эквайринга.';
															|en = 'Get an up-to-date list of Online acquiring participants.'");
	
	ПараметрыПроцедуры = Новый Структура;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ПолучитьУчастниковВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьУчастниковЗапускЗаданияЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			УстановитьОтображениеИнформацииОбОшибке(
				РезультатОперации.СообщениеОбОшибке,
				Ложь,
				Ложь);
		Иначе
			
			ПереданБИКПриОткрытии = ЗначениеЗаполнено(БИК) И Не ДанныеУчастниковЗагружены;
			
			ДоступныеУчастники.Очистить();
			
			ИскатьУчастника = ЗначениеЗаполнено(БИК);
			УчастникВыбран  = ЗначениеЗаполнено(ВыбранныйУчастник);

			ПолученныеДанные = РезультатОперации.ДанныеУчастников; // Массив Из см. ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника
			
			Если ПолученныеДанные.Количество() = 0 Тогда
			
				УстановитьОтображениеИнформацииОбОшибке(
					НСтр("ru = 'Не удалось найти ни одного участника Интернет-эквайринга, с которым можно настроить взаимодействие.';
						|en = 'Cannot find any Online acquiring participant you can set up interaction with.'"));
			
			Иначе
			
				Для Каждого ОписаниеУчастника Из ПолученныеДанные Цикл
					
					НоваяСтрока = ДоступныеУчастники.Добавить();
					ЗаполнитьЗначенияСвойств(
						НоваяСтрока,
						ОписаниеУчастника,
						,
						"ПоляАутентификации");
					
					Для Каждого ПолеАутентификации Из ОписаниеУчастника.ПоляАутентификации Цикл
						НовоеПоле = НоваяСтрока.ПоляАутентификации.Добавить();
						ЗаполнитьЗначенияСвойств(НовоеПоле, ПолеАутентификации);
						
						НовоеПоле.Идентификатор = НовоеПоле.Идентификатор;
						
					КонецЦикла;
					
					Если Не УчастникВыбран И ИскатьУчастника И НоваяСтрока.БИК = БИК Тогда
						ВыбранныйУчастник = НоваяСтрока.Идентификатор;
					КонецЕсли;
					
				КонецЦикла;
				
				ДанныеУчастниковЗагружены = Истина;
				
				Если ДоступныеУчастники.Количество() = 1 Тогда
					ВыбранныйУчастник = ДоступныеУчастники[0].Идентификатор;
				КонецЕсли;
				
				ВыбраннаяСтрока = ВыделитьВыбранногоУчастника();
				
				Если ОбновлениеСпискаУчастников Тогда
					Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
				Иначе
					Если ОтображатьОписаниеСервиса Тогда
						Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация;
					ИначеЕсли Не ЗаполненыДанныеАутентификации Тогда
						Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
					Иначе
						Если ЗначениеЗаполнено(ВыбранныйУчастник) Тогда
							ОбработатьВыборУчастника(ВыбраннаяСтрока);
						Иначе
							Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
							Если ПереданБИКПриОткрытии Тогда
								ОбщегоНазначенияКлиент.СообщитьПользователю(
									НСтр("ru = 'Не удалось найти участника с указанным БИК. Возможно он исключен из доступных.';
										|en = 'Cannot find the participant with the specified BIC. They might be excluded from the available ones.'"),
									,
									"ДоступныеУчастники");
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		
		ИнформацияОбОшибке = ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(
			Результат.ИнформацияОбОшибке);
		УстановитьОтображениеИнформацииОбОшибке(ИнформацияОбОшибке);
	КонецЕсли;
	
	ОбновлениеСпискаУчастников = Ложь;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ВыделитьВыбранногоУчастника()
	
	Если Не ЗначениеЗаполнено(ВыбранныйУчастник) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПоискУчастника = ДоступныеУчастники.НайтиСтроки(Новый Структура("Идентификатор", ВыбранныйУчастник));
	Если ПоискУчастника.Количество() = 0 Тогда
		ВыбранныйУчастник = "";
		Возврат Неопределено;
	КонецЕсли;

	НайденнаяСтрока = ПоискУчастника[0];

	ТекущиеДанные = Элементы.ДоступныеУчастники.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено
		И ТекущиеДанные.Идентификатор = НайденнаяСтрока.Идентификатор Тогда
		Возврат НайденнаяСтрока;
	КонецЕсли;
	
	Элементы.ДоступныеУчастники.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
	
	Возврат НайденнаяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВыборУчастника(ТекущиеДанные)

	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ВыбранныйУчастник = ТекущиеДанные.Идентификатор;
	Если ВыбранныйУчастник <> ВыбранныйУчастникПредыдущий Тогда
		
		ВыбранныйУчастникПредыдущий = ВыбранныйУчастник;
		
		ПоляАутентификации = Новый Массив;
		Для Каждого СтрокаТаблицы Из ТекущиеДанные.ПоляАутентификации Цикл
			ОписаниеПараметра = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыПоляАутентификации();
			ЗаполнитьЗначенияСвойств(ОписаниеПараметра, СтрокаТаблицы);
			ПоляАутентификации.Добавить(ОписаниеПараметра);
		КонецЦикла;
		
		ВывестиПараметрыАутентификации(ПоляАутентификации);
		
		Если ЗначениеЗаполнено(ТекущиеДанные.СсылкаСправка)
			И Не СтрНачинаетсяС(ТекущиеДанные.СсылкаСправка, "https://example.com") Тогда
			Элементы.ДекорацияПодсказка.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для подключения Интернет-эквайринга заполните настройки.
						|Получить параметры подключения можно в <a href = ""%1"">%2</a>';
						|en = 'To enable Online acquiring, fill in the settings.
						|You can get the connection parameters in <a href = ""%1"">%2</a>'"),
					ТекущиеДанные.СсылкаСправка,
					ТекущиеДанные.Представление));
		Иначе
			АдресСсылки = НастройкиПлатежныхСистемКлиентСервер.АдресСтраницыПодключенияКСБП(
				ТекущиеДанные.Идентификатор,
				"acquiring");
			Элементы.ДекорацияПодсказка.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Для подключения Интернет-эквайринга заполните настройки или
						|отправьте <a href = ""%1"">заявку на подключение</a> в %2';
						|en = 'To connect the Online acquiring service, fill in the settings or
						|send <a href = ""%1"">an activation request</a> to %2'"),
					АдресСсылки,
					ТекущиеДанные.Представление));
		КонецЕсли;
		
	КонецЕсли;

	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника;
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСсылкуСОписаниемПоУчастнику(ИмяРеквизита)

	Если Не ЗначениеЗаполнено(ВыбранныйУчастник) Тогда
		Возврат "";
	КонецЕсли;
	
	ПоискНастройки = ДоступныеУчастники.НайтиСтроки(
		Новый Структура(
			"Идентификатор",
			ВыбранныйУчастник));
	Если ПоискНастройки.Количество() = 0 Тогда
		Возврат "";
	КонецЕсли;
	
	Возврат ПоискНастройки[0][ИмяРеквизита];
	
КонецФункции

#КонецОбласти

#Область Подключение

&НаСервереБезКонтекста
Процедура ПроверитьПодключениеКПорталу1СИТС(
		Знач Логин,
		Знач Пароль,
		Знач Отказ)
	
	РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(
		Логин,
		Пароль);
	
	Если РезультатПроверки.Результат Тогда
		
		ДанныеАутентификации = Новый Структура;
		ДанныеАутентификации.Вставить("Логин", Логин);
		ДанныеАутентификации.Вставить("Пароль", Пароль);
		
		УстановитьПривилегированныйРежим(Истина);
		ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(
			ДанныеАутентификации);
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			РезультатПроверки.СообщениеОбОшибке,
			,
			,
			,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВывестиПараметрыАутентификации(Знач ПоляАутентификации)
	
	ИнтернетЭквайрингСлужебный.ВывестиПараметрыАутентификации(
			ЭтотОбъект,
			Элементы.ГруппаНастройкиПодключения,
			ПоляАутентификации);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапросТокена(Отказ)

	ТокенСервиса = "";
	
	Если Не ИнтернетЭквайрингКлиент.ДинамическиеРеквизитыЗаполнены(ЭтотОбъект, "НастройкиАутентификации") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ОтобразитьДлительнуюОперацию(
			НСтр("ru = 'Проверка параметров подключения к Интернет-эквайрингу.';
				|en = 'Check the Online acquiring connection parameters.'"));
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ЗапросТокенаЗапускЗадания();
	
	Если РезультатВыполнения = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ЗапросТокенаЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ЗапросТокенаЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ЗапросТокенаЗапускЗадания()
	
	ЗначенияРеквизитов = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект);
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить(
		"ИдентификаторМерчанта",
		ЗначенияРеквизитов.ИдентификаторМерчанта);
	ПараметрыНастройки.Вставить(
		"ИдентификаторУчастника",
		ВыбранныйУчастник);
	ПараметрыНастройки.Вставить(
		"ПараметрыАутентификации",
		ЗначенияРеквизитов.НастройкиАутентификации);
	ПараметрыНастройки.Вставить(
		"ДополнительныеПараметры",
		ЗначенияРеквизитов.ДополнительныеПараметры);
	ПараметрыНастройки.Вставить(
		"СпособАутентификации",
		"");
	ПараметрыНастройки.Вставить(
		"ИдентификаторТокена",
		ИдентификаторТокена);
		
	ПоискУчастника = ДоступныеУчастники.НайтиСтроки(
		Новый Структура(
			"Идентификатор",
			ВыбранныйУчастник));
			
	Если ПоискУчастника.Количество() > 0 Тогда
		ПараметрыНастройки.СпособАутентификации = ПоискУчастника[0].СпособАутентификации;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Проверка параметров подключения к Интернет-эквайрингу.';
				|en = 'Check the Online acquiring connection parameters.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ЗапросТокенаПриСозданииНастройкиВФоне",
		ПараметрыНастройки,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ЗапросТокенаЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекстПредупреждения = "";
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника;
			ТекстПредупреждения = РезультатОперации.СообщениеОбОшибке;
		Иначе
			ОбработатьПолучениеТокена(РезультатОперации);
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастника;
		ТекстПредупреждения = ОбработкаОшибок.ПредставлениеОшибкиДляПользователя(
			Результат.ИнформацияОбОшибке);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстПредупреждения) Тогда
		ПоказатьПредупреждение(
			Неопределено,
			ТекстПредупреждения);
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьПолучениеТокена(РезультатОперации)
	
	Эталон = Новый Структура("ДополнительныеПараметры", Новый Массив);
		
	Если ТипЗнч(ДинамическиеРеквизиты) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(Эталон, ДинамическиеРеквизиты);
	КонецЕсли;

	СоздаватьРеквизиты = РезультатОперации.ТребуетсяНастройка;
	
	НовыеЗначения = ИнтернетЭквайрингКлиент.ОписаниеДополнительныхПараметров(
		РезультатОперации,
		Эталон.ДополнительныеПараметры);
		
	Эталон.ДополнительныеПараметры = ОбновитьДополнительныеПараметры(
		Эталон.ДополнительныеПараметры,
		СоздаватьРеквизиты,
		НовыеЗначения);

	ДинамическиеРеквизиты.Вставить(
		"ДополнительныеПараметры",
		Эталон.ДополнительныеПараметры);
	
	Если СоздаватьРеквизиты Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТребуемыеНастройки;
		
	ИначеЕсли ЗначениеЗаполнено(РезультатОперации.Токен) Тогда
		ТокенСервиса = РезультатОперации.Токен;
		ВывестиПрикладныеНастройки();
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьДополнительныеПараметры(
		Знач ДополнительныеПараметры,
		Знач СоздаватьРеквизиты,
		Знач НовыеЗначения)

	ПараметрыМетода = ИнтернетЭквайрингСлужебный.НовыйПараметрыВыводаДополнительныхНастроек(
		ЭтотОбъект,
		СоздаватьРеквизиты,
		ДополнительныеПараметры,
		НовыеЗначения,
		"ГруппаТребуемыеНастройкиДинамические");

	ИнтернетЭквайрингСлужебный.ВывестиДополнительныеНастройкиАутентификации(ПараметрыМетода);
	
	Возврат ДополнительныеПараметры;
	
КонецФункции

&НаКлиенте
Процедура ОтозватьТокен()
	
	ОтобразитьДлительнуюОперацию(
			НСтр("ru = 'Отзыв неиспользованного токена аутентификации сервиса.';
				|en = 'Revoke an unused service authentication token.'"));

	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ОтзывТокенаЗапускЗадания(
		ТокенСервиса,
		УникальныйИдентификатор,
		ИдентификаторТокена);
	
	Если РезультатВыполнения = Неопределено Тогда
		ТокенСервиса = "";
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОтзывТокенаЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ОтзывТокенаЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	Элементы.Назад.Видимость = Ложь;
	Элементы.Далее.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОтзывТокенаЗапускЗадания(
		Знач Токен,
		Знач УникальныйИдентификатор,
		Знач ИдентификаторТокена)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Отзыв неиспользованного токена аутентификации сервиса при отмене настройки нового подключения.';
				|en = 'Revoke an unused service authentication token when setting up a new connection is canceled.'");
	
	Токены = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		ИдентификаторТокена);
	
	ПараметрыМетода = Новый Структура("Токены", Токены);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"ИнтернетЭквайрингСлужебный.ОтзывТокенаПриСозданииНастройкиВФоне",
		ПараметрыМетода,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ОтзывТокенаЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	
	Если Результат.Статус = "Выполнено" Тогда
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			ЕстьОшибки = Истина
		КонецЕсли;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ЗакрытиеПредупрежденияОтзывТокена",
			ЭтотОбъект);
		
		ПоказатьПредупреждение(
			Оповещение,
			НСтр("ru = 'Не удалось отозвать токен при отмене создания новой настройки подключения.
			|Подробную информацию смотрите в журнале регистрации';
			|en = 'Cannot revoke the token when canceling the creation of a new connection setting.
			|For more information, see the event log'"));
			
	Иначе
		
		ТокенСервиса = "";
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеПредупрежденияОтзывТокена(ДополнительныеПараметры) Экспорт
	
	ТокенСервиса = "";
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПрикладныеПараметры

&НаСервере
Процедура ВывестиПрикладныеНастройки()
	
	ИнтернетЭквайрингСлужебный.ВывестиПрикладныеНастройки(
		ЭтотОбъект,
		ПараметрыВыводаПрикладныхНастроек());
	
	НастройкаКопированиемЗагружена = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииПрикладныхНастроекНаСервере()
	
	ИнтернетЭквайрингСлужебный.НастроитьЭлементыФормыПодключения(
		ЭтотОбъект,
		ПараметрыВыводаПрикладныхНастроек());
	
КонецПроцедуры

&НаСервере
Функция ПараметрыВыводаПрикладныхНастроек()
	
	ПараметрыВывода = ИнтернетЭквайрингСлужебный.НовыйПараметрыВывода();
	ПараметрыВывода.Родитель                     = Элементы.ГруппаНастройкиОплаты;
	ПараметрыВывода.ДополнительныеПараметры      = Параметры.ДополнительныеПараметры;
	ПараметрыВывода.ИдентификаторУчастника       = ВыбранныйУчастник;
	ПараметрыВывода.Настройка                    = Параметры.НастройкаДляКопирования;
	ПараметрыВывода.ПрикладныеПараметрыЗагружены = НастройкаКопированиемЗагружена;
	
	Возврат ПараметрыВывода;
	
КонецФункции

&НаСервере
Функция ЗначенияПрикладныхНастроек()

	Возврат ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект).ПрикладныеНастройки;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПрикладныеПараметры(Отказ)
	
	Если Не ЗначениеЗаполнено(Наименование) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнено поле ""Наименование"".';
				|en = 'The field ""Description"" is blank.'"),
			,
			,
			"Наименование");
		Отказ = Истина;
	КонецЕсли;
	
	Если Не ИнтернетЭквайрингКлиент.ДинамическиеРеквизитыЗаполнены(ЭтотОбъект, "ПрикладныеНастройки") Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	ОтобразитьДлительнуюОперацию(
		НСтр("ru = 'Проверка параметров. Пожалуйста, подождите';
			|en = 'Checking the parameters. Please wait.'"));
	
	Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Сохранение информации. Пожалуйста, подождите';
												|en = 'Saving the information. Please wait.'");
	
	Если Не НоваяНастройкаПодключения() Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеНастройки

// Выполняет создание новой настройки подключения на основе указанных параметров.
// 
// Возвращаемое значение:
//  Булево - Истина, если создание настройки выполнено успешно. Ложь в противном случае.
//
&НаСервере
Функция НоваяНастройкаПодключения()
	
	ПоискУчастника = ДоступныеУчастники.НайтиСтроки(
		Новый Структура(
			"Идентификатор",
			ВыбранныйУчастник));
	
	Если ПоискУчастника.Количество() = 0 Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Не выбран участник Интернет-эквайринга.';
				|en = 'The Online acquiring participant is not selected.'"),
			,
			,
			"ДоступныеУчастники");
		Возврат Ложь;
	КонецЕсли;
	
	ПараметрыУчастника = ИнтернетЭквайрингКлиентСервер.НовыйПараметрыУчастника();
	ЗаполнитьЗначенияСвойств(ПараметрыУчастника, ПоискУчастника[0]);
	
	Настройки = ИнтернетЭквайрингСлужебный.ЗначенияДинамическихРеквизитов(ЭтотОбъект);
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование",          Наименование);
	ПараметрыЗаполнения.Вставить("ПараметрыУчастника",    ПараметрыУчастника);
	ПараметрыЗаполнения.Вставить("Настройки",             Настройки);
	ПараметрыЗаполнения.Вставить(
		"ТокенСервиса",
		Новый Структура(
			"Идентификатор, Токен",
			ИдентификаторТокена,
			ТокенСервиса));
	
	Результат = Справочники.НастройкиПодключенияКИнтернетЭквайрингу.НоваяНастройкаПодключения(
		ПараметрыЗаполнения);
	
	Если Результат.Ошибка Тогда
		УстановитьОтображениеИнформацииОбОшибке(
			Результат.СообщениеОбОшибке,
			Истина,
			Ложь);
	Иначе
		УстановитьОтображениеУспешногоЗавершения();
		НастройкаСохранена = Истина;
	КонецЕсли;
	
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаНавигационныхСсылок(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если Элемент.Имя = "ДекорацияДополнительнаяИнформация" Тогда
		
		ДанныеФормы = Новый Структура;
		ДанныеФормы.Вставить("ПрикладныеНастройки", ЗначенияПрикладныхНастроек());
		ДанныеФормы.Вставить("НастройкаПодключения", Неопределено);
		ДанныеФормы.Вставить("Модифицированность", Модифицированность);
		
		ИнтернетЭквайрингКлиентПереопределяемый.ПриОбработкеНавигационнойСсылкиНастроекПодключения(
			Элемент,
			НавигационнаяСсылкаФорматированнойСтроки,
			СтандартнаяОбработка,
			ДанныеФормы);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "open:BankInstrurtions" Тогда
		СтандартнаяОбработка = Ложь;
		НавигационнаяСсылка = ПолучитьСсылкуСОписаниемПоУчастнику("СсылкаСправка");
		Если ЗначениеЗаполнено(НавигационнаяСсылка) Тогда
			ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылка)
		КонецЕсли;
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "open:log" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетЭквайрингКлиент.ОткрытьЖурналРегистрации();
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "open:userAgreement" Тогда
		СтандартнаяОбработка = Ложь;

		НастройкиПлатежныхСистемКлиент.ФормаПользовательскогоСоглашения(ЭтотОбъект);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "action:openPortal" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуИнтегрированногоСайта(
			ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin(
				,
				ИнтернетПоддержкаПользователейКлиент.НастройкиСоединенияССерверами()),
			"",
			Истина);
			
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "open:IADescription" Тогда
		СтандартнаяОбработка = Ложь;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьДлительнуюОперацию(ТекстПредупреждения)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	Элементы.ДекорацияСостояние.Заголовок   = ТекстПредупреждения;
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьОтказДлительнаяОперация(Отказ, НоваяТекущаяСтраница)
	
	Если Отказ Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = НоваяТекущаяСтраница;
		УстановитьВидимостьДоступность(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
