#Область ОписаниеПеременных
&НаКлиенте
Перем МассивУдаленныхСтрок;
&НаКлиенте
Перем МассивДобавленныхСтрок;
#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьКадровыйУчет") Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Данное рабочее место недоступно при включенном использовании кадрового учета.';
													|en = 'This workplace is not available when HR recordkeeping is enabled.'"));
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДополнительныйРазрез = Константы.ДополнительныйРазрезТарифнойСетки.Получить();
	
	УстановитьУсловноеОформление();
	
	СформироватьСтруктуруТарифнойСетки();
	
	//Видимость
	Элементы.Подразделение.Видимость = ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Подразделение;
	Элементы.Организация.Видимость = ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Организация;
	
	Элементы.ТарифнаяСеткаИндивидуальный.Видимость = Ложь;
	
	Автор = Пользователи.АвторизованныйПользователь();
	
	ПрочитатьЗаполнитьТарифнуюСетку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПодразделениеПриИзменении(Элемент)
	
	Если Подразделение = ТекущееПодразделение Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьЗавершение", ЭтотОбъект, Подразделение);
		ТекстВопроса       = НСтр("ru = 'Все несохраненные данные будут потеряны. Изменить подразделение?';
									|en = 'All unsaved data will be lost. Change the business unit?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
		
	КонецЕсли;
	
	НастройкаПоляИндивидуальный(Подразделение);
	ПрочитатьЗаполнитьТарифнуюСетку();
	
	ТекущееПодразделение = Подразделение;
	
	МассивУдаленныхСтрок = Новый Массив;
	МассивДобавленныхСтрок = Новый Массив;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Организация = ТекущаяОрганизация Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьЗавершение", ЭтотОбъект, Организация);
		ТекстВопроса       = НСтр("ru = 'Все несохраненные данные будут потеряны. Изменить организацию?';
									|en = 'All unsaved data will be lost. Change the company?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
		
	КонецЕсли;
	
	НастройкаПоляИндивидуальный(Организация);
	ПрочитатьЗаполнитьТарифнуюСетку();
	
	ТекущаяОрганизация = Организация;
	
	МассивУдаленныхСтрок = Новый Массив;
	МассивДобавленныхСтрок = Новый Массив;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТарифнаяСетка

&НаКлиенте
Процедура ТарифнаяСеткаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование
		И (ЗначениеЗаполнено(Подразделение) ИЛИ ЗначениеЗаполнено(Организация)) Тогда
		Элементы.ТарифнаяСетка.ТекущиеДанные.Индивидуальный = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифнаяСеткаИндивидуальныйОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	Если НЕ ВыбранноеЗначение
		И (НЕ ЗначениеЗаполнено(МассивДобавленныхСтрок) ИЛИ МассивДобавленныхСтрок.Найти(Элементы.ТарифнаяСетка.ТекущаяСтрока) = Неопределено) Тогда
		
		Если НЕ ЗначениеЗаполнено(МассивУдаленныхСтрок) Тогда
			МассивУдаленныхСтрок = Новый Массив;
		КонецЕсли;
		
		Отбор = Новый Структура("Должность, Подразделение, Организация");
		Отбор.Должность = Элементы.ТарифнаяСетка.ТекущиеДанные.Должность;
		Отбор.Подразделение = Подразделение;
		Отбор.Организация = Организация;
		
		СтавкиКУдалению = Объект.ТарифныеСтавки.НайтиСтроки(Отбор);
		
		Для Каждого Ставка Из СтавкиКУдалению Цикл
			Ставка.Тариф = 0;
			МассивУдаленныхСтрок.Добавить(Ставка.ПолучитьИдентификатор());
		КонецЦикла;
	КонецЕсли;
	
	ВывестиЗамещающийТариф(Элементы.ТарифнаяСетка.ТекущиеДанные, ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура ТарифнаяСеткаПередУдалением(Элемент, Отказ)
	
	Если НЕ ЗначениеЗаполнено(МассивУдаленныхСтрок) Тогда
		МассивУдаленныхСтрок = Новый Массив;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы.ТарифнаяСетка.ВыделенныеСтроки;
	
	Если НЕ ЗначениеЗаполнено(Подразделение) И НЕ ЗначениеЗаполнено(Организация) Тогда
		
		Отбор = Новый Структура("Должность");
		Для Каждого Строка Из ВыделенныеСтроки Цикл
			
			Если ЗначениеЗаполнено(МассивДобавленныхСтрок) И МассивДобавленныхСтрок.Найти(Строка) <> Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			СтрокаТаблицы = ТарифнаяСетка.НайтиПоИдентификатору(Строка);
			
			Отбор.Должность = СтрокаТаблицы.Должность;
			СтавкиКУдалению = Объект.ТарифныеСтавки.НайтиСтроки(Отбор);
			
			Для Каждого Ставка Из СтавкиКУдалению Цикл
				Ставка.Тариф = 0;
				МассивУдаленныхСтрок.Добавить(Ставка.ПолучитьИдентификатор());
			КонецЦикла;
			
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Отказ = Истина;
	
	Модифицированность = Истина;
	
	Отбор = Новый Структура("Должность, Подразделение, Организация");
	
	Для Каждого Строка Из ВыделенныеСтроки Цикл
		
		Если ЗначениеЗаполнено(МассивДобавленныхСтрок) И МассивДобавленныхСтрок.Найти(Строка) <> Неопределено Тогда
			ТарифнаяСетка.Удалить(Элементы.ТарифнаяСетка.ДанныеСтроки(Строка));
			Продолжить;
		КонецЕсли;
		
		СтрокаТаблицы = ТарифнаяСетка.НайтиПоИдентификатору(Строка);
		
		Если СтрокаТаблицы.Индивидуальный Тогда
			
			СтрокаТаблицы.Индивидуальный = Ложь;
			
			Отбор.Должность = СтрокаТаблицы.Должность;
			Отбор.Подразделение = Подразделение;
			Отбор.Организация = Организация;
			
			СтавкиКУдалению = Объект.ТарифныеСтавки.НайтиСтроки(Отбор);
			
			Для Каждого Ставка Из СтавкиКУдалению Цикл
				Ставка.Тариф = 0;
				МассивУдаленныхСтрок.Добавить(Ставка.ПолучитьИдентификатор());
			КонецЦикла;
			
			ВывестиЗамещающийТариф(Элементы.ТарифнаяСетка.ДанныеСтроки(Строка), Ложь);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ТарифнаяСеткаПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НоваяСтрока И НЕ ОтменаРедактирования Тогда
		
		Если НЕ ЗначениеЗаполнено(МассивДобавленныхСтрок) Тогда
			МассивДобавленныхСтрок = Новый Массив;
		КонецЕсли;
		
		МассивДобавленныхСтрок.Добавить(Элементы.ТарифнаяСетка.ТекущаяСтрока);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	ВыделенныеСтроки = Элементы.ТарифнаяСетка.ВыделенныеСтроки;
	
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Необходимо выбрать должности, для которых необходимо заполнить тарифы.';
														|en = 'Select job titles to populate tariffs for.'"));
		Возврат;
	КонецЕсли;
	
	РазрядыСсылки = Новый Массив;
	Для Каждого Разряд Из Разряды Цикл
		РазрядыСсылки.Добавить(Разряд.Ссылка);
	КонецЦикла;
	
	ОткрытьФорму("Обработка.ТарифныеСтавки.Форма.ФормаТарифы",
		Новый Структура("Разряды", РазрядыСсылки),
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьТарифыЗавершение", ЭтотОбъект, Новый Структура("ВыделенныеСтроки", ВыделенныеСтроки)),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	
	ОткрытьФорму("Справочник.Должности.ФормаВыбора",
		ПараметрыФормы,
		ЭтотОбъект,
		УникальныйИдентификатор,
		,
		,
		Новый ОписаниеОповещения("ЗаполнитьДолжностиЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Модифицированность Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Отсутствуют изменения.';
														|en = 'There are no changes.'"));
		Возврат;
	КонецЕсли;
	
	ДублиДолжностей = Новый Массив;
	
	Для Каждого Строка Из ТарифнаяСетка Цикл
		Если ДублиДолжностей.Найти(Строка.Должность) <> Неопределено Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтрШаблон(НСтр("ru = 'Найдено дублирование должности %1.';
								|en = 'A duplicate of the %1 job title is found.'"), Строка.Должность),
				,
				"ТарифнаяСетка.Должность");
			Возврат;
		КонецЕсли;
		
		ДублиДолжностей.Добавить(Строка.Должность);
	КонецЦикла;
	
	Отбор = Новый Структура("Должность, Подразделение, Организация");
	
	ИзмененныеСтавки = Новый Массив;
	Если ЗначениеЗаполнено(МассивУдаленныхСтрок) Тогда
		ИзмененныеСтавки = МассивУдаленныхСтрок;
		МассивУдаленныхСтрок = Новый Массив;
	КонецЕсли;
	
	Для Каждого Строка Из ТарифнаяСетка Цикл
		
		Отбор.Должность = Строка.Должность;
		Если Строка.Индивидуальный Тогда
			ЗаполнитьЗначенияСвойств(Отбор, ЭтотОбъект, "Подразделение, Организация");
		Иначе
			Отбор.Подразделение = ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка");
			Отбор.Организация = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		КонецЕсли;
		
		МассивСтавок = Объект.ТарифныеСтавки.НайтиСтроки(Отбор);
		
		НоваяДолжность = Ложь;
		Если МассивСтавок.Количество() = 0 Тогда
			НоваяДолжность = Истина;
		КонецЕсли;
		
		Для Каждого Разряд Из Разряды Цикл
			
			Тариф = Строка[ИмяПоляРазряда(Разряд.Идентификатор)];
			
			Если НоваяДолжность И (Тариф <> 0 ИЛИ Строка.Индивидуальный) Тогда
				
				НоваяСтрока = Объект.ТарифныеСтавки.Добавить();
				НоваяСтрока.Должность = Строка.Должность;
				НоваяСтрока.Разряд = Разряд.Ссылка;
				
				Если ЗначениеЗаполнено(Подразделение) Тогда
					НоваяСтрока.Подразделение = Подразделение;
				ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
					НоваяСтрока.Организация = Организация;
				КонецЕсли;
				
				НоваяСтрока.Тариф = Тариф;
				
				ИзмененныеСтавки.Добавить(НоваяСтрока.ПолучитьИдентификатор());
				
				Продолжить;
			КонецЕсли;
			
			СуществующаяСтавкаРазряда = Неопределено;
			Для Каждого СтрокаТарифнойСтавки Из МассивСтавок Цикл
				Если СтрокаТарифнойСтавки.Разряд = Разряд.Ссылка Тогда
					СуществующаяСтавкаРазряда = СтрокаТарифнойСтавки;
				КонецЕсли;
			КонецЦикла;
			
			Если СуществующаяСтавкаРазряда = Неопределено И Тариф <> 0 Тогда
				
				НоваяСтрока = Объект.ТарифныеСтавки.Добавить();
				НоваяСтрока.Должность = Строка.Должность;
				НоваяСтрока.Разряд = Разряд.Ссылка;
				
				Если ЗначениеЗаполнено(Подразделение) Тогда
					НоваяСтрока.Подразделение = Подразделение;
				ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
					НоваяСтрока.Организация = Организация;
				КонецЕсли;
				
				НоваяСтрока.Тариф = Тариф;
				
				ИзмененныеСтавки.Добавить(НоваяСтрока.ПолучитьИдентификатор());
				
			ИначеЕсли СуществующаяСтавкаРазряда <> Неопределено И СуществующаяСтавкаРазряда.Тариф <> Тариф Тогда
				
				СуществующаяСтавкаРазряда.Тариф = Тариф;
				ИзмененныеСтавки.Добавить(СуществующаяСтавкаРазряда.ПолучитьИдентификатор());
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ИзмененныеСтавки.Количество() = 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Изменений тарифных ставок не обнаружено.';
														|en = 'No changes in tariff rates were detected.'"));
		Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	СохранитьНаСервере(ИзмененныеСтавки);
	
КонецПроцедуры

&НаКлиенте
Процедура Перечитать(Команда)
	
	Если Модифицированность Тогда
		
		ДопРазрез = Неопределено;
		Если ЗначениеЗаполнено(Подразделение) Тогда
			ДопРазрез = Подразделение;
		ИначеЕсли ЗначениеЗаполнено(Организация) Тогда
			ДопРазрез = Организация;
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПеречитатьЗавершение", ЭтотОбъект, ДопРазрез);
		ТекстВопроса       = НСтр("ru = 'Все несохраненные данные будут потеряны. Обновить?';
									|en = 'All unsaved data will be lost. Refresh?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
		
	КонецЕсли;
	
	ПрочитатьЗаполнитьТарифнуюСетку();
	
	МассивУдаленныхСтрок = Новый Массив;
	МассивДобавленныхСтрок = Новый Массив;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Разряды",     Разряды);
	ДополнительныеПараметры.Вставить("ИспользуютсяПодразделения", ДополнительныйРазрез = ПредопределенноеЗначение("Перечисление.ДополнительныеРазрезыТарифнойСетки.Подразделение"));
	ДополнительныеПараметры.Вставить("ИспользуютсяОрганизации", ДополнительныйРазрез = ПредопределенноеЗначение("Перечисление.ДополнительныеРазрезыТарифнойСетки.Организация"));
	
	ПоказатьФормуЗагрузкиИзФайла(ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СформироватьСтруктуруТарифнойСетки()
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Разряды.Ссылка КАК Ссылка,
		|	Разряды.Наименование КАК Наименование,
		|	УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Разряды.Ссылка) КАК Идентификатор
		|ИЗ
		|	Справочник.РазрядыКатегорииДолжностей КАК Разряды
		|ГДЕ
		|	НЕ Разряды.ПометкаУдаления");
	
	Разряды.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ОписаниеТипов = ОбщегоНазначенияУТ.ОписаниеТипаДенежногоПоля(ДопустимыйЗнак.Неотрицательный);
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого Разряд Из Разряды Цикл
		
		Реквизит = Новый РеквизитФормы(ИмяПоляРазряда(Разряд.Идентификатор),
			ОписаниеТипов,
			"ТарифнаяСетка",
			Разряд.Наименование,
			Истина);
			
		ДобавляемыеРеквизиты.Добавить(Реквизит);
		
	КонецЦикла;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	Для Каждого Разряд Из Разряды Цикл
		Элемент = Элементы.Добавить(ИмяПоляРазряда(Разряд.Идентификатор), Тип("ПолеФормы"), Элементы.ТарифнаяСетка);
		Элемент.Вид = ВидПоляФормы.ПолеВвода;
		Элемент.ПутьКДанным = "ТарифнаяСетка" + "."+ ИмяПоляРазряда(Разряд.Идентификатор);
		Элемент.АвтоМаксимальнаяШирина = Ложь;
		Элемент.МаксимальнаяШирина = 10;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ТарифнаяСетка.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТарифнаяСетка.Индивидуальный");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Если ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Подразделение Тогда
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение");
	Иначе
		ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
	КонецЕсли;
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.ТарифнаяСеткаИндивидуальный.Имя);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьЗаполнитьТарифнуюСетку()
	
	НаДату = НачалоДня(ТекущаяДатаСеанса());
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ТарифныеСтавки.Период        КАК Период,
		|	ТарифныеСтавки.Регистратор   КАК Регистратор,
		|	ТарифныеСтавки.Должность     КАК Должность,
		|	ТарифныеСтавки.Разряд        КАК Разряд,
		|	ТарифныеСтавки.Подразделение КАК Подразделение,
		|	ТарифныеСтавки.Тариф         КАК Тариф
		|ИЗ
		|	РегистрСведений.ТарифныеСтавки.СрезПоследних(&Период,
		|	&Подразделение = НЕОПРЕДЕЛЕНО И &ПустыеРазрезы
		|	ИЛИ (&Подразделение <> НЕОПРЕДЕЛЕНО И (Подразделение = &Подразделение ИЛИ &ПустыеРазрезы))) КАК ТарифныеСтавки
		|
		|ГДЕ
		|	ТарифныеСтавки.Тариф <> 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	Должность,
		|	Подразделение УБЫВ,
		|	Период");
	
	Если ДополнительныйРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Организация Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Подразделение", "Организация");
		Запрос.УстановитьПараметр("Организация", ?(ЗначениеЗаполнено(Организация), Организация, Неопределено));
	Иначе
		Запрос.УстановитьПараметр("Подразделение", ?(ЗначениеЗаполнено(Подразделение), Подразделение, Неопределено));
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПустыеРазрезы", "Подразделение = &ПустоеПодразделение И Организация = &ПустаяОрганизация");
	Запрос.УстановитьПараметр("ПустоеПодразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	Запрос.УстановитьПараметр("ПустаяОрганизация", Справочники.Организации.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("Период", НаДату);
	
	Результат = Запрос.Выполнить().Выгрузить();
	НайденноеЗначение = Результат.Найти(НаДату, "Период");
	
	Если НайденноеЗначение <> Неопределено Тогда
		ТекущийРегистратор = НайденноеЗначение.Регистратор;
	КонецЕсли;
	
	Объект.ТарифныеСтавки.Загрузить(Результат);
	
	ТарифнаяСетка.Очистить();
	
	Если Объект.ТарифныеСтавки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДолжность = Неопределено;
	Для Каждого Ставка Из Объект.ТарифныеСтавки Цикл
		Если Ставка.Должность <> ТекущаяДолжность Тогда
			ТекущаяДолжность = Ставка.Должность;
			
			СтрокаСетки = ТарифнаяСетка.Добавить();
			СтрокаСетки.Должность = ТекущаяДолжность;
			СтрокаСетки.Индивидуальный = ЗначениеЗаполнено(Ставка.Подразделение) ИЛИ ЗначениеЗаполнено(Ставка.Организация);
			
		КонецЕсли;
		
		Если НЕ СтрокаСетки.Индивидуальный
			ИЛИ СтрокаСетки.Индивидуальный И (ЗначениеЗаполнено(Ставка.Подразделение) ИЛИ ЗначениеЗаполнено(Ставка.Организация)) Тогда
			СтрокаСетки[ИмяПоляРазряда(Ставка.Разряд.УникальныйИдентификатор())] = Ставка.Тариф;
		КонецЕсли;
		
	КонецЦикла;
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере(ИзмененныеСтавки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		НовыйРегистратор = Ложь;
		
		Если НЕ ЗначениеЗаполнено(ТекущийРегистратор)
			ИЛИ ЗначениеЗаполнено(ТекущийРегистратор) И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущийРегистратор, "Дата") <> НаДату Тогда
			ДокументОбъект = Документы.РегистраторТарифнойСетки.СоздатьДокумент();
			ДокументОбъект.Дата = НаДату;
			ДокументОбъект.Автор = Автор;
			ДокументОбъект.Комментарий = Комментарий;
			ДокументОбъект.Проведен = Истина;
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать();
			
			ТекущийРегистратор = ДокументОбъект.Ссылка;
			НовыйРегистратор = Истина;
		КонецЕсли;
		
		БлокировкаДанных = Новый БлокировкаДанных;
	
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ТарифныеСтавки");
		ЭлементБлокировки.УстановитьЗначение("Период", НаДату);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Набор = РегистрыСведений.ТарифныеСтавки.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ТекущийРегистратор);
		
		Если НЕ НовыйРегистратор Тогда
			Набор.Прочитать();
		КонецЕсли;
		
		Для Каждого ИзмененнаяСтавка Из ИзмененныеСтавки Цикл
			
			Строка = Объект.ТарифныеСтавки.НайтиПоИдентификатору(ИзмененнаяСтавка);
			Если Строка <> Неопределено Тогда
				
				Если НЕ НовыйРегистратор Тогда
					
					ИзмененаЗапись = Ложь;
					
					Для Каждого Запись Из Набор Цикл
						Если Запись.Должность = Строка.Должность
							И Запись.Разряд = Строка.Разряд
							И Запись.Подразделение = Строка.Подразделение
							И Запись.Организация = Строка.Организация Тогда
							Запись.Тариф = Строка.Тариф;
							ИзмененаЗапись = Истина;
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если ИзмененаЗапись Тогда
						Продолжить;
					КонецЕсли;
					
				КонецЕсли;
				
				Запись = Набор.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Строка);
				Запись.Регистратор = ТекущийРегистратор;
				Запись.Период = НаДату;
			
			КонецЕсли;
			
		КонецЦикла;
		
		Набор.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
		
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДолжностиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено
		Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(МассивДобавленныхСтрок) Тогда
		МассивДобавленныхСтрок = Новый Массив;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Для Каждого Должность Из Результат Цикл
		НоваяСтрока = ТарифнаяСетка.Добавить();
		НоваяСтрока.Должность = Должность;
		
		Если ЗначениеЗаполнено(Подразделение) ИЛИ ЗначениеЗаполнено(Организация) Тогда
			НоваяСтрока.Индивидуальный = Истина;
		КонецЕсли;
		
		МассивДобавленныхСтрок.Добавить(ТарифнаяСетка.Индекс(НоваяСтрока));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТарифыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Модифицированность = Истина;
	
	Для Каждого Строка Из ДополнительныеПараметры.ВыделенныеСтроки Цикл
		
		СтрокаТаблицы = ТарифнаяСетка.НайтиПоИдентификатору(Строка);
		
		Если (ЗначениеЗаполнено(Подразделение) ИЛИ ЗначениеЗаполнено(Организация)) И НЕ СтрокаТаблицы.Индивидуальный Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Разряд Из Результат Цикл
			СтрокаТаблицы[ИмяПоляРазряда(Разряд.Ключ.УникальныйИдентификатор())] = Разряд.Значение;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Подразделение = ТекущееПодразделение;
		Организация = ТекущаяОрганизация;
		НастройкаПоляИндивидуальный(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	НастройкаПоляИндивидуальный(ДополнительныеПараметры);
	
	ПрочитатьЗаполнитьТарифнуюСетку();
	
	МассивУдаленныхСтрок = Новый Массив;
	МассивДобавленныхСтрок = Новый Массив;
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиЗамещающийТариф(ТекущиеДанные, Индивидуальный)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Должность", ТекущиеДанные.Должность);
	Отбор.Вставить("Подразделение", ?(Индивидуальный,
			Подразделение,
			ПредопределенноеЗначение("Справочник.СтруктураПредприятия.ПустаяСсылка")));
	
	Отбор.Вставить("Организация", ?(Индивидуальный,
			Организация,
			ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка")));
	
	Если Индивидуальный И Объект.ТарифныеСтавки.НайтиСтроки(Отбор).Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Отбор.Вставить("Разряд");
	
	Для Каждого Разряд Из Разряды Цикл
		
		Отбор.Разряд = Разряд.Ссылка;
		
		Тариф = 0;
		
		Результат = Объект.ТарифныеСтавки.НайтиСтроки(Отбор);
		Если Результат.Количество() > 0 Тогда
			Тариф = Результат[0].Тариф;
		КонецЕсли;
		
		ТекущиеДанные[ИмяПоляРазряда(Разряд.Идентификатор)] = Тариф;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяПоляРазряда(Идентификатор)
	Возврат "Разряд" + "_" +  СтрЗаменить(Строка(Идентификатор), "-", "_");
КонецФункции

&НаКлиенте
Процедура НастройкаПоляИндивидуальный(Разрез)
	
	Если ТипЗнч(Разрез) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
		ПредставлениеЛожь = НСтр("ru = '<Для всех подразделений>';
								|en = '<For all business units>'");
	ИначеЕсли ТипЗнч(Разрез) = Тип("СправочникСсылка.Организации") Тогда
		ПредставлениеЛожь = НСтр("ru = '<Для всех организаций>';
								|en = '<For all companies>'");
	КонецЕсли;
	
	Элементы.ТарифнаяСеткаИндивидуальный.Видимость = ЗначениеЗаполнено(Разрез);
	Элементы.ТарифнаяСеткаИндивидуальный.Формат = СтрШаблон("БЛ='%1'; БИ='%2';", ПредставлениеЛожь, Разрез);
	Элементы.ТарифнаяСеткаИндивидуальный.СписокВыбора[0].Представление = ПредставлениеЛожь;
	Элементы.ТарифнаяСеткаИндивидуальный.СписокВыбора[1].Представление = Разрез;
	
КонецПроцедуры

#Область ЗагрузкаДанныхИзФайла

&НаКлиенте
Процедура ПоказатьФормуЗагрузкиИзФайла(ДополнительныеПараметры)
	
	ПараметрыЗагрузки = ЗагрузкаДанныхИзФайлаКлиент.ПараметрыЗагрузкиДанных();
	ПараметрыЗагрузки.ПолноеИмяТабличнойЧасти = "Обработка.ТарифныеСтавки.ТарифныеСтавки";
	ПараметрыЗагрузки.Заголовок               = НСтр("ru = 'Загрузка тарифных ставок из файла';
													|en = 'Import tariff rates from file'");
	ПараметрыЗагрузки.ДополнительныеПараметры = ДополнительныеПараметры;
	
	ПараметрыЗагрузки.КолонкиМакета = ОписаниеКолонокМакетаДляЗагрузкиТарифнойСетки(ДополнительныеПараметры);
	
	Оповещение = Новый ОписаниеОповещения("ПоказатьФормуЗагрузкиИзФайлаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ЗагрузкаДанныхИзФайлаКлиент.ПоказатьФормуЗагрузки(ПараметрыЗагрузки, Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьФормуЗагрузкиИзФайлаЗавершение(АдресЗагруженныхДанных, ДополнительныеПараметры) Экспорт
	
	Если АдресЗагруженныхДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры);
	
	МассивУдаленныхСтрок = Новый Массив;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьИзФайлаНаСервере(АдресЗагруженныхДанных, ДополнительныеПараметры)
	
	ЗагруженныеДанные = ПолучитьИзВременногоХранилища(АдресЗагруженныхДанных);// ТаблицаЗначений
	
	ПредыдущаяСтрока = Новый Структура;
	
	ПредыдущаяСтрока.Вставить("Должность");
	ПредыдущаяСтрока.Вставить("Подразделение");
	ПредыдущаяСтрока.Вставить("Организация");
	
	МассивЗадублированныхСтавок = Новый Массив;
	МассивПустыхДолжностей = Новый Массив;
	
	КонтрольныйРазряд = Разряды[0].Ссылка;
	Для Каждого Строка Из ЗагруженныеДанные Цикл
		
		Если НЕ ЗначениеЗаполнено(Строка.Должность) Тогда
			МассивПустыхДолжностей.Добавить(Строка);
		КонецЕсли;
		
		Если Строка.Разряд <> КонтрольныйРазряд Тогда
			Продолжить;
		КонецЕсли;
		
		Если Строка.Должность = ПредыдущаяСтрока.Должность
			И Строка.Подразделение = ПредыдущаяСтрока.Подразделение
			И Строка.Организация = ПредыдущаяСтрока.Организация Тогда
			МассивЗадублированныхСтавок.Добавить(Строка);
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ПредыдущаяСтрока, Строка);
		
	КонецЦикла;
	
	Если МассивЗадублированныхСтавок.Количество() > 0 Тогда
		
		ТекстОшибки = НСтр("ru = 'Загрузка отменена. Ставки по должности %1, %2 задублированы.';
							|en = 'Import is cancelled. Rates for the %1, %2 job title are duplicated.'");
		Для Каждого Ставка Из МассивЗадублированныхСтавок Цикл
			
			ОбщегоНазначения.СообщитьПользователю(
				СтрШаблон(ТекстОшибки, Ставка.Должность, Ставка.Подразделение));
				
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	Для Каждого СтрокаБезДолжности Из МассивПустыхДолжностей Цикл
		ЗагруженныеДанные.Удалить(СтрокаБезДолжности);
	КонецЦикла;
	
	Объект.ТарифныеСтавки.Загрузить(ЗагруженныеДанные);
	
	Идентификаторы = Новый Массив;
	Для Каждого Элемент Из Объект.ТарифныеСтавки Цикл
		Идентификаторы.Добавить(Элемент.ПолучитьИдентификатор());
	КонецЦикла;
	
	СохранитьНаСервере(Идентификаторы);
	
	ПрочитатьЗаполнитьТарифнуюСетку();
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеКолонокМакетаДляЗагрузкиТарифнойСетки(ДополнительныеПараметры)
	
	КолонкиМакета = Новый Массив;
	
	Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Должность", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Должность';
																																						|en = 'Job title'"), 14);
	Колонка.ОбязательнаДляЗаполнения = Истина;
	Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
	КолонкиМакета.Добавить(Колонка);
	
	Если ДополнительныеПараметры.ИспользуютсяПодразделения Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Подразделение", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Подразделение';
																																								|en = 'Department'"), 14);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяОрганизации Тогда
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета("Организация", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(150)), НСтр("ru = 'Организация';
																																								|en = 'Company'"), 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЕсли;
	
	Для Каждого Разряд Из Разряды Цикл
		Колонка = ЗагрузкаДанныхИзФайлаКлиентСервер.ОписаниеКолонкиМакета(ИмяПоляРазряда(Разряд.Идентификатор), Новый ОписаниеТипов("Число",, Новый КвалификаторыЧисла(15, 2)), Разряд.Наименование, 10);
		Колонка.Позиция = КолонкиМакета.ВГраница() + 1;
		КолонкиМакета.Добавить(Колонка);
	КонецЦикла;
	
	Возврат КолонкиМакета;
	
КонецФункции

#КонецОбласти

#КонецОбласти
