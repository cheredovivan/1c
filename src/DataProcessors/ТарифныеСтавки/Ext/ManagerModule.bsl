#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ЗагрузкаДанныхИзФайла

// Производит сопоставление данных, загружаемых в табличную часть ПолноеИмяТабличнойЧасти,
// с данными в ИБ, и заполняет параметры АдресТаблицыСопоставления и СписокНеоднозначностей.
// 
// Параметры:
// 	АдресЗагружаемыхДанных- Строка - адрес временного хранилища с таблицей значений, в которой
//                                   находятся загруженные данные из файла.
// 	АдресТаблицыСопоставления - Строка - адрес временного хранилища с пустой таблицей значений,
//                                       являющейся копией табличной части документа, 
//                                       которую необходимо заполнить из таблицы АдресЗагружаемыхДанных.
// 	СписокНеоднозначностей - ТаблицаЗначений - состоит из:
//  * Идентификатор - Число - идентификатор
//  * Колонка - Строка - имя колонки
// 	ПолноеИмяТабличнойЧасти - Строка - полное имя табличной части
// 	ДополнительныеПараметры - Структура - дополнительные параметры, переданные из формы-источнике:
//  * Разряды - ТаблицаЗначений - Состоит из:
//  ** Ссылка - СправочникСсылка.РазрядыКатегорииДолжностей
//  ** Наименование - Строка
//  ** Идентификатор - УникальныйИдентификатор
//  * ИспользуютсяПодразделения - Булево
//  * ИспользуютсяОрганизации - Булево
Процедура СопоставитьЗагружаемыеДанные(АдресЗагружаемыхДанных, АдресТаблицыСопоставления, СписокНеоднозначностей, ПолноеИмяТабличнойЧасти, ДополнительныеПараметры) Экспорт
	
	ТарифныеСтавки = ПолучитьИзВременногоХранилища(АдресТаблицыСопоставления);// ТаблицаЗначений
	ЗагружаемыеДанные = ПолучитьИзВременногоХранилища(АдресЗагружаемыхДанных);// ТаблицаЗначений
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаВтЗагружаемыеДанныеТарифныеСтавки());
	ТекстыЗапроса.Добавить(ТекстЗапросаВтДанныеДолжностей());
	
	Если ДополнительныеПараметры.ИспользуютсяПодразделения Тогда
		ТекстыЗапроса.Добавить(ТекстЗапросаВтДанныеПодразделений());
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИспользуютсяОрганизации Тогда
		ТекстыЗапроса.Добавить(ТекстЗапросаВтДанныеОрганизаций());
	КонецЕсли;
	
	ТекстыЗапроса.Добавить(ТекстЗапросаЗагружаемыеДанныеТарифныеСтавки());
	
	Запрос = СоздатьНастроитьЗапрос(ТекстыЗапроса, ЗагружаемыеДанные, ДополнительныеПараметры);// Запрос
	Результат = Запрос.Выполнить().Выгрузить();
	
	Идентификатор = 1;
	Для Каждого Строка Из Результат Цикл
		
		СтрокаЗагружаемыхДанных = ЗагружаемыеДанные.Найти(Строка.Идентификатор, "Идентификатор");
		
		Для Каждого Разряд Из ДополнительныеПараметры.Разряды Цикл
			
			// Параллельное дозаполнение загружаемых данных для корректной работы механизма БСП
			СтрокаЗагружаемыхДанных.Идентификатор = Идентификатор;
			ЗаполнитьЗначенияСвойств(ЗагружаемыеДанные.Добавить(), СтрокаЗагружаемыхДанных);
			
			НоваяСтрока = ТарифныеСтавки.Добавить();
			Строка.Идентификатор = Идентификатор;
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.Идентификатор = Идентификатор;
			НоваяСтрока.Разряд = Разряд.Ссылка;
			НоваяСтрока.Тариф = Строка[ИмяПоляРазряда(Разряд.Идентификатор)];
			
			Если Строка.КоличествоДолжностей > 1 Тогда
				ЗаписьОНеоднозначности               = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = Строка.Идентификатор;
				ЗаписьОНеоднозначности.Колонка       = "Должность";
			КонецЕсли;
			
			Если ДополнительныеПараметры.ИспользуютсяПодразделения И Строка.КоличествоПодразделений > 1 Тогда
				ЗаписьОНеоднозначности               = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = Строка.Идентификатор;
				ЗаписьОНеоднозначности.Колонка       = "Подразделение";
			КонецЕсли;
			
			Если ДополнительныеПараметры.ИспользуютсяОрганизации И Строка.КоличествоОрганизаций > 1 Тогда
				ЗаписьОНеоднозначности               = СписокНеоднозначностей.Добавить();
				ЗаписьОНеоднозначности.Идентификатор = Строка.Идентификатор;
				ЗаписьОНеоднозначности.Колонка       = "Организация";
			КонецЕсли;
			
			Идентификатор = Идентификатор + 1;
			
		КонецЦикла;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(ТарифныеСтавки, АдресТаблицыСопоставления);
КонецПроцедуры

// Параметры:
//  ПолноеИмяТабличнойЧасти - Строка - полное имя табличной части
//  СписокНеоднозначностей - Массив из СтрокаТаблицыЗначений
//  ИмяКолонки - Строка
//  ЗагружаемыеЗначенияСтрока - СтрокаТаблицыЗначений
//  ДополнительныеПараметры - Структура
Процедура ЗаполнитьСписокНеоднозначностей(ПолноеИмяТабличнойЧасти, СписокНеоднозначностей, ИмяКолонки, ЗагружаемыеЗначенияСтрока, ДополнительныеПараметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ИмяКолонки = "Должность" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Должности.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Должности КАК Должности
		|
		|ГДЕ
		|	НЕ &Должность = """"
		|	И Должности.Наименование = &Должность
		|";
		
		Запрос.УстановитьПараметр("Должность", ЗагружаемыеЗначенияСтрока.Должность);
		
	КонецЕсли;
	
	Если ИмяКолонки = "Подразделение" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Подразделения.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.СтруктураПредприятия КАК Подразделения
		|
		|ГДЕ
		|	НЕ &Подразделение = """"
		|	И Подразделения.Наименование = &Подразделение
		|";
		
		Запрос.УстановитьПараметр("Подразделение", ЗагружаемыеЗначенияСтрока.Подразделение);
		
	КонецЕсли;
	
	Если ИмяКолонки = "Организация" Тогда
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|
		|ГДЕ
		|	НЕ &Организация = """"
		|	И Организации.Наименование = &Организация
		|";
		
		Запрос.УстановитьПараметр("Организация", ЗагружаемыеЗначенияСтрока.Организация);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Текст) Тогда
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			СписокНеоднозначностей.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьНастроитьЗапрос(ТекстыЗапроса, ЗагружаемыеДанные, ДополнительныеПараметры)
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	ПоляТарифРазряда = "";
	Для Каждого Разряд Из ДополнительныеПараметры.Разряды Цикл
		ПоляТарифРазряда = ПоляТарифРазряда + "
		|	ЗагружаемыеДанные." + ИмяПоляРазряда(Разряд.Идентификатор) + " КАК " +  ИмяПоляРазряда(Разряд.Идентификатор) + ",";
	КонецЦикла;
	
	ПоляТарифРазряда = Лев(ПоляТарифРазряда, СтрДлина(ПоляТарифРазряда) - 1);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗагружаемыеДанныеТарифРазряда", ПоляТарифРазряда);
	
	// Используются подразделения
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗагружаемыеДанныеПодразделение,",
		?(ДополнительныеПараметры.ИспользуютсяПодразделения, "ЗагружаемыеДанные.Подразделение КАК Подразделение,", ""));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Подразделение,", ?(ДополнительныеПараметры.ИспользуютсяПодразделения, "
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДанныеПодразделений.КоличествоПодразделений, 0) = 1
		|			ТОГДА ДанныеПодразделений.Подразделение
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)
		|	КОНЕЦ КАК Подразделение,", ""));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоПодразделений,", 
		?(ДополнительныеПараметры.ИспользуютсяПодразделения, "ЕСТЬNULL(ДанныеПодразделений.КоличествоПодразделений, 0) КАК КоличествоПодразделений,", ""));
	
	ТекстЗапроса = ТекстЗапроса + ?(ДополнительныеПараметры.ИспользуютсяПодразделения, "
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеПодразделений КАК ДанныеПодразделений
		|	ПО ДанныеПодразделений.Идентификатор = ЗагружаемыеДанные.Идентификатор", "");
	
	// Используются организации
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗагружаемыеДанныеОрганизация,",
		?(ДополнительныеПараметры.ИспользуютсяОрганизации, "ЗагружаемыеДанные.Организация КАК Организация,", ""));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Организация,", ?(ДополнительныеПараметры.ИспользуютсяОрганизации, "
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(ДанныеОрганизаций.КоличествоОрганизаций, 0) = 1
		|			ТОГДА ДанныеОрганизаций.Организация
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
		|	КОНЕЦ КАК Организация,", ""));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоличествоОрганизаций,", 
		?(ДополнительныеПараметры.ИспользуютсяОрганизации, "ЕСТЬNULL(ДанныеОрганизаций.КоличествоОрганизаций, 0) КАК КоличествоОрганизаций,", ""));
	
	ТекстЗапроса = ТекстЗапроса + ?(ДополнительныеПараметры.ИспользуютсяОрганизации, "
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеОрганизаций КАК ДанныеОрганизаций
		|	ПО ДанныеОрганизаций.Идентификатор = ЗагружаемыеДанные.Идентификатор", "");
	
	// Сортировка для проверки на дубли в форме рабочего места
	ТекстЗапроса = ТекстЗапроса + "
	|	УПОРЯДОЧИТЬ ПО
	|		Должность";
	
	ТекстЗапроса = ТекстЗапроса + ?(ДополнительныеПараметры.ИспользуютсяПодразделения, ",
	|		Подразделение", "");
	ТекстЗапроса = ТекстЗапроса + ?(ДополнительныеПараметры.ИспользуютсяОрганизации, ",
	|		Организация", "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ЗагружаемыеДанные", ЗагружаемыеДанные);
	
	Возврат Запрос;
	
КонецФункции

Функция ТекстЗапросаВтЗагружаемыеДанныеТарифныеСтавки()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗагружаемыеДанные.Идентификатор КАК Идентификатор,
	|	ЗагружаемыеДанные.Должность     КАК Должность,
	|	&ЗагружаемыеДанныеПодразделение,
	|	&ЗагружаемыеДанныеОрганизация,
	|	&ЗагружаемыеДанныеТарифРазряда
	|ПОМЕСТИТЬ ВтЗагружаемыеДанные
	|ИЗ
	|	&ЗагружаемыеДанные КАК ЗагружаемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеДолжностей()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Должности.Идентификатор                   КАК Идентификатор,
	|	МАКСИМУМ(Должности.Должность)             КАК Должность,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Должности.Должность) КАК КоличествоДолжностей
	|ПОМЕСТИТЬ ВтДанныеДолжностей
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗагружаемыеДанные.Идентификатор КАК Идентификатор,
	|		Должности.Ссылка                КАК Должность
	|	ИЗ
	|		ВтЗагружаемыеДанные КАК ЗагружаемыеДанные
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
	|		ПО Должности.Наименование = ЗагружаемыеДанные.Должность
	|
	|	ГДЕ
	|		НЕ ЗагружаемыеДанные.Должность = """"
	|	) КАК Должности
	|
	|СГРУППИРОВАТЬ ПО
	|	Должности.Идентификатор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеПодразделений()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Подразделения.Идентификатор                       КАК Идентификатор,
	|	МАКСИМУМ(Подразделения.Подразделение)             КАК Подразделение,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Подразделения.Подразделение) КАК КоличествоПодразделений
	|ПОМЕСТИТЬ ВтДанныеПодразделений
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗагружаемыеДанные.Идентификатор КАК Идентификатор,
	|		Подразделения.Ссылка            КАК Подразделение
	|	ИЗ
	|		ВтЗагружаемыеДанные КАК ЗагружаемыеДанные
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтруктураПредприятия КАК Подразделения
	|		ПО Подразделения.Наименование = ЗагружаемыеДанные.Подразделение
	|
	|	ГДЕ
	|		НЕ ЗагружаемыеДанные.Подразделение = """"
	|	) КАК Подразделения
	|
	|СГРУППИРОВАТЬ ПО
	|	Подразделения.Идентификатор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВтДанныеОрганизаций()
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Организации.Идентификатор                     КАК Идентификатор,
	|	МАКСИМУМ(Организации.Организация)             КАК Организация,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Организации.Организация) КАК КоличествоОрганизаций
	|ПОМЕСТИТЬ ВтДанныеОрганизаций
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЗагружаемыеДанные.Идентификатор КАК Идентификатор,
	|		Организации.Ссылка              КАК Организация
	|	ИЗ
	|		ВтЗагружаемыеДанные КАК ЗагружаемыеДанные
	|		
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Организации.Наименование = ЗагружаемыеДанные.Организация
	|
	|	ГДЕ
	|		НЕ ЗагружаемыеДанные.Организация = """"
	|	) КАК Организации
	|
	|СГРУППИРОВАТЬ ПО
	|	Организации.Идентификатор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Идентификатор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗагружаемыеДанныеТарифныеСтавки()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ЗагружаемыеДанные.Идентификатор                          КАК Идентификатор,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДанныеДолжностей.КоличествоДолжностей, 0) = 1
	|			ТОГДА ДанныеДолжностей.Должность
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)
	|	КОНЕЦ                                                    КАК Должность,
	|	&Подразделение,
	|	&Организация,
	|	ЕСТЬNULL(ДанныеДолжностей.КоличествоДолжностей, 0)       КАК КоличествоДолжностей,
	|	&КоличествоПодразделений,
	|	&КоличествоОрганизаций,
	|	&ЗагружаемыеДанныеТарифРазряда
	|ИЗ
	|	ВтЗагружаемыеДанные КАК ЗагружаемыеДанные
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВтДанныеДолжностей КАК ДанныеДолжностей
	|	ПО ДанныеДолжностей.Идентификатор = ЗагружаемыеДанные.Идентификатор
	|";
	
	Возврат ТекстЗапроса;
	
КонецФункции

#КонецОбласти

Функция ИмяПоляРазряда(Идентификатор)
	Возврат "Разряд" + "_" +  СтрЗаменить(Строка(Идентификатор), "-", "_");
КонецФункции

#КонецОбласти

#КонецЕсли