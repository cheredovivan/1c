
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокЗаполнения = "ВидЭП, Доверенность, ЕстьБумажнаяДоверенность, Идентификатор, ИдентификаторМП, ИдентификаторЭДО,
	|Имя, ИмяУстройства, ИНН, НомерТелефона, Организация, Отчество, ПолноеИмя,
	|СНИЛС, СерияВУ, НомерВУ, ДатаВыдачиВУ, Фамилия, ФизическоеЛицо, Роль, ВидыДокументов, БумажнаяДоверенностьДата,
	|БумажнаяДоверенностьНомер, ЛичныеДокументыДляОтправкиВМП";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокЗаполнения);
	
	ПрямойОбмен = Неопределено;
	Параметры.Свойство("ПрямойОбмен", ПрямойОбмен);
	
	СписокДоверенностей = Неопределено;
	Если Параметры.Свойство("СписокДоверенностей", СписокДоверенностей) Тогда
		Для Каждого Элемент Из СписокДоверенностей Цикл
			Элементы.Доверенность.СписокВыбора.Добавить(Элемент.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ПолноеИмя = Фамилия + " " + Имя + " " + Отчество;
	
	Если Элементы.ВидЭП.СписокВыбора.Количество() = 1 Тогда
		Элементы.ГруппаВидЭП.Видимость = Ложь;
		ВидЭП = Элементы.ВидЭП.СписокВыбора[0].Значение;
	КонецЕсли;
	
	УстановитьВидОбменаПриСозданииНаСервере();
	
	ПредставленияРолейМП = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияРолейМП();
	Для Каждого КиЗ Из ПредставленияРолейМП Цикл
		Элементы.Роль.СписокВыбора.Добавить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
	
	АтомарныеРолиМП = СервисВзаимодействияМПЭПДКлиентСервер.АтомарныеРолиМП();
	ОбязательныеРолиМП = СервисВзаимодействияМПЭПДКлиентСервер.ОбязательныеРолиМП();
	Для Каждого ОбязательнаяРоль Из ОбязательныеРолиМП Цикл
		Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(Роль, ОбязательнаяРоль, АтомарныеРолиМП.Количество()) = Ложь Тогда
			Роль = Роль + ОбязательнаяРоль;
		КонецЕсли;	
	КонецЦикла;
	
	ПредставленияВидовДокументовМП = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияВидовДокументовМП();
	Для Каждого КиЗ Из ПредставленияВидовДокументовМП Цикл
		Элементы.ВидыДокументов.СписокВыбора.Добавить(КиЗ.Ключ, КиЗ.Значение);
	КонецЦикла;
	
	ОформитьГиперссылкуДоверенности(ЭтотОбъект);
	УстановитьВидимостьВодительскогоУдостоверения(ЭтотОбъект);
	ЗаполнитьИОтразитьЛицензиюНаМедосмотрПриСоздании();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	УбратьРеквизиты = Новый Массив;
	ПроверяемыеРеквизиты.Очистить();
	
	ВсеРеквизиты = ПолучитьРеквизиты();
	Для Каждого СтрокаМассива Из ВсеРеквизиты Цикл
		Если СтрокаМассива.СохраняемыеДанные Тогда
			ПроверяемыеРеквизиты.Добавить(СтрокаМассива.Имя);
		КонецЕсли;
	КонецЦикла;
	
	ФорматированныйТелефон = ЗначениеТелефона(НомерТелефона);
	Если СтрДлина(ФорматированныйТелефон) <> 12 Тогда
		ТекстСообщения = НСтр("ru = 'Телефон должен быть введен полностью и содержать 11 цифр.';
								|en = 'The phone number must be entered in full and contain 11 digits.'");
		Поле = "НомерТелефона";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
	КонецЕсли;
	
	Результат = СервисВзаимодействияМПЭПДКлиентСервер.ВидПодписиДоступенДляРолиИВидаОбмена(ВидЭП,
			ПрямойОбмен, Роль);
	Если Не Результат.ВидПодписиДоступен Тогда
		ТекстСообщения =Результат.ТекстОшибки;;
		Поле = "ВидЭП";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , Поле, , Отказ);
	КонецЕсли;
	
	УстановитьНеобходимостьПроверкиВодительскгоУдостоверения(ПроверяемыеРеквизиты);
	
	ПроверяемыеРеквизиты.Добавить("ПредставлениеУчетнойЗаписиЭДО");
	
	УбратьРеквизиты.Добавить("ИНН");		
	Если ВидЭП = Перечисления.ВидыПодписиМПЭПД.НеИспользуется
		Или ВидЭП = Перечисления.ВидыПодписиМПЭПД.ПростаяПодпись
		Или ПрямойОбмен = Ложь Тогда
		УбратьРеквизиты.Добавить("СНИЛС");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, УбратьРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Роль", Роль);
	ПараметрыФормы.Вставить("ВидыДокументов", ВидыДокументов);
	
	РольОткрытие_Завершение = Новый ОписаниеОповещения("РольОткрытие_Завершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборРолей", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор, , , 
		РольОткрытие_Завершение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	

КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидыДокументов", ВидыДокументов);
	
	ВидыДокументов_Завершение = Новый ОписаниеОповещения("ВидыДокументов_Завершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВыборВидовДокументов", 
		ПараметрыФормы, 
		ЭтотОбъект, 
		УникальныйИдентификатор, , , 
		ВидыДокументов_Завершение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ФизическоеЛицоПриИзменении(Элемент)
	
	ОкончаниеПодбораСотруднкаСервер(ФизическоеЛицо);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТелефонаПриИзменении(Элемент)
	
	НомерТелефонаТолькоЦифрыИПрефикс = ЗначениеТелефона(НомерТелефона);
	Если МожноПолучитьПредставлениеТелефона(НомерТелефонаТолькоЦифрыИПрефикс) Тогда
		НомерТелефона = ОбменСГИСЭПДКлиентСервер.ПолучитьПредставлениеТелефона(НомерТелефонаТолькоЦифрыИПрефикс);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФамилияПриИзменении(Элемент)
	
	ИзменениеФИО();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПриИзменении(Элемент)
	
	ИзменениеФИО();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчествоПриИзменении(Элемент)
	
	ИзменениеФИО();
	
КонецПроцедуры

&НаКлиенте
Процедура СНИЛСПриИзменении(Элемент)
	
	ЗадавалсяВопросОДубляхПользователя = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ЗадавалсяВопросОДубляхПользователя = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЭППриИзменении(Элемент)
	
	Если ВидЭП = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.НеИспользуется")
		Или ВидЭП = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.ПростаяПодпись") Тогда
		Элементы.СНИЛС.ОтметкаНезаполненного = Ложь;
		Элементы.СНИЛС.АвтоОтметкаНезаполненного = Ложь;
	Иначе
		Элементы.СНИЛС.АвтоОтметкаНезаполненного = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидЭПНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуВыбораВидаПодписи(Элемент,
		ВидЭП, УникальныйИдентификатор);
	
	КонецПроцедуры
	
&НаКлиенте
Процедура ВидЭПОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриПолучениДанныхОтФормыВыбораВидаПодписи(ВыбранноеЗначение);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ГиперссылкаНаДоверенностьНажатие(Элемент)
	
	ОткрытьФормуДоверенности();
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОбменаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СтруктураДанныхДоверенности = Новый Структура;
	СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенностьДата", БумажнаяДоверенностьДата);
	СтруктураДанныхДоверенности.Вставить("БумажнаяДоверенностьНомер", БумажнаяДоверенностьНомер);
	СтруктураДанныхДоверенности.Вставить("Доверенность", Доверенность);
	СтруктураДанныхДоверенности.Вставить("ЕстьБумажнаяДоверенность", ЕстьБумажнаяДоверенность);
	
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуВыбораСхемыОбмена(Элемент,
		ПрямойОбмен, СтруктураДанныхДоверенности, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОбменаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПриПолучениДанныхОтФормыВыбораВидаОбмена(ВыбранноеЗначение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицензияНаМедосмотрСерияПриИзменении(Элемент)
	
	ЗаполнитьЛицензиюНаМедосмотрВЛичныеДокументыДляОтправкиВМП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицензияНаМедосмотрНомерПриИзменении(Элемент)
	
	ЗаполнитьЛицензиюНаМедосмотрВЛичныеДокументыДляОтправкиВМП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицензияНаМедосмотрДатаВыдачиПриИзменении(Элемент)
	
	ЗаполнитьЛицензиюНаМедосмотрВЛичныеДокументыДляОтправкиВМП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЛицензияНаМедосмотрСрокДействияПриИзменении(Элемент)
	
	ЗаполнитьЛицензиюНаМедосмотрВЛичныеДокументыДляОтправкиВМП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура РольОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументовОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьИЗакрыть(Команда)
	
	ЗакрытьПослеИзменения = Истина;
	ЗапуститьСценарийИзменениеПрофиляПользователя(ЗакрытьПослеИзменения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РольОткрытие_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Роль = Результат;
		
		ОформитьГиперссылкуДоверенности(ЭтотОбъект);
		УстановитьВидимостьВодительскогоУдостоверения(ЭтотОбъект);
		УстановитьВидимостьЛицензииНаМедосмотр(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыДокументов_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		ВидыДокументов = Результат;
		
		АтомарныеРолиМП = СервисВзаимодействияМПЭПДКлиентСервер.АтомарныеРолиМП();
		ПредставленияПоддерживаемыхРолейМПВсе = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияРолейМП(Ложь, , Неопределено);
		ПредставленияПоддерживаемыхРолейМП = СервисВзаимодействияМПЭПДКлиентСервер.ПредставленияРолейМП(Ложь, , ВидыДокументов);
		ЕстьУдаленныеРоли = Ложь;
		Для Каждого КиЗ Из ПредставленияПоддерживаемыхРолейМПВсе Цикл
			Если ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(Роль, КиЗ.Ключ, АтомарныеРолиМП.Количество())
			И ПредставленияПоддерживаемыхРолейМП.Получить(КиЗ.Ключ) = Неопределено Тогда
				Роль = Роль - КиЗ.Ключ;
				ЕстьУдаленныеРоли = Истина;
			КонецЕсли;
		КонецЦикла;
		Если ЕстьУдаленныеРоли Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Из списка установленных ролей удалены роли, не соответствующие выбранным видам документов';
											|en = 'Roles that do not correspond to the selected document types are removed from the list of set roles'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БлокироватьРаботуПользователя(Блокировать)
	
	Элементы.ФормаИзменитьИЗакрыть.Доступность = Не Блокировать;
	
КонецПроцедуры

&НаСервере
Процедура ОкончаниеПодбораСотруднкаСервер(Знач СсылкаСправочника)
	
	ДанныеСотрудника = СервисВзаимодействияМПЭПД.ПолучитьДанныеФизическогоЛица(СсылкаСправочника);
	
	ИНН = ДанныеСотрудника.ИНН;
	СНИЛС = ДанныеСотрудника.СтраховойНомерПФР;
	НомерТелефонаТолькоЦифрыИПрефикс = ЗначениеТелефона(ДанныеСотрудника.Телефон);
	Если МожноПолучитьПредставлениеТелефона(НомерТелефонаТолькоЦифрыИПрефикс) Тогда
		НомерТелефона = ОбменСГИСЭПДКлиентСервер.ПолучитьПредставлениеТелефона(НомерТелефонаТолькоЦифрыИПрефикс);
	Иначе
		НомерТелефона = НомерТелефонаТолькоЦифрыИПрефикс;
	КонецЕсли;
	Имя = ДанныеСотрудника.Имя;
	Фамилия = ДанныеСотрудника.Фамилия;
	Отчество = ДанныеСотрудника.Отчество;
	ПолноеИмя = Фамилия + " " + Имя + " " + Отчество;
	
	Если ПустаяСтрока(ИмяУстройства) Тогда
		ИмяУстройства = Фамилия + " " + Имя + " " + Отчество;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеТелефона(Знач ТекущееЗначение)
	
	МаскаПрименена = СтрНачинаетсяС(ТекущееЗначение, "+7");
	
	Результат = "";
	ТолькоСимволы = "0123456789";
	Для Счетчик = 1 По СтрДлина(ТекущееЗначение) Цикл
		ТекСимвол = Сред(ТекущееЗначение, Счетчик, 1);
		Если СтрНайти(ТолькоСимволы, ТекСимвол) > 0 Тогда
			Результат = Результат + ТекСимвол;
		КонецЕсли;
	КонецЦикла;
	
	Если Не МаскаПрименена 
		И Не ПустаяСтрока(Результат)
		И СтрДлина(Результат) = 10 Тогда
		Результат = "7" + Результат;
	КонецЕсли;
	
	Если СтрДлина(Результат) = 11 И Лев(Результат, 1) = "7" Тогда
		Результат = "+" + Результат;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МожноПолучитьПредставлениеТелефона(Знач Телефон)
	
	ЕстьПлюс = Лев(Телефон, 1) = "+";
	ДлинаТелефона = СтрДлина(Телефон);
	
	Если (ЕстьПлюс И ДлинаТелефона = 12) Или ДлинаТелефона = 11 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

#Область АсинхронныеСценарии

&НаКлиенте
Процедура ЗапуститьСценарийИзменениеПрофиляПользователя(ЗакрытьПослеИзменения)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураРезультата = ПроверитьУникальностьДанныхПользователя();
	
	Если СтруктураРезультата.Успешность Тогда
		СценарийИзменениеПрофиляПользователяПродолжение(ЗакрытьПослеИзменения);
	Иначе
		Структура = Новый Структура("ЗакрытьПослеИзменения", ЗакрытьПослеИзменения);
		ОписаниеОповещенияОтвета = Новый ОписаниеОповещения("ВопросОСохраненииНеУникальныхДанныхЗавершение", ЭтотОбъект, Структура);
		СообщениеПользователю = СтруктураРезультата.СообщениеПользователю + Символы.ПС + Символы.ПС + "Сохранить данные текущего мобильного устройства?";
		ПоказатьВопрос(ОписаниеОповещенияОтвета, СообщениеПользователю, РежимДиалогаВопрос.ОКОтмена, , , , ); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииНеУникальныхДанныхЗавершение(Результат, ДополнительныеДанные) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ЗадавалсяВопросОДубляхПользователя = Истина;
		СценарийИзменениеПрофиляПользователяПродолжение(ДополнительныеДанные.ЗакрытьПослеИзменения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СценарийИзменениеПрофиляПользователяПродолжение(ЗакрытьПослеИзменения)
	
	ПараметрыЦикла = Новый Структура;
	ПараметрыЦикла.Вставить("ЭтапЦикла", "Начало");
	ПараметрыЦикла.Вставить("ЗакрытьПослеИзменения", ЗакрытьПослеИзменения);
	
	СервисВзаимодействияМПЭПДКлиент.НачатьАсинхронныйСценарий(Новый ОписаниеОповещения("СценарийИзменениеПрофиляПользователя", ЭтотОбъект, ПараметрыЦикла));
	
КонецПроцедуры

&НаКлиенте
Функция СценарийИзменениеПрофиляПользователя(ОжиданиеРезультата, ПараметрыЦикла) Экспорт
	
	Результат = Истина;
	ЭтапЦикла = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ПараметрыЦикла, "ЭтапЦикла");
	
	Если ЭтапЦикла = "Начало" Тогда
		ПараметрыЦикла.Вставить("ЭтапЦикла", "ЗакрытьФорму");
		БлокироватьРаботуПользователя(Истина);
		
		Водитель = Новый Структура;
		Водитель.Вставить("ИдентификаторМП", ИдентификаторМП);
		Водитель.Вставить("Наименование", ИмяУстройства);
		Водитель.Вставить("ВидЭП", ВидЭП);
		Водитель.Вставить("Телефоны", ЗначениеТелефона(НомерТелефона));
		Водитель.Вставить("СНИЛС", СНИЛС);
		Водитель.Вставить("ИНН", ИНН);
		
		Водитель.Вставить("СерияВУ", СерияВУ);
		Водитель.Вставить("НомерВУ", НомерВУ);
		Водитель.Вставить("ДатаВыдачиВУ", ДатаВыдачиВУ);
		
		РольСУчетомДокументов = СервисВзаимодействияМПЭПДКлиентСервер.РольСУчетомВидовДокументов(Роль, ВидыДокументов);
		
		Водитель.Вставить("РольСУчетомДокументов", РольСУчетомДокументов);
		Водитель.Вставить("Роль", Роль);
		Водитель.Вставить("ВидыДокументов", ВидыДокументов);

		Водитель.Вставить("МЧД", Доверенность);
		Водитель.Вставить("БумажнаяДоверенностьНомер", БумажнаяДоверенностьНомер);
		Водитель.Вставить("БумажнаяДоверенностьДата", БумажнаяДоверенностьДата);
		Водитель.Вставить("ЕстьБумажнаяДоверенность", ЕстьБумажнаяДоверенность);
		Водитель.Вставить("ПрямойОбмен", ПрямойОбмен);
		Водитель.Вставить("ЛичныеДокументыДляОтправкиВМП", ЛичныеДокументыДляОтправкиВМП);
		
		ПараметрыФункции = СервисВзаимодействияМПЭПДКлиент.МассивИзЗначений(ИдентификаторЭДО, Водитель);
		СервисВзаимодействияМПЭПДКлиент.НовоеФоновоеЗадание("СервисВзаимодействияМПЭПД.СервисИзменитьПользователя",
			ПараметрыФункции, ПараметрыЦикла, УникальныйИдентификатор);
	КонецЕсли;
	
	Если ЭтапЦикла = "ЗакрытьФорму" Тогда
		Если ОжиданиеРезультата.Статус = "Выполнено" Тогда
			РезультатПроцедуры = ПолучитьИзВременногоХранилища(ОжиданиеРезультата.АдресРезультата);
			Если РезультатПроцедуры <> Истина Тогда
				ПоказатьПредупреждение(, НСтр("ru = 'Не удалось изменить данные пользователя в сервисе взаимодействия с мобильными устройствами.';
												|en = 'Cannot change the user data in the mobile device collaboration service.'"));
			Иначе
				Модифицированность = Ложь;
				ОповещенияОбИзмененииПрофиля = СервисВзаимодействияМПЭПДКлиент.ИмяОповещенияОбИзмененииПрофиляПользователяМП();
				СтруктураПолей = СервисВзаимодействияМПЭПДКлиент.СтруктураПолейРегистраПодключенныхМПЭПД();
				ЗаполнитьЗначенияСвойств(СтруктураПолей, ЭтотОбъект);
				СтруктураПолей.Вставить("УникальныйИдентификатор", ВладелецФормы.УникальныйИдентификатор);
				СтруктураПолей.Вставить("ИмяУстройства", ИмяУстройства);
				СтруктураПолей.Вставить("ВидЭП", ВидЭП);
				СтруктураПолей.Вставить("НомерТелефона", НомерТелефона);
				СтруктураПолей.Вставить("Доверенность", Доверенность);
				
				Оповестить(ОповещенияОбИзмененииПрофиля, СтруктураПолей);
				Если ПараметрыЦикла.ЗакрытьПослеИзменения Тогда
					Закрыть();
				КонецЕсли;
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(ОжиданиеРезультата.КраткоеПредставлениеОшибки);
		КонецЕсли;
		БлокироватьРаботуПользователя(Ложь);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция НовыйРезультатПроверкиУникальностиДанныхПользователя()
	
	Структура = Новый Структура;
	Структура.Вставить("Успешность", Истина);
	Структура.Вставить("СообщениеПользователю", "");
	Структура.Вставить("СоответствиеПользователей");
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция НоваяСтруктураДляВыбораПользователя()
		
	Структура = Новый Структура;
	Структура.Вставить("Наименование");
	Структура.Вставить("МассивПолей");
	Структура.Вставить("ПоляСтрокой");
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьУникальностьДанныхПользователяНаСервере(КлючевыеПоля, Идентификатор, ИдентификаторЭДО)
	
	Результат = НовыйРезультатПроверкиУникальностиДанныхПользователя();
	
	Запрос = Новый Запрос;
	РегистрыСведений.ПодключенныеМПЭПД.ДобавитьВЗапросТекстИПараметрыПоискаПовторяющихсяЗаписей(Запрос,
			КлючевыеПоля, Идентификатор, ИдентификаторЭДО);
			
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	СоответствиеПользователей = Новый Соответствие;
	МетоданныеРесурсов = Метаданные.НайтиПоТипу(Тип("РегистрСведенийКлючЗаписи.ПодключенныеМПЭПД")).Ресурсы;
	МассивПовторяющихсяПолей = Новый Массив;
	
	// Формирование соответствия с уникальными идентификаторами записей и повторяемыми полями
	Пока Выборка.Следующий() Цикл
		Если СоответствиеПользователей[Выборка.Идентификатор] = Неопределено Тогда
			СтруктураДляВыбораПользователя = НоваяСтруктураДляВыбораПользователя();
			СтруктураДляВыбораПользователя.Наименование = Выборка.Наименование;
			СтруктураДляВыбораПользователя.МассивПолей = Новый Массив;
			СоответствиеПользователей.Вставить(Выборка.Идентификатор, СтруктураДляВыбораПользователя);
		КонецЕсли;
		
		СтруктураДляВыбораПользователя = СоответствиеПользователей[Выборка.Идентификатор];
		СинонимРесурса = МетоданныеРесурсов[Выборка.ПолеПоискаПовторений].Синоним;
		СтруктураДляВыбораПользователя.МассивПолей.Добавить(СинонимРесурса);
		
		Если МассивПовторяющихсяПолей.Найти(СинонимРесурса) = Неопределено Тогда
			МассивПовторяющихсяПолей.Добавить(СинонимРесурса);
		КонецЕсли;
		
	КонецЦикла;
	
	// Формирование сообщения для пользователя
	Если МассивПовторяющихсяПолей.Количество() = 1 Тогда
		Шаблон = НСтр("ru = 'По одному из ключевых полей: %1';
						|en = 'By one of the key fields: %1'");
		СписокПолей = МассивПовторяющихсяПолей[0];
		Начало = СтрШаблон(Шаблон, СписокПолей);
	Иначе
		Шаблон = НСтр("ru = 'По ключевым полям: %1';
						|en = 'By key fields: %1'");
		СписокПолей = СтрСоединить(МассивПовторяющихсяПолей, ", ");
		Начало = СтрШаблон(Шаблон, СписокПолей);
	КонецЕсли;
	
	Если СоответствиеПользователей.Количество() = 1 Тогда
		Шаблон = НСтр("ru = ' найдено ранее добавленное мобильное устройство: %1.';
						|en = ' the previously added mobile device is found: %1.'");
	Иначе
		Шаблон = НСтр("ru = ' найдены ранее добавленные мобильные устройства.
				|Список устройств: %1.';
				|en = ' previously added mobile devices are found.
				|Device list: %1.'");
	КонецЕсли;
	
	МассивУстройств = Новый Массив;
	СписокУстройств = "";
	СчетчикЗаписей = 0;
	Для Каждого КлючЗначение Из СоответствиеПользователей Цикл
		
		СтруктураДляВыбораПользователя = КлючЗначение.Значение;
		ПоляСтрокой = СтрСоединить(СтруктураДляВыбораПользователя.МассивПолей, ", ");
		СтруктураДляВыбораПользователя.ПоляСтрокой = ПоляСтрокой;
		
		// Сообщение включает две проименованных записи, дальше добавляется приписка "и еще <количество>"
		СчетчикЗаписей = СчетчикЗаписей + 1;
		Если СчетчикЗаписей < 3 Тогда
			МассивУстройств.Добавить(СтруктураДляВыбораПользователя.Наименование);
			Если СчетчикЗаписей = 2 Тогда
				Если СоответствиеПользователей.Количество() > 2 Тогда
					СписокУстройств = СтрСоединить(МассивУстройств, ", ");
					СписокУстройств = СписокУстройств + " и еще " + СоответствиеПользователей.Количество() - 2;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если СписокУстройств = "" Тогда
		СписокУстройств = СтрСоединить(МассивУстройств, ", ");
	КонецЕсли;
	
	Середина = СтрШаблон(Шаблон, СписокУстройств);
	
	Шаблон = "%1%2";
	СообщениеПользователю = СтрШаблон(Шаблон, Начало, Середина);
	
	Результат.СообщениеПользователю = СообщениеПользователю;
	Результат.СоответствиеПользователей = СоответствиеПользователей;
	Результат.Успешность = Ложь;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПроверитьУникальностьДанныхПользователя()
	
	// Отмена проверки при повторной регистрации и если выопрос уже был задан ранее
	Если ЗадавалсяВопросОДубляхПользователя Тогда
		СтруктураРезультата = НовыйРезультатПроверкиУникальностиДанныхПользователя();
		Возврат СтруктураРезультата;
	КонецЕсли;
	
	КлючевыеПоля = СервисВзаимодействияМПЭПДКлиент.КлючевыеПоляКонтроляПовторенийРегистраПодключенныхМПЭПД();
	ЗаполнитьЗначенияСвойств(КлючевыеПоля, ЭтотОбъект);
	СтруктураРезультата = ПроверитьУникальностьДанныхПользователяНаСервере(КлючевыеПоля,
		Идентификатор, ИдентификаторЭДО);
	
	Возврат СтруктураРезультата;
	
КонецФункции

#КонецОбласти

#Область ИмяУстройства

&НаКлиенте
Процедура ИзменениеФИО()
	
	ПолноеИмя = Фамилия + " " + Имя + " " + Отчество;
	
	СформироватьИмяУстройства();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИмяУстройства()
	
	ИмяУстройства = ПолноеИмя;
	
КонецПроцедуры

#КонецОбласти

#Область ВодительскоеУдостоверение

&НаКлиентеНаСервереБезКонтекста
Функция РольВодителяУстановлена(Роль)
	
	АтомарныеРолиМП = СервисВзаимодействияМПЭПДКлиентСервер.АтомарныеРолиМП();
	РольВодитель = Pow(2, АтомарныеРолиМП.Найти(НСтр("ru = 'Водитель';
													|en = 'Driver'")));
	РольВодителяУстановлена = ОбменСГИСЭПДКлиентСервер.ВхождениеРоли(Роль, РольВодитель, АтомарныеРолиМП.Количество());
	Возврат РольВодителяУстановлена;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьВодительскогоУдостоверения(Форма)
	
	Элементы = Форма.Элементы;
	Роль = Форма.Роль;
	
	РольВодителяУстановлена = РольВодителяУстановлена(Роль);
	Элементы.ГруппаВУ.Видимость = РольВодителяУстановлена;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНеобходимостьПроверкиВодительскгоУдостоверения(МассивПроверяемыхРеквизитов)

	РольВодителяУстановлена = РольВодителяУстановлена(Роль);	
	Если Не РольВодителяУстановлена Тогда
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("СерияВУ");
		МассивНепроверяемыхРеквизитов.Добавить("НомерВУ");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВыдачиВУ");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(МассивПроверяемыхРеквизитов,
			МассивНепроверяемыхРеквизитов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ВидОбмена

&НаСервере
Процедура УстановитьВидОбменаПриСозданииНаСервере()
	
	Если ПрямойОбмен И 
		(ВидЭП <> ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УНЭПГосключ")
			И ВидЭП <> ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УКЭПГосключ")
			И ВидЭП <> ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УКЭПРутокен"))  Тогда
		ПрямойОбмен = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПолучениДанныхОтФормыВыбораВидаОбмена(ВыбранноеЗначение)
	
	Если ВыбранноеЗначение.ПрямойОбмен 
		И ВидЭП = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.ПростаяПодпись") Тогда
		Оповещения = Новый ОписаниеОповещения("ВопросСменыВидаОбменаЗавершениеСлужебный", ЭтотОбъект, ВыбранноеЗначение);
		ТекстВопроса = НСтр("ru = 'При обмене напрямую с другими участниками нельзя использовать простую подпись. Вид подписи будет очищен. Продолжить?';
							|en = 'When exchanging directly with other participants, you cannot use a simple signature. The signature kind will be cleared. Continue?'");
		ПоказатьВопрос(Оповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена); 
		Возврат;
	ИначеЕсли ВыбранноеЗначение.ПрямойОбмен 
		И ВидЭП = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УНЭПГосключ") Тогда
		Оповещения = Новый ОписаниеОповещения("ВопросСменыВидаОбменаЗавершениеСлужебный", ЭтотОбъект, ВыбранноеЗначение);
		ТекстВопроса = НСтр("ru = 'При обмене напрямую с другими участниками нельзя использовать УНЭП Госключ. Вид подписи будет очищен. Продолжить?';
							|en = 'When exchanging directly with other participants, you cannot use the enhanced encrypted non-certified digital signature of Goskey. The signature kind will be cleared. Continue?'");
		ПоказатьВопрос(Оповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена); 
		Возврат;
	Иначе
		ЗаполнитьЗначенияОтФормыВыбораВидаОбменаСлужебный(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСменыВидаОбменаЗавершениеСлужебный(Результат, ВыбранноеЗначение) Экспорт

	Если Результат = КодВозвратаДиалога.ОК Тогда
		ВидЭП = Неопределено; 
		ЗаполнитьЗначенияОтФормыВыбораВидаОбменаСлужебный(ВыбранноеЗначение);
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначенияОтФормыВыбораВидаОбменаСлужебный(ВыбранноеЗначение)

	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыбранноеЗначение);
	ОформитьГиперссылкуДоверенности(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область ВидПодписи

&НаКлиенте
Процедура ПриПолучениДанныхОтФормыВыбораВидаПодписи(ВыбранноеЗначение)
	
	Если ПрямойОбмен 
		И ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.ПростаяПодпись") Тогда
		Оповещения = Новый ОписаниеОповещения("ВопросСменыВидаПодписиЗавершениеСлужебный", ЭтотОбъект, ВыбранноеЗначение);
		ТекстВопроса = НСтр("ru = 'Простую подпись нельзя использовать при обмене напрямую с другими участниками. Будет установлен обмен проектами титулов внутри организации. Продолжить?';
							|en = 'Cannot use a simple signature when exchanging directly with other participants. An exchange of title projects within the company will be established. Continue?'");
		ПоказатьВопрос(Оповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена); 
		Возврат;
	ИначеЕсли ПрямойОбмен 
		И ВыбранноеЗначение = ПредопределенноеЗначение("Перечисление.ВидыПодписиМПЭПД.УНЭПГосключ") Тогда
		Оповещения = Новый ОписаниеОповещения("ВопросСменыВидаПодписиЗавершениеСлужебный", ЭтотОбъект, ВыбранноеЗначение);
		ТекстВопроса = НСтр("ru = 'УНЭП Госключ нельзя использовать при обмене напрямую с другими участниками. Будет установлен обмен проектами титулов внутри организации. Продолжить?';
							|en = 'Cannot use the enhanced encrypted non-certified digital signature of Goskey when exchanging directly with other participants. An exchange of title projects within the company will be established. Continue?'");
		ПоказатьВопрос(Оповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена);
		Возврат; 
	Иначе
		ВидЭП = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросСменыВидаПодписиЗавершениеСлужебный(Результат, ВыбранноеЗначение) Экспорт

	Если Результат = КодВозвратаДиалога.ОК Тогда
		ПрямойОбмен = Ложь;
		ЗаполнитьЗначенияВыбораВидаПодписиСлужебный(ВыбранноеЗначение);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЗначенияВыбораВидаПодписиСлужебный(ВыбранноеЗначение)

	ВидЭП = ВыбранноеЗначение;
	ОформитьГиперссылкуДоверенности(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#Область Доверенность

&НаКлиентеНаСервереБезКонтекста
Процедура ОформитьГиперссылкуДоверенности(Форма)
	
	ОтражатьДоверенность = УстановитьВидимостьДоверенности(Форма);
	
	Если Не ОтражатьДоверенность Тогда
		Возврат;
	КонецЕсли;
		
	ДоверенностьЗаполнена = (ЗначениеЗаполнено(Форма.БумажнаяДоверенностьНомер)
							И ЗначениеЗаполнено(Форма.БумажнаяДоверенностьДата)
							И Форма.ЕстьБумажнаяДоверенность)
							Или (ЗначениеЗаполнено(Форма.Доверенность) 
							И Не Форма.ЕстьБумажнаяДоверенность);
	
	Если Не ДоверенностьЗаполнена Тогда
		ИмяГиперссылкиСсылки = НСтр("ru = 'Заполнить';
									|en = 'Fill'");
	ИначеЕсли Форма.ЕстьБумажнаяДоверенность Тогда
		Шаблон = НСтр("ru = 'Доверенность №%1 от %2';
						|en = 'Letter of authority No. %1 dated %2'");
		ИмяГиперссылкиСсылки = СтрШаблон(Шаблон, Форма.БумажнаяДоверенностьНомер,
			Формат(Форма.БумажнаяДоверенностьДата, "ДФ=dd.MM.yyyy"));
	Иначе
		ИмяГиперссылкиСсылки = Строка(Форма.Доверенность);
	КонецЕсли;
	
	Форма.Элементы.ГиперссылкаНаДоверенность.Заголовок = ИмяГиперссылкиСсылки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьВидимостьДоверенности(Форма)

	ИндексРолиМедработника = СервисВзаимодействияМПЭПДКлиентСервер.ИндексАтомарнойРолиМедработника();
	РольТолькоМедработник = Pow(2, ИндексРолиМедработника);
	ОтражатьДоверенность = Форма.ПрямойОбмен И Форма.Роль <> РольТолькоМедработник;
	Форма.Элементы.ГруппаДоверенность.Видимость = ОтражатьДоверенность;
	
	Возврат ОтражатьДоверенность;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуДоверенности()

	ЕстьДоверенность = (ЗначениеЗаполнено(БумажнаяДоверенностьНомер)
							И ЗначениеЗаполнено(БумажнаяДоверенностьДата))
						Или ЗначениеЗаполнено(Доверенность);
	
	ПараметрыОткрытия = Новый Структура;
	Если ЕстьДоверенность Тогда
		ПараметрыОткрытия.Вставить("БумажнаяДоверенность", ЕстьБумажнаяДоверенность);
	Иначе
		ПараметрыОткрытия.Вставить("БумажнаяДоверенность", Неопределено);
	КонецЕсли;
	ПараметрыОткрытия.Вставить("ДатаБумажнойДоверенности", БумажнаяДоверенностьДата);
	ПараметрыОткрытия.Вставить("НомерБумажнойДоверенности", БумажнаяДоверенностьНомер);
	ПараметрыОткрытия.Вставить("МЧД", Доверенность);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыДоверенности", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВводДанныхДоверенности", ПараметрыОткрытия,
		ЭтотОбъект, УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыДоверенности(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьБумажнаяДоверенность = Результат.БумажнаяДоверенность;
	Доверенность = Результат.МЧД;
	БумажнаяДоверенностьДата = Результат.ДатаБумажнойДоверенности;
	БумажнаяДоверенностьНомер = Результат.НомерБумажнойДоверенности;

	ОформитьГиперссылкуДоверенности(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ЛицензияНаПроведниеМедосмотра

&НаСервере
Процедура ЗаполнитьИОтразитьЛицензиюНаМедосмотрПриСоздании()

	ЗаполнитьЛицензиюНаМедосмотр(ЭтотОбъект);
	УстановитьВидимостьЛицензииНаМедосмотр(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьЛицензиюНаМедосмотр(Форма)
	
	СрукутураЛицензии = 
		СервисВзаимодействияМПЭПДКлиентСервер.СрукутураЛицензииНаМедосмотрИзСтроки(Форма.ЛичныеДокументыДляОтправкиВМП);
	
	Форма.ЛицензияНаМедосмотрДатаВыдачи = СрукутураЛицензии.ДатаВыдачи;
	Форма.ЛицензияНаМедосмотрНомер = СрукутураЛицензии.Номер;
	Форма.ЛицензияНаМедосмотрСерия = СрукутураЛицензии.Серия;
	Форма.ЛицензияНаМедосмотрСрокДействия = СрукутураЛицензии.СрокДействия;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЛицензииНаМедосмотр(Форма)
	
	АтомарныеРолиМП = СервисВзаимодействияМПЭПДКлиентСервер.АтомарныеРолиМП();
	ИндексМедосмотра = АтомарныеРолиМП.Найти(НСтр("ru = 'Медработник';
													|en = 'Healthcare worker'"));
	ВыбранаРольМедосмотр = 
		СервисВзаимодействияМПЭПДКлиентСервер.РольПользователяБезУчетаДокументовПоИндексуУстановлена(Форма.Роль, ИндексМедосмотра);
	Форма.Элементы.ГруппаЛицензияНаМедосмотр.Видимость = ВыбранаРольМедосмотр;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьЛицензиюНаМедосмотрВЛичныеДокументыДляОтправкиВМП(Форма)

	Строка = СервисВзаимодействияМПЭПДКлиентСервер.ДанныеЛицензииНаМедосмотрСтрокой(Форма.ЛицензияНаМедосмотрСерия,
		Форма.ЛицензияНаМедосмотрНомер, Форма.ЛицензияНаМедосмотрДатаВыдачи, Форма.ЛицензияНаМедосмотрСрокДействия); 
	Форма.ЛичныеДокументыДляОтправкиВМП = Строка;

КонецПроцедуры

#КонецОбласти

#КонецОбласти
