
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокСвойствДляЗаполнения = "Документ, Организация, Роль, ИдентификаторЭДО, ДоступенОтказ, Титул, ИндексРабот";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, СписокСвойствДляЗаполнения);
	ИдентификаторЗаписиВыбранногоМП = Параметры.ИдентификаторЗаписиМП;
	ЗаголовокПользователяМП = Параметры.ЗаголовокПользователяМП;
	
	// Индекс работ может быть не задан
	Если ИндексРабот = 0 Тогда
		ЭтоДействиеПривязки = Не ЗначениеЗаполнено(ИдентификаторЗаписиВыбранногоМП);
		ТипВзаимодействия = СервисВзаимодействияМПЭПДКлиентСервер.ТипПривязкиОтвязки(Документ, ЭтоДействиеПривязки, Роль, Титул);
		ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыПоТипуВзаимодействия(ТипВзаимодействия);
	КонецЕсли;
	
	СклонениеПользователяМП = ПолучитьСклоненияСтроки(ЗаголовокПользователяМП, "Л=ru_RU;ПЛ=Мужской", "ПД=Родительный")[0];
	Шаблон = НСтр("ru = 'Мобильное устройство %1';
					|en = '%1 mobile device'");
	Заголовок = СтрШаблон(Шаблон, НРег(СклонениеПользователяМП));
	Элементы.ДекорацияПользовательСписком.Заголовок = ЗаголовокПользователяМП;
	
	ЗаполнитьИформациюОПользователяхПоДаннымДокументаПриСоздании(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ВыполняетсяПриСозданииНаСервере = Истина;
	ЗаполнитьДанныеУстройства(ЭтотОбъект, ВыполняетсяПриСозданииНаСервере);
	
	ОпределитьВозможностьОтвязкиПриСозданииНаСервере();
	
	ЦветНекорректныхДанныхЭПД = ЦветНекорректныхДанныхЭПД();
	ИзменитьСоставЭлементовУправления(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПользовательМППриИзменении(Элемент)
	
	ИзменениеНомераПользователяПоДаннымТитула();
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеМобильногоУстройстваОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФормуУстройства(ИдентификаторЗаписиВыбранногоМП);

КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеМобильногоУстройстваНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ВыбратьУстройство();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИнформационнаяНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ВыполнитьДействиеНажатияНаГиперссылкуМП(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеМобильногоУстройстваОчистка(Элемент, СтандартнаяОбработка)
	
	Если Не МожноОтвязатьДокумент Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(, НСтр("ru = 'Нельзя изменить мобильное устройство, от которого получен титул';
										|en = 'Cannot change the mobile device from which you received the title'"));
		Возврат;
	КонецЕсли;
	
	ОчиститьМобильноеУстройство();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтказатьсяОтПеревозки(Команда)
	
	ВыполнитьОтказОтПеревозки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ВыполнитьПривязкуОтвязку()
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПрочиеВспомогательныеМетоды

&НаСервереБезКонтекста
Функция ЦветНекорректныхДанныхЭПД()

	Возврат ЦветаСтиля.ЦветНекорректныхДанныхЭПД;	

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьСоставЭлементовУправления(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ФормаОтказатьсяОтПеревозки.Видимость = Форма.ДоступенОтказ;
	
КонецПроцедуры

#КонецОбласти

#Область ОтвязкаИПривязкаДокументов

&НаКлиенте
Процедура ВыполнитьПривязкуОтвязку()

	Если ИдентификаторЗаписиВыбранногоМП = ИдентификаторЗаписиМППривязанногоКДокументу Тогда
		Закрыть();
	
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗаписиВыбранногоМП) Тогда
		ПривязатьДокумент();
		
	ИначеЕсли Не ЗначениеЗаполнено(ИдентификаторЗаписиВыбранногоМП) Тогда
		ОтвязатьДокумент();
		
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция НоваяСтруктураРезультатаПривязкиОтвязки()

	Структура = Новый Структура;
	Структура.Вставить("Успешность", Истина);
	Структура.Вставить("ОписаниеОшибки", "");
	Возврат Структура;

КонецФункции

&НаКлиенте
Процедура ПоказатьОшибкуОтвязкиПривязки(Результат)
	
	ПоказатьПредупреждение(, Результат.ОписаниеОшибки);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ДобавитьОшибкуОтвязкиПривязки(Результат, ОписаниеОшибки)
	
	Результат.Успешность = Ложь;
	Результат.ОписаниеОшибки = ОписаниеОшибки;

КонецПроцедуры

&НаКлиенте
Процедура ОтвязатьДокумент() 
	
	Результат = СервисОтвязатьДокумент(ИдентификаторЭДО, Документ, ИндексРабот);
	
	Если Результат.Успешность Тогда
		ПараметрОповещения = СервисВзаимодействияМПЭПДКлиентСервер.ПараметрыОтвязкиМПНаФорме(ИндексРабот);
		Оповестить("МПОтвязаноОтДокумента", ПараметрОповещения, Документ);
		Закрыть();
	Иначе
		ПоказатьОшибкуОтвязкиПривязки(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СервисОтвязатьДокумент(Знач ИдентификаторЭДО, Знач Документ, Знач ИндексРабот)
	
	Результат = НоваяСтруктураРезультатаПривязкиОтвязки();
	
	ОсновнойЭлектронныйДокумент = ИнтеграцияЭДО.ОсновнойЭлектронныйДокументОбъектаУчета(Документ);
	Если ОсновнойЭлектронныйДокумент = Неопределено Тогда
		ДобавитьОшибкуОтвязкиПривязки(Результат, НСтр("ru = 'Не удалось отвязать мобильное устройство от документа, не найден основной электронный документ';
														|en = 'Cannot unlink the mobile device from the document, the main electronic document is not found'"));
		Возврат Результат;
	КонецЕсли;
	
	ЭтоДействиеПривязки = Истина;
	ТипВзаимодействияМПЭПД = СервисВзаимодействияМПЭПДКлиентСервер.ТипПривязкиОтвязки(Документ, ЭтоДействиеПривязки,
		, , ИндексРабот);
	Успешность = СервисВзаимодействияМПЭПД.СервисОтвязатьДокумент(Документ, ИдентификаторЭДО, ТипВзаимодействияМПЭПД);
	
	Если Успешность <> Истина Тогда
		ДобавитьОшибкуОтвязкиПривязки(Результат, НСтр("ru = 'Не удалось отвязать мобильное устройство от документа на сервере мобильных устройств';
														|en = 'Cannot unlink the mobile device from the document on the mobile device server'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПривязатьДокумент() 
	
	Если ВладелецФормы <> Неопределено Тогда
		ИдентификаторАдресаХранилища = ВладелецФормы.УникальныйИдентификатор;
	Иначе
		ИдентификаторАдресаХранилища = Новый УникальныйИдентификатор();	
	КонецЕсли;
	
	Результат = СервисПривязатьДокумент(ИдентификаторЭДО, Документ, ИндексРабот,
		ИдентификаторЗаписиВыбранногоМП, ДанныеВыбранногоУстройства, ИдентификаторАдресаХранилища);
		
	Если Результат.Успешность Тогда
		ПараметрОповещения = Результат.ПараметрОповещения;
		Если ПараметрОповещения <> Неопределено Тогда
			Оповестить("МППривязаноКДокументу", ПараметрОповещения, Документ);
		КонецЕсли;
		
		Закрыть();
	Иначе
		ПоказатьОшибкуОтвязкиПривязки(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СервисПривязатьДокумент(Знач ИдентификаторЭДО, Знач Документ, Знач ИндексРабот,
		Знач ИдентификаторЗаписиМП, Знач ДанныеВыбранногоУстройства, Знач ИдентификаторАдресаХранилища)
		
	Результат = НоваяСтруктураРезультатаПривязкиОтвязки();
	
	ОсновнойЭлектронныйДокумент = ИнтеграцияЭДО.ОсновнойЭлектронныйДокументОбъектаУчета(Документ);
	Если ОсновнойЭлектронныйДокумент = Неопределено Тогда
		ДобавитьОшибкуОтвязкиПривязки(Результат, НСтр("ru = 'Нельзя привязать мобильное устройство к неотправленному документу';
														|en = 'Cannot link a mobile device to an unsent document'"));
		Возврат Результат;
	КонецЕсли;
	
	ЭтоДействиеПривязки = Истина;
	ТипВзаимодействияМПЭПД = СервисВзаимодействияМПЭПДКлиентСервер.ТипПривязкиОтвязки(Документ,
		ЭтоДействиеПривязки, , , ИндексРабот);
	ИдентификаторДокументооборота = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновнойЭлектронныйДокумент,
		"ИдентификаторДокументооборота");
	СтруктураДанныхПоРаботеМП = СервисВзаимодействияМПЭПДКлиентСервер.НоваяСтруктураДанныхПоРаботеМП();
	СтруктураДанныхПоРаботеМП.ИдентификаторМП = ДанныеВыбранногоУстройства.ИдентификаторМП;
	СтруктураДанныхПоРаботеМП.ИдентификаторЗаписиМП = ИдентификаторЗаписиМП;
	СтруктураДанныхПоРаботеМП.ИндексРабот = ИндексРабот;
	НаименованиеМП = ДанныеВыбранногоУстройства.Наименование;
	
	Успешность = СервисВзаимодействияМПЭПД.СервисИзменитьДокумент(ИдентификаторЭДО,
		ИдентификаторДокументооборота, ТипВзаимодействияМПЭПД, Документ, СтруктураДанныхПоРаботеМП, , НаименованиеМП);	
			
	Если Успешность = Истина Тогда
		ПараметрОповещения = ПараметрыОповещенияОПривязкеДокумента(ИдентификаторАдресаХранилища,
			ДанныеВыбранногоУстройства, ИндексРабот);
		Результат.Вставить("ПараметрОповещения", ПараметрОповещения);
	Иначе
		ДобавитьОшибкуОтвязкиПривязки(Результат, НСтр("ru = 'Не удалось привязать мобильное устройство к документу на сервере мобильных устройств';
														|en = 'Cannot link the mobile device to the document on the mobile device server'"));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПараметрыОповещенияОПривязкеДокумента(Знач ИдентификаторАдресаХранилища, Знач ДанныеВыбранногоУстройства, Знач ИндексРабот)
	
	ПараметрыПривязки = Неопределено;
	
	Если ДанныеВыбранногоУстройства = Неопределено Тогда
		Возврат ПараметрыПривязки;
	КонецЕсли;
	
	СтруктураДанныхПоРаботеМП = СервисВзаимодействияМПЭПДКлиентСервер.НоваяСтруктураДанныхПоРаботеМП();
	СтруктураДанныхПоРаботеМП.ИдентификаторМП = ДанныеВыбранногоУстройства.ИдентификаторМП;
	СтруктураДанныхПоРаботеМП.ИдентификаторЗаписиМП = ДанныеВыбранногоУстройства.Идентификатор; 
	СтруктураДанныхПоРаботеМП.ИндексРабот = ИндексРабот;
	
	ФотоДвоичныеДанные = ДанныеВыбранногоУстройства.Фото.Получить();
	Если ФотоДвоичныеДанные <> Неопределено Тогда		
		МПФото = ПоместитьВоВременноеХранилище(ДанныеВыбранногоУстройства.Фото.Получить(), ИдентификаторАдресаХранилища);
	Иначе
		МПВыбрано = Истина;
		КартинкаДвоичныеДанные = СервисВзаимодействияМПЭПДКлиентСервер.КартинкаРаботПоИндексу(
			ИндексРабот, МПВыбрано).ПолучитьДвоичныеДанные();
		МПФото = ПоместитьВоВременноеХранилище(КартинкаДвоичныеДанные, ИдентификаторАдресаХранилища);
	КонецЕсли;
	
	ПараметрыПривязки = СервисВзаимодействияМПЭПДКлиентСервер.ПараметрыПривязкиМПНаФорме(СтруктураДанныхПоРаботеМП,
		ДанныеВыбранногоУстройства.Наименование, МПФото); 
	
	Возврат ПараметрыПривязки;
		
КонецФункции

&НаСервере
Процедура ОпределитьВозможностьОтвязкиПриСозданииНаСервере()

	Если ИндексРабот = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыВодитель()
		Или Не ЗначениеЗаполнено(ИдентификаторЗаписиВыбранногоМП) Тогда
		МожноОтвязатьДокумент = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КэшВзаимодействияМПЭПД.ДанныеСообщения КАК ДанныеСообщения
	|ИЗ
	|	РегистрСведений.КэшВзаимодействияМПЭПД КАК КэшВзаимодействияМПЭПД
	|ГДЕ
	|	КэшВзаимодействияМПЭПД.ДокументЭПД = &Документ
	|	И КэшВзаимодействияМПЭПД.ВидСообщения = ЗНАЧЕНИЕ(Перечисление.ТипыВзаимодействияМПЭПД.Сообщение)";
	Запрос.УстановитьПараметр("Документ", Документ);
	
	СоответствиеТитуловТипамМП = СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеТитуловТипамМП();
	
	Выборка = Запрос.Выполнить().Выбрать();
	МожноОтвязатьДокумент = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Выборка.ДанныеСообщения) Тогда
			СтруктураДанныхСообщения = СервисВзаимодействияМПЭПД.СтруктураИзJson(Выборка.ДанныеСообщения);
			
			Тип = Неопределено;
			Если Не СтруктураДанныхСообщения.Свойство("Type", Тип) Тогда
				Продолжить;
			КонецЕсли;
			
			ЭтоТипСообщенияОбОтправкеТитула = СервисВзаимодействияМПЭПДКлиентСервер.ЭтоТипСообщенияОбОтправкеТитулаИзМП(Тип);
			Если ЭтоТипСообщенияОбОтправкеТитула Тогда
				МожноОтвязатьДокумент = Ложь;
				Возврат;
			КонецЕсли;
			
			ТитулСообщения = СоответствиеТитуловТипамМП.Получить(Тип);
			Если ТитулСообщения = Титул Тогда
				МожноОтвязатьДокумент = Ложь;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область МобильныеУстройстваИПользователиМП

&НаСервере
Процедура ЗаполнитьИформациюОПользователяхПоДаннымДокументаПриСоздании(Отказ)
	
	МассивСтруктурКлючевхПолейПользователей = Параметры.МассивСтруктурКлючевхПолейПользователей;
	
	Если МассивСтруктурКлючевхПолейПользователей = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПользователейНет = МассивСтруктурКлючевхПолейПользователей.Количество() = 0;
	ПользовательОдин = МассивСтруктурКлючевхПолейПользователей.Количество() = 1;
	
	СчетчикПользователей = 0;
	СоответствиеДанных = Новый Соответствие;
	Для Каждого СтруктураДанныхПользователя Из МассивСтруктурКлючевхПолейПользователей Цикл
		
		СтруктураКлючевыхПолей = СервисВзаимодействияМПЭПДКлиентСервер.СтруктураКлючевыхПолейДляПоискаМП();
		ЗаполнитьЗначенияСвойств(СтруктураКлючевыхПолей, СтруктураДанныхПользователя);
		Если СтруктураДанныхПользователя.Свойство("ФЛ_ИНН") Тогда
			СтруктураКлючевыхПолей.ИНН = СтруктураДанныхПользователя.ФЛ_ИНН;
		КонецЕсли;
		
		СоответствиеДанных.Вставить(СчетчикПользователей, СтруктураКлючевыхПолей);
		
		ПредставлениеПользователя = ПолучитьПредставлениеПользователя(СтруктураДанныхПользователя);
		Элементы.НомерПользователяВДанныхТитула.СписокВыбора.Добавить(СчетчикПользователей, ПредставлениеПользователя);
		СчетчикПользователей = СчетчикПользователей + 1;
	КонецЦикла;
	ИнформацияОПользователяхПоДаннымТитула = Новый ФиксированноеСоответствие(СоответствиеДанных);
	
	Если ПользователейНет Тогда
		Элементы.ГруппаНомерПользователяВДанныхТитула.Видимость = Ложь;
	ИначеЕсли ПользовательОдин Тогда
		Элементы.НомерПользователяВДанныхТитула.КнопкаВыпадающегоСписка = Ложь;
		Элементы.НомерПользователяВДанныхТитула.ТолькоПросмотр = Истина;
	Иначе
		Элементы.НомерПользователяВДанныхТитула.КнопкаВыпадающегоСписка = Истина;
		Элементы.НомерПользователяВДанныхТитула.ТолькоПросмотр = Ложь;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьДанныеУстройства(Форма, ВыполняетсяПриСозданииНаСервере)
	
	СтруктураДанныхУстройства = СтруктураДанныхУстройстваПоКлючевымПолям(Форма.ИнформацияОПользователяхПоДаннымТитула,
		Форма.ИдентификаторЗаписиВыбранногоМП, Форма.НомерПользователяВДанныхТитула, Форма.ИдентификаторЭДО, Форма.Роль);
		
	Если СтруктураДанныхУстройства = Неопределено Тогда
		Форма.ДанныеНайденногоУстройства = Новый ФиксированнаяСтруктура;
		Форма.Элементы.ДекорацияИнформационнаяНадпись.Заголовок = Новый ФорматированнаяСтрока("");
		Возврат;
	КонецЕсли;
	
	Форма.ДанныеНайденногоУстройства = Новый ФиксированнаяСтруктура(СтруктураДанныхУстройства);
	
	УстройствоВыбраноПриСозданииНаСервере = ВыполняетсяПриСозданииНаСервере
		И ЗначениеЗаполнено(Форма.ИдентификаторЗаписиВыбранногоМП);
	
	Если УстройствоВыбраноПриСозданииНаСервере Тогда
		ВыполнитьВыборНайденногоУстройстваНеИнтерактивно(Форма);
		Форма.ИдентификаторЗаписиМППривязанногоКДокументу = Форма.ИдентификаторЗаписиВыбранногоМП;
	Иначе
		Форма.ДанныеВыбранногоУстройства = Новый ФиксированнаяСтруктура;
		
		МассивСтрок = Новый Массив;
		
		СтрокаОбУстройстве = СформироватьИнформационнуюСтрокуОМобильномПриложениия(Форма.ДанныеНайденногоУстройства.Наименование);
		МассивСтрок.Добавить(СтрокаОбУстройстве);
		
		Форма.Элементы.ДекорацияИнформационнаяНадпись.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьИнформационнуюСтрокуОМобильномПриложениия(Знач НаименованиеУстройства)
	
	ПолужирныйШрифт = Новый Шрифт(, , Истина, , , , );
	ОбычныйШрифт = Новый Шрифт(, , , , , , );
	
	МассивПодстрок = Новый Массив;
	//@skip-check bsl-nstr-string-literal-format
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'Найдено устройство ';
															|en = 'Device is found'"), ОбычныйШрифт));
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(СтрШаблон("%1 ", НаименованиеУстройства), ПолужирныйШрифт));
	МассивПодстрок.Добавить(Новый ФорматированнаяСтрока(НСтр("ru = 'подставить';
															|en = 'add'"), ОбычныйШрифт,
		, , ЗначениеГиперссылкиВыбратьУстройство()));
	ИнформационнаяСтрока = Новый ФорматированнаяСтрока(МассивПодстрок);
	
	Возврат ИнформационнаяСтрока;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ЗначениеГиперссылкиВыбратьУстройство()
	
	Возврат "ЗначениеГиперссылкиВыбратьУстройство";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПользователя(СтруктураДанныхСФИО)
	
	МассивФИО = Новый Массив;
	ДобавитьНеПустоеЗначениеВМассив(МассивФИО, СтруктураДанныхСФИО.Фамилия);
	ДобавитьНеПустоеЗначениеВМассив(МассивФИО, СтруктураДанныхСФИО.Имя);
	ДобавитьНеПустоеЗначениеВМассив(МассивФИО, СтруктураДанныхСФИО.Отчество);
	ПредставлениеПользователя = СтрСоединить(МассивФИО, " ");
	
	Возврат ПредставлениеПользователя;
	
КонецФункции 

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьНеПустоеЗначениеВМассив(Массив, Значение)

	СервисВзаимодействияМПЭПДКлиентСервер.ДобавитьНеПустоеЗначениеВМассив(Массив, Значение);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ВыполнитьВыборНайденногоУстройстваНеИнтерактивно(Форма)
	
	// Заполнение данных формы
	Форма.ИдентификаторЗаписиВыбранногоМП = Форма.ДанныеНайденногоУстройства.Идентификатор;
	Форма.ДанныеВыбранногоУстройства = Форма.ДанныеНайденногоУстройства;
	Форма.ПредставлениеМобильногоУстройства = Форма.ДанныеНайденногоУстройства.Наименование;
	
	Форма.Элементы.ДекорацияИнформационнаяНадпись.Заголовок = "";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыборНайденногоУстройстваНаКлиенте()
	
	// Определение расхождений в ключевых полях
	СтруктураКлючевыхПолей = ИнформацияОПользователяхПоДаннымТитула.Получить(НомерПользователяВДанныхТитула);
	СоответствиеСРасхождениями = СформироватьРасхожденияПоКлючевымПолям(ДанныеНайденногоУстройства,
		СтруктураКлючевыхПолей);
		
	Если СоответствиеСРасхождениями.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьВыборНайденногоУстройстваНаКлиентеЗавершение", ЭтотОбъект);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("СоответствиеСРасхождениями", СоответствиеСРасхождениями);
		ПараметрыОткрытия.Вставить("ИдентификаторЗаписиМП", ДанныеНайденногоУстройства.Идентификатор);
		ПараметрыОткрытия.Вставить("ИмяМобильногоУстройства", ДанныеНайденногоУстройства.Наименование);
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
		
		ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ПредупреждениеОРазличииПолей", ПараметрыОткрытия, ЭтотОбъект,
			УникальныйИдентификатор, , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ВыполнитьВыборНайденногоУстройстваНеИнтерактивно(ЭтотОбъект);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыборНайденногоУстройстваНаКлиентеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ВыполнитьВыборНайденногоУстройстваНеИнтерактивно(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураДанныхУстройстваПоКлючевымПолям(Знач ИнформацияОПользователяхПоДаннымТитула, Знач ИдентификаторЗаписиМП,
		Знач НомерПользователяВДанныхТитула, Знач ИдентификаторЭДО, Знач Роль)
		
   	// Если ключевые поля и идентификатор записи МП не заполнены, тогда поиск не осуществляется
	Если ИнформацияОПользователяхПоДаннымТитула.Количество() = 0 
		И Не ЗначениеЗаполнено(ИдентификаторЗаписиМП) Тогда
		Возврат Неопределено;
	КонецЕсли;

	Если ЗначениеЗаполнено(ИдентификаторЗаписиМП) Тогда
		СтруктураКлючевыхПолей = Неопределено;
	Иначе
		// Из соответствия "ИнформацияОПользователяхПоДаннымТитула" получается структура с ключевыми полями по данным документа.
		// "ИнформацияОПользователяхПоДаннымТитула" заполняется по параметру "МассивСтруктурКлючевхПолейПользователей".
		СтруктураКлючевыхПолей = ИнформацияОПользователяхПоДаннымТитула.Получить(НомерПользователяВДанныхТитула);
	КонецЕсли;
	
	СтруктураДанныхМП = СервисВзаимодействияМПЭПД.СтруктураДанныхМП(ИдентификаторЭДО, Роль,
		ИдентификаторЗаписиМП, СтруктураКлючевыхПолей);

	Возврат СтруктураДанныхМП;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СформироватьРасхожденияПоКлючевымПолям(Знач КоллекцияПолейМП, Знач СтруктураКлючевыхПолей)
	
	Возврат СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеРазличийКлючевыхПолей(КоллекцияПолейМП, СтруктураКлючевыхПолей);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействиеНажатияНаГиперссылкуМП(ЗначениеГиперссылки, СтандартнаяОбработка)

	Если ЗначениеГиперссылки = ЗначениеГиперссылкиВыбратьУстройство() Тогда
		СтандартнаяОбработка = Ложь;
		ВыполнитьВыборНайденногоУстройстваНаКлиенте();		
	КонецЕсли;	

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуУстройства(ИдентификаторЗаписиОткрываемогоУстройства)

	Если ПустаяСтрока(ИдентификаторЗаписиОткрываемогоУстройства) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗаписьЭДО", ИдентификаторЭДО);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Идентификатор", ИдентификаторЗаписиОткрываемогоУстройства);
	
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуПользователяМобильногоУстройства(ПараметрыФормы);	

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьУстройство()
	
	МассивВключающихРолей = ОбменСГИСЭПДКлиентСервер.ВсеВключающиеРоли(Роль, 5);
	СписокРолей = Новый СписокЗначений;
	СписокРолей.ЗагрузитьЗначения(МассивВключающихРолей);
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Роль", СписокРолей); 
		
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработкаВыбораУстройства", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	ПараметрыФормы.Вставить("СтруктураОтбора", СтруктураОтбора);
	ПараметрыФормы.Вставить("МожноПодключитьНовоеУстройство", Истина);
	ПараметрыФормы.Вставить("ТолькоАктивные", Истина);
	ПараметрыФормы.Вставить("УстановленОтборПоРоли", Роль);
	
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуВыбораМП(ЭтотОбъект, ПараметрыФормы, ОповещениеОЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораУстройства(Результат, ДополнительныПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗаписиВыбранногоМП = Результат.Идентификатор;
	
	СтруктураДанныхУстройства = СтруктураДанныхУстройстваПоКлючевымПолям(ИнформацияОПользователяхПоДаннымТитула,
		ИдентификаторЗаписиВыбранногоМП, НомерПользователяВДанныхТитула, ИдентификаторЭДО, Роль);
		
	Если СтруктураДанныхУстройства = Неопределено Тогда
		ДанныеВыбранногоУстройства = Новый ФиксированнаяСтруктура;
		Элементы.ДекорацияИнформационнаяНадпись.Заголовок = "";
		Возврат;
	КонецЕсли;
	
	// Определение расхождений в ключевых полях
	СтруктураКлючевыхПолей = ИнформацияОПользователяхПоДаннымТитула.Получить(НомерПользователяВДанныхТитула);
	Если СтруктураКлючевыхПолей = Неопределено Тогда
		ЕстьРасхождения = Ложь;
	Иначе
		СоответствиеСРасхождениями = СформироватьРасхожденияПоКлючевымПолям(СтруктураДанныхУстройства,
			СтруктураКлючевыхПолей);
		ЕстьРасхождения = СоответствиеСРасхождениями.Количество() > 0;
	КонецЕсли;
		
	Если ЕстьРасхождения Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборУстройстваЗавершение", ЭтотОбъект, СтруктураДанныхУстройства);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("СоответствиеСРасхождениями", СоответствиеСРасхождениями);
		ПараметрыОткрытия.Вставить("ИдентификаторЗаписиМП", СтруктураДанныхУстройства.Идентификатор);
		ПараметрыОткрытия.Вставить("ИмяМобильногоУстройства", СтруктураДанныхУстройства.Наименование);
		ПараметрыОткрытия.Вставить("Организация", Организация);
		ПараметрыОткрытия.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
		
		ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ПредупреждениеОРазличииПолей", ПараметрыОткрытия, ЭтотОбъект,
			УникальныйИдентификатор, , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ДанныеВыбранногоУстройства = Новый ФиксированнаяСтруктура(СтруктураДанныхУстройства);
		ИдентификаторЗаписиВыбранногоМП = ДанныеВыбранногоУстройства.Идентификатор;
		ПредставлениеМобильногоУстройства = ДанныеВыбранногоУстройства.Наименование;
		
		Элементы.ДекорацияИнформационнаяНадпись.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборУстройстваЗавершение(Результат, ДополнительныПараметры) Экспорт
	
	Если Результат = Истина Тогда
		СтруктураДанныхУстройства = ДополнительныПараметры;
		
		ДанныеВыбранногоУстройства = Новый ФиксированнаяСтруктура(СтруктураДанныхУстройства);
		ИдентификаторЗаписиВыбранногоМП = ДанныеВыбранногоУстройства.Идентификатор;
		ПредставлениеМобильногоУстройства = ДанныеВыбранногоУстройства.Наименование;
		
		Элементы.ДекорацияИнформационнаяНадпись.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменениеНомераПользователяПоДаннымТитула()

	ВыполняетсяПриСозданииНаСервере = Ложь;
	ЗаполнитьДанныеУстройства(ЭтотОбъект, ВыполняетсяПриСозданииНаСервере);

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьМобильноеУстройство()

	ДанныеВыбранногоУстройства = Новый ФиксированнаяСтруктура;
	ИдентификаторЗаписиВыбранногоМП = "";
	
	ВыполняетсяПриСозданииФормы = Ложь;
	ЗаполнитьДанныеУстройства(ЭтотОбъект, ВыполняетсяПриСозданииФормы);
	
КонецПроцедуры

#КонецОбласти

#Область УОУ

&НаКлиенте
Процедура ВыполнитьОтказОтПеревозки()
	
	ОповещениеПослеВводаТекста = Новый ОписаниеОповещения("ОтправитьУОУ_ПослеВводаТекстаУточнения", ЭтотОбъект);
	ПоказатьВводСтроки(ОповещениеПослеВводаТекста, "", "Текст запроса уточнения (отклонения)", , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУОУ_ПослеВводаТекстаУточнения(Текст, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Текст) = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Если УстановитьТитулУОУ() = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыДокумента = ОбменСГИСЭПДВызовСервера.ЗначенияРеквизитовОбъекта(Документ, "ТекущийПолученныйТитул");
	
	НаборДействий = Новый Соответствие();
	
	ДействиеОтклонить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отклонить");
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ДействиеОтклонить);
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Подписать"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
					"Перечисление.ДействияПоЭДО.Отправить"));
	
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОбъектыУчета.Добавить(Документ);
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	
	ДополнительныеПараметрыОтклонения = ЭлектронныеДокументыЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия();
	ДополнительныеПараметрыОтклонения.Вставить("Комментарий", Текст);
	ДополнительныеПараметрыОтклонения.Вставить("ЭлементДляОтклонения", РеквизитыДокумента.ТекущийПолученныйТитул);
	ПараметрыВыполненияДействийПоЭДО.ДополнительныеПараметрыДействий.Вставить(ДействиеОтклонить, ДополнительныеПараметрыОтклонения);
	
	ДополнительныеПараметрыУспешногоЗавершения = Новый Структура;
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("ЭтоИсправление", Ложь);
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("НаборДействий", НаборДействий);
	ДополнительныеПараметрыУспешногоЗавершения.Вставить("ТекущийТитул", );
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ЭтоИсправление", Ложь);
	ДополнительныеПараметры.Вставить("ОповещениеУспешногоЗавершения",
		Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ЭтотОбъект, 
									ДополнительныеПараметрыУспешногоЗавершения));

	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент, ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияДействийПоЭДО(Результат, ДополнительныеПараметры) Экспорт
	
	ДействиеОтправить = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отправить");
	КоличествоОтправлено = Результат.ОбработаноПоДействиям.Получить(ДействиеОтправить);
	
	Если КоличествоОтправлено <> Неопределено И КоличествоОтправлено > 0 Тогда
		Шаблон = НСтр("ru = 'Титул ""%1"" отправлен';
						|en = 'The ""%1"" title is sent'");
		ТекстОповещения = СтрШаблон(Шаблон, ДополнительныеПараметры.ТекущийТитул);
		ЗаголовокОповещения = НСтр("ru = 'Электронные документы';
									|en = 'Electronic documents'");
		ДействиеПриНажатии = Неопределено;
		КартинкаОповещения = БиблиотекаКартинок.ЭмблемаСервиса1СЭДО48;

		#Если Не МобильноеПриложениеКлиент Тогда
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, ДействиеПриНажатии, ТекстОповещения, КартинкаОповещения,
			СтатусОповещенияПользователя.Важное);
		#КонецЕсли
	Иначе
		Доступность = Истина;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция УстановитьТитулУОУ()
	
	Результат = Ложь;
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	
	ТитулУОУ = ПолучитьТитулУОУ(Документ);
	
	Если ЗначениеЗаполнено(ТитулУОУ) Тогда
		ДокументОбъект.ТекущийТитул = ТитулУОУ;
		ДокументОбъект.Записать();
		
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТитулУОУ(Знач Документ)
	
	Результат = Неопределено;
	
	Если ТипЗнч(Документ) = Тип("ДокументСсылка.ЭлектроннаяТранспортнаяНакладная") Тогда
		Результат = ПредопределенноеЗначение("Перечисление.ТипыЭлементовРегламентаЭДО.ЭТрН_ОткП");
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти