
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИндексПрямойОбмен = ПолучитьИндексПоВидуОбмена(Параметры.ПрямойОбмен);
	ЗаполняемыеСвойства = "БумажнаяДоверенностьДата, БумажнаяДоверенностьНомер, Доверенность, ЕстьБумажнаяДоверенность";
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, ЗаполняемыеСвойства);
	
	ПодготовитьСсылкуНаФормуЗаполненияДоверенности(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
    ПрямойОбмен = ПолучитьВидОбменаПоИндексу(ИндексПрямойОбмен);
	
	Структура = Новый Структура;
	Структура.Вставить("БумажнаяДоверенностьДата", БумажнаяДоверенностьДата);
	Структура.Вставить("БумажнаяДоверенностьНомер", БумажнаяДоверенностьНомер);
	Структура.Вставить("Доверенность", Доверенность);
	Структура.Вставить("ПрямойОбмен", ПрямойОбмен);
	Структура.Вставить("ЕстьБумажнаяДоверенность", ЕстьБумажнаяДоверенность);
	
	ОповеститьОВыборе(Структура);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГиперссылкаНаДоверенностьНажатие(Элемент)
	
	ОткрытьФормуДоверенности();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьВидОбменаПоИндексу(Индекс)

	Если Индекс = 1 Тогда
		ПрямойОбмен = Истина;
		
	ИначеЕсли Индекс = 0 Тогда
		ПрямойОбмен = Ложь;
		
	КонецЕсли;

	Возврат ПрямойОбмен;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИндексПоВидуОбмена(ПрямойОбмен)

	Если ПрямойОбмен Тогда
		Индекс = 1;
	Иначе		
		Индекс = 0;
	КонецЕсли;

	Возврат Индекс;
	
КонецФункции

#Область Доверенность

&НаКлиентеНаСервереБезКонтекста
Процедура ПодготовитьСсылкуНаФормуЗаполненияДоверенности(Форма)
	
	ДоверенностьЗаполнена = (ЗначениеЗаполнено(Форма.БумажнаяДоверенностьНомер)
							И ЗначениеЗаполнено(Форма.БумажнаяДоверенностьДата)
							И Форма.ЕстьБумажнаяДоверенность)
							Или (ЗначениеЗаполнено(Форма.Доверенность) 
							И Не Форма.ЕстьБумажнаяДоверенность);
	
	Если Не ДоверенностьЗаполнена Тогда
		ИмяГиперссылкиСсылки = НСтр("ru = 'Заполнить данные доверенности';
									|en = 'Fill in the letter of authority data'");
	ИначеЕсли Форма.ЕстьБумажнаяДоверенность Тогда
		Шаблон = НСтр("ru = 'Доверенность №%1 от %2';
						|en = 'Letter of authority No. %1 dated %2'");
		ИмяГиперссылкиСсылки = СтрШаблон(Шаблон, Форма.БумажнаяДоверенностьНомер,
			Формат(Форма.БумажнаяДоверенностьДата, "ДФ=dd.MM.yyyy"));
	Иначе
		ИмяГиперссылкиСсылки = Строка(Форма.Доверенность);
	КонецЕсли;
	
	Форма.Элементы.ГиперссылкаНаДоверенность.Заголовок = ИмяГиперссылкиСсылки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуДоверенности()
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("БумажнаяДоверенность", ЕстьБумажнаяДоверенность);
	ПараметрыОткрытия.Вставить("ДатаБумажнойДоверенности", БумажнаяДоверенностьДата);
	ПараметрыОткрытия.Вставить("НомерБумажнойДоверенности", БумажнаяДоверенностьНомер);
	ПараметрыОткрытия.Вставить("МЧД", Доверенность);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеЗакрытияФормыДоверенности", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.ВзаимодействиеМПЭПД.Форма.ВводДанныхДоверенности", ПараметрыОткрытия,
		ЭтотОбъект, УникальныйИдентификатор, , , ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыДоверенности(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьБумажнаяДоверенность = Результат.БумажнаяДоверенность;
	Доверенность = Результат.МЧД;
	БумажнаяДоверенностьДата = Результат.ДатаБумажнойДоверенности;
	БумажнаяДоверенностьНомер = Результат.НомерБумажнойДоверенности;

	ПодготовитьСсылкуНаФормуЗаполненияДоверенности(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти