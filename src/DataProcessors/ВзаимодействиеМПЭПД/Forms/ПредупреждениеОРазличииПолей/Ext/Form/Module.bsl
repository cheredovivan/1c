
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СоответствиеСРасхождениями = Параметры.СоответствиеСРасхождениями;
	ИмяМобильногоУстройства = Параметры.ИмяМобильногоУстройства;
	ИдентификаторЗаписиМП = Параметры.ИдентификаторЗаписиМП;
	Организация = Параметры.Организация;
	ИдентификаторЭДО = Параметры.ИдентификаторЭДО;
	
	ФорматированнаяСтрокаСРасхождениями = СформироватьСтрокуСРасхождениямиПоКлючевымПолям(
		СоответствиеСРасхождениями, ИмяМобильногоУстройства);
		
	Элементы.ДекорацияРасхождения.Заголовок = ФорматированнаяСтрокаСРасхождениями;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияРасхожденияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = ГиперссылкаОткрытиеМП() Тогда
		ОткрытьФормуУстройства();
	КонецЕсли;
	
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьСтрокуСРасхождениямиПоКлючевымПолям(СоответствиеСРасхождениями, ИмяМобильногоУстройства)
	
	Если СоответствиеСРасхождениями.Количество() = 0 Тогда
		Возврат Новый ФорматированнаяСтрока("");
	КонецЕсли;
	
	МассивСтрок = Новый Массив;
	ЦветТекста = Новый Цвет();
	
	ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, НСтр("ru = 'Обнаружено расхождение в данных устройства ';
															|en = 'A discrepancy was found in the device and'") + Символы.ПС);
	ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, ИмяМобильногоУстройства, , ГиперссылкаОткрытиеМП());
	ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, НСтр("ru = ' и документа:';
															|en = ' document data:'"));
	
	ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, Символы.ПС);
	СимволРазделительСписка = Символ(8226);
	Шаблон = НСтр("ru = '%1 %2: по документу %3, в данных устройства %4';
					|en = '%1 %2: under the document %3, in device data %4'");
	СоответствиеНаименований = СервисВзаимодействияМПЭПДКлиентСервер.СоответствиеНаименованийКлючевыхПолейДляПоискаМП();
	
	Для Каждого КлючЗначение Из СоответствиеСРасхождениями Цикл
		НаименованиеПоля = СоответствиеНаименований[КлючЗначение.Ключ];
		
		ПоДаннымМП = Формат(КлючЗначение.Значение.ПоДаннымМП, "ДФ=dd.MM.yyyy");
		КлючевоеПоле = Формат(КлючЗначение.Значение.КлючевыеПоля, "ДФ=dd.MM.yyyy");
		ТекстРасхождения = СтрШаблон(Шаблон, СимволРазделительСписка, НаименованиеПоля, КлючевоеПоле, ПоДаннымМП); 
		
		ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, Символы.ПС);
		ДобавитьФорматированнуюСтрокуВМассив(МассивСтрок, ТекстРасхождения);
	КонецЦикла;
	
	Возврат Новый ФорматированнаяСтрока(МассивСтрок); 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьФорматированнуюСтрокуВМассив(Массив, СтрокаДляДобавления, ЦветТекста = Неопределено, Ссылка = "")

	ФорматированнаяСтрока = Новый ФорматированнаяСтрока(СтрокаДляДобавления, , ЦветТекста, , Ссылка);
	Массив.Добавить(ФорматированнаяСтрока);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ГиперссылкаОткрытиеМП()

	Возврат "ГиперссылкаОткрытиеМП";

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуУстройства()

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Идентификатор", ИдентификаторЗаписиМП);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("УчетнаяЗаписьЭДО", ИдентификаторЭДО);
	
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуПользователяМобильногоУстройства(ПараметрыФормы);	

КонецПроцедуры

#КонецОбласти


