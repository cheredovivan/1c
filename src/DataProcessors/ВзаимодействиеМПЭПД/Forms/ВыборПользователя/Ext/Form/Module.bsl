
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	ИдентификаторЭДО = Параметры.ИдентификаторЭДО;
	ТолькоАктивные = Параметры.ТолькоАктивные;
	УстановленОтборПоРоли = Параметры.УстановленОтборПоРоли;
	Элементы.ПодключитьПользователя.Видимость = Параметры.МожноПодключитьНовоеУстройство;
	
	Если ТипЗнч(Параметры.СтруктураОтбора) = Тип("Структура") Тогда
		НоваяСтруктураОтбора = Параметры.СтруктураОтбора;
	Иначе
		НоваяСтруктураОтбора = Новый Структура; 
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		НоваяСтруктураОтбора.Вставить("Организация", Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		НоваяСтруктураОтбора.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	КонецЕсли;
	
	Если ТолькоАктивные Тогда
		Заголовок = "Выберите активное мобильное устройства 1С-ЭПД";
		СписокСостояний = Новый СписокЗначений;
		СписокСостояний.Добавить(Перечисления.СостояниеМПЭПД.Подключен);
		НоваяСтруктураОтбора.Вставить("Состояние", СписокСостояний)
	Иначе
		Заголовок = "Выберите мобильное устройства 1С-ЭПД";
	КонецЕсли;
	
	СтруктураОтбора = Новый ФиксированнаяСтруктура(НоваяСтруктураОтбора);
	
	ЗаполнитьТаблицуМобильныхУстройств();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = СервисВзаимодействияМПЭПДКлиент.ИмяОповещенияОбИзмененииУстройства() Тогда
		
		Если Параметр.ИдентификаторОповещенияОСозданииУстройства = УникальныйИдентификатор Тогда
			ИдентификаторЗаписиНовогоУстройства = Параметр.Идентификатор;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = СервисВзаимодействияМПЭПДКлиент.ИмяОповещенияОбОбновленииСпискаМобильныхУстройств() Тогда
		
		Если Параметр.ИдентификаторОповещенияОСозданииУстройства = УникальныйИдентификатор Тогда
			ИдентификаторЗаписиНовогоУстройства = Параметр.Идентификатор;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаМобильныхУстройств

&НаКлиенте
Процедура ТаблицаМобильныхУстройствВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыборМобильногУстройства(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаМобильныхУстройствВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ВыборМобильногУстройства(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодключитьПользователя(Команда)

	ИдентификаторЭДО = "";
	Идентификатор = "";
	Если ЗначениеЗаполнено(Организация) Тогда 
		МассивИдентификаторовЭДО = МассивИдентификаторовЭДО(Организация);
		Если МассивИдентификаторовЭДО.Количество() = 1 Тогда
			ИдентификаторЭДО = МассивИдентификаторовЭДО[0];
		КонецЕсли;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ФормаПользователяМобильногоУстройстваЗакрытие",
		ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Идентификатор", Идентификатор);
	ПараметрыФормы.Вставить("УчетнаяЗаписьЭДО", ИдентификаторЭДО);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ИдентификаторОповещенияОСозданииУстройства", УникальныйИдентификатор);
	СервисВзаимодействияМПЭПДКлиент.ОткрытьФормуПользователяМобильногоУстройства(ПараметрыФормы,
		Неопределено, ОповещениеОЗавершении);
			
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМобильныеПриложения(Команда)
	
	ЗаполнитьТаблицуМобильныхУстройств();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция НовыйРезультатОтображенияМП()

	Структура = Новый Структура;
	Структура.Вставить("ЕстьУстройство", Ложь);
	Структура.Вставить("ПоказатьПредупреждениеОбОтсутствиии", Ложь);
	Структура.Вставить("ПричинаОсутствияВСписке", "");
	
    Возврат Структура;
	
КонецФункции

&НаСервере
Функция ЗаполнитьТаблицуМобильныхУстройств(НайтиУстройствоСИдентификатором = Ложь)
	
	ТаблицаПодключенныхМП = СервисВзаимодействияМПЭПД.СписокПодключенныхМП(СтруктураОтбора, ТолькоАктивные);
	ТаблицаМобильныхУстройств.Загрузить(ТаблицаПодключенныхМП);
	
	Если НайтиУстройствоСИдентификатором Тогда
		Результат = НовыйРезультатОтображенияМП();
		
		СтруктураПоискаУстройства = Новый Структура("Идентификатор", ИдентификаторЗаписиНовогоУстройства);
		МассивСтрок = ТаблицаМобильныхУстройств.НайтиСтроки(СтруктураПоискаУстройства);
		ЕстьУстройство = МассивСтрок.Количество() <> 0;
		
		Результат.ЕстьУстройство = ЕстьУстройство;
		
		Если Не ЕстьУстройство Тогда
			ОпределитьПричинуОтсутствияВСписке(Результат, ИдентификаторЗаписиНовогоУстройства);
		КонецЕсли;
				
		Возврат Результат;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОпределитьПричинуОтсутствияВСписке(Результат, Идентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПодключенныеМПЭПД.Состояние КАК Состояние,
	|	ПодключенныеМПЭПД.Роль КАК Роль
	|ИЗ
	|	РегистрСведений.ПодключенныеМПЭПД КАК ПодключенныеМПЭПД
	|ГДЕ
	|	ПодключенныеМПЭПД.Идентификатор = &Идентификатор";
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат.ПоказатьПредупреждениеОбОтсутствиии = Истина;
		
		Если ТолькоАктивные Тогда 
			Если Выборка.Состояние <> Перечисления.СостояниеМПЭПД.Подключен Тогда
				Результат.ПричинаОсутствияВСписке = НСтр("ru = 'в списке отображаются только активные устройства';
														|en = 'the list shows only active devices'");
			КонецЕсли;
		ИначеЕсли СтруктураОтбора.Свойство("Роль") Тогда
			СписокРолей = СтруктураОтбора.Роль;
			Если СписокРолей.НайтиПоЗначению(Выборка.Роль) = Неопределено Тогда
				Результат.ПричинаОсутствияВСписке = НСтр("ru = 'в списке отображаются только устройства с ролью ';
														|en = 'the list shows only devices with the role'") + УстановленОтборПоРоли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборМобильногУстройства(СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	ТекущаяСтрока = Элементы.ТаблицаМобильныхУстройств.ТекущиеДанные;
	
	Если ТекущаяСтрока <> Неопределено Тогда
		Результат = Новый Структура;
		Результат.Вставить("Наименование", ТекущаяСтрока.Наименование);
		Результат.Вставить("Организация", ТекущаяСтрока.Организация);
		Результат.Вставить("ИдентификаторЭДО", ТекущаяСтрока.ИдентификаторЭДО);
		Результат.Вставить("ИдентификаторМП", ТекущаяСтрока.ИдентификаторМП);
		Результат.Вставить("Идентификатор", ТекущаяСтрока.Идентификатор);

		Закрыть(Результат);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивИдентификаторовЭДО(Знач Организация)

	МассивИдентификаторовЭДО = СервисВзаимодействияМПЭПД.УчетныеЗаписиОрганизации(Организация);
	Возврат МассивИдентификаторовЭДО;
	
КонецФункции

&НаКлиенте
Процедура ФормаПользователяМобильногоУстройстваЗакрытие(Результат, ДополнительныеПараметры) Экспорт

	Если ПустаяСтрока(ИдентификаторЗаписиНовогоУстройства) Тогда
		Возврат;
	КонецЕсли;
	
	НайтиУстройствоСИдентификатором = Истина;
	Результат = ЗаполнитьТаблицуМобильныхУстройств(НайтиУстройствоСИдентификатором);
	Если Не Результат.ЕстьУстройство И Результат.ПоказатьПредупреждениеОбОтсутствиии Тогда
		Шаблон = НСтр("ru = 'Сохраненное устройство не отображается, т.к. %1';
						|en = 'The saved device is not displayed as %1'");
		Предупреждение = СтрШаблон(Шаблон, Результат.ПричинаОсутствияВСписке);
		ПоказатьПредупреждение(, Предупреждение);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти