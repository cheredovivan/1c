
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользуетсяНесколькоОрганизаций = ОбщегоНазначенияБЭД.ИспользуетсяНесколькоОрганизаций();
	
	Если ЗначениеЗаполнено(Параметры.Организация) Тогда
		Организация = Параметры.Организация;
	Иначе
		Если ИспользуетсяНесколькоОрганизаций Тогда
			Организация = Параметры.Организация;
		Иначе
			Организация = СервисВзаимодействияМПЭПД.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	ПослеВыбораОрганизации(ЭтотОбъект, Параметры.ИдентификаторЭДО);
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторЭДОПриИзменении(Элемент)
	
	ПослеЗаполненияИдентификатораЭДО(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ПослеВыбораОрганизации(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Не ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не заполнен идентификатор ЭДО. Настройки сохраняются для идентификатору ЭДО';
										|en = 'The EDI ID is not filled in. The settings are saved for the EDI ID'"));
	КонецЕсли;
	
	СохранитьНастройкиФорм(ИдентификаторЭДО, СписокНастроек);
	Закрыть(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФорм(Знач ИдентификаторЭДО, Знач СписокНастроек)

	НастройкиФорм = РегистрыСведений.НастройкиВзаимодействияМПЭПД.НастройкиФорм(ИдентификаторЭДО);
	НастройкиЭПЛ = НастройкиФорм["ЭлектронныйПутевойЛист"];
	НастройкиЭТрН = НастройкиФорм["ЭлектроннаяТранспортнаяНакладная"];
	
	ИндексРаботыВодитель = СервисВзаимодействияМПЭПДКлиентСервер.ИндексРаботыВодитель();
	
	Для Каждого ЭлементСписка Из СписокНастроек Цикл
		НастройкиЭПЛ.ИндексыРаботОтображаемыеМП.Вставить(ЭлементСписка.Значение, ЭлементСписка.Пометка);
		
		Если ЭлементСписка.Значение = ИндексРаботыВодитель Тогда
			НастройкиЭТрН.ИндексыРаботОтображаемыеМП.Вставить(ЭлементСписка.Значение, ЭлементСписка.Пометка);
		КонецЕсли;
	КонецЦикла;
	
	РегистрыСведений.НастройкиВзаимодействияМПЭПД.СохранитьНастройкиФорм(ИдентификаторЭДО, НастройкиФорм);

КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеПодбораУчетнойЗаписи(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ИдентификаторЭДО = РезультатВыбора.ИдентификаторЭДО;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоответствиеУчетныхЗаписейЭДОИНастроек(Знач Организация)

	МассивИдентификаторовЭДО = СервисВзаимодействияМПЭПД.УчетныеЗаписиОрганизации(Организация);
	Возврат НастройкиФорм(МассивИдентификаторовЭДО);

КонецФункции

// Возвращает настройки форм.
//
// Параметры:
//  ИдентификаторыЭДО - Строка, Массив Из Строка - 
// 
//  Соответствие, Структура - Структура с настройками форм, см. СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию().
//							  Соответствие если передан массив в ИдентификаторыЭДО.
//
&НаСервереБезКонтекста
Функция НастройкиФорм(Знач ИдентификаторыЭДО)

	Возврат РегистрыСведений.НастройкиВзаимодействияМПЭПД.НастройкиФорм(ИдентификаторыЭДО);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ПослеВыбораОрганизации(Форма, ВыбранныйИдентификаторЭДО = "")
	
	Элементы = Форма.Элементы;
	Организация = Форма.Организация;
		
	Счетчик = 0;
	СоответствиеУчетныхЗаписейЭДОИНастроек = СоответствиеУчетныхЗаписейЭДОИНастроек(Организация);
	Элементы.ИдентификаторЭДО.СписокВыбора.Очистить();
	Для Каждого КлючЗначение Из СоответствиеУчетныхЗаписейЭДОИНастроек Цикл
		ТекущаяУчетнаяЗаписьЭДО = КлючЗначение.Ключ;
		Элементы.ИдентификаторЭДО.СписокВыбора.Добавить(ТекущаяУчетнаяЗаписьЭДО);
		Счетчик = Счетчик + 1;
	КонецЦикла;
	
	Если Счетчик = 1 Тогда
		Элементы.ИдентификаторЭДО.КнопкаВыпадающегоСписка = Ложь;
		Элементы.ИдентификаторЭДО.ТолькоПросмотр = Истина;
		Элементы.ИдентификаторЭДО.Видимость = Ложь;
		Форма.ИдентификаторЭДО = Элементы.ИдентификаторЭДО.СписокВыбора[0].Значение;
	Иначе
		Элементы.ИдентификаторЭДО.КнопкаВыпадающегоСписка = Истина;
		Элементы.ИдентификаторЭДО.ТолькоПросмотр = Ложь;
		Если ПустаяСтрока(ВыбранныйИдентификаторЭДО) Тогда
			Форма.ИдентификаторЭДО = "";
		Иначе
			Форма.ИдентификаторЭДО = ВыбранныйИдентификаторЭДО;
		КонецЕсли;
	КонецЕсли;
    
	ПослеЗаполненияИдентификатораЭДО(Форма, СоответствиеУчетныхЗаписейЭДОИНастроек);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПослеЗаполненияИдентификатораЭДО(Форма, СоответствиеУчетныхЗаписейЭДОИНастроек = Неопределено)
	
	Если СоответствиеУчетныхЗаписейЭДОИНастроек = Неопределено Тогда
		НастройкиФорм = НастройкиФорм(Форма.ИдентификаторЭДО);
	Иначе
		НастройкиФорм = СоответствиеУчетныхЗаписейЭДОИНастроек.Получить(Форма.ИдентификаторЭДО); 
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(НастройкиФорм) Тогда
		НастройкиФорм = СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию();
	КонецЕсли;
	
	// Берутся настройки ЭПЛ т.к. они включают настройку для ЭТрН по водителю (просто имеют такой же индекс)
	НастройкиЭПЛ = НастройкиФорм["ЭлектронныйПутевойЛист"];
	МассивИндексовРабот = СервисВзаимодействияМПЭПДКлиентСервер.МассивИндексовРаботПоДокументу(
		"ЭлектронныйПутевойЛист");
	Форма.СписокНастроек.Очистить();
	Для Каждого ИндексРабот Из МассивИндексовРабот Цикл
		ПредставлениеРабот = СервисВзаимодействияМПЭПДКлиентСервер.НаименованиеРаботПоИндексу(ИндексРабот);
		// Может быть Неопределено
		Пометка = НастройкиЭПЛ.ИндексыРаботОтображаемыеМП.Получить(ИндексРабот) = Истина;
		Форма.СписокНастроек.Добавить(ИндексРабот, ПредставлениеРабот, Пометка);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти