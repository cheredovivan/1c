
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.БумажнаяДоверенность = Неопределено Тогда
		ИспользуетсяМЧД = Истина;
	Иначе
		ИспользуетсяМЧД = Не Параметры.БумажнаяДоверенность;
		МЧД = Параметры.МЧД;
		ДатаБумажнойДоверенности = Параметры.ДатаБумажнойДоверенности;
		НомерБумажнойДоверенности = Параметры.НомерБумажнойДоверенности;
	КонецЕсли;
	
	ВидФормыПоВидуДоверенности(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		МЧД = ВыбранноеЗначение;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользуетсяМЧДПриИзменении(Элемент)
	
	ВидФормыПоВидуДоверенности(ЭтотОбъект);
	
КонецПроцедуры
	
&НаКлиенте
Процедура МЧДНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("РежимВыбора", Истина);
	ОткрытьФорму("РегистрСведений.ЖурналМашиночитаемыхДоверенностей.ФормаСписка",
		ПараметрыОткрытия, ЭтотОбъект, , , , , РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)

	Если ЗначениеЗаполнено(МЧД) Тогда
		ВМЧДЕстьСсылкаНаРеестр = ВМЧДЕстьСсылкаНаРаспределенныйРеестр(МЧД);
		Если Не ВМЧДЕстьСсылкаНаРеестр Тогда
			ТекстСообщения = НСтр("ru = 'МЧД недействительна или отсутствует ссылка на реестр, в котором хранится МЧД. Выберите другую МЧД.';
									|en = 'The MR LOA is invalid or there is no reference to the registry where the MR LOA is stored. Choose a different MR LOA.'");
			ЗаголовокПредупреждения = "Ошибка выбора МЧД";
			ПоказатьПредупреждение(, ТекстСообщения, , ЗаголовокПредупреждения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Результат = НовыйРезультатВыбораДоверенности(Не ИспользуетсяМЧД,
		МЧД, ДатаБумажнойДоверенности, НомерБумажнойДоверенности);
	Закрыть(Результат);

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВидФормыПоВидуДоверенности(Форма)
	
	Форма.Элементы.ГруппаМЧД.Видимость = Форма.ИспользуетсяМЧД;
	Форма.Элементы.ГруппаБумажныйНоситель.Видимость = Не Форма.ИспользуетсяМЧД;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НовыйРезультатВыбораДоверенности(БумажнаяДоверенность, МЧД, ДатаБумажнойДоверенности, НомерБумажнойДоверенности)

	Структура = Новый Структура;
	Структура.Вставить("БумажнаяДоверенность", БумажнаяДоверенность);
	Структура.Вставить("МЧД", МЧД);
	Структура.Вставить("ДатаБумажнойДоверенности", ДатаБумажнойДоверенности);
	Структура.Вставить("НомерБумажнойДоверенности", НомерБумажнойДоверенности);
	
	Возврат Структура;

КонецФункции

&НаСервереБезКонтекста
Функция ВМЧДЕстьСсылкаНаРаспределенныйРеестр(МЧД)
	
	Попытка
		ДанныеДоверенности = МашиночитаемыеДоверенности.ДанныеДоверенностиДляКонтейнера(МЧД);
		Если ДанныеДоверенности.Свойство("СсылкаНаДоверенностьВРеестре") Тогда
			ВМЧДЕстьСсылкаНаРеестр = ЗначениеЗаполнено(ДанныеДоверенности.СсылкаНаДоверенностьВРеестре);
		Иначе
			ВМЧДЕстьСсылкаНаРеестр = Ложь;
		КонецЕсли;
	Исключение
		ВМЧДЕстьСсылкаНаРеестр = Ложь;
	КонецПопытки;
	
	Возврат ВМЧДЕстьСсылкаНаРеестр;
	
КонецФункции

#КонецОбласти