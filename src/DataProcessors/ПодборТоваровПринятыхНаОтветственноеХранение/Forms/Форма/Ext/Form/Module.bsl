
#Область ОписаниеПеременных

&НаКлиенте
Перем ВыполняетсяЗакрытие Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.Владелец) Тогда
		ВызватьИсключение НСтр("ru = 'Предусмотрено открытие обработки только из документов.';
								|en = 'Data processor can be opened only from documents.'");
	КонецЕсли;
	
	Объект.Владелец       = Параметры.Владелец;
	Объект.Организация    = Параметры.Организация;
	Объект.Склад          = Параметры.Склад;
	Объект.Договор        = Параметры.Договор;
	Объект.ТипЗапасов     = Параметры.ТипЗапасов;
	ХозяйственнаяОперация = Параметры.ХозяйственнаяОперация;
	Заказ                 = Параметры.Заказ;
	
	Параметры.Свойство("Ссылка", Ссылка);
	Параметры.Свойство("АдресТоваровНакладнойВХранилище", АдресТоваровНакладнойВХранилище);
	
	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок     = Параметры.Заголовок;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораРежимОстатков();
	ЗаполнитьСкладыНастроитьВидимость();
	ЗаполнитьТаблицуТовары();
	
	СформироватьИнформационнуюНадписьОтборы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы
		И Модифицированность Тогда
		
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Данные были изменены. Все изменения будут потеряны.';
									|en = 'All changes made to the data will be lost.'");
		
		Возврат;
		
	КонецЕсли;
	
	Если ПеренестиВДокумент
		Или ВыполняетсяЗакрытие
		Или Не ТоварыПодобраны Тогда
		
		Возврат;
		
	Иначе
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Подобранные товары не перенесены в документ. Перенести?';
							|en = 'Picked items are not added to the document. Do you want to add them?'");
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Если РазрешитьПереносРезультатовВДокумент() Тогда
			ПеренестиВДокумент  = Истина;
			ВыполняетсяЗакрытие = Истина;
			
			Закрыть();
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	ВыполняетсяЗакрытие = Истина;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьНастройкиФормыНаСервере();
	
	Если ПеренестиВДокумент Тогда
		Структура = Новый Структура("АдресТоваровВХранилище", АдресТоваровВХранилище());
		ОповеститьОВыборе(Структура);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РежимОстатковПриИзменении(Элемент)
	
	РежимОстатковПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПометкаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные.Пометка Тогда
		ТекущиеДанные.КоличествоПодобрано = ТекущиеДанные.КоличествоОстаток;
	Иначе
		ТекущиеДанные.КоличествоПодобрано = ТекущиеДанные.КоличествоВНакладной;
	КонецЕсли;
	
	РассчитатьКоличествоВСтроке(Объект.Товары, ТекущиеДанные, ТоварыПодобраны);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПодобраноПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ТекущиеДанные.Пометка = ТекущиеДанные.КоличествоПодобрано > 0;
	
	РассчитатьКоличествоВСтроке(Объект.Товары, ТекущиеДанные, ТоварыПодобраны);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ОчиститьСообщения();
	
	Если РазрешитьПереносРезультатовВДокумент() Тогда
		ПеренестиВДокумент = Истина;
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьВсе(Команда)
	
	ОтметитьВсеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометку(Команда)
	
	СнятьПометкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Установка условного оформления для элемента 'КоличествоПодобрано' табличной части 'Товары'.
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоПодобрано.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.Пометка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// Полностью подобранные строки выделяем серым цветом
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Товары.Имя);
	
	ГруппаОтбора = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
	
	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоВНакладной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоОстаток");

	ОтборЭлемента = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоОсталосьПодобрать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Gray);
	
	// Измененн строки выделяем серым цветом
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКоличествоПодобрано.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоВНакладной");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("Объект.Товары.КоличествоПодобрано");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ГиперссылкаЦвет);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораРежимОстатков()
	
	РежимыОстатков = Новый СписокЗначений;
	
	Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтгрузкаПринятыхСПравомПродажиТоваровСХранения Тогда
		
		РежимыОстатков.Добавить("ВсеПринятые", НСтр("ru = 'Все принятые';
													|en = 'All received'"));
		
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеПринятыхТоваровЗаСчетПоклажедателя
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеПринятыхТоваровНаРасходы Тогда
		
		РежимыОстатков.Добавить("КСписанию", НСтр("ru = 'К списанию';
													|en = 'To write off'"));
	
	//++ НЕ УТКА
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровДавальцаЗаСчетДавальца
		Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.СписаниеТоваровДавальцаНаРасходы Тогда
	
		РежимыОстатков.Добавить("КСписанию", НСтр("ru = 'К списанию';
													|en = 'To write off'"));
	
	ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаДавальцу2_5
		  Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДавальцу2_5
		  Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетДавальцу2_5
		  Или ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровДавальца Тогда
		
		РежимыОстатков.Добавить("ВсеПринятые", НСтр("ru = 'Все принятые';
													|en = 'All received'"));
	//-- НЕ УТКА
		
	Иначе
		
		РежимыОстатков.Добавить("ВсеПринятые", НСтр("ru = 'Все принятые';
													|en = 'All received'"));
		РежимыОстатков.Добавить("Выбывшие", НСтр("ru = 'Выбывшие';
												|en = 'Retired'"));
		
	КонецЕсли;
	
	Элементы.РежимОстатков.СписокВыбора.Очистить();
	
	Для Каждого Режим Из РежимыОстатков Цикл
		Элементы.РежимОстатков.СписокВыбора.Добавить(Режим.Значение, Режим.Представление);
	КонецЦикла;
	
	Если РежимыОстатков.Количество() = 1 Тогда
		Элементы.РежимОстатков.Видимость = Ложь;
		Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
	Иначе
		ЗагрузитьНастройкиФормыНаСервере(РежимыОстатков);
	КонецЕсли;
	
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормыНаСервере(РежимыОстатков)
	
	КлючОбъекта             = "Обработка_ПодборТоваровПринятыхНаОтветственноеХранилище";
	НастройкаРежимаОстатков = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючОбъекта, "");
	
	Если НастройкаРежимаОстатков = Неопределено Тогда
		Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
	Иначе
		
		ЗначениеРежима = РежимыОстатков.НайтиПоЗначению(НастройкаРежимаОстатков);
		
		Если ЗначениеРежима <> Неопределено Тогда
			Объект.РежимОстатков = ЗначениеРежима.Значение;
		Иначе
			Объект.РежимОстатков = РежимыОстатков.Получить(0).Значение;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	ВидимостьКолонкиМестоХранения = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов")
									Или ПолучитьФункциональнуюОпцию("ИспользоватьПроизводство")
									//++ НЕ УТКА
									Или ПолучитьФункциональнуюОпцию("ИспользоватьПроизводствоИзДавальческогоСырья2_5")
									//-- НЕ УТКА
									Или ПолучитьФункциональнуюОпцию("ИспользоватьПередачуНаОтветственноеХранениеСПравомПродажи");
	
	ЗаголовокКолонкиМестоХранения = НСтр("ru = 'Место хранения';
										|en = 'Storage location'");
	ЗаголовокКолонкиОстаток       = НСтр("ru = 'Остаток';
										|en = 'Remaining stock'");
	
	Если Объект.РежимОстатков = "ВсеПринятые" Тогда
		
		ЗаголовокКолонкиОстаток       = НСтр("ru = 'Всего принято';
											|en = 'Total received'");
		
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупПринятыхНаХранениеТоваров Тогда
			ВидимостьКолонкиМестоХранения = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
		//++ НЕ УТКА
		ИначеЕсли ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыкупТоваровДавальца Тогда
			ВидимостьКолонкиМестоХранения = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
		//-- НЕ УТКА
		Иначе
			ВидимостьКолонкиМестоХранения = Склады.Количество() > 1;
			ЗаголовокКолонкиМестоХранения = НСтр("ru = 'Склад';
												|en = 'Warehouse'");
		КонецЕсли;
		
		//++ НЕ УТКА
		Если ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ПередачаДавальцу2_5")
			Или ХозяйственнаяОперация = ПредопределенноеЗначение("Перечисление.ХозяйственныеОперации.ОтчетДавальцу2_5") Тогда
			ЗаголовокКолонкиОстаток = НСтр("ru = 'Всего произведено';
											|en = 'Total produced'");
		КонецЕсли;
		//-- НЕ УТКА
		
	ИначеЕсли Объект.РежимОстатков = "Выбывшие" Тогда
		ЗаголовокКолонкиОстаток = НСтр("ru = 'Выбыло';
										|en = 'Retired'");
	ИначеЕсли Объект.РежимОстатков = "КСписанию" Тогда
		ЗаголовокКолонкиОстаток = НСтр("ru = 'К списанию';
										|en = 'To write off'");
	КонецЕсли;
	
	Элементы.ТоварыГруппаМестоХранения.Видимость = ВидимостьКолонкиМестоХранения;
	
	Элементы.ТоварыМестоХранения.Заголовок     = ЗаголовокКолонкиМестоХранения;
	Элементы.ТоварыКоличествоОстаток.Заголовок = ЗаголовокКолонкиОстаток;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСкладыНастроитьВидимость()
	
	Если ТипЗнч(Объект.Склад) = Тип("СправочникСсылка.Склады")
		И Справочники.Склады.ЭтоГруппа(Объект.Склад) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Склады.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Ссылка В ИЕРАРХИИ(&Склад)
		|	И НЕ Склады.ЭтоГруппа
		|	И НЕ Склады.ПометкаУдаления";
		
		Запрос.УстановитьПараметр("Склад", Объект.Склад);
		
		УстановитьПривилегированныйРежим(Истина);
		Выборка = Запрос.Выполнить().Выбрать();
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока Выборка.Следующий() Цикл
			Склады.Добавить(Выборка.Склад);
		КонецЦикла;
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Склад) Тогда
		Склады.Добавить(Объект.Склад);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуТовары(КешироватьДанные = Истина)
	
	ОбновитьПодобранныеТовары(КешироватьДанные);
	
	Если Объект.РежимОстатков = "ВсеПринятые" Тогда
		
		// Вариант подбора товаров в документы "Выкуп товаров принятых на хранение", "Отгрузка товаров с хранения".
		Если Не ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыОрганизаций) Тогда
			
			Объект.Товары.Очистить();
			Возврат;
			
		Иначе
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТоварыНакладной.НомерСтроки КАК НомерСтроки,
			|	ТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ТоварыНакладной.Характеристика КАК Характеристика,
			|	ТоварыНакладной.Серия КАК Серия,
			|	ТоварыНакладной.СтатусУказанияСерий КАК СтатусУказанияСерий,
			|	ТоварыНакладной.Назначение КАК Назначение,
			|	ТоварыНакладной.МестоХранения КАК МестоХранения,
			|	ТоварыНакладной.Количество КАК Количество
			|ПОМЕСТИТЬ ВтТоварыНакладной
			|ИЗ
			|	&Товары КАК ТоварыНакладной
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ВтТоварыНакладной.НомерСтроки) КАК НомерСтроки,
			|	ВтТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ВтТоварыНакладной.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА ВтТоварыНакладной.СтатусУказанияСерий <> 0
			|			И НЕ ВтПолитикиУчетаСерий.УчитыватьСебестоимостьПоСериям
			|			И ВтПолитикиУчетаСерий.УказыватьПриПланированииОтгрузки
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|		ИНАЧЕ ВтТоварыНакладной.Серия
			|	КОНЕЦ КАК Серия,
			|	ВЫБОР
			|		КОГДА ВтТоварыНакладной.СтатусУказанияСерий <> 0
			|			И НЕ ВтПолитикиУчетаСерий.УчитыватьСебестоимостьПоСериям
			|			И ВтПолитикиУчетаСерий.УказыватьПриПланированииОтгрузки
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК РазныйУчетСерий,
			|	ВтТоварыНакладной.Назначение КАК Назначение,
			|	ВтТоварыНакладной.МестоХранения КАК МестоХранения,
			|	СУММА(ВтТоварыНакладной.Количество) КАК Количество
			|ПОМЕСТИТЬ ТоварыНакладной
			|ИЗ
			|	ВтТоварыНакладной КАК ВтТоварыНакладной
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК ВтНоменклатура
			|		ПО ВтТоварыНакладной.Номенклатура = ВтНоменклатура.Ссылка
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
			|		ПО ВтНоменклатура.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
			|			И ВтТоварыНакладной.МестоХранения = ПолитикиУчетаСерий.Склад
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПолитикиУчетаСерий КАК ВтПолитикиУчетаСерий
			|		ПО ПолитикиУчетаСерий.ПолитикаУчетаСерий = ВтПолитикиУчетаСерий.Ссылка
			|		
			|СГРУППИРОВАТЬ ПО
			|	ВтТоварыНакладной.Номенклатура,
			|	ВтТоварыНакладной.Характеристика,
			|	ВЫБОР
			|		КОГДА ВтТоварыНакладной.СтатусУказанияСерий <> 0
			|			И НЕ ВтПолитикиУчетаСерий.УчитыватьСебестоимостьПоСериям
			|			И ВтПолитикиУчетаСерий.УказыватьПриПланированииОтгрузки
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
			|		ИНАЧЕ ВтТоварыНакладной.Серия
			|	КОНЕЦ,
			|	ВЫБОР
			|		КОГДА ВтТоварыНакладной.СтатусУказанияСерий <> 0
			|			И НЕ ВтПолитикиУчетаСерий.УчитыватьСебестоимостьПоСериям
			|			И ВтПолитикиУчетаСерий.УказыватьПриПланированииОтгрузки
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ,
			|	ВтТоварыНакладной.Назначение,
			|	ВтТоварыНакладной.МестоХранения
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.Ссылка КАК ВидЗапасов
			|ПОМЕСТИТЬ ТаблицаЗапасов
			|ИЗ
			|	Справочник.ВидыЗапасов КАК ВидыЗапасов
			|ГДЕ
			|	ВидыЗапасов.ВладелецТовара = &Партнер
			|	И ВидыЗапасов.Организация  = &Организация
			|	И ВидыЗапасов.Договор      = &Договор
			|	И (&ПоВсемТипам
			|		ИЛИ ВидыЗапасов.ТипЗапасов В (&ТипыЗапасов))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТоварыПринятые.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыПринятые.Организация                КАК Организация,
			|	СУММА(ТоварыПринятые.КоличествоОстаток)   КАК КоличествоОстаток
			|ПОМЕСТИТЬ ТаблицаПринятыхТоваров
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТоварыПринятыеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|		ТоварыПринятыеОстатки.Организация                КАК Организация,
			|		ТоварыПринятыеОстатки.КоличествоОстаток          КАК КоличествоОстаток
			|	ИЗ
			|		РегистрНакопления.ТоварыОрганизаций.Остатки(,
			|			Организация = &Организация
			|				И ВидЗапасов В
			|					(ВЫБРАТЬ
			|						ТаблицаЗапасов.ВидЗапасов
			|					ИЗ
			|						ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыПринятыеОстатки
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		ТоварыПринятыеРегистратор.АналитикаУчетаНоменклатуры,
			|		ТоварыПринятыеРегистратор.Организация,
			|		ТоварыПринятыеРегистратор.Количество
			|	ИЗ
			|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыПринятыеРегистратор
			|	ГДЕ
			|		ТоварыПринятыеРегистратор.Регистратор = &Регистратор
			|		И ТоварыПринятыеРегистратор.Активность
			|		И ТоварыПринятыеРегистратор.Организация = &Организация
			|		И ТоварыПринятыеРегистратор.ВидЗапасов В
			|			(ВЫБРАТЬ
			|				ТаблицаЗапасов.ВидЗапасов
			|			ИЗ
			|				ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыПринятые
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыПринятые.АналитикаУчетаНоменклатуры,
			|	ТоварыПринятые.Организация
			|
			|ИМЕЮЩИЕ
			|	СУММА(ТоварыПринятые.КоличествоОстаток) > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.МестоХранения, НЕОПРЕДЕЛЕНО) КАК МестоХранения,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
			|	КОНЕЦ                                     КАК Назначение,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
			|	СУММА(ТоварыПринятые.КоличествоОстаток)   КАК КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыПринятыеГруппировка
			|ИЗ
			|	ТаблицаПринятыхТоваров КАК ТоварыПринятые
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиУчетаНоменклатуры
			|		ПО ТоварыПринятые.АналитикаУчетаНоменклатуры = АналитикиУчетаНоменклатуры.Ссылка
			|
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
			|		ПО АналитикиУчетаНоменклатуры.Номенклатура = Товары.Ссылка
			|ГДЕ
			|	АналитикиУчетаНоменклатуры.ТипМестаХранения <> ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
			|	И (АналитикиУчетаНоменклатуры.МестоХранения В(&Склады)
			|		ИЛИ &ПоВсемСкладам)
			|	И (&ПоВсемЗаказам ИЛИ АналитикиУчетаНоменклатуры.Назначение.Заказ В (&МассивЗаказов))
			|
			|СГРУППИРОВАТЬ ПО
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.МестоХранения, НЕОПРЕДЕЛЕНО),
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
			|	КОНЕЦ,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыПринятые.Номенклатура КАК Номенклатура,
			|	ТоварыПринятые.Характеристика КАК Характеристика,
			|	ТоварыПринятые.Назначение КАК Назначение,
			|	ТоварыПринятые.Серия КАК Серия,
			|	ЕСТЬNULL(ТоварыНакладной.РазныйУчетСерий, ЛОЖЬ) КАК РазныйУчетСерий,
			|	ТоварыПринятые.МестоХранения КАК МестоХранения,
			|	ТоварыПринятые.КоличествоОстаток КАК КоличествоОстаток,
			|	ТоварыПринятые.КоличествоОстаток - ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоОсталосьПодобрать,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоПодобрано,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоВНакладной
			|
			|ИЗ
			|	ТоварыПринятыеГруппировка КАК ТоварыПринятые
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНакладной КАК ТоварыНакладной
			|		ПО ТоварыПринятые.Номенклатура = ТоварыНакладной.Номенклатура
			|		И ТоварыПринятые.Характеристика = ТоварыНакладной.Характеристика
			|		И ТоварыПринятые.Назначение = ТоварыНакладной.Назначение
			|		И ТоварыПринятые.Серия = ТоварыНакладной.Серия
			|		И ТоварыПринятые.МестоХранения = ТоварыНакладной.МестоХранения
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЕСТЬNULL(ТоварыНакладной.НомерСтроки, 99999),
			|	МестоХранения,
			|	Номенклатура,
			|	Характеристика,
			|	Назначение,
			|	Серия
			|";
			
		КонецЕсли;
		
	ИначеЕсли Объект.РежимОстатков = "Выбывшие" Тогда
		
		// Вариант подбора товаров в документ "Выкуп товаров принятых на хранение".
		Если Не ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.РезервыТоваровОрганизаций) Тогда
			
			Объект.Товары.Очистить();
			Возврат;
			
		Иначе
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТоварыНакладной.НомерСтроки КАК НомерСтроки,
			|	ТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ТоварыНакладной.Характеристика КАК Характеристика,
			|	ТоварыНакладной.Серия КАК Серия,
			|	ТоварыНакладной.СтатусУказанияСерий КАК СтатусУказанияСерий,
			|	ТоварыНакладной.Назначение КАК Назначение,
			|	ТоварыНакладной.МестоХранения КАК МестоХранения,
			|	ТоварыНакладной.Хранитель КАК Хранитель,
			|	ТоварыНакладной.Количество КАК Количество
			|ПОМЕСТИТЬ ВтТоварыНакладной
			|ИЗ
			|	&Товары КАК ТоварыНакладной
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ВтТоварыНакладной.НомерСтроки) КАК НомерСтроки,
			|	ВтТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ВтТоварыНакладной.Характеристика КАК Характеристика,
			|	ВтТоварыНакладной.Серия КАК Серия,
			|	ЛОЖЬ КАК РазныйУчетСерий,
			|	ВтТоварыНакладной.Назначение КАК Назначение,
			|	ВтТоварыНакладной.МестоХранения КАК МестоХранения,
			|	ВтТоварыНакладной.Хранитель КАК Хранитель,
			|	СУММА(ВтТоварыНакладной.Количество) КАК Количество
			|ПОМЕСТИТЬ ТоварыНакладной
			|ИЗ
			|	ВтТоварыНакладной КАК ВтТоварыНакладной
			|СГРУППИРОВАТЬ ПО
			|	ВтТоварыНакладной.Номенклатура,
			|	ВтТоварыНакладной.Характеристика,
			|	ВтТоварыНакладной.Серия,
			|	ВтТоварыНакладной.Назначение,
			|	ВтТоварыНакладной.МестоХранения,
			|	ВтТоварыНакладной.Хранитель
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.Ссылка КАК ВидЗапасов
			|
			|ПОМЕСТИТЬ ТаблицаЗапасов
			|ИЗ
			|	Справочник.ВидыЗапасов КАК ВидыЗапасов
			|
			|ГДЕ
			|	ВидыЗапасов.ВладелецТовара = &Партнер
			|	И ВидыЗапасов.Организация  = &Организация
			|	И ВидыЗапасов.Договор      = &Договор
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТоварыВыбывшие.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	ТоварыВыбывшие.Организация                КАК Организация,
			|	СУММА(ТоварыВыбывшие.КоличествоОстаток)  КАК КоличествоОстаток
			|ПОМЕСТИТЬ ТаблицаВыбывшихТоваров
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТоварыВыбывшиеОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|		ТоварыВыбывшиеОстатки.Организация                КАК Организация,
			|		-ТоварыВыбывшиеОстатки.КоличествоОстаток         КАК КоличествоОстаток
			|	ИЗ
			|		РегистрНакопления.РезервыТоваровОрганизаций.Остатки(,
			|			Организация = &Организация
			|				И ВидЗапасов В
			|					(ВЫБРАТЬ
			|						ТаблицаЗапасов.ВидЗапасов
			|					ИЗ
			|						ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыВыбывшиеОстатки
			|
			|	ОБЪЕДИНИТЬ ВСЕ
			|
			|	ВЫБРАТЬ
			|		ТоварыВыбывшиеРегистратор.АналитикаУчетаНоменклатуры,
			|		ТоварыВыбывшиеРегистратор.Организация,
			|		-ТоварыВыбывшиеРегистратор.Количество
			|	ИЗ
			|		РегистрНакопления.РезервыТоваровОрганизаций КАК ТоварыВыбывшиеРегистратор
			|	ГДЕ
			|		ТоварыВыбывшиеРегистратор.Регистратор = &Регистратор
			|		И ТоварыВыбывшиеРегистратор.Активность
			|		И ТоварыВыбывшиеРегистратор.Организация = &Организация
			|		И ТоварыВыбывшиеРегистратор.ВидЗапасов В
			|			(ВЫБРАТЬ
			|				ТаблицаЗапасов.ВидЗапасов
			|			ИЗ
			|				ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыВыбывшие
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыВыбывшие.АналитикаУчетаНоменклатуры,
			|	ТоварыВыбывшие.Организация
			|
			|ИМЕЮЩИЕ
			|	СУММА(ТоварыВыбывшие.КоличествоОстаток) > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.МестоХранения, НЕОПРЕДЕЛЕНО) КАК МестоХранения,
			|	ВЫБОР
			|		КОГДА АналитикиУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
			|			ТОГДА ЕСТЬNULL(АналитикиУчетаНоменклатуры.Партнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
			|	КОНЕЦ                                     КАК Хранитель,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)) КАК Характеристика,
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
			|	КОНЕЦ                                     КАК Назначение,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)) КАК Серия,
			|	СУММА(ТоварыВыбывшие.КоличествоОстаток)   КАК КоличествоОстаток
			|ПОМЕСТИТЬ ТоварыВыбывшиеГруппировка
			|ИЗ
			|	ТаблицаВыбывшихТоваров КАК ТоварыВыбывшие
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиУчетаНоменклатуры
			|		ПО ТоварыВыбывшие.АналитикаУчетаНоменклатуры = АналитикиУчетаНоменклатуры.Ссылка
			|		
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Товары
			|		ПО АналитикиУчетаНоменклатуры.Номенклатура = Товары.Ссылка
			|
			|СГРУППИРОВАТЬ ПО
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.МестоХранения, НЕОПРЕДЕЛЕНО),
			|	ВЫБОР
			|		КОГДА АналитикиУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
			|			ТОГДА ЕСТЬNULL(АналитикиУчетаНоменклатуры.Партнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
			|	КОНЕЦ,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)),
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Характеристика, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)),
			|	ВЫБОР
			|		КОГДА Товары.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ ЕСТЬNULL(АналитикиУчетаНоменклатуры.Назначение, ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка))
			|	КОНЕЦ,
			|	ЕСТЬNULL(АналитикиУчетаНоменклатуры.Серия, ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка))
			|;
			|
			|ВЫБРАТЬ
			|	ТоварыВыбывшие.МестоХранения КАК МестоХранения,
			|	ТоварыВыбывшие.Хранитель КАК Хранитель,
			|	ТоварыВыбывшие.Номенклатура КАК Номенклатура,
			|	ТоварыВыбывшие.Характеристика КАК Характеристика,
			|	ТоварыВыбывшие.Назначение КАК Назначение,
			|	ТоварыВыбывшие.Серия КАК Серия,
			|	ЕСТЬNULL(ТоварыНакладной.РазныйУчетСерий, ЛОЖЬ) КАК РазныйУчетСерий,
			|	ТоварыВыбывшие.КоличествоОстаток КАК КоличествоОстаток,
			|	ТоварыВыбывшие.КоличествоОстаток - ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоОсталосьПодобрать,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоПодобрано,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоВНакладной
			|
			|ИЗ
			|	ТоварыВыбывшиеГруппировка КАК ТоварыВыбывшие
			|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНакладной КАК ТоварыНакладной
			|		ПО ТоварыВыбывшие.Номенклатура = ТоварыНакладной.Номенклатура
			|		И ТоварыВыбывшие.Характеристика = ТоварыНакладной.Характеристика
			|		И ТоварыВыбывшие.Назначение = ТоварыНакладной.Назначение
			|		И ТоварыВыбывшие.Серия = ТоварыНакладной.Серия
			|		И ТоварыВыбывшие.МестоХранения = ТоварыНакладной.МестоХранения
			|		И ТоварыВыбывшие.Хранитель = ТоварыНакладной.Хранитель
			|	
			|УПОРЯДОЧИТЬ ПО
			|	ЕСТЬNULL(ТоварыНакладной.НомерСтроки, 99999),
			|	МестоХранения,
			|	Номенклатура,
			|	Характеристика,
			|	Назначение,
			|	Серия
			|";
			
		КонецЕсли;
		
	ИначеЕсли Объект.РежимОстатков = "КСписанию" Тогда
		
		// Вариант подбора товаров в документ "ОтчетОСписанииТоваровСХранения".
		Если Не ПравоДоступа("Чтение", Метаданные.РегистрыНакопления.ТоварыОрганизаций) Тогда
			
			Объект.Товары.Очистить();
			Возврат;
			
		Иначе
			
			ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТоварыНакладной.НомерСтроки КАК НомерСтроки,
			|	ТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ТоварыНакладной.Характеристика КАК Характеристика,
			|	ТоварыНакладной.Серия КАК Серия,
			|	ТоварыНакладной.СтатусУказанияСерий КАК СтатусУказанияСерий,
			|	ТоварыНакладной.Назначение КАК Назначение,
			|	ТоварыНакладной.МестоХранения КАК МестоХранения,
			|	ТоварыНакладной.Хранитель КАК Хранитель,
			|	ТоварыНакладной.Количество КАК Количество
			|ПОМЕСТИТЬ ВтТоварыНакладной
			|ИЗ
			|	&Товары КАК ТоварыНакладной
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	МИНИМУМ(ВтТоварыНакладной.НомерСтроки) КАК НомерСтроки,
			|	ВтТоварыНакладной.Номенклатура КАК Номенклатура,
			|	ВтТоварыНакладной.Характеристика КАК Характеристика,
			|	ВтТоварыНакладной.Серия КАК Серия,
			|	ЛОЖЬ КАК РазныйУчетСерий,
			|	ВтТоварыНакладной.Назначение КАК Назначение,
			|	ВтТоварыНакладной.МестоХранения КАК МестоХранения,
			|	ВтТоварыНакладной.Хранитель КАК Хранитель,
			|	СУММА(ВтТоварыНакладной.Количество) КАК Количество
			|ПОМЕСТИТЬ ТоварыНакладной
			|ИЗ
			|	ВтТоварыНакладной КАК ВтТоварыНакладной
			|СГРУППИРОВАТЬ ПО
			|	ВтТоварыНакладной.Номенклатура,
			|	ВтТоварыНакладной.Характеристика,
			|	ВтТоварыНакладной.Серия,
			|	ВтТоварыНакладной.Назначение,
			|	ВтТоварыНакладной.Хранитель,
			|	ВтТоварыНакладной.МестоХранения
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВидыЗапасов.Ссылка КАК ВидЗапасов
			|ПОМЕСТИТЬ ТаблицаЗапасов
			|ИЗ
			|	Справочник.ВидыЗапасов КАК ВидыЗапасов
			|
			|ГДЕ
			|	ВидыЗапасов.ВладелецТовара = &Партнер
			|	И ВидыЗапасов.Организация  = &Организация
			|	И ВидыЗапасов.Договор      = &Договор
			|	И (&ПоВсемТипам
			|		ИЛИ ВидыЗапасов.ТипЗапасов В (&ТипыЗапасов))
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	ТоварыКСписанию.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|	СУММА(ТоварыКСписанию.КОформлениюСписанияОстаток) КАК КОформлениюСписанияОстаток
			|ПОМЕСТИТЬ ТоварыКСписанию
			|ИЗ
			|	(ВЫБРАТЬ
			|		ТоварыКСписаниюОстатки.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры,
			|		ТоварыКСписаниюОстатки.КОформлениюСписанияОстаток КАК КОформлениюСписанияОстаток
			|	ИЗ
			|		РегистрНакопления.ТоварыОрганизаций.Остатки(,
			|			Организация = &Организация
			|				И ВидЗапасов В
			|					(ВЫБРАТЬ
			|						ТаблицаЗапасов.ВидЗапасов
			|					ИЗ
			|						ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыКСписаниюОстатки
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		ТоварыКСписаниюРегистратор.АналитикаУчетаНоменклатуры,
			|		ТоварыКСписаниюРегистратор.КОформлениюСписания
			|	ИЗ
			|		РегистрНакопления.ТоварыОрганизаций КАК ТоварыКСписаниюРегистратор
			|	ГДЕ
			|		ТоварыКСписаниюРегистратор.Регистратор = &Регистратор
			|		И ТоварыКСписаниюРегистратор.Активность
			|		И ТоварыКСписаниюРегистратор.Организация = &Организация
			|		И ТоварыКСписаниюРегистратор.ВидЗапасов В
			|			(ВЫБРАТЬ
			|				ТаблицаЗапасов.ВидЗапасов
			|			ИЗ
			|				ТаблицаЗапасов КАК ТаблицаЗапасов)) КАК ТоварыКСписанию
			|
			|СГРУППИРОВАТЬ ПО
			|	ТоварыКСписанию.АналитикаУчетаНоменклатуры
			|
			|ИМЕЮЩИЕ
			|	СУММА(ТоварыКСписанию.КОформлениюСписанияОстаток) > 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	АналитикаУчетаНоменклатуры.МестоХранения  КАК МестоХранения,
			|	ВЫБОР
			|		КОГДА АналитикиУчетаНоменклатуры.ТипМестаХранения = ЗНАЧЕНИЕ(Перечисление.ТипыМестХранения.ДоговорКонтрагента)
			|			ТОГДА АналитикиУчетаНоменклатуры.Партнер
			|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
			|	КОНЕЦ                                     КАК Хранитель,
			|	АналитикаУчетаНоменклатуры.Номенклатура   КАК Номенклатура,
			|	АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
			|	ВЫБОР
			|		КОГДА АналитикаУчетаНоменклатуры.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
			|			ТОГДА ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
			|		ИНАЧЕ АналитикаУчетаНоменклатуры.Назначение
			|	КОНЕЦ                                     КАК Назначение,
			|	АналитикаУчетаНоменклатуры.Серия          КАК Серия,
			|	КОформлениюСписанияОстаток                КАК КОформлениюСписанияОстаток
			|ПОМЕСТИТЬ ВтТоварыКСписанию
			|ИЗ
			|	ТоварыКСписанию КАК ТоварыКСписанию
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК АналитикиУчетаНоменклатуры
			|		ПО ТоварыКСписанию.АналитикаУчетаНоменклатуры = АналитикиУчетаНоменклатуры.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТоварыКСписанию.МестоХранения КАК МестоХранения,
			|	ТоварыКСписанию.Хранитель КАК Хранитель,
			|	ТоварыКСписанию.Номенклатура КАК Номенклатура,
			|	ТоварыКСписанию.Характеристика КАК Характеристика,
			|	ТоварыКСписанию.Назначение КАК Назначение,
			|	ТоварыКСписанию.Серия КАК Серия,
			|	ЕСТЬNULL(ТоварыНакладной.РазныйУчетСерий, ЛОЖЬ) КАК РазныйУчетСерий,
			|	ТоварыКСписанию.КОформлениюСписанияОстаток КАК КоличествоОстаток,
			|	ТоварыКСписанию.КОформлениюСписанияОстаток - ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоОсталосьПодобрать,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоПодобрано,
			|	ЕСТЬNULL(ТоварыНакладной.Количество, 0) КАК КоличествоВНакладной
			|
			|ИЗ ВтТоварыКСписанию КАК ТоварыКСписанию
			|	ЛЕВОЕ СОЕДИНЕНИЕ ТоварыНакладной КАК ТоварыНакладной
			|		ПО ТоварыКСписанию.Номенклатура = ТоварыНакладной.Номенклатура
			|		И ТоварыКСписанию.Характеристика = ТоварыНакладной.Характеристика
			|		И ТоварыКСписанию.Назначение = ТоварыНакладной.Назначение
			|		И ТоварыКСписанию.Серия = ТоварыНакладной.Серия
			|		И ТоварыКСписанию.МестоХранения = ТоварыНакладной.МестоХранения
			|		И ТоварыКСписанию.Хранитель = ТоварыНакладной.Хранитель
			|
			|УПОРЯДОЧИТЬ ПО
			|	ЕСТЬNULL(ТоварыНакладной.НомерСтроки, 99999),
			|	ТоварыКСписанию.МестоХранения,
			|	ТоварыКСписанию.Номенклатура,
			|	ТоварыКСписанию.Характеристика,
			|	ТоварыКСписанию.Назначение,
			|	ТоварыКСписанию.Серия
			|";
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	Если Не ЗначениеЗаполнено(Объект.ТипЗапасов) Тогда
		ТипыЗапасов = Неопределено;
	ИначеЕсли ТипЗнч(Объект.ТипЗапасов) = Тип("СписокЗначений") Тогда
		ТипыЗапасов = Объект.ТипЗапасов;
	Иначе
		ТипыЗапасов = Новый СписокЗначений;
		ТипыЗапасов.Добавить(Объект.ТипЗапасов);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Заказ) Тогда
		МассивЗаказов = Неопределено;
	Иначе
		МассивЗаказов = Новый Массив;
		МассивЗаказов.Добавить(Заказ);
		//++ НЕ УТКА
		Если ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОтчетДавальцу2_5 Тогда
			МассивЗаказов.Добавить(Неопределено);
		КонецЕсли;
		//-- НЕ УТКА
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистратор",   Ссылка);
	Запрос.УстановитьПараметр("Партнер",       Объект.Владелец);
	Запрос.УстановитьПараметр("Организация",   Объект.Организация);
	Запрос.УстановитьПараметр("Склады",        Склады);
	Запрос.УстановитьПараметр("ПоВсемСкладам", Склады.Количество() = 0);
	Запрос.УстановитьПараметр("Договор",       Объект.Договор);
	Запрос.УстановитьПараметр("ТипыЗапасов",   ТипыЗапасов);
	Запрос.УстановитьПараметр("ПоВсемТипам",   ТипыЗапасов = Неопределено);
	Запрос.УстановитьПараметр("МассивЗаказов", МассивЗаказов);
	Запрос.УстановитьПараметр("ПоВсемЗаказам",  МассивЗаказов = Неопределено);
	
	Если ЗначениеЗаполнено(АдресТоваровНакладнойВХранилище) Тогда
		Товары = ПолучитьИзВременногоХранилища(АдресТоваровНакладнойВХранилище); // ТаблицаЗначений
		
		Если (Объект.РежимОстатков = "КСписанию" Или Объект.РежимОстатков = "Выбывшие")
			И Товары.Колонки.Найти("Хранитель") = Неопределено Тогда
			Товары.Колонки.Добавить("Хранитель", Новый ОписаниеТипов("СправочникСсылка.Партнеры"));
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Товары", Товары);
		
	КонецЕсли;
	
	Объект.Товары.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ЗаполнитьТоварыПодобраннымиТоварами();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьПодобранныеТовары(КешироватьДанные)
	
	ХозОперации = Новый Массив;
	ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ВыкупПринятыхНаХранениеТоваров);
	//++ НЕ УТКА
	ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ВыкупТоваровДавальца);
	//-- НЕ УТКА
	
	Если ХозОперации.Найти(ХозяйственнаяОперация) = Неопределено
		Или Не КешироватьДанные Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Отбор = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, МестоХранения");
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		
		Если СтрокаТоваров.Пометка
			Или СтрокаТоваров.КоличествоПодобрано > 0 Тогда
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТоваров);
			
			СтрокиПодобранныхТоваров = ПодобранныеТовары.НайтиСтроки(Отбор);
			
			Если СтрокиПодобранныхТоваров.Количество() = 0 Тогда
				НоваяСтрока = ПодобранныеТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТоваров);
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокиПодобранныхТоваров[0], СтрокаТоваров);
			КонецЕсли;
			
		ИначеЕсли Не СтрокаТоваров.Пометка
			И СтрокаТоваров.КоличествоПодобрано = 0 Тогда
			
			ЗаполнитьЗначенияСвойств(Отбор, СтрокаТоваров);
			
			СтрокиПодобранныхТоваров = ПодобранныеТовары.НайтиСтроки(Отбор);
			
			Если СтрокиПодобранныхТоваров.Количество() > 0 Тогда
				ПодобранныеТовары.Удалить(СтрокиПодобранныхТоваров[0]);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТоварыПодобраннымиТоварами()
	
	ХозОперации = Новый Массив;
	ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ВыкупПринятыхНаХранениеТоваров);
	//++ НЕ УТКА
	ХозОперации.Добавить(Перечисления.ХозяйственныеОперации.ВыкупТоваровДавальца);
	//-- НЕ УТКА
	
	Если ХозОперации.Найти(ХозяйственнаяОперация) = Неопределено
		Или Объект.Товары.Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Отбор = Новый Структура("Номенклатура, Характеристика, Назначение, Серия, МестоХранения");
	
	Для Каждого СтрокаПодобранныхТоваров Из ПодобранныеТовары Цикл
		
		ЗаполнитьЗначенияСвойств(Отбор, СтрокаПодобранныхТоваров);
		
		СтрокиТоваров = Объект.Товары.НайтиСтроки(Отбор);
		
		Если СтрокиТоваров.Количество() > 0 Тогда
			ИзменяемаяСтрока = СтрокиТоваров[0];
			ЗаполнитьЗначенияСвойств(ИзменяемаяСтрока, СтрокаПодобранныхТоваров);
			
			Если ИзменяемаяСтрока.Пометка Тогда
				
				ИзменяемаяСтрока.КоличествоОсталосьПодобрать = ИзменяемаяСтрока.КоличествоОстаток
																- ИзменяемаяСтрока.КоличествоПодобрано;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиФормыНаСервере()
	
	КлючОбъекта = "Обработка_ПодборТоваровПринятыхНаОтветственноеХранилище";
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(КлючОбъекта, "", Объект.РежимОстатков);
	
КонецПроцедуры

&НаСервере
Функция АдресТоваровВХранилище()
	
	Отбор = Новый Структура("Пометка", Истина);
	ОтобранныеТовары      = Объект.Товары.Выгрузить(Отбор);
	
	Возврат ПоместитьВоВременноеХранилище(ОтобранныеТовары, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Процедура РежимОстатковПриИзмененииНаСервере()
	
	ЗаполнитьТаблицуТовары();
	НастроитьФорму();
	
КонецПроцедуры

&НаСервере
Функция РазрешитьПереносРезультатовВДокумент()
	
	Возврат ПроверитьЗаполнение();
	
КонецФункции

&НаСервере
Процедура ОтметитьВсеНаСервере()
	
	Модифицированность = Истина;
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.КоличествоПодобрано = СтрокаТоваров.КоличествоОстаток;
		СтрокаТоваров.Пометка = Истина;
		
		РассчитатьКоличествоВСтроке(Объект.Товары, СтрокаТоваров, ТоварыПодобраны);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СнятьПометкуНаСервере()
	
	Для Каждого СтрокаТоваров Из Объект.Товары Цикл
		СтрокаТоваров.Пометка = Ложь;
		СтрокаТоваров.КоличествоПодобрано = СтрокаТоваров.КоличествоВНакладной;
		
		РассчитатьКоличествоВСтроке(Объект.Товары, СтрокаТоваров, ТоварыПодобраны);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	ПодобранныеТовары.Очистить();
	ЗаполнитьТаблицуТовары(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИнформационнуюНадписьОтборы()
	
	Если Не ОбщегоНазначенияУТКлиентСервер.АвторизованВнешнийПользователь() Тогда
		ИнформационнаяНадписьОтборы = НСтр("ru = 'Отбор по: %Организация%, %Партнер%, %Договор%, %Склад%';
											|en = 'Filter by: %Организация%, %Партнер%, %Договор%, %Склад%'");
	Иначе
		ИнформационнаяНадписьОтборы = НСтр("ru = 'Отбор по: %Договор%, %Склад%';
											|en = 'Filter by: %Договор%, %Склад%'");
	КонецЕсли;
	
	Если Склады.Количество() > 1 Тогда
		НадписьСклад = НСтр("ru = 'группе складов: ""%Склад%""';
							|en = 'warehouse group: ""%Склад%""'");
		НадписьСклад = СтрЗаменить(НадписьСклад, "%Склад%", Объект.Склад);
	Иначе
		НадписьСклад = "";
	КонецЕсли;
	
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Организация%", Объект.Организация);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Партнер%",     Объект.Владелец);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Договор%",     Объект.Договор);
	ИнформационнаяНадписьОтборы = СтрЗаменить(ИнформационнаяНадписьОтборы, "%Склад%",       НадписьСклад);
	
	Если Прав(ИнформационнаяНадписьОтборы, 2) = ", " Тогда
		ИнформационнаяНадписьОтборы = Лев(ИнформационнаяНадписьОтборы, СтрДлина(ИнформационнаяНадписьОтборы) - 2);
	КонецЕсли;
	
	ИнформационнаяНадписьОтборы = ИнформационнаяНадписьОтборы + ".";
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьКоличествоВСтроке(Товары, ТекущиеДанные, ТоварыПодобраны)
	
	ТекущиеДанные.КоличествоОсталосьПодобрать = ТекущиеДанные.КоличествоОстаток -ТекущиеДанные.КоличествоПодобрано;
	
	Отбор = Новый Структура("Пометка", Истина);
	ПомеченныеСтроки = Товары.НайтиСтроки(Отбор);
	
	ТоварыПодобраны = ПомеченныеСтроки.Количество() > 0;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

ВыполняетсяЗакрытие = Ложь;

#КонецОбласти
