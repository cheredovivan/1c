
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Сценарий = Параметры.Сценарий; // СправочникСсылка.СценарииТоварногоПланирования -
	ВидПлана = Параметры.ВидПлана; // СправочникСсылка.ВидыПланов -
	
	ТекстОшибки = Обработки.ПанельУправленияСервисомПрогнозирования.ОбновитьМодельВСервисе(ВидПлана);
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Возврат;
	КонецЕсли;
	
	СтоимостьОбучения = СервисПрогнозирования.ПолучитьСтоимостьОбученияМодели(ВидПлана);
	ПолученаСтоимостьОбучения = ?(ПустаяСтрока(СтоимостьОбучения.ТекстОшибки), Истина, Ложь);
	
	НастройкиВидаПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПлана, "Периодичность, ТипПлана, НачалоПрогнозирования,
		|ЗаполнятьСкладВТЧ, ЗаполнятьПартнераВТЧ, ЗаполнятьПоХарактеристикамНоменклатуры");
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, НастройкиВидаПлана);
	
	ТекущийБаланс = Параметры.Баланс;
	Если ПолученаСтоимостьОбучения Тогда
		ЗаполнитьПеременныеРасчета(СтоимостьОбучения.ДесериализованноеЗначение);
		БалансОстаток = ТекущийБаланс - КоличествоНеобходимыхЕдиниц;
	КонецЕсли;
	
	УстановитьЗаголовкиВидимостьЭлементовФормы();
	Элементы.ГруппаДанныеПрогноза.Скрыть();
	Элементы.ГруппаПравилаСписанияБаланса.Скрыть();
	
	ЗаполнитьСводкуДанныхПрогноза();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		// Отмена из-за ошибки в запросах по модели
		Оповестить("ЗапуститьОбучение", ТекстОшибки, ВладелецФормы);
		ВызватьИсключение ТекстОшибки;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗапроситьПрогноз(Команда)
	
	Если ПолученаСтоимостьОбучения Тогда 
		Оповестить("ЗапуститьОбучение", "", ВладелецФормы);
		Закрыть();
	Иначе
		ТекстВопроса = НСтр("ru = 'Неизвестно количество вычислительных единиц, которые будут списаны с баланса на построение прогноза. Продолжить?';
							|en = 'The number of units that will be debited from the balance for building the forecast is unknown. Continue?'");
		Обработчик = Новый ОписаниеОповещения("ЗапроситьПрогнозБезРасчетаЗатрат", ЭтотОбъект);
		ПоказатьВопрос(Обработчик, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВКалькулятор(Команда)
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
		СервисПрогнозированияПереопределяемыйКлиентСервер.СсылкаНаКалькуляторРасчетаЗатратНаПостроениеПрогноза());
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьЗапросСтоимостиОбучения(Команда)
	
	ОбновитьРасчетЗатрат();
	
	Если Не ПолученаСтоимостьОбучения Тогда
		ОчиститьСообщения();
		ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(
			НСтр("ru = 'Не удалось повторно получить стоимость обучения по причине:
				|%1.';
				|en = 'Cannot get the training cost again due to:
				|%1.'"),
			ТекстОшибки));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовкиВидимостьЭлементовФормы()
	
	Если ЗаполнятьСкладВТЧ
		Или ЗаполнятьПартнераВТЧ
		Или ЗаполнятьПоХарактеристикамНоменклатуры Тогда
		
		ПеречислениеДетализаций = НСтр("ru = 'по %1';
										|en = 'to %1'");
		СтрокаДетализаций = "";
		Если ЗаполнятьСкладВТЧ Тогда
			СтрокаДетализаций = НСтр("ru = 'складам';
									|en = 'Warehouses'");
		КонецЕсли;
		Если ЗаполнятьПартнераВТЧ Тогда
			Если ПустаяСтрока(СтрокаДетализаций) Тогда
				СтрокаДетализаций = НСтр("ru = 'клиентам';
										|en = 'Customers'");
			Иначе
				СтрокаДетализаций = СтрокаДетализаций + ", " + НСтр("ru = 'клиентам';
																	|en = 'Customers'");
			КонецЕсли;
		КонецЕсли;
		Если ЗаполнятьПоХарактеристикамНоменклатуры Тогда
			Если ПустаяСтрока(СтрокаДетализаций) Тогда
				СтрокаДетализаций = НСтр("ru = 'характеристикам';
										|en = 'Variants'");
			Иначе
				СтрокаДетализаций = СтрокаДетализаций + ", " + НСтр("ru = 'характеристикам';
																	|en = 'Variants'");
			КонецЕсли;
		КонецЕсли;
		ПеречислениеДетализаций = СтрШаблон(ПеречислениеДетализаций, СтрокаДетализаций);
	Иначе
		ПеречислениеДетализаций = "";
	КонецЕсли;
	
	Если ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
		ТекстОбъектыВПравилахСписания = НСтр("ru = 'пар категорий товаров';
											|en = 'Product category pairs'");
	Иначе
		ТекстОбъектыВПравилахСписания = НСтр("ru = 'пар товаров';
											|en = 'Goods pairs'");
	КонецЕсли;
	
	Если Периодичность = Перечисления.Периодичность.День Тогда
		ТекстПериодичностьВИзменениеБаланса = НСтр("ru = 'дня';
													|en = 'day'");
		ТекстПериодичностьВПравилахСписания = НСтр("ru = 'дней';
													|en = 'days'");
		ТекстПериодичностьВДанныхПрогноза   = НСтр("ru = 'дням';
													|en = 'Days'");
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		ТекстПериодичностьВИзменениеБаланса = НСтр("ru = 'недели';
													|en = 'weeks'");
		ТекстПериодичностьВПравилахСписания = НСтр("ru = 'недель';
													|en = 'weeks'");
		ТекстПериодичностьВДанныхПрогноза   = НСтр("ru = 'неделям';
													|en = 'Weeks'");
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		ТекстПериодичностьВИзменениеБаланса = НСтр("ru = 'месяца';
													|en = 'month'");
		ТекстПериодичностьВПравилахСписания = НСтр("ru = 'месяцев';
													|en = 'months'");
		ТекстПериодичностьВДанныхПрогноза   = НСтр("ru = 'месяцам';
													|en = 'Months'");
	КонецЕсли;
	
	Если ПолученаСтоимостьОбучения Тогда
		
		Элементы.СписаниеБаланса.Заголовок = СтрШаблон(Элементы.СписаниеБаланса.Заголовок,
			КоличествоНеобходимыхЕдиниц);
		Если БалансОстаток < 0 Тогда
			Элементы.ИзменениеБалансаПоложительный.Видимость = Ложь;
			Элементы.ИзменениеБалансаОтрицательный.Видимость = Истина;
			БалансВЗаголовке = БалансОстаток * -1;
			Элементы.ИзменениеБалансаОтрицательный.Заголовок = СтрШаблон(Элементы.ИзменениеБалансаОтрицательный.Заголовок,
				БалансВЗаголовке);
		Иначе
			Элементы.ИзменениеБалансаПоложительный.Видимость = Истина;
			Элементы.ИзменениеБалансаОтрицательный.Видимость = Ложь;
			БалансВЗаголовке = БалансОстаток;
			Элементы.ИзменениеБалансаПоложительный.Заголовок = СтрШаблон(Элементы.ИзменениеБалансаПоложительный.Заголовок,
				БалансВЗаголовке);
		КонецЕсли;
		
		ШаблонСтроки = НСтр("ru = 'При построении прогноза каждые %1 %2, за год будет списано %3 единиц.';
							|en = 'When generating a forecast every %1 %2, %3 units will be written off for a year.'");
		ШаблонСтроки = СтрШаблон(ШаблонСтроки,
			КоличествоПериодовПрогноза, ТекстПериодичностьВИзменениеБаланса, КоличествоНеобходимыхЕдиницВГод);
		Элементы.ЗатратыИзменениеБалансаЗаГод.Заголовок = ШаблонСтроки;
		
		Элементы.ГруппаОшибкаПолученияСтоимости.Видимость = Ложь;
		Элементы.ГруппаИнформацияРасчетаЗатрат.Видимость  = Истина;
	Иначе
		Элементы.ГруппаОшибкаПолученияСтоимости.Видимость = Истина;
		Элементы.ГруппаИнформацияРасчетаЗатрат.Видимость  = Ложь;
	КонецЕсли;
	
	ШаблонСтроки = НСтр("ru = 'При расчете необходимого количества единиц, учитываются следующие данные:
		|1) Число объектов прогнозирования - количество %1 %2.
		|2) Горизонт прогнозирования - количество %3, на которые строится прогноз.
		|3) История объектов прогнозирования - количество %3 средней длины рядов истории продаж,
		|   где длина ряда - количество %3 продаж конкретного объекта.';
		|en = 'When calculating the required number of units, the following data is used:
		|1) Number of forecasting objects. Number of %1 %2.
		|2) Forecast horizon. Number of %3 to build a forecast.
		|3) Forecasting object history. Number of %3 of an average sales history range length
		| where the range length is a number of %3 sales for a specific object.'");
	ШаблонСтроки = СтрШаблон(ШаблонСтроки,
		ТекстОбъектыВПравилахСписания, ПеречислениеДетализаций, ТекстПериодичностьВПравилахСписания);
	Элементы.ПеременныеСписанияБаланса.Заголовок = ШаблонСтроки;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПеременныеРасчета(Переменные)
	
	КоличествоНеобходимыхЕдиниц       = Переменные["credit_units"];
	КоличествоПериодовПрогноза        = Переменные["horizon"];
	СредняяДлинаИсторииПродаж         = Переменные["mean_history"];
	КоличествоОбъектовПрогнозирования = Переменные["n_train_objs"];
	
	Если Периодичность = Перечисления.Периодичность.День Тогда
		КоличествоПериодовВГоду = 365;
	ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
		КоличествоПериодовВГоду = 52;
	ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
		КоличествоПериодовВГоду = 12;
	КонецЕсли;
	
	КоличествоПрогнозовЗаГод        = Окр(КоличествоПериодовВГоду / КоличествоПериодовПрогноза);
	КоличествоНеобходимыхЕдиницВГод = КоличествоПрогнозовЗаГод * КоличествоНеобходимыхЕдиниц;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСводкуДанныхПрогноза()
	
	СтрокиСводки = Новый Массив;
	
	ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Сценарий: %1';
																|en = 'Scenario: %1'"),
		Сценарий);
	СтрокиСводки.Добавить(ШаблонСтроки);
	СтрокиСводки.Добавить(Символы.ПС);
	ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Вид плана: %1';
																|en = 'Plan profile: %1'"),
		ВидПлана);
	СтрокиСводки.Добавить(ШаблонСтроки);
	СтрокиСводки.Добавить(Символы.ПС);
	ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Начало прогнозирования: %1';
																|en = 'Forecasting start: %1'"),
		Формат(НачалоПрогнозирования, "ДЛФ=D"));
	СтрокиСводки.Добавить(ШаблонСтроки);
	СтрокиСводки.Добавить(Символы.ПС);
	
	Если Не ПустаяСтрока(ПеречислениеДетализаций) Тогда
		ШаблонСтроки = НСтр("ru = 'Детализация %1';
							|en = 'Dimensions %1'");
		ШаблонСтроки = СтрШаблон(ШаблонСтроки, ПеречислениеДетализаций);
		СтрокиСводки.Добавить(ШаблонСтроки);
		СтрокиСводки.Добавить(Символы.ПС);
	КонецЕсли;
	ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Прогнозировать по %1';
																|en = 'Forecast by %1'"),
		ТекстПериодичностьВДанныхПрогноза);
	СтрокиСводки.Добавить(ШаблонСтроки);
	СтрокиСводки.Добавить(Символы.ПС);
	
	Если ПолученаСтоимостьОбучения Тогда
		
		СтрокиСводки.Добавить(Символы.ПС);
		
		Если ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
			ШаблонСтроки = НСтр("ru = 'Количество прогнозируемых категорий: %1';
								|en = 'Number of categories to forecast: %1'");
		Иначе
			ШаблонСтроки = НСтр("ru = 'Количество прогнозируемых товаров: %1';
								|en = 'Number of goods to forecast: %1'");
		КонецЕсли;
		ШаблонСтроки = СтрШаблон(ШаблонСтроки, КоличествоОбъектовПрогнозирования);
		СтрокиСводки.Добавить(ШаблонСтроки);
		СтрокиСводки.Добавить(Символы.ПС);
		ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Прогнозировать на (горизонт): %1';
																	|en = 'Forecast for (horizon): %1'"),
			КоличествоПериодовПрогноза);
		СтрокиСводки.Добавить(ШаблонСтроки);
		СтрокиСводки.Добавить(Символы.ПС);
		ШаблонСтроки = СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = 'Средняя длина истории продаж: %1';
																	|en = 'Average sale history length: %1'"),
			СредняяДлинаИсторииПродаж);
		СтрокиСводки.Добавить(ШаблонСтроки);
		
	КонецЕсли;
	
	СводкаДанныхПрогноза.УстановитьФорматированнуюСтроку(Новый ФорматированнаяСтрока(СтрокиСводки));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРасчетЗатрат()
	
	СтоимостьОбучения = СервисПрогнозирования.ПолучитьСтоимостьОбученияМодели(ВидПлана);
	ТекстОшибки = СтоимостьОбучения.ТекстОшибки;
	
	ПолученаСтоимостьОбучения = ?(ПустаяСтрока(ТекстОшибки), Истина, Ложь);
	Если ПолученаСтоимостьОбучения Тогда
		ЗаполнитьПеременныеРасчета(СтоимостьОбучения.ДесериализованноеЗначение);
		БалансОстаток = ТекущийБаланс - КоличествоНеобходимыхЕдиниц;
	КонецЕсли;
	
	УстановитьЗаголовкиВидимостьЭлементовФормы();
	ЗаполнитьСводкуДанныхПрогноза();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьПрогнозБезРасчетаЗатрат(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Оповестить("ЗапуститьОбучение", "БезРасчетаЗатрат", ВладелецФормы);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
