
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПереданныеОшибки = Параметры.ПереданныеОшибки; // см. СервисПрогнозирования.РезультатПроверкиКорректностиНастроекСервиса
	Если ПереданныеОшибки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПереданныеОшибки.Свойство("ЕстьПользовательскиеОшибки") Тогда
		// Наличие пользовательских ошибок в выгрузке или настройках.
		ПользовательскиеОшибки = Истина;
		Сценарий = Параметры.Сценарий; // СправочникСсылка.СценарииТоварногоПланирования -
		ВидПлана = Параметры.ВидПлана; // СправочникСсылка.ВидыПланов -
		ЗаполнитьПользовательскиеОшибки(ПереданныеОшибки);
	Иначе
		// Наличие технической ошибки выполнения запроса к сервису.
		ПользовательскиеОшибки = Ложь;
		ВидПлана = ПереданныеОшибки.ВидПлана; // СправочникСсылка.ВидыПланов -
		Сценарий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "Владелец"); // СправочникСсылка.СценарииТоварногоПланирования -
		ЗаполнитьТехническуюОшибку(Ошибки, ПереданныеОшибки, ВидПлана);
	КонецЕсли;
	УстановитьЗаголовкиЭлементовФормы();
	ОбновитьВидимостьЭлементовФормы();
	
	СброситьРазмерыИПоложениеОкна();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияИсправитьОшибки) Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗаданияИсправитьОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СформироватьИнформациюДляТехподдержкиНажатие(Элемент)
	
	Если ПользовательскиеОшибки Тогда
		Если Ошибки.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		ЕстьОшибкиНастроекВидаПлана = Ложь;
		Для Каждого Ошибка Из Ошибки Цикл
			
			ЕстьОшибкиНастроекВидаПлана = ОшибкаНастроекВидаПлана(Ошибка.Причина);
			Если ЕстьОшибкиНастроекВидаПлана Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		Информация = ИнформацияДляТехподдержки(ЕстьОшибкиНастроекВидаПлана);
	Иначе
		Информация = ИнформацияДляТехподдержки(Ложь);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Информация",               Информация);
	ПараметрыФормы.Вставить("ПоказатьКнопкуСохранения", Истина);
	ОткрытьФорму("Обработка.ПанельУправленияСервисомПрогнозирования.Форма.ФормаПроизвольнойИнформации",
		ПараметрыФормы,,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИнформациюОТехническойОшибкеНажатие(Элемент)
	
	Оповестить("ОчиститьИнформациюОТехническойОшибке",, ВладелецФормы);
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОшибки

&НаКлиенте
Процедура ОшибкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Поле.Имя = "ОшибкиОписание"
		Или Поле.Имя = "ОшибкиРекомендация" Тогда
		
		ШаблонИнформации = НСтр("ru = 'Описание:
		|%1
		|
		|Рекомендация:
		|%2';
		|en = 'Details:
		|%1
		|
		|Recommendation:
		|%2'");
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Информация", СтрШаблон(ШаблонИнформации,
			Элемент.ТекущиеДанные.Описание,
			Элемент.ТекущиеДанные.Рекомендация));
		ПараметрыФормы.Вставить("ПоказатьКнопкуСохранения", Ложь);
		ПараметрыФормы.Вставить("Заголовок", НСтр("ru = 'Информация по ошибке';
													|en = 'Error details'"));
		ОткрытьФорму("Обработка.ПанельУправленияСервисомПрогнозирования.Форма.ФормаПроизвольнойИнформации",
			ПараметрыФормы,
			ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Возврат;
		
	КонецЕсли;
	
	Если Поле.Имя <> "ОшибкиИсправление" Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элемент.ТекущиеДанные;
	Если ТекущаяСтрока.Исправление <> Неопределено
		И Не ПустаяСтрока(ТекущаяСтрока.Исправление) Тогда
		ОчиститьСообщения();
		РезультатФоновогоЗадания = ИсправитьОшибки(ТекущаяСтрока.Причина);
		ПодключитьОжиданиеФоновогоЗадания(РезультатФоновогоЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьАвтоматически(Команда)
	
	ОчиститьСообщения();
	
	РезультатФоновогоЗадания = ИсправитьОшибки();
	ПодключитьОжиданиеФоновогоЗадания(РезультатФоновогоЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовкиЭлементовФормы()
	
	Если ПользовательскиеОшибки Тогда
		Заголовок = НСтр("ru = 'Отчет по проблемам';
						|en = 'Report on problems'");
		Элементы.Предупреждение.Заголовок = НСтр("ru = 'Найдены некорректные настройки сервиса или модели, из-за которых построение прогноза невозможно.
			|Следуйте рекомендациям для их решения или воспользуйтесь командой автоматического исправления (не все настройки возможно исправить автоматически).
			|Строки ошибок удаляются при их успешном исправлении.';
			|en = 'Incorrect service or model settings found. Cannot generate a forecast.
			|To fix the issue, follow the recommendations or click Fix automatically (some settings cannot be fixed automatically).
			|Error lines are deleted when fixed.'");
	Иначе
		Заголовок = НСтр("ru = 'К сожалению, возникла непредвиденная ситуация';
						|en = 'Oops… An unexpected situation has occurred'");
		Элементы.Предупреждение.Заголовок = НСтр("ru = 'Попробуйте повторить операцию. В случае повторного возникновения ошибки, воспользуйтесь формированием информации для передачи в техподдержку.';
												|en = 'Try to repeat the operation. In case the error persists, try to gather the information to transfer it to the technical support.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьЭлементовФормы()
	
	Элементы.ОчиститьИнформациюОТехническойОшибке.Видимость = Не ПользовательскиеОшибки;
	Если ПользовательскиеОшибки Тогда
		Элементы.ГруппаКнопокПользовательскиеОшибки.Видимость = ОшибокСВозможностьюИсправления > 0;
	Иначе
		Элементы.ГруппаКнопокПользовательскиеОшибки.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПользовательскиеОшибки(ПереданныеОшибки)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ПереданныеОшибки.ДанныеНастроек);
	ОтсутствиеОжидаемыхДетализаций = ПереданныеОшибки.ОтсутствиеОжидаемыхДетализаций;
	КоличествоНеХватающихПериодов  = ПереданныеОшибки.ДанныеПоГоризонту.КоличествоНеХватающихПериодов;
	
	ДанныеЗапрета                  = ПереданныеОшибки.ДанныеЗапрета;
	ДатаЗапрета                    = ДанныеЗапрета.ДатаЗапрета;
	
	ДанныеПоДатам                  = ПереданныеОшибки.ДанныеПоДатам;
	ДатаНачалаПродаж               = ДанныеПоДатам.ДатаНачалаПродаж;
	МаксимальноДопустимаяДата      = ДанныеПоДатам.МаксимальноДопустимаяДата;
	
	ОшибокСВозможностьюИсправления = 0;
	
	СтатусПодключения = ПереданныеОшибки.СтатусПодключения;
	Если Не ПустаяСтрока(СтатусПодключения) Тогда
		СтрокаТаблицы = Ошибки.Добавить();
		Если СтатусПодключения = "НеАвторизован" Тогда
			СтрокаТаблицы.Описание      = НСтр("ru = 'Не найдена регистрация в сервисе';
												|en = 'The registration is not found on the server'");
			СтрокаТаблицы.Рекомендация  = НСтр("ru = 'Откройте мастер подключения в расширенном режиме и нажмите команду ""Отправить заявку""';
												|en = 'Open the connection wizard in the extended mode and press the ""Send request command""'");
		ИначеЕсли СтатусПодключения = "НеАктивирован" Тогда
			СтрокаТаблицы.Описание      = НСтр("ru = 'Заявка отправлена, ожидается активация в сервисе';
												|en = 'The request is sent, activation is pending on the server'");
			СтрокаТаблицы.Рекомендация  = НСтр("ru = 'Подождите, активируем в ближайшее время';
												|en = 'Wait, will be activated in the near future'");
		КонецЕсли;
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
	КонецЕсли;
	
	Если ПереданныеОшибки.НеактуальныеКоллекции.Количество() > 0 Тогда
		ШаблонСтроки = НСтр("ru = 'У коллекций ""%1"" изменился состав передаваемых данных';
							|en = 'The composition of the transferred data for the ""%1"" collections has changed'");
		Если ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж)
			Или Пользователи.ЭтоПолноправныйПользователь() Тогда
			ТекстРекомендации = НСтр("ru = 'Заново выгрузите коллекции в сервис, открыв помощник подключения, в котором:
				|В простом режиме - нажмите команду ""Выгрузить данные"".
				|В расширенном режиме - перейдите в последний раздел и нажмите команду ""Полная выгрузка данных"".';
				|en = 'Export the collection in the service by opening the connection wizard in which:
				|In the basic mode, press the ""Export data"" command.
				|In the extended mode, go to the last section and press the ""Full export of data"" command.'");
		Иначе
			ТекстРекомендации = НСтр("ru = 'Заново выгрузите коллекции в сервис.
				|Обратитесь к администратору.';
				|en = 'Re-export the collections into the service.
				|Contact the administrator.'");
		КонецЕсли;
		
		СтрокаТаблицы = Ошибки.Добавить();
		СтрокаТаблицы.Описание          = СтрШаблон(ШаблонСтроки, СтрСоединить(ПереданныеОшибки.НеактуальныеКоллекции, ", "));
		СтрокаТаблицы.Рекомендация      = ТекстРекомендации;
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
	КонецЕсли;
	
	НастройкиСервиса = Неопределено;
	Если ОтсутствиеОжидаемыхДетализаций.КодКатегории Тогда
		НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
		Коллекция = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары];
		
		СтрокаТаблицы = Ошибки.Добавить();
		Если Коллекция.Выгружать
			И Коллекция.ВложенноеОписание.КодКатегории.Выгружать Тогда
			ТекстРекомендации = ТекстОтсутствиеОжидаемойДетализацииПолеОтмечено(Коллекция.ИмяВИсточнике,
				Коллекция.ВложенноеОписание.КодКатегории.Представление);
		Иначе
			ТекстРекомендации = НСтр("ru = 'Отметьте к выгрузке коллекцию ""Товары"" и ее вложенное поле ""Код категории"", после чего заново выгрузите коллекцию';
									|en = 'Mark the ""Goods"" collection for export and its attached ""Category code"" field after which export the collection again'");
			СтрокаТаблицы.Исправление = НСтр("ru = 'Отметить';
											|en = 'Mark'"); // Частичное исправление
			СтрокаТаблицы.Причина     = "ОтсутствиеОжидаемыхДетализаций";
			СтрокаТаблицы.ДополнительныйПараметр = Новый Структура("Коллекция, ВложенноеПоле",
				Коллекция.ИмяВИсточнике, Коллекция.ВложенноеОписание.КодКатегории.Представление);
		КонецЕсли;
		СтрокаТаблицы.Описание          = НСтр("ru = 'Прогнозирование по категориям невозможно, поскольку в сервис прогнозирования не выгружена информация о категориях';
												|en = 'Forecasting by categories is not possible because the information about the categories is not exported to the forecasting service'");
		СтрокаТаблицы.Рекомендация      = ТекстРекомендации;
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
	КонецЕсли;
	Если ОтсутствиеОжидаемыхДетализаций.КодКлиента Тогда
		Если НастройкиСервиса = Неопределено Тогда
			НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
			Коллекция = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
		Иначе
			Коллекция = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
		КонецЕсли;
		
		СтрокаТаблицы = Ошибки.Добавить();
		Если Коллекция.ВложенноеОписание.КодКлиента.Выгружать Тогда
			ТекстРекомендации = ТекстОтсутствиеОжидаемойДетализацииПолеОтмечено(Коллекция.ИмяВИсточнике,
				Коллекция.ВложенноеОписание.КодКлиента.Представление);
		Иначе
			ТекстРекомендации = НСтр("ru = 'Отметьте к выгрузке вложенное поле ""Код покупателя"" у коллекции ""Продажи"", после чего заново выгрузите коллекцию';
									|en = 'Mark the nested ""Customer code"" field for export of the ""Sales"" collection, then export the collection again'");
			СтрокаТаблицы.Исправление = НСтр("ru = 'Отметить';
											|en = 'Mark'"); // Частичное исправление
			СтрокаТаблицы.Причина     = "ОтсутствиеОжидаемыхДетализаций";
			СтрокаТаблицы.ДополнительныйПараметр = Новый Структура("Коллекция, ВложенноеПоле",
				Коллекция.ИмяВИсточнике, Коллекция.ВложенноеОписание.КодКлиента.Представление);
		КонецЕсли;
		СтрокаТаблицы.Описание          = НСтр("ru = 'Прогнозирование с детализацией до клиента невозможно, поскольку в сервис прогнозирования не выгружена информация о покупателях';
												|en = 'Forecasting with details up to a customer is unavailable because customer information is not exported to the forecasting service'");
		СтрокаТаблицы.Рекомендация      = ТекстРекомендации;
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
	КонецЕсли;
	Если ОтсутствиеОжидаемыхДетализаций.КодХарактеристики Тогда
		Если НастройкиСервиса = Неопределено Тогда
			НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
			Коллекция = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
		Иначе
			Коллекция = НастройкиСервиса.Коллекции[Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи];
		КонецЕсли;
		
		СтрокаТаблицы = Ошибки.Добавить();
		Если Коллекция.ВложенноеОписание.КодХарактеристики.Выгружать Тогда
			ТекстРекомендации = ТекстОтсутствиеОжидаемойДетализацииПолеОтмечено(Коллекция.ИмяВИсточнике,
				Коллекция.ВложенноеОписание.КодХарактеристики.Представление);
		Иначе
			ТекстРекомендации = НСтр("ru = 'Отметьте к выгрузке вложенное поле ""Код характеристики"" у коллекции ""Продажи"", после чего заново выгрузите коллекцию';
									|en = 'Mark the nested ""Variant code"" field of the ""Sales"" collection for export and export the collection again'");
			СтрокаТаблицы.Исправление = НСтр("ru = 'Отметить';
											|en = 'Mark'"); // Частичное исправление
			СтрокаТаблицы.Причина     = "ОтсутствиеОжидаемыхДетализаций";
			СтрокаТаблицы.ДополнительныйПараметр = Новый Структура("Коллекция, ВложенноеПоле",
				Коллекция.ИмяВИсточнике, Коллекция.ВложенноеОписание.КодХарактеристики.Представление);
		КонецЕсли;
		СтрокаТаблицы.Описание          = НСтр("ru = 'Прогнозирование с детализацией до характеристик номенклатуры невозможно, поскольку в сервис прогнозирования не выгружена информация о характеристиках';
												|en = 'Forecasting with details up to item variants is unavailable because variant information is not exported to the forecasting service'");
		СтрокаТаблицы.Рекомендация      = ТекстРекомендации;
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЗапрета) Тогда
		СтрокаТаблицы = Ошибки.Добавить();
		СтрокаТаблицы.Описание = ДанныеЗапрета.Описание;
		
		// Недопустимый разрыв при сдвигании начала прогнозирования за дату запрета.
		Если ДанныеЗапрета.РазницаМеждуДатамиЗаЗапретом > 0 Тогда
			ШаблонСтроки = НСтр("ru = 'Нельзя сдвинуть дату начала прогнозирования %1 за дату запрета %2 из-за недопустимого промежутка без продаж в %3 дней.
				|Измените дату запрета или выгрузите продажи за даты, позднее даты последней продажи для уменьшения разрыва данных.';
				|en = 'You cannot move the forecasting start date %1 past the period-end closing date %2 because of an invalid period without sales of %3 days.
				|Change the period-end closing date or export the sales for the date after the last sale date to decrease the data interval.'");
			СтрокаТаблицы.Рекомендация      = СтрШаблон(ШаблонСтроки,
				Формат(НачалоПрогнозирования, "ДЛФ=D"), Формат(ДатаЗапрета, "ДЛФ=D"), ДанныеЗапрета.РазницаМеждуДатамиЗаЗапретом);
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		Иначе
			ШаблонСтроки = НСтр("ru = 'Сдвиньте дату начала прогнозирования %1 за дату запрета %2 или измените сам запрет';
								|en = 'Move forecasting start date %1 outside closing date %2 or change the closing date'");
			СтрокаТаблицы.Рекомендация      = СтрШаблон(ШаблонСтроки,
				Формат(НачалоПрогнозирования, "ДЛФ=D"), Формат(ДатаЗапрета, "ДЛФ=D"));
			СтрокаТаблицы.Исправление       = НСтр("ru = 'Исправить';
													|en = 'Fix'");
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
			СтрокаТаблицы.Причина           = "ДатаЗапрета";
			ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
		КонецЕсли;
		
	КонецЕсли;
	
	Если КоличествоНеХватающихПериодов > 0 Тогда
		Если Периодичность = Перечисления.Периодичность.День Тогда
			ПериодичностьСтрокой = НСтр("ru = 'дней';
										|en = 'days'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Неделя Тогда
			ПериодичностьСтрокой = НСтр("ru = 'недель';
										|en = 'weeks'");
		ИначеЕсли Периодичность = Перечисления.Периодичность.Месяц Тогда
			ПериодичностьСтрокой = НСтр("ru = 'месяцев';
										|en = 'months'");
		КонецЕсли;
		
		СтрокаТаблицы = Ошибки.Добавить();
		ШаблонСтроки = НСтр("ru = 'Нет истории продаж достаточной длины для построения прогноза на %1 %2';
							|en = 'No sales history long enough to generate a forecast for %1 %2'");
		СтрокаТаблицы.Описание = СтрШаблон(ШаблонСтроки, КоличествоПериодов, ПериодичностьСтрокой);
		
		// Недостаточно данных для исправления путем уменьшения горизонта.
		Если КоличествоНеХватающихПериодов >= КоличествоПериодов Тогда
			// Пустая история продаж.
			Если ПустаяСтрока(ПереданныеОшибки.ДанныеПоГоризонту.ТоповыйТовар) Тогда
				ШаблонСтроки = НСтр("ru = 'Выгрузите минимум %1 %2 продаж любого товара';
									|en = 'Export at least %1 %2 sales of any item'");
				СтрокаТаблицы.Рекомендация  = СтрШаблон(ШаблонСтроки,
					КоличествоНеХватающихПериодов, ПериодичностьСтрокой);
			Иначе
				ШаблонСтроки = НСтр("ru = 'Выгрузите еще как минимум %1 %2 продаж товара ""%3""';
									|en = 'Export at least %1 %2 more sales of the ""%3"" item'");
				СтрокаТаблицы.Рекомендация  = СтрШаблон(ШаблонСтроки,
					КоличествоНеХватающихПериодов, ПериодичностьСтрокой, ПереданныеОшибки.ДанныеПоГоризонту.ТоповыйТовар);
			КонецЕсли;
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		Иначе
			ШаблонСтроки = НСтр("ru = 'Выгрузите еще как минимум %1 %2 продаж товара ""%3"". Или уменьшите количество %2 прогнозирования на %1 или более.';
								|en = 'Export at least %1 %2 more sales of the ""%3"" item or lower the number of forecasting %2 by %1 or more.'");
			СтрокаТаблицы.Рекомендация      = СтрШаблон(ШаблонСтроки,
				КоличествоНеХватающихПериодов, ПериодичностьСтрокой, ПереданныеОшибки.ДанныеПоГоризонту.ТоповыйТовар);
			СтрокаТаблицы.Исправление       = НСтр("ru = 'Исправить';
													|en = 'Fix'");
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
			СтрокаТаблицы.Причина           = "НеХватаетДанныхПоГоризонту";
			ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеПоДатам.ДатаОкончанияПродаж) Тогда
		
		СтрокаТаблицы = Ошибки.Добавить();
		ШаблонСтроки = НСтр("ru = 'Недопустимый промежуток без продаж в %1 дней между датой начала прогнозирования %2 и датой последней продажи %3';
							|en = 'Invalid no-sales period of %1 days between forecasting start date %2 and last sale date %3'");
		СтрокаТаблицы.Описание              = СтрШаблон(ШаблонСтроки,
			ДанныеПоДатам.РазницаМеждуДатами,
			Формат(НачалоПрогнозирования, "ДЛФ=D"),
			Формат(ДанныеПоДатам.ДатаОкончанияПродаж, "ДЛФ=D"));
		Если ЗначениеЗаполнено(ДанныеПоДатам.ДатаЗапретаПриИсправленииРазрыва) Тогда
			ШаблонСтроки = НСтр("ru = 'Нельзя сдвинуть дату начала прогнозирования %1 к %2 из-за попадания на дату запрета %3.
				|Измените дату запрета или выгрузите продажи за даты, позднее даты последней продажи для уменьшения разрыва данных.';
				|en = 'You cannot move forecasting start date %1 to %2 because it overlaps with closing date %3.
				|Change the closing date or export sales from the date that is later than the last sale date to decrease the data interval.'");
			СтрокаТаблицы.Рекомендация      = СтрШаблон(ШаблонСтроки, Формат(НачалоПрогнозирования, "ДЛФ=D"),
				Формат(МаксимальноДопустимаяДата, "ДЛФ=D"), Формат(ДанныеПоДатам.ДатаЗапретаПриИсправленииРазрыва, "ДЛФ=D"));
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		Иначе
			ШаблонСтроки = НСтр("ru = 'Сдвиньте дату начала прогнозирования хотя бы к %1 или более близкой к дате последней продажи';
								|en = 'Move the forecasting start date at least to %1 or closer to the last sale date'");
			СтрокаТаблицы.Рекомендация      = СтрШаблон(ШаблонСтроки, Формат(МаксимальноДопустимаяДата, "ДЛФ=D"));
			СтрокаТаблицы.Исправление       = НСтр("ru = 'Исправить';
													|en = 'Fix'");
			СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
			СтрокаТаблицы.Причина           = "НачалоПрогнозированияРазрывДатаОкончания";
			ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ДатаНачалаПродаж) Тогда
		СтрокаТаблицы = Ошибки.Добавить();
		ШаблонСтроки = НСтр("ru = 'Дата начала прогнозирования %1 меньше даты первой продажи %2';
							|en = 'Forecasting start date %1 is earlier than first sale date %2'");
		СтрокаТаблицы.Описание          = СтрШаблон(ШаблонСтроки,
			Формат(НачалоПрогнозирования, "ДЛФ=D"), Формат(ДатаНачалаПродаж, "ДЛФ=D"));
		СтрокаТаблицы.Рекомендация      = НСтр("ru = 'Сдвиньте дату начала прогнозирования к дате первой продажи';
												|en = 'Move the forecasting start date to the first sale date'");
		СтрокаТаблицы.Исправление       = НСтр("ru = 'Исправить';
												|en = 'Fix'");
		СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
		СтрокаТаблицы.Причина           = "НачалоПрогнозированияМеньшеДатыНачала";
		ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления + 1;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьТехническуюОшибку(ТаблицаОшибок, Ошибка, ВидПлана)
	
	ОписаниеРекомендацияОшибки = ОписаниеИРекомендацияТехническойОшибки(Ошибка.Запрос, ВидПлана, Ошибка.Коллекция);
	
	СтрокаТаблицы = ТаблицаОшибок.Добавить();
	СтрокаТаблицы.Описание          = ОписаниеРекомендацияОшибки.Описание;
	СтрокаТаблицы.Рекомендация      = ОписаниеРекомендацияОшибки.Рекомендация;
	СтрокаТаблицы.СтатусИсправления = БиблиотекаКартинок.ВосклицательныйЗнакКрасный;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеИРекомендацияТехническойОшибки(ИмяДействия, ВидПлана, Коллекция)
	
	ОписаниеРекомендация = Новый Структура;
	ОписаниеРекомендация.Вставить("Описание",     "");
	ОписаниеРекомендация.Вставить("Рекомендация", "");
	
	Если ИмяДействия = "РегистрацияПриложения" Тогда
		ОписаниеРекомендация.Описание     = НСтр("ru = 'Не удалось зарегистрировать приложение в сервисе';
												|en = 'Cannot register the application in the service'");
		ОписаниеРекомендация.Рекомендация = НСтр("ru = 'Попробуйте еще раз отправить заявку в сервис';
												|en = 'Send the request to the service again'");
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОКоллекциях" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось получить идентификатор коллекции ""%1"" с сервиса';
				|en = 'Cannot receive the ""%1"" collection ID from the service'"),
			Коллекция);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз запустить полную выгрузку данных. Для этого:
			|1) Остановите текущую выгрузку, воспользовавшись командой ""Прервать выгрузку"" на форме команд администратора,
			|которую можно открыть из формы отчета по пути: ""Еще - Команды сервиса прогнозирования - Команды администратора"".
			|2) Откройте помощник подключения, в котором:
			|В простом режиме - нажмите команду ""Выгрузить данные"".
			|В расширенном режиме - перейдите в последний раздел и нажмите команду ""Полная выгрузка данных"".';
			|en = 'Try to restart the full data export:
			|1) Open the report form, click ""More > Forecasting service commands > Administrator commands"",
			|and click ""Terminate export"" on the form to cancel the current export.
			|2) Open the connection wizard.
			|In the simple mode, click ""Export data"".
			|In the extended mode, go to the last section and click ""Full data export"".'");
	ИначеЕсли ИмяДействия = "УдалениеКоллекции" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось удалить коллекцию ""%1"" в сервисе';
				|en = 'Cannot delete the ""%1"" collection in the service'"),
			Коллекция);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз запустить полную выгрузку данных. Для этого:
			|1) Остановите текущую выгрузку, воспользовавшись командой ""Прервать выгрузку"" на форме команд администратора,
			|которую можно открыть из формы отчета по пути: ""Еще - Команды сервиса прогнозирования - Команды администратора"".
			|2) Откройте помощник подключения, в котором:
			|В простом режиме - нажмите команду ""Выгрузить данные"".
			|В расширенном режиме - перейдите в последний раздел и нажмите команду ""Полная выгрузка данных"".';
			|en = 'Try to restart the full data export:
			|1) Open the report form, click ""More > Forecasting service commands > Administrator commands"",
			|and click ""Terminate export"" on the form to cancel the current export.
			|2) Open the connection wizard.
			|In the simple mode, click ""Export data"".
			|In the extended mode, go to the last section and click ""Full data export"".'");
	ИначеЕсли ИмяДействия = "СозданиеМодели" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось создать модель у вида плана ""%1"" в сервисе';
				|en = 'Cannot create the ""%1"" plan profile model in the service'"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз создать модель, нажав на гиперссылку ""запросить прогноз""';
			|en = 'Try to create the model again and click ""Request forecast""'");
	ИначеЕсли ИмяДействия = "СозданиеКонфигурацииМодели" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось создать конфигурацию модели у вида плана ""%1"" в сервисе при запросе прогноза';
				|en = 'Cannot create the ""%1"" plan profile model configuration in the service when requesting the forecast'"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз создать конфигурацию, нажав на гиперссылку ""запросить прогноз""';
			|en = 'Try to create the configuration again and click ""Request forecast""'");
	ИначеЕсли ИмяДействия = "ПолучениеСтоимости" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось получить стоимость обучения модели у вида плана ""%1"" с сервиса';
				|en = 'Cannot receive the model training cost for the %1 plan profile from the service'"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз открыть форму расчета затрат на построение прогноза, нажав на гиперссылку ""запросить прогноз""';
			|en = 'Try clicking ""Request forecast"" again to open the form for forecast generation cost calculation'");
	ИначеЕсли ИмяДействия = "ЗапускОбученияМодели" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось запустить обучение модели по виду плана ""%1""';
				|en = 'Cannot start the model training for the ""%1"" plan profile'"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз запустить обучение, нажав на гиперссылку ""запросить прогноз""';
			|en = 'Try to start the training again and click ""Request forecast""'");
	ИначеЕсли ИмяДействия = "ОбновлениеИнформацииОСтатусеОбучения" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось обновить статус обучения модели у вида плана ""%1""';
				|en = 'Cannot update the model training status for the ""%1"" plan profile'"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз обновить статус, нажав на гиперссылку ""обновить статус""';
			|en = 'Try again to update the status by pressing the ""update status"" hyperlink.'");
	ИначеЕсли ИмяДействия = "ПолучениеПрогнозаСервиса" Тогда
		ОписаниеРекомендация.Описание = СтрШаблон(
			НСтр("ru = 'Не удалось получить данные прогноза по виду плана ""%1"" с сервиса';
				|en = 'Cannot obtain the forecast data for the ""%1"" plan profile from the service '"),
			ВидПлана);
		ОписаниеРекомендация.Рекомендация = НСтр(
			"ru = 'Попробуйте еще раз получить прогноз, нажав на гиперссылку ""получить прогноз""';
			|en = 'Try to create the forecast again by clicking on ""Request forecast""'");
	КонецЕсли;
	
	Возврат ОписаниеРекомендация;
	
КонецФункции

&НаСервере
Функция ИсправитьОшибки(ПричинаОшибки = Неопределено)
	
	ИсправляемыеОшибки = Новый Массив;
	Если ПричинаОшибки <> Неопределено Тогда
		ДанныеОшибки = Новый Структура("Причина, Исправлено", ПричинаОшибки, Ложь);
		ИсправляемыеОшибки.Добавить(ДанныеОшибки);
	Иначе
		
		Для Каждого Ошибка Из Ошибки Цикл
			Если ПустаяСтрока(Ошибка.Исправление) Тогда
				Продолжить;
			КонецЕсли;
			ДанныеОшибки = Новый Структура("Причина, Исправлено", Ошибка.Причина, Ложь);
			ИсправляемыеОшибки.Добавить(ДанныеОшибки);
		КонецЦикла;
		
	КонецЕсли;
	
	Если ИсправляемыеОшибки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		"СервисПрогнозирования.ИсправитьПользовательскиеОшибки",
		ВидПлана, ИсправляемыеОшибки, ДанныеДляИсправленияОшибок());
	
	Если РезультатВыполнения.Статус = "Выполняется" Тогда
		ИдентификаторЗаданияИсправитьОшибки = РезультатВыполнения.ИдентификаторЗадания;
		АдресХранилищаИсправитьОшибки       = РезультатВыполнения.АдресРезультата;
	ИначеЕсли РезультатВыполнения.Статус = "Выполнено" Тогда
		АдресХранилищаИсправитьОшибки       = РезультатВыполнения.АдресРезультата;
		ИсправитьОшибкиНаСервере();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция ДанныеДляИсправленияОшибок()
	
	Данные = Новый Структура;
	Данные.Вставить("Периодичность",                  Периодичность);
	Данные.Вставить("НачалоПрогнозирования",          НачалоПрогнозирования);
	Данные.Вставить("КоличествоПериодов",             КоличествоПериодов);
	Данные.Вставить("ОтсутствиеОжидаемыхДетализаций", ОтсутствиеОжидаемыхДетализаций);
	Данные.Вставить("КоличествоНеХватающихПериодов",  КоличествоНеХватающихПериодов);
	Данные.Вставить("ДатаЗапрета",                    ДатаЗапрета);
	Данные.Вставить("МаксимальноДопустимаяДата",      МаксимальноДопустимаяДата);
	Данные.Вставить("ДатаНачалаПродаж",               ДатаНачалаПродаж);
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатусИсправленияОшибок(ИсправляемыеОшибки)
	
	Для Каждого Ошибка Из ИсправляемыеОшибки Цикл
		Если Не Ошибка.Исправлено Тогда
			Продолжить; // Не удалось исправить ошибку.
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура("Причина", Ошибка.Причина);
		Строки = Ошибки.НайтиСтроки(ПараметрыОтбора);
		Если Строки.Количество() > 0 Тогда
			
			Если Ошибка.Причина = "ОтсутствиеОжидаемыхДетализаций" Тогда
				// Обновление после частичного исправления.
				Для Каждого Строка Из Строки Цикл
					Строка.Рекомендация = ТекстОтсутствиеОжидаемойДетализацииПолеОтмечено(
						Строка.ДополнительныйПараметр.Коллекция,
						Строка.ДополнительныйПараметр.ВложенноеПоле);
					Строка.Исправление  = "";
					ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления - 1;
				КонецЦикла;
			Иначе
				Ошибки.Удалить(Строки[0]);
				ОшибокСВозможностьюИсправления = ОшибокСВозможностьюИсправления - 1;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Если ОшибокСВозможностьюИсправления = 0 Тогда
		ОбновитьВидимостьЭлементовФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОшибкаНастроекВидаПлана(Причина)
	
	ОшибкаВидаПлана = Ложь;
	Если Причина = "ДатаЗапрета"
		Или Причина = "НеХватаетДанныхПоГоризонту"
		Или Причина = "НачалоПрогнозированияРазрывДатаОкончания"
		Или Причина = "НачалоПрогнозированияМеньшеДатыНачала" Тогда
		ОшибкаВидаПлана = Истина;
	КонецЕсли;
	Возврат ОшибкаВидаПлана;
	
КонецФункции

&НаСервере
Функция ИнформацияДляТехподдержки(ЕстьОшибкиНастроекВидаПлана)
	
	ПараметрыШаблона = Новый Массив;
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	ШаблонИнформации = НСтр("ru = 'Аутентификационные данные:
	|%1
	|========================================
	|Общая информация (может отличаться от данных на сервере):
	|Последняя известная дата продаж (с учетом отборов): %2
	|Идентификаторы коллекций:
	|%3';
	|en = 'Authentication data:
	|%1
	|========================================
	|General information (can differ from the data on the server):
	|The last known sale data (with filters taken into account): %2
	|Collection IDs:
	|%3'");
	
	// Аутентификация.
	ШаблонАутентификационныеДанные = НСтр("ru = 'Имя сервиса (service_name): %1
	|Токены заданы: %2, Дата обновления токенов: %3
	|Идентификатор приложения (application_uuid): %4
	|Статус подключения: %5, Баланс: %6
	|Дата окончания действия тарифа(ов): %7';
	|en = 'Service_name: %1
	|Tokens are set: %2, Token update date: %3
	|Application_uuid: %4
	|Connection status: %5, Balance: %6
	|Support plan expiration date: %7'");
	
	Если НастройкиСервиса.СтатусПодключения = СервисПрогнозирования.СтатусПодключенияАктивен() Тогда
		ТекстСтатусПодключения = НСтр("ru = 'Активен';
										|en = 'Active'");
	ИначеЕсли НастройкиСервиса.СтатусПодключения = СервисПрогнозирования.СтатусПодключенияВОчереди() Тогда
		ТекстСтатусПодключения = НСтр("ru = 'В очереди';
										|en = 'In queue'");
	Иначе
		ТекстСтатусПодключения = НСтр("ru = 'Нет';
										|en = 'No'");
	КонецЕсли;
	
	ШаблонАутентификационныеДанные = СтрШаблон(ШаблонАутентификационныеДанные,
		НастройкиСервиса.ИмяСервиса,
		Не ПустаяСтрока(НастройкиСервиса.ТокенПриложения) И Не ПустаяСтрока(НастройкиСервиса.ТокенОбновления),
		НастройкиСервиса.ДатаОбновленияТокенов,
		НастройкиСервиса.ИдентификаторПриложения,
		ТекстСтатусПодключения,
		НастройкиСервиса.Баланс,
		Формат(НастройкиСервиса.ДатаОкончанияМаксимальногоТарифа, "ДЛФ=D"));
	
	ПараметрыШаблона.Добавить(ШаблонАутентификационныеДанные);
	
	// Последняя известная дата продаж.
	ДатаОкончанияПродаж = СервисПрогнозированияПереопределяемый.ПоследняяИзвестнаяДатаПродажи();
	ПараметрыШаблона.Добавить(Формат(ДатаОкончанияПродаж, "ДЛФ=D"));
	
	// Коллекции.
	ШаблонСтрокиКоллекции = НСтр("ru = '%1: %2';
								|en = '%1: %2'");
	
	ИнфоОКоллекцияхСтрокой = "";
	ВсеКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьВсеКоллекции();
	Для Каждого КлючЗначение Из ВсеКоллекции Цикл
		Если ПустаяСтрока(КлючЗначение.Значение.ИдКоллекции) Тогда
			Продолжить;
		КонецЕсли;
		ИнфоОКоллекцияхСтрокой = ИнфоОКоллекцияхСтрокой
			+ СтрШаблон(ШаблонСтрокиКоллекции, КлючЗначение.Ключ, Строка(КлючЗначение.Значение.ИдКоллекции))
			+ Символы.ПС;
	КонецЦикла;
	ПараметрыШаблона.Добавить(ИнфоОКоллекцияхСтрокой);
	
	ИнформацияТехподдержки = СтрШаблон(ШаблонИнформации,
		ПараметрыШаблона[0],
		ПараметрыШаблона[1],
		ПараметрыШаблона[2]);
	
	Если ПользовательскиеОшибки Тогда
		
		ШаблонИнформации = НСтр("ru = '========================================
		|%1:
		|%2';
		|en = '========================================
		|%1:
		|%2'");
		ШаблонНекорректнойНастройки = НСтр("ru = 'Описание:
		|%1
		|Рекомендация:
		|%2';
		|en = 'Details:
		|%1
		|Recommendation:
		|%2'");
		
		НекорректныеНастройки = "";
		Разделитель = Символы.ПС + "---------------" + Символы.ПС;
		Для Каждого Ошибка Из Ошибки Цикл
			Если ПустаяСтрока(НекорректныеНастройки) Тогда
				НекорректныеНастройки = СтрШаблон(ШаблонНекорректнойНастройки, Ошибка.Описание, Ошибка.Рекомендация);
			Иначе
				НекорректныеНастройки = НекорректныеНастройки
					+ Разделитель
					+ СтрШаблон(ШаблонНекорректнойНастройки, Ошибка.Описание, Ошибка.Рекомендация);
			КонецЕсли;
		КонецЦикла;
		
		Заголовок = ?(ЕстьОшибкиНастроекВидаПлана,
			НСтр("ru = 'Некорректные настройки сервиса и модели';
				|en = 'Some service settings and models'"),
			НСтр("ru = 'Некорректные настройки сервиса';
				|en = 'Incorrect service settings'"));
		ИнформацияТехподдержки = ИнформацияТехподдержки
			+ СтрШаблон(ШаблонИнформации, Заголовок, НекорректныеНастройки);
		
		Если ЕстьОшибкиНастроекВидаПлана Тогда
			ИнформацияТехподдержки = ИнформацияТехподдержки + Символы.ПС;
			ШаблонМодели = НСтр("ru = '========================================
			|Настройки модели у вида плана ""%1"" с некорректными настройками (могут отличаться от данных на сервере):
			|%2';
			|en = '========================================
			|Model settings for the ""%1"" plan profile with incorrect settings (may differ from the data on the server):
			|%2'");
			ИнформацияТехподдержки = ИнформацияТехподдержки
				+ СтрШаблон(ШаблонМодели, Строка(ВидПлана), ИнформацияНастроекПроблемногоВидаПлана());
		КонецЕсли;
		
	Иначе // Техническая ошибка по запросу.
		
		ШаблонИнформации = НСтр("ru = '========================================
		|Техническая информация по ошибке запроса:
		|%1
		|Текст ошибки:
		|%2';
		|en = '========================================
		|Technical information on the query error:
		|%1
		|Error text:
		|%2'");
		ТекущаяОшибка = НастройкиСервиса.ТекущаяОшибка;
		
		ИнформацияПоОшибке = ТекущаяОшибка.Метод + " " + ТекущаяОшибка.Адрес;
		Если ЗначениеЗаполнено(ТекущаяОшибка.ВидПлана) Тогда
			// Ошибка по запросу модели конкретного вида плана.
			ИдМодели = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяОшибка.ВидПлана, "ИдентификаторМоделиПрогнозирования");
			ИнформацияПоОшибке = ИнформацияПоОшибке
				+ Символы.ПС + "modelId: " + ИдМодели;
		ИначеЕсли ЗначениеЗаполнено(ТекущаяОшибка.Коллекция) Тогда
			ИмяКоллекции = СоответствиеИменКоллекцийСервиса(ТекущаяОшибка.Коллекция);
			ИнформацияПоОшибке = ИнформацияПоОшибке
				+ Символы.ПС + "DataName: " + ИмяКоллекции;
		КонецЕсли;
		Если Не ПустаяСтрока(ТекущаяОшибка.Тело) Тогда
			ИнформацияПоОшибке = ИнформацияПоОшибке + Символы.ПС + ТекущаяОшибка.Тело;
		КонецЕсли;
		
		ИнформацияТехподдержки = ИнформацияТехподдержки
			+ СтрШаблон(ШаблонИнформации, ИнформацияПоОшибке, ТекущаяОшибка.ТекстОшибки) + Символы.ПС;
		
	КонецЕсли;
	
	// Добавление дополнительной информации по статусам, истории и ошибкам обмена данными.
	ИнформацияТехподдержки = ИнформацияТехподдержки + ИнформацияОбменаДанными();
	
	Возврат ИнформацияТехподдержки;
	
КонецФункции

&НаСервере
Функция СоответствиеИменКоллекцийСервиса(Коллекция)
	
	Если Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи Тогда
		Возврат "sales";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары Тогда
		Возврат "products";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ХарактеристикиНоменклатуры Тогда
		Возврат "product_variants";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Покупатели Тогда
		Возврат "customers";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Склады Тогда
		Возврат "shops";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Остатки Тогда
		Возврат "stock";
	ИначеЕсли Коллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.Промо Тогда
		Возврат "promo";
	Иначе
		Возврат "";
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ИнформацияНастроекПроблемногоВидаПлана()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыПланов.ТипПлана                                       КАК ТипПлана,
	|	ВидыПланов.ИдентификаторМоделиПрогнозирования             КАК ИдентификаторМоделиПрогнозирования,
	|	ВидыПланов.ЗаполнятьПоХарактеристикамНоменклатуры         КАК ЗаполнятьПоХарактеристикамНоменклатуры,
	|	ВидыПланов.ЗаполнятьСкладВТЧ                              КАК ЗаполнятьСклад,
	|	ВидыПланов.ЗаполнятьПартнераВТЧ                           КАК ЗаполнятьПартнера,
	|	ВидыПланов.АвтоматическиОбновлятьПрогноз                  КАК АвтоматическиОбновлятьПрогноз,
	|	ВидыПланов.ПериодичностьОбновленияПрогноза                КАК ПериодичностьОбновленияПрогноза,
	|	ВидыПланов.КоличествоОбновленийЗаПериод                   КАК КоличествоОбновленийЗаПериод,
	// Настройки, устанавливаемые в конфигурации модели.
	|	ВЫБОР
	|		КОГДА ВидыПланов.Периодичность = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА ""day""
	|		КОГДА ВидыПланов.Периодичность =  ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА ""week""
	|		КОГДА ВидыПланов.Периодичность =  ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА ""month""
	|		ИНАЧЕ ""wrong""
	|	КОНЕЦ                                                КАК dateGranularity,
	|	ВидыПланов.ДеньНеделиНачалаПрогноза                  КАК startWeekDay,
	|	ВидыПланов.КоличествоПериодов                        КАК horizon,
	|	ВЫБОР
	|		КОГДА ВидыПланов.СглаживаниеВыбросовИсторическихДанных = 0
	|			ТОГДА ""no""
	|		КОГДА ВидыПланов.СглаживаниеВыбросовИсторическихДанных = 1
	|			ТОГДА ""bound""
	|		ИНАЧЕ ""no""
	|	КОНЕЦ                                                КАК smoothingType,
	|	ВидыПланов.ВерхняяГраницаВыброса                     КАК stdUpperBound,
	|	ВидыПланов.НижняяГраницаВыброса                      КАК stdLowerBound,
	|	ВидыПланов.РассчитыватьОтклонениеПоСезоннымЗначениям КАК stdCalculatingType,
	|	ВидыПланов.НачалоПрогнозирования                     КАК dtStartForecast,
	|	ВидыПланов.МетрикаОценкиКачестваПрогноза             КАК userMetric,
	|	ВидыПланов.ВзвешиваниеОбъектовПриПодсчетеМетрики     КАК usedForMetricWeighing
	|ИЗ
	|	Справочник.ВидыПланов КАК ВидыПланов
	|ГДЕ
	|	ВидыПланов.Ссылка = &ВидПлана";
	
	Запрос.УстановитьПараметр("ВидПлана", ВидПлана);
	Запрос.УстановитьПараметр("ТекстНеУчитывать",               НСтр("ru = 'Не учитывать';
																	|en = 'Do not record'"));
	Запрос.УстановитьПараметр("ТекстВосстанавливатьПоЗаказам",  НСтр("ru = 'Восстанавливать по заказам';
																	|en = 'Restore by orders'"));
	Запрос.УстановитьПараметр("ТекстВосстанавливатьПоОстаткам", НСтр("ru = 'Восстанавливать по остаткам';
																	|en = 'Restore by balance'"));
	
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СервисПрогнозирования.ПредставитьТаблицуСтрокой(ТаблицаРезультата, Ложь) + Символы.ПС;
	
КонецФункции

&НаСервере
Функция ИнформацияОбменаДанными()
	
	ПараметрыШаблона = Новый Массив;
	
	ШаблонИнформацииОбмена = НСтр("ru = '========================================
	|Статус обмена данными:
	|%1
	|========================================
	|История обменов данными:
	|%2
	|========================================
	|Ошибки обмена данными:
	|%3';
	|en = '========================================
	|Data exchange status:
	|%1
	|========================================
	|Data exchange history:
	|%2
	|========================================
	|Data exchange errors:
	|%3'");
	
	// Статус обмена данными.
	СтатусыОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	СтатусыОбменаДаннымиСборка = Новый Массив();
	
	ШаблонСтрокиПараметровМодели = НСтр("ru = '%1: %2';
										|en = '%1: %2'");
	Для Каждого КлючЗначение Из СтатусыОбменаДанными Цикл
		СтатусыОбменаДаннымиСтрока = СтрШаблон(ШаблонСтрокиПараметровМодели, КлючЗначение.Ключ, КлючЗначение.Значение);
		СтатусыОбменаДаннымиСборка.Добавить(СтатусыОбменаДаннымиСтрока);
	КонецЦикла;
	СтатусыОбменаДаннымиСтрокой = СтрСоединить(СтатусыОбменаДаннымиСборка, Символы.ПС);
	
	ПараметрыШаблона.Добавить(СтатусыОбменаДаннымиСтрокой);
	
	// История обменов данными.
	ИнформацияОПоследнихЗаписях = РегистрыСведений.ЖурналСервисаПрогнозирования.ПолнаяИнформацияОПоследнихЗаписях(10);
	ИнформацияОПоследнихЗаписяхСтрокой = СервисПрогнозирования.ПредставитьТаблицуСтрокой(ИнформацияОПоследнихЗаписях, Истина);
	ПараметрыШаблона.Добавить(ИнформацияОПоследнихЗаписяхСтрокой);
	
	// Ошибки обмена данными.
	ИнформацияОПоследнихЗаписях = РегистрыСведений.ЖурналСервисаПрогнозирования.ПолнаяИнформацияОПоследнихЗаписях(5, Истина);
	ИнформацияОПоследнихЗаписяхСтрокой = СервисПрогнозирования.ПредставитьТаблицуСтрокой(ИнформацияОПоследнихЗаписях, Истина);
	ПараметрыШаблона.Добавить(ИнформацияОПоследнихЗаписяхСтрокой);
	
	Возврат СтрШаблон(ШаблонИнформацииОбмена,
		ПараметрыШаблона[0],
		ПараметрыШаблона[1],
		ПараметрыШаблона[2]);
	
КонецФункции

&НаСервере
Функция ТекстОтсутствиеОжидаемойДетализацииПолеОтмечено(Коллекция, ВложенноеПоле = "Код детализации")
	
	ТекстШаблона = НСтр("ru = 'Вложенное поле ""%1"" у коллекции ""%2"" отмечено к выгрузке. Заново запустите полную выгрузку данных для передачи детализации в сервис';
						|en = 'The nested ""%1"" field of the ""%2"" collection is marked for export. To send the details to the service, restart the full data export'");
	Возврат СтрШаблон(ТекстШаблона, ВложенноеПоле, Коллекция);
	
КонецФункции

&НаСервере
Процедура СброситьРазмерыИПоложениеОкна()
	
	ИмяПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().Имя;
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ПолноеИмяФормы = Метаданные.Обработки.ПанельУправленияСервисомПрогнозирования.Формы.ОтчетПоПроблемам.ПолноеИмя();
		ХранилищеСистемныхНастроек.Удалить(ПолноеИмяФормы, "", ИмяПользователя);
	КонецЕсли;
	КлючСохраненияПоложенияОкна = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ТипЗнч(ИдентификаторЗадания) = Тип("Строка") Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(Новый УникальныйИдентификатор(ИдентификаторЗадания));
	Иначе
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#Область Подключаемые

&НаКлиенте
Процедура ПодключитьОжиданиеФоновогоЗадания(РезультатФоновогоЗадания)
	
	Если РезультатФоновогоЗадания <> Неопределено Тогда
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ИсправитьОшибкиОповещение", ЭтотОбъект);
	
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатФоновогоЗадания, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьОшибкиОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ИсправитьОшибкиНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсправитьОшибкиНаСервере()
	
	ИдентификаторЗаданияИсправитьОшибки = "";
	РезультатИсправления = ПолучитьИзВременногоХранилища(АдресХранилищаИсправитьОшибки);
	
	ОбновитьСтатусИсправленияОшибок(РезультатИсправления.Ошибки);
	ОбщегоНазначения.СообщитьПользователю(РезультатИсправления.ТекстИсправлений);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
