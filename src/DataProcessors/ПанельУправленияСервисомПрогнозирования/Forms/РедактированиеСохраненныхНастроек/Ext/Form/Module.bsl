
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДеревоНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоНастроек

&НаКлиенте
Процедура ДеревоНастроекКлючПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ДеревоНастроек.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекСтрока.ТипЗначенияКлюча = Строка(ТипЗнч(ТекСтрока.Ключ));
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНастроекЗначениеПриИзменении(Элемент)
	
	ТекСтрока = Элементы.ДеревоНастроек.ТекущиеДанные;
	Если ТекСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ТекСтрока.ТипЗначенияЗначения = Строка(ТипЗнч(ТекСтрока.Значение));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Перечитать(Команда)
	ДеревоНастроек.ПолучитьЭлементы().Очистить();
	ЗаполнитьДеревоНастроек();
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	ТекстВопроса =НСтр("ru = 'Перезапись настроек может привести к ошибкам. Продолжить?';
						|en = 'Overwriting the settings can lead to errors. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ЗаписатьЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПолныйСбросНастроек(Команда)
	ТекстВопроса = НСтр("ru = 'Будет восстановлена структура настроек по умолчанию.
		|При этом все настройки будут удалены. Продолжить?';
		|en = 'The default setting structure will be restored.
		|All settings will be deleted. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ПолныйСбросНастроекЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьОшибочныеНастройки(Команда)
	ТекстВопроса = НСтр("ru = 'Для настроек, имеющих тип отличный от ожидаемого, будет восстановлено состояние по умолчанию.
		|Текущие значения таких настроек будут удалены. Продолжить?';
		|en = 'The settings that are not of the expected type will be restored to their default state.
		|The current values of such settings will be deleted. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ВосстановитьОшибочныеНастройкиЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоНастроек()
	
	СохраненныеНастройки = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	ЗаполнитьДеревоНастроекРекурсивно(СохраненныеНастройки);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоНастроекРекурсивно(СохраненныеНастройки, Родитель = Неопределено)
	
	Для Каждого КлючЗначение Из СохраненныеНастройки Цикл
		
		Если Родитель = Неопределено Тогда
			ТекущаяСтрокаГруппа = ДеревоНастроек.ПолучитьЭлементы().Добавить();
		Иначе
			ТекущаяСтрокаГруппа = Родитель.ПолучитьЭлементы().Добавить();
		КонецЕсли;
		
		Ключ            = "";
		Значение        = "";
		ТипКлючЗначение = ТипЗнч(КлючЗначение);
		
		Если ТипКлючЗначение = Тип("СправочникСсылка.Пользователи") Тогда
			Ключ                = "";
			Значение            = КлючЗначение;
			ТипЗначенияКлюча    = "";
			ТипЗначенияЗначения = Строка(ТипКлючЗначение);
		Иначе
			Ключ                = КлючЗначение.Ключ;
			Значение            = КлючЗначение.Значение;
			ТипЗначенияКлюча    = Строка(ТипЗнч(Ключ));
			ТипЗначенияЗначения = Строка(ТипЗнч(Значение));
		КонецЕсли;
		
		ТекущаяСтрокаГруппа.Ключ                = Ключ;
		ТекущаяСтрокаГруппа.ТипЗначенияКлюча    = ТипЗначенияКлюча;
		ТекущаяСтрокаГруппа.ТипЗначенияЗначения = ТипЗначенияЗначения;
		
		Если ТипЗнч(Значение) = Тип("Структура") Тогда
			ЗаполнитьДеревоНастроекРекурсивно(Значение, ТекущаяСтрокаГруппа);
		ИначеЕсли ТипЗнч(Значение) = Тип("Соответствие") Тогда
			ЗаполнитьДеревоНастроекРекурсивно(Значение, ТекущаяСтрокаГруппа);
		ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
			ЗаполнитьДеревоНастроекРекурсивно(Значение, ТекущаяСтрокаГруппа);
		Иначе
			ТекущаяСтрокаГруппа.Значение = Значение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ПреобразоватьДеревоВСтруктуру()
	НовыеНастройки = ПреобразоватьДеревоВСтруктуруРекурсивно(ДеревоНастроек);
	Возврат НовыеНастройки;
КонецФункции

&НаСервере
Функция ПреобразоватьДеревоВСтруктуруРекурсивно(ЭлементДерева, ЭтоСоответствие = Ложь, ЭтоМассив = Ложь)
	
	Если ЭтоСоответствие Тогда
		НовыеНастройкиЛокальные = Новый Соответствие();
	ИначеЕсли ЭтоМассив Тогда
		НовыеНастройкиЛокальные = Новый Массив;
	Иначе
		НовыеНастройкиЛокальные = Новый Структура();
	КонецЕсли;
	
	ЭлементыДерева = ЭлементДерева.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из ЭлементыДерева Цикл
		
		Если ЭтоМассив Тогда
			НовыеНастройкиЛокальные.Добавить(СтрокаДерева.Значение);
			Продолжить;
		КонецЕсли;
		
		Если СтрокаДерева.ТипЗначенияЗначения = Строка(Тип("Массив")) Тогда
			Значение = ПреобразоватьДеревоВСтруктуруРекурсивно(СтрокаДерева, Ложь, Истина);
		ИначеЕсли СтрокаДерева.ТипЗначенияЗначения = Строка(Тип("Структура")) Тогда
			Значение = ПреобразоватьДеревоВСтруктуруРекурсивно(СтрокаДерева);
		ИначеЕсли СтрокаДерева.ТипЗначенияЗначения = Строка(Тип("Соответствие")) Тогда
			Значение = ПреобразоватьДеревоВСтруктуруРекурсивно(СтрокаДерева, Истина);
		Иначе
			Значение = СтрокаДерева.Значение;
		КонецЕсли;
		
		НовыеНастройкиЛокальные.Вставить(СтрокаДерева.Ключ, Значение);
		
	КонецЦикла;
	
	Возврат НовыеНастройкиЛокальные;
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьЗавершение(Результат, ПараметрКоманды) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	НовыеНастройки = ПреобразоватьДеревоВСтруктуру();
	СервисПрогнозирования.ОбновитьНастройкиСервиса(НовыеНастройки);
КонецПроцедуры

&НаКлиенте
Процедура ПолныйСбросНастроекЗавершение(Результат, ПараметрКоманды) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ПолныйСбросНастроекНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолныйСбросНастроекНаСервере()
	СервисПрогнозирования.СброситьНастройкиСервиса();
	ЗаполнитьДеревоНастроек();
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьОшибочныеНастройкиЗавершение(Результат, ПараметрКоманды) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ВосстановитьОшибочныеНастройкиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ВосстановитьОшибочныеНастройкиНаСервере()
	СервисПрогнозирования.ВосстановитьНастройкиСервиса();
КонецПроцедуры

#КонецОбласти

