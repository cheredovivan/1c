
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИнфоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(
		Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары);
	ИдентификаторКоллекцииТоваров = СтрЗаменить(ИнфоКоллекции.ИдКоллекции, Символы.НПП, "");
	
	ЗаполнитьСписокВыгружаемыхКоллекций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжиданияИндикатораВыгрузки();
	ОбновитьИнформациюОВыгрузкеКоллекций();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПланаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВидПлана) Тогда
		ВидПланаПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолныйСбросНастроек(Команда)
	ТекстВопроса = НСтр("ru = 'Будет выполнен полный сброс настроек. Продолжить?';
						|en = 'All the settings will be reset. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ПолныйСбросНастроекЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура ПолнаяВыгрузкаМягкая(Команда)
	ТекстВопроса = НСтр("ru = 'Будет выполнена полная выгрузка данных. Продолжить?';
						|en = 'Full data export will be performed. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ПолнаяВыгрузкаМягкаяЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура ПолнаяВыгрузкаЖесткая(Команда)
	ТекстВопроса = НСтр("ru = 'Будет выполнена полная выгрузка данных с предварительным удалением на сервере. Продолжить?';
						|en = 'All data will be deleted on the server and exported. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ПолнаяВыгрузкаЖесткаяЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписокВыгружаемыхКоллекций(Команда)
	ОбновитьСписокВыгружаемыхКоллекцийНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КоллекцииНаСервереСнятьВыделение(Команда)
	КоллекцииНаСервереСнятьВыделениеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура КоллекцииНаСервереВыделитьВсе(Команда)
	КоллекцииНаСервереВыделитьВсеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСоставКоллекций(Команда)
	ОбновитьСоставКоллекцийНаСервере();
	Если Не ПустаяСтрока(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжиданияФоновогоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВыбранныеКоллекции(Команда)
	ОчиститьВыбранныеКоллекцииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УдалитьВыбранныеКоллекции(Команда)
	УдалитьВыбранныеКоллекцииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТочностьМодели(Команда)
	ОчиститьСообщения();
	ОбновитьТочностьМоделиНаСервере();
	Если Не ПустаяСтрока(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжиданияФоновогоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТочностьМоделиПланФактПоВсемПериодам(Команда)
	ОчиститьСообщения();
	ОбновитьТочностьМоделиПланФактПоВсемПериодамНаСервере();
	Если Не ПустаяСтрока(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжиданияФоновогоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТочностьМоделиПланФактПоПериодам(Команда)
	ОчиститьСообщения();
	ОбновитьТочностьМоделиПланФактПоПериодамНаСервере();
	Если Не ПустаяСтрока(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжиданияФоновогоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИсториюОбучения(Команда)
	ОчиститьСообщения();
	ОбновитьИсториюОбученияНаСервере();
	Если Не ПустаяСтрока(ИдентификаторЗадания) Тогда
		ПодключитьОбработчикОжиданияФоновогоЗадания();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПрерватьВыгрузку(Команда)
	ТекстВопроса = НСтр("ru = 'Текущая выгрузка будет прервана. Выгруженные данные останутся на сервере. Продолжить?';
						|en = 'The current export will be interrupted. The exported data will remain on the server. Continue?'");
	Оповещение = Новый ОписаниеОповещения("ПрерватьВыгрузкуЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьСбросНастроек(Команда)
	Элементы.ПолныйСбросНастроек.Доступность = Не Элементы.ПолныйСбросНастроек.Доступность;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	НеВыполняетсяЗапросКСервису = ПустаяСтрока(ИдентификаторЗадания);
	Элементы.КоллекцииНаСервереОбновитьСоставКоллекций.Доступность = НеВыполняетсяЗапросКСервису;
	Элементы.ОбновитьИсториюОбучения.Доступность = НеВыполняетсяЗапросКСервису;
	Элементы.ОбновитьТочностьМодели.Доступность = НеВыполняетсяЗапросКСервису;
	Элементы.ОбновитьТочностьМоделиПланФактПоВсемПериодам.Доступность = НеВыполняетсяЗапросКСервису;
	Элементы.ОбновитьТочностьМоделиПланФактПоПериодам.Доступность = НеВыполняетсяЗапросКСервису;
	
	// История обучения.
	ПрогнозТолькоПоКатегориям = ПрогнозПоКатегориям И ВариантПрогнозированияПоКатегориям = 0;
	Элементы.ИсторияОбученияФильтрПоТовару.Видимость         = ?(ПрогнозТолькоПоКатегориям, Ложь, Истина);
	Элементы.ИсторияОбученияФильтрПоХарактеристике.Видимость = ?(ПрогнозПоКатегориям, Ложь, Истина);
	Элементы.ГруппаФильтрацияПоКатегории.Видимость           = ?(ПрогнозПоКатегориям, Истина, Ложь);
	Элементы.ИсторияОбученияФильтрПоПартнеру.Видимость       = ?(ПрогнозПоКатегориям, Ложь, Истина);
	
	// Прогнозы.
	Элементы.КачествоМоделейПрогнозированияПоПериодамСквозноеТрендовоеРазложение.Видимость = ПрогнозПоМесяцам;
	
КонецПроцедуры

&НаСервере
Функция СписокКоллекций()
	
	Массив = Новый Массив();
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.ХарактеристикиНоменклатуры);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Покупатели);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Склады);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Остатки);
	Массив.Добавить(Перечисления.КоллекцииСервисаПрогнозированияПродаж.Промо);
	
	Возврат Массив;
	
КонецФункции

&НаСервере
Процедура ВидПланаПриИзмененииНаСервере()
	
	РеквизитыВидаПлана = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидПлана,
		"ТипПлана, Периодичность, ВариантПрогнозированияПоКатегориям");
	ПрогнозПоМесяцам                   = РеквизитыВидаПлана.Периодичность = Перечисления.Периодичность.Месяц;
	ПрогнозПоКатегориям                = РеквизитыВидаПлана.ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям;
	ВариантПрогнозированияПоКатегориям = РеквизитыВидаПлана.ВариантПрогнозированияПоКатегориям;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолныйСбросНастроекЗавершение(Результат, ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ПолныйСбросНастроекНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПолныйСбросНастроекНаСервере()
	
	НачатьТранзакцию();
		
	Попытка
	
		СервисПрогнозирования.СброситьНастройкиСервиса();
		
		СхемаКомпоновкиДанныхВыгрузка = ПолучитьОбщийМакет("СервисПрогнозированияФильтрОтправляемыхДанных");
		СхемаКомпоновкиДанныхЗагрузка = ПолучитьОбщийМакет("СервисПрогнозированияФильтрЗагрузкиПрогноза");
		
		КомпоновщикВыгрузки = Новый КомпоновщикНастроекКомпоновкиДанных();
		КомпоновщикВыгрузки.ЗагрузитьНастройки(СхемаКомпоновкиДанныхВыгрузка.НастройкиПоУмолчанию);
		КомпоновщикВыгрузки.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		НастройкиОтборов = Новый Структура();
		НастройкиОтборов.Вставить("ОбщийОтборВыгрузки", КомпоновщикВыгрузки.Настройки);
		ХранилищеПараметров = Новый ХранилищеЗначения(НастройкиОтборов);
	
		Константы.НастройкиОтборовНаВыгрузкуСервисаПрогнозирования.Установить(ХранилищеПараметров);
		
		КомпоновщикЗагрузки = Новый КомпоновщикНастроекКомпоновкиДанных();
		КомпоновщикЗагрузки.ЗагрузитьНастройки(СхемаКомпоновкиДанныхЗагрузка.НастройкиПоУмолчанию);
		КомпоновщикЗагрузки.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		НастройкиОтборов = Новый Структура();
		НастройкиОтборов.Вставить("ОбщийОтборЗагрузки", КомпоновщикЗагрузки.Настройки);
		
		ХранилищеПараметров = Новый ХранилищеЗначения(НастройкиОтборов);
	
		Константы.НастройкиОтборовНаЗагрузкуСервисаПрогнозирования.Установить(ХранилищеПараметров);
		
		Набор = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.СоздатьНаборЗаписей();
		Набор.Записать();
		
		Для Каждого Коллекция Из СписокКоллекций() Цикл
			НаборЗаписей = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Коллекция.Установить(Коллекция);
			
			НоваяЗапись = НаборЗаписей.Добавить(); // В строке остаются значения по умолчанию.
			НоваяЗапись.Коллекция = Коллекция;
			НаборЗаписей.Записать();
		КонецЦикла;
		
		НаборЗаписей = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторЗаписи.Установить("1");
		НоваяЗапись = НаборЗаписей.Добавить(); // В строке остаются значения по умолчанию.
		НоваяЗапись.ИдентификаторЗаписи = "1";
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
			
		ОтменитьТранзакцию();
		
		ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ШаблонСообщения = НСтр("ru = 'Не удалось выполнить сброс настроек по причине: %1';
								|en = 'Cannot reset settings. Reason: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ПредставлениеОшибки);
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Ошибка,
			Неопределено, 
			Неопределено, 
			ТекстСообщения);
		
	КонецПопытки;
		
КонецПроцедуры

&НаКлиенте
Процедура ПолнаяВыгрузкаМягкаяЗавершение(Результат, ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ОчиститьСообщения();
	ПолнаяВыгрузкаМягкаяНаСервере();
	Оповестить("ЗапускВыгрузкиДанныхВСервисПрогнозирования");
	
КонецПроцедуры

&НаСервере
Процедура ПолнаяВыгрузкаМягкаяНаСервере()
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	Если Не ЗначениеЗаполнено(НастройкиСервиса.ТокенПриложения)
		Или Не НастройкиСервиса.СтатусПодключения = СервисПрогнозирования.СтатусПодключенияАктивен() Тогда
		ТекстСообщенияОбОшибке = ТекстОшибкиАвторизацияНеНайдена();
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат;
	КонецЕсли;
	
	КоллекцииКВыгрузке.Очистить();
	Для Каждого Коллекция Из ВыгружаемыеКоллекции Цикл
		Если Не Коллекция.Пометка Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеКоллекции = НастройкиСервиса.Коллекции[Коллекция.Значение];
		Если Не ОписаниеКоллекции.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		КоллекцииКВыгрузке.Добавить(Коллекция.Значение);
		
		СводкаПоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(
			ОписаниеКоллекции.ИмяВИсточнике);
		
		ИдКоллекции          = СтрЗаменить(СводкаПоКоллекции.ИдКоллекции, " ", "");
		КоллекцияВыгружалась = Не ПустаяСтрока(ИдКоллекции)
			И Число(ИдКоллекции) > 1
			И СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки > Дата(1,1,1);
		Если КоллекцияВыгружалась
			И Не ОписаниеКоллекции.Категориальный Тогда
			// При выгрузке будет автоматически сброшена дата актуальности.
			СводкаПоКоллекции.ИзмененыОтборы = Истина;
			РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(
				ОписаниеКоллекции.ИмяВИсточнике, СводкаПоКоллекции);
		КонецЕсли;
	КонецЦикла;
	
	Если КоллекцииКВыгрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	РезультатРасчета = ДлительныеОперации.ВыполнитьВФоне("СервисПрогнозирования.ВыгрузитьКоллекции",
	                                                     КоллекцииКВыгрузке.ВыгрузитьЗначения(),
	                                                     ПараметрыВыполнения);
	
	ОбновитьИнформациюОВыгрузкеКоллекций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолнаяВыгрузкаЖесткаяЗавершение(Результат, ПараметрКоманды) Экспорт
	
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ОчиститьСообщения();
	ПолнаяВыгрузкаЖесткаяНаСервере();
	Оповестить("ЗапускВыгрузкиДанныхВСервисПрогнозирования");
	
КонецПроцедуры

&НаСервере
Процедура ПолнаяВыгрузкаЖесткаяНаСервере()
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	Если Не ЗначениеЗаполнено(НастройкиСервиса.ТокенПриложения)
		Или Не НастройкиСервиса.СтатусПодключения = СервисПрогнозирования.СтатусПодключенияАктивен() Тогда
		ТекстСообщенияОбОшибке = ТекстОшибкиАвторизацияНеНайдена();
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщенияОбОшибке);
		Возврат;
	КонецЕсли;
	
	КоллекцииКВыгрузке.Очистить();
	Для Каждого Коллекция Из ВыгружаемыеКоллекции Цикл
		Если Не Коллекция.Пометка Тогда
			Продолжить;
		КонецЕсли;
		ОписаниеКоллекции = НастройкиСервиса.Коллекции[Коллекция.Значение];
		Если Не ОписаниеКоллекции.Выгружать Тогда
			Продолжить;
		КонецЕсли;
		КоллекцииКВыгрузке.Добавить(Коллекция.Значение);
		
		СводкаПоКоллекции = РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(
			ОписаниеКоллекции.ИмяВИсточнике);
		
		ИдКоллекции          = СтрЗаменить(СводкаПоКоллекции.ИдКоллекции, " ", "");
		КоллекцияВыгружалась = Не ПустаяСтрока(ИдКоллекции)
			И Число(ИдКоллекции) > 1
			И СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки > Дата(1,1,1);
		Если КоллекцияВыгружалась Тогда
			// Коллекция будет удалена на сервере в методе СоздатьКоллекции.
			СводкаПоКоллекции.ИзмененФормат = Истина;
			Если Не ОписаниеКоллекции.Категориальный Тогда
				// При выгрузке будет автоматически сброшена дата актуальности.
				СводкаПоКоллекции.ИзмененыОтборы = Истина;
			КонецЕсли;
			РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ЗаписатьИнформациюКоллекции(
				ОписаниеКоллекции.ИмяВИсточнике, СводкаПоКоллекции);
		КонецЕсли;
	КонецЦикла;
	
	Если КоллекцииКВыгрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Предварительная установка флага начала выгрузки до вызова "ВыгрузитьКоллекции" для контроля наличия выгрузки.
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	СводкаОбменаДанными.ОбменДаннымиАктивен = Истина;
	СводкаОбменаДанными.ЗапланированоВыгрузитьКоллекций = 0;
	СводкаОбменаДанными.ВыгруженоОбъектовКоллекции = 0;
	СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции = 0;
	СводкаОбменаДанными.ВыгружаемаяСейчасКоллекция = Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
	РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
	
	РегистрыСведений.ЖурналСервисаПрогнозирования.ПроверитьОчиститьЖурналСервисаПрогнозирования();
	
	СервисПрогнозирования.СоздатьКоллекции(КоллекцииКВыгрузке.ВыгрузитьЗначения());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	РезультатРасчета = ДлительныеОперации.ВыполнитьВФоне("СервисПрогнозирования.ВыгрузитьКоллекции",
	                                                     КоллекцииКВыгрузке.ВыгрузитьЗначения(),
	                                                     ПараметрыВыполнения);
	
	ОбновитьИнформациюОВыгрузкеКоллекций();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокВыгружаемыхКоллекцийНаСервере()
	ЗаполнитьСписокВыгружаемыхКоллекций();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыгружаемыхКоллекций()
	
	ВыгружаемыеКоллекции.Очистить();
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	Для Каждого КлючЗначениеКоллекции Из НастройкиСервиса.Коллекции Цикл
		ОписаниеКоллекции = КлючЗначениеКоллекции.Значение;
		Если ОписаниеКоллекции.Выгружать Тогда
			НоваяСтрока = ВыгружаемыеКоллекции.Добавить();
			НоваяСтрока.Значение = КлючЗначениеКоллекции.Ключ;
			НоваяСтрока.Пометка = Ложь;
			Если РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.КоллекцияВыгружалась(КлючЗначениеКоллекции.Ключ) Тогда
				СводкаПоКоллекции= РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(КлючЗначениеКоллекции.Ключ);
				ШаблонПредставления = НСтр("ru = '%1, выгрузка начата: %2, последняя порция выгружена: %3, выгружено: %4 записей, принято сервером: %5 записей';
											|en = '%1, export is started: %2, last batch is exported: %3, exported: %4 records, accepted by the server: %5 records'");
				НоваяСтрока.Представление = СтрШаблон(ШаблонПредставления, КлючЗначениеКоллекции.Значение.Представление,
					СводкаПоКоллекции.ДатаНачалаПоследнейВыгрузки,
					СводкаПоКоллекции.ДатаПоследнейВыгрузки,
					СводкаПоКоллекции.КоличествоДанныхВыгруженное,
					СводкаПоКоллекции.КоличествоДанныхНаСервере);
			Иначе
				ШаблонПредставления = НСтр("ru = '%1, выгрузка не проводилась';
											|en = '%1, export was not performed'");
				НоваяСтрока.Представление = СтрШаблон(ШаблонПредставления, КлючЗначениеКоллекции.Значение.Представление);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура КоллекцииНаСервереВыделитьВсеНаСервере()
	Для Каждого Строка Из КоллекцииНаСервере Цикл
		Строка.Пометка = Истина;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура КоллекцииНаСервереСнятьВыделениеНаСервере()
	Для Каждого Строка Из КоллекцииНаСервере Цикл
		Строка.Пометка = Ложь;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура ОбновитьСоставКоллекцийНаСервере()
	
	ИмяМетода = ИмяМетодаПолучитьПереченьКоллекций();
	ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода);
	
	Если ПустаяСтрока(ИдентификаторЗадания) Тогда
		ОбновитьСоставКоллекцийНаСервереФинальный();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСоставКоллекцийНаСервереФинальный()
	
	СписокКоллекций = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	КоллекцииНаСервере.Очистить();
	
	ОписаниеКоллекций = СервисПрогнозированияПереопределяемый.ПолучитьОписаниеРеквизитовВсехКоллекций();
	Если ТипЗнч(СписокКоллекций.ДесериализованноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Строка Из СписокКоллекций.ДесериализованноеЗначение Цикл
		НоваяСтрока = КоллекцииНаСервере.Добавить();
		НоваяСтрока.ИмяВСервисе = Строка["name"];
		НоваяСтрока.ИдентификаторКоллекции = Строка["id"];
		
		Для Каждого ОписаниеКоллекции Из ОписаниеКоллекций Цикл
			Если ОписаниеКоллекции.Значение.ИмяВСервисе = НоваяСтрока.ИмяВСервисе Тогда
				НоваяСтрока.ПредставлениеКоллекции = ОписаниеКоллекции.Значение.Представление;
				НоваяСтрока.ИмяВИсточнике = ОписаниеКоллекции.Значение.ИмяВИсточнике;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.КоллекцияВыгружалась(НоваяСтрока.ИмяВИсточнике) Тогда
			СводкаПоКоллекции= РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ПолучитьИнформациюКоллекции(НоваяСтрока.ИмяВИсточнике);
			Если СводкаПоКоллекции.ИдКоллекции = НоваяСтрока.ИдентификаторКоллекции Тогда
				НоваяСтрока.КоличествоВСервисеПриПоследнейВыгрузке = СводкаПоКоллекции.КоличествоДанныхНаСервере;
				НоваяСтрока.КоличествоВИсточникеПриПоследнейВыгрузке = СводкаПоКоллекции.КоличествоДанныхВыгруженное;
			КонецЕсли;
		КонецЕсли;
		
		ИнфоКоллекций = СервисПрогнозирования.ПолучитьИнфоКоллекцииНаСервере(НоваяСтрока.ИдентификаторКоллекции).ДесериализованноеЗначение;
		Если ТипЗнч(ИнфоКоллекций) = Тип("Соответствие") Тогда
			НоваяСтрока.КоличествоДанных = Число(ИнфоКоллекций["info"]["rowCount"]);
		КонецЕсли;
	КонецЦикла;
	
	КоллекцииНаСервере.Сортировать("ИмяВСервисе, ИдентификаторКоллекции");
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода, ПараметрыМетода = Неопределено)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Если ПараметрыМетода = Неопределено
		Или ПараметрыМетода.Количество() = 0 Тогда
		РезультатРасчета = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
			ИмяМетода);
	ИначеЕсли ПараметрыМетода.Количество() = 1 Тогда
		РезультатРасчета = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
			ИмяМетода,
			ПараметрыМетода[0]);
	ИначеЕсли ПараметрыМетода.Количество() = 2 Тогда
		РезультатРасчета = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
			ИмяМетода,
			ПараметрыМетода[0],
			ПараметрыМетода[1]);
	ИначеЕсли ПараметрыМетода.Количество() = 3 Тогда
		РезультатРасчета = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
			ИмяМетода,
			ПараметрыМетода[0],
			ПараметрыМетода[1],
			ПараметрыМетода[2]);
	КонецЕсли;
	ИдентификаторЗадания = РезультатРасчета.ИдентификаторЗадания;
	АдресРезультата = РезультатРасчета.АдресРезультата;
	ИмяМетодаФоновогоЗадания = ИмяМетода;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьВыбранныеКоллекцииНаСервере()
	
	Для Каждого Строка Из КоллекцииНаСервере Цикл
		Если Строка.Пометка
			И Строка.ИдентификаторКоллекции <> 0 Тогда
			ОчиститьУдалитьКоллекцию(Строка.ИдентификаторКоллекции);
		КонецЕсли;
	КонецЦикла;
	ОбновитьСоставКоллекцийНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура УдалитьВыбранныеКоллекцииНаСервере()
	
	Для Каждого Строка Из КоллекцииНаСервере Цикл
		Если Строка.Пометка
			И Строка.ИдентификаторКоллекции <> 0 Тогда
			
			ОчиститьУдалитьКоллекцию(Строка.ИдентификаторКоллекции, Истина);
			
			Коллекция = СоответствиеКоллекцийСервиса(Строка.ИмяВСервисе);
			РегистрыСведений.КоллекцииСервисаПрогнозированияПродаж.ОчиститьКоллекцию(Коллекция);
			
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьСоставКоллекцийНаСервере();
	
КонецПроцедуры

// Очистить или удалить коллекцию на сервере.
// 
// Параметры:
//  ИдКоллекции - Число - Ид коллекции
//  УдалитьКоллекцию - Булево - Удалить коллекцию
&НаСервере
Процедура ОчиститьУдалитьКоллекцию(ИдКоллекции, УдалитьКоллекцию = Ложь)
	
	СервисПрогнозирования.ОчиститьУдалитьКоллекцию(ИдКоллекции, УдалитьКоллекцию);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиНаСервере()
	Если ПроверитьВозможностьЗапросаКСервису() Тогда
		ИмяМетода = ИмяМетодаПолучитьТочностьМодели();
		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ВидПлана);
		ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода, ПараметрыМетода);
		
		Если ПустаяСтрока(ИдентификаторЗадания) Тогда
			ОбновитьТочностьМоделиНаСервереФинальный();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиНаСервереФинальный()
	
	СтруктураОтвета = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(СтруктураОтвета.ДесериализованноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	КачествоМоделейПрогнозирования.Очистить();
	
	Для Каждого ИсключенныйОбъект Из СтруктураОтвета.ДесериализованноеЗначение Цикл
		НоваяСтрока = КачествоМоделейПрогнозирования.Добавить();
		НоваяСтрока.ИмяМодели = ИсключенныйОбъект["model"];
		НоваяСтрока.СредняяАбсолютнаяОшибка = Число(ИсключенныйОбъект["MAE"]);
		НоваяСтрока.СредняяАбсолютнаяОшибкаФинал = Число(ИсключенныйОбъект["MAE_final"]);
		НоваяСтрока.СредняяАбсолютнаяОшибкаРяды = Число(ИсключенныйОбъект["MAE_series"]);
		НоваяСтрока.СредняяАбсолютнаяПроцентнаяОшибка = Число(ИсключенныйОбъект["MAPE"]);
		НоваяСтрока.СредняяАбсолютнаяПроцентнаяОшибкаФинал = Число(ИсключенныйОбъект["MAPE_final"]);
		НоваяСтрока.СредняяАбсолютнаяПроцентнаяОшибкаРяды = Число(ИсключенныйОбъект["MAPE_series"]);
		НоваяСтрока.СредняяНормированнаяАбсолютнаяПроцентнаяОшибка = Число(ИсключенныйОбъект["PMAPE"]);
		НоваяСтрока.СредняяНормированнаяАбсолютнаяПроцентнаяОшибкаФинал = Число(ИсключенныйОбъект["PMAPE_final"]);
		НоваяСтрока.СредняяНормированнаяАбсолютнаяПроцентнаяОшибкаРяды = Число(ИсключенныйОбъект["PMAPE_series"]);
		НоваяСтрока.СреднеквадратичноеОтклонение = Число(ИсключенныйОбъект["RMSE"]);
		НоваяСтрока.СреднеквадратичноеОтклонениеФинал = Число(ИсключенныйОбъект["RMSE_final"]);
		НоваяСтрока.СреднеквадратичноеОтклонениеРяды = Число(ИсключенныйОбъект["RMSE_series"]);
		НоваяСтрока.СредняяСимметричнаяАбсолютнаяПроцентнаяОшибка = Число(ИсключенныйОбъект["SMAPE"]);
		НоваяСтрока.СредняяСимметричнаяАбсолютнаяПроцентнаяОшибкаФинал = Число(ИсключенныйОбъект["SMAPE_final"]);
		НоваяСтрока.СредняяСимметричнаяАбсолютнаяПроцентнаяОшибкаРяды = Число(ИсключенныйОбъект["SMAPE_series"]);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиПланФактПоВсемПериодамНаСервере()
	Если ПроверитьВозможностьЗапросаКСервису() Тогда
		ИмяМетода = ИмяМетодаПолучитьТочностьМоделиПланФактПоВсемПериодам();
		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ВидПлана);
		ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода, ПараметрыМетода);
		
		Если ПустаяСтрока(ИдентификаторЗадания) Тогда
			ОбновитьТочностьМоделиПланФактПоВсемПериодамНаСервереФинальный();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиПланФактПоВсемПериодамНаСервереФинальный()
	
	СтруктураОтвета = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(СтруктураОтвета.ДесериализованноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	КачествоМоделейПрогнозированияПоВсемПериодам.Очистить();
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	Если ПрогнозПоКатегориям Тогда
		Если ВариантПрогнозированияПоКатегориям = 0 Тогда
			КлючОбъекта = "category_id_" + ИдентификаторКоллекцииТоваров; // "category_processing" = "group".
		Иначе
			КлючОбъекта = "item_id"; // "category_processing" = "reduce".
		КонецЕсли;
	Иначе
		КлючОбъекта = "item_id";
	КонецЕсли;
	
	Для Каждого ОбъектКачества Из СтруктураОтвета.ДесериализованноеЗначение Цикл
		НоваяСтрока = КачествоМоделейПрогнозированияПоВсемПериодам.Добавить();
		НоваяСтрока.ИмяМодели                                           = ОбъектКачества["model"];
		НоваяСтрока.СредняяАбсолютнаяОшибка                             = Число(ОбъектКачества["MAE"]);
		НоваяСтрока.СредняяАбсолютнаяОшибкаСумма                        = Число(ОбъектКачества["MAE_series"]);
		НоваяСтрока.СредняяАбсолютнаяПроцентнаяОшибка                   = Число(ОбъектКачества["MAPE"]);
		НоваяСтрока.СредняяАбсолютнаяПроцентнаяОшибкаСумма              = Число(ОбъектКачества["MAPE_series"]);
		НоваяСтрока.СредняяНормированнаяАбсолютнаяПроцентнаяОшибка      = Число(ОбъектКачества["PMAPE"]);
		НоваяСтрока.СредняяНормированнаяАбсолютнаяПроцентнаяОшибкаСумма = Число(ОбъектКачества["PMAPE_series"]);
		НоваяСтрока.СреднеквадратичноеОтклонение                        = Число(ОбъектКачества["RMSE"]);
		НоваяСтрока.СреднеквадратичноеОтклонениеСумма                   = Число(ОбъектКачества["RMSE_series"]);
		НоваяСтрока.СредняяСимметричнаяАбсолютнаяПроцентнаяОшибка       = Число(ОбъектКачества["SMAPE"]);
		НоваяСтрока.СредняяСимметричнаяАбсолютнаяПроцентнаяОшибкаСумма  = Число(ОбъектКачества["SMAPE_series"]);
		
		НоваяСтрока.Партнер = Справочники.Партнеры.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектКачества["customer_id"]));
		НоваяСтрока.Склад = Справочники.Склады.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектКачества["shop_id"]));
		
		ИдентификаторОбъектКачества = ОбъектКачества[КлючОбъекта];
		Если СервисПрогнозирования.ЭтоПустойИдентификатор(ИдентификаторОбъектКачества) Тогда
			НоваяСтрока.Объект = "";
			Продолжить;
		КонецЕсли;
		
		Если ПрогнозПоКатегориям И ВариантПрогнозированияПоКатегориям = 0 Тогда
			НоваяСтрока.Объект = СервисПрогнозированияПереопределяемый.ПолучитьТоварнуюКатегориюПоИдентификаторуКатегории(
				ИдентификаторОбъектКачества);
		Иначе
			Ответ = СервисПрогнозированияПереопределяемый.ПолучитьНоменклатуруПоИдентификатору(
				ИдентификаторОбъектКачества,
				НастройкиСервиса);
			НоваяСтрока.Объект = Ответ;
		КонецЕсли;
		
	КонецЦикла;
	
	КачествоМоделейПрогнозированияПоВсемПериодам.Сортировать("Объект, Склад, Партнер");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиПланФактПоПериодамНаСервере()
	Если ПроверитьВозможностьЗапросаКСервису() Тогда
		ИмяМетода = ИмяМетодаПолучитьТочностьМоделиПланФактПоПериодам();
		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ВидПлана);
		ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода, ПараметрыМетода);
		
		Если ПустаяСтрока(ИдентификаторЗадания) Тогда
			ОбновитьТочностьМоделиПланФактПоПериодамНаСервереФинальный();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьТочностьМоделиПланФактПоПериодамНаСервереФинальный()
	
	СтруктураОтвета = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(СтруктураОтвета.ДесериализованноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	КачествоМоделейПрогнозированияПоПериодам.Очистить();
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	Если ПрогнозПоКатегориям Тогда
		Если ВариантПрогнозированияПоКатегориям = 0 Тогда
			КлючОбъекта = "category_id_" + ИдентификаторКоллекцииТоваров; // "category_processing" = "group".
		Иначе
			КлючОбъекта = "item_id"; // "category_processing" = "reduce".
		КонецЕсли;
	Иначе
		КлючОбъекта = "item_id";
	КонецЕсли;
	
	Для Каждого ОбъектКачества Из СтруктураОтвета.ДесериализованноеЗначение Цикл
		НоваяСтрока = КачествоМоделейПрогнозированияПоПериодам.Добавить();
		
		НоваяСтрока.Модель1с = Число(ОбъектКачества["1C_model"]);
		Если ПрогнозПоМесяцам Тогда
			// Присутствует только у прогнозов по месяцам.
			НоваяСтрока.СквозноеТрендовоеРазложение = ОбъектКачества["STL_model"];
		КонецЕсли;
		НоваяСтрока.ТрендовоеРазложение = Число(ОбъектКачества["TL_model"]);
		НоваяСтрока.ТройноеЭкспоненциальноеСглаживание = ОбъектКачества["ESTS_model"];
		НоваяСтрока.ЭкспоненциальноеСглаживание = Число(ОбъектКачества["exp_smoothing"]);
		НоваяСтрока.ЛогарифмЭкспоненциальногоСглаживания = Число(ОбъектКачества["exp_smoothing_log"]);
		НоваяСтрока.НаивнаяМодель = Число(ОбъектКачества["naive"]);
		НоваяСтрока.РекуррентнаяНейроннаяСеть = Число(ОбъектКачества["rnn_model"]);
		НоваяСтрока.СкользящееСреднее = Число(ОбъектКачества["rolling_mean3"]);
		НоваяСтрока.ЛогарифмСкользящегоСреднего = Число(ОбъектКачества["rolling_mean3_log"]);
		НоваяСтрока.СредняяСезонность = Число(ОбъектКачества["season_mean"]);
		
		НоваяСтрока.Дата = СервисПрогнозирования.ДатаСервисаВДатуПрограммы(ОбъектКачества["date"]);
		
		НоваяСтрока.Партнер = Справочники.Партнеры.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектКачества["customer_id"]));
		НоваяСтрока.Склад = Справочники.Склады.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектКачества["shop_id"]));
		
		ИдентификаторОбъектаКачества = ОбъектКачества[КлючОбъекта];
		Если СервисПрогнозирования.ЭтоПустойИдентификатор(ИдентификаторОбъектаКачества) Тогда
			НоваяСтрока.Объект = "";
			Продолжить;
		КонецЕсли;
		
		Если ПрогнозПоКатегориям И ВариантПрогнозированияПоКатегориям = 0 Тогда
			НоваяСтрока.Объект = СервисПрогнозированияПереопределяемый.ПолучитьТоварнуюКатегориюПоИдентификаторуКатегории(
				ИдентификаторОбъектаКачества);
		Иначе
			Ответ = СервисПрогнозированияПереопределяемый.ПолучитьНоменклатуруПоИдентификатору(
				ИдентификаторОбъектаКачества,
				НастройкиСервиса);
			НоваяСтрока.Объект = Ответ;
		КонецЕсли;
	КонецЦикла;
	
	КачествоМоделейПрогнозированияПоПериодам.Сортировать("Объект, Дата, Склад, Партнер");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИсториюОбученияНаСервере()
	Если ПроверитьВозможностьЗапросаКСервису() Тогда
		ИмяМетода = ИмяМетодаОбновитьИсториюОбучения();
		ПараметрыМетода = Новый Массив();
		ПараметрыМетода.Добавить(ВидПлана);
		ПараметрыМетода.Добавить(ФильтрыЗапросаИсторииОбученияПоОбъекту()); // Структура - 
		ЗапуститьФоновоеЗаданиеПолучениеДанныхСервиса(ИмяМетода, ПараметрыМетода);
		
		Если ПустаяСтрока(ИдентификаторЗадания) Тогда
			ОбновитьИсториюОбученияНаСервереФинальный();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбновитьИсториюОбученияНаСервереФинальный()
	
	СтруктураОтвета = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(СтруктураОтвета.ДесериализованноеЗначение) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	ИсторияОбучения.Очистить();
	
	НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
	
	Если ПрогнозПоКатегориям Тогда
		Если ВариантПрогнозированияПоКатегориям = 0 Тогда
			КлючОбъекта = "category_id_" + ИдентификаторКоллекцииТоваров; // "category_processing" = "group".
		Иначе
			КлючОбъекта = "item_id"; // "category_processing" = "reduce".
		КонецЕсли;
	Иначе
		КлючОбъекта = "item_id";
	КонецЕсли;
	
	Для Каждого ОбъектИстории Из СтруктураОтвета.ДесериализованноеЗначение Цикл
		НоваяСтрока = ИсторияОбучения.Добавить();
		НоваяСтрока.Партнер = Справочники.Партнеры.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектИстории["customer_id"]));
		НоваяСтрока.Склад = Справочники.Склады.ПолучитьСсылку(
			СервисПрогнозирования.ПолучитьУникальныйИдентификатор(ОбъектИстории["shop_id"]));
		НоваяСтрока.Дата = СервисПрогнозирования.ДатаСервисаВДатуПрограммы(ОбъектИстории["date"]);
		НоваяСтрока.СкорректированноеЗначениеПродаж = ОбъектИстории["q_corrected"];
		НоваяСтрока.ИсходноеЗначениеПродаж = ОбъектИстории["q_init"];
		
		ИдентификаторОбъектаИстории = ОбъектИстории[КлючОбъекта];
		Если СервисПрогнозирования.ЭтоПустойИдентификатор(ИдентификаторОбъектаИстории) Тогда
			НоваяСтрока.Объект = "";
			Продолжить;
		КонецЕсли;
		
		Если ПрогнозПоКатегориям И ВариантПрогнозированияПоКатегориям = 0 Тогда
			НоваяСтрока.Объект = СервисПрогнозированияПереопределяемый.ПолучитьТоварнуюКатегориюПоИдентификаторуКатегории(
				ИдентификаторОбъектаИстории);
		Иначе
			Ответ = СервисПрогнозированияПереопределяемый.ПолучитьНоменклатуруПоИдентификатору(
				ИдентификаторОбъектаИстории,
				НастройкиСервиса);
			НоваяСтрока.Объект = Ответ;
		КонецЕсли;
		
	КонецЦикла;
	
	ИсторияОбучения.Сортировать("Объект, Дата, Склад, Партнер");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияИндикатораВыгрузки()
	
	ПодключитьОбработчикОжидания("Подключаемый_ИндикаторВыгрузки", 4, Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПрерватьВыгрузкуНаСервере()
	
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	СводкаОбменаДанными.ОбменДаннымиАктивен = Ложь;
	РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ЗаписатьСтатусыОбменаДанными(СводкаОбменаДанными);
	
	СобытиеЖурналаРегистрации = СервисПрогнозированияПереопределяемый.ТекстСобытиеЖурналаРегистрации();
	Комментарий = НСтр("ru = 'Выгрузка данных в сервис прогнозирования продаж прервана вручную.';
						|en = 'The data export to the sale forecasting service is interrupted manually.'", ОбщегоНазначения.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации,
								УровеньЖурналаРегистрации.Информация,
								,
								,
								Комментарий);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(ИдентификаторЗадания)
	
	Если ТипЗнч(ИдентификаторЗадания) = Тип("Строка") Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(Новый УникальныйИдентификатор(ИдентификаторЗадания));
	Иначе
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПроверитьВозможностьЗапросаКСервису()
	
	Если Не ЗначениеЗаполнено(СценарийПрогнозирования) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнено поле ""Сценарий"".';
							|en = '""Scenario"" field is not filled.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"СценарийПрогнозирования");
		Возврат Ложь;
	ИначеЕсли Не ЗначениеЗаполнено(ВидПлана) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнено поле ""Вид плана"".';
							|en = '""Plan profile"" field is not filled.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"ВидПлана");
		Возврат Ложь;
	ИначеЕсли Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидПлана, "ЗаполнятьПоДаннымСервиса") Тогда
		ТекстОшибки = НСтр("ru = 'Выбранный вид плана не настроен для работы с сервисом прогнозирования.';
							|en = 'The selected plan profile is not set up to work with the forecasting service.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки,,"ВидПлана");
		Возврат Ложь;
	КонецЕсли;
	
	ИдетОбменДанными = СервисПрогнозирования.ИдетОбменДанными();
	Если ИдетОбменДанными Тогда
		ТекстОшибки = НСтр("ru = 'Выполнить действие невозможно, поскольку идет обмен данными.';
							|en = 'Cannot run the action because of the data exchange.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	ИнфоМодели = РегистрыСведений.СервисПрогнозированияИдентификаторыОбучения.ИнформацияОбОбученииМодели(ВидПлана);
	Если ИнфоМодели.ЗагруженнаяМодель = Неопределено Тогда
		Если ИнфоМодели.ОбучающаясяМодель = Неопределено Тогда
			ТекстОшибки = НСтр("ru = 'У выбранного вида плана не найдена информация об обучении модели.';
								|en = 'Information on model training was not found for the selected plan profile. '");
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			Возврат Ложь;
		КонецЕсли;
		
		СтатусОбучения = ИнфоМодели.ОбучающаясяМодель.СтатусОбучения;
		Если СтатусОбучения = СервисПрогнозирования.СтатусГотовКПолучению() Тогда
			Возврат Истина;
		КонецЕсли;
		Если СтатусОбучения = СервисПрогнозирования.СтатусОшибкаОбучения() Тогда
			ТекстОшибки = НСтр("ru = 'У выбранного вида плана произошла ошибка обучения.
				|Попробуйте запросить новый прогноз. В случае повторения ошибки, обратитесь в техподдержку.';
				|en = 'An error occurred for the selected plan profile.
				|Try to request a new forecast. If the error persists, contact the technical support.'");
		ИначеЕсли СтатусОбучения = СервисПрогнозирования.СтатусПрерваноОбучение() Тогда
			ТекстОшибки = НСтр("ru = 'У выбранного вида плана было прервано обучение.
				|Запросите новый прогноз и подождите завершения обучения.';
				|en = 'Training was interrupted for the selected plan profile.
				|Request a new forecast and wait for the completion of the training.'");
		Иначе
			ТекстОшибки = НСтр("ru = 'У выбранного вида плана модель в процессе обучения.
				|Попробуйте обновить статус обучения. Если прогноз не готов к получению, подождите завершения обучения.';
				|en = 'The model for the selected plan profile is in  the training process.
				|Try to update the training status. If the forecast is not ready for receipt, wait until the completion of training.'");
		КонецЕсли;
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СоответствиеКоллекцийСервиса(ИмяВСервисе)
	
	Если ИмяВСервисе = "sales" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Продажи;
	ИначеЕсли ИмяВСервисе = "products" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Товары;
	ИначеЕсли ИмяВСервисе = "product_variants" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.ХарактеристикиНоменклатуры;
	ИначеЕсли ИмяВСервисе = "category" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Категории;
	ИначеЕсли ИмяВСервисе = "customers" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Покупатели;
	ИначеЕсли ИмяВСервисе = "shops" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Склады;
	ИначеЕсли ИмяВСервисе = "stock" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Остатки;
	ИначеЕсли ИмяВСервисе = "customer_orders" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Заказы;
	ИначеЕсли ИмяВСервисе = "promo" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Промо;
	ИначеЕсли ИмяВСервисе = "sales_plan" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПланыПродаж;
	ИначеЕсли ИмяВСервисе = "calendar" Тогда
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.Календарь;
	Иначе
		Возврат Перечисления.КоллекцииСервисаПрогнозированияПродаж.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ФильтрыЗапросаИсторииОбученияПоОбъекту()
	
	ФильтрыИсторииОбученияПоОбъекту = Новый Структура();
	Если ЗначениеЗаполнено(ИсторияОбученияФильтрПоСкладу) Тогда
		ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоСкладу.УникальныйИдентификатор());
		ФильтрыИсторииОбученияПоОбъекту.Вставить("shop_id", ИдентификаторОбъектаИстории);
	КонецЕсли;
	Если ПрогнозПоКатегориям Тогда
		Если ЗначениеЗаполнено(ИсторияОбученияФильтрПоТоварнойКатегории) Тогда
			ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоТоварнойКатегории.УникальныйИдентификатор());
			ФильтрыИсторииОбученияПоОбъекту.Вставить("category_id_" + ИдентификаторКоллекцииТоваров,
				ИдентификаторОбъектаИстории);
		КонецЕсли;
		Если ВариантПрогнозированияПоКатегориям = 1
			И ЗначениеЗаполнено(ИсторияОбученияФильтрПоТовару) Тогда
			ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоТовару.УникальныйИдентификатор());
			ФильтрыИсторииОбученияПоОбъекту.Вставить("item_id", ИдентификаторОбъектаИстории);
		КонецЕсли;
	Иначе
		Если ЗначениеЗаполнено(ИсторияОбученияФильтрПоТовару) Тогда
			ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоТовару.УникальныйИдентификатор());
			ФильтрыИсторииОбученияПоОбъекту.Вставить("item_id", ИдентификаторОбъектаИстории);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсторияОбученияФильтрПоПартнеру) Тогда
			ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоПартнеру.УникальныйИдентификатор());
			ФильтрыИсторииОбученияПоОбъекту.Вставить("customer_id", ИдентификаторОбъектаИстории);
		КонецЕсли;
		Если ЗначениеЗаполнено(ИсторияОбученияФильтрПоХарактеристике) Тогда
			ИдентификаторОбъектаИстории = Строка(ИсторияОбученияФильтрПоХарактеристике.УникальныйИдентификатор());
			ФильтрыИсторииОбученияПоОбъекту.Вставить("item_variant_id", ИдентификаторОбъектаИстории);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ФильтрыИсторииОбученияПоОбъекту;
	
КонецФункции

&НаКлиенте
Процедура ПрерватьВыгрузкуЗавершение(Результат, ПараметрКоманды) Экспорт
	Если Результат <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	ПрерватьВыгрузкуНаСервере();
КонецПроцедуры

#Область ПодключаемыеМетоды

&НаКлиенте
Процедура Подключаемый_ИндикаторВыгрузки()
	
	ОбновитьИнформациюОВыгрузкеКоллекций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьОбработчикОжиданияФоновогоЗадания()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьВыполнениеФоновогоЗаданияОповещение", ЭтотОбъект);

	ДлительныеОперацииКлиент.ОжидатьЗавершение(РезультатРасчета, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыполнениеФоновогоЗаданияОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(Результат.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		ОбработатьВыполнениеФоновогоЗаданияСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаПодключаемыхМетодов

&НаСервере
Процедура ОбработатьВыполнениеФоновогоЗаданияСервер()
	ИдентификаторЗадания = "";
	
	Ответ = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Не ПустаяСтрока(Ответ.ТекстОшибки) Тогда
		УстановитьВидимостьДоступность();
		ВызватьИсключение Ответ.ТекстОшибки;
	КонецЕсли;
	
	Если ИмяМетодаФоновогоЗадания = ИмяМетодаПолучитьПереченьКоллекций() Тогда
		ОбновитьСоставКоллекцийНаСервереФинальный();
	ИначеЕсли ИмяМетодаФоновогоЗадания = ИмяМетодаОбновитьИсториюОбучения() Тогда
		ОбновитьИсториюОбученияНаСервереФинальный();
	ИначеЕсли ИмяМетодаФоновогоЗадания = ИмяМетодаПолучитьТочностьМодели() Тогда
		ОбновитьТочностьМоделиНаСервереФинальный();
	ИначеЕсли ИмяМетодаФоновогоЗадания = ИмяМетодаПолучитьТочностьМоделиПланФактПоВсемПериодам() Тогда
		ОбновитьТочностьМоделиПланФактПоВсемПериодамНаСервереФинальный();
	ИначеЕсли ИмяМетодаФоновогоЗадания = ИмяМетодаПолучитьТочностьМоделиПланФактПоПериодам() Тогда
		ОбновитьТочностьМоделиПланФактПоПериодамНаСервереФинальный();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИнформациюОВыгрузкеКоллекций()
	
	СводкаОбменаДанными = РегистрыСведений.СтатусыОбменаДаннымиССервисомПрогнозированияПродаж.ПолучитьСтатусыОбменаДанными();
	ВыгружаемаяСейчасКоллекция = СводкаОбменаДанными.ВыгружаемаяСейчасКоллекция;
	ВыгружаемаяСейчасКоллекцияПредставление = "";
	Если ЗначениеЗаполнено(ВыгружаемаяСейчасКоллекция) Тогда
		НастройкиСервиса = СервисПрогнозирования.ПолучитьНастройкиСервиса();
		ВыгружаемаяСейчасКоллекцияПредставление = НастройкиСервиса.Коллекции[ВыгружаемаяСейчасКоллекция].Представление;
	КонецЕсли;
	
	Если СводкаОбменаДанными.ОбменДаннымиАктивен
		И СводкаОбменаДанными.ЗапланированоВыгрузитьКоллекций > 0 Тогда
		ДоляКоллекции = 1 / СводкаОбменаДанными.ЗапланированоВыгрузитьКоллекций;
		ПроцентовКоллекций = СводкаОбменаДанными.ВыгруженоКоллекций / СводкаОбменаДанными.ЗапланированоВыгрузитьКоллекций * 100;
		
		ПроцентовПоОбъектно = 0;
		Если СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции > 0 Тогда
			ПроцентовПоОбъектно = СводкаОбменаДанными.ВыгруженоОбъектовКоллекции / СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции * 100;
			ПроцентовПоОбъектно = ПроцентовПоОбъектно * ДоляКоллекции;
		КонецЕсли;
		
		ИндикаторВыгрузки = ПроцентовКоллекций + ПроцентовПоОбъектно;
	Иначе
		ИндикаторВыгрузки = 0;
	КонецЕсли;
	ШаблонПодсказки = НСтр("ru = 'Выгружается коллекция: %1, %2 из %3 записей.';
							|en = 'Exporting collection: %1, %2 out of %3 records.'");
	Элементы.ИндикаторВыгрузки.Подсказка = СтроковыеФункции.ФорматированнаяСтрока(ШаблонПодсказки,
		ВыгружаемаяСейчасКоллекцияПредставление,
		СводкаОбменаДанными.ВыгруженоОбъектовКоллекции,
		СводкаОбменаДанными.ЗапланированоВыгрузитьОбъектовКоллекции);
	
	ИдетВыгрузкаДанных = СводкаОбменаДанными.ОбменДаннымиАктивен;
	
	Если ИдетВыгрузкаДанных Тогда
		Элементы.ГруппаВариантыВыгрузкиСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыВыгрузкиСтраницаИдетВыгрузка;
		Если Элементы.ГруппаВариантыВыгрузкиСтраницы.ТекущаяСтраница <> Элементы.ГруппаВариантыВыгрузкиСтраницаИдетВыгрузка Тогда
			ЗаполнитьСписокВыгружаемыхКоллекций();
		КонецЕсли;
	Иначе
		Элементы.ГруппаВариантыВыгрузкиСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыВыгрузкиСтраницаВарианты;
		Если Элементы.ГруппаВариантыВыгрузкиСтраницы.ТекущаяСтраница <> Элементы.ГруппаВариантыВыгрузкиСтраницаВарианты Тогда
			ЗаполнитьСписокВыгружаемыхКоллекций();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИменаМетодов

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетодаПолучитьПереченьКоллекций()
	Возврат "СервисПрогнозирования.ПолучитьПереченьКоллекцийНаСервере";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетодаОбновитьИсториюОбучения()
	Возврат "СервисПрогнозирования.ПолучитьИсториюОбучения";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетодаПолучитьТочностьМодели()
	Возврат "СервисПрогнозирования.ПолучитьКачествоМоделей";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетодаПолучитьТочностьМоделиПланФактПоВсемПериодам()
	Возврат "СервисПрогнозирования.ПолучитьКачествоМоделейПоОбъектам";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяМетодаПолучитьТочностьМоделиПланФактПоПериодам()
	Возврат "СервисПрогнозирования.ПолучитьФактическиеЗначенияИПредсказанияНаТестовыхПериодах";
КонецФункции

#КонецОбласти

#Область Тексты

&НаКлиентеНаСервереБезКонтекста
Функция ТекстОшибкиАвторизацияНеНайдена()
	
	Возврат НСтр("ru = 'Авторизация не найдена. Перейдите в помощник подключения к сервису.';
				|en = 'Authorization is not found. Go to the service connection wizard.'");
	
КонецФункции

#КонецОбласти

#КонецОбласти

