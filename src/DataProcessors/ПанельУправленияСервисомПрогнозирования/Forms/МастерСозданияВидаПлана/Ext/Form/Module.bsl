
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ТипПлана = Перечисления.ТипыПланов.ПланПродаж;
	Если Не Параметры.ИспользоватьПланированиеПродаж Тогда
		ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям;
		СписокТиповПланов = Элементы.ТипПлана.СписокВыбора;
		НеИспользуемыйТипПлана = СписокТиповПланов.НайтиПоЗначению(Перечисления.ТипыПланов.ПланПродаж);
		СписокТиповПланов.Удалить(НеИспользуемыйТипПлана);
	ИначеЕсли Не Параметры.ИспользоватьПланированиеПродажПоКатегориям Тогда
		СписокТиповПланов = Элементы.ТипПлана.СписокВыбора;
		НеИспользуемыйТипПлана = СписокТиповПланов.НайтиПоЗначению(Перечисления.ТипыПланов.ПланПродажПоКатегориям);
		СписокТиповПланов.Удалить(НеИспользуемыйТипПлана);
	КонецЕсли;
	
	ТекстВариантыПрогнозированияПоКатегориям = НСтр("ru = 'Выбранный вариант определяет, в каком виде данные прогнозов будут выводиться для анализа:
		| %1 Только по категориям   - данные выводятся по категориям.
		| %1 С разбивкой по товарам - данные выводятся по товарам.';
		|en = 'The selected option identifies how the forecast data will be displayed for analysis:
		| %1 Only by category. Data is displayed by category.
		| %1 Broken down by items. Data is displayed by item.'");
	ТекстВариантыПрогнозированияПоКатегориям = СтрШаблон(ТекстВариантыПрогнозированияПоКатегориям, Символ(8226));
	Элементы.ДекорацияВариантПрогнозированияПоКатегориям.Подсказка = ТекстВариантыПрогнозированияПоКатегориям;
	
	СценарийПрогнозирования = Параметры.СценарийПрогнозирования;
	Периодичность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СценарийПрогнозирования, "Периодичность");
	КоличествоПериодовПрогнозирования = 5;
	
	ДетализацияПоХарактеристике = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
	ПересчитатьПоследнююИзвестнуюДатуПродажи();
	
	УстановитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаНачалаПрогнозированияПриИзменении(Элемент)
	ПересчитатьПоследнююИзвестнуюДатуПродажи();
КонецПроцедуры

&НаКлиенте
Процедура ТипПланаПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Создать(Команда)
	
	ОчиститьСообщения();
	
	Если ПроверитьЗаполнение() Тогда
		НовыйВидПланов = СоздатьНаСервере();
		
		ПараметрыОповещения = Новый Структура("НовыйВидПланов", НовыйВидПланов);
		Оповестить("СозданиеВидаПлана", ПараметрыОповещения, ВладелецФормы);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимость()
	
	ЭтоПланПродажПоНоменклатуре = ТипПлана = Перечисления.ТипыПланов.ПланПродаж;
	ИспользоватьНесколькоСкладов = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	
	Элементы.ДетализацияПоХарактеристике.Видимость = ЭтоПланПродажПоНоменклатуре;
	Элементы.ДетализацияПоКлиентам.Видимость = ЭтоПланПродажПоНоменклатуре;
	Элементы.ДетализацияПоСкладам.Видимость = ИспользоватьНесколькоСкладов;
	Элементы.ГруппаВариантПрогнозированияПоКатегориям.Видимость = Не ЭтоПланПродажПоНоменклатуре;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьПоследнююИзвестнуюДатуПродажи()
	
	ПоследняяИзвестнаяПродажа = СервисПрогнозированияПереопределяемый.ПоследняяИзвестнаяДатаПродажи();
	
КонецПроцедуры

&НаСервере
Функция СоздатьНаСервере()
	
	ЭтоПланПродажПоНоменклатуре = ТипПлана = Перечисления.ТипыПланов.ПланПродаж;
	ДетализацияПоХарактеристике = ?(ЭтоПланПродажПоНоменклатуре, ДетализацияПоХарактеристике, Ложь);
	ДетализацияПоКлиентам = ?(ЭтоПланПродажПоНоменклатуре, ДетализацияПоКлиентам, Ложь);
	
	НовыйВидПланов = Справочники.ВидыПланов.СоздатьЭлемент();
	
	ЗначенияВидовПлановПоУмолчанию = СервисПрогнозированияПереопределяемыйКлиентСервер.ЗначенияВидаПлановПоУмолчанию();
	
	ЗаполнитьЗначенияСвойств(НовыйВидПланов, ЗначенияВидовПлановПоУмолчанию, , "Периодичность");
	
	НовыйВидПланов.Владелец = СценарийПрогнозирования;
	НовыйВидПланов.ТипПлана = ТипПлана;
	
	НовыйВидПланов.Наименование = Наименование;
	НовыйВидПланов.КоличествоПериодов = КоличествоПериодовПрогнозирования;
	НовыйВидПланов.Периодичность = Периодичность;
	НовыйВидПланов.НачалоПрогнозирования = ДатаНачалаПрогнозирования;
	
	НовыйВидПланов.ЗаполнятьПоХарактеристикамНоменклатуры = ДетализацияПоХарактеристике;
	Если ДетализацияПоКлиентам Тогда
		НовыйВидПланов.ЗаполнятьПартнера    = Ложь;
		НовыйВидПланов.ЗаполнятьПартнераВТЧ = Истина;
	КонецЕсли;
	Если ДетализацияПоСкладам Тогда
		НовыйВидПланов.ЗаполнятьСклад    = Ложь;
		НовыйВидПланов.ЗаполнятьСкладВТЧ = Истина;
	КонецЕсли;
	
	НовыйВидПланов.ЗаполнятьПоФормуле = Ложь;
	НовыйВидПланов.ЗаполнятьПоДаннымСервиса = Истина;
	
	Если ТипПлана = Перечисления.ТипыПланов.ПланПродажПоКатегориям Тогда
		НовыйВидПланов.ВариантПрогнозированияПоКатегориям = ВариантПрогнозированияПоКатегориям;
	КонецЕсли;
	
	НовыйВидПланов.Записать();
	
	Возврат НовыйВидПланов.Ссылка;
	
КонецФункции

#КонецОбласти
