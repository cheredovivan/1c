
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		ВызватьИсключение НСтр("ru = 'Не найдена подсистема интернет поддержки пользователей';
								|en = 'The online user support subsystem is not found'");
	КонецЕсли;
	
	Если Не ПравоДоступа("Использование", Метаданные.Обработки.РаботаСОблачнымиКлассификаторами) Тогда
		Отказ = Истина;
		ОбработкаНеисправностейБЭД.СообщитьПользователюОНарушенииПравДоступа();
		Возврат;
	КонецЕсли;
	
	РежимВыбораЭлемента     = Параметры.РежимВыбораЭлемента;
	КодВыбранногоЭлемента   = Параметры.КодВыбранногоЭлемента;
	ПараметрыДополнительные = Параметры.ДополнительныеПараметры;
	
	ИнтернетПоддержкаПодключена = ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	УстановитьУсловноеОформление();
	НастроитьВнешнийВидФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ИнтернетПоддержкаПодключена Тогда
		ПостроитьДеревоКлассификатора();
		
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПостроитьДеревоПослеОткрытияПродолжение", ЭтотОбъект);
		
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			ОповещениеОЗавершении, 
			ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоКлассификатора

&НаКлиенте
Процедура ДеревоКлассификатораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если РежимВыбораЭлемента Тогда
		СтандартнаяОбработка = Ложь;
		ВыполнитьВыбор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоКлассификатораПометкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДеревоКлассификатора.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекущиеДанные.Пометка = 2 Тогда
		ТекущиеДанные.Пометка = 0;
	КонецЕсли;
	
	ОбходДереваКлассификатораВверх(ТекущиеДанные);
	ОбходДереваКлассификатораВниз(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьДанные(Команда)
	
	Если РежимВыбораЭлемента Тогда
		ВыполнитьВыбор();
		
	Иначе
		ЗагрузитьДанныеЗавершение = Новый ОписаниеОповещения("ЗагрузитьДанныеЗавершение", ЭтотОбъект, Новый Структура);
		
		ОблачныеКлассификаторыКлиент.ЗагрузитьВБазуДанныеОКЕИ(ЗагрузитьДанныеЗавершение, 
			ЭтотОбъект,
			ПоместитьДеревоКлассификатораВоВременноеХранилище(),
			Неопределено,
			Истина,
			ПараметрыДополнительные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НастроитьВнешнийВидФормы()
	
	Если Не РежимВыбораЭлемента Тогда
		Заголовок = НСтр("ru = 'Загрузка из классификатора единиц измерения';
						|en = 'Import from UOM classifier'");
		Элементы.ЗагрузитьДанные.Заголовок = НСтр("ru = 'Загрузить данные';
													|en = 'Import data'");
	Иначе
		Заголовок = НСтр("ru = 'Выберите единицу измерения из классификатора';
						|en = 'Select the unit of measure from the classifier'");
		Элементы.ЗагрузитьДанные.Заголовок = НСтр("ru = 'Выбрать';
													|en = 'Select'");
		Элементы.ПодсказкаКФорме.Видимость = Ложь;
		Элементы.ДеревоКлассификатораПометка.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораУсловноеОбозначениеНациональное.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораУсловноеОбозначениеМеждународное.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораКодовоеБуквенноеОбозначениеНациональное.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораКодовоеБуквенноеОбозначениеМеждународное.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораТипИзмеряемойВеличины.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораКод.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатораТипИзмеряемойВеличины.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Код");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатора.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Код");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.ЖирныйШрифтБЭД);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДеревоКлассификатора.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоКлассификатора.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьДеревоПослеОткрытияПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура")
			И Результат.Свойство("Логин") Тогда
		ИнтернетПоддержкаПодключена = Истина;
		ПостроитьДеревоКлассификатора();
		
	Иначе
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
		Элементы.ЗагрузитьДанные.Доступность = Ложь;
		Элементы.ПодсказкаКФорме.Видимость   = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьДеревоКлассификатора()
	
	ОценкаПроизводительностиКлиент.ЗамерВремени(
		"Обработка.РаботаСОблачнымиКлассификаторами.Форма.КлассификаторЕдиницИзмерения.ПостроитьДеревоКлассификатора",
		Ложь, 
		Истина);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПостроитьДеревоКлассификатораЗавершение", ЭтотОбъект);
	
	ОблачныеКлассификаторыКлиент.ПолучитьИерархиюЭлементовОКЕИ(ОповещениеОЗавершении,
		ЭтотОбъект, 
		Неопределено, 
		Истина, 
		ПараметрыДополнительные);
	
КонецПроцедуры

&НаКлиенте
Процедура ПостроитьДеревоКлассификатораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если Результат.Сообщения <> Неопределено Тогда
		Для Каждого Сообщение Из Результат.Сообщения Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Не Результат.Свойство("Статус")
			Или Результат.Статус <> "Выполнено" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Ошибка выполнения запроса к сервису. Повторите попытку позже.';
				|en = 'Error executing a request to a service. Please try again later.'"));
		Возврат;
	КонецЕсли;
	
	ПостроитьДеревоКлассификатораНаСервере(Результат.АдресРезультата);
	ДоступностьДляЗагрузки = (ДеревоКлассификатора.ПолучитьЭлементы().Количество() > 0);
	
	Элементы.ЗагрузитьДанные.Доступность       = ДоступностьДляЗагрузки;
	Элементы.ЗагрузитьДанные.КнопкаПоУмолчанию = ДоступностьДляЗагрузки;
	Элементы.ПодсказкаКФорме.Видимость         = Не РежимВыбораЭлемента И ДоступностьДляЗагрузки;
	
	Для Каждого ЭлементКоллекции Из ДеревоКлассификатора.ПолучитьЭлементы() Цикл
		Элементы.ДеревоКлассификатора.Развернуть(ЭлементКоллекции.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
	Если РежимВыбораЭлемента
			И ЗначениеЗаполнено(КодВыбранногоЭлемента) Тогда
		НайденнаяСтрока = НайтиСтрокуДереваПоКоду(ДеревоКлассификатора, СокрЛП(КодВыбранногоЭлемента));
		Если НайденнаяСтрока <> Неопределено Тогда
			Элементы.ДеревоКлассификатора.ТекущаяСтрока = НайденнаяСтрока.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПостроитьДеревоКлассификатораНаСервере(АдресРезультата)
	
	Если Не ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ТипЗнч(Результат) = Тип("ДеревоЗначений") Тогда
		ЗначениеВРеквизитФормы(Результат, "ДеревоКлассификатора");
		
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Ошибка выполнения запроса к сервису. Подробности см. в журнале регистрации.';
				|en = 'An error occurred while executing a query to the service. See the event log for details.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбходДереваКлассификатораВверх(Знач ТекущиеДанные)
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	
	Если Родитель <> Неопределено Тогда
		ДочерниеСтроки      = Родитель.ПолучитьЭлементы();
		ОбщееКоличество     = 0;
		КоличествоВыбранных = 0;
		
		Для Каждого ЭлементКоллекции Из ДочерниеСтроки Цикл
			ОбщееКоличество = ОбщееКоличество + 1;
			
			Если ЗначениеЗаполнено(ЭлементКоллекции.Ссылка) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭлементКоллекции.Пометка = 2 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 0.5;
			ИначеЕсли ЭлементКоллекции.Пометка = 1 Тогда
				КоличествоВыбранных = КоличествоВыбранных + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если ОбщееКоличество = КоличествоВыбранных Тогда
			Родитель.Пометка = 1;
		ИначеЕсли КоличествоВыбранных = 0 Тогда
			Родитель.Пометка = 0;
		Иначе
			Родитель.Пометка = 2;
		КонецЕсли;
		
		ОбходДереваКлассификатораВверх(Родитель);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбходДереваКлассификатораВниз(Знач ТекущиеДанные)
	
	ДочерниеСтроки = ТекущиеДанные.ПолучитьЭлементы();
	
	Для Каждого ЭлементКоллекции Из ДочерниеСтроки Цикл
		Если ЗначениеЗаполнено(ЭлементКоллекции.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементКоллекции.Пометка = ТекущиеДанные.Пометка;
		
		ОбходДереваКлассификатораВниз(ЭлементКоллекции);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокуДереваПоКоду(Знач Родитель, Знач Код)
	
	ДочерниеСтроки = Родитель.ПолучитьЭлементы();
	
	Для Каждого ЭлементКоллекции Из ДочерниеСтроки Цикл
		Если ЗначениеЗаполнено(ЭлементКоллекции.Код)
				И ЭлементКоллекции.Код = Код Тогда
			Возврат ЭлементКоллекции;
		КонецЕсли;
		
		НайденнаяСтрока = НайтиСтрокуДереваПоКоду(ЭлементКоллекции, Код);
		Если НайденнаяСтрока <> Неопределено Тогда
			Возврат НайденнаяСтрока;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьВыбор()
	
	ТекущиеДанные = Элементы.ДеревоКлассификатора.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран элемент для загрузки.';
										|en = 'Item for import is not selected.'"));
		Возврат;
	ИначеЕсли Не ЗначениеЗаполнено(ТекущиеДанные.Код) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Выберите элемент, а не группу.';
										|en = 'Select an item, not a group.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Код",                                      ТекущиеДанные.Код);
	Результат.Вставить("Наименование",                             ТекущиеДанные.Наименование);
	Результат.Вставить("УсловноеОбозначениеНациональное",          ТекущиеДанные.УсловноеОбозначениеНациональное);
	Результат.Вставить("УсловноеОбозначениеМеждународное",         ТекущиеДанные.УсловноеОбозначениеМеждународное);
	Результат.Вставить("КодовоеБуквенноеОбозначениеНациональное",  ТекущиеДанные.КодовоеБуквенноеОбозначениеНациональное);
	Результат.Вставить("КодовоеБуквенноеОбозначениеМеждународное", ТекущиеДанные.КодовоеБуквенноеОбозначениеМеждународное);
	Результат.Вставить("ТипИзмеряемойВеличины",                    ТекущиеДанные.ТипИзмеряемойВеличины);
	Результат.Вставить("Числитель",                                ТекущиеДанные.Числитель);
	Результат.Вставить("Знаменатель",                              ТекущиеДанные.Знаменатель);
	Результат.Вставить("Ссылка",                                   ТекущиеДанные.Ссылка);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьДеревоКлассификатораВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(РеквизитФормыВЗначение("ДеревоКлассификатора"), УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьДанныеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если Результат.Сообщения <> Неопределено Тогда
		Для Каждого Сообщение Из Результат.Сообщения Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	
	Если Результат.Свойство("Статус") 
			И Результат.Статус = "Выполнено" Тогда
		ЗакрытьФорму      = Истина;
		РезультатЗагрузки = ЗагрузитьДанныеНаСервере(Результат.АдресРезультата, ЗакрытьФорму);
		
		Если ЗакрытьФорму Тогда
			Закрыть(РезультатЗагрузки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗагрузитьДанныеНаСервере(АдресРезультата, ЗакрытьФорму = Истина)
	
	Если Не ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ЗначениеЗаполнено(Результат.СообщениеОбОшибке) Тогда
	    ОбщегоНазначения.СообщитьПользователю(Результат.СообщениеОбОшибке);
		ЗакрытьФорму = Ложь;
		Возврат Неопределено;
		
	ИначеЕсли Результат.КоличествоОбработанных = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
