
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НайтиАналитику(Команда)
	НайтиАналитикуНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура Исправить(Команда)
	
	ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'По данным первичных движений будут перезаполнены регистры: %1
		|
		|Задания к переотражению в БУ, МФУ, НДС и к закрытию месяца созданы не будут. Продолжить?';
		|en = 'The following registers will be re-populated: %1 according to the primary register records data
		|
		|Jobs to be recorded in the local accounting, IFRS, VAT and by the month-end closing will not be created. Continue?'"),
		ИменаРегистров());
	ПоказатьВопрос(Новый ОписаниеОповещения("ИсправитьЗавершение", ЭтотОбъект), ТекстВопроса,РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ИсправитьЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		Сообщить(НСтр("ru = 'Начало операции:';
						|en = 'Operation start:'") + " " + ТекущаяДата());
		ИсправитьНаСервере();
		Сообщить(НСтр("ru = 'Операция выполнена успешно:';
						|en = 'Operation has been successfully installed:'") + " " + ТекущаяДата());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИменаРегистров(ТолькоПлановые = Ложь)
	Если НЕ ТолькоПлановые Тогда
		Возврат 
			СтрШаблон("
			|	%1
			|	%2
			|	%3
			|	%4
			|	%5
			|	%6",
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОплат.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланОплат.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланПоставок.Синоним
			);
	Иначе
		Возврат 
			СтрШаблон("
			|	%1
			|	%2
			|	%3
			|	%4",
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОплат.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСКлиентамиПланОтгрузок.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланОплат.Синоним,
			Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПланПоставок.Синоним
			);
	КонецЕсли;
КонецФункции

&НаСервере
Функция НайтиПроблемнуюАналитику()
	
	Операции = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаАналитикаДляПересчета();
	Запрос.УстановитьПараметр("Операции", Операции);
	
	Результаты = Запрос.ВыполнитьПакет();
	
	Ошибки = Новый Структура;
	Аналитика = Результаты[5].Выгрузить();
	Аналитика.Индексы.Добавить("АналитикаУчетаПоПартнерам");
	Аналитика.Индексы.Добавить("ОбъектРасчетов");
	Ошибки.Вставить("Аналитика", Аналитика);
	Ошибки.Вставить("КорректировкиРегистров", Результаты[6].Выгрузить());
	
	Возврат Ошибки;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаАналитикаДляПересчета()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор КАК ДокументРегистратор,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентамиПоСрокам.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСКлиентамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСКлиентамиПоСрокам.Предоплата - РасчетыСКлиентамиПоСрокам.Долг
	|			ИНАЧЕ РасчетыСКлиентамиПоСрокам.Долг - РасчетыСКлиентамиПоСрокам.Предоплата
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ВТКлиентыПоСрокам
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|ГДЕ
	|	РасчетыСКлиентамиПоСрокам.Активность
	|	И НЕ РасчетыСКлиентамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентамиПоСрокам.ДокументРегистратор,
	|	РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентамиПоСрокам.ОбъектРасчетов,
	|	РасчетыСКлиентамиПоСрокам.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСКлиентамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСКлиентамиПоСрокам.Предоплата - РасчетыСКлиентамиПоСрокам.Долг
	|			ИНАЧЕ РасчетыСКлиентамиПоСрокам.Долг - РасчетыСКлиентамиПоСрокам.Предоплата
	|		КОНЕЦ) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСКлиентами.Регистратор КАК Регистратор,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РасчетыСКлиентами.Сумма
	|			ИНАЧЕ РасчетыСКлиентами.Сумма
	|		КОНЕЦ) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТКлиенты
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.Активность
	|	И НЕ РасчетыСКлиентами.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.Регистратор,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСКлиентами.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -РасчетыСКлиентами.Сумма
	|			ИНАЧЕ РасчетыСКлиентами.Сумма
	|		КОНЕЦ) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСПоставщикамиПоСрокам.ДокументРегистратор КАК ДокументРегистратор,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоСрокам.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСПоставщикамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСПоставщикамиПоСрокам.Предоплата - РасчетыСПоставщикамиПоСрокам.Долг
	|			ИНАЧЕ РасчетыСПоставщикамиПоСрокам.Долг - РасчетыСПоставщикамиПоСрокам.Предоплата
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ВТПоставщикиПоСрокам
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|ГДЕ
	|	РасчетыСПоставщикамиПоСрокам.Активность
	|	И НЕ РасчетыСПоставщикамиПоСрокам.ДокументРегистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщикамиПоСрокам.ДокументРегистратор,
	|	РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов,
	|	РасчетыСПоставщикамиПоСрокам.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСПоставщикамиПоСрокам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСПоставщикамиПоСрокам.Предоплата - РасчетыСПоставщикамиПоСрокам.Долг
	|			ИНАЧЕ РасчетыСПоставщикамиПоСрокам.Долг - РасчетыСПоставщикамиПоСрокам.Предоплата
	|		КОНЕЦ) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ДокументРегистратор,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСПоставщиками.Регистратор КАК Регистратор,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСПоставщиками.Сумма
	|			ИНАЧЕ -РасчетыСПоставщиками.Сумма
	|		КОНЕЦ) КАК СуммаОборот
	|ПОМЕСТИТЬ ВТПоставщики
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	|	РасчетыСПоставщиками.Активность
	|	И НЕ РасчетыСПоставщиками.Регистратор ССЫЛКА Документ.КорректировкаРегистров
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.Регистратор,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА РасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА РасчетыСПоставщиками.Сумма
	|			ИНАЧЕ -РасчетыСПоставщиками.Сумма
	|		КОНЕЦ) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВложенныйЗапрос.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВложенныйЗапрос.Валюта КАК ВалютаРасчетов,
	|	ВложенныйЗапрос.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|ПОМЕСТИТЬ втАналитика
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЕСТЬNULL(ВТКлиенты.АналитикаУчетаПоПартнерам, ВТКлиентыПоСрокам.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|		ЕСТЬNULL(ВТКлиенты.ОбъектРасчетов, ВТКлиентыПоСрокам.ОбъектРасчетов) КАК ОбъектРасчетов,
	|		ЕСТЬNULL(ВТКлиенты.Валюта, ВТКлиентыПоСрокам.Валюта) КАК Валюта,
	|		ИСТИНА КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		ВТКлиентыПоСрокам КАК ВТКлиентыПоСрокам
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТКлиенты КАК ВТКлиенты
	|			ПО (ВТКлиенты.Регистратор = ВТКлиентыПоСрокам.ДокументРегистратор)
	|				И (ВТКлиенты.АналитикаУчетаПоПартнерам = ВТКлиентыПоСрокам.АналитикаУчетаПоПартнерам)
	|				И (ВТКлиенты.ОбъектРасчетов = ВТКлиентыПоСрокам.ОбъектРасчетов)
	|				И (ВТКлиенты.Валюта = ВТКлиентыПоСрокам.Валюта)
	|	ГДЕ
	|		ЕСТЬNULL(ВТКлиентыПоСрокам.Сумма, 0) <> ЕСТЬNULL(ВТКлиенты.СуммаОборот, 0)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ЕСТЬNULL(ВТПоставщики.АналитикаУчетаПоПартнерам, ВТПоставщикиПоСрокам.АналитикаУчетаПоПартнерам) КАК АналитикаУчетаПоПартнерам,
	|		ЕСТЬNULL(ВТПоставщики.ОбъектРасчетов, ВТПоставщикиПоСрокам.ОбъектРасчетов) КАК ОбъектРасчетов,
	|		ЕСТЬNULL(ВТПоставщики.Валюта, ВТПоставщикиПоСрокам.Валюта) КАК Валюта,
	|		ЛОЖЬ КАК ЭтоРасчетыСКлиентами
	|	ИЗ
	|		ВТПоставщикиПоСрокам КАК ВТПоставщикиПоСрокам
	|			ПОЛНОЕ СОЕДИНЕНИЕ ВТПоставщики КАК ВТПоставщики
	|			ПО (ВТПоставщики.Регистратор = ВТПоставщикиПоСрокам.ДокументРегистратор)
	|				И (ВТПоставщики.АналитикаУчетаПоПартнерам = ВТПоставщикиПоСрокам.АналитикаУчетаПоПартнерам)
	|				И (ВТПоставщики.ОбъектРасчетов = ВТПоставщикиПоСрокам.ОбъектРасчетов)
	|				И (ВТПоставщики.Валюта = ВТПоставщикиПоСрокам.Валюта)
	|	ГДЕ
	|		ЕСТЬNULL(ВТПоставщикиПоСрокам.Сумма, 0) <> ЕСТЬNULL(ВТПоставщики.СуммаОборот, 0)
	|	) КАК ВложенныйЗапрос
	|	
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Аналитика.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Аналитика.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Аналитика.ВалютаРасчетов КАК ВалютаРасчетов,
	|	Аналитика.ЭтоРасчетыСКлиентами КАК ЭтоРасчетыСКлиентами
	|ИЗ
	|	втАналитика КАК Аналитика
	|УПОРЯДОЧИТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Регистратор КАК Регистратор,
	|	ВложенныйЗапрос.Операция КАК Операция
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		КорректировкаРегистров.Ссылка КАК Регистратор,
	|		КорректировкаРегистров.Операция КАК Операция
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК РасчетыСКлиентамиПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|			ПО РасчетыСКлиентамиПоСрокам.ДокументРегистратор = КорректировкаРегистров.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитика КАК Аналитика
	|			ПО РасчетыСКлиентамиПоСрокам.АналитикаУчетаПоПартнерам = Аналитика.АналитикаУчетаПоПартнерам
	|				И РасчетыСКлиентамиПоСрокам.ОбъектРасчетов = Аналитика.ОбъектРасчетов
	|				И РасчетыСКлиентамиПоСрокам.Валюта = Аналитика.ВалютаРасчетов
	|	ГДЕ
	|		РасчетыСКлиентамиПоСрокам.Активность
	|		И КорректировкаРегистров.Операция <> ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|		
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		КорректировкаРегистров.Ссылка КАК Регистратор,
	|		КорректировкаРегистров.Операция КАК Операция
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК РасчетыСПоставщикамиПоСрокам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаРегистров КАК КорректировкаРегистров
	|			ПО РасчетыСПоставщикамиПоСрокам.ДокументРегистратор = КорректировкаРегистров.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втАналитика КАК Аналитика
	|			ПО РасчетыСПоставщикамиПоСрокам.АналитикаУчетаПоПартнерам = Аналитика.АналитикаУчетаПоПартнерам
	|				И РасчетыСПоставщикамиПоСрокам.ОбъектРасчетов = Аналитика.ОбъектРасчетов
	|				И РасчетыСПоставщикамиПоСрокам.Валюта = Аналитика.ВалютаРасчетов
	|	ГДЕ
	|		РасчетыСПоставщикамиПоСрокам.Активность
	|		И КорректировкаРегистров.Операция <> ЗНАЧЕНИЕ(Перечисление.ОперацииКорректировкиРегистров.РучнаяКорректировка)
	|	) КАК ВложенныйЗапрос
	|
	|УПОРЯДОЧИТЬ ПО
	|	Регистратор";
	
	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Процедура НайтиАналитикуНаСервере()
	
	ТабличныйДокумент.Очистить();
	
	Ошибки = НайтиПроблемнуюАналитику();
	Если Ошибки.Аналитика.Количество() > 0 Тогда
		ВывестиТаблицуСЗаголовком(Ошибки.Аналитика,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Аналитика расчетов по срокам для пересчета (Количество: %1)';
					|en = 'Calculation dimension by due dates for recalculation (Quantity: %1)'"),
				Строка(Ошибки.Аналитика.Количество())));
	Иначе
		ВывестиЗаголовок(НСтр("ru = 'Ошибок не обнаружено';
								|en = 'No errors detected'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИсправитьНаСервере()
	
	ТабличныйДокумент.Очистить();
	
	Ошибки = НайтиПроблемнуюАналитику();
	Если Ошибки.Аналитика.Количество() = 0 Тогда
		ВывестиЗаголовок(НСтр("ru = 'Ошибок не обнаружено';
								|en = 'No errors detected'"));
		Возврат;
	КонецЕсли;
	
	УдалитьСистемныеКорректировкиРегистров(Ошибки);
	Для Каждого Аналитика Из Ошибки.Аналитика Цикл
		ЗаполнитьПоУказаннымНаСервере(Аналитика.АналитикаУчетаПоПартнерам, Аналитика.ОбъектРасчетов);
	КонецЦикла;
	
	ВывестиТаблицуСЗаголовком(Ошибки.Аналитика,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Выполнен пересчет (Количество: %1)';
				|en = 'Recalculation is performed (Quantity: %1)'"),
			Строка(Ошибки.Аналитика.Количество())));
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоУказаннымНаСервере(КлючАналитикиУчетаПоПартнерам, ОбъектРасчетов)
	
	Если Не ЗначениеЗаполнено(КлючАналитикиУчетаПоПартнерам) Тогда
		ТекстИсключения = НСтр("ru = 'Для частичного заполнения укажите Ключ аналитики учета по партнерам.';
								|en = 'For partial filling, specify a Dimension key of accounting by partners.'");
		ВызватьИсключение ТекстИсключения;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АналитикаУчетаПоПартнерам", КлючАналитикиУчетаПоПартнерам);
	Запрос.УстановитьПараметр("ОбъектРасчетов",ОбъектРасчетов);
	
	Если ЗначениеЗаполнено(ОбъектРасчетов) И ЗначениеЗаполнено(КлючАналитикиУчетаПоПартнерам) Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                 КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|	И Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|	И Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|	И Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|	И Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам";
		
		ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
		
	ИначеЕсли ЗначениеЗаполнено(ОбъектРасчетов) Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                 КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.ОбъектРасчетов = &ОбъектРасчетов";
		
		ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
		
	Иначе
		
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                 КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентами КАК Расчеты
		|ГДЕ
		|	Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщиками КАК Расчеты
		|ГДЕ
		|	Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Ложь                   КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСПоставщикамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Расчеты.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Расчеты.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Расчеты.Валюта         КАК ВалютаРасчетов,
		|	Истина                 КАК ЭтоРасчетыСКлиентами
		|ИЗ
		|	РегистрНакопления.РасчетыСКлиентамиПоСрокам КАК Расчеты
		|ГДЕ
		|	Расчеты.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам";
		
		ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
		
	КонецЕсли;
	
	ОперативныеВзаиморасчетыСервер.ОчиститьРегистрыВзаиморасчетовИУдалитьСлужебныеРегистраторы(
		?(ЗначениеЗаполнено(ОбъектРасчетов), ОбъектРасчетов, Неопределено),
		?(ЗначениеЗаполнено(КлючАналитикиУчетаПоПартнерам), КлючАналитикиУчетаПоПартнерам, Неопределено));
	
	Для Каждого СтрокаТаблицы Из ТаблицаОбъектов Цикл
		
		ОсновныеПараметры = ОперативныеВзаиморасчетыСервер.СтруктураПараметровЗаполненияВзаиморасчетов();
		ЗаполнитьЗначенияСвойств(ОсновныеПараметры,СтрокаТаблицы);
		ОсновныеПараметры.ПерезаполнениеРегистровВзаиморасчетов = Истина;
		ОсновныеПараметры.ЭтоРасчетыСКлиентами = СтрокаТаблицы.ЭтоРасчетыСКлиентами;
		ОперативныеВзаиморасчетыСервер.ЗаполнитьОперативныеВзаиморасчеты(ОсновныеПараметры);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСистемныеКорректировкиРегистров(Ошибки)
	
	ТаблицаАналитик = Ошибки.Аналитика;
	Для Каждого Корректировка Из Ошибки.КорректировкиРегистров Цикл
		
		КорректировкаОбъект = Корректировка.Регистратор.ПолучитьОбъект(); // ДокументОбъект.КорректировкаРегистров
		Для Каждого Регистр Из КорректировкаОбъект.ТаблицаРегистров Цикл
			Движения = КорректировкаОбъект.Движения[Регистр.Имя]; // РегистрНакопленияНаборЗаписей
			Движения.ОбменДанными.Загрузка = Истина;
			РегистрРасчетовСКлиентами = Регистр.Имя = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам.Имя;
			Если РегистрРасчетовСКлиентами ИЛИ Регистр.Имя = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам.Имя Тогда
				Движения.Прочитать();
				ТаблицаДвижений = Движения.Выгрузить();
				Для Каждого Аналитика Из ТаблицаАналитик Цикл
					Если Аналитика.ЭтоРасчетыСКлиентами <> РегистрРасчетовСКлиентами Тогда
						Продолжить;
					КонецЕсли;
					Если ТаблицаДвижений.Количество() = 0 Тогда
						Прервать;
					КонецЕсли;
					Отбор = Новый Структура();
					Отбор.Вставить("АналитикаУчетаПоПартнерам", Аналитика.АналитикаУчетаПоПартнерам);
					Отбор.Вставить("ОбъектРасчетов", Аналитика.ОбъектРасчетов);
					Отбор.Вставить("Валюта", Аналитика.ВалютаРасчетов);
					НайденныеСтроки = ТаблицаДвижений.НайтиСтроки(Отбор);
					Для Каждого Строка Из НайденныеСтроки Цикл
						ТаблицаДвижений.Удалить(Строка);
					КонецЦикла;
				КонецЦикла;//по аналитикам
				Движения.Загрузить(ТаблицаДвижений);
				Движения.Записать();
			КонецЕсли;//это регистр по срокам
		КонецЦикла;//по движениям регистров
		
	КонецЦикла;//по документам КорректировкаРегистров
	
КонецПроцедуры

#Область ВыводВТабличныйДокумент

&НаСервере
Процедура ВывестиЗаголовок(Заголовок)
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок2|Колонка");
	
	ОбластьЗаголовок.Параметры.Значение = Заголовок;
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
КонецПроцедуры

&НаСервере
Процедура ВывестиТаблицуСЗаголовком(Таблица, Заголовок = "", Описание = "")
	
	Макет = РеквизитФормыВЗначение("Объект").ПолучитьМакет("Макет");
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок2|Колонка");
	
	ОбластьЗаголовок.Параметры.Значение = Заголовок;
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);
	
	Если Описание <> "" Тогда
		Область = ТабличныйДокумент.ПолучитьОбласть(2,1);
		Область.ТекущаяОбласть.Текст = Описание;
		ТабличныйДокумент.Вывести(Область);
	КонецЕсли;
	
	Расхождения = ВывестиТаблицуЗначений(Таблица);
	Расхождения.Область().СоздатьФорматСтрок();
	ТабличныйДокумент.Вывести(Расхождения);
	
КонецПроцедуры

&НаСервере
Функция ВывестиТаблицуЗначений(Таблица, ОбластьЗаголовокТаблицы = Неопределено)
	
	ТабДокумент = Новый ТабличныйДокумент;
	ШириныКолонок = ШириныКолонок();
	НомерСтроки = 1;
	
	// Выводим заголовок таблицы
	Если ОбластьЗаголовокТаблицы <> Неопределено Тогда
		ТабДокумент.Вывести(ОбластьЗаголовокТаблицы);
		НомерСтроки = НомерСтроки + 1;
	КонецЕсли;
	
	Arial8Полужирный = Новый Шрифт("Arial",8,Истина);
	Линия = Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная,1,Ложь);
	ТипВсеСсылки = ОписаниеТипаПочтиВсеСсылки();
	
	// Выводим шапку таблицы
	КолонкаИндекс = 0;
	Пока КолонкаИндекс < Таблица.Колонки.Количество() Цикл
		Колонка = Таблица.Колонки[КолонкаИндекс];
		ШиринаКолонки = ШириныКолонок[Колонка.Имя];
		
		Область = ТабДокумент.Область(НомерСтроки, КолонкаИндекс + 1, НомерСтроки, КолонкаИндекс + 1);
		Область.Текст = Колонка.Имя;
		Область.ШиринаКолонки = ?(ШиринаКолонки = Неопределено, 9, ШиринаКолонки);
		Область.Обвести(Линия,Линия,Линия,Линия);
		Область.Шрифт = Arial8Полужирный;
		Область.ЦветРамки = ЦветаСтиля.ЦветЛинииОтчета;
		Область.ЦветФона = ЦветаСтиля.ЦветФонаШапкиОтчета;
		
		КолонкаИндекс = КолонкаИндекс + 1;
	КонецЦикла;
	
	// Выводим строки таблицы
	СтрокаИндекс = 0;
	Пока СтрокаИндекс < Таблица.Количество() Цикл
		Таблица_Строка = Таблица[СтрокаИндекс];
		КолонкаИндекс = 0;
		Пока КолонкаИндекс < Таблица.Колонки.Количество() Цикл
			Колонка = Таблица.Колонки[КолонкаИндекс];
			Область = ТабДокумент.Область(СтрокаИндекс + НомерСтроки + 1, КолонкаИндекс + 1, СтрокаИндекс + НомерСтроки + 1, КолонкаИндекс + 1);
			Значение = Таблица_Строка[Колонка.Имя];
			Область.Текст = Формат(Значение, "ЧГ=0");
			Если ТипВсеСсылки.СодержитТип(ТипЗнч(Значение)) Тогда
				Область.Расшифровка = Значение;
			КонецЕсли;
			Если ОбщегоНазначенияКлиентСервер.ЭтоЧисло(Значение) Тогда
				Область.ГоризонтальноеПоложение=ГоризонтальноеПоложение.Право;
			КонецЕсли;
			Область.Обвести(Линия,Линия,Линия,Линия);
			Область.ЦветРамки = ЦветаСтиля.ЦветЛинииОтчета;
			КолонкаИндекс = КолонкаИндекс + 1;
		КонецЦикла;
		СтрокаИндекс = СтрокаИндекс + 1;
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

&НаСервере
Функция ОписаниеТипаПочтиВсеСсылки() Экспорт
	
	// к типу все справочники добавляем все документы, перечисления и т.д.
	ПочтиВсеСсылки = Новый ОписаниеТипов(Справочники.ТипВсеСсылки(), Документы.ТипВсеСсылки().Типы());
	ПочтиВсеСсылки = Новый ОписаниеТипов(ПочтиВсеСсылки, Перечисления.ТипВсеСсылки().Типы());
	ПочтиВсеСсылки = Новый ОписаниеТипов(ПочтиВсеСсылки, ПланыВидовХарактеристик.ТипВсеСсылки().Типы());
	ПочтиВсеСсылки = Новый ОписаниеТипов(ПочтиВсеСсылки, ПланыСчетов.ТипВсеСсылки().Типы());
	Возврат ПочтиВсеСсылки;
	
КонецФункции

&НаСервере
Функция ШириныКолонок()
	
	Результат = Новый Соответствие;
	
	ШиринаРесурса = 9;
	
	Результат.Вставить("Регистратор",45);
	Результат.Вставить("ДокументРегистратор",40);
	Результат.Вставить("АналитикаУчетаПоПартнерам",55);
	Результат.Вставить("ОбъектРасчетов",40);
	Результат.Вставить("ВалютаРасчетов",10);
	Результат.Вставить("Операция",40);
	Результат.Вставить("ЭтоРасчетыСКлиентами",20);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти