
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("Распоряжение", Распоряжение) Тогда
		ВызватьИсключение НСтр("ru = 'Обработка не предназначена для непосредственного использования.';
								|en = 'This data processor is not intended for manual use.'");
	КонецЕсли;
	
	ЗагрузитьНастройкиФормы();
	УстановитьОтборПоказыватьЗавершенныеЭтапы(ЭтапыДинамические, ПоказыватьЗавершенныеДинамическиеЭтапы);
	
	ПрочитатьДанныеРаспоряжения();
	
	НастроитьФормуПриСоздании();
	
	ОбновитьДанныеГрафика();
	
	УправлениеДоступностью(ЭтотОбъект);
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	НачатьОжиданиеДлительныхОпераций();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ГрафикЗапланирован И НЕ ДинамическаяСтруктура Тогда
		
		Отказ = Истина;
		ПоказатьВопросОСохраненииГрафикаПередЗакрытием();
		
	ИначеЕсли УдалитьРезультатыПередЗакрытием Тогда
		
		Отказ = Истина;
		УдалитьРезультатыПланированияНаКлиенте(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОтменитьДлительныеОперацииПриЗакрытии();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьТолькоЗадерживающиеЗаказПриИзменении(Элемент)
	
	УстановитьОтборТолькоЗадерживающиеЗаказЭтапы(Этапы, ТолькоЗадерживающиеЗаказЭтапы);
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияЗадержкиВОбеспеченииОтсутствуютНажатие(Элемент)
	
	РасшифроватьОбеспечениеМатериаламиПоМодели();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьЗавершенныеДинамическиеЭтапыПриИзменении(Элемент)
	
	УстановитьОтборПоказыватьЗавершенныеЭтапы(ЭтапыДинамические, ПоказыватьЗавершенныеДинамическиеЭтапы);
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДиаграммыФормыСрокиВыполнения

&НаКлиенте
Процедура СрокиВыполненияОбработкаРасшифровки(Элемент, Расшифровки, СтандартнаяОбработка, Дата)
	
	Если ТипЗнч(Расшифровки) = Тип("Массив")
		И Расшифровки.Количество() > 1 Тогда
		
		ЗначениеРасшифровки  = Расшифровки[1];
		
		Если ТипЗнч(ЗначениеРасшифровки) = Тип("Число") Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗаказНаПроизводство", Распоряжение);
			ПараметрыФормы.Вставить("СтатусГрафика", ЗначениеРасшифровки);
			
			ОткрытьФорму(
				"Отчет.МониторингЗаказа.ФормаОбъекта",
				ПараметрыФормы,, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДиаграммыФормыЗагрузкаОборудования

&НаКлиенте
Процедура ЗагрузкаОборудованияВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	ОбработкаРасшифровкиЗагрузкаОборудования(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузкаОборудованияОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	ОбработкаРасшифровкиЗагрузкаОборудования(СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовДиаграммыФормыОбеспечениеМатериалами

&НаКлиенте
Процедура ОбеспечениеМатериаламиВыбор(Элемент, ЗначениеДиаграммы, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьОбеспечениеМатериаламиПоМодели();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбеспечениеМатериаламиОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РасшифроватьОбеспечениеМатериаламиПоМодели();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОшибки

&НаКлиенте
Процедура ОшибкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ДанныеСтроки = Ошибки.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	КодыОшибок = КодыОшибокПланирования();
	
	Если ДанныеСтроки.Расшифровка.КодОшибки = КодыОшибок.КодОшибкиДоступностьВРЦ Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("РежимРаботы", ПредопределенноеЗначение(
			"Перечисление.РежимыРедактированияДоступностиВидовРЦ.ВводДоступностиДляФормированияГрафикаПроизводства"));
		
		Если ЗначениеЗаполнено(ДанныеСтроки.Расшифровка.ВидРабочегоЦентра) Тогда
			ПараметрыФормы.Вставить("ВидРабочегоЦентра", ДанныеСтроки.Расшифровка.ВидРабочегоЦентра);
			ПараметрыФормы.Вставить("НачалоПериода", ДанныеСтроки.Расшифровка.Период);
		Иначе
			ПараметрыФормы.Вставить("Подразделение", ДанныеСтроки.Расшифровка.Подразделение);
		КонецЕсли;
		
		ОткрытьФорму("Обработка.ДоступностьВидовРабочихЦентров.Форма", ПараметрыФормы);
		
	ИначеЕсли ДанныеСтроки.Расшифровка.КодОшибки = КодыОшибок.КодОшибкиГрафикРаботыПодразделения Тогда
		
		ПоказатьЗначение(, ДанныеСтроки.Расшифровка.Подразделение);
		
	ИначеЕсли ДанныеСтроки.Расшифровка.КодОшибки = КодыОшибок.КодОшибкиГрафикРаботыПредприятия Тогда
		
		ОткрытьФорму("Обработка.ПанельАдминистрированияУТ.Форма.Предприятие");
		
	ИначеЕсли ДанныеСтроки.Расшифровка.КодОшибки = КодыОшибок.КодОшибкиОтсутствуетГрафикПредшественника Тогда
		
		ПараметрыФормы = Новый Структура("Распоряжение", ДанныеСтроки.Расшифровка.Распоряжение);
		ОткрытьФорму(
			"Обработка.ПланированиеГрафикаПроизводства2_2.Форма.ПланированиеГрафикаЗаказа",
			ПараметрыФормы,
			,
			ДанныеСтроки.Расшифровка.Распоряжение);
			
	ИначеЕсли ДанныеСтроки.Расшифровка.КодОшибки = КодыОшибок.КодОшибкиЕстьЦиклыВЦепочкеЭтапов Тогда
		
		ПоказатьЗначение(, ДанныеСтроки.Расшифровка.ЭтапПроизводства);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыЭтапыДинамические

&НаКлиенте
Процедура ЭтапыДинамическиеПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтапыДинамическиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ЭтапыДинамические.ТекущиеДанные;
	
	Если Поле.Имя = "ЭтапыДинамическиеПодразделение" Тогда
		ПоказатьЗначение(, ТекущиеДанные.Подразделение);
	ИначеЕсли Поле.Имя = "ЭтапыДинамическиеСпецификация" Тогда
		ПоказатьЗначение(, ТекущиеДанные.Спецификация);
	ИначеЕсли Поле.Имя = "ЭтапыДинамическиеНоменклатура" Тогда
		ПоказатьЗначение(, ТекущиеДанные.Номенклатура);
	ИначеЕсли Поле.Имя = "ЭтапыДинамическиеЗаказНаПроизводство" Тогда
		ПоказатьЗначение(, ТекущиеДанные.ЗаказНаПроизводство);
	Иначе
		ПоказатьЗначение(, ТекущиеДанные.Этап);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Планировать(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборНастроекПланированияЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗаказНаПроизводство", Распоряжение);
	ПараметрыФормы.Вставить("ДинамическаяСтруктура", ДинамическаяСтруктура);
	
	ОткрытьФорму("Обработка.ПланированиеГрафикаПроизводства2_2.Форма.НастройкиПланированияРаспоряжения",
		ПараметрыФормы,
		,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	Если ГрафикЗапланирован Тогда
		
		СохранитьНаКлиенте(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Перечитать(Команда)
	
	Если (ГрафикЗапланирован ИЛИ МодельЗапланирована) И НЕ ДинамическаяСтруктура Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросПеречитатьДанныеЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные изменены. Перечитать данные?';
							|en = 'Data is changed. Reread?'");
		Кнопки = РежимДиалогаВопрос.ДаНет;
		КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,, КнопкаПоУмолчанию);
		
	Иначе
		
		ПеречитатьДанныеНаСервере();
		НачатьОжиданиеДлительныхОпераций();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаСмежныхЭтапов(Команда)
	
	Если Не ПроверитьВыделеннуюВСпискеСтроку(Элементы.Этапы) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтапПроизводства = Элементы.Этапы.ТекущаяСтрока;
	СтатусГрафика = СтатусГрафикаСписокЭтапы(ЭтотОбъект);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрКоманды", ЭтапПроизводства);
	ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафика);
	
	ОткрытьФорму(
		"Отчет.ДиаграммаСмежныхЭтаповПроизводства.ФормаОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтапПроизводства);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиагностикаЭтапа(Команда)
	
	Если Не ПроверитьВыделеннуюВСпискеСтроку(Элементы.Этапы) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтапПроизводства = Элементы.Этапы.ТекущаяСтрока;
	СтатусГрафика = СтатусГрафикаСписокЭтапы(ЭтотОбъект);
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрКоманды", ЭтапПроизводства);
	ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафика);
	
	ОткрытьФорму(
		"Отчет.ДиагностикаЭтапаПроизводства.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		ЭтапПроизводства);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиагностикаЭтапаДинамического(Команда)
	
	Если Не ПроверитьВыделеннуюВСпискеСтроку(Элементы.ЭтапыДинамические) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЭтапыДинамические.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	Если ТипЗнч(ТекущиеДанные.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		ПараметрыФормы.Вставить("ПараметрКоманды", ТекущиеДанные.Этап);
	Иначе
		ПараметрыФормы.Вставить("Этап", ТекущиеДанные.Этап);
		ПараметрыФормы.Вставить("КлючПартия", ТекущиеДанные.КлючПартия);
		ПараметрыФормы.Вставить("ЗаказНаПроизводство", Распоряжение);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафикаСписокЭтапы(ЭтотОбъект));
	
	КлючУникальности = Строка(ТекущиеДанные.КлючПартия)
		+ Строка(ТекущиеДанные.Этап.УникальныйИдентификатор());
	
	ОткрытьФорму(
		"Отчет.ДиагностикаЭтапаПроизводства.Форма",
		ПараметрыФормы,
		ЭтотОбъект,
		КлючУникальности);
	
КонецПроцедуры

&НаКлиенте
Процедура ДиаграммаСмежныхДинамическихЭтапов(Команда)
	
	Если Не ПроверитьВыделеннуюВСпискеСтроку(Элементы.ЭтапыДинамические) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЭтапыДинамические.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СтатусГрафика", СтатусГрафикаСписокЭтапы(ЭтотОбъект));
	Если ТипЗнч(ТекущиеДанные.Этап) = Тип("ДокументСсылка.ЭтапПроизводства2_2") Тогда
		ПараметрыФормы.Вставить("ПараметрКоманды", ТекущиеДанные.Этап);
	Иначе
		ПараметрыФормы.Вставить("Этап", ТекущиеДанные.Этап);
		ПараметрыФормы.Вставить("КлючПартия", ТекущиеДанные.КлючПартия);
		ПараметрыФормы.Вставить("ЗаказНаПроизводство", Распоряжение);
	КонецЕсли;
	
	КлючУникальности = Строка(ТекущиеДанные.КлючПартия)
		+ Строка(ТекущиеДанные.Этап.УникальныйИдентификатор());
		
	ОткрытьФорму(
		"Отчет.ДиаграммаСмежныхЭтаповПроизводства.ФормаОбъекта",
		ПараметрыФормы,
		ЭтотОбъект,
		КлючУникальности);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Планирование

&НаКлиенте
Процедура ВыборНастроекПланированияЗавершение(НастройкиРасчета, ДополнительныеПараметры) Экспорт
	
	Если НастройкиРасчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	НачатьОжидание = ПланироватьГрафикВФоне(НастройкиРасчета);
	
	Если НачатьОжидание Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("ПланироватьГрафикВФонеЗавершение", ЭтотОбъект),
			ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
	Иначе
		ПослеПланированияГрафикаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПланироватьГрафикВФоне(НастройкиРасчета)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Обработки.ПланированиеГрафикаПроизводства2_2.НаименованиеЗаданияРасчетаГрафика();
	
	Если ДинамическаяСтруктура Тогда
		ИмяПроцедуры = "ГрафикПроизводстваСтруктурыЗаказа.РассчитатьРабочийГрафикИМодель";
		
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
			ПротоколРасчета = Документы.ЗаказНаПроизводство2_2.ПротоколРасчетаСтруктурыЗаказа(Распоряжение);
			
			Если ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитан
				Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.РассчитанЕстьОшибки
				Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.НеРассчитанЕстьОшибки
				Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Отменяется Тогда
				ОбщегоНазначения.СообщитьПользователю(
					НСтр("ru = 'Структура заказа на производство не рассчитана. Рекомендуется пересчитать график после расчета структуры.';
						|en = 'Структура заказа на производство не рассчитана. Рекомендуется пересчитать график после расчета структуры.'"),
					Распоряжение);
			ИначеЕсли ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.Рассчитывается
				Или ПротоколРасчета.Состояние = ПротоколРасчета.КодыСостояний.ОжидаетРасчета Тогда
				ПараметрыВыполнения.ЗапуститьВФоне = Истина;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ИмяПроцедуры = "УправлениеПроизводствомПереопределяемый.ПланироватьПредварительныйГрафикРаспоряжения";
	КонецЕсли;
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, НастройкиРасчета, ПараметрыВыполнения);
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		НачатьОжидание = Истина;
	Иначе
		ОбработатьРезультатПланированияГрафика(ДлительнаяОперация);
		НачатьОжидание = Ложь;
		ДлительнаяОперация = Неопределено;
	КонецЕсли;
	
	Возврат НачатьОжидание;
	
КонецФункции

&НаКлиенте
Процедура ПланироватьГрафикВФонеЗавершение(Результат, ДопПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат <> Неопределено Тогда
		ОбработатьРезультатПланированияГрафика(Результат);
		ПослеПланированияГрафикаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатПланированияГрафика(Результат)
	
	Если Результат.Статус = "Ошибка" Тогда
		
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
		
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		
		РезультатСтруктура = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ТипЗнч(РезультатСтруктура) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(
				ЭтотОбъект,
				РезультатСтруктура,
				"ГрафикЗапланирован, МодельЗапланирована, НомерЗаданияКРасчетуГрафикаПроизводства, ОтменитьРучныеИзмененияГрафика");
			РазмещенныеЭтапы.ЗагрузитьЗначения(РезультатСтруктура.РазмещенныеЭтапы);
			
			ЗначениеВРеквизитФормы(РезультатСтруктура.Ошибки, "Ошибки");
			СообщитьОбОшибкахПриПланировании();
			
			ПоказатьОповещения(РезультатСтруктура.Оповещения);
			
			УправлениеДоступностью(ЭтотОбъект);
			
			ОбновитьДанныеГрафика();
			
			Если МодельЗапланирована
				ИЛИ ГрафикЗапланирован И НЕ ДинамическаяСтруктура Тогда
				УдалитьРезультатыПередЗакрытием = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПланированияГрафикаНаКлиенте()
	
	НачатьОжиданиеДлительныхОпераций();
	Если ДинамическаяСтруктура И ГрафикЗапланирован Тогда
		Оповестить("Запись_ГрафикПроизводства", Распоряжение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСНесколькимиФЗ

&НаСервере
Процедура ЗаполнитьДлительныеОперацииФормы()
	
	НоваяСтрока = ДлительныеОперацииФормы.Добавить();
	НоваяСтрока.Имя = ИмяДлительнойОперацииСрокиВыпуска();
	НоваяСтрока.НачатьОжидание = Ложь;
	НоваяСтрока.ДлительнаяОперация = Неопределено;
	
	НоваяСтрока = ДлительныеОперацииФормы.Добавить();
	НоваяСтрока.Имя = ИмяДлительнойОперацииЗагрузкаОборудования();
	НоваяСтрока.НачатьОжидание = Ложь;
	НоваяСтрока.ДлительнаяОперация = Неопределено;
	
	НоваяСтрока = ДлительныеОперацииФормы.Добавить();
	НоваяСтрока.Имя = ИмяДлительнойОперацииОбеспечениеМатериалами();
	НоваяСтрока.НачатьОжидание = Ложь;
	НоваяСтрока.ДлительнаяОперация = Неопределено;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ОписаниеДлительнойОперации(Форма, Имя)
	
	СтруктураПоиска = Новый Структура("Имя", Имя);
	НайденныеСтроки = Форма.ДлительныеОперацииФормы.НайтиСтроки(СтруктураПоиска);
	Если НайденныеСтроки.Количество() = 0 Тогда
		Результат = Неопределено;
	Иначе
		Результат = НайденныеСтроки[0];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОтменитьДлительныеОперацииПриЗакрытии()
	
	Для каждого Строка Из ДлительныеОперацииФормы Цикл
		Если НЕ Строка.ДлительнаяОперация = Неопределено Тогда
			
			ОтменитьДлительнуюОперацию(Строка.Имя);

		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьДлительнуюОперацию(Имя)
	
	ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, Имя);
	
	Если НЕ ОписаниеОперации.ДлительнаяОперация = Неопределено Тогда
		
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ОписаниеОперации.ДлительнаяОперация.ИдентификаторЗадания);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДлительнойОперацииСрокиВыпуска()
	
	Возврат "ЧтениеСроковВыпускаПродукции";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДлительнойОперацииЗагрузкаОборудования()
	
	Возврат "ЧтениеЗагрузкиОборудования";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяДлительнойОперацииОбеспечениеМатериалами()
	
	Возврат "ЧтениеОбеспеченияМатериалами";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусДлительнойОперацииВыполняется()
	
	Возврат "Выполняется";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусДлительнойОперацииВыполнено()
	
	Возврат "Выполнено";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СтатусДлительнойОперацииОшибка()
	
	Возврат "Ошибка";
	
КонецФункции

&НаКлиенте
Процедура НачатьОжиданиеДлительныхОпераций()
	
	Для каждого Строка Из ДлительныеОперацииФормы Цикл
		
		Если Строка.НачатьОжидание Тогда
			
			ОповещениеОЗавершении = ОповещениеОЗавершенииДлительнойОперации(Строка.Имя);
			
			ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
			ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
			
			ДлительныеОперацииКлиент.ОжидатьЗавершение(
				Строка.ДлительнаяОперация,
				ОповещениеОЗавершении,
				ПараметрыОжидания);
			
			ОтобразитьНачалоОжиданияДлительнойОперации(Строка.Имя);
			
			Строка.НачатьОжидание = Ложь;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Функция ОповещениеОЗавершенииДлительнойОперации(Имя)
	
	Если Имя = ИмяДлительнойОперацииСрокиВыпуска() Тогда
		
		Результат = Новый ОписаниеОповещения("ПрочитатьСрокиВыпускаПродукцииВФонеЗавершение", ЭтотОбъект);
		
	ИначеЕсли Имя = ИмяДлительнойОперацииЗагрузкаОборудования() Тогда
		
		Результат = Новый ОписаниеОповещения("ЗаполнитьДиаграммуЗагрузкаОборудованияВФонеЗавершение", ЭтотОбъект);
		
	ИначеЕсли Имя = ИмяДлительнойОперацииОбеспечениеМатериалами() Тогда
		
		Результат = Новый ОписаниеОповещения("ЗаполнитьДиаграммуОбеспечениеМатериаламиВФонеЗавершение", ЭтотОбъект);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтобразитьНачалоОжиданияДлительнойОперации(Имя)
	
	Если Имя = ИмяДлительнойОперацииСрокиВыпуска() Тогда
		
		Элементы.ГруппаПродукцияДлительнаяОперация.ТекущаяСтраница = Элементы.СтраницаПродукцияДлительнаяОперация;
		
	ИначеЕсли Имя = ИмяДлительнойОперацииЗагрузкаОборудования() Тогда
		
		Элементы.ГруппаЗагрузкаОборудованияДлительнаяОперация.ТекущаяСтраница =
			Элементы.СтраницаЗагрузкаОборудованияДлительнаяОперация;
	
	ИначеЕсли Имя = ИмяДлительнойОперацииОбеспечениеМатериалами() Тогда
		
		Элементы.ГруппаОбеспечениеМатериаламиДлительнаяОперация.ТекущаяСтраница =
			Элементы.СтраницаОбеспечениеМатериаламиДлительнаяОперация;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЧтениеОбновлениеДанных

&НаСервере
Процедура ПеречитатьДанныеНаСервере()
	
	Ошибки.Очистить();
	
	ПрочитатьДанныеРаспоряжения();
	ОбновитьДанныеГрафика();
	
	ПеречитываниеДанных = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеРаспоряжения()
	
	Если Распоряжение.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Заказ.Дата КАК Дата,
		|	Заказ.Номер КАК Номер,
		|	Заказ.ТипПроизводственногоПроцесса КАК ТипПроизводственногоПроцесса,
		|	Заказ.ДинамическаяСтруктура КАК ДинамическаяСтруктура,
		|	Заказ.НачатьНеРанее КАК НачатьНеРанее
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Заказ
		|ГДЕ
		|	Заказ.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(Продукция.ДатаПотребности) КАК ДатаПотребности
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2.Продукция КАК Продукция
		|ГДЕ
		|	Продукция.Ссылка = &Ссылка
		|ИМЕЮЩИЕ
		|	КОЛИЧЕСТВО(Продукция.Ссылка) > 0");
	Запрос.УстановитьПараметр("Ссылка", Распоряжение);
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ДанныеРаспоряжения = РезультатыЗапроса[0].Выбрать();
	ДанныеРаспоряжения.Следующий();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеРаспоряжения, "НачатьНеРанее, ДинамическаяСтруктура");
	
	ДанныеТабличнойЧасти = РезультатыЗапроса[1].Выбрать();
	Если ДанныеТабличнойЧасти.Следующий() Тогда
		ДатаПотребности = ДанныеТабличнойЧасти.ДатаПотребности;
	Иначе
		ДатаПотребности = '00010101';
	КонецЕсли;
	
	ЭтоРазборка = (ДанныеРаспоряжения.ТипПроизводственногоПроцесса = Перечисления.ТипыПроизводственныхПроцессов.Разборка);
	
	НомерРаспоряжения = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(
		ДанныеРаспоряжения.Номер, Ложь, Истина);
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Планирование графика производства заказа № %1 от %2';
			|en = 'Production schedule creating for order No.%1 dated %2'"),
		НомерРаспоряжения,
		Формат(ДанныеРаспоряжения.Дата, "ДЛФ=D"));
	Элементы.ФормаСохранить.Видимость = НЕ ДинамическаяСтруктура;
	Элементы.ГруппаСтраницыСписокЭтапов.ТекущаяСтраница = ?(ДинамическаяСтруктура,
		Элементы.СтраницаСписокЭтаповДинамических,
		Элементы.СтраницаСписокЭтапов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеГрафика()
	
	ПрочитатьСрокиГрафика();
	
	ЗаполнитьДиаграммуСрокиВыполнения();
	ЗаполнитьДиаграммуЗагрузкаОборудованияВФоне();
	ЗаполнитьДиаграммуОбеспечениеМатериаламиВФоне();
	
	ПрочитатьСрокиВыпускаПродукцииВФоне();
	
	ЗаполнитьИнформационныеНадписи();
	
	УстановитьПараметрыСпискаЭтапы();
	
	ПоказатьСкрытьОшибки();
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСрокиГрафика()
	
	СтатусРабочийГрафик = СтатусРабочийГрафик();
	СтатусПредварительныйГрафик = СтатусПредварительныйГрафик();
	СтатусМодель = СтатусМодельГрафика();
	
	СтатусыГрафика = Новый Массив;
	Если ГрафикЗапланирован И НЕ ДинамическаяСтруктура Тогда
		СтатусыГрафика.Добавить(СтатусПредварительныйГрафик);
	Иначе
		СтатусыГрафика.Добавить(СтатусРабочийГрафик);
	КонецЕсли;
	Если МодельЗапланирована Тогда
		СтатусыГрафика.Добавить(СтатусМодель);
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГрафикЭтаповПроизводства2_2.Этап КАК Этап
	|ПОМЕСТИТЬ ВТРазмещенныеЭтапы
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|ГДЕ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика <> &СтатусРабочийГрафик
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Этап
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ГрафикЭтаповПроизводства2_2.Начало) КАК ДатаЗапуска
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТРазмещенныеЭтапы КАК ВТРазмещенныеЭтапы
	|		ПО ГрафикЭтаповПроизводства2_2.Этап = ВТРазмещенныеЭтапы.Этап
	|ГДЕ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик
	|	И ВТРазмещенныеЭтапы.Этап ЕСТЬ NULL 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ГрафикЭтаповПроизводства2_2.СтатусГрафика,
	|	МИНИМУМ(ГрафикЭтаповПроизводства2_2.Начало) КАК ДатаЗапуска,
	|	МАКСИМУМ(ГрафикЭтаповПроизводства2_2.Окончание) КАК ДатаВыпуска
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|ГДЕ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика В(&СтатусыГрафика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ГрафикЭтаповПроизводства2_2.СтатусГрафика");
	
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", Распоряжение);
	Запрос.УстановитьПараметр("СтатусыГрафика", СтатусыГрафика);
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатов[1].Выбрать();
	Если Выборка.Следующий() И ЗначениеЗаполнено(Выборка.ДатаЗапуска) Тогда
		ДатаЗапускаРабочийГрафик = Выборка.ДатаЗапуска;
	Иначе
		ДатаЗапускаРабочийГрафик = Неопределено;
	КонецЕсли;
	
	Выборка = МассивРезультатов[2].Выбрать();
	
	СрокиГрафикаЗаполнены = Ложь;
	СрокиМоделиЗаполнены = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.СтатусГрафика = СтатусПредварительныйГрафик
			ИЛИ Выборка.СтатусГрафика = СтатусРабочийГрафик Тогда
			
			ДатаЗапускаГрафик = Выборка.ДатаЗапуска;
			ДатаВыпускаГрафик = Выборка.ДатаВыпуска;
			СрокиГрафикаЗаполнены = Истина;
			
		ИначеЕсли Выборка.СтатусГрафика = СтатусМодель Тогда
			
			ДатаЗапускаМодель = Выборка.ДатаЗапуска;
			ДатаВыпускаМодель = Выборка.ДатаВыпуска;
			СрокиМоделиЗаполнены = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ДатаЗапускаРабочийГрафик) Тогда
		
		Если ЗначениеЗаполнено(ДатаЗапускаГрафик) Тогда
			ДатаЗапускаГрафик = Мин(ДатаЗапускаГрафик, ДатаЗапускаРабочийГрафик);
		КонецЕсли;
		Если ЗначениеЗаполнено(ДатаЗапускаМодель) Тогда
			ДатаЗапускаМодель = Мин(ДатаЗапускаМодель, ДатаЗапускаРабочийГрафик);
		КонецЕсли;
		
	КонецЕсли;
	
	Если СрокиГрафикаЗаполнены Тогда
		
		Если ЗначениеЗаполнено(ДатаПотребности) Тогда
			ОпозданиеГрафик = РазностьДатСтрокой(ДатаПотребности, ДатаВыпускаГрафик);
		Иначе
			ОпозданиеГрафик = "";
		КонецЕсли;
		
		ДлительностьГрафик = РазностьДатСтрокой(ДатаЗапускаГрафик, ДатаВыпускаГрафик);
		
	Иначе
		ДатаЗапускаГрафик = '00010101';
		ДатаВыпускаГрафик = '00010101';
		ОпозданиеГрафик = "";
		ДлительностьГрафик = "";
	КонецЕсли;
	
	Если СрокиМоделиЗаполнены Тогда
		
		Если ЗначениеЗаполнено(ДатаПотребности) Тогда
			ОпозданиеМодель = РазностьДатСтрокой(ДатаПотребности, ДатаВыпускаМодель);
		Иначе
			ОпозданиеМодель = "";
		КонецЕсли;
		
		ДлительностьМодель = РазностьДатСтрокой(ДатаЗапускаМодель, ДатаВыпускаМодель);
	Иначе
		ДатаЗапускаМодель = '00010101';
		ДатаВыпускаМодель = '00010101';
		ОпозданиеМодель = "";
		ДлительностьМодель = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьСрокиВыпускаПродукцииВФоне()
	
	ИмяОперации = ИмяДлительнойОперацииСрокиВыпуска();
	ОтменитьДлительнуюОперацию(ИмяОперации);
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Распоряжение", Распоряжение);
	ПараметрыПроцедуры.Вставить("ГрафикЗапланирован", ГрафикЗапланирован);
	ПараметрыПроцедуры.Вставить("МодельЗапланирована", МодельЗапланирована);
	ПараметрыПроцедуры.Вставить("ДинамическаяСтруктура", ДинамическаяСтруктура);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение сроков выпуска продукции';
															|en = 'Read product release time'");
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ДлительнаяОперацияФормы = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ПланированиеГрафикаПроизводства2_2.ПрочитатьСрокиВыпускаПродукции",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
	Если ДлительнаяОперацияФормы.Статус = СтатусДлительнойОперацииВыполняется() Тогда
		
		ОписаниеОперации.ДлительнаяОперация = ДлительнаяОперацияФормы;
		ОписаниеОперации.НачатьОжидание = Истина;
		
	Иначе
		
		ОписаниеОперации.ДлительнаяОперация = Неопределено;
		ОписаниеОперации.НачатьОжидание = Ложь;
		
		ОбработатьРезультатЧтенияСроковВыпускаПродукцииВФоне(ДлительнаяОперацияФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьСрокиВыпускаПродукции(АдресРезультата)
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ТипЗнч(Результат) = Тип("ТаблицаЗначений") Тогда
		
		Продукция.Загрузить(Результат);
		Для каждого Строка Из Продукция Цикл
			
			Если ЗначениеЗаполнено(Строка.ДатаЗапускаГрафик) Тогда
				
				Если ЗначениеЗаполнено(Строка.ДатаПотребности) Тогда
					Строка.ОпозданиеГрафик = РазностьДатСтрокой(
						Строка.ДатаПотребности, Строка.ДатаВыпускаГрафик);
				Иначе
					Строка.ОпозданиеГрафик = "";
				КонецЕсли;
				
				Строка.ДлительностьГрафик = РазностьДатСтрокой(
					Строка.ДатаЗапускаГрафик, Строка.ДатаВыпускаГрафик);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Строка.ДатаЗапускаМодель) Тогда
				Если ЗначениеЗаполнено(Строка.ДатаПотребности) Тогда
					Строка.ОпозданиеМодель = РазностьДатСтрокой(
						Строка.ДатаПотребности, Строка.ДатаВыпускаМодель);
				Иначе
					Строка.ОпозданиеМодель = "";
				КонецЕсли;
				
				Строка.ДлительностьМодель = РазностьДатСтрокой(
					Строка.ДатаЗапускаМодель, Строка.ДатаВыпускаМодель);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Элементы.ГруппаПродукцияДлительнаяОперация.ТекущаяСтраница = Элементы.СтраницаПродукцияДанные;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьСрокиВыпускаПродукции(Форма)
	
	Форма.Продукция.Очистить();
	Форма.Элементы.ГруппаПродукцияДлительнаяОперация.ТекущаяСтраница = Форма.Элементы.СтраницаПродукцияДанные;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочитатьСрокиВыпускаПродукцииВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяОперации = ИмяДлительнойОперацииСрокиВыпуска();
	ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
	ОписаниеОперации.ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьСрокиВыпускаПродукции(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатЧтенияСроковВыпускаПродукцииВФоне(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЧтенияСроковВыпускаПродукцииВФоне(Результат)
	
	Если Результат.Статус = СтатусДлительнойОперацииВыполнено() Тогда
		
		ЗагрузитьСрокиВыпускаПродукции(Результат.АдресРезультата);
		
	Иначе
		
		ОчиститьИПоказатьСрокиВыпускаПродукции(ЭтотОбъект);
		
		Если Результат.Статус = СтатусДлительнойОперацииОшибка() Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформационныеНадписи()
	
	РазмещеноВручную = 0;
	РассчитатьГрафик = 0;
	ЕстьПриоритетные = Ложь;
	ЕстьЗаписиВГрафике = Ложь;
	
	ПрочитатьДанныеИнформационныхНадписей(РазмещеноВручную, РассчитатьГрафик, ЕстьПриоритетные, ЕстьЗаписиВГрафике);
	
	Если РазмещеноВручную > 0 Тогда
		
		Элементы.НадписьРазмещенныеВручную.Заголовок = СтрШаблон(
			НСтр("ru = 'Имеются этапы, запланированные вручную (всего: %1).';
				|en = 'There are stages scheduled manually (total: %1).'"),
			РазмещеноВручную);
		Элементы.ГруппаРазмещенныеВручную.Видимость = Истина;
		
	Иначе
		
		Элементы.ГруппаРазмещенныеВручную.Видимость = Ложь;
		
	КонецЕсли;
	
	Если РассчитатьГрафик > 0 Тогда
		
		Если ДинамическаяСтруктура Тогда
			Элементы.НадписьАктуальностьГрафика.Заголовок = 
				НСтр("ru = 'График заказа не актуален. Имеются этапы, требующие перепланирования.';
					|en = 'Order schedule is not relevant. There are stages which require rescheduling.'");
		Иначе
			Элементы.НадписьАктуальностьГрафика.Заголовок = СтрШаблон(
				НСтр("ru = 'График заказа не актуален. Имеются этапы, требующие перепланирования (всего: %1).';
					|en = 'Order schedule is not relevant. There are stages which require replanning (total: %1).'"),
				РассчитатьГрафик);
		КонецЕсли;
		
	Иначе
		
		Элементы.НадписьАктуальностьГрафика.Заголовок = НСтр("ru = 'График заказа актуален.';
															|en = 'Order schedule is relevant.'");
		
	КонецЕсли;
	
	Элементы.ГруппаНаличиеБолееПриоритетныхЗаказов.Видимость = ЕстьПриоритетные;
	Элементы.ГруппаТребуетсяРассчитатьГрафик.Видимость = Не ЕстьЗаписиВГрафике;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИнформационныхНадписей(РазмещеноВручную, РассчитатьГрафик, ЕстьПриоритетные, ЕстьЗаписиВГрафике)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.ЗаказНаПроизводство2_2.СоздатьВТЗаказыСБольшимПриоритетом(
		МенеджерВременныхТаблиц,
		"ВТПриоритетныеЗаказы",
		Распоряжение);
	
	Обработки.ПланированиеГрафикаПроизводства2_2.ПрочитатьКоличествоОбъектовДляПланирования(
		РазмещеноВручную,
		РассчитатьГрафик,
		ЕстьПриоритетные,
		ЕстьЗаписиВГрафике,
		Распоряжение,
		МенеджерВременныхТаблиц,
		"ВТПриоритетныеЗаказы");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыСпискаЭтапы()
	
	СтатусГрафика = СтатусГрафикаСписокЭтапы(ЭтотОбъект);
	
	Этапы.Параметры.УстановитьЗначениеПараметра("Распоряжение", Распоряжение);
	Этапы.Параметры.УстановитьЗначениеПараметра("СтатусГрафика", СтатусГрафика);
	
	ЭтапыДинамические.Параметры.УстановитьЗначениеПараметра("Распоряжение", Распоряжение);
	ЭтапыДинамические.Параметры.УстановитьЗначениеПараметра("СтатусГрафика", СтатусГрафика);
	
	СтатусыЭтапа = Новый Массив;
	СтатусыЭтапа.Добавить(Перечисления.СтатусыЭтаповПроизводства2_2.Формируется);
	СтатусыЭтапа.Добавить(Перечисления.СтатусыЭтаповПроизводства2_2.Сформирован);
	СтатусыЭтапа.Добавить(Перечисления.СтатусыЭтаповПроизводства2_2.КВыполнению);
	Этапы.Параметры.УстановитьЗначениеПараметра("СтатусыЭтапа", СтатусыЭтапа);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтатусГрафикаСписокЭтапы(Форма)
	
	Если Форма.ГрафикЗапланирован И НЕ Форма.ДинамическаяСтруктура Тогда
		Результат = СтатусПредварительныйГрафик();
	Иначе
		Результат = СтатусРабочийГрафик();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура СообщитьОбОшибкахПриПланировании()
	
	Если Ошибки.Количество() > 0 Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'При планировании графика возникли ошибки.';
				|en = 'Errors occurred when planning the schedule.'"),
			,
			"Ошибки");
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьОповещения(Оповещения)
	
	Если ТипЗнч(Оповещения) = Тип("Массив") Тогда
		Для каждого Оповещение Из Оповещения Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Оповещение);
		КонецЦикла;
	ИначеЕсли ТипЗнч(Оповещения) = Тип("Соответствие") Тогда
		Для каждого КлючИЗначение Из Оповещения Цикл
			ОбщегоНазначения.СообщитьПользователю(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоказатьСкрытьОшибки()
	
	Элементы.СтраницаОшибки.Видимость = НЕ (Ошибки.Количество() = 0);
	
КонецПроцедуры

#КонецОбласти

#Область Сохранение

&НаКлиенте
Процедура СохранитьНаКлиенте(ПередЗакрытием)
	
	СохранениеПередЗакрытием = ПередЗакрытием;
	
	НачатьОжидание = СохранитьНаСервере();
	
	Если НачатьОжидание Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("СохранитьГрафикВФонеЗавершение", ЭтотОбъект),
			ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
	Иначе
		ПослеСохраненияГрафикаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()
	
	ИмяПроцедуры = "Обработки.ПланированиеГрафикаПроизводства2_2.ЗаписатьПредварительныйГрафикВРабочий";
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Распоряжение", Распоряжение);
	ПараметрыПроцедуры.Вставить("НомерЗаданияКРасчетуГрафикаПроизводства", НомерЗаданияКРасчетуГрафикаПроизводства);
	ПараметрыПроцедуры.Вставить("ОтменитьРучныеИзмененияГрафика", ОтменитьРучныеИзмененияГрафика);
	ПараметрыПроцедуры.Вставить("РазмещенныеЭтапы", РазмещенныеЭтапы.ВыгрузитьЗначения());
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Сохранение графика производства заказа';
															|en = 'Сохранение графика производства заказа'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, ПараметрыПроцедуры, ПараметрыВыполнения);
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		НачатьОжидание = Истина;
	Иначе
		ОбработатьРезультатСохраненияГрафика(ДлительнаяОперация);
		НачатьОжидание = Ложь;
		ДлительнаяОперация = Неопределено;
	КонецЕсли;
	
	Возврат НачатьОжидание;
	
КонецФункции

&НаКлиенте
Процедура СохранитьГрафикВФонеЗавершение(Результат, ДопПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат <> Неопределено Тогда
		ОбработатьРезультатСохраненияГрафика(Результат);
		ПослеСохраненияГрафикаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьРезультатСохраненияГрафика(Результат)
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеСохраненияГрафикаНаКлиенте()
	
	ОчиститьРезультатыПланирования();
	Оповестить("Запись_ГрафикПроизводства", Распоряжение);
	
	Если СохранениеПередЗакрытием Тогда
		
		Закрыть();
		
	Иначе
		
		УправлениеДоступностью(ЭтотОбъект);
		ОбновитьДанныеГрафика();
		НачатьОжиданиеДлительныхОпераций();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВопросОСохраненииГрафикаПередЗакрытием()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОСохраненииПередЗакрытиемЗавершение", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?';
						|en = 'Data has changed. Save the changes?'");
	Кнопки = РежимДиалогаВопрос.ДаНетОтмена;
	КнопкаПоУмолчанию = КодВозвратаДиалога.Отмена;
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,, КнопкаПоУмолчанию);
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОСохраненииПередЗакрытиемЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ОтветПользователя = РезультатЗакрытия;
	
	Если ОтветПользователя = КодВозвратаДиалога.Да Тогда
		
		СохранитьНаКлиенте(Истина);
		
	ИначеЕсли ОтветПользователя = КодВозвратаДиалога.Нет Тогда
		
		УдалитьРезультатыПланированияНаКлиенте(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УдалениеРезультатовПланирования

&НаКлиенте
Процедура УдалитьРезультатыПланированияНаКлиенте(ПередЗакрытием)
	
	УдалениеРезультатовПередЗакрытием = ПередЗакрытием;
	
	НачатьОжидание = УдалитьРезультатыПланированияНаСервере();
	
	Если НачатьОжидание Тогда
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			Новый ОписаниеОповещения("УдалитьРезультатыПланированияВФонеЗавершение", ЭтотОбъект),
			ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект));
	Иначе
		ПослеУдаленияРезультатовПланирования();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УдалитьРезультатыПланированияНаСервере()
	
	ИмяПроцедуры = "Обработки.ПланированиеГрафикаПроизводства2_2.ОчиститьПредварительныйГрафик";
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Очистка предварительного графика производства';
															|en = 'Очистка предварительного графика производства'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяПроцедуры, Распоряжение, ПараметрыВыполнения);
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		НачатьОжидание = Истина;
	Иначе
		ОбработатьУдалениеРезультатаПланирования(ДлительнаяОперация);
		НачатьОжидание = Ложь;
		ДлительнаяОперация = Неопределено;
	КонецЕсли;
	
	Возврат НачатьОжидание;
	
КонецФункции

&НаКлиенте
Процедура УдалитьРезультатыПланированияВФонеЗавершение(Результат, ДопПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат <> Неопределено Тогда
		ОбработатьУдалениеРезультатаПланирования(Результат);
		ПослеУдаленияРезультатовПланирования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьУдалениеРезультатаПланирования(Результат)
	
	Если Результат.Статус = "Ошибка" Тогда
		ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеУдаленияРезультатовПланирования()
	
	ОчиститьРезультатыПланирования();
	
	Если УдалениеРезультатовПередЗакрытием Тогда
		
		Закрыть();
		
	Иначе
		
		УправлениеДоступностью(ЭтотОбъект);
		
		Если ПеречитываниеДанных Тогда
			ПеречитатьДанныеНаСервере();
		Иначе
			ОбновитьДанныеГрафика();
		КонецЕсли;
		
		НачатьОжиданиеДлительныхОпераций();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьРезультатыПланирования()
	
	ГрафикЗапланирован = Ложь;
	МодельЗапланирована = Ложь;
	УдалитьРезультатыПередЗакрытием = Ложь;
	НомерЗаданияКРасчетуГрафикаПроизводства = 0;
	ОтменитьРучныеИзмененияГрафика = Ложь;
	Ошибки.Очистить();
	РазмещенныеЭтапы.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область ДиаграммаСрокиВыполнения

&НаСервере
Процедура НастроитьДиаграммуСрокиВыполнения()
	
	СрокиВыполнения.АвтоОпределениеПолногоИнтервала = Ложь;
	СрокиВыполнения.АвтоУстановкаТекстаТочек = Ложь;
	СрокиВыполнения.Анимация = АнимацияДиаграммы.НеИспользовать;
	СрокиВыполнения.ВертикальнаяПрокрутка = Ложь;
	СрокиВыполнения.Окантовка = Ложь;
	СрокиВыполнения.ОтображатьЛегенду = Ложь;
	СрокиВыполнения.ОтображатьПустыеЗначения = Ложь;
	СрокиВыполнения.ОтображатьЗаголовок = Ложь;
	СрокиВыполнения.ОтображениеТекстаЗначения = ОтображениеТекстаЗначенияДиаграммыГанта.НеОтображать;
	СрокиВыполнения.ПоддержкаМасштаба = ПоддержкаМасштабаДиаграммыГанта.ВсеДанные;
	СрокиВыполнения.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки);
	
	Элементы.ЦветРаспоряжение.ЦветФона = ЦветИнтервала(ВидИнтервалаРаспоряжение());
	Элементы.ЦветГрафик.ЦветФона = ЦветИнтервала(ВидИнтервалаГрафик());
	Элементы.ЦветМодель.ЦветФона = ЦветИнтервала(ВидИнтервалаМодель());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуСрокиВыполнения()
	
	СрокиВыполнения.Обновление = Ложь;
	СрокиВыполнения.Очистить();
	
	Точка = СрокиВыполнения.Точки.Добавить();
	
	Границы = Новый Структура("Начало, Окончание", '39991231', '00010101');
	
	Если ЗначениеЗаполнено(НачатьНеРанее) И ЗначениеЗаполнено(ДатаПотребности) И НЕ ЭтоРазборка Тогда
		ДобавитьИнтервалВСрокиВыполнения(Точка, НачатьНеРанее, ДатаПотребности, ВидИнтервалаРаспоряжение(), Границы);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЗапускаГрафик) И ЗначениеЗаполнено(ДатаВыпускаГрафик) Тогда
		ДобавитьИнтервалВСрокиВыполнения(Точка, ДатаЗапускаГрафик, ДатаВыпускаГрафик, ВидИнтервалаГрафик(), Границы);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДатаЗапускаМодель) И ЗначениеЗаполнено(ДатаВыпускаМодель) Тогда
		ДобавитьИнтервалВСрокиВыполнения(Точка, ДатаЗапускаМодель, ДатаВыпускаМодель, ВидИнтервалаМодель(), Границы);
	КонецЕсли;
	
	СрокиВыполнения.Обновление = Истина;
	
	Если ЗначениеЗаполнено(Границы.Окончание) Тогда
		НастроитьШкалуСрокиВыполнения(Границы.Начало, Границы.Окончание);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьИнтервалВСрокиВыполнения(Точка, Начало, Окончание, ВидИнтервала, Границы)
	
	Серия = СрокиВыполнения.Серии.Добавить();
	Серия.Текст = ВидИнтервала;
	Серия.Цвет = ЦветИнтервала(ВидИнтервала);
	
	Значение = СрокиВыполнения.ПолучитьЗначение(Точка, Серия);
	
	Интервал = Значение.Добавить();
	Интервал.Начало = Начало;
	Интервал.Конец = Окончание;
	
	Если ВидИнтервала = ВидИнтервалаРаспоряжение() Тогда
		Интервал.Расшифровка = Распоряжение;
	ИначеЕсли ВидИнтервала = ВидИнтервалаГрафик() Тогда
		Если ГрафикЗапланирован И НЕ ДинамическаяСтруктура Тогда
			Интервал.Расшифровка = СтатусПредварительныйГрафик();
		Иначе
			Интервал.Расшифровка = СтатусРабочийГрафик();
		КонецЕсли;
	ИначеЕсли ВидИнтервала = ВидИнтервалаМодель() Тогда
		Интервал.Расшифровка = СтатусМодельГрафика();
	КонецЕсли;
	
	Границы.Начало = Мин(Границы.Начало, Начало);
	Границы.Окончание = Макс(Границы.Окончание, Окончание);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьШкалуСрокиВыполнения(Начало, Окончание)
	
	НастройкиШкалы = УправлениеПроизводствомКлиентСервер.НастройкиШкалыДиаграммыГантаВРежимеВсеДанные(
		Начало, Окончание, 3);
	
	ШкалаВремениЭлементы = СрокиВыполнения.ОбластьПостроения.ШкалаВремени.Элементы;
	
	Для Индекс = 1 По ШкалаВремениЭлементы.Количество()-1 Цикл
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[Индекс]);
	КонецЦикла;
	
	ЭлементШкалы = ШкалаВремениЭлементы.Добавить();
	ЭлементШкалы.Единица = НастройкиШкалы.Единица;
	ЭлементШкалы.Формат = НастройкиШкалы.Формат;
	
	Если ШкалаВремениЭлементы.Количество() = 2 Тогда
		ШкалаВремениЭлементы.Удалить(ШкалаВремениЭлементы[0]);
	КонецЕсли;
	
	СрокиВыполнения.УстановитьПолныйИнтервал(
		НастройкиШкалы.НачалоПолногоИнтервала,
		НастройкиШкалы.ОкончаниеПолногоИнтервала);
	
КонецПроцедуры

&НаСервере
Функция ВидИнтервалаРаспоряжение()
	
	Возврат "Распоряжение";
	
КонецФункции

&НаСервере
Функция ВидИнтервалаГрафик()
	
	Возврат "График";
	
КонецФункции

&НаСервере
Функция ВидИнтервалаМодель()
	
	Возврат "Модель";
	
КонецФункции

&НаСервере
Функция ЦветИнтервала(ВидИнтервала)
	
	Если ВидИнтервала = ВидИнтервалаРаспоряжение() Тогда
		
		Результат = WebЦвета.СветлоЗеленый;
		
	ИначеЕсли ВидИнтервала = ВидИнтервалаГрафик() Тогда
		
		Результат = WebЦвета.Голубой;
		
	ИначеЕсли ВидИнтервала = ВидИнтервалаМодель() Тогда
		
		Результат = WebЦвета.Хаки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ДиаграммаЗагрузкаОборудования

&НаСервере
Процедура НастроитьДиаграммуЗагрузкаОборудования()
	
	Если НЕ ИспользуетсяПланированиеПоПроизводственнымРесурсам() Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьГистограмму(ЗагрузкаОборудования);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуЗагрузкаОборудованияВФоне()
	
	Если НЕ ИспользуетсяПланированиеПоПроизводственнымРесурсам() Тогда
		Возврат;
	КонецЕсли;
	
	Если МодельЗапланирована Тогда
		
		ИмяОперации = ИмяДлительнойОперацииЗагрузкаОборудования();
		ОтменитьДлительнуюОперацию(ИмяОперации);
		
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("Распоряжение", Распоряжение);
		ПараметрыПроцедуры.Вставить("КоличествоЕдиниц", 5);
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение загрузки оборудования по модели';
																|en = 'Reading the equipment load according to model'");
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
		ДлительнаяОперацияФормы = ДлительныеОперации.ВыполнитьВФоне(
			"Отчеты.ЗагрузкаОборудованияПоМоделиГрафикаПроизводства.ПолучитьНаиболееЗагруженноеОборудование",
			ПараметрыПроцедуры,
			ПараметрыВыполнения);
		
		ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
		Если ДлительнаяОперацияФормы.Статус = СтатусДлительнойОперацииВыполняется() Тогда
			
			ОписаниеОперации.ДлительнаяОперация = ДлительнаяОперацияФормы;
			ОписаниеОперации.НачатьОжидание = Истина;
			
		Иначе
			
			ОписаниеОперации.ДлительнаяОперация = Неопределено;
			ОписаниеОперации.НачатьОжидание = Ложь;
			
			ОбработатьРезультатЧтенияЗагрузкиОборудованияВФоне(ДлительнаяОперацияФормы);
			
		КонецЕсли;
		
	Иначе
		
		ОчиститьИПоказатьЗагрузкуОборудования(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДиаграммуЗагрузкаОборудованияВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяОперации = ИмяДлительнойОперацииЗагрузкаОборудования();
	ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
	ОписаниеОперации.ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьЗагрузкуОборудования(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатЧтенияЗагрузкиОборудованияВФоне(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЧтенияЗагрузкиОборудованияВФоне(Результат)
	
	Если Результат.Статус = СтатусДлительнойОперацииВыполнено() Тогда
		
		ЗаполнитьЗагрузкуОборудования(Результат.АдресРезультата);
		
	Иначе
		
		ОчиститьИПоказатьЗагрузкуОборудования(ЭтотОбъект);
		
		Если Результат.Статус = СтатусДлительнойОперацииОшибка() Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗагрузкуОборудования(АдресРезультата)
	
	ЗагрузкаОборудования.Обновление = Ложь;
	ЗагрузкаОборудования.Очистить();
		
	Данные = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ЗначениеЗаполнено(Данные) Тогда
		
		РеквизитыВРЦ = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
			Данные.ВыгрузитьКолонку("ВидРабочегоЦентра"),
			"Представление");
		
		Данные.Сортировать("ЗагрузкаПроценты Возр");
		
		Серия = ЗагрузкаОборудования.Серии.Добавить("Загрузка");
		Серия.Цвет = WebЦвета.Лосось;
		
		Для Индекс = 0 По Данные.Количество()-1 Цикл
			
			Расшифровка = Данные[Индекс].ВидРабочегоЦентра;
			ТекстТочки = РеквизитыВРЦ.Получить(Расшифровка).Представление;
			Значение = Данные[Индекс].ЗагрузкаПроценты;
			Подсказка = ТекстТочки + ", " + Значение + "%";
			
			Точка = ЗагрузкаОборудования.Точки.Добавить();
			Точка.Текст = ТекстТочки;
			
			ЗагрузкаОборудования.УстановитьЗначение(Точка, Серия, Значение, Расшифровка, Подсказка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ЗагрузкаОборудования.Обновление = Истина;
	
	Элементы.ГруппаЗагрузкаОборудованияДлительнаяОперация.ТекущаяСтраница = Элементы.СтраницаЗагрузкаОборудованияДиаграмма;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьЗагрузкуОборудования(Форма)
	
	Форма.ЗагрузкаОборудования.Очистить();
	Форма.Элементы.ГруппаЗагрузкаОборудованияДлительнаяОперация.ТекущаяСтраница = Форма.Элементы.СтраницаЗагрузкаОборудованияДиаграмма;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаРасшифровкиЗагрузкаОборудования(СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если МодельЗапланирована Тогда
		
		Отбор = Новый Структура("Распоряжение", Распоряжение);
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("Отбор", Отбор);
		ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыОтчета.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
		
		ОткрытьФорму(
			"Отчет.ЗагрузкаОборудованияПоМоделиГрафикаПроизводства.ФормаОбъекта",
			ПараметрыОтчета,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДиаграммаОбеспечениеМатериалами

&НаСервере
Процедура НастроитьДиаграммуОбеспечениеМатериалами()
	
	Если НЕ ИспользуетсяПланированиеПоМатериальнымРесурсам() Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьГистограмму(ОбеспечениеМатериалами);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДиаграммуОбеспечениеМатериаламиВФоне()
	
	Если НЕ ИспользуетсяПланированиеПоМатериальнымРесурсам() Тогда
		Возврат;
	КонецЕсли;
	
	Если МодельЗапланирована Тогда
		
		ИмяОперации = ИмяДлительнойОперацииОбеспечениеМатериалами();
		ОтменитьДлительнуюОперацию(ИмяОперации);
		
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("Распоряжение", Распоряжение);
		ПараметрыПроцедуры.Вставить("КоличествоЕдиниц", 5);
		
		ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Чтение потребности в материалах по модели';
																|en = 'Reading the demand for materials according to model'");
		ПараметрыВыполнения.ЗапуститьВФоне = Истина;
		
		ДлительнаяОперацияФормы = ДлительныеОперации.ВыполнитьВФоне(
			"Отчеты.ПотребностьВМатериалахПоМоделиГрафикаПроизводства.ПолучитьМатериалыСНаибольшейЗадержкойВОбеспечении",
			ПараметрыПроцедуры,
			ПараметрыВыполнения);
		
		ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
		Если ДлительнаяОперацияФормы.Статус = СтатусДлительнойОперацииВыполняется() Тогда
			
			ОписаниеОперации.ДлительнаяОперация = ДлительнаяОперацияФормы;
			ОписаниеОперации.НачатьОжидание = Истина;
			
		Иначе
			
			ОписаниеОперации.ДлительнаяОперация = Неопределено;
			ОписаниеОперации.НачатьОжидание = Ложь;
			
			ОбработатьРезультатЧтенияОбеспеченияМатериаламиВФоне(ДлительнаяОперацияФормы);
			
		КонецЕсли;
		
	Иначе
		
		ОчиститьИПоказатьОбеспечениеМатериалами(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДиаграммуОбеспечениеМатериаламиВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяОперации = ИмяДлительнойОперацииОбеспечениеМатериалами();
	ОписаниеОперации = ОписаниеДлительнойОперации(ЭтотОбъект, ИмяОперации);
	ОписаниеОперации.ДлительнаяОперация = Неопределено;
	
	Если Результат = Неопределено Тогда
		
		ОчиститьИПоказатьОбеспечениеМатериалами(ЭтотОбъект);
		
	Иначе
		
		ОбработатьРезультатЧтенияОбеспеченияМатериаламиВФоне(Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатЧтенияОбеспеченияМатериаламиВФоне(Результат)
	
	Если Результат.Статус = СтатусДлительнойОперацииВыполнено() Тогда
		
		ЗаполнитьОбеспечениеМатериалами(Результат.АдресРезультата);
		
	Иначе
		
		ОчиститьИПоказатьОбеспечениеМатериалами(ЭтотОбъект);
		
		Если Результат.Статус = СтатусДлительнойОперацииОшибка() Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбеспечениеМатериалами(АдресРезультата)
	
	ОбеспечениеМатериалами.Очистить();
		
	Данные = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если ЗначениеЗаполнено(Данные) Тогда
		
		ОбеспечениеМатериалами.Обновление = Ложь;
		
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
			Данные.ВыгрузитьКолонку("Номенклатура"),
			"Представление");
		
		Данные.Сортировать("Опоздание Возр");
		
		Серия = ОбеспечениеМатериалами.Серии.Добавить("Опоздание");
		Серия.Цвет = WebЦвета.Лосось;
		
		Для Индекс = 0 По Данные.Количество()-1 Цикл
			
			Расшифровка = Данные[Индекс].Номенклатура;
			ТекстТочки = РеквизитыНоменклатуры.Получить(Расшифровка).Представление;
			Значение = Данные[Индекс].Опоздание;
			Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1, %2 дн.';
					|en = '%1, %2 days'"), ТекстТочки, Значение);
			
			Точка = ОбеспечениеМатериалами.Точки.Добавить();
			Точка.Текст = ТекстТочки;
			
			ОбеспечениеМатериалами.УстановитьЗначение(Точка, Серия, Значение, Расшифровка, Подсказка);
			
		КонецЦикла;
		
		ОбеспечениеМатериалами.Обновление = Истина;
		Элементы.ГруппаОбеспечениеМатериаламиДлительнаяОперация.ТекущаяСтраница =
			Элементы.СтраницаОбеспечениеМатериаламиДиаграмма;
		
	Иначе
		
		Элементы.ГруппаОбеспечениеМатериаламиДлительнаяОперация.ТекущаяСтраница =
			Элементы.СтраницаОбеспечениеМатериаламиЗадержкиОтсутствуют;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОчиститьИПоказатьОбеспечениеМатериалами(Форма)
	
	Форма.ОбеспечениеМатериалами.Очистить();
	Форма.Элементы.ГруппаОбеспечениеМатериаламиДлительнаяОперация.ТекущаяСтраница = Форма.Элементы.СтраницаОбеспечениеМатериаламиДиаграмма;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифроватьОбеспечениеМатериаламиПоМодели()
	
	Если МодельЗапланирована Тогда
		
		Отбор = Новый Структура("Распоряжение", Распоряжение);
		
		ПараметрыОтчета = Новый Структура;
		ПараметрыОтчета.Вставить("Отбор", Отбор);
		ПараметрыОтчета.Вставить("КлючВарианта", "ПотребностьВМатериалахКонтекст");
		ПараметрыОтчета.Вставить("СформироватьПриОткрытии", Истина);
		ПараметрыОтчета.Вставить("ЗакрыватьПриЗакрытииВладельца", Истина);
		
		ОткрытьФорму(
			"Отчет.ПотребностьВМатериалахПоМоделиГрафикаПроизводства.ФормаОбъекта",
			ПараметрыОтчета,
			ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	#Область ЕдиницыИзмерения
	
	ПараметрыУсловногоОформления = НоменклатураСервер.НовыеПараметрыУсловногоОформленияЕдиницИзмерения();
	ПараметрыУсловногоОформления.ИмяПоляЕдиницаИзмерения = "ПродукцияНоменклатураЕдиницаИзмерения";
	ПараметрыУсловногоОформления.ПутьКПолюУпаковка = "Продукция.Упаковка";
	НоменклатураСервер.УстановитьУсловноеОформлениеЕдиницИзмерения(ЭтаФорма, ПараметрыУсловногоОформления);
	
	#КонецОбласти

	#Область ОпозданиеПоГрафикуШапка
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаВыпускаГрафик.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОпозданиеГрафик.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаВыпускаГрафик");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоРазборка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
	#КонецОбласти
	
	#Область ОпозданиеПоГрафикуПродукция
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаВыпускаГрафик.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияОпозданиеГрафик.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Продукция.ДатаВыпускаГрафик");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоРазборка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
	#КонецОбласти
	
	#Область ОпозданиеПоМоделиШапка
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаВыпускаМодель.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ОпозданиеМодель.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаВыпускаМодель");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоРазборка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
	#КонецОбласти
		
	#Область ОпозданиеПоМоделиПродукция
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаВыпускаМодель.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияОпозданиеМодель.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Продукция.ДатаВыпускаМодель");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Больше;
	ОтборЭлемента.ПравоеЗначение = Новый ПолеКомпоновкиДанных("ДатаПотребности");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтоРазборка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
	#КонецОбласти
	
	#Область ДатыГрафика
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаЗапускаГрафик.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаВыпускаГрафик.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаЗапускаМодель.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаВыпускаМодель.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПродукцияДатаПотребности.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыНачало.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыОкончание.Имя);
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыДинамическиеНачало.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыДинамическиеОкончание.Имя);
	
	Элемент.Оформление.УстановитьЗначениеПараметра(
		"Формат",
		ФорматнаяСтрокаДляДатыГрафикаПроизводства());
	
	#КонецОбласти
	
	#Область ДинамическиеЭтапыШрифт
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭтапыДинамические.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭтапыДинамические.МожетБытьЗапланирован");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.СерыйЦветТекста1);
	
	#КонецОбласти
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПриСоздании()
	
	НастроитьВидимостьЭлементовФормы();
	НастроитьФорматЭлементовСТипомДата();
	НастроитьДиаграммуСрокиВыполнения();
	НастроитьДиаграммуЗагрузкаОборудования();
	НастроитьДиаграммуОбеспечениеМатериалами();
	ЗаполнитьДлительныеОперацииФормы();
	ЗаполнитьОбозначенияПродукции();
	НастроитьТекстЗапросаСпискаЭтапы();
	
	Элементы.ФормаПланировать.Доступность = НЕ Распоряжение.Пустая();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОбозначенияПродукции()
	
	Макет = Обработки.ПланированиеГрафикаПроизводства2_2.ПолучитьМакет("РасшифровкаТаблицыПродукция");
	Область = Макет.ПолучитьОбласть("Расшифровка");
	ОбозначенияПродукции.Вывести(Область);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьТекстЗапросаСпискаЭтапы()
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = СтрЗаменить(
		Этапы.ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа("ЭтапыПереопределяемый"));
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Этапы, СвойстваСписка);
	
	ТекстЗапроса = ЭтапыДинамические.ТекстЗапроса;
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ПредставлениеЭтапа",
		Документы.ЭтапПроизводства2_2.ТекстЗапросаПредставлениеЭтапа(
			"ВЫРАЗИТЬ(ГрафикПереопределяемый.Этап КАК Документ.ЭтапПроизводства2_2)"));
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&СтатусОжидаетФормирования",
		НСтр("ru = '""Ожидает формирования""';
			|en = '""Pending generation""'"));
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.ЭтапыДинамические, СвойстваСписка);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементовФормы()
	
	Элементы.ГруппаЗагрузкаОборудования.Видимость = ИспользуетсяПланированиеПоПроизводственнымРесурсам();
	Элементы.ГруппаОбеспечениеМатериалами.Видимость = ИспользуетсяПланированиеПоМатериальнымРесурсам();
	
	// Шапка
	Элементы.ДатаПотребности.Видимость = НЕ ЭтоРазборка;
	Элементы.ДекорацияЗаголовок4.Видимость = НЕ ЭтоРазборка;
	Элементы.ОпозданиеГрафик.Видимость = НЕ ЭтоРазборка;
	Элементы.ОпозданиеМодель.Видимость = НЕ ЭтоРазборка;
	
	// Колонка Опоздание в таблице
	Элементы.ПродукцияОпозданиеГрафик.Видимость = НЕ ЭтоРазборка;
	Элементы.ПродукцияОпозданиеМодель.Видимость = НЕ ЭтоРазборка;
	Элементы.ПродукцияДатаПотребности.Видимость = ДинамическаяСтруктура;
	Элементы.ПродукцияДатаЗапуска.Видимость = НЕ ДинамическаяСтруктура;
	Элементы.ПродукцияДлительность.Видимость = НЕ ДинамическаяСтруктура;
	
	// Легенда диаграммы
	Элементы.ЦветРаспоряжение.Видимость = НЕ ЭтоРазборка;
	Элементы.ЛегендаРаспоряжение.Видимость = НЕ ЭтоРазборка;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорматЭлементовСТипомДата()
	
	ФорматнаяСтрока = ФорматнаяСтрокаДляДатыГрафикаПроизводства();
	
	Элементы.НачатьНеРанее.ФорматРедактирования = ФорматнаяСтрока;
	Элементы.ДатаПотребности.ФорматРедактирования = ФорматнаяСтрока;
	Элементы.ДатаЗапускаГрафик.ФорматРедактирования = ФорматнаяСтрока;
	Элементы.ДатаВыпускаГрафик.ФорматРедактирования = ФорматнаяСтрока;
	Элементы.ДатаЗапускаМодель.ФорматРедактирования = ФорматнаяСтрока;
	Элементы.ДатаВыпускаМодель.ФорматРедактирования = ФорматнаяСтрока;
	
КонецПроцедуры

&НаСервере
Функция ФорматнаяСтрокаДляДатыГрафикаПроизводства()
	
	Возврат УправлениеПроизводством.ФорматнаяСтрокаДляДатыГрафикаПроизводства();	
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция РазностьДатСтрокой(Знач Дата1, Знач Дата2)
	
	РазностьЧасы = Цел((Дата2 - Дата1 + 1)/3600);
	
	Если РазностьЧасы < 1 Тогда
		
		Результат = "";
		
	Иначе
		
		Если РазностьЧасы <= 48 Тогда
			
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 ч.';
					|en = '%1 h.'"),
				РазностьЧасы);
			
		Иначе
			
			РазностьДни = Цел((НачалоДня(Дата2 + 1) - НачалоДня(Дата1))/86400);
			
			Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1 дн.';
					|en = '%1 days'"),
				Формат(РазностьДни, "ЧГ=0"));
				
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеДоступностью(Форма)
	
	Форма.Элементы.ФормаСохранить.Доступность = Форма.ГрафикЗапланирован;
	Форма.Модифицированность = Форма.ГрафикЗапланирован И НЕ Форма.ДинамическаяСтруктура;
	
	Форма.Элементы.ДатаЗапускаМодель.Доступность = Форма.МодельЗапланирована;
	Форма.Элементы.ДатаВыпускаМодель.Доступность = Форма.МодельЗапланирована;
	Форма.Элементы.ОпозданиеМодель.Доступность = Форма.МодельЗапланирована;
	Форма.Элементы.ДлительностьМодель.Доступность = Форма.МодельЗапланирована;
	Форма.Элементы.ЗагрузкаОборудования.Доступность = Форма.МодельЗапланирована;
	Форма.Элементы.ОбеспечениеМатериалами.Доступность = Форма.МодельЗапланирована;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПеречитатьДанныеЗавершение(ОтветПользователя, ДополнительныеПараметры) Экспорт
	
	Если ОтветПользователя = КодВозвратаДиалога.Да Тогда
		
		ПеречитываниеДанных = Истина;
		УдалитьРезультатыПланированияНаКлиенте(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтатусРабочийГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик();
	
КонецФункции

&НаСервереБезКонтекста
Функция СтатусПредварительныйГрафик()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусПредварительныйГрафик();
	
КонецФункции
	
&НаСервереБезКонтекста
Функция СтатусМодельГрафика()
	
	Возврат РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусМодельГрафика();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборТолькоЗадерживающиеЗаказЭтапы(Список, ЗначениеОтбора)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, 
		"ЗадерживаетЗаказ", 
		ЗначениеОтбора, 
		ВидСравненияКомпоновкиДанных.Равно,
		, // Представление - автоматически
		ЗначениеОтбора);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоказыватьЗавершенныеЭтапы(ЭтапыДинамические, ПоказыватьЗавершенныеДинамическиеЭтапы)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		ЭтапыДинамические,
		"Статус",
		ПредопределенноеЗначение("Перечисление.СтатусыЭтаповПроизводства2_2.Завершен"),
		ВидСравненияКомпоновкиДанных.НеРавно,
		, // Представление - автоматически
		Не ПоказыватьЗавершенныеДинамическиеЭтапы);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиФормыКлиент()
	
	Настройки = Новый Структура;
	Настройки.Вставить("ТолькоЗадерживающиеЗаказЭтапы", ТолькоЗадерживающиеЗаказЭтапы);
	Настройки.Вставить("ПоказыватьЗавершенныеДинамическиеЭтапы", ПоказыватьЗавершенныеДинамическиеЭтапы);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючФормы(),
		КлючНастроекФормы(),
		Настройки);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		КлючФормы(),
		КлючНастроекФормы());
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
		УстановитьОтборТолькоЗадерживающиеЗаказЭтапы(Этапы, ТолькоЗадерживающиеЗаказЭтапы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Обработка.ПланированиеГрафикаПроизводства2_2.ПланированиеГрафикаЗаказа";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастроекФормы()
	
	Возврат "Основные";
	
КонецФункции

&НаСервере
Процедура НастроитьГистограмму(Диаграмма)
	
	Диаграмма.Анимация = АнимацияДиаграммы.НеИспользовать;
	Диаграмма.АвтоУстановкаТекстаТочек = Ложь;
	Диаграмма.ТипДиаграммы = ТипДиаграммы.ГистограммаГоризонтальная;
	Диаграмма.ОтображатьЛегенду = Ложь;
	Диаграмма.ОтображатьЗаголовок = Ложь;
	Диаграмма.ОбластьПостроения.Верх = 0;
	Диаграмма.ОбластьПостроения.Лево = 0;
	Диаграмма.ОбластьПостроения.ШкалаЗначений.ОтображениеЛинийСетки = ОтображениеЛинийСеткиДиаграммы.НеОтображать;
	Диаграмма.ОбластьПостроения.ЦветФона = ЦветаСтиля.ЦветФонаФормы;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КодыОшибокПланирования()
	
	Менеджер = Обработки.ПланированиеГрафикаПроизводства2_2;
	
	Результат = Новый Структура;
	Результат.Вставить("КодОшибкиДоступностьВРЦ", Менеджер.КодОшибкиДоступностьВРЦ());
	Результат.Вставить("КодОшибкиГрафикРаботыПодразделения", Менеджер.КодОшибкиГрафикРаботыПодразделения());
	Результат.Вставить("КодОшибкиГрафикРаботыПредприятия", Менеджер.КодОшибкиГрафикРаботыПредприятия());
	Результат.Вставить("КодОшибкиОтсутствуетГрафикПредшественника", Менеджер.КодОшибкиОтсутствуетГрафикПредшественника());
	Результат.Вставить("КодОшибкиЕстьЦиклыВЦепочкеЭтапов", Менеджер.КодОшибкиЕстьЦиклыВЦепочкеЭтапов());
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользуетсяПланированиеПоПроизводственнымРесурсам()
	
	Возврат УправлениеПроизводствомПовтИсп.ИспользуетсяПланированиеПоПроизводственнымРесурсам();
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользуетсяПланированиеПоМатериальнымРесурсам()
	
	Возврат УправлениеПроизводствомПовтИсп.ИспользуетсяПланированиеПоМатериальнымРесурсам();
	
КонецФункции

// Проверить выделенную в списке строку.
// 
// Параметры:
//  Список - ТаблицаФормы
// 
// Возвращаемое значение:
//  Булево - Истина если проверка пройдена успешно
//
&НаКлиенте
Функция ПроверитьВыделеннуюВСпискеСтроку(Список)
	
	Если Список.ТекущаяСтрока = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбрана строка списка';
									|en = 'The list row is not selected'"));
		Возврат Ложь;
	ИначеЕсли ТипЗнч(Список.ТекущаяСтрока) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Действие не может быть выполнено для строки группировки списка';
									|en = 'Action cannot be executed for the list grouping row'"));
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#КонецОбласти
