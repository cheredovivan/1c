
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПланированиеГрафикаЭтапов = Параметры.ПланированиеГрафикаЭтапов;
	ЕстьДинамическиеЗаказы = Ложь;
	
	ЗагрузитьНастройкиФормы();
	
	Если ПланированиеГрафикаЭтапов
		И ЗначениеЗаполнено(Параметры.Этапы) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СУММА(
			|		ВЫБОР
			|			КОГДА Этапы.РучноеРазмещениеВГрафике
			|				ТОГДА 1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ЭтаповСРучнымРазмещением,
			|	СУММА(
			|		ВЫБОР
			|			КОГДА Этапы.ДинамическаяСтруктура
			|				ТОГДА 1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК ЭтаповДинамическойСтруктуры
			|ИЗ
			|	Документ.ЭтапПроизводства2_2 КАК Этапы
			|ГДЕ
			|	Этапы.Ссылка В(&Этапы)");
		Запрос.УстановитьПараметр("Этапы", Параметры.Этапы);
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			Если Выборка.ЭтаповСРучнымРазмещением > 0 Тогда
				Элементы.НадписьРазмещенныеВручную.Заголовок = СтрШаблон(
					НСтр("ru = 'Имеются этапы, запланированные вручную (всего: %1).';
						|en = 'There are stages scheduled manually (total: %1).'"),
					Выборка.ЭтаповСРучнымРазмещением);
				Элементы.ГруппаРазмещенныеВручную.Видимость = Истина;
			Иначе
				Элементы.ГруппаРазмещенныеВручную.Видимость = Ложь;
			КонецЕсли;
			
			Если Выборка.ЭтаповДинамическойСтруктуры > 0 Тогда
				Элементы.НадписьАктуальностьГрафика.Заголовок = СтрШаблон(
					НСтр("ru = 'Имеются этапы динамической структуры заказа (всего: %1).
						|Для их рассчета необходимо пересчитать график всего заказа.';
						|en = 'There are stages with dynamic structure of order (total: %1).
						|To calculate the structure, recalculate the whole schedule.'"),
					Выборка.ЭтаповДинамическойСтруктуры);
				Элементы.ГруппаАктуальностьГрафика.Видимость = Истина;
			Иначе
				Элементы.ГруппаАктуальностьГрафика.Видимость = Ложь;
			КонецЕсли;
		Иначе
			Элементы.ГруппаРазмещенныеВручную.Видимость = Ложь;
			Элементы.ГруппаАктуальностьГрафика.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ГруппаНаличиеБолееПриоритетныхЗаказов.Видимость = Ложь;
		
	ИначеЕсли НЕ ПланированиеГрафикаЭтапов
		И ЗначениеЗаполнено(Параметры.Распоряжения) Тогда
		
		Запрос = Новый Запрос(
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	ИСТИНА
			|ИЗ
			|	Документ.ЗаказНаПроизводство2_2 КАК Заказы
			|ГДЕ
			|	Заказы.Ссылка В (&Ссылки)
			|	И Заказы.ДинамическаяСтруктура");
		Запрос.УстановитьПараметр("Ссылки", Параметры.Распоряжения);
		РезультатЗапроса = Запрос.Выполнить();
		ЕстьДинамическиеЗаказы = НЕ РезультатЗапроса.Пустой();
		
		РазмещеноВручную = 0;
		РассчитатьГрафик = 0;
		ЕстьПриоритетные = Ложь;
		
		ПрочитатьДанныеИнформационныхНадписей(
			РазмещеноВручную, РассчитатьГрафик, ЕстьПриоритетные, Параметры.Распоряжения);
		
		Если РазмещеноВручную > 0 Тогда
			Элементы.НадписьРазмещенныеВручную.Заголовок = СтрШаблон(
				НСтр("ru = 'Имеются этапы, запланированные вручную (всего: %1).';
					|en = 'There are stages scheduled manually (total: %1).'"),
				РазмещеноВручную);
			Элементы.ГруппаРазмещенныеВручную.Видимость = Истина;
		Иначе
			Элементы.ГруппаРазмещенныеВручную.Видимость = Ложь;
		КонецЕсли;
		
		Если РассчитатьГрафик > 0 Тогда
			Если ЕстьДинамическиеЗаказы Тогда
				// В дин. структуре есть задания на партии и заказ
				Элементы.НадписьАктуальностьГрафика.Заголовок = НСтр("ru = 'График не актуален.';
																	|en = 'Schedule is not relevant.'");
			Иначе
				Элементы.НадписьАктуальностьГрафика.Заголовок = СтрШаблон(
					НСтр("ru = 'График не актуален. Имеются этапы, требующие перепланирования (всего: %1).';
						|en = 'Schedule is not relevant. There are stages which require rescheduling (total: %1).'"),
					РассчитатьГрафик);
			КонецЕсли;
		Иначе
			Элементы.НадписьАктуальностьГрафика.Заголовок = НСтр("ru = 'График актуален.';
																|en = 'Schedule is relevant.'");
		КонецЕсли;
		Элементы.ГруппаАктуальностьГрафика.Видимость = Истина;
		
		Элементы.ГруппаНаличиеБолееПриоритетныхЗаказов.Видимость = ЕстьПриоритетные;
		
	КонецЕсли;
	
	Настройки = ПроизводствоСервер.НастройкиПодсистемыПроизводство();
	Если НЕ Настройки.ИспользуетсяПланированиеПоПроизводственнымРесурсам Тогда
		Элементы.ЗадействоватьРезервДоступности.Видимость = Ложь;
		ЗадействоватьРезервДоступности = Ложь;
	КонецЕсли;
	
	Элементы.ПолноеПерепланирование.Видимость = НЕ(ПланированиеГрафикаЭтапов ИЛИ ЕстьДинамическиеЗаказы);
	Если ЕстьДинамическиеЗаказы Тогда
		ПолноеПерепланирование = Истина;
	КонецЕсли;
	
	Элементы.ОтменитьРучныеИзмененияГрафика.Доступность = ПолноеПерепланирование
		ИЛИ НЕ Элементы.ПолноеПерепланирование.Видимость;
	
	ЗаполнитьПояснениеНастроек(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗадействоватьРезервДоступностиПриИзменении(Элемент)
	
	СохранитьНастройкиФормыКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПолноеПерепланированиеПриИзменении(Элемент)
	
	Элементы.ОтменитьРучныеИзмененияГрафика.Доступность = ПолноеПерепланирование;
	Если НЕ ПолноеПерепланирование Тогда
		ОтменитьРучныеИзмененияГрафика = Ложь;
	КонецЕсли;
	
	СохранитьНастройкиФормыКлиент();
	ЗаполнитьПояснениеНастроек(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРучныеИзмененияГрафикаПриИзменении(Элемент)
	
	ЗаполнитьПояснениеНастроек(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Планировать(Команда)
	
	НастройкиПланирования = Новый Структура;
	
	НастройкиПланирования.Вставить("ЗадействоватьРезервДоступности", ЗадействоватьРезервДоступности);
	НастройкиПланирования.Вставить("ОтменитьРучныеИзмененияГрафика", ОтменитьРучныеИзмененияГрафика);
	
	Если НЕ ПланированиеГрафикаЭтапов Тогда
		НастройкиПланирования.Вставить("ПолноеПерепланирование", ПолноеПерепланирование);
	КонецЕсли;
	
	Закрыть(НастройкиПланирования);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗагрузитьНастройкиФормы()
	
	Настройки = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(КлючФормы(), Неопределено);
	
	Если ТипЗнч(Настройки) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройкиФормыКлиент()
	
	Настройки = Новый Структура;
	Настройки.Вставить("ЗадействоватьРезервДоступности", ЗадействоватьРезервДоступности);
	Настройки.Вставить("ПолноеПерепланирование", ПолноеПерепланирование);
	
	СохранитьНастройкиФормыСервер(Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкиФормыСервер(Настройки)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		КлючФормы(),
		Неопределено,
		Настройки);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючФормы()
	
	Возврат "Обработка.ПланированиеГрафикаПроизводства2_2.НастройкиПланированияСпискаРаспоряженийИЭтапов";
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПояснениеНастроек(Форма)
	
	Если Форма.ПланированиеГрафикаЭтапов
		ИЛИ Форма.ЕстьДинамическиеЗаказы Тогда
		Возврат;
	КонецЕсли;
	
	Подсказка = УправлениеПроизводствомКлиентСервер.ОписаниеНастроекПланирования(
		Форма.ПолноеПерепланирование,
		Форма.ОтменитьРучныеИзмененияГрафика);
	
	Форма.Элементы.Группа1.Подсказка = Подсказка;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеИнформационныхНадписей(РазмещеноВручную, РассчитатьГрафик, ЕстьПриоритетные, Распоряжения)
	
	Распоряжение = РаспоряжениеСНаименьшимПриоритетом(Распоряжения);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Документы.ЗаказНаПроизводство2_2.СоздатьВТЗаказыСБольшимПриоритетом(
		МенеджерВременныхТаблиц,
		"ВТПриоритетныеЗаказы",
		Распоряжение);
	
	ИсключитьИзВТПланируемыеРаспоряжения(
		МенеджерВременныхТаблиц,
		"ВТПриоритетныеЗаказы",
		Распоряжения);
	
	ЕстьЗаписиВГрафике = Ложь;
	
	Обработки.ПланированиеГрафикаПроизводства2_2.ПрочитатьКоличествоОбъектовДляПланирования(
		РазмещеноВручную,
		РассчитатьГрафик,
		ЕстьПриоритетные,
		ЕстьЗаписиВГрафике,
		Распоряжения,
		МенеджерВременныхТаблиц,
		"ВТПриоритетныеЗаказы");
	
КонецПроцедуры

&НаСервере
Функция РаспоряжениеСНаименьшимПриоритетом(Распоряжения)
	
	Если Распоряжения.Количество() = 1 Тогда
		
		Результат = Распоряжения[0];
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗаказНаПроизводство2_2.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК ЗаказНаПроизводство2_2
		|ГДЕ
		|	ЗаказНаПроизводство2_2.Проведен = ИСТИНА
		|	И ЗаказНаПроизводство2_2.Ссылка В(&Распоряжения)
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗаказНаПроизводство2_2.Приоритет.РеквизитДопУпорядочивания УБЫВ,
		|	ЗаказНаПроизводство2_2.Подразделение.РеквизитДопУпорядочивания УБЫВ,
		|	ЗаказНаПроизводство2_2.Очередь УБЫВ");
		
		Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
		Выборка = Запрос.Выполнить().Выбрать();
		Выборка.Следующий();
		
		Результат = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ИсключитьИзВТПланируемыеРаспоряжения(МенеджерВременныхТаблиц, ИмяВременнойТаблицы, Распоряжения)
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВТПриоритетныеЗаказы.Ссылка
	|ПОМЕСТИТЬ ВТЗаказы
	|ИЗ
	|	ВТПриоритетныеЗаказы КАК ВТПриоритетныеЗаказы
	|ГДЕ
	|	НЕ ВТПриоритетныеЗаказы.Ссылка В (&Распоряжения)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТПриоритетныеЗаказы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТЗаказы.Ссылка
	|ПОМЕСТИТЬ ВТПриоритетныеЗаказы
	|ИЗ
	|	ВТЗаказы КАК ВТЗаказы";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТПриоритетныеЗаказы", ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Распоряжения", Распоряжения);
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти
