///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПодсистемаПодключаемыеКоманды = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПодключаемыеКоманды");
	
	Если ПустаяСтрока(Параметры.Подсистема) Тогда
		Параметры.Подсистема = "Все";
	КонецЕсли;
	КешПараметров = Новый Структура;
	
	ТолькоЭлементы = Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Элементы;
	ТолькоГруппы   = Параметры.ВыборГруппИЭлементов = ИспользованиеГруппИЭлементов.Группы;
	
	НастройкиПлатежныхСистемСобытия.ПриСозданииФормы(ЭтотОбъект);
	НастройкиПлатежныхСистемСобытия.ПриПодготовкеПараметровФормы(ЭтотОбъект);
	НастройкиПлатежныхСистемСобытия.ПриОпределенииИменПодсистем(КешПараметров.ВсеИменаПодсистем);
	НастройкиПлатежныхСистемСобытия.ПриДобавленииКоманд(ЭтотОбъект);
	НастройкиПлатежныхСистемСобытия.ПриОпределенииДоступностиКоманд(ЭтотОбъект);
	
	Если Не СоздатьДеревоНаСервере(Истина) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Параметры.РежимОткрытияОкна <> Неопределено Тогда
		РежимОткрытияОкна = Параметры.РежимОткрытияОкна;
	КонецЕсли;

	Элементы.СписокВыбрать.Видимость = Параметры.РежимВыбора;
	
	Если ЗначениеЗаполнено(Параметры.Отображение) Тогда
		Если Параметры.Отображение = "Список" Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы.Список;
		ИначеЕсли Параметры.Отображение = "Дерево" Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы.Дерево;
		ИначеЕсли Параметры.Отображение = "ИерархическийСписок" Тогда
			Элементы.Список.Отображение = ОтображениеТаблицы.ИерархическийСписок;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
	// Помещаем в конец командной панели, иначе после поля поиска будут команды,
	// создаваемые программно
	Элементы.Переместить(Элементы.ДополнениеПоиск, Элементы.Список.КоманднаяПанель);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбновитьСписок = Ложь;
	НастройкиПлатежныхСистемКлиентСобытия.ПриОбработкеОповещения(
		ЭтотОбъект,
		ОбновитьСписок,
		ИмяСобытия,
		Параметр,
		Источник);
		
	Если ОбновитьСписок Тогда
		СоздатьДерево();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыбор(СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	Если ЕстьПодсистемаПодключаемыеКоманды Тогда
		МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
		МодульПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Не (Элементы.СписокКонтекстноеМенюСкопировать.Видимость
		Или Элементы.СписокКонтекстноеМенюИзменить.Видимость) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	ДоступностьКнопок = Неопределено;
	
	Если (ТекущиеДанные <> Неопределено
		И ТекущиеДанные.ТипЭлемента <> НастройкиПлатежныхСистемКлиентСервер.ИмяКорень()) Тогда
		ДоступностьКнопок = КешПараметров.ДоступностьНастроек.Получить(ТекущиеДанные.Подсистема);
	КонецЕсли;
	
	Если ДоступностьКнопок = Неопределено Тогда
		ДоступностьКнопок = Ложь;
	КонецЕсли;

	Элементы.СписокКонтекстноеМенюСкопировать.Доступность = ДоступностьКнопок;
	Элементы.СписокКонтекстноеМенюИзменить.Доступность = ДоступностьКнопок;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОбработатьВыбор(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Скопировать(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОбновитьДанныеФормы",
		ЭтотОбъект);
	
	НастройкиПлатежныхСистемКлиентСобытия.ПриКопированииНастройки(ТекущиеДанные, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	СоздатьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.ТипЭлемента = НастройкиПлатежныхСистемКлиентСервер.ИмяКорень()
		Или ТипЗнч(ТекущиеДанные.Ссылка) = Тип("Строка") Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройкиДляИзменения(
		ТекущиеДанные);
	ПараметрыВыбора.РезультатВыбора = ТекущиеДанные.Ссылка;
	
	НастройкиПлатежныхСистемКлиентСобытия.ПриОбработкеВыбораНастройкиДляИзменения(
		ТекущиеДанные.Подсистема,
		ПараметрыВыбора);
	
	Если ПараметрыВыбора.Отказ Тогда
		
		Если ЗначениеЗаполнено(ПараметрыВыбора.СообщениеОбОшибке) Тогда
			ПоказатьПредупреждение(
				Неопределено,
				ПараметрыВыбора.СообщениеОбОшибке);
		КонецЕсли;

		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыВыбора.ИмяФормы)
		И ЗначениеЗаполнено(ПараметрыВыбора.РезультатВыбора) Тогда
		ПараметрыОткрытия = Новый Структура
			("Ключ", ПараметрыВыбора.РезультатВыбора);
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения(
			"ОбработкаЗакрытияФормыЭлемента",
			ЭтотОбъект);
		
		ОткрытьФорму(
			ПараметрыВыбора.ИмяФормы,
			ПараметрыОткрытия,
			ЭтотОбъект,
			УникальныйИдентификатор,
			,
			,
			ОповещениеОЗакрытии);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СоздатьНастройку(Команда)
	
	ИмяПодсистемы = КешПараметров.ПодсистемыКнопкиСоздать.Получить(Команда.Имя);
	
	Если Не КешПараметров.ДоступностьНастроек.Получить(ИмяПодсистемы) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПодключения = НастройкиПлатежныхСистемКлиент.НовыйПараметрыПодключения(
		Неопределено,
		Неопределено,
		ОписаниеОповещенияЗакрытиеФормыСоздания("СозданиеЭлемента"));
	
	НастройкиПлатежныхСистемКлиент.ПодключитьПлатежнуюСистему(
		ИмяПодсистемы,
		ПараметрыПодключения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуДополнительно(Команда)
	
	НастройкиПлатежныхСистемКлиентСобытия.ПриВыполненииКомандыДополнительно(
		ЭтотОбъект,
		Команда);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеДерева

&НаКлиенте
Процедура СоздатьДерево()
	
	РазвернутыеСтроки = Новый Соответствие;
	
	ВыделеннаяСтрока = Неопределено;
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ВыделеннаяСтрока = Новый Структура(
			"Подсистема, Ссылка",
			ТекущиеДанные.Подсистема,
			ТекущиеДанные.Ссылка);
	КонецЕсли;
	
	ЭлементыДерева = Список.ПолучитьЭлементы();
	Для Каждого Ветка Из ЭлементыДерева Цикл
		ЗаполнитьРазвернутыеСтроки(Ветка, РазвернутыеСтроки);
	КонецЦикла;
	
	ИсходныйПустой = (ЭлементыДерева.Количество() = 0);
	
	СоздатьДеревоНаСервере(Ложь);
	
	Для Каждого Ветка Из Список.ПолучитьЭлементы() Цикл
		РазвернутьСтрокиПослеОбновления(
			Ветка,
			РазвернутыеСтроки,
			ВыделеннаяСтрока,
			ИсходныйПустой);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция СоздатьДеревоНаСервере(Знач ЭтоСозданиеФормы)
	
	Список.ПолучитьЭлементы().Очистить();
	
	ДеревоРезультат = РеквизитФормыВЗначение("Список");
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора",         Параметры.РежимВыбора);
	ПараметрыФормы.Вставить("ТолькоГруппы",        ТолькоГруппы);
	ПараметрыФормы.Вставить("ТолькоЭлементы",      ТолькоЭлементы);
	ПараметрыФормы.Вставить("ПараметрыПодсистемы", Параметры.ПараметрыПодсистемы);
	ПараметрыФормы.Вставить("Отображение",         Параметры.Отображение);
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("ЕстьПодсистемы",               Ложь);
	ПараметрыВывода.Вставить("ВыводимыеКолонки",             Новый Массив);
	ПараметрыВывода.Вставить("УсловноеОформление",           Новый Соответствие);
	ПараметрыВывода.Вставить("ПараметрыФормы",               ПараметрыФормы);

	Для Каждого ИмяПодсистемы Из КешПараметров.ВсеИменаПодсистем Цикл
		Если КешПараметров.ВидимостьПодсистемВСписке[ИмяПодсистемы] Тогда
			ВывестиНастройкиПодсистемы(ИмяПодсистемы, ДеревоРезультат, ПараметрыВывода);
		КонецЕсли;
	КонецЦикла;

	Если Не ПараметрыВывода.ЕстьПодсистемы Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Ни одна используемая подсистема платежных сервисов не включена в общие настройки.';
				|en = 'None of the used payment service subsystems is included in the general settings.'"));
		Возврат Ложь;
	КонецЕсли;

	ЗначениеВРеквизитФормы(ДеревоРезультат, "Список");
	
	Если ЭтоСозданиеФормы Тогда
		ЗадатьУсловноеОформление(ПараметрыВывода);
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьРазвернутыеСтроки(Ветка, РазвернутыеСтроки)
	
	Если Элементы.Список.Развернут(Ветка.ПолучитьИдентификатор()) = Истина Тогда
		РазвернутыеСтроки.Вставить(Ветка.Ссылка, Истина);
	КонецЕсли;
	
	Для Каждого Строки Из Ветка.ПолучитьЭлементы() Цикл
		ЗаполнитьРазвернутыеСтроки(Строки, РазвернутыеСтроки);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура РазвернутьСтрокиПослеОбновления(
		Ветка,
		РазвернутыеСтроки,
		ВыделеннаяСтрока,
		ИсходныйПустой)
	
	Если ИсходныйПустой
		Или РазвернутыеСтроки.Получить(Ветка.Ссылка) <> Неопределено Тогда
		Элементы.Список.Развернуть(Ветка.ПолучитьИдентификатор(), Ложь);
	КонецЕсли;
	
	Для Каждого Строки Из Ветка.ПолучитьЭлементы() Цикл
		РазвернутьСтрокиПослеОбновления(
			Строки,
			РазвернутыеСтроки,
			ВыделеннаяСтрока,
			ИсходныйПустой);
	КонецЦикла;
	
	Если ВыделеннаяСтрока <> Неопределено Тогда
		Если ВыделеннаяСтрока.Подсистема = Ветка.Подсистема
			И ВыделеннаяСтрока.Ссылка = Ветка.Ссылка Тогда
			Элементы.Список.ТекущаяСтрока = Ветка.ПолучитьИдентификатор();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат, ДополнительныеПараметры) Экспорт
	
	СоздатьДерево();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВывестиНастройкиПодсистемы(ИмяПодсистемы, ДеревоРезультат, ПараметрыВывода)
	
	ДанныеДереваПодсистемы = СформироватьДеревоПоПодсистеме(ИмяПодсистемы, ПараметрыВывода.ПараметрыФормы);
	Если Не ДанныеДереваПодсистемы.ПараметрыДерева.ВыводитьПодсистему Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВывода.ЕстьПодсистемы = Истина;
	
	Если ДанныеДереваПодсистемы.Дерево.Строки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыВывода.ПараметрыФормы.Отображение = "Список" Тогда
		СтрокаПодсистема = ДеревоРезультат;
	Иначе
		СтрокаПодсистема = НовыйСтрокаПодсистемы(
			ДеревоРезультат.Строки,
			ДанныеДереваПодсистемы);
	КонецЕсли;
	
	ПараметрыПереноса = НовыйПараметрыПереносаДерева();

	ПараметрыПереноса.Подсистема = ИмяПодсистемы;
	
	ПеренестиДерево(
		ДанныеДереваПодсистемы.Дерево,
		СтрокаПодсистема,
		ПараметрыПереноса);
	
	ПараметрыВывода.УсловноеОформление.Вставить(
		ИмяПодсистемы,
		ДанныеДереваПодсистемы.ПараметрыДерева.УсловноеОформление);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьДеревоПоПодсистеме(ИмяПодсистемы, ПараметрыФормы)

	Дерево          = НастройкиПлатежныхСистемСлужебный.НовыйДеревоНастроек();
	ПараметрыДерева = НастройкиПлатежныхСистемСлужебный.НовыйПараметрыДерева();
	
	ЗаполнитьЗначенияСвойств(ПараметрыДерева, ПараметрыФормы);

	Попытка
	
		Подсистема = Метаданные.Подсистемы.ИнтернетПоддержкаПользователей.Подсистемы.Найти(ИмяПодсистемы);
		Если Подсистема = Неопределено Тогда
			ПараметрыДерева.ПредставлениеПодсистемы = ИмяПодсистемы;
		Иначе
			ПараметрыДерева.ПредставлениеПодсистемы = Подсистема.Синоним;
		КонецЕсли;
	
	Исключение
		
		ПараметрыДерева.ПредставлениеПодсистемы = ИмяПодсистемы;
		
	КонецПопытки;
	
	ПараметрыДерева.КартинкаПодсистемы = -1;
	НастройкиПлатежныхСистемСобытия.ПриОпределенииНомераКартинкиПодсистемы(
		ИмяПодсистемы,
		ПараметрыДерева.КартинкаПодсистемы);
	
	НастройкиПлатежныхСистемСобытия.ПриФормированииДереваПоПодсистеме(ИмяПодсистемы, Дерево, ПараметрыДерева);
	
	ПараметрыДерева.Подсистема = ИмяПодсистемы;
	ПараметрыДерева.Удалить("ПараметрыПодсистемы");
	
	Результат = Новый Структура();
	Результат.Вставить("Дерево",          Дерево);
	Результат.Вставить("ПараметрыДерева", ПараметрыДерева);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйСтрокаПодсистемы(Строки, ДанныеДереваПодсистемы)
	
	ПараметрыДерева = ДанныеДереваПодсистемы.ПараметрыДерева;
	
	СтрокаПодсистема             = Строки.Добавить();
	СтрокаПодсистема.Ссылка      = ПараметрыДерева.ПредставлениеПодсистемы;
	СтрокаПодсистема.Подсистема  = ПараметрыДерева.Подсистема;
	СтрокаПодсистема.Картинка    = ПараметрыДерева.КартинкаПодсистемы;
	СтрокаПодсистема.ТипЭлемента = НастройкиПлатежныхСистемКлиентСервер.ИмяКорень();
	
	Возврат СтрокаПодсистема;
	
КонецФункции

// Определяет параметры копирования строк дерева.
// 
// Возвращаемое значение:
//  Структура - Новый параметры переноса дерева:
// * Подсистема - Строка - подсистема, в которой было сформировано дерево.
// * Родитель - Неопределено - текущий родитель для выводимой строки.
//
&НаСервереБезКонтекста
Функция НовыйПараметрыПереносаДерева()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Подсистема", "");
	Результат.Вставить("Родитель",   Неопределено);
	
	Возврат Результат;
	
КонецФункции

// Копирует строки одного дерева в другое
// 
// Параметры:
//  Источник - СтрокаДереваЗначений - дерево или строка дерева значений, которую нужно перенести в новое дерево.
//  Получатель - СтрокаДереваЗначений - дерево или строка дерева, в которое добавляются новые строки
//  ПараметрыПереноса - см. НовыйПараметрыПереносаДерева
//
&НаСервереБезКонтекста
Процедура ПеренестиДерево(
		Источник,
		Получатель,
		ПараметрыПереноса)
	
	Для Каждого СтрокаИсточник Из Источник.Строки Цикл
		
		Если СтрокаИсточник.Ссылка = ПараметрыПереноса.Родитель Тогда
			ЗаполнитьЗначенияСвойств(Получатель, СтрокаИсточник);
			Продолжить;
		КонецЕсли;
		
		СтрокаПолучатель = Получатель.Строки.Добавить();
		СтрокаПолучатель.Подсистема = ПараметрыПереноса.Подсистема;
		
		ЗаполнитьЗначенияСвойств(СтрокаПолучатель, СтрокаИсточник);
		
		Если СтрокаИсточник.Строки.Количество() > 0 Тогда
			Родитель = ПараметрыПереноса.Родитель;
			ПеренестиДерево(
				СтрокаИсточник,
				СтрокаПолучатель,
				ПараметрыПереноса);
			ПараметрыПереноса.Родитель = Родитель;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗадатьУсловноеОформление(ПараметрыВывода)
	
	Для Каждого КлючЗначение Из ПараметрыВывода.УсловноеОформление Цикл
		
		ИмяПодсистемы = КлючЗначение.Ключ;
		ОформлениеПодсистемы = КлючЗначение.Значение;
		
		Для Каждого ОписаниеОформления Из ОформлениеПодсистемы Цикл
			
			НовыйЭлемент = УсловноеОформление.Элементы.Добавить();
			НовыйЭлемент.Использование = Истина;
			
			// Условие применения оформления
			ГруппаПодсистема = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаПодсистема.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			
			НовыйОтбор                = ГруппаПодсистема.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.Подсистема");
			НовыйОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			НовыйОтбор.ПравоеЗначение = ИмяПодсистемы;
			
			НовыйОтбор                = ГруппаПодсистема.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.ТипЭлемента");
			НовыйОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
			НовыйОтбор.ПравоеЗначение = НастройкиПлатежныхСистемКлиентСервер.ИмяКорень();
			
			ДобавитьУсловиеОтбораВОформление(
				ОписаниеОформления.Условия,
				ГруппаПодсистема,
				ИмяПодсистемы);
			
			// Оформляемое поле
			Для Каждого ИмяПоля Из ОписаниеОформления.ОформляемыеПоля Цикл
			
				ИмяПоляПодсистема = ИмяПоля;
				
				НовоеПоле               = НовыйЭлемент.Поля.Элементы.Добавить();
				НовоеПоле.Использование = Истина;
				НовоеПоле.Поле          = Новый ПолеКомпоновкиДанных("Список" + ИмяПоляПодсистема);
			
			КонецЦикла;
			
			// Оформление
			Для Каждого Оформление Из ОписаниеОформления.Оформление Цикл
			
				НовыйЭлемент.Оформление.УстановитьЗначениеПараметра(
					Оформление.ИмяПараметра,
					Оформление.ЗначениеПараметра);
			
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУсловиеОтбораВОформление(
		УсловияОтбора,
		Родитель,
		ИмяПодсистемы)
	
	Для Каждого УсловиеОтбора Из УсловияОтбора Цикл
			
		Если УсловиеОтбора.ЭтоГруппа Тогда
			
			ТипГруппы = Неопределено;
			
			Если ВРег(УсловиеОтбора.ТипГруппы) = "И" Тогда
				ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
			ИначеЕсли ВРег(УсловиеОтбора.ТипГруппы) = "ИЛИ" Тогда
				ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			ИначеЕсли ВРег(УсловиеОтбора.ТипГруппы) = "НЕ" Тогда
				ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;
			Иначе
				Продолжить;
			КонецЕсли;
			
			НоваяГруппа = Родитель.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			НоваяГруппа.ТипГруппы = ТипГруппы;
			
			ДобавитьУсловиеОтбораВОформление(
				УсловиеОтбора.СоставГруппы,
				НоваяГруппа,
				ИмяПодсистемы);
		Иначе
			
			НовыйОтбор = Родитель.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список." + УсловиеОтбора.ЛевоеЗначение);
			НовыйОтбор.ВидСравнения   = УсловиеОтбора.ВидСравнения;
			НовыйОтбор.ПравоеЗначение = УсловиеОтбора.ПравоеЗначение;

		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СобытияТаблицы

&НаКлиенте
Процедура ОбработатьВыбор(СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено
		Или ТекущиеДанные.ТипЭлемента = НастройкиПлатежныхСистемКлиентСервер.ИмяКорень() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыВыбора = НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки(
		ТекущиеДанные);
		
	ПараметрыВыбора.ИмяПодсистемы   = ТекущиеДанные.Подсистема;
	ПараметрыВыбора.РежимВыбора     = Параметры.РежимВыбора;
	ПараметрыВыбора.ТолькоЭлементы  = ТолькоЭлементы;
	ПараметрыВыбора.ТолькоГруппы    = ТолькоГруппы;
	ПараметрыВыбора.РезультатВыбора = ТекущиеДанные.Ссылка;
	
	НастройкиПлатежныхСистемКлиентСобытия.ПриОбработкеВыбораНастройки(ПараметрыВыбора);
	
	Если ПараметрыВыбора.ТребуетсяДополнительнаяОбработка Тогда
		
		ОбработкаЗавершения = Новый ОписаниеОповещения(
			"ОбработкаВыбораНастройкиЗавершение",
			ЭтотОбъект);
		
		НастройкиПлатежныхСистемКлиентСобытия.ПриДополнительнойОбработкеВыбораНастройки(
			ПараметрыВыбора,
			ОбработкаЗавершения);
		
	Иначе
		ОбработкаВыбораНастройкиЗавершение(ПараметрыВыбора);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаЗакрытияФормыЭлемента(Результат, ДополнительныеПараметры) Экспорт
	
	СоздатьДерево();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораНастройкиЗавершение(
		ПараметрыВыбора,
		ДополнительныеПараметры = Неопределено) Экспорт

	Если ПараметрыВыбора.Отказ Тогда
		
		Если ЗначениеЗаполнено(ПараметрыВыбора.СообщениеОбОшибке) Тогда
			ПоказатьПредупреждение(
				Неопределено,
				ПараметрыВыбора.СообщениеОбОшибке);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;

	Если Параметры.РежимВыбора Тогда
		
		ОповеститьОВыборе(ПараметрыВыбора.РезультатВыбора);
		
	ИначеЕсли ЗначениеЗаполнено(ПараметрыВыбора.РезультатВыбора) Тогда
		
		Если ЗначениеЗаполнено(ПараметрыВыбора.ИмяОткрываемойФормы) Тогда
			
			ПараметрыОткрытия = Новый Структура
				("Ключ", ПараметрыВыбора.РезультатВыбора);
			
			ОповещениеОЗакрытии = Новый ОписаниеОповещения(
				"ОбработкаЗакрытияФормыЭлемента",
				ЭтотОбъект);
			
			ОткрытьФорму(
				ПараметрыВыбора.ИмяОткрываемойФормы,
				ПараметрыОткрытия,
				ЭтотОбъект,
				УникальныйИдентификатор,
				,
				,
				ОповещениеОЗакрытии);
			
		КонецЕсли;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбщегоНазначения

&НаКлиенте
Процедура ОбработкаЗакрытияФормыСоздания(Результат, ДополнительныеПараметры) Экспорт
	
	ДопПараметры = Новый Структура("Источник, ДопПараметры");
	ЗаполнитьЗначенияСвойств(ДопПараметры, ДополнительныеПараметры);
	
	Если ДопПараметры.Источник = "ОткрытиеЗначения"
		Или ДопПараметры.Источник = "СозданиеЭлемента" Тогда
		СоздатьДерево();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОписаниеОповещенияЗакрытиеФормыСоздания(
		Источник,
		ДополнительныеПараметры = Неопределено)
	
	ДопПараметры = Новый Структура;
	
	ДопПараметры.Вставить("Источник",     Источник);
	ДопПараметры.Вставить("ДопПараметры", ДополнительныеПараметры);
	
	Возврат Новый ОписаниеОповещения(
		"ОбработкаЗакрытияФормыСоздания",
		ЭтотОбъект,
		ДопПараметры);
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	ВидимостьОбщихКоманд = Ложь;
	
	НастройкиПлатежныхСистемСобытия.ПриОпределенииВидимостиОбщихКоманд(
		КешПараметров,
		ВидимостьОбщихКоманд);
	
	Элементы.ГруппаСоздать.Видимость = ВидимостьОбщихКоманд;
	Элементы.СписокКонтекстноеМенюСкопировать.Видимость = ВидимостьОбщихКоманд;
	Элементы.СписокКонтекстноеМенюИзменить.Видимость = ВидимостьОбщихКоманд;

	НесколькоПодсистем = (Параметры.Подсистема = "Все")
		И Элементы.ГруппаСоздать.ПодчиненныеЭлементы.Количество() > 1;
	
	Если НесколькоПодсистем Тогда
		Элементы.ГруппаСоздать.Видимость = Истина;
		Элементы.ГруппаСоздатьОдинТип.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСоздать.Видимость = Ложь;
		Элементы.ГруппаСоздатьОдинТип.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СтандартныеПодсистемы_ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	
	Если Не НастройкиПлатежныхСистемКлиент.КомандаДоступна(ЭтотОбъект, Команда) Тогда
		Возврат;
	КонецЕсли;
	
	МодульПодключаемыеКомандыКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиент");
	МодульПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
	
	ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(Знач ПараметрыВыполнения)
	
	МодульПодключаемыеКоманды = ОбщегоНазначения.ОбщийМодуль("ПодключаемыеКоманды");
	МодульПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	
	МодульПодключаемыеКомандыКлиентСервер = ОбщегоНазначенияКлиент.ОбщийМодуль("ПодключаемыеКомандыКлиентСервер");
	МодульПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
