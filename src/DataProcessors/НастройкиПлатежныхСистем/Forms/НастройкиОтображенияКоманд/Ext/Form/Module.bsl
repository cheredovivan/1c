///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КешПараметров = Новый Структура;
	КешПараметров.Вставить("ИменаПодсистем", Новый Массив);
	КешПараметров.Вставить("ОписаниеКоманд", Новый Соответствие);
	
	ЗаполнитьСписокОбъектов(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	СохранитьНаСервере(Объекты, КешПараметров.ИменаПодсистем);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработатьКоманду(Команда)
	
	ОписаниеКоманды = КешПараметров.ОписаниеКоманд.Получить(
		Команда.Имя);
	
	Если ОписаниеКоманды = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ОписаниеКоманды.ТипКнопки = "УстановитьВсе" Тогда
		УстановитьЗначениеФлагов(
			ОписаниеКоманды.ИмяПодсистемы,
			Истина);
	ИначеЕсли ОписаниеКоманды.ТипКнопки = "СнятьВсе" Тогда
		УстановитьЗначениеФлагов(
			ОписаниеКоманды.ИмяПодсистемы,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокОбъектов(Отказ)
	
	ПараметрыПодсистем = Новый Массив; // Массив Из см. НовыйПараметрыПодсистемы
	
	Настройки = НастройкиПлатежныхСистемСлужебный.НастройкиОтображенияКоманд();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		ПриДобавленииПодсистемыСБП(
			ПараметрыПодсистем,
			Настройки.Получить("СистемаБыстрыхПлатежей"));
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	Для Каждого ПараметрыПодсистемы Из ПараметрыПодсистем Цикл
		
		КешПараметров.ИменаПодсистем.Добавить(ПараметрыПодсистемы.ИмяПодсистемы);
		
		ДобавитьОписаниеРеквизита(
			ДобавляемыеРеквизиты,
			ПараметрыПодсистемы.ИмяПодсистемы,
			ПараметрыПодсистемы.Представление);
		
	КонецЦикла;
	
	Если КешПараметров.ИменаПодсистем.Количество() = 0 Тогда
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Ни одна из доступных подсистем не использует команды, либо отсутствуют права на настройку.';
				|en = 'None of the available subsystems uses commands, or there are no setup rights.'"));
		Возврат;
	КонецЕсли;
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	ТолькоОднаПодсистема = ПараметрыПодсистем.Количество() = 1;
	
	Для Каждого ПараметрыПодсистемы Из ПараметрыПодсистем Цикл
		
		ДобавитьЭлементы(
			ПараметрыПодсистемы.ИмяПодсистемы,
			ПараметрыПодсистемы.Представление,
			ТолькоОднаПодсистема);

		ИмяПоляИспользуется = ПараметрыПодсистемы.ИмяПодсистемы + "_Используется";
		ИмяПоляДоступно     = ПараметрыПодсистемы.ИмяПодсистемы + "_Доступно";
		
		Для Каждого ОписаниеОбъекта Из ПараметрыПодсистемы.СписокОбъектов Цикл
			
			ПоискОбъекта = Объекты.НайтиСтроки(
				Новый Структура(
					"Объект",
					ОписаниеОбъекта.ИмяОбъекта));
			
			Если ПоискОбъекта.Количество() = 0 Тогда
				СтрокаТаблицы = Объекты.Добавить();
				СтрокаТаблицы.Объект = ОписаниеОбъекта.ИмяОбъекта;
				СтрокаТаблицы.Представление = ОписаниеОбъекта.Представление;
			Иначе
				СтрокаТаблицы = ПоискОбъекта[0];
			КонецЕсли;
			
			СтрокаТаблицы[ИмяПоляИспользуется] = ОписаниеОбъекта.Используется;
			СтрокаТаблицы[ИмяПоляДоступно]     = Истина;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если ТолькоОднаПодсистема Тогда
		Элементы.Объекты.Шапка = Ложь;
	КонецЕсли;
	
	Объекты.Сортировать("Представление");
	
КонецПроцедуры

// Функция-конструктор описания подсистемы, для которой есть возможность использовать команды.
//
// Параметры:
//  ИмяПодсистемы - Строка - произвольный идентификатор подсистемы.
//    Должен соответствовать требованиям к именам идентификаторов.
//  Представление - Строка - Представление подсистемы.
//    Если не задано, то будет использоваться идентификатор.
//
// Возвращаемое значение:
//  Структура - описание подсистемы:
//    * ИмяПодсистемы - Строка - имя подсистемы.
//    * СписокОбъектов - Массив Из см. НовыйОписаниеОбъектаСКомандой.
//
&НаСервере
Функция НовыйПараметрыПодсистемы(
		ИмяПодсистемы,
		Представление = "")

	Результат = Новый Структура;
	
	Результат.Вставить("ИмяПодсистемы",  ИмяПодсистемы);
	Результат.Вставить("СписокОбъектов", Новый Массив);
	
	Если ПустаяСтрока(Представление) Тогда
		Результат.Вставить("Представление", ИмяПодсистемы);
	Иначе
		Результат.Вставить("Представление", Представление);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции
	
// Функция-конструктор описания объекта, для которого доступен вывод команды.
//
// Параметры:
//  ИмяОбъекта - Строка - имя объекта метаданных.
//  Представление - Строка - Представление объекта метаданных.
//  Используется - Булево - признак использования команд подсистемы в объекте.
//
// Возвращаемое значение:
//  Структура - описание подсистемы:
//    * ИмяОбъекта - Строка - имя объекта метаданных.
//    * Представление - Строка - Представление объекта метаданных.
//    * Используется - Булево - признак использования команд подсистемы в объекте.
//
&НаСервере
Функция НовыйОписаниеОбъектаСКомандой(
		ИмяОбъекта,
		Представление,
		Используется)

	Результат = Новый Структура;
	
	Результат.Вставить("ИмяОбъекта",    ИмяОбъекта);
	Результат.Вставить("Представление", Представление);
	Результат.Вставить("Используется",  Используется);
	
	Возврат Результат;

КонецФункции

// Выполняет добавление информации о подсистеме Система быстрых платежей.
// 
// Параметры:
//  ПараметрыПодсистем - Массив Из см. НовыйПараметрыПодсистемы.
//  Настройки - см. НастройкиПлатежныхСистемСлужебный.НастройкиОтображенияКоманд
//
&НаСервере
Процедура ПриДобавленииПодсистемыСБП(ПараметрыПодсистем, Настройки)
	
	МодульСистемаБыстрыхПлатежей = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежей");
	
	Если Не МодульСистемаБыстрыхПлатежей.НастройкаПодключенияДоступна() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыСБП = НовыйПараметрыПодсистемы("СистемаБыстрыхПлатежей", "СБП");
	
	МодульСистемаБыстрыхПлатежейСлужебный = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
	
	ИменаОбъектов = МодульСистемаБыстрыхПлатежейСлужебный.ИменаДокументовСКомандой();
	
	Для Каждого ИмяОбъекта Из ИменаОбъектов Цикл
		Менеджер = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(
			ИмяОбъекта);
		МетаданныеОбъекта = Метаданные.НайтиПоТипу(
			ТипЗнч(Менеджер.ПустаяСсылка()));
		Пометка = МодульСистемаБыстрыхПлатежейСлужебный.КомандаСБПИспользуется(
			ИмяОбъекта,
			Настройки);
		ПараметрыСБП.СписокОбъектов.Добавить(
			НовыйОписаниеОбъектаСКомандой(
				ИмяОбъекта,
				МетаданныеОбъекта.Синоним,
				Пометка));
	КонецЦикла;
	
	ПараметрыПодсистем.Добавить(ПараметрыСБП);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОписаниеРеквизита(
		ДобавляемыеРеквизиты,
		ИмяПодсистемы,
		ПредставлениеПодсистемы)
	
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			ИмяПодсистемы + "_Используется",
			Новый ОписаниеТипов("Булево"),
			"Объекты",
			ПредставлениеПодсистемы,
			Истина));
			
	ДобавляемыеРеквизиты.Добавить(
		Новый РеквизитФормы(
			ИмяПодсистемы + "_Доступно",
			Новый ОписаниеТипов("Булево"),
			"Объекты"));
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьЭлементы(
		Знач ИмяПодсистемы,
		Знач ПредставлениеПодсистемы,
		Знач ТолькоОднаПодсистема)
	
	// Колонка таблицы
	ИмяПоля = ИмяПодсистемы + "_Используется";
		
	НоваяКолонка = Элементы.Добавить(
		ИмяПоля,
		Тип("ПолеФормы"),
		Элементы.Объекты);
	
	НоваяКолонка.Вид         = ВидПоляФормы.ПолеФлажка;
	НоваяКолонка.ПутьКДанным = "Объекты." + ИмяПоля;
	
	Если ТолькоОднаПодсистема Тогда
		Элементы.Переместить(НоваяКолонка, Элементы.Объекты, Элементы.ОбъектыПредставление);
		РодительУстановитьФлажки = Элементы.ГруппаПометки;
		РодительСнятьФлажки      = Элементы.ГруппаПометки;
		ОтображениеКоманды       = ОтображениеКнопки.Картинка;
	Иначе
		РодительУстановитьФлажки = Элементы.ПодменюУстановитьФлажки;
		РодительСнятьФлажки      = Элементы.ПодменюСнятьФлажки;
		ОтображениеКоманды       = ОтображениеКнопки.Текст;
	КонецЕсли;
	
	УстановитьУсловноеОформление(ИмяПодсистемы);
	
	// Кнопка "Установить все"
	ИмяКоманды = ИмяПодсистемы + "_УстановитьВсе";
	
	НоваяКоманда = Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Заголовок   = ПредставлениеПодсистемы;
	НоваяКоманда.Действие    = "Подключаемый_ОбработатьКоманду";
	НоваяКоманда.Картинка    = БиблиотекаКартинок.УстановитьФлажки;
	НоваяКоманда.Отображение = ОтображениеКоманды;
	
	Кнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), РодительУстановитьФлажки);
	Кнопка.ИмяКоманды = ИмяКоманды;
	
	КешПараметров.ОписаниеКоманд.Вставить(
		ИмяКоманды,
		Новый Структура(
			"ИмяПодсистемы, ТипКнопки",
			ИмяПодсистемы,
			"УстановитьВсе"));
	
	// Кнопка "Снять все"
	ИмяКоманды = ИмяПодсистемы + "_СнятьВсе";
	
	НоваяКоманда = Команды.Добавить(ИмяКоманды);
	НоваяКоманда.Заголовок   = ПредставлениеПодсистемы;
	НоваяКоманда.Действие    = "Подключаемый_ОбработатьКоманду";
	НоваяКоманда.Картинка    = БиблиотекаКартинок.СнятьФлажки;
	НоваяКоманда.Отображение = ОтображениеКоманды;
	
	Кнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), РодительСнятьФлажки);
	Кнопка.ИмяКоманды = ИмяКоманды;
	
	КешПараметров.ОписаниеКоманд.Вставить(
		ИмяКоманды,
		Новый Структура(
			"ИмяПодсистемы, ТипКнопки",
			ИмяПодсистемы,
			"СнятьВсе"));
			
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление(ИмяПодсистемы)

	НовыйЭлемент = УсловноеОформление.Элементы.Добавить();
	НовыйЭлемент.Использование = Истина;
	
	// Условие применения оформления
	НовыйОтбор                = НовыйЭлемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	НовыйОтбор.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Объекты." + ИмяПодсистемы + "_Доступно");
	НовыйОтбор.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	НовыйОтбор.ПравоеЗначение = Ложь;
	
	// Оформляемое поле
	НовоеПоле               = НовыйЭлемент.Поля.Элементы.Добавить();
	НовоеПоле.Использование = Истина;
	НовоеПоле.Поле          = Новый ПолеКомпоновкиДанных(ИмяПодсистемы + "_Используется");
	
	// Оформление
	НовыйЭлемент.Оформление.УстановитьЗначениеПараметра("Доступность", Ложь);
	НовыйЭлемент.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначениеФлагов(ИмяПодсистемы, НовоеЗначение)
	
	ИмяКолонкиИспользуется = ИмяПодсистемы + "_Используется";
	ИмяКолонкиДоступно     = ИмяПодсистемы + "_Доступно";
	
	Для Каждого ТекущаяСтрока Из Объекты Цикл
		Если ТекущаяСтрока[ИмяКолонкиДоступно] Тогда
			ТекущаяСтрока[ИмяКолонкиИспользуется] = НовоеЗначение;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНаСервере(Знач Объекты, Знач ИменаПодсистем)
	
	НовыеНастройки = Новый Соответствие;
	
	Для Каждого ИмяПодсистемы Из ИменаПодсистем Цикл
		
		НастройкиПодсистемы = Новый Соответствие;
		
		ПоискСтрок = Объекты.НайтиСтроки(
			Новый Структура(
				ИмяПодсистемы + "_Доступно",
				Истина));
		ИмяКолонки = ИмяПодсистемы + "_Используется";
		Для Каждого ТекущаяСтрока Из ПоискСтрок Цикл
			НастройкиПодсистемы.Вставить(
				ТекущаяСтрока.Объект,
				ТекущаяСтрока[ИмяКолонки]);
		КонецЦикла;
		
		НовыеНастройки.Вставить(ИмяПодсистемы, НастройкиПодсистемы);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	НастройкиПлатежныхСистемСлужебный.УстановитьНастройкиОтображенияКоманд(
		НовыеНастройки);
	УстановитьПривилегированныйРежим(Ложь);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти
