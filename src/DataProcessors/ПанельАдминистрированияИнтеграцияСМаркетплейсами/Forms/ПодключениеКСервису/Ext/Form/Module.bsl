
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнтеграцияСМаркетплейсамиСИ.УстановитьЗначенияРеквизитовПоПараметрам(ЭтотОбъект, Параметры);
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыСервиса.ДанныеИнтеграции, , "client_id, client_secret");
	
	УстановитьКэшированныеЗначения();
	НастроитьФормуПоВидуМаркетплейса();
	ЗаполнитьФункциональность();
	СформироватьДанныеВыбораОрганизаций(Параметры.ДействующиеОрганизации);
	УстановитьУсловноеОформление();
	УстановитьОформлениеЭлементов();
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Кэш.АвторизацияПоФункциональномуТокену Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("client_id"));
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("client_secret"));
	Иначе
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Токены"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(Элементы.ДекорацияСостояние.Подсказка)
		И Не ПустаяСтрока(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		ПоказатьПредупреждение( , Элементы.ДекорацияСостояние.Подсказка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФункциональность

&НаКлиенте
Процедура ФункциональностьПриАктивизацииЯчейки(Элемент)
	Элементы.Функциональность.ВыделенныеСтроки.Очистить();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТокены

&НаКлиенте
Процедура ТокеныТокенПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Токены.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ХешТокена = "";
	ЗаполнитьДанныеФормы(Элементы.Токены.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТокеныПриИзменении(Элемент)
	УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
КонецПроцедуры

&НаКлиенте
Процедура ТокеныПослеУдаления(Элемент)
	РассчитатьФункциональностьПоТокенамПриНеобходимости();
КонецПроцедуры

&НаКлиенте
Процедура ТокеныВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент = Элементы.ТокеныСостояние И Не ПустаяСтрока(Элементы.Токены.ТекущиеДанные.Информация) Тогда
		Если Элементы.Токены.ТекущиеДанные.Состояние = 7 Тогда
			Заголовок = НСтр("ru = 'Информация';
							|en = 'Information'");
		Иначе
			Заголовок = "";
		КонецЕсли;
		ПоказатьПредупреждение( , Элементы.Токены.ТекущиеДанные.Информация, , Заголовок);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервере = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОпераций(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	
	Если ИнтернетПоддержкаПодключена Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, ПараметрыОперации);
	Иначе
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДлительныхОперацийПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОперацийПродолжение(Результат, ПараметрыОперации) Экспорт
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
		ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации)
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.Токены.КоманднаяПанель.Доступность = Ложь;
		УстановитьОформлениеСостояния(Элементы);
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	Если ЗначениеЗаполнено(ДатаПодключенияСИ) Тогда
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	КонецЕсли;
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ПараметрыПолучитьУчетныеЗаписи(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись() Тогда
			ПараметрыСоздатьОбновитьУчетнуюЗапись(Операция.Значение, ПараметрыОперации, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыПолучитьУчетныеЗаписи(Значение, ПараметрыОперации, Отказ)
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьУчетныеЗаписи();
	Значение.УчетнаяЗапись = УчетнаяЗапись;
	Значение.ВсеУчетныеЗаписи = Ложь;
	Значение.ПроверятьТокены = Истина;
	Значение.ВидМаркетплейса = ВидМаркетплейса;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыСоздатьОбновитьУчетнуюЗапись(Значение, ПараметрыОперации, Отказ)
	
	Если Не ПроверитьЗаполнение() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаСоздатьОбновитьУчетнуюЗапись();
	Если Не ОбщегоНазначения.СсылкаСуществует(УчетнаяЗапись) Тогда
		УчетнаяЗапись = Справочники.УчетныеЗаписиМаркетплейсов.ПолучитьСсылку()
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(Значение, ЭтотОбъект, , "Токены, Функциональность");
	Значение.ВерсияКонфигурации = ИнтеграцияСМаркетплейсамиСИ.ВерсияКонфигурацииДляСИ();
	Значение.Представление = Строка(Организация);
	УстановитьПривилегированныйРежим(Истина);
	ДанныеОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "ИНН, КПП");
	УстановитьПривилегированныйРежим(Ложь);
	Значение.ИНН = ДанныеОрганизации.ИНН;
	Значение.КПП = ДанныеОрганизации.КПП;
	
	Значение.Функциональность = Функциональность.Выгрузить(Новый Структура("Использовать", Истина),
		"Идентификатор").ВыгрузитьКолонку("Идентификатор");
	Если Значение.Функциональность.Количество() = 0 И Кэш.АвторизацияПоФункциональномуТокену Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Для продолжения требуется активировать хотя бы одну функциональность.';
				|en = 'To continue, enable at least one feature.'"), , "Функциональность", , Отказ);
		Возврат;
	КонецЕсли;
	
	КатегорииТокенов = Новый Соответствие;
	УникальностьТокенов = Новый Соответствие;
	
	Для Каждого Строка Из Токены Цикл
		
		Если Не ПустаяСтрока(Строка.ХешТокена) И Строка.Состояние = СостояниеУспех() Тогда
			Значение.Токены.Добавить(Строка.ХешТокена);
			УникальностьТокенов.Вставить(Строка.ХешТокена, Истина);
			Продолжить;
		КонецЕсли;
		
		Если Элементы.Организация.ТолькоПросмотр И Не Строка.Токен = БезопаснаяМаска() Тогда
			ЗаполнитьДанныеТокена(Строка);
		КонецЕсли;
		
		Если Строка.Состояние = СостояниеОшибка() И Не ПустаяСтрока(Строка.Информация) Тогда
			ОбщегоНазначения.СообщитьПользователю(Строка.Информация, , СтрШаблон("Токены[%1].Токен",
				Токены.Индекс(Строка)), , Отказ);
		КонецЕсли;
		
		Если Строка.Состояние = СостояниеПредупреждение() И Не ПустаяСтрока(Строка.Информация) Тогда
			ОбщегоНазначения.СообщитьПользователю(Строка.Информация, , СтрШаблон("Токены[%1].Токен",
				Токены.Индекс(Строка)), , Отказ);
		КонецЕсли;
		
		Если УникальностьТокенов[Строка.Токен] = Неопределено
			И УникальностьТокенов[ОбщегоНазначения.КонтрольнаяСуммаСтрокой(Строка.Токен)] = Неопределено Тогда
			УникальностьТокенов.Вставить(Строка.Токен, Истина);
		Иначе
			ОбщегоНазначения.СообщитьПользователю(НСтр(
				"ru = 'Обнаружены идентичные токены, для продолжения удалите дубли.';
				|en = 'Identical tokens are found. Delete duplicates to continue.'"), ,
				СтрШаблон("Токены[%1].Токен", Токены.Индекс(Строка)), , Отказ);
		КонецЕсли;
		
		Для Каждого Элемент Из СтрРазделить(Строка.Категории, ",") Цикл
			КатегорииТокенов.Вставить(СокрЛП(Элемент), Истина);
		КонецЦикла;
		
		Значение.Токены.Добавить(Строка.Токен);
		
	КонецЦикла;
	
	Если (client_id = БезопаснаяМаска() Или client_secret = БезопаснаяМаска()) И ЗначениеЗаполнено(ХешКлючейАвторизации) Тогда
		Значение.client_id = "";
		Значение.client_secret = "";
	Иначе
		Значение.ХешКлючейАвторизации = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Истина);
		Возврат;
	КонецЕсли;
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Если ДополнительныеПараметры.ОбработкаРезультатаНаСервере Тогда
		РезультатМенеджера = ОбработатьРезультатМенеджераДлительныхОпераций(Результат.АдресРезультата);
	Иначе
		РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	КонецЕсли;
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			
			ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки, Отказ);
			Если Не Отказ Тогда
				УстановитьОформлениеСостояния(Элементы, РезультатОбработки, РезультатОперации.Ошибки);
			КонецЕсли;
			
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись() Тогда
			
			Если РезультатОперации.КодСостояния = 200 Тогда
				Если Не Кэш.АвторизацияПоФункциональномуТокену 
					И ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
					
					НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций);
					Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
					
				КонецЕсли;

				Оповестить("Маркетплейсы_УчетнаяЗаписьСозданиеОбновление");
				УстановитьОформлениеСостояния(Элементы, Истина);

			ИначеЕсли РезультатОперации.КодСостояния = 400 Тогда
				УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
			Иначе
				Отказ = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

// Обрабатывает результат менеджера длительных операций на сервере.
// 
// Параметры:
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Произвольный - Результат обработки
&НаСервере
Функция ОбработатьРезультатМенеджераДлительныхОпераций(Знач АдресРезультата)
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись() Тогда
			ОбработатьРезультатСоздатьОбновитьУчетнуюЗапись(РезультатОперации, РезультатОбработки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат РезультатМенеджера;
	
КонецФункции

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ТолькоПросмотр = Ложь;
	Элементы.Токены.КоманднаяПанель.Доступность = Истина;
	УстановитьОформлениеОсновнойКнопки(ЭтотОбъект);
	
	Если Отказ Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь);
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработать результат получить учетные записи.
// 
// Параметры:
//  РезультатОперации - См. ИнтеграцияСМаркетплейсамиСИ.НовыйРезультатМетодаСервиса
//  РезультатОбработки - Булево - Результат обработки
//  Отказ - Булево - Отказ
&НаСервере
Процедура ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.Ответ.УчетныеЗаписи.Количество() <> 1 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Данные = РезультатОперации.Ответ.УчетныеЗаписи[0]; // см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
	
	ХешКлючейАвторизации = Данные.ХешКлючейАвторизации;
	client_id = БезопаснаяМаска();
	client_secret = БезопаснаяМаска();
	
	Организация = Данные.Организация;
	Если ЗначениеЗаполнено(Организация) И Элементы.Организация.СписокВыбора.НайтиПоЗначению(Организация) = Неопределено Тогда
		Элементы.Организация.СписокВыбора.Добавить(Организация);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Данные.Ошибки) Тогда
		РезультатОперации.Ошибки.Добавить(Данные.Ошибки);
	КонецЕсли;

	Для Каждого ОписаниеТокена Из Данные.Токены Цикл
		НовыйТокен = Токены.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйТокен, ОписаниеТокена);
		НовыйТокен.Состояние = СостояниеУспех();
		НовыйТокен.Токен = БезопаснаяМаска();
		Если Не ПустаяСтрока(ОписаниеТокена.Ошибка) Тогда
			Если ОписаниеТокена.Ошибка <> ТекстОшибкиСрокДействияТокенаИстекает() Тогда
				НовыйТокен.Информация = ОписаниеТокена.Ошибка;
				НовыйТокен.Состояние = СостояниеОшибка();
			КонецЕсли;
		КонецЕсли;
		Если Не ПустаяСтрока(ОписаниеТокена.Ошибка) Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (Строка №%2)';
																						|en = '%1 (line %2)'"),
				ОписаниеТокена.Ошибка, Токены.Индекс(НовыйТокен) + 1);
			РезультатОперации.Ошибки.Добавить(ТекстОшибки);
		КонецЕсли;
	КонецЦикла;
	
	ОбновитьФункциональностьПоДаннымСервисаПриНеобходимости(Данные);
	
	РассчитатьФункциональностьПоТокенамПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатСоздатьОбновитьУчетнуюЗапись(РезультатОперации, РезультатОбработки)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		
		Если РезультатОперации.Ошибки.Количество() > 0 Тогда
			Ошибки = СтрРазделить(РезультатОперации.Ошибки[0], Символы.ПС);
			Если Ошибки.Количество() = 2 Тогда
				Токен = Токены.НайтиСтроки(Новый Структура("Токен", Ошибки[1]));
				Если Токен.Количество() = 0 Тогда
					Токен = Токены.НайтиСтроки(Новый Структура("ХешТокена", Ошибки[1]));
				КонецЕсли;
				Если Токен.Количество() = 1 Тогда
					Токен[0].Состояние = СостояниеОшибка();
					Токен[0].Информация = Ошибки[0];
					РезультатОперации.Ошибки[0] = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru = '%1 (Строка №%2)';
						|en = '%1 (line %2)'"), Ошибки[0], Токены.Индекс(Токен[0]) + 1);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		РассчитатьФункциональностьПоТокенамПриНеобходимости();
		РезультатОбработки = Ложь;
		Возврат;
		
	КонецЕсли;
	
	Если Не ОбщегоНазначения.СсылкаСуществует(УчетнаяЗапись) Тогда
		УчетнаяЗаписьОбъект = Справочники.УчетныеЗаписиМаркетплейсов.СоздатьЭлемент();
		УчетнаяЗаписьОбъект.УстановитьСсылкуНового(УчетнаяЗапись);
		ЗаполнитьЗначенияСвойств(УчетнаяЗаписьОбъект, ЭтотОбъект);
		УчетнаяЗаписьОбъект.ВидМаркетплейса = ВидМаркетплейса;
		УчетнаяЗаписьОбъект.Наименование = Строка(Организация);
		УчетнаяЗаписьОбъект.Записать();
	КонецЕсли;
	
	Элементы.Организация.ТолькоПросмотр = Истина;
	
	Модифицированность = Ложь;

	Если Не Кэш.АвторизацияПоФункциональномуТокену Тогда
		// получим созданную учетную запись для расчета функциональности
		ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());

		ВыполнитьДлительныеОперации(Истина, ПараметрыОперации);
		Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций", ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура НастроитьФормуПоВидуМаркетплейса()
	
	ТорговаяПлощадка = ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса);
	Элементы.ДекорацияЛоготип.Картинка = 
		ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ВидМаркетплейса, "64");
	Элементы.ДекорацияЛоготип.РасширеннаяПодсказка.Заголовок = ТорговаяПлощадка;
	
	Элементы.ФункциональностьПодсказка.Видимость = Кэш.АвторизацияПоФункциональномуТокену;
	Элементы.ГруппаТокены.Видимость = Кэш.АвторизацияПоФункциональномуТокену;
	Элементы.ГруппаУчетныеДанные.Видимость = Не Кэш.АвторизацияПоФункциональномуТокену;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	КатегорииМаркетплейса = Новый Соответствие;
	КатегорииМаркетплейса.Вставить(1, НСтр("ru = 'Контент';
											|en = 'Content'"));
	КатегорииМаркетплейса.Вставить(2, НСтр("ru = 'Аналитика';
											|en = 'Dimension'"));
	КатегорииМаркетплейса.Вставить(3, НСтр("ru = 'Цены и скидки';
											|en = 'Prices and discounts'"));
	КатегорииМаркетплейса.Вставить(4, НСтр("ru = 'Маркетплейс';
											|en = 'Marketplace'"));
	КатегорииМаркетплейса.Вставить(5, НСтр("ru = 'Статистика';
											|en = 'Statistics'"));
	КатегорииМаркетплейса.Вставить(6, НСтр("ru = 'Продвижение';
											|en = 'Promotion'"));
	КатегорииМаркетплейса.Вставить(7, НСтр("ru = 'Вопросы и отзывы';
											|en = 'Questions and feedback'"));
	КатегорииМаркетплейса.Вставить(9, НСтр("ru = 'Чат с покупателями';
											|en = 'Chat with customers'"));
	КатегорииМаркетплейса.Вставить(10, НСтр("ru = 'Поставки';
											|en = 'Deliveries'"));
	КатегорииМаркетплейса.Вставить(11, НСтр("ru = 'Возвраты покупателями';
											|en = 'Returns from customers'"));
	КатегорииМаркетплейса.Вставить(12, НСтр("ru = 'Документы';
											|en = 'Documents'"));
	КатегорииМаркетплейса.Вставить(30, НСтр("ru = 'Только чтение';
											|en = 'Read only'"));
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("КатегорииМаркетплейса", КатегорииМаркетплейса);
	Кэш.Вставить("АвторизацияПоФункциональномуТокену", ТипАвторизации(ВидМаркетплейса) = "ФункциональныйТокен");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Оформление цвета текста наименования функциональности по использованию
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныйДляВыбораЭлементБЭД);
	Элемент.Оформление.УстановитьЗначениеПараметра("ВертикальноеПоложение", ВертикальноеПоложение.Центр);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ФункциональностьНаименование.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Функциональность.Использовать");
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	// Оформление цвета текста подсказки функциональности
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныйДляВыбораЭлементБЭД);
	Элемент.Оформление.УстановитьЗначениеПараметра("ВертикальноеПоложение", ВертикальноеПоложение.Центр);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ФункциональностьПодсказка.Имя);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементов()
	
	Элементы.Организация.ТолькоПросмотр = ЗначениеЗаполнено(Организация);
	Элементы.Функциональность.ВысотаВСтрокахТаблицы = Функциональность.Количество();
	УстановитьОформлениеСостояния(Элементы);
	УстановитьОформлениеОсновнойКнопки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОформлениеОсновнойКнопки(ТекущийОбъект)
	
	Элементы = ТекущийОбъект.Элементы;
	
	Если Элементы.Организация.ТолькоПросмотр Тогда
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Переподключить';
													|en = 'Reconnect'");
	Иначе
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Подключить';
													|en = 'Connect'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОформлениеСостояния(Элементы, Результат = Неопределено, Информация = "")
	
	ЭлементСостояния = Элементы.ДекорацияСостояние; // ЭлементФормы
	ЭлементСостояния.Подсказка = "";
	Информация = ?(ТипЗнч(Информация) = Тип("Массив"), СтрСоединить(Информация, Символы.ПС), Информация);
	Текст1 = НСтр("ru = 'Текущее состояние';
					|en = 'Current state'");
	
	Если Результат = Ложь Тогда
		Шаблон = "%1: <span style=""color: ПоясняющийОшибкуТекст"">%2</span> <a href=""Ошибка"">(%3)</a>";
		Элементы.ДекорацияСостояниеКартинка.Картинка = БиблиотекаКартинок.ПредупреждениеКрасноеБЭД16;
		Если ПустаяСтрока(Информация) Тогда
			Информация = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ВнутренняяОшибкаСервиса();
		КонецЕсли;
		ЭлементСостояния.Подсказка = Информация;
		Текст2 = НСтр("ru = 'Не подключено';
						|en = 'Not connected'");
		Текст3 = НСтр("ru = 'ошибка';
						|en = 'Error'")
	ИначеЕсли Результат = Истина Тогда
		Если ПустаяСтрока(Информация) Тогда
			Шаблон = "%1: <span style=""color: ПоясняющийТекст"">%2</span>";
			Текст2 = НСтр("ru = 'Подключено';
							|en = 'Connected'");
			Элементы.ДекорацияСостояниеКартинка.Картинка = БиблиотекаКартинок.Успешно;
		Иначе
			Шаблон = "%1: <span style=""color: ПоясняющийТекст"">%2</span> <a href=""Ошибка"">(%3)</a>";
			Текст2 = НСтр("ru = 'Требуется переподключение';
							|en = 'Reconnection is required'");
			Элементы.ДекорацияСостояниеКартинка.Картинка = БиблиотекаКартинок.ПредупреждениеБЭД;
			ЭлементСостояния.Подсказка = Информация;
		КонецЕсли;
		Текст3 = НСтр("ru = 'Ошибка';
						|en = 'Error'");
	ИначеЕсли Результат = "ТребуетсяПодключение" Тогда
		Шаблон = "%1: <span style=""color: ЦветГиперссылкиБЭД"">%2</span>";
		Текст2 = НСтр("ru = 'Требуется подключение';
						|en = 'Connection is required'");
		Элементы.ДекорацияСостояниеКартинка.Картинка = БиблиотекаКартинок.Информация16;
	ИначеЕсли Результат = Неопределено Тогда
		Элементы.ДекорацияСостояниеКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация16;
		ЭлементСостояния.Заголовок = НСтр("ru = 'Обработка данных...';
											|en = 'Processing data...'");
		Возврат;
	КонецЕсли;
	
	#Если Сервер Тогда
	ЭлементСостояния.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Шаблон, Текст1, Текст2, Текст3);
	#Иначе
	ЭлементСостояния.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(Шаблон, Текст1, Текст2, Текст3);
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеФормы(Знач ДанныеТокена = Неопределено)
	
	Если ТипЗнч(ДанныеТокена) = Тип("Число") Тогда
		ДанныеТокена = Токены.НайтиПоИдентификатору(ДанныеТокена);
	КонецЕсли;
	
	Если ДанныеТокена <> Неопределено Тогда
		ЗаполнитьДанныеТокена(ДанныеТокена);
	КонецЕсли;
	РассчитатьФункциональностьПоТокенамПриНеобходимости();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьФункциональностьПоТокенамПриНеобходимости()
	
	Если Не Кэш.АвторизацияПоФункциональномуТокену Тогда
		Возврат;
	КонецЕсли;
	
	КатегорииТокенов = Новый Массив;
	
	Для Каждого ДанныеТокена Из Токены Цикл
		Если ДанныеТокена.Состояние <> СостояниеУспех() Тогда
			Продолжить;
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КатегорииТокенов,
			СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(ДанныеТокена.Категории, ",", Истина, Истина), Истина);
	КонецЦикла;
	
	Для Каждого ДанныеФункциональности Из Функциональность Цикл
		ТребуемыеКатегории = ДанныеФункциональности.Категории.ВыгрузитьЗначения();
		Разность = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ТребуемыеКатегории, КатегорииТокенов);
		ДанныеФункциональности.Использовать = Разность.Количество() = 0;
		Если ДанныеФункциональности.Использовать Тогда
			ДанныеФункциональности.Подсказка = "";
		Иначе
			ДанныеФункциональности.Подсказка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'требуется токен с категориями: %1';
					|en = 'a token with the following categories is required: %1'"), СтрСоединить(Разность, ", "));
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Обновить функциональность по данным сервиса при необходимости.
// 
// Параметры:
//  ДанныеСервиса - см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
&НаСервере
Процедура ОбновитьФункциональностьПоДаннымСервисаПриНеобходимости(ДанныеСервиса)
	Если Кэш.АвторизацияПоФункциональномуТокену Или ЗначениеЗаполнено(ДанныеСервиса.Ошибки) Тогда
		Возврат;
	КонецЕсли;
	Для Каждого ДанныеФункциональности Из Функциональность Цикл
		ДанныеФункциональности.Использовать = 
			ДанныеСервиса.Функциональность.Найти(ДанныеФункциональности.Идентификатор) <> Неопределено;		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеТокена(Знач ДанныеТокена)
	
	Если ТипЗнч(ДанныеТокена) = Тип("Число") Тогда
		ДанныеТокена = Токены.НайтиПоИдентификатору(ДанныеТокена);
	КонецЕсли;
	
	ДанныеТокена.Информация = "";
	
	Расшифровка = ИнтеграцияСМаркетплейсамиСИ.РасшифроватьТокенJWT(ДанныеТокена.Токен);
	
	Если Не ПустаяСтрока(Расшифровка.ТекстОшибки) Тогда
		ДанныеТокена.Информация = Расшифровка.ТекстОшибки;
		ДанныеТокена.Состояние = СостояниеОшибка();
		Возврат;
	КонецЕсли;
	
	Если Расшифровка.Данные["t"] = Истина Тогда
		ДанныеТокена.Информация = НСтр("ru = 'Токен предназначен для тестового контура.';
										|en = 'The token is intended for testing.'");
		ДанныеТокена.Состояние = СостояниеПредупреждение();
		Возврат;
	КонецЕсли;
	
	ДанныеТокена.Состояние = СостояниеУспех();
	ДанныеТокена.Категории = ТокенКатегории(Расшифровка.Данные["s"]);
	ДанныеТокена.ДействуетДо = МестноеВремя(Дата(1970, 1, 1) + Расшифровка.Данные["exp"], "GMT"); // RFC 7519
	ДанныеТокена.ТолькоЧтение = СтрНайти(ДанныеТокена.Категории, ТокенТолькоЧтение()) > 0;
	
	Если ДанныеТокена.ТолькоЧтение Тогда
		ДанныеТокена.Информация = НСтр("ru = 'Токен только для чтения.';
										|en = 'The token is read-only.'");
		ДанныеТокена.Состояние = СостояниеПредупреждение();
	КонецЕсли;
	
	Если УниверсальноеВремя(ТекущаяДатаСеанса()) > ДанныеТокена.ДействуетДо Тогда
		ДанныеТокена.Информация = НСтр("ru = 'Срок действия токена истек.';
										|en = 'The token has expired.'");
		ДанныеТокена.Состояние = СостояниеПредупреждение();
	КонецЕсли;
	
КонецПроцедуры

// Токен только чтение.
// 
// Возвращаемое значение:
//  Строка - Токен только чтение
&НаКлиентеНаСервереБезКонтекста
Функция ТокенТолькоЧтение()
	Возврат НСтр("ru = 'Только чтение';
				|en = 'Read only'");
КонецФункции

&НаСервере
Функция ТокенКатегории(Знач Число)
	
	ДвоичныйКод = "";
	Пока Число <> 0 Цикл
		ДвоичныйКод = ДвоичныйКод + Число % 2;
		Число = Цел(Число / 2);
	КонецЦикла;
	
	МассивКатегорий = Новый Массив;
	
	Для НачальныйНомер = 1 По СтрДлина(ДвоичныйКод) Цикл
		Символ = Сред(ДвоичныйКод, НачальныйНомер, 1);
		Если Символ = "1" Тогда
			МассивКатегорий.Добавить(Кэш.КатегорииМаркетплейса[НачальныйНомер - 1]);
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(МассивКатегорий, ", ");
	
КонецФункции

&НаСервере
Процедура ЗаполнитьФункциональность()
	
	НоваяФункциональность = Функциональность.Добавить();
	НоваяФункциональность.Идентификатор = 1;
	НоваяФункциональность.Использовать = Ложь;
	НоваяФункциональность.Наименование = НСтр("ru = 'Выгрузка остатков';
												|en = 'Export stock balance'");
	НоваяФункциональность.Категории.Добавить(Кэш.КатегорииМаркетплейса[1]); // Контент
	НоваяФункциональность.Категории.Добавить(Кэш.КатегорииМаркетплейса[4]); // Маркетплейс
	
	НоваяФункциональность = Функциональность.Добавить();
	НоваяФункциональность.Идентификатор = 2;
	НоваяФункциональность.Использовать = Ложь;
	НоваяФункциональность.Наименование = НСтр("ru = 'Выгрузка цен';
												|en = 'Export prices'");
	НоваяФункциональность.Категории.Добавить(Кэш.КатегорииМаркетплейса[1]); // Контент
	НоваяФункциональность.Категории.Добавить(Кэш.КатегорииМаркетплейса[3]); // Цены и скидки
	
	РассчитатьФункциональностьПоТокенамПриНеобходимости();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТипАвторизации(ВидМаркетплейса)
	Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) Тогда
		Возврат "ФункциональныйТокен";
	Иначе
		Возврат "УчетныеДанныеКлиента";
	КонецЕсли;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеОшибка()
	Возврат 4;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеПредупреждение()
	Возврат 5;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостояниеУспех()
	Возврат 7;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция БезопаснаяМаска()
	Возврат "**********";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстОшибкиСрокДействияТокенаИстекает()
	Возврат "Срок действия токена подходит к концу.";
КонецФункции

&НаСервере
Процедура СформироватьДанныеВыбораОрганизаций(ДействующиеОрганизации)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДействующиеОрганизации", ДействующиеОрганизации);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.Ссылка НЕ В (&ДействующиеОрганизации)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Элементы.Организация.СписокВыбора.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти