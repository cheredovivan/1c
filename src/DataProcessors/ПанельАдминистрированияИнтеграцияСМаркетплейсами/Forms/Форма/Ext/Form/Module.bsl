#Область ОписаниеПеременных

&НаКлиенте
Перем ЭтоОткрытиеФормы;

&НаКлиенте
Перем ОбновитьИнтерфейс;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСЯндексМаркет");
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСOzon");
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСWildberries");
	ОбновитьЗначениеРеквизитов("Константа_ИспользоватьИнтеграциюСLamoda");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтоОткрытиеФормы = Истина;
	УстановитьДоступностьЭлементов();
	ЭтоОткрытиеФормы = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	ОбновитьИнтерфейсПрограммы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьИнтеграциюСЯндексМаркетПриИзменении(Элемент)
	УстановитьДоступностьЭлементов(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтеграциюСOzonПриИзменении(Элемент)
	УстановитьДоступностьЭлементов(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтеграциюСWildberriesПриИзменении(Элемент)
	УстановитьДоступностьЭлементов(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтеграциюСLamodaПриИзменении(Элемент)
	УстановитьДоступностьЭлементов(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьНастройкиWildberriesНажатие(Элемент)
	
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.УчетныеЗаписи";
	ПараметрыФормы.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсWildberries");
	ПараметрыФормы.РежимОткрытияОкнаФормы = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьНастройкиПодключенияWildberriesНажатие(Элемент)
	
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НастройкиИнтеграции";
	ПараметрыФормы.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсWildberries");
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьНастройкиLamodaНажатие(Элемент)
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.УчетныеЗаписи";
	ПараметрыФормы.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсLamoda");
	ПараметрыФормы.РежимОткрытияОкнаФормы = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОткрытьНастройкиПодключенияLamodaНажатие(Элемент)
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НастройкиИнтеграции";
	ПараметрыФормы.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсLamoda");
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьНастройкиПодключенияЯндексМаркет(Команда)
	
	ОчиститьСообщения();   
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ОткрытьИзРазделаАдминистрирования",Истина);
	
	ОткрытьФорму("Обработка.ПанельАдминистрированияПродажиЯндексМаркет.Форма",
		ПараметрыОткрытия,, 
		Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиОбменаДаннымиЯндексМаркет(Команда)
	
	ОчиститьСообщения(); 
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПоказыватьВыборУчетнойЗаписи", Истина);
	
	ОткрытьФорму("Обработка.ПанельАдминистрированияПродажиЯндексМаркет.Форма.НастройкиОбменаДанными",
		ПараметрыОткрытия,,
		Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиПодключенияOzon(Команда)

	ОчиститьСообщения();
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.СписокПодключенийКСервису",, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиСинхронизацииДанныхOzon(Команда)

	ОчиститьСообщения();
	ОткрытьФорму("Обработка.УправлениеПродажамиНаOzon.Форма.НастройкиИнтеграции");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьДоступностьЭлементов(Элемент = Неопределено)
	
	Если Элемент = Неопределено Или Элемент = Элементы.ИспользоватьИнтеграциюСЯндексМаркет Тогда
		ПриИзмененииРеквизита(Элементы.ИспользоватьИнтеграциюСЯндексМаркет, Не ЭтоОткрытиеФормы);
		Элементы.ОткрытьНастройкиПодключенияЯндексМаркет.Доступность = Константа_ИспользоватьИнтеграциюСЯндексМаркет;
		Элементы.ОткрытьНастройкиОбменаДанными.Доступность = Константа_ИспользоватьИнтеграциюСЯндексМаркет;
	КонецЕсли;
	
	Если Элемент = Неопределено Или Элемент = Элементы.ИспользоватьИнтеграциюСOzon Тогда
		ПриИзмененииРеквизита(Элементы.ИспользоватьИнтеграциюСOzon, Не ЭтоОткрытиеФормы);
		Элементы.ОткрытьНастройкиOzon.Доступность = Константа_ИспользоватьИнтеграциюСOzon;
		Элементы.ОткрытьНастройкиПодключенияOzon.Доступность = Константа_ИспользоватьИнтеграциюСOzon;
	КонецЕсли;
	
	Если Элемент = Неопределено Или Элемент = Элементы.Константа_ИспользоватьИнтеграциюСWildberries Тогда
		ПриИзмененииРеквизита(Элементы.Константа_ИспользоватьИнтеграциюСWildberries, Не ЭтоОткрытиеФормы);
		Элементы.ДекорацияОткрытьНастройкиWildberries.Доступность = Константа_ИспользоватьИнтеграциюСWildberries;
		Элементы.ДекорацияОткрытьНастройкиПодключенияWildberries.Доступность = Константа_ИспользоватьИнтеграциюСWildberries;
	КонецЕсли;
	
	Если Элемент = Неопределено Или Элемент = Элементы.Константа_ИспользоватьИнтеграциюСLamoda Тогда
		ПриИзмененииРеквизита(Элементы.Константа_ИспользоватьИнтеграциюСLamoda, Не ЭтоОткрытиеФормы);
		Элементы.ДекорацияОткрытьНастройкиLamoda.Доступность = Константа_ИспользоватьИнтеграциюСLamoda;
		Элементы.ДекорацияОткрытьНастройкиПодключенияLamoda.Доступность = Константа_ИспользоватьИнтеграциюСLamoda;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизита(Элемент, ОбновлятьИнтерфейс = Истина, ВнешнееИзменение = Ложь)
	
	ПриИзмененииРеквизитаСервер(Элемент.Имя);
	
	Если ОбновлятьИнтерфейс Тогда
		ОбновитьИнтерфейс = Истина;
		ПодключитьОбработчикОжидания("ОбновитьИнтерфейсПрограммы", 2, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнтерфейсПрограммы()
	
	Если ОбновитьИнтерфейс = Истина Тогда
		ОбновитьИнтерфейс = Ложь;
		ОбщегоНазначенияКлиент.ОбновитьИнтерфейсПрограммы();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриИзмененииРеквизитаСервер(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	КонстантаИмя = СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Новый Структура);
	
	ОбновитьЗначениеРеквизитов(РеквизитПутьКДанным);
	
	ОбновитьПовторноИспользуемыеЗначения();
	
	Возврат КонстантаИмя;
	
КонецФункции

// Сохранить значение реквизита.
// 
// Параметры:
//  РеквизитПутьКДанным - Строка - Реквизит путь к данным
//  Результат - Структура - Результат
// 
// Возвращаемое значение:
//  Строка - Сохранить значение реквизита
&НаСервере
Функция СохранитьЗначениеРеквизита(РеквизитПутьКДанным, Результат)
	
	Если РеквизитПутьКДанным = "" Тогда
		Возврат "";
	КонецЕсли;
	
	// Определение имени константы.
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
		КонстантаИмя = Сред(РеквизитПутьКДанным, 11);
	КонецЕсли;
	
	// Сохранения значения константы.
	Если КонстантаИмя <> "" Тогда
		КонстантаМенеджер = Константы[КонстантаИмя];
		
		Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
			КонстантаЗначение = ЭтотОбъект[РеквизитПутьКДанным];
		КонецЕсли;
		
		Если ПравоДоступа("Изменение", Метаданные.Константы[КонстантаИмя]) Тогда
			Если КонстантаМенеджер.Получить() <> КонстантаЗначение Тогда
				КонстантаМенеджер.Установить(КонстантаЗначение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат КонстантаИмя;
	
КонецФункции

&НаСервере
Процедура ОбновитьЗначениеРеквизитов(РеквизитПутьКДанным = "")
	
	КонстантаИмя = "";
	Если НРег(Лев(РеквизитПутьКДанным, 10)) = НРег("Константа_") Тогда
		КонстантаИмя = Сред(РеквизитПутьКДанным, 11);
	КонецЕсли;
	
	Если КонстантаИмя <> "" Тогда
		УстановитьПривилегированныйРежим(Истина);
		ЭтотОбъект[РеквизитПутьКДанным] = Константы[КонстантаИмя].Получить();
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти