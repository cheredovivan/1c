
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//@skip-check unknown-form-parameter-access
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыСервиса.ДанныеИнтеграции);
	ИнтеграцияСМаркетплейсамиСИ.УстановитьЗначенияРеквизитовПоПараметрам(ЭтотОбъект, Параметры);
	НастроитьФормуПоВидуМаркетплейса();
	УстановитьУсловноеОформление();
	УстановитьКэшированныеЗначения();
	УстановитьОформлениеСостояния(Элементы, Истина);
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Маркетплейсы_УчетнаяЗаписьСозданиеОбновление"
		Или ИмяСобытия = "Маркетплейсы_ПодключениеУстановлено"
		Или ИмяСобытия = "ИнтернетПоддержкаПодключена" Тогда
		ПолучитьУчетныеЗаписи();
	ИначеЕсли ИмяСобытия = "ИнтернетПоддержкаОтключена"
		Или ИмяСобытия = "Маркетплейсы_ПодключениеУдалено" Тогда
		Список.Очистить();
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияСостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(Элементы.ДекорацияСостояние.Подсказка)
		И Не ПустаяСтрока(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		ПоказатьПредупреждение( , Элементы.ДекорацияСостояние.Подсказка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПодключенийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОчиститьСообщения();
	
	ТекущиеДанные = Список.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.УчетнаяЗапись) Тогда
		ПоказатьПредупреждение( , СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Учетная запись ""%1"" не принадлежит этой информационной базе.';
				|en = 'The ""%1"" account does not belong to this infobase.'"), ТекущиеДанные.Представление));
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущийЭлемент = Элементы.СписокПредставление Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.Организация) Тогда
			ПоказатьЗначение( , ТекущиеДанные.Организация);
		КонецЕсли;
		
	ИначеЕсли ТекущиеДанные.СостояниеКартинка = 2 Тогда
		
		ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
		ПараметрыОперации.Очередь.Добавить(ТекущиеДанные.УчетнаяЗапись , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
		
		НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
		
	Иначе
		
		ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
		ПараметрыФормы.ИмяФормы = "Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.ПодключениеКСервису";
		ПараметрыФормы.ВидМаркетплейса = ВидМаркетплейса;
		ПараметрыФормы.РежимОткрытияОкнаФормы = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ПараметрыФормы.Вставить("УчетнаяЗапись", ТекущиеДанные.УчетнаяЗапись);
		ПараметрыФормы.Вставить("Организация", ТекущиеДанные.Организация);
		ПараметрыФормы.Вставить("ДействующиеОрганизации", Кэш.ДействующиеОрганизации);
		
		ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ФормаУдалить.Доступность = ЗначениеЗаполнено(Элемент.ТекущиеДанные.УчетнаяЗапись);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СервисИнтеграции(Команда)
	
	ОчиститьСообщения();
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.ПодключениеКСервисуИнтеграции";
	ПараметрыФормы.РежимОткрытияОкнаФормы = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	ОчиститьСообщения();
	
	Модифицированность = Ложь;
	
	ПараметрыФормы = ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы();
	ПараметрыФормы.ИмяФормы = "Обработка.ПанельАдминистрированияИнтеграцияСМаркетплейсами.Форма.ПодключениеКСервису";
	ПараметрыФормы.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыФормы.РежимОткрытияОкнаФормы = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыФормы.Вставить("ДействующиеОрганизации", Кэш.ДействующиеОрганизации);
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОткрытьФормуСервисаМаркетплейсы(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура Удалить(Команда)
	
	ОчиститьСообщения();
	
	Модифицированность = Ложь;
	
	УдалитьУчетнуюЗаписьПродолжение();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОчиститьСообщения();
	
	Список.Очистить();
	Кэш.ДействующиеОрганизации.Очистить();
	
	Модифицированность = Ложь;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОпераций(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	
	Если ИнтернетПоддержкаПодключена Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, ПараметрыОперации);
	Иначе
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДлительныхОперацийПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОперацийПродолжение(Результат, ПараметрыОперации) Экспорт
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
		ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации)
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		ТолькоПросмотр = Истина;
		УстановитьОформлениеСостояния(Элементы);
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьУчетныеЗаписи()
	
	Список.Очистить();
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьУчетнуюЗаписьПродолжение()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Учетная запись не определена.';
														|en = 'The account is not defined.'"), , "Список");
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.УчетнаяЗапись) Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Невозможно удалить учетную запись, не принадлежащую этой информационной базе.';
										|en = 'Cannot delete an account that does not belong to this infobase.'"));
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru = 'Учетная запись ""%1"" станет недоступна. Продолжить?';
				|en = 'The ""%1"" account will become unavailable. Continue?'");
	
	Ответ = Ждать ВопросАсинх(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущиеДанные.Представление),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить(ТекущиеДанные.УчетнаяЗапись, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьУчетнуюЗапись());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ПараметрыПолучитьУчетныеЗаписи(Операция.Значение);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьУчетнуюЗапись() Тогда
			ПараметрыУдалитьУчетнуюЗапись(Операция.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыОперации.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыПолучитьУчетныеЗаписи(Значение)
	
	УчетнаяЗапись = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаПолучитьУчетныеЗаписи();
	Значение.УчетнаяЗапись = УчетнаяЗапись;
	Значение.ПроверятьТокены = Истина;
	Значение.ВсеУчетныеЗаписи = Не ЗначениеЗаполнено(УчетнаяЗапись);
	Значение.ВидМаркетплейса = ВидМаркетплейса;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыУдалитьУчетнуюЗапись(Значение)
	
	УчетнаяЗапись = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыйПараметрыЗапросаУдалитьУчетнуюЗапись();
	Значение.УчетнаяЗапись = УчетнаяЗапись;
	Значение.ВидМаркетплейса = ВидМаркетплейса;
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Истина);
		Возврат;
	КонецЕсли;
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		Метод = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьУчетнуюЗапись() Тогда
			ОбработатьРезультатУдалитьУчетнуюЗапись(РезультатОперации, РезультатОбработки, Отказ);
		ИначеЕсли Метод = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
			ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ТолькоПросмотр = Ложь;
	
	Если Отказ Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь);
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатУдалитьУчетнуюЗапись(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Поиск = Список.НайтиСтроки(РезультатОперации.Ответ);
	
	Если Поиск.Количество() = 1 Тогда
		
		ДействующаяОрганизация = Кэш.ДействующиеОрганизации.Найти(Поиск[0].Организация);
		Если ДействующаяОрганизация <> Неопределено Тогда
			Кэш.ДействующиеОрганизации.Удалить(ДействующаяОрганизация);
		КонецЕсли;
		
		Список.Удалить(Поиск[0]);
		
	КонецЕсли;
	
	Оповестить("Маркетплейсы_УчетнаяЗаписьУдаление", РезультатОперации.Ответ.УчетнаяЗапись);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолучитьУчетныеЗаписи(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(РезультатОперации.Ответ.УчетнаяЗапись) Тогда
		Список.Очистить();
		Кэш.ДействующиеОрганизации.Очистить();
	КонецЕсли;
	
	Для Каждого Элемент Из РезультатОперации.Ответ.УчетныеЗаписи Цикл
		
		Если ЗначениеЗаполнено(РезультатОперации.Ответ.УчетнаяЗапись) Тогда
			УчетныеЗаписи = Список.НайтиСтроки(Новый Структура("УчетнаяЗапись", РезультатОперации.Ответ.УчетнаяЗапись));
			Если УчетныеЗаписи.Количество() = 1 Тогда
				УчетнаяЗапись = УчетныеЗаписи[0];
			Иначе
				Прервать;
			КонецЕсли;
		Иначе
			УчетнаяЗапись = Список.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(УчетнаяЗапись, Элемент);
		УчетнаяЗапись.СостояниеКартинка = УчетнаяЗапись.Состояние;
		
		Если ЗначениеЗаполнено(Элемент.Организация) Тогда
			Если Кэш.ДействующиеОрганизации.Найти(Элемент.Организация) = Неопределено Тогда
				Кэш.ДействующиеОрганизации.Добавить(Элемент.Организация);
			КонецЕсли;
		Иначе
			УчетнаяЗапись.Состояние = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Список.Количество() = 0 Тогда
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
	Иначе
		УстановитьОформлениеСостояния(Элементы, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаСервере
Процедура НастроитьФормуПоВидуМаркетплейса()
	ТорговаяПлощадка = ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса);
	Элементы.ДекорацияЛоготип.Картинка = 
		ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ВидМаркетплейса, "64");
	Элементы.ДекорацияЛоготип.РасширеннаяПодсказка.Заголовок = ТорговаяПлощадка;
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Оформление отображаемого текста по состоянию
	ОформляемыйЭлемент = Элементы.СписокСостояние;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Нет в информационной базе';
																|en = 'Missing in the infobase'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Лево);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = 0;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Требуется переподключение';
																|en = 'Reconnection is required'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Лево);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = 1;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Неизвестно (обновить)';
																|en = 'Unknown (update)'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Лево);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = 2;
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Подключено';
																|en = 'Connected'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение",
		ГоризонтальноеПоложение.Лево);
	
	ОформляемоеПоле = Элемент.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ОформляемыйЭлемент.ПутьКДанным);
	ОтборЭлемента.ПравоеЗначение = 3;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОформлениеСостояния(Элементы, Результат = Неопределено, Информация = "")
	
	ЭлементСостояния = Элементы.ДекорацияСостояние;
	ЭлементСостояния.Подсказка = "";
	Информация = ?(ТипЗнч(Информация) = Тип("Массив"), СтрСоединить(Информация, Символы.ПС), Информация);
	Текст1 = НСтр("ru = 'Текущее состояние';
					|en = 'Current state'");
	
	Если Результат = Ложь Тогда
		Шаблон = "%1: <a href=""Ошибка"">(%2)</a>";
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.ПредупреждениеКрасноеБЭД16;
		Если ПустаяСтрока(Информация) Тогда
			Информация = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ВнутренняяОшибкаСервиса();
		КонецЕсли;
		ЭлементСостояния.Подсказка = Информация;
		Текст2 = НСтр("ru = 'Ошибка';
						|en = 'Error'");
	ИначеЕсли Результат = Истина Тогда
		Элементы.ДекорацияКартинка.Картинка = Новый Картинка;
		ЭлементСостояния.Заголовок = "";
		Возврат;
	ИначеЕсли Результат = Неопределено Тогда
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация16;
		ЭлементСостояния.Заголовок = НСтр("ru = 'Обработка данных...';
											|en = 'Processing data...'");
		Возврат;
	ИначеЕсли Результат = "ТребуетсяПодключение" Тогда
		Шаблон = "%1: <span style=""color: ЦветГиперссылкиБЭД"">%2</span>";
		Текст2 = НСтр("ru = 'Требуется подключение';
						|en = 'Connection is required'");
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.Информация16;
	КонецЕсли;
	
	#Если Сервер Тогда
	ЭлементСостояния.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Шаблон, Текст1, Текст2);
	#Иначе
	ЭлементСостояния.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(Шаблон, Текст1, Текст2);
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	Кэш.Вставить("ДействующиеОрганизации", Новый Массив);
	
КонецПроцедуры

#КонецОбласти