
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ПараметрыСервиса.ДанныеИнтеграции);
	
	Если ПустаяСтрока(Логин) Тогда
		Логин = ИнтеграцияСМаркетплейсамиСИ.ЛогинПользователяИТС();
	КонецЕсли;
	
	УстановитьКэшированныеЗначения();
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ТолькоПросмотр = Истина;
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяНастройкаАдминистратором");
		Возврат;
	КонецЕсли;
	
	УстановитьОформлениеЭлементов();
	ИнициироватьДлительныеОперации();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Кэш.ПараметрыМенеджераДлительныхОпераций) Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, Кэш.ПараметрыМенеджераДлительныхОпераций);
		Кэш.Удалить("ПараметрыМенеджераДлительныхОпераций");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций" Тогда
		Если Источник.ИдентификаторНазначения = УникальныйИдентификатор Тогда
			ОбработатьРезультатДлительныхОпераций(Параметр, Источник);
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИнтернетПоддержкаПодключена" Тогда
		ИПППослеПодключения(Параметр);
	ИначеЕсли ИмяСобытия = "ИнтернетПоддержкаОтключена" Тогда
		ОбработатьОтключениеИнтернетПоддержки();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		СтандартнаяОбработка = Ложь;
		Закрыть(РезультатЗакрытия());
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияУчетнаяЗаписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетПоддержкаПользователейКлиент");
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
								|en = 'Online support service is disabled.'");
	КонецЕсли;
	
	Если ПустаяСтрока(Логин) Тогда
		ОписаниеОЗавершении = Новый ОписаниеОповещения("ИПППослеПодключения", ЭтотОбъект);
		МодульИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОЗавершении, ЭтотОбъект);
	Иначе
		МодульИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуОфициальнаяПоддержка();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияСостояниеОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ПустаяСтрока(Элементы.ДекорацияСостояние.Подсказка)
		И Не ПустаяСтрока(НавигационнаяСсылкаФорматированнойСтроки) Тогда
		ПоказатьПредупреждение( , Элементы.ДекорацияСостояние.Подсказка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодключения

&НаКлиенте
Процедура ПодключенияПриИзменении(Элемент)
	
	Если Подключения.Количество() = 0 Тогда
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Подключить';
													|en = 'Connect'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключенияПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Значение = Неопределено Тогда
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Подключить';
													|en = 'Connect'");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Значение.ДатаПодключенияСИ) Тогда
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Переподключить';
													|en = 'Reconnect'");
	Иначе
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Подключить';
													|en = 'Connect'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключенияПередУдалением(Элемент, Отказ)
	
	ОчиститьСообщения();
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Значение = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не Элемент.ТекущиеДанные.Значение.ЭтоНовоеПодключение Тогда
		УдалитьПодключениеПродолжение(Истина);
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключенияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ПодключенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элемент.ТекущийЭлемент <> Элементы.ПодключенияКартинка Тогда
		Возврат;
	КонецЕсли;
	
	Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Дата подключения: %1';
																		|en = 'Connection date: %1'"),
		Элементы.Подключения.ТекущиеДанные.Значение.ДатаПодключенияСИ);
	ПоказатьПредупреждение( , Текст);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключить(Команда)
	
	ОчиститьСообщения();
	УстановитьСоединение();
	
КонецПроцедуры

&НаКлиенте
Процедура НовоеПодключение(Команда)
	
	ОчиститьСообщения();
	ДобавитьПодключение(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьПодключение(Команда)
	
	ОчиститьСообщения();
	УдалитьПодключениеПродолжение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперации

#Область ИнициализацияДлительныхОпераций

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОпераций(ПараметрыОперации)
	
	ИнтернетПоддержкаПодключена = Ложь;
	ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации);
	
	Если ИнтернетПоддержкаПодключена Тогда
		НачатьВыполнениеДлительныхОперацийПродолжение(Истина, ПараметрыОперации);
	Иначе
		Оповещение = Новый ОписаниеОповещения("НачатьВыполнениеДлительныхОперацийПродолжение", ЭтотОбъект, ПараметрыОперации);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьВыполнениеДлительныхОперацийПродолжение(Результат, ПараметрыОперации) Экспорт
	
	ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	
	Если Результат = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствует подключение к Интернет-поддержке пользователей.';
								|en = 'No connection to Online user support.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	ИначеЕсли ТипЗнч(Результат) = Тип("Структура") Тогда
		// Повторный вызов метода после подключения к Интернет-поддержке.
		ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
		ФоновоеЗадание = ПараметрыОперации.ФоновоеЗадание;
	КонецЕсли;
	
	Если ФоновоеЗадание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ФоновоеЗадание.Статус = "Выполняется" Тогда
		ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации);
	ИначеЕсли ФоновоеЗадание.Статус = "Выполнено" Тогда
		ОбработатьРезультатДлительныхОпераций(ФоновоеЗадание, ПараметрыОперации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжидатьЗавершениеДлительныхОпераций(ПараметрыОперации)
	
	Если Не ПараметрыОперации.ВыводитьОкноОжидания Тогда
		
		ТолькоПросмотр = Истина;
		Элементы.Подключения.ТолькоПросмотр = Истина;
		УстановитьОформлениеСостояния(Элементы);
		
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ОжидатьЗавершениеДлительныхОпераций(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаСервере
Процедура ИнициироватьДлительныеОперации()
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	Если ЗначениеЗаполнено(ДатаПодключенияСИ) Тогда
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения());
	Иначе
		ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение());
	КонецЕсли;
	
	ВыполнитьДлительныеОперации(Ложь, ПараметрыОперации);
	Кэш.ПараметрыМенеджераДлительныхОпераций = ПараметрыОперации;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДлительныеОперации(ИнтернетПоддержкаПодключена, ПараметрыОперации)
	
	Если Не ИнтеграцияСМаркетплейсамиСИ.ВозможенЗапускФоновогоЗадания(ЭтотОбъект, ПараметрыОперации, ИнтернетПоддержкаПодключена) Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Ложь;
	ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИ.ВыполнитьДлительныеОперации(ЭтотОбъект, ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура УстановитьСоединение(ИПППослеПодключения = Ложь)
	
	Модифицированность = Ложь;
	
	Если ПустаяСтрока(Логин) Тогда
		ОписаниеОЗавершении = Новый ОписаниеОповещения("ИПППослеПодключения", ЭтотОбъект);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(ОписаниеОЗавершении, ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если ИПППослеПодключения Тогда
		
		ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
		ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
		Если ЗначениеЗаполнено(ДатаПодключенияСИ) Тогда
			ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения());
		Иначе
			ПараметрыОперации.Очередь.Добавить( , ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение());
		КонецЕсли;
		
		НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
		
		Возврат;
		
	КонецЕсли;
	
	ТекущееПодключение = Элементы.Подключения.ТекущиеДанные;
	
	Если ТекущееПодключение = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Подключение не определено. Добавьте новое.';
														|en = 'The connection is not defined. Add a new one.'"), , "Подключения");
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ТекущееПодключение.Представление) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Необходимо заполнить наименование подключения.';
				|en = 'Fill in the connection name.'"), , "Подключения");
		Возврат;
	КонецЕсли;
	
	Если ТекущееПодключение.Значение.ИдентификаторИнформационнойБазы <> Кэш.ИдентификаторИнформационнойБазы Тогда
		
		Текст = НСтр("ru = 'Другие информационные базы утратят доступ к подключению ""%1"". Продолжить?';
					|en = 'Other infobases will lose access to the ""%1"" connection. Continue?'");
		
		Ответ = Ждать ВопросАсинх(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущееПодключение.Значение.Представление),
			РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		
		Если Ответ <> КодВозвратаДиалога.Да Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	ТекущееПодключение.Значение.Представление = ТекущееПодключение.Представление;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить(ТекущееПодключение.Значение, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура УдалитьПодключениеПродолжение(СтандартнаяОбработка = Ложь)
	
	Модифицированность = Ложь;
	
	ТекущееПодключение = Элементы.Подключения.ТекущиеДанные;
	
	Если ТекущееПодключение = Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Подключение не определено.';
														|en = 'The connection is not defined.'"), , "Подключения");
		Возврат;
	ИначеЕсли ТекущееПодключение.Значение.ЭтоНовоеПодключение Тогда
		Если Не СтандартнаяОбработка Тогда
			ИскомыйЭлемент = Подключения.НайтиПоЗначению(Элементы.Подключения.ТекущиеДанные.Значение);
			Если ИскомыйЭлемент <> Неопределено Тогда
				Подключения.Удалить(ИскомыйЭлемент);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Логин) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Выполните подключение к сервису Интернет-поддержки.';
														|en = 'Connect to the online support service.'"));
		Возврат;
	КонецЕсли;
	
	Если ТекущееПодключение.Значение.ИдентификаторИнформационнойБазы <> Кэш.ИдентификаторИнформационнойБазы Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Невозможно удалить подключение, принадлежащее другой информационной базе.';
										|en = 'Cannot delete a connection that belongs to another infobase.'"));
		Возврат;
	КонецЕсли;
	
	Текст = НСтр("ru = 'Данные по подключению ""%1"" станут недоступны. Продолжить?';
				|en = 'The ""%1"" connection data will become unavailable. Continue?'");
	
	Ответ = Ждать ВопросАсинх(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Текст, ТекущееПодключение.Значение.Представление),
		РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Нет);
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.Очередь.Добавить(ТекущееПодключение.Значение, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьПодключение());
	
	НачатьВыполнениеДлительныхОпераций(ПараметрыОперации);
	
КонецПроцедуры

#КонецОбласти

#Область ПараметрыДлительныхОпераций

&НаСервере
Процедура ЗаполнитьПараметрыДлительныхОпераций(ПараметрыОперации, Отказ)
	
	Если ПустаяСтрока(Логин) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		Если Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения() Тогда
			ПараметрыПолучитьПодключения(Операция.Значение, ПараметрыОперации, Отказ)
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение() Тогда
			ПараметрыУстановитьСоединение(Операция.Значение, ПараметрыОперации, Отказ);
		ИначеЕсли Операция.Представление = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьПодключение() Тогда
			ПараметрыУдалитьПодключение(Операция.Значение, ПараметрыОперации, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыУстановитьСоединение(Значение, ПараметрыОперации, Отказ)
	
	Если Значение = Неопределено Тогда
		ДобавитьПодключение(ЭтотОбъект);
		Значение = Подключения[0].Значение;
	КонецЕсли;
	
	ТекущееПодключение = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыеПараметрыУстановитьСоединение();
	ЗаполнитьЗначенияСвойств(Значение, ТекущееПодключение);
	Значение.Логин = Логин;
	Значение.ИдентификаторИнформационнойБазы = Кэш.ИдентификаторИнформационнойБазы;
	Значение.ВерсияКонфигурации = ИнтеграцияСМаркетплейсамиСИ.ВерсияКонфигурацииДляСИ();
	
КонецПроцедуры

&НаСервере
Процедура ПараметрыПолучитьПодключения(Значение, ПараметрыОперации, Отказ)
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыеПараметрыПолучитьПодключения();
КонецПроцедуры

&НаСервере
Процедура ПараметрыУдалитьПодключение(Значение, ПараметрыОперации, Отказ)
	
	ТекущееПодключение = Значение;
	
	Значение = ИнтеграцияСМаркетплейсамиСИ.НовыеПараметрыУдалитьПодключение();
	Значение.Вставить("Логин", Логин);
	Значение.Вставить("КлючПодключения", ТекущееПодключение.КлючПодключения);
	Значение.Вставить("ЭтоТекущийКлючПодключения", ТекущееПодключение.КлючПодключения = КлючПодключения);
	
КонецПроцедуры

#КонецОбласти

#Область РезультатыДлительныхОпераций

&НаКлиенте
Процедура ОбработатьРезультатДлительныхОпераций(Результат, ДополнительныеПараметры)
	
	Отказ = ТипЗнч(Результат) <> Тип("Структура");
	
	Если Отказ Или Результат.Статус <> "Выполнено" Тогда
		ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Истина);
		Возврат;
	КонецЕсли;
	
	РезультатМенеджера = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	
	Очередь = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(РезультатМенеджера, "Очередь", Новый СписокЗначений);
	
	Для Каждого Операция Из Очередь Цикл
		
		ИмяМетода = Операция.Представление;
		РезультатОперации = Операция.Значение;
		РезультатОбработки = Истина;
		
		Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения() Тогда
			ОбработатьРезультатПолучитьПодключения(РезультатОперации, РезультатОбработки, Отказ);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьПодключение() Тогда
			ОбработатьРезультатУстановитьПодключение(РезультатОперации, РезультатОбработки, Отказ);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение() Тогда
			ОбработатьРезультатУстановитьСоединение(РезультатОперации, РезультатОбработки, Отказ);
		ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьПодключение() Тогда
			ОбработатьРезультатУдалитьПодключение(РезультатОперации, РезультатОбработки, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры, Отказ)
	
	ТолькоПросмотр = Ложь;
	Элементы.Подключения.ТолькоПросмотр = Ложь;
	УстановитьОформлениеОсновнойКнопки(ЭтотОбъект);
	
	Если Отказ Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь);
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСИКлиент.ПослеОбработкиРезультатаДлительныхОпераций(Результат, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатУстановитьПодключение(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	УстановитьОформлениеСостояния(Элементы, Истина, ДатаПодключенияСИ);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатОперации.Ответ);
	Оповестить("Маркетплейсы_ПодключениеУстановлено");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПолучитьПодключения(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
		Параметры.ЗакрытьПослеЗавершенияОперации = Ложь;
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекущееПодключение = Неопределено;
	
	Подключения.Очистить();
	Для Каждого Подключение Из РезультатОперации.Ответ Цикл
		Пометка = Подключение.Значение.КлючПодключения = КлючПодключения
			И Подключение.Значение.ИдентификаторИнформационнойБазы = Кэш.ИдентификаторИнформационнойБазы;
		Если Пометка Тогда
			ТекущееПодключение = Подключение.Значение;
		КонецЕсли;
		Подключения.Добавить(Подключение.Значение, Подключение.Значение.Представление, Пометка, БиблиотекаКартинок.ИнформацияБЭД);
	КонецЦикла;
	
	Если ТекущееПодключение = Неопределено Тогда
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
	Иначе
		ДатаПодключенияСИ = ТекущееПодключение.ДатаПодключенияСИ;
		УстановитьОформлениеСостояния(Элементы, Истина, ДатаПодключенияСИ);
	КонецЕсли;
	
	Если Подключения.Количество() = 0 Тогда
		ДобавитьПодключение(ЭтотОбъект);
	ИначеЕсли Подключения.Количество() = 1 И Параметры.ЗакрытьПослеЗавершенияОперации Тогда
		Закрыть(ДатаПодключенияСИ);
	КонецЕсли;
	
	Параметры.ЗакрытьПослеЗавершенияОперации = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатУстановитьСоединение(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Ложь, РезультатОперации.Ошибки);
		РезультатОбработки = Ложь;
		Возврат;
	КонецЕсли;
	
	УстановитьОформлениеСостояния(Элементы, Истина, ДатаПодключенияСИ);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатУдалитьПодключение(РезультатОперации, РезультатОбработки, Отказ)
	
	Если РезультатОперации.КодСостояния <> 200 Тогда
		УстановитьОформлениеСостояния(Элементы, Истина, ДатаПодключенияСИ);
		РезультатОбработки = Ложь;
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось удалить подключение.';
														|en = 'Cannot delete the connection.'"));
		Возврат;
	КонецЕсли;
	
	Для Каждого Подключение Из Подключения Цикл
		Если Подключение.Значение.КлючПодключения = РезультатОперации.Ответ.КлючПодключения Тогда
			Подключения.Удалить(Подключение);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если РезультатОперации.Ответ.ЭтоТекущийКлючПодключения Тогда
		УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
		КлючПодключения = "";
		ДатаПодключенияСИ = Дата(1, 1, 1);
		Оповестить("Маркетплейсы_ПодключениеУдалено");
	Иначе
		УстановитьОформлениеСостояния(Элементы, Истина, ДатаПодключенияСИ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура ИПППослеПодключения(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		
		Логин = Результат.Логин;
		Элементы.ДекорацияУчетнаяЗапись.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
			ЗаголовокНадписиПодключенияИнтернетПоддержки(Логин));
		ПрочитатьДанныеИзБезопасногоХранилища(Логин);
		УстановитьСоединение(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОформлениеЭлементов()
	
	Если Логин = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПользовательМоделиСервиса() Тогда
		Элементы.ДекорацияУчетнаяЗапись.Видимость = Ложь;
	КонецЕсли;
	
	ОтобразитьСостояниеПодключенияНаСервере();
	УстановитьОформлениеОсновнойКнопки(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОформлениеОсновнойКнопки(ТекущийОбъект)
	
	Элементы = ТекущийОбъект.Элементы;
	
	Если ЗначениеЗаполнено(ТекущийОбъект.ДатаПодключенияСИ) Тогда
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Переподключить';
													|en = 'Reconnect'");
	Иначе
		Элементы.ФормаПодключить.Заголовок = НСтр("ru = 'Подключить';
													|en = 'Connect'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОформлениеСостояния(Элементы, Результат = Неопределено, Информация = "")
	
	ЭлементСостояния = Элементы.ДекорацияСостояние; // ЭлементФормы
	ЭлементСостояния.Подсказка = "";
	Информация = ?(ТипЗнч(Информация) = Тип("Массив"), СтрСоединить(Информация, Символы.ПС), Информация);
	Текст1 = НСтр("ru = 'Текущее состояние';
					|en = 'Current state'");
	
	Если Результат = Ложь Тогда
		Шаблон = "%1: <span style=""color: ПоясняющийОшибкуТекст"">%2</span> <a href=""Ошибка"">(%3)</a>";
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.ПредупреждениеКрасноеБЭД16;
		Если ПустаяСтрока(Информация) Тогда
			Информация = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ВнутренняяОшибкаСервиса();
		КонецЕсли;
		ЭлементСостояния.Подсказка = Информация;
		Текст2 = НСтр("ru = 'Не подключено';
						|en = 'Not connected'");
		Текст3 = НСтр("ru = 'ошибка';
						|en = 'error'")
	ИначеЕсли Результат = Истина Тогда
		Текст2 = НСтр("ru = 'Подключено';
						|en = 'Connected'");
		Если ЗначениеЗаполнено(Информация) Тогда
			Шаблон = "%1: <span style=""color: ПоясняющийТекст"">%2 (%3)</span>";
			Текст3 = Информация;
		Иначе
			Шаблон = "%1: <span style=""color: ПоясняющийТекст"">%2</span>";
		КонецЕсли;
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.Успешно;
	ИначеЕсли Результат = Неопределено Тогда
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.ДлительнаяОперация16;
		ЭлементСостояния.Заголовок = НСтр("ru = 'Обработка данных...';
											|en = 'Processing data...'");
		Возврат;
	ИначеЕсли Результат = "ТребуетсяНастройкаАдминистратором" Тогда
		Шаблон = "<span style=""color: ПоясняющийОшибкуТекст"">%1</span>";
		Текст1 = НСтр("ru = 'Для работы с маркетплейсом требуется настроить подключение с сервисом интеграции. Обратитесь к администратору.';
						|en = 'To use the marketplace, set up an integration service connection. Contact the administrator.'");
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.ПредупреждениеКрасноеБЭД16;
	ИначеЕсли Результат = "ТребуетсяПодключение" Тогда
		Шаблон = "%1: <span style=""color: ГиперссылкаЦвет"">%2</span>";
		Текст2 = НСтр("ru = 'Требуется подключение';
						|en = 'Connection is required'");
		Элементы.ДекорацияКартинка.Картинка = БиблиотекаКартинок.Информация16;
	КонецЕсли;
	
	#Если Сервер Тогда
	ЭлементСостояния.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(Шаблон, Текст1, Текст2, Текст3);
	#Иначе
	ЭлементСостояния.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(Шаблон, Текст1, Текст2, Текст3);
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКэшированныеЗначения()
	
	Кэш = Новый Структура;
	Кэш.Вставить("ПараметрыМенеджераДлительныхОпераций");
	ЗаголовокСистемы = Константы.ЗаголовокСистемы.Получить();
	Если ЗначениеЗаполнено(ЗаголовокСистемы) Тогда
		Кэш.Вставить("ЗаголовокКлиентскогоПриложения",
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 / %2", 
			Константы.ЗаголовокСистемы.Получить(), Метаданные.КраткаяИнформация));
	Иначе
		Кэш.Вставить("ЗаголовокКлиентскогоПриложения", Метаданные.КраткаяИнформация);
	КонецЕсли;
	
	Кэш.Вставить("ИдентификаторИнформационнойБазы", ИнтеграцияСМаркетплейсамиСИ.ИдентификаторИнформационнойБазы());
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьСостояниеПодключенияНаСервере()
	
	Элементы.ДекорацияУчетнаяЗапись.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		ЗаголовокНадписиПодключенияИнтернетПоддержки(Логин));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗаголовокНадписиПодключенияИнтернетПоддержки(Знач Логин)
	
	Текст1 = НСтр("ru = 'Учетная запись';
					|en = 'Account'");
	Текст2 = ?(ПустаяСтрока(Логин), НСтр("ru = 'Подключиться к Интернет-поддержке';
										|en = 'Connect to online support'"), Логин);
	
	ЗаголовокПодключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		"%1: <a href=""action:openUsersSite"">%2</a>", Текст1, Текст2);
	
	Возврат ЗаголовокПодключения;
	
КонецФункции

&НаСервере
Процедура ПрочитатьДанныеИзБезопасногоХранилища(Знач Логин)
	
	ДанныеИнтеграции = ИнтеграцияСМаркетплейсамиСИ.ПрочитатьДанныеИзБезопасногоХранилища( , Логин);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеИнтеграции, , "Логин");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ДобавитьПодключение(ТекущийОбъект)
	
	ПараметрыПодключения = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыПодключения();
	ПараметрыПодключения.Представление = ТекущийОбъект.Кэш.ЗаголовокКлиентскогоПриложения;
	ПараметрыПодключения.ИдентификаторИнформационнойБазы = ТекущийОбъект.Кэш.ИдентификаторИнформационнойБазы;
	ПараметрыПодключения.КлючПодключения = Строка(Новый УникальныйИдентификатор);
	ПараметрыПодключения.ЭтоНовоеПодключение = Истина;
	
	ТекущийОбъект.Подключения.Вставить(0, ПараметрыПодключения, ПараметрыПодключения.Представление, , БиблиотекаКартинок.ИнформацияБЭД);
	ТекущийОбъект.Элементы.Подключения.ТекущаяСтрока = ТекущийОбъект.Подключения[0].ПолучитьИдентификатор();
	
	ТекущийОбъект.Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтключениеИнтернетПоддержки()
	
	Логин = "";
	Элементы.ДекорацияУчетнаяЗапись.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		ЗаголовокНадписиПодключенияИнтернетПоддержки(Логин));
	Подключения.Очистить();
	УстановитьОформлениеСостояния(Элементы, "ТребуетсяПодключение");
	
КонецПроцедуры

&НаКлиенте
Функция РезультатЗакрытия()
	
	РезультатЗакрытия = Неопределено;
	
	Если ЗначениеЗаполнено(ДатаПодключенияСИ) Тогда
		РезультатЗакрытия = Новый Структура;
		РезультатЗакрытия.Вставить("Источник", "ПодключениеКСервисуИнтеграции");
		РезультатЗакрытия.Вставить("ДатаПодключенияСИ", ДатаПодключенияСИ);
	КонецЕсли;
	
	Возврат РезультатЗакрытия;
	
КонецФункции

#КонецОбласти