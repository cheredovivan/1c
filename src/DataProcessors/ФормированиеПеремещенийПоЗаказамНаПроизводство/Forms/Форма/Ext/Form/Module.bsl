
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПеремещениеТоваров") Тогда
		
		ТекстСообщения = НСтр("ru = 'В системе не используются перемещения товаров, рабочее место недоступно.';
								|en = 'Goods transfers are not used in the system. The workplace is unavailable.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат;
	
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство") Тогда

		ТекстСообщения = НСтр("ru = 'В системе не используется динамическая структура заказов на производство, рабочее место недоступно.';
								|en = 'The system does not use a dynamic production order structure, the workplace is not available.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		Возврат;

	КонецЕсли;
	
	ДоступенВариантЗаказыНаПеремещение = Константы.КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления.Получить();
	
	ИнициализироватьКомпоновщикНастроек();
	
	ВосстановитьНастройки();
	
	СтруктураОтборов = Неопределено;
	
	Если Параметры.Свойство("СтруктураОтборов", СтруктураОтборов) Тогда
		
		Если СтруктураОтборов.Свойство("Номенклатура", ОтборПоНоменклатуре) Тогда
			ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "Номенклатура", ОтборПоНоменклатуре);
		КонецЕсли;
		
		Если СтруктураОтборов.Свойство("ЗаказНаПроизводство", ОтборПоЗаказу) Тогда
			ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "ЗаказНаПроизводство", ОтборПоЗаказу);
		КонецЕсли;
		
		Если СтруктураОтборов.Свойство("Спецификация", ОтборПоСпецификации) Тогда
			ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "Спецификация", ОтборПоСпецификации);
		КонецЕсли;
		
		СтруктураОтборовЗаполненаПоПереданнымПараметрам = Истина;
		
	КонецЕсли;
	
	УстановитьСтраницыПомощника(ЭтаФорма, "ШагОтбор");
	
	ЦветВыбранногоРежима = ЦветаСтиля.СерыйЦветТекста1;
	ЦветНеВыбранногоРежима = ЦветаСтиля.ГиперссылкаЦвет;
	
	СтруктураДоступа = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.СтруктураДоступа();
	ИспользуютсяЗаказыНаПеремещение = СтруктураДоступа.ЗаказНаПеремещениеДоступен;
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Объект.ДатаОтгрузки = ТекущаяДата;
	
	УстановитьЗаголовкиКолонокКорзиныПоСкладам();
	НастроитьЭлементыФормы();
	
	СобытияФорм.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВариантОформления) ИЛИ НЕ ДоступенВариантЗаказыНаПеремещение Тогда
		Объект.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров");
	КонецЕсли;
	
	Если ОтразитьОтборКомпоновкиВПростыхОтборах() Тогда
		
		// в данной конфигурации дополнительных действий не требуется
		
	Иначе
		
		ЗагрузитьНастройкиОтбораПоУмолчанию();
		
	КонецЕсли;
	
	ОбновитьНастройкиПоВариантуОформления(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	Если НавигационнаяСсылкаФорматированнойСтроки = "ЗаполнитьНоменклатурныйПлан" Тогда
		
		СтандартнаяОбработка = Ложь;
		ОграничениеВыборки   = Ложь;
		
		ЗаполнитьНоменклатурныйПланНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

#Область ШагОтбор

&НаКлиенте
Процедура ОтборПоСкладуПолучателюПриИзменении(Элемент)
	
	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "СкладПолучатель", ОтборПоСкладуПолучателю);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоСкладуОтправителюПриИзменении(Элемент)
	
	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "СкладОтправитель", ОтборПоСкладуОтправителю);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоНаправлениюДеятельностиПриИзменении(Элемент)

	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "НаправлениеДеятельности", ОтборПоНаправлениюДеятельности);

КонецПроцедуры

&НаКлиенте
Процедура ОтборПоНазначениюПриИзменении(Элемент)

	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "Назначение", ОтборПоНазначению);

КонецПроцедуры

&НаКлиенте
Процедура ОтборПоНоменклатуреПриИзменении(Элемент)

	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "Номенклатура", ОтборПоНоменклатуре);

КонецПроцедуры

&НаКлиенте
Процедура ОтборПоЗаказуПриИзменении(Элемент)
	
	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "ЗаказНаПроизводство", ОтборПоЗаказу);

КонецПроцедуры

&НаКлиенте
Процедура ОтборТолькоЗапущенныеПартииПриИзменении(Элемент)
	
	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(КомпоновщикНастроек, "ТолькоЗапущенныеПартии", ОтборТолькоЗапущенныеПартии,, Не ОтборТолькоЗапущенныеПартии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПотребностиДоДатыПриИзменении(Элемент)
	
	СохранитьНастройки = Истина;
	
	ОтразитьПростойОтборВОтборахКомпоновки(
		КомпоновщикНастроек, "ДатаОкончания", Объект.ПотребностиДоДаты, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантОформленияПриИзменении(Элемент)
	
	ОбновитьНастройкиПоВариантуОформления(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ШагПлан

&НаКлиенте
Процедура ПланВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ИмяПоля = СтрЗаменить(Поле.Имя, "План", "");
	
	Если СтрНайти("Номенклатура,Характеристика,Назначение,СкладПолучатель,СкладОтправитель,ЗаказНаПроизводство,Спецификация,Изделие", ИмяПоля) > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПоказатьЗначение(, Элемент.ТекущиеДанные[ИмяПоля]);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПланПриАктивизацииСтроки(Элемент)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.ФормированиеПеремещенийПоЗаказамНаПроизводство.Форма.Элемент.План.ПриАктивизацииСтроки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПланОтметкаПриИзменении(Элемент)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.ФормированиеПеремещенийПоЗаказамНаПроизводство.Форма.Элемент.План.ПриИзмененииКоличества");
	
	ТекущиеДанные = Элементы.План.ТекущиеДанные;
	
	Коэффициент = ?(ТекущиеДанные.Отметка, 1, -1);
	СтрокаИзменений = КорзинаПоСкладамИзменения.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаИзменений, ТекущиеДанные);
	СтрокаИзменений.Вес = Коэффициент * ТекущиеДанные.КПеремещению * ТекущиеДанные.ВесЕдиницы;
	СтрокаИзменений.Объем = Коэффициент * ТекущиеДанные.КПеремещению * ТекущиеДанные.ОбъемЕдиницы;
	СтрокаИзменений.ЕстьСтрокиСНулевымВесом = ТекущиеДанные.ВесЕдиницы = 0;
	СтрокаИзменений.ЕстьСтрокиСНулевымОбъемом = ТекущиеДанные.ОбъемЕдиницы = 0;
	СтрокаИзменений.ЧастотаСтрок = Коэффициент;
	ОбновитьКорзинуПоСкладам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПланКПеремещениюПриИзменении(Элемент)
	
	// &ЗамерПроизводительности
	ОценкаПроизводительностиКлиент.ЗамерВремени("Обработка.ФормированиеПеремещенийПоЗаказамНаПроизводство.Форма.Элемент.План.ПриИзмененииКоличества");
	
	ТекущиеДанные = Элементы.План.ТекущиеДанные;
	ОтметкаДоИзменения = ТекущиеДанные.Отметка;
	ТекущиеДанные.Отметка = ТекущиеДанные.КПеремещению > 0;
	ОбновлятьКорзину = Истина;
	
	Если Не ОтметкаДоИзменения И ТекущиеДанные.Отметка Тогда
		ТекущиеДанные.КПеремещениюДоИзменения = 0;
	ИначеЕсли Не ОтметкаДоИзменения И Не ТекущиеДанные.Отметка Тогда
		ОбновлятьКорзину = Ложь;
	КонецЕсли;
	
	Если ТекущиеДанные.КПеремещению <> ТекущиеДанные.КПеремещениюДоИзменения
	   И ОбновлятьКорзину Тогда
	
		Разность = ТекущиеДанные.КПеремещению - ТекущиеДанные.КПеремещениюДоИзменения;
		СтрокаИзменений = КорзинаПоСкладамИзменения.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаИзменений, ТекущиеДанные);
		СтрокаИзменений.Вес = Разность * ТекущиеДанные.ВесЕдиницы;
		СтрокаИзменений.Объем = Разность * ТекущиеДанные.ОбъемЕдиницы;
		СтрокаИзменений.ЕстьСтрокиСНулевымВесом = ТекущиеДанные.ВесЕдиницы = 0;
		СтрокаИзменений.ЕстьСтрокиСНулевымОбъемом = ТекущиеДанные.ОбъемЕдиницы = 0;
		
		Если Не ТекущиеДанные.Отметка Тогда
			СтрокаИзменений.ЧастотаСтрок = -1;
		ИначеЕсли ТекущиеДанные.КПеремещению > 0
			    И Не ОтметкаДоИзменения Тогда
			СтрокаИзменений.ЧастотаСтрок = 1;
		Иначе
			СтрокаИзменений.ЧастотаСтрок = 0;
		КонецЕсли;
		
		ТекущиеДанные.КПеремещениюДоИзменения = ТекущиеДанные.КПеремещению;
		ОбновитьКорзинуПоСкладам();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ШагРезультатФормирования

&НаКлиенте
Процедура СписокДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(Неопределено, Элементы.СписокДокументы.ТекущиеДанные.Ссылка);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область Отборы

&НаКлиенте
Процедура ВосстановитьНастройкиПоУмолчанию(Команда)
	
	ЗагрузитьНастройкиОтбораПоУмолчанию();
	
КонецПроцедуры

#КонецОбласти

#Область НоменклатурныйПлан

&НаКлиенте
Процедура УстановитьФлажкиНоменклатурныйПлан(Команда)
	
	ИзменитьФлажкиВВыделенныхСтроках(Истина, Элементы.План.ВыделенныеСтроки, "План");
	ОбновитьКорзинуПоСкладам();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажкиНоменклатурныйПлан(Команда)
	
	ИзменитьФлажкиВВыделенныхСтроках(Ложь, Элементы.План.ВыделенныеСтроки, "План");
	ОбновитьКорзинуПоСкладам();
	
КонецПроцедуры

&НаКлиенте
Процедура ТолькоОтмеченныеПриИзменении(Элемент)
	
	УстановитьДополнительныйОтборСтрокНоменклатурныйПлан(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаВсеИзделияНажатие(Элемент)
	
	ФильтрОбеспечениеТекущееЗначение = ФильтрОбеспечениеОтключен();
	
	УстановитьДополнительныйОтборСтрокНоменклатурныйПлан(ЭтаФорма);
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ФильтрОбеспечение");
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаОбеспеченоНажатие(Элемент)
	
	ФильтрОбеспечениеТекущееЗначение = ФильтрОбеспечениеОбеспечено();
	
	УстановитьДополнительныйОтборСтрокНоменклатурныйПлан(ЭтаФорма);
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма, "ФильтрОбеспечение");
	
КонецПроцедуры

&НаКлиенте
Процедура ПеречитатьНоменклатурныйПлан(Команда)
	
	ПеречитатьНоменклатурныйПланНаСервере();
	ОбновитьКорзинуПоСкладам();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СозданныеДокументы

&НаКлиенте
Процедура ОтменаПроведения(Команда)
	
	ЗаписатьНаКлиенте(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Провести(Команда)
	
	ЗаписатьНаКлиенте(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдаление(Команда)
	
	ЗаписатьНаКлиенте(Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусПередачМатериаловОтгружено(Команда)
	
	УстановитьСтатус(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусПередачМатериаловПринято(Команда)
	
	УстановитьСтатус(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусПеремещенийТоваровОтгружено(Команда)
	
	УстановитьСтатус(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусПеремещенийТоваровПринято(Команда)
	
	УстановитьСтатус(Команда.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область Шаги

&НаКлиенте
Процедура Далее(Команда)
	
	ОчиститьСообщения();
	
	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагОтбор Тогда
		
		Отказ = Ложь;
		
		Если Объект.ДатаОтгрузки < ТекущаяДата
		   И Объект.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение") Тогда
		
			ТекстСообщения = НСтр("ru = 'Дата отгрузки не должна быть меньше текущей даты';
									|en = 'The shipment date cannot be earlier than the current date'");
			Поле = "Объект.ДатаОтгрузки";
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
		
		КонецЕсли;
		
		Если НЕ Отказ Тогда
		
			ПерейтиКШагуНоменклатурныйПланНаКлиенте();
		
		КонецЕсли;
		
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагНоменклатурныйПлан Тогда
	
		ПерейтиКШагуРезультатФормированияНаКлиенте();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)

	ОчиститьСообщения();

	Если Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагРезультатФормирования Тогда
		
		ПерейтиНазадКШагуНоменклатурныйПланНаКлиенте();
		
	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагНоменклатурныйПлан Тогда
		
		ПерейтиНазадКШагуОтборыНаКлиенте();

	ИначеЕсли Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагОтбор Тогда
	
		Возврат;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьСДругимиОтборами(Команда)
	
	ОчиститьСообщения();
	ПродолжитьСДругимиОтборамиНаСервере();

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Шаги

#Область Отборы

&НаКлиенте
Процедура ПерейтиНазадКШагуОтборыНаКлиенте()
	
	Объект.План.Очистить();
	УстановитьТекстГиперссылокОбеспечено();
	Итоги.Очистить();
	
	УстановитьСтраницыПомощника(ЭтаФорма, "ШагОтбор");
	
КонецПроцедуры

#КонецОбласти

#Область НоменклатурныйПлан

&НаКлиенте
Процедура ПерейтиКШагуНоменклатурныйПланНаКлиенте() 
	
	ОграничениеВыборки = Истина;
	КорзинаПоСкладам.Очистить();
	КорзинаПоСкладамИзменения.Очистить();
	
	ЗаполнитьНоменклатурныйПланНаСервере();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	Иначе
		
		УстановитьСтраницыПомощника(ЭтаФорма, "ШагНоменклатурныйПлан");
		
	КонецЕсли;
	
	ОбновитьКорзинуПоСкладам();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНазадКШагуНоменклатурныйПланНаКлиенте()
	
	Если СозданныеОбъекты.Количество() <> 0 Тогда
		
		Отказ = Ложь;
		УдалитьСозданныеОбъекты(Отказ);
		
		Если Не Отказ Тогда
			
			СозданныеОбъекты.Очистить();
			
			ПоказатьОповещениеПользователя(
				НСтр("ru = 'Удаление объектов';
					|en = 'Delete objects'"),
				,
				НСтр("ru = 'Сформированные объекты помечены на удаление.';
					|en = 'Generated objects are marked for deletion.'"));
			
			УстановитьСтраницыПомощника(ЭтаФорма, "ШагНоменклатурныйПлан");
			
		КонецЕсли;
		
	Иначе
		УстановитьСтраницыПомощника(ЭтаФорма, "ШагНоменклатурныйПлан");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатурныйПланНаСервере()
	
	Если СохранитьНастройки И НЕ СтруктураОтборовЗаполненаПоПереданнымПараметрам Тогда
		СохранитьНастройкиКомпоновщикаОтборов();
	КонецЕсли;
	
	ОтменитьДлительнуюОперацию();
	
	Если Объект.ВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение Тогда
		ДатаОтгрузки = Объект.ДатаОтгрузки;
	Иначе
		ДатаОтгрузки = Дата(1, 1, 1);
	КонецЕсли;
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("Настройки", КомпоновщикНастроек.ПолучитьНастройки());
	ПараметрыПроцедуры.Вставить("ДатаОтгрузки", ДатаОтгрузки);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование перемещений: заполнение номенклатурного плана';
															|en = 'Transfer generation: item plan filling'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.ПолучитьНоменклатурныйПланОтложенно",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		ОбработчикОжиданияДлительнойОперации = "ЗаполнитьНоменклатурныйПланОтложенноЗавершение";
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		ЗаполнитьНоменклатурныйПланОтложенно(ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНоменклатурныйПланОтложенноЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	Если Результат <> Неопределено Тогда
		
		ЗаполнитьНоменклатурныйПланОтложенно(Результат);
		ОбновитьКорзинуПоСкладам();
		УстановитьСтраницыПомощника(ЭтаФорма, "ШагНоменклатурныйПлан");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНоменклатурныйПланОтложенно(Результат)
	
	Если Результат.Статус = "Выполнено" Тогда
		
		ДанныеЗаполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Объект.План.Загрузить(ДанныеЗаполнения);
		ИнициализироватьИзмененияКорзиныПоСкладам(ДанныеЗаполнения);
		
		ФильтрОбеспечениеТекущееЗначение = ?(ДанныеЗаполнения.Найти(Истина, "ЕстьОбеспечение") <> Неопределено,
			ФильтрОбеспечениеОбеспечено(), ФильтрОбеспечениеОтключен());
		
		УстановитьТекстГиперссылокОбеспечено();
		УстановитьДополнительныйОтборСтрокНоменклатурныйПлан(ЭтотОбъект);
		НастроитьЗависимыеЭлементыФормы(ЭтотОбъект, "ФильтрОбеспечение");
		
	Иначе
		
		Если Результат.Статус = "Ошибка" Тогда
			
			ВызватьИсключение Результат.КраткоеПредставлениеОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПеречитатьНоменклатурныйПланНаСервере()
	
	Объект.План.Очистить();
	УстановитьТекстГиперссылокОбеспечено();
	Итоги.Очистить();
	КорзинаПоСкладам.Очистить();
	КорзинаПоСкладамИзменения.Очистить();
	
	ЗаполнитьНоменклатурныйПланНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьДополнительныйОтборСтрокНоменклатурныйПлан(Форма)
	
	СтруктураОтбора = Новый Структура();
	
	Если Форма.ТолькоОтмеченные Тогда
		СтруктураОтбора.Вставить("Отметка", Истина);
	КонецЕсли;
	
	Если Не Форма.ФильтрОбеспечениеТекущееЗначение = ФильтрОбеспечениеОтключен() Тогда
		СтруктураОтбора.Вставить("ЕстьОбеспечение", Форма.ФильтрОбеспечениеТекущееЗначение = ФильтрОбеспечениеОбеспечено());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураОтбора) Тогда
		Форма.Элементы.План.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	Иначе
		Форма.Элементы.План.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция УстановитьТекстГиперссылокОбеспечено()
	
	КоличествоСтрок = Объект.План.Количество();
	
	Если КоличествоСтрок > 0 Тогда
		КоличествоОбеспечено   = Объект.План.НайтиСтроки(Новый Структура("ЕстьОбеспечение", Истина)).Количество();
		КоличествоНеОбеспечено = КоличествоСтрок - КоличествоОбеспечено;
	Иначе
		КоличествоОбеспечено   = 0;
		КоличествоНеОбеспечено = 0;
	КонецЕсли;
	
	Элементы.ГиперссылкаВсеИзделия.Заголовок   = НСтр("ru = 'Все позиции';
														|en = 'All items'");
	Элементы.ГиперссылкаОбеспечено.Заголовок   = СтрШаблон(НСтр("ru = 'Обеспечено (%1)';
																|en = 'Supplied (%1)'"), Формат(КоличествоОбеспечено, "ЧЦ=3; ЧН=; ЧГ="));

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ФильтрОбеспечениеОтключен()
	
	Возврат 0;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ФильтрОбеспечениеОбеспечено()
	
	Возврат 1;
	
КонецФункции

#КонецОбласти

#Область РезультатФормирования

&НаКлиенте
Процедура ПерейтиКШагуРезультатФормированияНаКлиенте()
	
	ОчиститьСообщения();
	СоздатьДокументыНаСервере();
	
	Если НачатьОжиданиеДлительнойОперации Тогда
		
		НачатьОжиданиеДлительнойОперации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыНаСервере()
	
	ОпцияАктивна = Константы.КонтролироватьЗапасыТоваровПодлежащихРезервированиюПоМереПоступления.Получить();
	
	Если Объект.ВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение
	   И НЕ ОпцияАктивна Тогда
		
		ТекстСообщения = НСтр("ru = 'Для формирования документов ""Заказ на перемещение"" должна быть включена опция ""Контролировать запасы товаров, подлежащих резервированию по мере поступления""';
								|en = 'To generate the ""Transfer order"" documents, the ""Control balance of goods to be reserved upon receipt"" option must be enabled'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
		
	КонецЕсли;
	
	ОтменитьДлительнуюОперацию();
	
	ПоляГруппировки = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.СоставКолонокДляСозданияДокументов();
	
	СтруктураОтбора = Новый Структура("Отметка", Истина);
	
	ВыгруженнаяТаблица = Объект.План.Выгрузить(СтруктураОтбора, ПоляГруппировки + ",КПеремещению, Обеспечено");
	ВыгруженнаяТаблица.Свернуть(ПоляГруппировки, "КПеремещению, Обеспечено");
	
	АдресРезультатаФормированияЗаказов = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("АдресРезультатаФормированияЗаказов", АдресРезультатаФормированияЗаказов);
	ПараметрыПроцедуры.Вставить("ВариантОформления", Объект.ВариантОформления);
	ПараметрыПроцедуры.Вставить("ДатаОтгрузки", Объект.ДатаОтгрузки);
	ПараметрыПроцедуры.Вставить("ВыгруженнаяТаблица", ВыгруженнаяТаблица);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Формирование перемещений: создание документов';
															|en = 'Transfer generation: document creation'");
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(
		"Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.СоздатьДокументыОтложенно",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Если ДлительнаяОперация.Статус = "Выполняется" Тогда
		
		ОбработчикОжиданияДлительнойОперации = "СоздатьДокументыОтложенноЗавершение";
		НачатьОжиданиеДлительнойОперации = Истина;
		
	Иначе
		
		СоздатьДокументыОтложенно(ДлительнаяОперация);
		
		НачатьОжиданиеДлительнойОперации = Ложь;
		ДлительнаяОперация = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыОтложенноЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперация = Неопределено;
	
	СоздатьДокументыОтложенно(Результат);
		
	Если Результат <> Неопределено И Результат.Статус = "Ошибка" Тогда
		ПоказатьПредупреждение(, Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьДокументыОтложенно(Результат)
	
	СтруктураРезультата = ПолучитьИзВременногоХранилища(АдресРезультатаФормированияЗаказов);
	
	Если ЗначениеЗаполнено(СтруктураРезультата) Тогда // Задание прервалось до того, как было что-то записано
		
		УстановитьСтраницыПомощника(ЭтаФорма, "ШагРезультатФормирования");
		
		СозданныеОбъекты.ЗагрузитьЗначения(СтруктураРезультата.Документы);
		
		УстановитьНастройкиСпискаСозданныхДокументов();
		
		ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(СписокДокументы, "Документы", СтруктураРезультата.Документы);
		
		Для Каждого Ошибка Из СтруктураРезультата.Сообщения Цикл
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Ошибка.Текст, Ошибка.КлючДанных, Ошибка.Поле, Ошибка.ПутьКДанным);
		КонецЦикла;
		
		Если Результат = Неопределено Или Результат.Статус <> "Выполнено" Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Заказы были сформированы не полностью';
									|en = 'Orders were not fully generated'");
			Сообщение.Сообщить();
		КонецЕсли
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьСозданныеОбъекты(Отказ)
	
	МассивДокументов = СозданныеОбъекты.ВыгрузитьЗначения();
	СписокОшибок = ОбщегоНазначенияУТ.УстановитьПометкуУдаленияДокументов(МассивДокументов);
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(СписокОшибок);

КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура ПродолжитьСДругимиОтборамиНаСервере()
	
	Объект.План.Очистить();
	УстановитьТекстГиперссылокОбеспечено();
	Итоги.Очистить();
	
	СозданныеОбъекты.Очистить();
	
	УстановитьСтраницыПомощника(ЭтаФорма, "ШагОтбор");
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область Отборы

&НаСервере
Процедура ИнициализироватьКомпоновщикНастроек()
	
	СхемаКомпоновкиДанных = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.ПолучитьМакет("НоменклатурныйПлан");
	
	АдресСхемыКомпоновкиДанных = ПоместитьВоВременноеХранилище(СхемаКомпоновкиДанных, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемыКомпоновкиДанных); 
	
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОтразитьПростойОтборВОтборахКомпоновки(
	КомпоновщикНастроек, ИмяПоля, Значение, ВидСравнения = Неопределено, ОтключитьИспользование = Ложь)
	
	Если ВидСравнения = Неопределено Тогда
		ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	
	// Отбор предопределенный в настройках СКД.
	ЗначениеПоиска = Новый ПолеКомпоновкиДанных(ИмяПоля);
	ЭлементНайден = Ложь;
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		Если ЭлементОтбора.ЛевоеЗначение = ЗначениеПоиска Тогда
			
			ЭлементПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(
													ЭлементОтбора.ИдентификаторПользовательскойНастройки);
					
			Если Не ЭлементПользовательскойНастройки = Неопределено Тогда
				
				ЭлементПользовательскойНастройки.Использование = (Значение <> Неопределено) И ЗначениеЗаполнено(Значение);
				Если ЭлементПользовательскойНастройки.Использование Тогда
					
					ЭлементПользовательскойНастройки.ВидСравнения   = ВидСравнения;
					ЭлементПользовательскойНастройки.ПравоеЗначение = Значение;
					
				КонецЕсли;
				
				Если ОтключитьИспользование Тогда
					ЭлементПользовательскойНастройки.Использование = Ложь;
				КонецЕсли;
				
				ЭлементНайден = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Отбор по подчиненным полям (поиск в добавляемых пользователем элементах отбора).
	Если Не ЭлементНайден Тогда
		
		ИдентификаторОтбора = КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки;
		Отбор = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторОтбора);
		ЭлементОтбора = Неопределено;
		Для каждого СуществующийЭлементОтбора Из Отбор.Элементы Цикл
			
			Если Строка(СуществующийЭлементОтбора.ЛевоеЗначение) = ИмяПоля Тогда
				ЭлементОтбора = СуществующийЭлементОтбора;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ЭлементОтбора = Неопределено Тогда
			ЭлементОтбора = Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		КонецЕсли;
		
		ЭлементОтбора.Использование  = ЗначениеЗаполнено(Значение);
		ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных(ИмяПоля);
		ЭлементОтбора.ПравоеЗначение = Значение;
		
		ЭлементОтбора.ВидСравнения   = ВидСравнения;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОтразитьОтборКомпоновкиВПростыхОтборах()
	
	// Поиск в добавляемых пользователем элементах отбора.
	
	ИдентификаторОтбора = КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки;
	Отбор = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдентификаторОтбора);

	Для каждого ЭлементОтбора Из Отбор.Элементы Цикл
		
		Если ЭлементОтбора.Использование Тогда 
			Возврат Ложь;
		КонецЕсли;
	
	КонецЦикла;
	
	ПростыеОтборы = "СкладПолучатель, СкладОтправитель, НаправлениеДеятельности, Назначение, Склад, Номенклатура, ЗаказНаПроизводство, ДатаОкончания, ТолькоЗапущенныеПартии, Спецификация";
	
	Для каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		ЭлементПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		
		Если ЭлементПользовательскойНастройки <> Неопределено
			 И ЭлементПользовательскойНастройки.Использование
			 И ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных")
			 И Не СтрНайти(ПростыеОтборы, ЭлементОтбора.ЛевоеЗначение) > 0 Тогда
			Возврат Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
	Для каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		
		ЭлементПользовательскойНастройки = КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ЭлементОтбора.ИдентификаторПользовательскойНастройки);
		Если Не ЭлементПользовательскойНастройки = Неопределено Тогда
			
			Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				
				ЗаполнитьПростойОтбор(ОтборПоСкладуПолучателю,
					"СкладПолучатель", ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
				
				ЗаполнитьПростойОтбор(ОтборПоСкладуОтправителю,
					"СкладОтправитель", ПредопределенноеЗначение("Справочник.Склады.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
				
				ЗаполнитьПростойОтбор(ОтборПоНаправлениюДеятельности,
					"НаправлениеДеятельности", ПредопределенноеЗначение("Справочник.НаправленияДеятельности.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
				
				ЗаполнитьПростойОтбор(ОтборПоНазначению,
					"Назначение", ПредопределенноеЗначение("Справочник.Назначения.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
				
				ЗаполнитьПростойОтбор(ОтборПоНоменклатуре,
					"Номенклатура", ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
				
				ЗаполнитьПростойОтбор(ОтборПоЗаказу,
					"ЗаказНаПроизводство", ПредопределенноеЗначение("Документ.ЗаказНаПроизводство2_2.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
					
				ЗаполнитьПростойОтбор(Объект.ПотребностиДоДаты,
					"ДатаОкончания", Дата(1, 1, 1),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
					
				ЗаполнитьПростойОтбор(ОтборТолькоЗапущенныеПартии,
					"ТолькоЗапущенныеПартии", Ложь,
					ЭлементОтбора, ЭлементПользовательскойНастройки);
					
				ЗаполнитьПростойОтбор(ОтборПоСпецификации,
					"Спецификация", ПредопределенноеЗначение("Справочник.РесурсныеСпецификации.ПустаяСсылка"),
					ЭлементОтбора, ЭлементПользовательскойНастройки);
					
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПростойОтбор(РеквизитФормы, ИмяПоля, ЗначениеПоУмолчанию, ЭлементОтбора, ЭлементПользовательскойНастройки)
	
	ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
	Если ПолеКомпоновки = ЭлементОтбора.ЛевоеЗначение Тогда
		
		РеквизитФормы = ?(ЭлементПользовательскойНастройки.Использование,
			ЭлементПользовательскойНастройки.ПравоеЗначение, ЗначениеПоУмолчанию);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ВосстановитьНастройки()

	ЗначениеНастроек = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("Обработка.ФормированиеПеремещенийПоЗаказамНаПроизводство", "КомпоновщикОтборов");

	Если ТипЗнч(ЗначениеНастроек) = Тип("Структура") Тогда
		
		КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ЗначениеНастроек.ПользовательскиеНастройки);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.ВариантОформления) ИЛИ НЕ ДоступенВариантЗаказыНаПеремещение Тогда
		Объект.ВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПользовательскиеНастройкиПоУмолчанию()
	
	СхемаКомпоновкиДанных = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.ПолучитьМакет("НоменклатурныйПлан");
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных);
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	
	КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	Возврат КомпоновщикНастроек.ПользовательскиеНастройки;

КонецФункции

&НаСервере
Процедура ЗагрузитьНастройкиОтбораПоУмолчанию()
	
	КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройкиПоУмолчанию());
	КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСтраницыПомощника(Форма, ИмяШага)
	
	Элементы = Форма.Элементы;
	
	Если ИмяШага = "ШагОтбор" Тогда

		Элементы.НадписьШаг.Заголовок = НСтр("ru = 'Шаг 1 из 3. Отбор';
											|en = 'Step 1 of 3. Filter'");
		
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		
		Элементы.Далее.Видимость = Истина;
		Элементы.Назад.Видимость = Ложь;
		
		Элементы.ПродолжитьСДругимиОтборами.Видимость = Ложь;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагОтбор;
	
	ИначеЕсли ИмяШага = "ШагНоменклатурныйПлан" Тогда

		Элементы.НадписьШаг.Заголовок = НСтр("ru = 'Шаг 2 из 3. Определение состава и объема перемещений';
											|en = 'Step 2 of 3. Decide what to transfer and how much'");
		
		Элементы.Далее.КнопкаПоУмолчанию = Истина;
		
		Элементы.Далее.Видимость = Истина;
		Элементы.Назад.Видимость = Истина;
		
		Элементы.ПродолжитьСДругимиОтборами.Видимость = Ложь;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагНоменклатурныйПлан;

	ИначеЕсли ИмяШага = "ШагРезультатФормирования" Тогда

		Элементы.НадписьШаг.Заголовок = НСтр("ru = 'Шаг 3 из 3. Уточнение сформированных документов';
											|en = 'Step 3 of 3. Specify generated documents'");
		
		Элементы.Закрыть.КнопкаПоУмолчанию = Истина;
		
		Элементы.Далее.Видимость = Ложь;
		Элементы.Назад.Видимость = Истина;
		
		Элементы.ПродолжитьСДругимиОтборами.Видимость = Истина;
		
		Элементы.СтраницыПомощника.ТекущаяСтраница = Элементы.ШагРезультатФормирования;
	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОтменитьДлительнуюОперацию()
	
	Если НЕ ДлительнаяОперация = Неопределено Тогда
		ДлительныеОперации.ОтменитьВыполнениеЗадания(ДлительнаяОперация.ИдентификаторЗадания);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОжиданиеДлительнойОперации(ВыводитьОкноОжидания = Истина)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(ОбработчикОжиданияДлительнойОперации, ЭтотОбъект);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	
	ПараметрыОжидания.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
			ДлительнаяОперация,
			ОповещениеОЗавершении,
			ПараметрыОжидания);
	
	НачатьОжиданиеДлительнойОперации = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиКомпоновщикаОтборов()
	
	Настройки = Новый Структура();
	Настройки.Вставить("ПользовательскиеНастройки", КомпоновщикНастроек.ПользовательскиеНастройки);
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("Обработка.ФормированиеПеремещенийПоЗаказамНаПроизводство", "КомпоновщикОтборов", Настройки);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыФормы()
	
	// Настройка формы по правам доступа.
	СтруктураДоступа = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.СтруктураДоступа();
	
	Если Не СтруктураДоступа.ЗаказНаПеремещениеДоступен И Не СтруктураДоступа.ЗаказМатериаловДоступен Тогда
		ИскомыйЭлемент = Элементы.ВариантОформления.СписокВыбора.НайтиПоЗначению(
			Перечисления.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение);
		Элементы.ВариантОформления.СписокВыбора.Удалить(ИскомыйЭлемент);
	КонецЕсли;
	
	Если Не СтруктураДоступа.ПеремещениеДоступно И Не СтруктураДоступа.ПередачаМатериаловДоступна Тогда
		ИскомыйЭлемент = Элементы.ВариантОформления.СписокВыбора.НайтиПоЗначению(
			Перечисления.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров);
		Элементы.ВариантОформления.СписокВыбора.Удалить(ИскомыйЭлемент);
	КонецЕсли;
	
	Элементы.ВариантОформления.Видимость = СтруктураДоступа.ЗаказНаПеремещениеДоступен;
	Элементы.ВариантОформления.Доступность = ДоступенВариантЗаказыНаПеремещение;
	
	Если НЕ ДоступенВариантЗаказыНаПеремещение Тогда
		
		Элементы.ВариантОформления.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
		ТекстПодсказки = НСтр("ru = 'Для выбора варианта оформления ""Заказ на перемещение"" должна быть включена опция ""Контролировать запасы товаров, подлежащих резервированию по мере поступления""';
								|en = 'To select the ""Transfer order"" option, the ""Control balance of goods to be reserved upon receipt"" option must be enabled'");
		Элементы.ВариантОформления.Подсказка = ТекстПодсказки;
		
	КонецЕсли;
	
	НастроитьЗависимыеЭлементыФормы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура НастроитьЗависимыеЭлементыФормы(Форма, СписокРеквизитов = "")
	
	Элементы = Форма.Элементы;
	
	Инициализация = ПустаяСтрока(СписокРеквизитов);
	СтруктураРеквизитов = Новый Структура(СписокРеквизитов);

	Если СтруктураРеквизитов.Свойство("ФильтрОбеспечение")
		ИЛИ Инициализация Тогда
		
		Форма.Элементы.ГиперссылкаВсеИзделия.ЦветТекста = ?(Форма.ФильтрОбеспечениеТекущееЗначение <> ФильтрОбеспечениеОтключен(),
			Форма.ЦветНеВыбранногоРежима, Форма.ЦветВыбранногоРежима);
		
		Форма.Элементы.ГиперссылкаОбеспечено.ЦветТекста = ?(Форма.ФильтрОбеспечениеТекущееЗначение <> ФильтрОбеспечениеОбеспечено(),
			Форма.ЦветНеВыбранногоРежима, Форма.ЦветВыбранногоРежима);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФлажкиВВыделенныхСтроках(Знач Значение, Знач ИдентификаторыСтрок, ИмяТаблицы = "")
	
	ТаблицаИзменений = Новый ТаблицаЗначений;
	ТаблицаИзменений.Колонки.Добавить("СкладОтправитель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаИзменений.Колонки.Добавить("СкладПолучатель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаИзменений.Колонки.Добавить("Вес", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Любой));
	ТаблицаИзменений.Колонки.Добавить("Объем", ОбщегоНазначения.ОписаниеТипаЧисло(15, 3, ДопустимыйЗнак.Любой));
	ТаблицаИзменений.Колонки.Добавить("ЧастотаСтрок", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	
	ТаблицаСтрокСНулевымВесом = Новый ТаблицаЗначений;
	ТаблицаСтрокСНулевымВесом.Колонки.Добавить("СкладОтправитель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаСтрокСНулевымВесом.Колонки.Добавить("СкладПолучатель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	ТаблицаСтрокСНулевымОбъемом = Новый ТаблицаЗначений;
	ТаблицаСтрокСНулевымОбъемом.Колонки.Добавить("СкладОтправитель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаСтрокСНулевымОбъемом.Колонки.Добавить("СкладПолучатель", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Коэффициент = ?(Значение, 1, -1);
	
	Для каждого ИдентификаторСтроки Из ИдентификаторыСтрок Цикл
		
		СтрокаТаблицы = Объект[ИмяТаблицы].НайтиПоИдентификатору(ИдентификаторСтроки);
		
		ЗначениеДоИзменения = СтрокаТаблицы.Отметка;
		СтрокаТаблицы.Отметка = Значение;
		
		Если ЗначениеДоИзменения <> Значение Тогда
		
			СтрокаКорзиныИзменения = ТаблицаИзменений.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКорзиныИзменения, СтрокаТаблицы);
			СтрокаКорзиныИзменения.Вес = Коэффициент * СтрокаТаблицы.КПеремещению * СтрокаТаблицы.ВесЕдиницы;
			СтрокаКорзиныИзменения.Объем = Коэффициент * СтрокаТаблицы.КПеремещению * СтрокаТаблицы.ОбъемЕдиницы;
			СтрокаКорзиныИзменения.ЧастотаСтрок = Коэффициент;
			
			Если СтрокаТаблицы.ВесЕдиницы = 0 Тогда
				СтрокаТаблицыВес = ТаблицаСтрокСНулевымВесом.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыВес, СтрокаКорзиныИзменения);
			КонецЕсли;
			
			Если СтрокаТаблицы.ОбъемЕдиницы = 0 Тогда
				СтрокаТаблицыОбъем = ТаблицаСтрокСНулевымОбъемом.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицыОбъем, СтрокаКорзиныИзменения);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаИзменений.Свернуть("СкладОтправитель, СкладПолучатель", "Вес, Объем, ЧастотаСтрок");
	КорзинаПоСкладамИзменения.Загрузить(ТаблицаИзменений);
	
	ПоискПоСкладам = Новый Структура("СкладОтправитель, СкладПолучатель");
	ТаблицаСтрокСНулевымВесом.Свернуть("СкладОтправитель, СкладПолучатель");
	ТаблицаСтрокСНулевымОбъемом.Свернуть("СкладОтправитель, СкладПолучатель");
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтрокСНулевымВесом Цикл
	
		ЗаполнитьЗначенияСвойств(ПоискПоСкладам, СтрокаТаблицы);
		МассивСтрокТЧ = КорзинаПоСкладамИзменения.НайтиСтроки(ПоискПоСкладам);
		Для Каждого СтрокаТЧ Из МассивСтрокТЧ Цикл
			СтрокаТЧ.ЕстьСтрокиСНулевымВесом = Истина;
		КонецЦикла;
	
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтрокСНулевымОбъемом Цикл
	
		ЗаполнитьЗначенияСвойств(ПоискПоСкладам, СтрокаТаблицы);
		МассивСтрокТЧ = КорзинаПоСкладамИзменения.НайтиСтроки(ПоискПоСкладам);
		Для Каждого СтрокаТЧ Из МассивСтрокТЧ Цикл
			СтрокаТЧ.ЕстьСтрокиСНулевымОбъемом = Истина;
		КонецЦикла;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьНаКлиенте(Команда)
	
	ОчиститьСообщения();
	МассивСсылок = Новый Массив();
	
	Для Каждого Строка Из Элементы.СписокДокументы.ВыделенныеСтроки Цикл
		
		СсылкаНаДокумент = Элементы.СписокДокументы.ДанныеСтроки(Строка).Ссылка;
		МассивСсылок.Добавить(СсылкаНаДокумент);
		
	КонецЦикла;
	ЗаписатьНаСервере(МассивСсылок, Команда.Имя);
	Элементы.СписокДокументы.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере(МассивСсылок, Действие)
	
	ШаблонОшибкиЗаблокировать     = НСтр("ru = 'Не удалось заблокировать %Документ%. %ОписаниеОшибки%';
										|en = 'Failed to lock %Документ%. %ОписаниеОшибки%'");
	ШаблонОшибкиЗаписать          = НСтр("ru = 'Не удалось записать %Документ%. %ОписаниеОшибки%';
										|en = 'Failed to save %Документ%. %ОписаниеОшибки%'");
	ШаблонОшибкиПровести          = НСтр("ru = 'Проведение не выполнено %1 %2 от %3';
										|en = 'Cannot post %1 %2 dated %3'");
	
	Для Каждого Ссылка Из МассивСсылок Цикл
		
		// Захват объекта для редактирования
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Ссылка);
		Исключение
			ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаблокировать, "%Документ%", Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка);
			Продолжить;
		КонецПопытки;
		
		// Получение объекта документа
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		
		ЗаполнениеКорректно = Истина;
		
		Если Действие = "Провести" Тогда
			
			ЗаполнениеКорректно = ДокументОбъект.ПроверитьЗаполнение();
			
			Если НЕ ЗаполнениеКорректно Тогда
				
				Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибкиПровести,
					ДокументОбъект.Метаданные().Синоним,
					ДокументОбъект.Номер,
					ДокументОбъект.Дата);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Текст, ДокументОбъект.Ссылка);
			
			КонецЕсли;
			
		КонецЕсли;
		
		Если ЗаполнениеКорректно Тогда
		
			// Запись документа
			Попытка
				
				Если Действие = "Провести" Тогда
					ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
				ИначеЕсли Действие = "ОтменаПроведения" Тогда
					ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				ИначеЕсли Действие = "ПометитьНаУдаление" Тогда
					ДокументОбъект.УстановитьПометкуУдаления(Не ДокументОбъект.ПометкаУдаления);
				КонецЕсли;
				
			Исключение
				ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаписать, "%Документ%", Ссылка);
				ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка);
			КонецПопытки;
		
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатус(Команда)
	
	ТипДокумента = Неопределено;
	
	Если Команда = Команды.УстановитьСтатусПеремещенийТоваровОтгружено.Имя Тогда
		
		ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров");
		Статус = ПредопределенноеЗначение("Перечисление.СтатусыПеремещенийТоваров.Отгружено");
		
	ИначеЕсли Команда = Команды.УстановитьСтатусПеремещенийТоваровПринято.Имя Тогда
		
		ТипДокумента = Тип("ДокументСсылка.ПеремещениеТоваров");
		Статус = ПредопределенноеЗначение("Перечисление.СтатусыПеремещенийТоваров.Принято");
		
	ИначеЕсли Команда = Команды.УстановитьСтатусПередачМатериаловОтгружено.Имя Тогда
		
		ТипДокумента = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов");
		Статус = ПредопределенноеЗначение("Перечисление.СтатусыДвиженияПродукцииИМатериалов.Отгружено");
		
	ИначеЕсли Команда = Команды.УстановитьСтатусПередачМатериаловПринято.Имя Тогда
		
		ТипДокумента = Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов");
		Статус = ПредопределенноеЗначение("Перечисление.СтатусыДвиженияПродукцииИМатериалов.Принято");
		
	КонецЕсли;
	
	МассивСсылок = Новый Массив();
	
	Для Каждого Строка Из Элементы.СписокДокументы.ВыделенныеСтроки Цикл
		
		СсылкаНаДокумент = Элементы.СписокДокументы.ДанныеСтроки(Строка).Ссылка;
		Если ТипЗнч(СсылкаНаДокумент) = ТипДокумента Тогда
			
			МассивСсылок.Добавить(СсылкаНаДокумент);
			
		КонецЕсли;
		
	КонецЦикла;
	УстановитьСтатусНаСервере(МассивСсылок, Статус);
	Элементы.СписокДокументы.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусНаСервере(МассивСсылок, Статус)
	
	Статус = ОбщегоНазначения.МенеджерОбъектаПоСсылке(Статус).Индекс(Статус);
	ВсегоСсылок = МассивСсылок.Количество();
	
	Для Счетчик = 1 По ВсегоСсылок Цикл
		
		ТекущийЭлементМассива = МассивСсылок[ВсегоСсылок - Счетчик];
		Если СозданныеОбъекты.НайтиПоЗначению(ТекущийЭлементМассива) = Неопределено Тогда
		
			МассивСсылок.Удалить(ТекущийЭлементМассива);
			
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьСтатусПоМассивуСсылок(МассивСсылок, Статус);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтатусПоМассивуСсылок(МассивСсылок, Статус)
	
	ШаблонОшибкиЗаблокировать     = НСтр("ru = 'Не удалось заблокировать %Документ%. %ОписаниеОшибки%';
										|en = 'Failed to lock %Документ%. %ОписаниеОшибки%'");
	ШаблонОшибкиЗаписать          = НСтр("ru = 'Не удалось записать %Документ%. %ОписаниеОшибки%';
										|en = 'Failed to save %Документ%. %ОписаниеОшибки%'");

	Для Каждого Ссылка Из МассивСсылок Цикл
		
		// Захват объекта для редактирования
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Ссылка);
		Исключение
			ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаблокировать, "%Документ%", Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка);
			Продолжить;
		КонецПопытки;
		
		// Получение объекта документа
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		
		// Установка статуса документа
		Если Не ДокументОбъект.УстановитьСтатус(Статус, Неопределено) Тогда
			Продолжить;
		КонецЕсли;
			
		// Запись документа
		Попытка
			
			ДокументОбъект.Записать(?(ДокументОбъект.Проведен, РежимЗаписиДокумента.Проведение, РежимЗаписиДокумента.Запись));
			
		Исключение
			ТекстОшибки = СтрЗаменить(ШаблонОшибкиЗаписать, "%Документ%", Ссылка);
			ТекстОшибки = СтрЗаменить(ТекстОшибки, "%ОписаниеОшибки%", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, Ссылка);
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиСпискаСозданныхДокументов()
	
	УстановитьТекстЗапросаДинамическогоСпискаСозданныхДокументов();
	
	УстановитьВидимостьКомандСозданныхДокументов();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКомандСозданныхДокументов()
	
	МассивТипов = Новый Массив();
	
	Для Каждого Элемент Из СозданныеОбъекты Цикл
		
		ТипДокумента = ТипЗнч(Элемент.Значение);
		
		Если МассивТипов.Найти(ТипДокумента) = Неопределено Тогда
			МассивТипов.Добавить(ТипДокумента);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого Элемент Из Элементы.ГруппаУстановитьСтатус.ПодчиненныеЭлементы Цикл
		Элемент.Видимость = Ложь;
	КонецЦикла;
	
	ВсегоТипов = 0;
	ИменаДокументов = Новый Массив();
	
	ИменаДокументов.Добавить(НСтр("ru = 'Перемещение товаров:';
									|en = 'Goods transfer:'"));
	ВидимостьСтатусов = МассивТипов.Найти(Тип("ДокументСсылка.ПеремещениеТоваров")) <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыПеремещенийТоваров");
	ВсегоТипов = ВсегоТипов + ?(ВидимостьСтатусов, 1, 0);
	
	Элементы.УстановитьСтатусПеремещенийТоваровОтгружено.Видимость = ВидимостьСтатусов;
	Элементы.УстановитьСтатусПеремещенийТоваровПринято.Видимость   = ВидимостьСтатусов;
	
	ИменаДокументов.Добавить(НСтр("ru = 'Передача материалов:';
									|en = 'Material transfer:'"));
	ВидимостьСтатусов = МассивТипов.Найти(Тип("ДокументСсылка.ДвижениеПродукцииИМатериалов")) <> Неопределено
		И ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыДвиженийПродукцииИМатериалов");
	ВсегоТипов = ВсегоТипов + ?(ВидимостьСтатусов, 1, 0);
	
	Элементы.УстановитьСтатусПередачМатериаловОтгружено.Видимость = ВидимостьСтатусов;
	Элементы.УстановитьСтатусПередачМатериаловПринято.Видимость   = ВидимостьСтатусов;
	
	Для Каждого ЭлементФормы Из Элементы.ГруппаУстановитьСтатус.ПодчиненныеЭлементы Цикл
		
		КомандаФормы = Команды[ЭлементФормы.ИмяКоманды]; // КомандаФормы -
		ЗаголовокКоманды = КомандаФормы.Заголовок;
		
		Если ВсегоТипов = 1 Тогда
			Для Каждого ИмяДокумента Из ИменаДокументов Цикл
				ЗаголовокКоманды = СокрЛП(СтрЗаменить(ЗаголовокКоманды, ИмяДокумента, ""));
			КонецЦикла;
		КонецЕсли;
		
		ЭлементФормы.Заголовок = ЗаголовокКоманды;
		
	КонецЦикла;
	
	Элементы.СписокДокументыСтатус.Видимость = ВсегоТипов > 0;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстЗапросаДинамическогоСпискаСозданныхДокументов()
	
	СтруктураДоступа = Обработки.ФормированиеПеремещенийПоЗаказамНаПроизводство.СтруктураДоступа();
	ТекстЗапроса = "";
	
	ТекущийВариантОформления = Объект.ВариантОформления;
	
	Если ТекущийВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров
			И СтруктураДоступа.ПеремещениеДоступно Тогда
		
		ДобавитьСекциюОбъединения(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР КОГДА ПеремещениеТоваровПереопределяемый.Проведен ТОГДА
			|				1
			|			КОГДА ПеремещениеТоваровПереопределяемый.ПометкаУдаления ТОГДА
			|				2
			|			ИНАЧЕ
			|				0
			|		КОНЕЦ                                                  КАК КартинкаСтроки,
			|	ПеремещениеТоваровПереопределяемый.Ссылка                  КАК Ссылка,
			|	ПеремещениеТоваровПереопределяемый.ПометкаУдаления         КАК ПометкаУдаления,
			|	ПеремещениеТоваровПереопределяемый.Номер                   КАК Номер,
			|	ПеремещениеТоваровПереопределяемый.Дата                    КАК Дата,
			|	ПеремещениеТоваровПереопределяемый.Проведен                КАК Проведен,
			|	ПеремещениеТоваровПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ПеремещениеТоваровПереопределяемый.Организация             КАК Организация,
			|	ПеремещениеТоваровПереопределяемый.ОрганизацияПолучатель   КАК ОрганизацияПолучатель,
			|	ПеремещениеТоваровПереопределяемый.Ответственный           КАК Ответственный,
			|	ПеремещениеТоваровПереопределяемый.Подразделение           КАК Подразделение,
			|	ПеремещениеТоваровПереопределяемый.СкладОтправитель        КАК СкладОтправитель,
			|	ПеремещениеТоваровПереопределяемый.СкладПолучатель         КАК СкладПолучатель,
			|	ПеремещениеТоваровПереопределяемый.Статус                  КАК Статус
			|ИЗ
			|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваровПереопределяемый
			|ГДЕ
			|	ПеремещениеТоваровПереопределяемый.Ссылка В(&Документы)";
			
	КонецЕсли;
	
	Если ТекущийВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров
			И СтруктураДоступа.ПередачаМатериаловДоступна Тогда
		
		ДобавитьСекциюОбъединения(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР КОГДА ДвижениеПродукцииИМатериаловПереопределяемый.Проведен ТОГДА
			|				1
			|			КОГДА ДвижениеПродукцииИМатериаловПереопределяемый.ПометкаУдаления ТОГДА
			|				2
			|			ИНАЧЕ
			|				0
			|		КОНЕЦ                                                            КАК КартинкаСтроки,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Ссылка                  КАК Ссылка,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.ПометкаУдаления         КАК ПометкаУдаления,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Номер                   КАК Номер,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Дата                    КАК Дата,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Проведен                КАК Проведен,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Организация             КАК Организация,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Организация             КАК ОрганизацияПолучатель,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Ответственный           КАК Ответственный,
			|	ВЫРАЗИТЬ(ДвижениеПродукцииИМатериаловПереопределяемый.Получатель
			|		КАК Справочник.Склады).Подразделение                             КАК Подразделение,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Отправитель             КАК СкладОтправитель,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Получатель              КАК СкладПолучатель,
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Статус                  КАК Статус
			|ИЗ
			|	Документ.ДвижениеПродукцииИМатериалов КАК ДвижениеПродукцииИМатериаловПереопределяемый
			|ГДЕ
			|	ДвижениеПродукцииИМатериаловПереопределяемый.Ссылка В(&Документы)";
		
	КонецЕсли;
	
	Если ТекущийВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение
			И СтруктураДоступа.ЗаказНаПеремещениеДоступен Тогда
		
		ДобавитьСекциюОбъединения(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР КОГДА ЗаказНаПеремещениеПереопределяемый.Проведен ТОГДА
			|				1
			|			КОГДА ЗаказНаПеремещениеПереопределяемый.ПометкаУдаления ТОГДА
			|				2
			|			ИНАЧЕ
			|				0
			|		КОНЕЦ                                                  КАК КартинкаСтроки,
			|	ЗаказНаПеремещениеПереопределяемый.Ссылка                  КАК Ссылка,
			|	ЗаказНаПеремещениеПереопределяемый.ПометкаУдаления         КАК ПометкаУдаления,
			|	ЗаказНаПеремещениеПереопределяемый.Номер                   КАК Номер,
			|	ЗаказНаПеремещениеПереопределяемый.Дата                    КАК Дата,
			|	ЗаказНаПеремещениеПереопределяемый.Проведен                КАК Проведен,
			|	ЗаказНаПеремещениеПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ЗаказНаПеремещениеПереопределяемый.Организация             КАК Организация,
			|	ЗаказНаПеремещениеПереопределяемый.ОрганизацияПолучатель   КАК ОрганизацияПолучатель,
			|	ЗаказНаПеремещениеПереопределяемый.Ответственный           КАК Ответственный,
			|	ЗаказНаПеремещениеПереопределяемый.Подразделение           КАК Подразделение,
			|	ЗаказНаПеремещениеПереопределяемый.СкладОтправитель        КАК СкладОтправитель,
			|	ЗаказНаПеремещениеПереопределяемый.СкладПолучатель         КАК СкладПолучатель,
			|	ЗаказНаПеремещениеПереопределяемый.Статус                  КАК Статус
			|ИЗ
			|	Документ.ЗаказНаПеремещение КАК ЗаказНаПеремещениеПереопределяемый
			|ГДЕ
			|	ЗаказНаПеремещениеПереопределяемый.Ссылка В(&Документы)";
		
	КонецЕсли;
	
	Если ТекущийВариантОформления = Перечисления.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение
			И СтруктураДоступа.ЗаказМатериаловДоступен Тогда
		
		ДобавитьСекциюОбъединения(ТекстЗапроса);
		ТекстЗапроса = ТекстЗапроса +
			"ВЫБРАТЬ
			|	ВЫБОР КОГДА ЗаказМатериаловПереопределяемый.Проведен ТОГДА
			|				1
			|			КОГДА ЗаказМатериаловПереопределяемый.ПометкаУдаления ТОГДА
			|				2
			|			ИНАЧЕ
			|				0
			|		КОНЕЦ                                               КАК КартинкаСтроки,
			|	ЗаказМатериаловПереопределяемый.Ссылка                  КАК Ссылка,
			|	ЗаказМатериаловПереопределяемый.ПометкаУдаления         КАК ПометкаУдаления,
			|	ЗаказМатериаловПереопределяемый.Номер                   КАК Номер,
			|	ЗаказМатериаловПереопределяемый.Дата                    КАК Дата,
			|	ЗаказМатериаловПереопределяемый.Проведен                КАК Проведен,
			|	ЗаказМатериаловПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
			|	ЗаказМатериаловПереопределяемый.Организация             КАК Организация,
			|	ЗаказМатериаловПереопределяемый.Организация             КАК ОрганизацияПолучатель,
			|	ЗаказМатериаловПереопределяемый.Ответственный           КАК Ответственный,
			|	ЗаказМатериаловПереопределяемый.Подразделение           КАК Подразделение,
			|	ЗаказМатериаловПереопределяемый.Склад                   КАК СкладОтправитель,
			|	ЗаказМатериаловПереопределяемый.ЦеховаяКладовая         КАК СкладПолучатель,
			|	ЗаказМатериаловПереопределяемый.Статус                  КАК Статус
			|ИЗ
			|	Документ.ЗаказМатериаловВПроизводство КАК ЗаказМатериаловПереопределяемый
			|ГДЕ
			|	ЗаказМатериаловПереопределяемый.Ссылка В(&Документы)";
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекстЗапроса) Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВЫБОР КОГДА ПеремещениеТоваровПереопределяемый.Проведен ТОГДА
		|				1
		|			КОГДА ПеремещениеТоваровПереопределяемый.ПометкаУдаления ТОГДА
		|				2
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ                                                  КАК КартинкаСтроки,
		|	ПеремещениеТоваровПереопределяемый.Ссылка                  КАК Ссылка,
		|	ПеремещениеТоваровПереопределяемый.ПометкаУдаления         КАК ПометкаУдаления,
		|	ПеремещениеТоваровПереопределяемый.Номер                   КАК Номер,
		|	ПеремещениеТоваровПереопределяемый.Дата                    КАК Дата,
		|	ПеремещениеТоваровПереопределяемый.Проведен                КАК Проведен,
		|	ПеремещениеТоваровПереопределяемый.Организация             КАК Организация,
		|	ПеремещениеТоваровПереопределяемый.ОрганизацияПолучатель   КАК ОрганизацияПолучатель,
		|	ПеремещениеТоваровПереопределяемый.НаправлениеДеятельности КАК НаправлениеДеятельности,
		|	ПеремещениеТоваровПереопределяемый.Ответственный           КАК Ответственный,
		|	ПеремещениеТоваровПереопределяемый.Подразделение           КАК Подразделение,
		|	ПеремещениеТоваровПереопределяемый.СкладОтправитель        КАК СкладОтправитель,
		|	ПеремещениеТоваровПереопределяемый.СкладПолучатель         КАК СкладПолучатель,
		|	ПеремещениеТоваровПереопределяемый.Статус                  КАК Статус
		|ИЗ
		|	Документ.ПеремещениеТоваров КАК ПеремещениеТоваровПереопределяемый
		|ГДЕ
		|	ПеремещениеТоваровПереопределяемый.Ссылка В(&Документы)";
		
	КонецЕсли;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, СписокДокументы);
	СвойстваСписка.ТекстЗапроса = ТекстЗапроса;
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.СписокДокументы, СвойстваСписка);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСекциюОбъединения(ТекстЗапроса)
	
	Если ТекстЗапроса <> "" Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьКорзинуПоСкладам()
	
	СтруктураПоиска = Новый Структура("СкладОтправитель, СкладПолучатель");
	Для каждого СтрокаИзменений Из КорзинаПоСкладамИзменения Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаИзменений);
		МассивСтрокКорзина = КорзинаПоСкладам.НайтиСтроки(СтруктураПоиска);
		Если МассивСтрокКорзина.Количество() = 0 Тогда
			СтрокаКорзина = КорзинаПоСкладам.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаКорзина, СтруктураПоиска);
		Иначе
			СтрокаКорзина = МассивСтрокКорзина[0];
		КонецЕсли;
		
		СтрокаКорзина.Вес = СтрокаКорзина.Вес + СтрокаИзменений.Вес;
		СтрокаКорзина.Объем = СтрокаКорзина.Объем + СтрокаИзменений.Объем;
		СтрокаКорзина.ЧастотаСтрок = СтрокаКорзина.ЧастотаСтрок + СтрокаИзменений.ЧастотаСтрок;
		
		Если СтрокаИзменений.ЕстьСтрокиСНулевымВесом Тогда
			СтрокаКорзина.ПризнакЕстьСтрокиСНулевымВесом = 1;
		КонецЕсли;
		
		Если СтрокаИзменений.ЕстьСтрокиСНулевымОбъемом Тогда
			СтрокаКорзина.ПризнакЕстьСтрокиСНулевымОбъемом = 1;
		КонецЕсли;
		
	КонецЦикла;
	
	КУдалению = КорзинаПоСкладам.НайтиСтроки(Новый Структура("ЧастотаСтрок", 0));
	Для каждого СтрокаМассива Из КУдалению Цикл
		КорзинаПоСкладам.Удалить(СтрокаМассива);
	КонецЦикла;
	
	КорзинаПоСкладамИзменения.Очистить();
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьИзмененияКорзиныПоСкладам(ДанныеЗаполнения)

	ДанныеЗаполненияПоСкладам = ДанныеЗаполнения.Скопировать(
		Новый Структура("Отметка", Истина), "СкладОтправитель, СкладПолучатель, Вес, Объем");
	ДанныеЗаполненияПоСкладам.Колонки.Добавить("ЧастотаСтрок", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ДанныеЗаполненияПоСкладам.ЗаполнитьЗначения(1, "ЧастотаСтрок");
	
	ДанныеЗаполненияПоСкладам.Индексы.Добавить("Вес");
	ДанныеЗаполненияПоСкладам.Индексы.Добавить("Объем");
		
	ТаблицаСтрокСНулевымВесом = ДанныеЗаполненияПоСкладам.Скопировать(
		Новый Структура("Вес", 0), "СкладОтправитель, СкладПолучатель");
	ТаблицаСтрокСНулевымОбъемом = ДанныеЗаполненияПоСкладам.Скопировать(
		Новый Структура("Объем", 0), "СкладОтправитель, СкладПолучатель");
		
	ТаблицаСтрокСНулевымВесом.Свернуть("СкладОтправитель, СкладПолучатель");
	ТаблицаСтрокСНулевымОбъемом.Свернуть("СкладОтправитель, СкладПолучатель");
		
	ДанныеЗаполненияПоСкладам.Свернуть("СкладОтправитель, СкладПолучатель", "Вес, Объем, ЧастотаСтрок");
	КорзинаПоСкладамИзменения.Загрузить(ДанныеЗаполненияПоСкладам);
	
	ПоискПоСкладам = Новый Структура("СкладОтправитель, СкладПолучатель");
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтрокСНулевымВесом Цикл
	
		ЗаполнитьЗначенияСвойств(ПоискПоСкладам, СтрокаТаблицы);
		МассивСтрокТЧ = КорзинаПоСкладамИзменения.НайтиСтроки(ПоискПоСкладам);
		Для Каждого СтрокаТЧ Из МассивСтрокТЧ Цикл
			СтрокаТЧ.ЕстьСтрокиСНулевымВесом = Истина;
		КонецЦикла;
	
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ТаблицаСтрокСНулевымОбъемом Цикл
	
		ЗаполнитьЗначенияСвойств(ПоискПоСкладам, СтрокаТаблицы);
		МассивСтрокТЧ = КорзинаПоСкладамИзменения.НайтиСтроки(ПоискПоСкладам);
		Для Каждого СтрокаТЧ Из МассивСтрокТЧ Цикл
			СтрокаТЧ.ЕстьСтрокиСНулевымОбъемом = Истина;
		КонецЦикла;
	
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиКолонокКорзиныПоСкладам()

	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПРЕДСТАВЛЕНИЕ(Константы.ЕдиницаИзмеренияВеса)   КАК ЕдиницаИзмеренияВеса,
		|	ПРЕДСТАВЛЕНИЕ(Константы.ЕдиницаИзмеренияОбъема) КАК ЕдиницаИзмеренияОбъема
		|ИЗ
		|	Константы КАК Константы";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();

	ЗаголовокВес   = СтрШаблон(НСтр("ru = 'Вес (%1)';
									|en = 'Weight (%1)'"), Выборка.ЕдиницаИзмеренияВеса);
	ЗаголовокОбъем = СтрШаблон(НСтр("ru = 'Объем (%1)';
									|en = 'Volume (%1)'"), Выборка.ЕдиницаИзмеренияОбъема);
	
	Элементы.КорзинаПоСкладамВес.Заголовок = ЗаголовокВес;
	Элементы.КорзинаПоСкладамОбъем.Заголовок = ЗаголовокОбъем;

КонецПроцедуры 

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьНастройкиПоВариантуОформления(Форма)
	
	Объект = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Если Форма.ИспользуютсяЗаказыНаПеремещение Тогда
	
		Если Объект.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров") Тогда
		
			Объект.ДатаОтгрузки = Форма.ТекущаяДата;
			Элементы.ОтборПоДатеОтгрузки.Доступность = Ложь;
			
		ИначеЕсли Объект.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ЗаказНаПеремещение") Тогда
			
			Элементы.ОтборПоДатеОтгрузки.Доступность = Истина;
					
		КонецЕсли;
		
	Иначе
		
		Объект.ВариантОформления = ПредопределенноеЗначение("Перечисление.ВариантыОформленияДокументовПеремещения.ПеремещениеТоваров");
		Объект.ДатаОтгрузки = Форма.ТекущаяДата;
		Элементы.ОтборПоДатеОтгрузки.Доступность = Ложь;
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
