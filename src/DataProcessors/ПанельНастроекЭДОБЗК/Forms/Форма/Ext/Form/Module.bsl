#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаписьНастройкиЭДОБЗК = РегистрыСведений.НастройкиЭДОБЗК.СоздатьМенеджерЗаписи();
	ЗаписьНастройкиЭДОБЗК.Прочитать();
	ЗначениеВРеквизитФормы(ЗаписьНастройкиЭДОБЗК, "НастройкиЭДОБЗК");
	
	ПрочитатьНастройкиИнтеграцииСРаботаВРоссии();
	
	ОбновитьОтображение(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Не ЗавершениеРаботы Тогда
		Если Модифицированность Тогда
			Отказ = Истина;
			ТекстВопроса = НСтр("ru = 'При закрытии формы настроек, изменения будут потеряны.
				|Сохранить изменения перед закрытием?';
				|en = 'Changes will be lost upon closing the report settings form.
				|Save the changes before closing?'");
			Оповещение = Новый ОписаниеОповещения("ОбработкаОтветаОПотереИзмененийПриЗакрытии", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаОПотереИзмененийПриЗакрытии(Результат, Дополнительныепараметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		Закрыть(СохранитьНастройки());
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьИнтеграциюСРаботаВРоссииПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементовФормыИнтеграцииСРаботаВРоссии(ЭтотОбъект, Неопределено);
	
	ТребуетсяОбновлениеИнтерфейса = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеВключитьЭлектроннуюПодписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьНажатиеНаГиперссылкуЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.ОбщиеНастройки", , ЭтотОбъект, Истина, , ,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	Если Модифицированность Тогда
		Закрыть(СохранитьНастройки());
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СохранитьНастройкиНаСервере()
	
	Константы.ИспользоватьИнтеграциюСРаботаВРоссии.Установить(ИспользоватьИнтеграциюСРаботаВРоссии);
	Запись = РеквизитФормыВЗначение("НастройкиЭДОБЗК");
	Запись.Записать();
	
	Модифицированность = Ложь;
	
	Возврат ЭДОБЗК.НастройкиЭДОБЗК();
	
КонецФункции

&НаКлиенте
Функция СохранитьНастройки()
	
	Настройки = СохранитьНастройкиНаСервере();
	
	Если ЭтаФорма.ТребуетсяОбновлениеИнтерфейса Тогда
		ОбновитьИнтерфейс();
		ТребуетсяОбновлениеИнтерфейса = Ложь;
	КонецЕсли;
	
	Возврат Настройки;
	
КонецФункции

&НаКлиенте
Процедура ПояснениеНастройкиЭДОБЗКРегистрироватьЗаданияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьНажатиеНаГиперссылкуЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПанельАдминистрированияБСП.Форма.Органайзер", , ЭтотОбъект, Истина, , ,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеНастройкиЭДОБЗКИспользоватьСистемуВзаимодействияОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьНажатиеНаГиперссылкуЗавершение", ЭтотОбъект);
	ОткрытьФорму("Обработка.ПанельАдминистрированияБИП.Форма.ИнтернетПоддержкаИСервисы", , ЭтотОбъект, Истина, , ,
		ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНажатиеНаГиперссылкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	ОбновитьОтображение(ЭтотОбъект);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьОтображение(УправляемаяФорма)
	
	Настройки = НастройкиИБ();
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"КабинетСотрудникаИспользуетсяДекорация",
		"Видимость",
		Настройки.ИспользуетсяЭДОБЗККабинетСотрудника);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"КабинетСотрудникаНеИспользуетсяДекорация",
		"Видимость",
		Не Настройки.ИспользуетсяЭДОБЗККабинетСотрудника);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"НастройкиЭДОБЗКРегистрироватьЗадания",
		"ТолькоПросмотр",
		Не Настройки.ИспользуютсяБизнесПроцессыИЗадачи);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		УправляемаяФорма.Элементы,
		"НастройкиЭДОБЗКИспользоватьСистемуВзаимодействия",
		"ТолькоПросмотр",
		Не Настройки.СистемаВзаимодействийПодключена);
	
	УстановитьДоступностьЭлементовФормыИнтеграцииСРаботаВРоссии(УправляемаяФорма, Настройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиИБ()
	Настройки = Новый Структура;
	НастройкиИнтеграции = РегистрыСведений.НастройкиИнтеграцииКабинетСотрудника.НастройкиИнтеграции();
	Настройки.Вставить("ИспользуетсяЭДОБЗККабинетСотрудника", НастройкиИнтеграции.ИспользуетсяЭДОБЗК);
	Настройки.Вставить("ИспользуютсяБизнесПроцессыИЗадачи", Константы.ИспользоватьБизнесПроцессыИЗадачи.Получить());
	Настройки.Вставить("СистемаВзаимодействийПодключена", Обсуждения.СистемаВзаимодействийПодключена());
	Настройки.Вставить("ИспользуетсяЭлектроннаяПодпись", ЭлектроннаяПодпись.ИспользоватьЭлектронныеПодписи());
	Возврат Настройки;
КонецФункции

&НаСервере
Процедура ПрочитатьНастройкиИнтеграцииСРаботаВРоссии()
	
	ИспользоватьИнтеграциюСРаботаВРоссии = Константы.ИспользоватьИнтеграциюСРаботаВРоссии.Получить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьДоступностьЭлементовФормыИнтеграцииСРаботаВРоссии(Форма, Настройки)
	
	УсловияВыполнены = ?(Настройки = Неопределено, Истина, Настройки.ИспользуетсяЭлектроннаяПодпись);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"ИспользоватьИнтеграциюСРаботаВРоссии",
		"ТолькоПросмотр",
		Не УсловияВыполнены);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"РегистрационныеДанныеРаботаВРоссии",
		"Видимость",
		УсловияВыполнены);
		
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"РегистрационныеДанныеРаботаВРоссии",
		"Доступность",
		Форма.ИспользоватьИнтеграциюСРаботаВРоссии);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура РегистрационныеДанныеРаботаВРоссии(Команда)
	
	Если Модифицированность Тогда
		
		Режим = РежимДиалогаВопрос.ДаНет;
	    Ответ = Ждать ВопросАсинх(НСтр("ru = 'Настройки будут сохранены';
										|en = 'The settings will be saved'"), Режим, 0);
	    Если Ответ = КодВозвратаДиалога.Нет Тогда
	        Возврат;
		КонецЕсли;
		СохранитьНастройки();
		
	КонецЕсли;
	
	ИнтеграцияСРаботаВРоссииКлиент.ОткрытьНастройкиОрганизации();
	
КонецПроцедуры

#КонецОбласти
