#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИдентификаторПользователяИП", ИдентификаторПользователяИП);
	Параметры.Свойство("Приложение", Приложение);
	Если Не ЗначениеЗаполнено(ИдентификаторПользователяИП) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаДлительнаяОперация;
	НачатьПроверкуИдентификатора();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Новый ОписаниеОповещения("ПриЗавершенииЗадания", ЭтотОбъект), ПараметрыОжидания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриЗавершенииЗадания(Результат, ИмяЗадания) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ТекстСообщения = НСтр("ru = 'При выполнении фонового задания возникла ошибка:';
								|en = 'An error occurred while executing a background job:'") + Символы.ПС + Результат.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ПроверкаИдентификатораЗавершение(Результат.АдресРезультата);

КонецПроцедуры

&НаСервере
Процедура НачатьПроверкуИдентификатора()
	
	ИмяМетода = "ИнтеграцияУправлениеПерсоналомМенеджер.ПолучитьАбонентаИнтернетПоддержкиФоновоеЗадание";
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.ЗапуститьВФоне = Истина;
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	
	ПараметрыМетода = Новый Структура("Приложение", Приложение);
	
	ДлительнаяОперация = ДлительныеОперации.ВыполнитьВФоне(ИмяМетода, ПараметрыМетода, ПараметрыВыполненияВФоне);
	
КонецПроцедуры

&НаСервере
Процедура ПроверкаИдентификатораЗавершение(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	ТекстСообщения = "";
	ШаблонТекстаСообщения = НСтр("ru = 'Не удалось проверить пользователя Интернет-поддержки';
								|en = 'Cannot verify online support user'");
	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		ТекстСообщения = ШаблонТекстаСообщения;
	ИначеЕсли Не ЗначениеЗаполнено(Результат.Абонент) Тогда
		ТекстСообщения = ШаблонТекстаСообщения;
	ИначеЕсли Результат.СообщениеОбОшибке <> Неопределено Тогда
		ТекстСообщения = СтрШаблон("%1%2%3", ШаблонТекстаСообщения, Символы.ПС, Результат.СообщениеОбОшибке);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстСообщения) Тогда
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	АбонентНеИзменился = (СокрЛП(ИдентификаторПользователяИП) = СокрЛП(Результат.Абонент.Идентификатор));
	Если АбонентНеИзменился Тогда
		ТекстЗаголовка = НСтр("ru = 'Абонент является текущим пользователем Интернет-поддержки, для изменения необходимо подключить Интернет-поддержку для другого пользователя.';
								|en = 'The subscriber is a current online support user, connect online support for another user to edit.'");
		Элементы.ДекорацияИзменениеНеДоступно.Заголовок = ТекстЗаголовка;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаИзменениеНеДоступно;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаИзменениеАбонента;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти



