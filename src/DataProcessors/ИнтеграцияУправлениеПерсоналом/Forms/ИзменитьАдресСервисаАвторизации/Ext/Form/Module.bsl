
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЦветСтиляПоясняющийОшибкуТекст 	= ЦветаСтиля.ПоясняющийОшибкуТекст;
	ЦветСтиляПоясняющийТекст 		= ЦветаСтиля.ПоясняющийТекст;
	ЦветСтиляИнформационнойНадписи  = ЦветаСтиля.ТекстИнформационнойНадписи;
	
	Приложение = Параметры.Приложение;
	
	Настройки = ИнтеграцияУправлениеПерсоналом.НастройкиПодключения(Приложение);
	АдресСервисаАвторизации = Настройки.АдресСервисаАвторизации;
	АдресСервисаАвторизацииПрежнееЗначение = АдресСервисаАвторизации;
	Если ЗначениеЗаполнено(АдресСервисаАвторизации) Тогда
		ИспользоватьАдресПриложения = 0;
		ИспользоватьАдресСервисаАвторизации = 1;
	Иначе
		ИспользоватьАдресПриложения = 1;
		ИспользоватьАдресСервисаАвторизации = 0;
	КонецЕсли;
	
	Заголовок = НСтр("ru = 'Адрес сервиса авторизации';
					|en = 'Authorization service address'");
	СообщениеПроверкаНеВыполнялась = НСтр("ru = 'Требуется проверить адрес сервиса авторизации.';
											|en = 'Check the authorization service address.'");
	
	ОбновитьЭУВыборАдреса(ЭтаФорма);
	ОбновитьСообщениеОПроверке(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьАдресПриложенияПриИзменении(Элемент)
	
	Если ИспользоватьАдресПриложения = 1 Тогда
		ИспользоватьАдресСервисаАвторизации = 0;
		АдресСервисаАвторизацииПрежнееЗначение = АдресСервисаАвторизации;
		АдресСервисаАвторизации = "";
	КонецЕсли;

	СброситьНастройкиПроверки();
	ОбновитьЭУВыборАдреса(ЭтаФорма);
	ОбновитьСообщениеОПроверке(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАдресСервисаАвторизацииПриИзменении(Элемент)
	
	Если ИспользоватьАдресСервисаАвторизации = 1 Тогда
		ИспользоватьАдресПриложения = 0;
		АдресСервисаАвторизации = АдресСервисаАвторизацииПрежнееЗначение;
	КонецЕсли;
	
	СброситьНастройкиПроверки();
	ОбновитьЭУВыборАдреса(ЭтаФорма);
	ОбновитьСообщениеОПроверке(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьНастройки(Команда)
	
	Если ИспользоватьАдресПриложения = 1 Или НастройкиПравильные Или Модифицированность = Ложь Тогда
		СохранитьНастройкиНаСервер();
		Закрыть();
	Иначе
		Если Не ПроверкаВыполнялась Тогда
			ТекстПредупреждения = НСтр("ru = 'Необходимо проверить адрес сервиса авторизации.';
										|en = 'Check the authorization service address.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Новый адрес сервиса авторизации не прошел проверку.';
										|en = 'The new authorization service address failed the check.'");
		КонецЕсли;
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНастройки(Команда)
	
	Если Не ЗначениеЗаполнено(АдресСервисаАвторизации) Тогда
		ТекстПредупреждения = НСтр("ru = 'Укажите адрес сервиса авторизации';
									|en = 'Specify the authorization service address'");
		ПоказатьПредупреждение( ,ТекстПредупреждения);
		Возврат;
	КонецЕсли;
	
	НачатьПроверкуНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаНовыйНастроек

&НаКлиенте
Процедура НачатьПроверкуНастроек()

	ПроверкаВыполнялась = Истина;
	НастройкиПравильные = Ложь;
	СообщениеПроверки 	= "";
	
	ДлительнаяОперация = ПроверкаНастроекПодключенияНаСервере();
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = НСтр("ru = 'Проверка сервиса авторизации.';
															|en = 'Authorization service check.'");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверкаНастроекЗавершение", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ПроверкаНастроекПодключенияНаСервере()
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("Приложение", Приложение);
	ПараметрыПроверки.Вставить("АдресСервисаАвторизации", АдресСервисаАвторизации);
	
	ПараметрыВыполненияВФоне = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполненияВФоне.Вставить("НаименованиеФоновогоЗадания", НСтр("ru = 'Проверка сервиса авторизации';
																			|en = 'Authorization service check'"));
	ПараметрыВыполненияВФоне.ОжидатьЗавершение = 0;
	Возврат ДлительныеОперации.ВыполнитьВФоне(
		"ИнтеграцияУправлениеПерсоналомОбмен.ПроверкаАдресаСервисаАвторизацииФоновоеЗадание",
		ПараметрыПроверки,
		ПараметрыВыполненияВФоне);
	
КонецФункции

&НаКлиенте
Процедура ПроверкаНастроекЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		СообщениеПроверки = НСтр("ru = 'Не удалось выполнить проверку .';
								|en = 'Cannot check.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеПроверки);
		Возврат;
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		СообщениеПроверки = НСтр("ru = 'При выполнении фонового задания возникла ошибка';
								|en = 'An error occurred while executing the background job'");
		ТекстСообщения = СтрШаблон("%1: %2%3",СообщениеПроверки, Символы.ПС, Результат.КраткоеПредставлениеОшибки) + Символы.ПС + Результат.КраткоеПредставлениеОшибки;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатПроверки(Результат.АдресРезультата);
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьРезультатПроверки(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	Если Результат.ТокенПолучен Тогда
		НастройкиПравильные = Истина;
		СообщениеПроверки = НСтр("ru = 'Адрес сервиса авторизации проверен.';
								|en = 'The authorization service address is checked.'");
	Иначе
		СообщениеПроверки = Результат.СообщениеОбОшибке;
	КонецЕсли;
	
	ОбновитьСообщениеОПроверке(ЭтаФорма, Истина);

КонецПроцедуры

#КонецОбласти

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьЭУВыборАдреса(Форма)

	Элементы = Форма.Элементы;
	Если Форма.ИспользоватьАдресПриложения = 1 Тогда
		Элементы.АдресСервисаАвторизации.Доступность = Ложь;
		Элементы.АдресСервисаАвторизации.АвтоОтметкаНезаполненного = Ложь;
		Элементы.АдресСервисаАвторизации.ОтметкаНезаполненного = Ложь;
	Иначе
		Форма.АдресСервисаАвторизации = Форма.АдресСервисаАвторизацииПрежнееЗначение;
		Элементы.АдресСервисаАвторизации.Доступность = Истина;
		Элементы.АдресСервисаАвторизации.АвтоОтметкаНезаполненного = Истина;
		Элементы.АдресСервисаАвторизации.ОтметкаНезаполненного = Не ЗначениеЗаполнено(Форма.АдресСервисаАвторизации);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьСообщениеОПроверке(Форма, ОбработкаРезультатаПроверки = Ложь)
	
	Форма.Элементы.ФормаПроверитьНастройки.Доступность = Истина;
	Форма.Элементы.СостояниеПроверкиКартинка.Видимость = Истина;
	Если Форма.ИспользоватьАдресПриложения = 1 Тогда
		Форма.СостояниеПроверкиОписание = "";
		Форма.Элементы.СостояниеПроверкиКартинка.Видимость = Ложь;
		Форма.Элементы.ФормаПроверитьНастройки.Доступность = Ложь;
	Иначе
		Если Не ОбработкаРезультатаПроверки И Форма.Модифицированность = Ложь Тогда
			Форма.СостояниеПроверкиОписание = "";
			Форма.Элементы.СостояниеПроверкиКартинка.Видимость = Ложь;
		ИначеЕсли Не Форма.ПроверкаВыполнялась Тогда
			Форма.СостояниеПроверкиКартинка = 1;
			Форма.СостояниеПроверкиОписание = Форма.СообщениеПроверкаНеВыполнялась;
			Форма.Элементы.СостояниеПроверкиОписание.ЦветТекста = Форма.ЦветСтиляПоясняющийТекст;
		ИначеЕсли Форма.НастройкиПравильные Тогда
			Форма.СостояниеПроверкиКартинка = 0;
			Форма.СостояниеПроверкиОписание = Форма.СообщениеПроверки;
			Форма.Элементы.СостояниеПроверкиОписание.ЦветТекста = Форма.ЦветСтиляИнформационнойНадписи;
		Иначе
			Форма.СостояниеПроверкиКартинка = 2;
			Форма.СостояниеПроверкиОписание = Форма.СообщениеПроверки;
			Форма.Элементы.СостояниеПроверкиОписание.ЦветТекста = Форма.ЦветСтиляПоясняющийОшибкуТекст;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СброситьНастройкиПроверки()
	
	ПроверкаВыполнялась = Ложь;
	НастройкиПравильные = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиНаСервер()

	ИнтеграцияУправлениеПерсоналом.СохранитьНовыйАдресСервисаАвторизации(Приложение, АдресСервисаАвторизации);
	Модифицированность = Ложь;

КонецПроцедуры

#КонецОбласти