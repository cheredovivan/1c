
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.ВидОперации = НСтр("ru = 'Подписание электронного документа';
										|en = 'Sign electronic document'")
		ИЛИ Параметры.ВидОперации = НСтр("ru = 'Подписание электронных документов';
										|en = 'Sign electronic documents'") Тогда
		Элементы.Выбрать.Заголовок = НСтр("ru = 'Подписать';
											|en = 'Sign'");
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	ЗначенияРеквизитовНастройки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(НастройкаОбмена, 
		"ПрограммаБанка, ИмяПользователя");
	
	ПрограммаБанка = ЗначенияРеквизитовНастройки.ПрограммаБанка;
	
	Пользователь = ЗначенияРеквизитовНастройки.ИмяПользователя;
	
	ТребуетсяSMSАвторизация = ОбменСБанкамиСлужебный.ТребуетсяSMSАвторизация(НастройкаОбмена);
	
	ЭтоМобильныйКлиент = ОбщегоНазначения.ЭтоМобильныйКлиент();
	
	Если ТипЗнч(Параметры.МассивСертификатов) = Тип("Массив") И Параметры.МассивСертификатов.Количество() > 0 Тогда
		
		Для Каждого Элемент Из Параметры.МассивСертификатов Цикл
			
			Элементы.ВыбранныйСертификат.СписокВыбора.Добавить(Элемент);
			
		КонецЦикла;
		
		Если Параметры.МассивСертификатов.Количество() > 1 Тогда
			
			Элементы.ВыбранныйСертификат.РежимВыбораИзСписка = Истина;
			
		Иначе
			
			ВыбранныйСертификат = Элемент;
			
		КонецЕсли;
		
	Иначе
		
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводЛогинаИПароля;
		Элементы.СтраницаНаВремяСеанса.Видимость = Ложь;
		Элементы.СтраницаВарианты.Видимость = Ложь;
		Элементы.СтраницаМобильныйКлиент.Видимость = Ложь;
		
		Если ЗначенияРеквизитовНастройки.ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен
			И ТребуетсяSMSАвторизация = Перечисления.SMSАвторизацияОбменСБанками.Требуется Тогда
			
			Если ЭтоМобильныйКлиент Тогда
				
				Элементы.СтраницаМобильныйКлиент.Видимость = Истина;
				Элементы.СтраницыВариантовХраненияПароля.ТекущаяСтраница = Элементы.СтраницаМобильныйКлиент;
				СпособХраненияПароля = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
					"ОбменСБанками", "СпособХраненияПароля", "НеСохранять");
				
			Иначе
				
				Элементы.СтраницаВарианты.Видимость = Истина;
				Элементы.СтраницыВариантовХраненияПароля.ТекущаяСтраница = Элементы.СтраницаВарианты;
				СпособХраненияПароля = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
					"ОбменСБанками", "СпособХраненияПароля", "НеСохранять");
			
			КонецЕсли;

		Иначе
			
			Элементы.СтраницаНаВремяСеанса.Видимость = Истина;
			СпособХраненияПароля = "НеСохранять";

			Если ЭтоМобильныйКлиент Тогда
				
				Элементы.ЗапомнитьПарольЛогинНаВремяСеанса.Заголовок = НСтр("ru = 'Запомнить пароль';
																			|en = 'Remember password'");
				Элементы.ЗапомнитьПарольЛогинНаВремяСеанса.Подсказка = 
					НСтр("ru = 'Позволит не вводить пароль при каждом обмене с банком до закрытия приложения.
								|При снятии флажка пароль стирается из памяти, если запоминался ранее.';
								|en = 'It will allow you not to enter a password during each exchange with the bank until the application is closed.
								|When cleared, the password is erased from memory if it was previously remembered.'");
				
			КонецЕсли;
			
		КонецЕсли;

		УстановитьПривилегированныйРежим(Истина);
		Логины = ХранилищеСистемныхНастроек.Загрузить("ОбменСБанками", "Логины");
		
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Логины <> Неопределено И Логины.Получить(НастройкаОбмена) <> Неопределено Тогда
			
			Пользователь = Логины.Получить(НастройкаОбмена);
			
		КонецЕсли;
		
		ПользовательПоУмолчанию = Пользователь;
		
	КонецЕсли;
	
	МассивСообщенийОбмена = Новый Массив;
	
	Если ТипЗнч(Параметры.ОбъектыДляОбработки) = Тип("Массив") И Параметры.ОбъектыДляОбработки.Количество() > 0 Тогда
		
		Если Параметры.ОбъектыДляОбработки.Количество() = 1 Тогда
			
			СообщениеОбмена = Параметры.ОбъектыДляОбработки[0];
			ШаблонГиперссылки = НСтр("ru = '%1 № %2 от %3';
									|en = '%1 No. %2 from %3'");
			ТекстГиперссылки = Строка(СообщениеОбмена);
			МассивСообщенийОбмена.Добавить(СообщениеОбмена);
			
		Иначе
			
			МассивСообщенийОбмена = Параметры.ОбъектыДляОбработки;
			ШаблонГиперссылки = НСтр("ru = 'Электронные документы (%1)';
									|en = 'Electronic documents (%1)'");
			ТекстГиперссылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонГиперссылки,
				Параметры.ОбъектыДляОбработки.Количество());
				
		КонецЕсли;
		
		ТекстГиперссылки = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ТекстГиперссылки);
		НадписьОбъектыДляОбработки = ТекстГиперссылки;
		
	КонецЕсли;
	
	МассивСообщенийОбменаСсылка = ПоместитьВоВременноеХранилище(МассивСообщенийОбмена, УникальныйИдентификатор);
	
	КлючСохраненияПоложенияОкна = ?(МассивСообщенийОбмена.Количество() > 0, "СОбъектами", "БезОбъектов");
	
	Если ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		
		Элементы.ВыбранныйСертификат.ТолькоПросмотр = Истина;
		
	КонецЕсли;
	
	Элементы.ОбъектыДляОбработки.Видимость = (МассивСообщенийОбмена.Количество() > 0);
	
	СрокХраненияПароляИстек = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СрокХраненияПароляИстек", Ложь);
	ИзменилисьПараметрыОкружения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "ИзменилисьПараметрыОкружения", Ложь);
	
	Элементы.СрокХраненияПароляИстек.Видимость = Ложь;
	Элементы.ИзменилисьПараметрыОкружения.Видимость = Ложь;
	Элементы.СообщениеПользователю.Видимость = Ложь;
	
	Если СрокХраненияПароляИстек = Истина ИЛИ ИзменилисьПараметрыОкружения = Истина Тогда
		
		Элементы.ГруппаСрокХраненияПароляИстек.Видимость = Истина;
		Элементы.СрокХраненияПароляИстек.Видимость = СрокХраненияПароляИстек;
		Элементы.ИзменилисьПараметрыОкружения.Видимость = ИзменилисьПараметрыОкружения;
		
	КонецЕсли;
	
	СообщениеПользователю = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "СообщениеПользователю", "");
	Если ЗначениеЗаполнено(СообщениеПользователю) Тогда
		
		Элементы.ГруппаСрокХраненияПароляИстек.Видимость = Истина;
		Элементы.СообщениеПользователю.Видимость = Истина;
		Элементы.СообщениеПользователю.Заголовок = Новый ФорматированнаяСтрока(СообщениеПользователю);
		
	КонецЕсли;
	
	КлючСохраненияПоложенияОкна = Новый УникальныйИдентификатор();
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Элементы.ВыбранныйСертификат.СписокВыбора.Количество() = 1 Тогда
		ОбработкаВыбораСертификата();
	КонецЕсли;
	ОбновитьОтображениеДанных();
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		ТекущийЭлемент = Элементы.ПарольАвторизации;
	КонецЕсли;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
		И ТребуетсяSMSАвторизация = ПредопределенноеЗначение("Перечисление.SMSАвторизацияОбменСБанками.Требуется")
		И ЭтоМобильныйКлиент Тогда
		
		АктуализироватьСпособХраненияПароляМобильныйКлиент();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВыбранныйСертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ", ВыбранныйСертификат);
	ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбранныйСертификатПриИзменении(Элемент)
	
	ОбработкаВыбораСертификата();
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПользователяОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	#Если НЕ ВебКлиент Тогда
		ПарольПользователя = Текст;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольАвторизацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	
	#Если НЕ ВебКлиент Тогда
		ПарольПользователя = Текст;
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОбъектыДляОбработкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	МассивСообщенийОбмена = ПолучитьИзВременногоХранилища(МассивСообщенийОбменаСсылка);
	
	Если МассивСообщенийОбмена.Количество() > 1 Тогда
		ПараметрыФормы = Новый Структура("МассивСообщенийОбмена", МассивСообщенийОбмена);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.СписокЭлектронныхДокументов", ПараметрыФормы, ЭтотОбъект);
	Иначе
		ОбменСБанкамиСлужебныйКлиент.ОткрытьЭДДляПросмотра(МассивСообщенийОбмена[0], , ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьПарольЛогинНаВремяСеансаПриИзменении(Элемент)
	
	Если НЕ Элементы.ПарольАвторизации.Доступность Тогда
		Элементы.ПарольАвторизации.Доступность = НЕ ЗапомнитьПарольНаВремяСеанса;
	КонецЕсли;
	Если ЗапомнитьПарольНаВремяСеанса Тогда
		СпособХраненияПароля = "НаВремяСеанса";	
	Иначе
		СпособХраненияПароля = "НеСохранять";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапомнитьПарольСертификатНаВремяСеансаПриИзменении(Элемент)
	
	Если НЕ Элементы.ПарольПользователя.Доступность Тогда
		Элементы.ПарольПользователя.Доступность = Элементы.ЗапомнитьПарольСертификатНаВремяСеанса.Доступность
			И НЕ ЗапомнитьПарольНаВремяСеанса;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СпособХраненияПароляПриИзменении(Элемент)
	
	АктуализироватьСпособХраненияПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособХраненияПароляМобильныйКлиентПриИзменении(Элемент)
	
	АктуализироватьСпособХраненияПароляМобильныйКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаГотово(Команда)
	
	АктуализироватьСпособХраненияПароля();
	
	Отказ = Ложь;
	
	// Блок проверки на заполненность сертификата ЭП.
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводПароляКСертификату
		И Не ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		ТекстСообщения = ОбщегоНазначенияБЭДКлиентСервер.ТекстСообщения(
			"Поле", "Заполнение", НСтр("ru = 'Сертификат подписи';
										|en = 'Signature certificate'"));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ВыбранныйСертификат");
		Отказ = Истина;
	ИначеЕсли Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводЛогинаИПароля Тогда
		Если ПустаяСтрока(Пользователь) Тогда
			ТекстСообщения = ОбщегоНазначенияБЭДКлиентСервер.ТекстСообщения(
				"Поле", "Заполнение", НСтр("ru = 'Логин';
											|en = 'Username'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Пользователь");
			Отказ = Истина;
		КонецЕсли;
		Если ПустаяСтрока(ПарольПользователя) Тогда
			ТекстСообщения = ОбщегоНазначенияБЭДКлиентСервер.ТекстСообщения(
				"Поле", "Заполнение", НСтр("ru = 'Пароль';
											|en = 'Password'"));
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "ПарольПользователя");
			Отказ = Истина;
		КонецЕсли;
			
		Если Не Отказ И Пользователь <> ПользовательПоУмолчанию Тогда
			СохранитьЗначенияПараметров(Пользователь, НастройкаОбмена, СпособХраненияПароля);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не Отказ Тогда
		ОбработатьПолучениеПароля();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьПолучениеПароля()
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводПароляКСертификату Тогда
		СохраняемыйКлюч = ВыбранныйСертификат;
		СохраняемоеЗначение = ПарольПользователя;
	Иначе
		СохраняемыйКлюч = НастройкаОбмена;
		ДанныеАвторизации = ОбменСБанкамиСлужебныйКлиент.ДанныеАвторизации(Пользователь, ПарольПользователя);
		СохраняемоеЗначение = ДанныеАвторизации;
	КонецЕсли;
	
	Если Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВводЛогинаИПароля Тогда
		
		Если ЗапомнитьПарольНаВремяСеанса ИЛИ ЗапомнитьПарольНаДлительныйСрок Тогда
			
			СохранитьПароль(СохраняемыйКлюч, СохраняемоеЗначение);
			
		Иначе
			
			ОбменСБанкамиСлужебныйКлиент.УдалитьПарольИзСеанса(СохраняемыйКлюч);
			ОбменСБанкамиСлужебныйВызовСервера.УдалитьДанныеАвторизацииОбменСБанками(НастройкаОбмена, "ДляПользователя");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Закрыть(ПараметрыВозвратаПароля());
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьЗначенияПараметров(Знач Логин, Знач НастройкаОбмена, Знач СпособХраненияПароля)
	
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбменСБанками", "СпособХраненияПароля", СпособХраненияПароля);

	УстановитьПривилегированныйРежим(Истина);
	Логины = ХранилищеСистемныхНастроек.Загрузить("ОбменСБанками", "Логины");
	Если Логины = Неопределено Тогда
		Логины = Новый Соответствие;
	КонецЕсли;
	Логины.Вставить(НастройкаОбмена, Логин);
	ХранилищеСистемныхНастроек.Сохранить("ОбменСБанками", "Логины", Логины);

КонецПроцедуры

&НаКлиенте
Функция ПараметрыВозвратаПароля()
	
	РезультатВыбора = Новый Структура;
	
	РезультатВыбора.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
	РезультатВыбора.Вставить("Логин", Пользователь);
	РезультатВыбора.Вставить("Пароль", ПарольПользователя);
	РезультатВыбора.Вставить("ПарольСертификата", ПарольПользователя);
	
	Возврат РезультатВыбора;
	
КонецФункции

&НаКлиенте
Процедура ОбработкаВыбораСертификата()
	
	Если НЕ ЗначениеЗаполнено(ВыбранныйСертификат) Тогда
		Возврат;
	КонецЕсли;
	
	Пароль = Неопределено;
	НаВремяСеанса = Ложь;
	ПарольПолучен = ОбменСБанкамиСлужебныйКлиент.ПарольКСертификатуПолучен(ВыбранныйСертификат, Пароль, НаВремяСеанса);
	ПарольПользователя = Пароль;
	ЗапомнитьПарольНаВремяСеанса = НаВремяСеанса;
	Элементы.ЗапомнитьПарольСертификатНаВремяСеанса.Доступность = НаВремяСеанса ИЛИ НЕ ПарольПолучен;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьПароль(СохраняемыйКлюч, СохраняемоеЗначение)
	
	НужноЗапомнитьПарольМобКлиентТребуетсяSMSАвторизация = ЗапомнитьПарольНаВремяСеанса
		И ЭтоМобильныйКлиент
		И ТребуетсяSMSАвторизация = ПредопределенноеЗначение("Перечисление.SMSАвторизацияОбменСБанками.Требуется");
	
	Если ЗапомнитьПарольНаДлительныйСрок ИЛИ НужноЗапомнитьПарольМобКлиентТребуетсяSMSАвторизация Тогда
		
		ОбменСБанкамиСлужебныйКлиент.СохранитьПарольВБезопасноеХранилище(СохраняемыйКлюч, СохраняемоеЗначение);
		
	КонецЕсли;
	
	Если ЗапомнитьПарольНаВремяСеанса Тогда
		
		ОбменСБанкамиСлужебныйКлиент.СохранитьПарольНаВремяСеанса(СохраняемыйКлюч, СохраняемоеЗначение);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьСпособХраненияПароля()
	
	ЗапомнитьПарольНаВремяСеанса = СпособХраненияПароля = "НаВремяСеанса";
	ЗапомнитьПарольНаДлительныйСрок = СпособХраненияПароля = "НаДлительныйСрок";
	
КонецПроцедуры

&НаКлиенте
Процедура АктуализироватьСпособХраненияПароляМобильныйКлиент()
	
	АктуализироватьСпособХраненияПароля();
	Если СпособХраненияПароля = "НеСохранять" Тогда
		
		Элементы.ДекорацияПояснениеМобильныйКлиент.Заголовок =
			НСтр("ru = 'Потребует ввода пароля при каждом обмене с банком';
				|en = 'It will require entering a password at each exchange with the bank'");
		
	ИначеЕсли СпособХраненияПароля = "НаВремяСеанса" Тогда
		
		Элементы.ДекорацияПояснениеМобильныйКлиент.Заголовок =
			НСтр("ru = 'Позволит не вводить пароль при каждом обмене с банком до закрытия приложения';
				|en = 'It will allow you not to enter a password during each exchange with the bank until the application is closed'");
		
	ИначеЕсли СпособХраненияПароля = "НаДлительныйСрок" Тогда
		
		Элементы.ДекорацияПояснениеМобильныйКлиент.Заголовок =
			НСтр("ru = 'Позволит в течение месяца не вводить пароль при каждом обмене с банком.
			|Рекомендуется установить пароль на разблокирование устройства для обеспечения безопасности.';
			|en = 'It will allow you not to enter a password during each exchange with the bank for a month.
			|We recommend that you set a password to unlock the device to ensure security.'");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
