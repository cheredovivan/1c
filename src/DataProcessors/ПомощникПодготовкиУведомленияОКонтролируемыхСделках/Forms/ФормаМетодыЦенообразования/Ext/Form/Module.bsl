
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Уведомление = Параметры.Уведомление;
	
	Если Не ЗначениеЗаполнено(Уведомление) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	УстановитьЗаголовок();
	
	УстановитьУсловноеОформление();
	
	ЗаполнитьСписокСпособов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_СпособыОпределенияЦенКонтролируемыхСделок" Тогда
		ОбновитьОписаниеСпособаОпределенияЦен(Источник);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМетодыЦенообразования

&НаКлиенте
Процедура МетодыЦенообразованияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.МетодыЦенообразования.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СпособОпределенияЦены = Элементы.МетодыЦенообразования.ТекущиеДанные.СпособОпределенияЦены;
	Если Не ЗначениеЗаполнено(СпособОпределенияЦены) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, СпособОпределенияЦены);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаИзменить(Команда)
	
	Если Элементы.МетодыЦенообразования.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СпособОпределенияЦены = Элементы.МетодыЦенообразования.ТекущиеДанные.СпособОпределенияЦены;
	Если Не ЗначениеЗаполнено(СпособОпределенияЦены) Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьЗначение(, СпособОпределенияЦены);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовок()
	
	РеквизитыУведомления = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Уведомление, "ОтчетныйГод,НомерКорректировки");
	ПредставлениеУведомления = Формат(РеквизитыУведомления.ОтчетныйГод, "ДФ=yyyy");
	Если РеквизитыУведомления.НомерКорректировки <> 0 Тогда
		ПредставлениеУведомления = ПредставлениеУведомления + " " + СтрШаблон(НСтр("ru = '( корр. %1)';
																					|en = '(corr. %1)'"), РеквизитыУведомления.НомерКорректировки);
	КонецЕсли;
	Заголовок = СтрШаблон(НСтр("ru = 'Методы ценообразования для уведомления за %1';
								|en = 'Pricing methods for notification for %1'"), ПредставлениеУведомления);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "МетодыЦенообразованияКодМетодаЦенообразования");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "МетодыЦенообразованияСвойстваМетода");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "МетодыЦенообразованияТекстОшибки");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"МетодыЦенообразования.ЕстьОшибки", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветОшибкиОтправкиБРО);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокСпособов()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Уведомление", Уведомление);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	КонтролируемаяСделка.СпособОпределенияЦеныСделки КАК СпособОпределенияЦеныСделки
	|ПОМЕСТИТЬ СпособыОпределенияЦенСделок
	|ИЗ
	|	Документ.КонтролируемаяСделка КАК КонтролируемаяСделка
	|ГДЕ
	|	НЕ КонтролируемаяСделка.ПометкаУдаления
	|	И КонтролируемаяСделка.УведомлениеОКонтролируемойСделке = &Уведомление
	|	И КонтролируемаяСделка.Строка122СделкаВОбластиВнешнейТорговли
	|	И КонтролируемаяСделка.СпособОпределенияЦеныСделки <> ЗНАЧЕНИЕ(Справочник.СпособыОпределенияЦенКонтролируемыхСделок.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СпособыОпределенияЦенКонтролируемыхСделок.Ссылка КАК Ссылка,
	|	СпособыОпределенияЦенКонтролируемыхСделок.КодМетодаЦенообразования КАК КодМетодаЦенообразования
	|ИЗ
	|	СпособыОпределенияЦенСделок КАК СпособыОпределенияЦенСделок
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СпособыОпределенияЦенКонтролируемыхСделок КАК СпособыОпределенияЦенКонтролируемыхСделок
	|		ПО СпособыОпределенияЦенСделок.СпособОпределенияЦеныСделки = СпособыОпределенияЦенКонтролируемыхСделок.Ссылка";
	
	Методы = Запрос.Выполнить().Выгрузить();
	
	СвойстваМетодов = Справочники.СпособыОпределенияЦенКонтролируемыхСделок.СвойстваСпособовЦенообразования(Методы.ВыгрузитьКолонку("Ссылка"));
	
	Для Каждого СтрокаМетода Из Методы Цикл
		СтрокаМетодаЦенообразования = МетодыЦенообразования.Добавить();
		СтрокаМетодаЦенообразования.СпособОпределенияЦены = СтрокаМетода.Ссылка;
		СтрокаМетодаЦенообразования.КодМетодаЦенообразования = СтрокаМетода.КодМетодаЦенообразования;
		
		СтрокиСвойств = СвойстваМетодов.Получить(СтрокаМетода.Ссылка);
		Если СтрокиСвойств <> Неопределено Тогда
			ЗаполнитьСвойстваМетода(СтрокаМетодаЦенообразования, СтрокиСвойств);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьОписаниеСпособаОпределенияЦен(СпособОпределенияЦены)
	
	СтрокиСпособа = МетодыЦенообразования.НайтиСтроки(Новый Структура("СпособОпределенияЦены", СпособОпределенияЦены));
	Если СтрокиСпособа.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-check bsl-legacy-check-string-literal
	НовыйКодМетодаЦенообразования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СпособОпределенияЦены, "КодМетодаЦенообразования");
	СвойстваМетодов = Справочники.СпособыОпределенияЦенКонтролируемыхСделок.СвойстваСпособовЦенообразования(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СпособОпределенияЦены));
	СвойстваМетода = СвойстваМетодов.Получить(СпособОпределенияЦены);
	
	Для Каждого СтрокаСпособа Из СтрокиСпособа Цикл
		
		СтрокаСпособа.КодМетодаЦенообразования = НовыйКодМетодаЦенообразования;
		ЗаполнитьСвойстваМетода(СтрокаСпособа, СвойстваМетода);
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвойстваМетода(СтрокаМетодаЦенообразования, СтрокиСвойств)
	
	ОписаниеСвойств = КонтролируемыеСделкиЦенообразование.ОписаниеСвойствМетодаЦенообразования(
		СтрокаМетодаЦенообразования.КодМетодаЦенообразования, СтрокиСвойств);
	
	СтрокаМетодаЦенообразования.ЕстьОшибки = ОписаниеСвойств.ЕстьОшибкиЗаполнения;
	СтрокаМетодаЦенообразования.ТекстОшибки = ОписаниеСвойств.ТекстОшибкиЗаполнения;
	СтрокаМетодаЦенообразования.СвойстваМетода = ОписаниеСвойств.ЗначенияСвойствСтрокой;
	
	Если Не ЗначениеЗаполнено(СтрокаМетодаЦенообразования.СвойстваМетода) Тогда
		СтрокаМетодаЦенообразования.СвойстваМетода = ТекстЗаполнить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ТекстЗаполнить()
	Возврат НСтр("ru = 'Заполнить';
				|en = 'Fill'");
КонецФункции

#КонецОбласти
