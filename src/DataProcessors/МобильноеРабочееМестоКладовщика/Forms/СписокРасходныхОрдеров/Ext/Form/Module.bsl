
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	Склад                 = Параметры.Склад;
	Помещение             = Параметры.Помещение;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	Режим             	  = Параметры.Режим;
	Пользователь          = Параметры.Исполнитель;
		
	РежимФормированияРасходныхОрдеров = Константы.РежимФормированияРасходныхОрдеров.Получить();
	
	Если Режим = "ОрдераКОтбору" 
		И РежимФормированияРасходныхОрдеров = Перечисления.РежимыФормированияРасходныхОрдеров.Кладовщиком Тогда
		Исполнитель = Параметры.Исполнитель;
		Элементы.ГруппаМеню.Видимость = Ложь;
		Элементы.НазначитьМне.Видимость = Ложь;
		Элементы.Начать.Видимость = Ложь;
	ИначеЕсли Режим = "ОрдераКОтбору"
		И РежимФормированияРасходныхОрдеров = Перечисления.РежимыФормированияРасходныхОрдеров.Автоматически Тогда
		Режим = "ОрдераКОтборуАвтоматически";
	ИначеЕсли Режим = "ОрдераКПроверке" Тогда
		#Если Не МобильныйАвтономныйСервер Тогда
		ПроверкаТолькоСвоихОрдеров = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров()
			И Не Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуЧужихОрдеров();
		ПроверкаТолькоЧужихОрдеров = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуЧужихОрдеров()
			И Не Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров();
		#КонецЕсли
	КонецЕсли;
	
	СформироватьПредставлениеОтборов();
	ОбновитьСписокСортировок();
	ОбновитьСписокСтатусов();
	
	ЗаполнитьСпискиОрдеров();
	УстановитьУсловноеОформление();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОткрытьСтраницу("СтраницаСвободныеОрдера");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Склады.Форма.ФормаВыбораМРМ") Тогда
		
		Склад = ВыбранноеЗначение;
		Помещение = Неопределено;
		СформироватьПредставлениеОтборов();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ") Тогда
		
		Помещение = ВыбранноеЗначение;
		СформироватьПредставлениеОтбораПомещение();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Пользователи.Форма.ФормаСписка") Тогда
		
		Исполнитель = ВыбранноеЗначение;
		ОбновитьДанныеФормы();
		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ИсполнительОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОчиститьСообщения();
	
	Если ПроверкаТолькоЧужихОрдеров И ВыбранноеЗначение = Пользователь Тогда
		ТекстОшибки = НСтр("ru = 'Текущий пользователь может проверять только чужие ордера.';
							|en = 'The current user can only check other notes.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Исполнитель = ВыбранноеЗначение;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролерПриИзменении(Элемент) 
	
	ОбновитьДанныеФормы();

КонецПроцедуры

&НаКлиенте
Процедура ОтгрузилПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();

КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент) 
	
	ОбновитьДанныеФормы();
	
	Если Не (Режим = "ОрдераКОтгрузке" И Статус = "Проверен") Тогда 
		Элементы.Начать.Видимость = Ложь;
		Элементы.НазначитьМне.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода Тогда
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если КонецПериода < НачалоПериода И ЗначениеЗаполнено(КонецПериода) Или Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = КонецПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыОткрытия = Новый Структура("РежимВыбора", Истина);
	
	Если ПроверкаТолькоЧужихОрдеров Тогда
		
		СписокПользователи = Новый СписокЗначений;
		СписокПользователи.Добавить(Пользователь);
		ПараметрыОткрытия.Вставить("СкрываемыеПользователи", СписокПользователи);
		
	КонецЕсли;
	
	ОткрытьФорму("Справочник.Пользователи.Форма.ФормаСписка", ПараметрыОткрытия, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОрдеровСвободные

&НаКлиенте
Процедура СписокОрдеровСвободныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.СписокОрдеровСвободные.ТекущиеДанные; 
	
	ПараметрыФормы = Новый Структура; 
	ПараметрыФормы.Вставить("Ссылка", ТекущаяСтрока.Ссылка);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);

	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
		
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОрдеровМои

&НаКлиенте
Процедура СписокОрдеровМоиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.СписокОрдеровМои.ТекущиеДанные;
	
	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);

	Если Режим = "ОрдераКОтборуАвтоматически"
		И ИспользоватьАдресноеХранение Тогда
			
		Если ИспользоватьСкладскиеПомещения И Не ЗначениеЗаполнено(Помещение) Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст =  НСтр("ru = 'Укажите помещение в отборе';
												|en = 'Specify the wareroom for picking'");
			СообщениеПользователю.Сообщить();
			Возврат;
		КонецЕсли;
			
		СтруктураЗаполнения = Новый Структура;
		СтруктураЗаполнения.Вставить("Склад", Склад);
		СтруктураЗаполнения.Вставить("Помещение", Помещение);
		СтруктураЗаполнения.Вставить("Распоряжение", ТекущаяСтрока.Ссылка);
		СтруктураЗаполнения.Вставить("Получатель", ТекущаяСтрока.Получатель);
			
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипДокумента", 3);

		ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
			
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.Товары", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый); 
	Иначе

		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", ТекущаяСтрока.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборыОткрыть(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
	
	Отбор = Новый Структура();
	Отбор.Вставить("ИспользоватьОрдернуюСхемуПриОтгрузке", Истина);
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
		
	ОткрытьФорму(
		"Справочник.Склады.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Команда)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтборыОчиститьСклад(Команда)
	
	Склад = Неопределено;
	Помещение = Неопределено;
	СформироватьПредставлениеОтборов();
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОткрытьПомещение(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Владелец", Склад);
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму(
		"Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПомещение(Команда)
	
	Помещение = Неопределено;
	СформироватьПредставлениеОтбораПомещение();
	ОбновитьДанныеФормы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСортировку(Команда)
	
	Сортировка = "Дата";
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПериод(Команда)
	
	НачалоПериода =  '00010101';
	КонецПериода =  '00010101';
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьИсполнителя(Команда)
	
	Исполнитель = Неопределено;
	ОбновитьДанныеФормы();
	
КонецПроцедуры  

&НаКлиенте
Процедура ОтборыОчиститьКонтролер(Команда)
	
	Контролер = Неопределено;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьОтгрузил(Команда)
	
	Отгрузил = Неопределено;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСтатус(Команда)
	
	Статус = "Все";
	
	Элементы.Начать.Видимость = Ложь;
	Элементы.НазначитьМне.Видимость = Ложь; 
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры 

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьПериодЗавершение",
		ЭтотОбъект);
		
	МобильноеРабочееМестоКладовщикаКлиент.РедактироватьПериод(
		ЭтотОбъект,
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоПериода", "КонецПериода"),
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаправлениеСортировки(Команда)
	
	Если Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию Тогда
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоУбыванию;
		НаправлениеСортировки = "ПоУбыванию";
	Иначе
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
		НаправлениеСортировки = "ПоВозрастанию";
	КонецЕсли;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделСвободные(Команда)
	
	ОткрытьСтраницу("СтраницаСвободныеОрдера");

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделМои(Команда)

	ОткрытьСтраницу("СтраницаМоиОрдера");

КонецПроцедуры

&НаКлиенте
Процедура Начать(Команда)
	
	ОчиститьСообщения();
	
	ТекущаяСтрока = Элементы.СписокОрдеровСвободные.ТекущиеДанные;
	
	Если Не НазначитьМнеНаСервере(ТекущаяСтрока.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
	ТекущаяСтрока = Элементы.СписокОрдеровСвободные.ТекущиеДанные;
	
	Если Режим = "ОрдераКОтборуАвтоматически"
		И ИспользоватьАдресноеХранение Тогда 
		
		Если ИспользоватьСкладскиеПомещения И Не ЗначениеЗаполнено(Помещение) Тогда
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст =  НСтр("ru = 'Укажите помещение в отборе';
												|en = 'Specify the wareroom for picking'");
			СообщениеПользователю.Сообщить();
			Возврат;
		КонецЕсли; 
		
		СтруктураЗаполнения = Новый Структура;
		СтруктураЗаполнения.Вставить("Склад", Склад);
		СтруктураЗаполнения.Вставить("Помещение", Помещение);
		СтруктураЗаполнения.Вставить("Распоряжение", ТекущаяСтрока.Ссылка);
		СтруктураЗаполнения.Вставить("Получатель", ТекущаяСтрока.Получатель);
			
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТипДокумента", 3);

		ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
			
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.Товары", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый); 
	Иначе
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", ТекущаяСтрока.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОтгрузку(Команда)  
	
	ТекущаяСтрока = Элементы.СписокОрдеровМои.ТекущиеДанные;
	ЗавершитьОтгрузкуНаСервере(ТекущаяСтрока.Ссылка);
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьМне(Команда)  
		
	ТекущаяСтрока = Элементы.СписокОрдеровСвободные.ТекущиеДанные;
	НазначитьМнеНаСервере(ТекущаяСтрока.Ссылка);
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьСМеня(Команда)

	ТекущаяСтрока = Элементы.СписокОрдеровМои.ТекущиеДанные;
	СнятьСМеняНаСервере(ТекущаяСтрока.Ссылка);
	
	ОбновитьДанныеФормы();
			
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элементы.ГруппаМеню.Видимость = Не (ИспользоватьСтатусыРасходныхОрдеров И Режим = "ОрдераКОтбору");
	
	Если Режим = "ОрдераКОтбору" Тогда
		ТекстЗаголовка = НСтр("ru = 'Расходные ордера к отбору';
								|en = 'Goods issues to pick'");
		Элементы.Начать.Заголовок = НСтр("ru = 'Начать отбор';
										|en = 'Start picking'");
	ИначеЕсли Режим = "ОрдераКОтборуАвтоматически" Тогда
		ТекстЗаголовка = НСтр("ru = 'Расходные ордера к отбору';
								|en = 'Goods issues to pick'");
		Элементы.СписокОрдеровМоиКонтролер.Видимость = Ложь;
		Элементы.Начать.Заголовок = НСтр("ru = 'Начать отбор';
										|en = 'Start picking'");
	ИначеЕсли Режим = "ОрдераКПроверке" Тогда 
		ТекстЗаголовка = НСтр("ru = 'Расходные ордера к проверке';
								|en = 'Goods issues to check'");
		Элементы.Начать.Заголовок = НСтр("ru = 'Начать проверку';
										|en = 'Начать проверку'");
		Элементы.СписокОрдеровМоиКонтролер.Видимость = Ложь;
	ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда
		ТекстЗаголовка = НСтр("ru = 'Расходные ордера к отгрузке';
								|en = 'Goods issues to ship'");
		Элементы.Начать.Заголовок = НСтр("ru = 'Начать отгрузку';
										|en = 'Start shipment'");
		Элементы.СписокОрдеровМоиОтветственный.Видимость = Ложь;
		Элементы.ОтборыКонтролер.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
		Элементы.ОтборыОтгрузил.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
		Элементы.ОтборыСтатус.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
	КонецЕсли;
	Заголовок = ТекстЗаголовка;

	#Если МобильныйАвтономныйСервер Тогда
	Элементы.Начать.Видимость = Ложь;
	Элементы.НазначитьМне.Видимость = Ложь;
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаполнитьСпискиОрдеров();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпискиОрдеров()
	
	Если ЗначениеЗаполнено(Склад) И Не ИспользоватьСтатусыРасходныхОрдеров Тогда
		ЗаполнитьСписокОрдеровБезСтатусов();
		Возврат;
	КонецЕсли;
		
	СписокОрдеровСвободные.Очистить();
	СписокОрдеровМои.Очистить();
	
	ЗапросСвободные = Обработки.МобильноеРабочееМестоКладовщика.ЗапросСписокРасходныхОрдеров();
	ЗапросМои = Обработки.МобильноеРабочееМестоКладовщика.ЗапросСписокРасходныхОрдеров();
	
	ЗапросСвободные = СформированныйЗапрос(ЗапросСвободные, Истина);
	ЗапросМои = СформированныйЗапрос(ЗапросМои, Ложь);
	
	СписокОрдеровСвободные.Загрузить(ЗапросСвободные.Выполнить().Выгрузить());
	Если Не Режим = "ОрдераКОтбору" Тогда
		СписокОрдеровМои.Загрузить(ЗапросМои.Выполнить().Выгрузить());
	КонецЕсли;

	СформироватьЗаголовкиКомандМеню();

КонецПроцедуры  

&НаСервере
Процедура ЗаполнитьСписокОрдеровБезСтатусов()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСписокОрдеровСвободные;

	СписокОрдеровСвободные.Очистить();
	СписокОрдеровМои.Очистить();
	
	ЗапросСвободные = Обработки.МобильноеРабочееМестоКладовщика.ЗапросСписокРасходныхОрдеров();
	ЗапросМои = Обработки.МобильноеРабочееМестоКладовщика.ЗапросСписокРасходныхОрдеров();
	
	ЗапросСвободные = СформированныйЗапросБезСтатусов(ЗапросСвободные, Истина);
	ЗапросМои = СформированныйЗапросБезСтатусов(ЗапросМои, Ложь);
	
	СписокОрдеровСвободные.Загрузить(ЗапросСвободные.Выполнить().Выгрузить());
	СписокОрдеровМои.Загрузить(ЗапросМои.Выполнить().Выгрузить());

	СформироватьЗаголовкиКомандМеню();

КонецПроцедуры

&НаСервере
Функция СформированныйЗапрос(Запрос, СписокСвободные = Истина)
	
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		Запрос.Текст = Запрос.Текст  + "
			|	И РасходныйОрдерНаТовары.Помещение = &Помещение";
		Запрос.УстановитьПараметр("Помещение", Помещение);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И РасходныйОрдерНаТовары.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода + 60*60*24);
	КонецЕсли;
	
	ИспользоватьСтатусы = ИспользоватьСтатусыРасходныхОрдеров Или Режим = "ОрдераКОтгрузке";
	Если ИспользоватьСтатусы И ЗначениеЗаполнено(Статус) И СписокСвободные Тогда
		Если Статус = "ВсеНеотгруженные" Тогда 
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Статус В (&Статусы)";
			Статусы = Новый Массив;
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.Подготовлен);
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.КОтбору);
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.КПроверке);
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки);
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.Проверен);
			Статусы.Добавить(Перечисления.СтатусыРасходныхОрдеров.КОтгрузке);
			Запрос.УстановитьПараметр("Статусы", Статусы);
		ИначеЕсли Не Статус = "Все" Тогда
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Статус = &Статус";
			Запрос.УстановитьПараметр("Статус", Перечисления.СтатусыРасходныхОрдеров[Статус]);
		КонецЕсли; 
	Иначе
		Запрос.Текст = Запрос.Текст + "
			|	И РасходныйОрдерНаТовары.Статус = &Статус";
	КонецЕсли;
	
	СтатусСвободные = Неопределено;
	
	Если Режим = "ОрдераКОтбору" Тогда
		СтатусСвободные = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	ИначеЕсли Режим = "ОрдераКОтборуАвтоматически" Тогда
		СтатусСвободные = Перечисления.СтатусыРасходныхОрдеров.Подготовлен;
		СтатусМои = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	ИначеЕсли Режим = "ОрдераКПроверке" Тогда
		СтатусСвободные = Перечисления.СтатусыРасходныхОрдеров.КПроверке;
		СтатусМои = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки;
	ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда
		СтатусМои = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
		Если Не ЗначениеЗаполнено(Статус) Тогда
			СтатусСвободные = Перечисления.СтатусыРасходныхОрдеров.Проверен;
			Статус = "Проверен";
		КонецЕсли;
	КонецЕсли; 
	
	Если СписокСвободные Тогда
		Запрос.УстановитьПараметр("ОтветственныйЗаголовок", НСтр("ru = 'Исполнитель';
																|en = 'Assignee'"));
		
		Если Режим = "ОрдераКПроверке" Тогда
			Если ЗначениеЗаполнено(Исполнитель) Тогда
				Запрос.Текст = Запрос.Текст + "
					|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
				Запрос.УстановитьПараметр("Ответственный", Исполнитель);
				
			ИначеЕсли ПроверкаТолькоЧужихОрдеров Тогда
				Запрос.Текст = Запрос.Текст + "
					|	И РасходныйОрдерНаТовары.Ответственный <> &Ответственный";
				Запрос.УстановитьПараметр("Ответственный", Пользователь);
			КонецЕсли;
			
			Если ПроверкаТолькоСвоихОрдеров Тогда
				Запрос.Текст = Запрос.Текст + "
					|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
				Запрос.УстановитьПараметр("Ответственный", Пользователь);
			КонецЕсли; 
		ИначеЕсли ЗначениеЗаполнено(Исполнитель) Тогда
				Запрос.Текст = Запрос.Текст + "
					|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
				Запрос.УстановитьПараметр("Ответственный", Исполнитель);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Контролер) Тогда 
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Контролер = &Контролер";
			Запрос.УстановитьПараметр("Контролер", Контролер);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Отгрузил) Тогда 
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Отгрузил = &Отгрузил";
			Запрос.УстановитьПараметр("Отгрузил", Отгрузил);
		КонецЕсли;
		
		Если СтатусСвободные <> Неопределено Тогда
			Запрос.УстановитьПараметр("Статус", СтатусСвободные);
		КонецЕсли;
	Иначе
		Если Режим = "ОрдераКОтборуАвтоматически" Тогда
			
			Запрос.УстановитьПараметр("ОтветственныйЗаголовок", НСтр("ru = 'Исполнитель';
																	|en = 'Assignee'"));
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
			Запрос.УстановитьПараметр("Ответственный", Пользователь);  
			
		ИначеЕсли Режим = "ОрдераКПроверке" Тогда 
			
			Запрос.УстановитьПараметр("ОтветственныйЗаголовок", НСтр("ru = 'Исполнитель';
																	|en = 'Assignee'"));
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Контролер = &Контролер";
			Запрос.УстановитьПараметр("Контролер", Пользователь);
			
		ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда 
			
			Запрос.УстановитьПараметр("ОтветственныйЗаголовок", НСтр("ru = 'Контролер';
																	|en = 'Inspector'"));
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Отгрузил = &Отгрузил";
			Запрос.УстановитьПараметр("Отгрузил", Пользователь); 
			
		КонецЕсли;
		Запрос.УстановитьПараметр("Статус", СтатусМои);
	КонецЕсли;
			
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	";
	
	Если Сортировка = "Получатель" Тогда
		Запрос.Текст = Запрос.Текст + "Получатель.Наименование";
	Иначе
		Запрос.Текст = Запрос.Текст + Сортировка;
	КонецЕсли;
	
	Если НаправлениеСортировки = "ПоУбыванию" Тогда
		Запрос.Текст = Запрос.Текст + " УБЫВ";
	Иначе
		Запрос.Текст = Запрос.Текст + " ВОЗР";
	КонецЕсли;

	Возврат Запрос;
	
КонецФункции

&НаСервере
Функция СформированныйЗапросБезСтатусов(Запрос, СписокСвободные = Истина)
	
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Запрос.Текст = Запрос.Текст + "
			|	И РасходныйОрдерНаТовары.Статус = &Статус";
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		Запрос.Текст = Запрос.Текст  + "
			|	И РасходныйОрдерНаТовары.Помещение = &Помещение";
		Запрос.УстановитьПараметр("Помещение", Помещение);	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И РасходныйОрдерНаТовары.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода + 60*60*24);
	КонецЕсли;
		
	СтатусМои = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
	СтатусСвободные = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
	
	Запрос.УстановитьПараметр("ОтветственныйЗаголовок", НСтр("ru = 'Исполнитель';
															|en = 'Assignee'"));

	Если СписокСвободные Тогда
		Если ЗначениеЗаполнено(Исполнитель) Тогда
			Запрос.Текст = Запрос.Текст + "
				|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
			Запрос.УстановитьПараметр("Ответственный", Исполнитель);
		КонецЕсли;
		Запрос.УстановитьПараметр("Статус", СтатусСвободные);
	Иначе
		Запрос.Текст = Запрос.Текст + "
			|	И РасходныйОрдерНаТовары.Ответственный = &Ответственный";
		Запрос.УстановитьПараметр("Ответственный", Пользователь);
		Запрос.УстановитьПараметр("Статус", СтатусМои);
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	";
	
	Если Сортировка = "Получатель" Тогда
		Запрос.Текст = Запрос.Текст + "Получатель.Наименование";
	Иначе
		Запрос.Текст = Запрос.Текст + Сортировка;
	КонецЕсли;
	
	Если НаправлениеСортировки = "ПоУбыванию" Тогда
		Запрос.Текст = Запрос.Текст + " УБЫВ";
	Иначе
		Запрос.Текст = Запрос.Текст + " ВОЗР";
	КонецЕсли;

	Возврат Запрос;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСтраницу(ИмяВкладки)
	
	Элементы.РамкаСвободные.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаСвободныеОрдера.ЦветТекста = WebЦвета.ТемноСерый;
	
	Элементы.РамкаМои.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаМоиОрдера.ЦветТекста = WebЦвета.ТемноСерый;
	
	Если ИмяВкладки = "СтраницаСвободныеОрдера" Тогда 
		Элементы.КомандаСвободныеОрдера.ЦветТекста = WebЦвета.Черный;
		Элементы.РамкаСвободные.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСписокОрдеровСвободные;
		Элементы.ОтборыИсполнитель.Видимость = Истина;
		Элементы.ОтборыСтатус.Видимость = Ложь;
		Если Режим = "ОрдераКПроверке" Тогда
			Элементы.ОтборыИсполнитель.Видимость = Не ПроверкаТолькоСвоихОрдеров;
		ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда
			Элементы.ОтборыКонтролер.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
			Элементы.ОтборыОтгрузил.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
			Элементы.ОтборыСтатус.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
		КонецЕсли;
	ИначеЕсли ИмяВкладки = "СтраницаМоиОрдера" Тогда
		Элементы.КомандаМоиОрдера.ЦветТекста = WebЦвета.Черный;
		Элементы.РамкаМои.Картинка   = БиблиотекаКартинок.РамкаМенюЧерная;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСписокОрдеровМои;
		Если Режим = "ОрдераКОтгрузке" Тогда
			Элементы.ЗавершитьОтгрузку.Видимость = Истина;
		КонецЕсли;
		Элементы.ОтборыСтатус.Видимость = Ложь;
		Элементы.ОтборыИсполнитель.Видимость = Ложь;
		Элементы.ОтборыКонтролер.Видимость = Ложь;
		Элементы.ОтборыОтгрузил.Видимость = Ложь;
	КонецЕсли;
			
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтборов()
	
	СформироватьПредставлениеОтбораСклад();
	СформироватьПредставлениеОтбораПомещение();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтбораСклад()
	
	ИспользоватьСкладскиеПомещения = Ложь;
	ИспользоватьСтатусыРасходныхОрдеров = Ложь;
	ИспользоватьАдресноеХранение = Ложь;
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Склад) Тогда
		ПредставленияОтборов = Склад;
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.Черный;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
		
		СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(Склад);
		ИспользоватьСтатусыРасходныхОрдеров = СвойстваСклада.ИспользоватьСтатусыРасходныхОрдеров;
		ИспользоватьСкладскиеПомещения = СвойстваСклада.ИспользоватьСкладскиеПомещения; 
		ИспользоватьАдресноеХранение = СвойстваСклада.ИспользоватьАдресноеХранение; 
		Элементы.КомандаОтборыОчистить.Видимость = Истина;
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Склад';
									|en = 'Warehouse'");
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчистить.Видимость = Ложь;
		Элементы.ГруппаМеню.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.ГруппаОтборыПомещение.Видимость = ИспользоватьСкладскиеПомещения;
	Элементы.КомандаОтборыОткрыть.Заголовок = ПредставленияОтборов;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтбораПомещение()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Помещение) Тогда
		ПредставленияОтборов = Помещение;
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюЧерная; 
		
		ИспользоватьАдресноеХранение = Помещение["ИспользоватьАдресноеХранение"];

	Иначе
		ПредставленияОтборов = НСтр("ru = 'Выбрать помещение';
									|en = 'Select a wareroom'");
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.КомандаОтборыОткрытьПомещение.Заголовок = ПредставленияОтборов;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСтатусов()
	
	Элементы.Статус.СписокВыбора.Очистить();
	
	Элементы.Статус.СписокВыбора.Добавить("Все", НСтр("ru = 'Все';
														|en = 'All'"));
	Элементы.Статус.СписокВыбора.Добавить("ВсеНеотгруженные", НСтр("ru = 'Все неотгруженные';
																	|en = 'All unshipped'"));		
	Элементы.Статус.СписокВыбора.Добавить("Подготовлен", НСтр("ru = 'Подготовлен';
																|en = 'Prepared'"));
	Элементы.Статус.СписокВыбора.Добавить("КОтбору", НСтр("ru = 'К отбору';
															|en = 'Ready for picking'"));
	Элементы.Статус.СписокВыбора.Добавить("КПроверке", НСтр("ru = 'К проверке';
															|en = 'To verify'"));
	Элементы.Статус.СписокВыбора.Добавить("ВПроцессеПроверки", НСтр("ru = 'В процессе проверки';
																	|en = 'Check in progress'"));
	Элементы.Статус.СписокВыбора.Добавить("Проверен", НСтр("ru = 'Проверен';
															|en = 'Checked'"));
	Элементы.Статус.СписокВыбора.Добавить("КОтгрузке", НСтр("ru = 'К отгрузке';
															|en = 'To ship'"));
	Элементы.Статус.СписокВыбора.Добавить("Отгружен", НСтр("ru = 'Отгружен';
															|en = 'Shipped'"));
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСортировок()
	
	Если Не Режим = "ОрдераКПроверке" 
		Или (Режим = "ОрдераКПроверке" И Не ПроверкаТолькоЧужихОрдеров) Тогда
		Элементы.Исполнитель.СписокВыбора.Очистить();
		Элементы.Исполнитель.СписокВыбора.Добавить(Пользователь, НСтр("ru = '<Мои документы>';
																		|en = '<My documents>'"));
	КонецЕсли;
	
	Элементы.Сортировка.СписокВыбора.Очистить();
	Элементы.Сортировка.СписокВыбора.Добавить("Дата", НСтр("ru = 'Дата';
															|en = 'Date'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Номер", НСтр("ru = 'Номер';
															|en = 'Number'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Получатель", НСтр("ru = 'Получатель';
																|en = 'Recipient'"));
	Сортировка = "Дата";
	
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		ПериодПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	ЗаполнитьСпискиОрдеров();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовкиКомандМеню()
	
	Элементы.КомандаСвободныеОрдера.Заголовок = СтрШаблон(НСтр("ru = 'Свободные (%1)';
																|en = 'Free (%1)'"), СписокОрдеровСвободные.Количество());
	Элементы.КомандаМоиОрдера.Заголовок = СтрШаблон(НСтр("ru = 'Мои (%1)';
														|en = 'My (%1)'"), СписокОрдеровМои.Количество());
	
КонецПроцедуры

&НаСервере
Функция НазначитьМнеНаСервере(Знач РасходныйОрдер)
	
	Если Режим = "ОрдераКОтборуАвтоматически" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	ИначеЕсли Режим = "ОрдераКПроверке" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки;
	ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
	КонецЕсли;
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(РасходныйОрдер, СтатусОрдера);
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗавершитьОтгрузкуНаСервере(РасходныйОрдер)

	СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
	Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(РасходныйОрдер, СтатусОрдера);
	
КонецПроцедуры 

&НаСервере
Процедура СнятьСМеняНаСервере(Знач РасходныйОрдер)

	Если Режим = "ОрдераКОтборуАвтоматически" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.Подготовлен;
	ИначеЕсли Режим = "ОрдераКПроверке" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.КПроверке;
	ИначеЕсли Режим = "ОрдераКОтгрузке" Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.Проверен;
	КонецЕсли; 
	
	Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(РасходныйОрдер, СтатусОрдера);
	
КонецПроцедуры

#КонецОбласти