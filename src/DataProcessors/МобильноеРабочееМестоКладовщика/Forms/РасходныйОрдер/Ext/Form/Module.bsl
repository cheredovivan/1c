
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Ссылка = Параметры.Ссылка;
	
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	
	ЗаблокироватьДанныеДляРедактирования(Ссылка,,УникальныйИдентификатор);
	
	ЗаполнитьФорму();
	
	//++ Локализация
	
	ИспользоватьУчетМаркируемойПродукции = Константы.ВестиУчетМаркируемойПродукцииИСМП.Получить();
	ПроверкаИПодборПродукцииИСМППереопределяемый.ПриОпределенииОрганизации(Ссылка, Организация);
	
	ЕстьМаркируемаяПродукция = Ложь;
	ВидыПродукцииИСМП = МобильноеРабочееМестоКладовщикаЛокализация.ВидыПродукцииИСМП();
	ПроверкаИПодборПродукцииИСМППереопределяемый.ЕстьМаркируемаяПродукцияВКоллекции(ТоварыКОтбору, ВидыПродукцииИСМП, ЕстьМаркируемаяПродукция);
	Если НЕ ЕстьМаркируемаяПродукция Тогда
		ПроверкаИПодборПродукцииИСМППереопределяемый.ЕстьМаркируемаяПродукцияВКоллекции(ТоварыКОтгрузке, ВидыПродукцииИСМП, ЕстьМаркируемаяПродукция);
	КонецЕсли;
	Если НЕ ЕстьМаркируемаяПродукция Тогда
		ПроверкаИПодборПродукцииИСМППереопределяемый.ЕстьМаркируемаяПродукцияВКоллекции(ТоварыКПроверке, ВидыПродукцииИСМП, ЕстьМаркируемаяПродукция);
	КонецЕсли;
	
	//-- Локализация
	
	ПроверкаТолькоСвоихОрдеров = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров()
		И Не Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуЧужихОрдеров(); 
	ПроверкаТолькоЧужихОрдеров = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуЧужихОрдеров()
		И Не Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров();
	ПроверкаВсехОрдеров = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуЧужихОрдеров()
		И Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров();
	
	УстановитьУсловноеОформление();	
	
	Если УказаниеСерий Тогда
		ЕстьОшибкиУказанияСерий();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОткрытьМенюПоСтатусу();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			
			// Преобразуем предварительно к ожидаемому формату
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			ПараметрыКарточкаТовара = НайтиТоварСервер(Штрихкод);
			
			Если НЕ ЗначениеЗаполнено(ПараметрыКарточкаТовара.Номенклатура) Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru = 'Не найдена номенклатура со штрихкодом: %1';
											|en = 'Item with a barcode is not found: %1'"), Штрихкод);
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				
				Возврат;
			КонецЕсли;
			
			Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Номенклатура", ПараметрыКарточкаТовара.Номенклатура);
			ПараметрыФормы.Вставить("Характеристика", ПараметрыКарточкаТовара.Характеристика);
			ПараметрыФормы.Вставить("Упаковка", ПараметрыКарточкаТовара.Упаковка);
			ПараметрыФормы.Вставить("Количество", ПараметрыКарточкаТовара.Количество);
			Если ЭтоОтбор() Тогда 
				ПараметрыФормы.Вставить("Режим", "ОтборИзЯчейкиОтгрузка");
			ИначеЕсли ЭтоПроверка() Тогда
				ПараметрыФормы.Вставить("Режим", "ПересчетОтгрузка"); 
			ИначеЕсли УказаниеСерий Тогда
				ПараметрыФормы.Вставить("Режим", "УказаниеСерий"); 
			ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен") 
				И Не ИспользоватьСтатусыРасходныхОрдеров Тогда 
				ПараметрыФормы.Вставить("Режим", "Отгрузка");
			Иначе
				ПараметрыФормы.Вставить("Режим", "Просмотр"); 
			КонецЕсли;
			ПараметрыФормы.Вставить("НомерСтроки", 0);
			ПараметрыФормы.Вставить("Склад", Склад);
			ПараметрыФормы.Вставить("Помещение", Помещение);
			ПараметрыФормы.Вставить("ЭтоСканирование", Истина);
			ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
			ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара", ПараметрыФормы,
				ЭтотОбъект,,,,Описание,
				РежимОткрытияОкнаФормы.Независимый);
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Конец ПодключаемоеОборудование
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ОформитьМеню("КОтгрузке");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

&НаКлиенте
Процедура ПомещениеПриИзменении(Элемент)
	
	ЗаполнитьЗонуПриемки();
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрТоварыПриИзменении(Элемент)
	
	ФильтрТоварыОтобразитьТаблицу();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура",   Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия",          Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение",     Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка",       Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество",     Элемент.ТекущиеДанные.КоличествоУпаковок);
	ПараметрыФормы.Вставить("НомерСтроки",    ТоварыКОтбору.Индекс(Элементы.Товары.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад",          Склад);
	ПараметрыФормы.Вставить("Помещение",      Помещение);
	ПараметрыФормы.Вставить("Артикул",        Элемент.ТекущиеДанные.Артикул);
	
	Если ЭтоОтбор() Тогда
		ПараметрыФормы.Вставить("Режим", "ОтборИзЯчейкиОтгрузка");
	ИначеЕсли УказаниеСерий Тогда
		ПараметрыФормы.Вставить("Режим", ?(ЗначениеЗаполнено(Элемент.ТекущиеДанные.Серия), 
			"РедактированиеСерий", "УказаниеСерий"));
	Иначе
		ПараметрыФормы.Вставить("Режим", "Просмотр");
	КонецЕсли;
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара", ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыКПроверке

&НаКлиенте
Процедура ТоварыКПроверкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковокФакт);
	Если ЭтоПроверка() Тогда
		ПараметрыФормы.Вставить("Режим", ?(ЗначениеЗаполнено(Элемент.ТекущиеДанные.Серия),
			"РедактированиеПересчетОтгрузка", "РедактированиеПересчетОтгрузкаБезСерии"));
	Иначе
		ПараметрыФормы.Вставить("Режим", "Просмотр");
	КонецЕсли;
	ПараметрыФормы.Вставить("НомерСтроки", ТоварыКПроверке.Индекс(Элементы.ТоварыКПроверке.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);
    ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара", ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыКОтгрузке

&НаКлиенте
Процедура ТоварыКОтгрузкеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен") 
		И Не ИспользоватьСтатусыРасходныхОрдеров Тогда
		ПараметрыФормы.Вставить("Режим", "РедактированиеОтгрузка");
	Иначе
		ПараметрыФормы.Вставить("Режим", "Просмотр"); 
	КонецЕсли;
	ПараметрыФормы.Вставить("НомерСтроки", ТоварыКОтгрузке.Индекс(Элементы.ТоварыКОтгрузке.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара", ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиВРазделКОтбору(Команда)
	
	ОформитьМеню("КОтбору");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделКПроверке(Команда)
	
	ОформитьМеню("КПроверке");
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделКОтгрузке(Команда)
	
	ОформитьМеню("КОтгрузке");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьОтбор(Команда)
	
	Если ЕстьОшибкиЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	Если ТоварыКПроверке.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не возможно закончить отбор, пока не отобрано ни одного товара.';
							|en = 'Cannot finish picking until at least one item is selected.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	
	Если ПроверкаТолькоСвоихОрдеров Или ПроверкаВсехОрдеров Тогда
		
		СписокЗначений.Добавить("Проверить", НСтр("ru = 'Перейти к проверке ордера';
													|en = 'Go to note check'"));
		СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить работу с ордером';
													|en = 'Close the note'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить отбор';
													|en = 'Continue picking'")); 
		Если ТоварыКОтбору.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Товары, оставшиеся на вкладке ""К отбору"" не будут отгружены.
			|После завершения отбора перейти к проверке ордера или завершить работу с текущим ордером?';
			|en = 'The goods left on the ""Pick list"" tab will not be shipped.
			|After picking is completed, do you want to proceed to checking the goods issue or close the current goods issue?'");
		Иначе  
			ТекстСообщения = НСтр("ru = 'После завершения отбора перейти к проверке ордера или завершить работу с текущим ордером?';
									|en = 'After picking is completed, do you want to proceed to checking the goods issue or close the current goods issue?'");
		КонецЕсли;
		
	Иначе
		
		СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить';
													|en = 'Exit'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить';
													|en = 'Continue'"));
		Если ТоварыКОтбору.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Товары, оставшиеся на вкладке ""К отбору"" не будут отгружены. Завершить отбор?';
									|en = 'The goods left on the ""Pick list"" tab will not be shipped. Finish picking?'");
		Иначе  
			ТекстСообщения = НСтр("ru = 'Завершить отбор?';
									|en = 'Finish picking job?'");
		КонецЕсли;
		
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИмяПроцесса", "ЗавершитьОтбор");
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0); 

КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьПроверку(Команда)
	
	Если ЕстьОшибкиЗаполнения() Тогда
		Возврат;
	КонецЕсли;  
	
	РезультатПроверки = ЕстьНедоборИзлишекОшибкаСерии(); 
	
	Если РезультатПроверки.ОшибкаСерии Тогда 
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПроверки", ЭтотОбъект);

	СписокЗначений = Новый СписокЗначений();
	
	Если РезультатПроверки.Недобор Тогда 
		
		СписокЗначений.Добавить("Отобрать", НСтр("ru = 'Добрать товар';
												|en = 'Add goods'"));
		СписокЗначений.Добавить("Отгрузить", НСтр("ru = 'Уменьшить количество в ордере';
													|en = 'Reduce quantity in the note'"));
		СписокЗначений.Добавить("ОтгрузитьБезПроверки", НСтр("ru = 'Завершить без проверки';
															|en = 'Complete without check'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить проверку';
													|en = 'Continue checking'")); 
		Если РезультатПроверки.Излишек Тогда
			ТекстСообщения = НСтр("ru = 'Проверенного товара больше, чем требуется отгрузить в ордере. Товар необходимо оставить в зоне отгрузки.
			|Не все из отобранных товаров найдены или проверены.';
			|en = 'More goods are checked than required to be shipped under the goods issue. Leave the goods in the outbound area.
			|Not all the picked goods are found or checked.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Не все из отобранных товаров найдены или проверены.';
									|en = 'Not all the picked goods are found or checked.'");
		КонецЕсли;
		
		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
		
	ИначеЕсли РезультатПроверки.Излишек Тогда   
		
		СписокЗначений.Добавить("ИзлишекОтгрузить", НСтр("ru = 'Перейти к отгрузке';
														|en = 'Go to shipment'"));
		СписокЗначений.Добавить("ИзлишекЗавершить", НСтр("ru = 'Завершить работу с ордером';
														|en = 'Close the note'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить проверку';
													|en = 'Continue checking'"));
		ТекстСообщения = НСтр("ru = 'Проверенного товара больше, чем требуется отгрузить в ордере. Товар необходимо оставить в зоне отгрузки. 
		|
		|После завершения проверки перейти к отгрузке ордера или завершить работу с текущим ордером?';
		|en = 'More goods are checked than required to be shipped under the goods issue. Leave the goods in the outbound area.
		|
		|After the check is completed, do you want to proceed to shipping the goods issue or close the current goods issue?'");

		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0); 
		
	Иначе
		ПослеЗакрытияВопросаПроверки("Отгрузить"); 
	КонецЕсли;
		
КонецПроцедуры 

&НаКлиенте
Процедура ЗакончитьОтгрузку(Команда)
	
	Если ЕстьОшибкиЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Завершить отгрузку?';
							|en = 'Finish shipment?'"); 
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить';
												|en = 'Exit'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить';
												|en = 'Continue'"));
	
	ДополнительныеПараметры = Новый Структура("ИмяПроцесса", "ЗавершитьОтгрузку");
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0); 
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	Описание = Новый ОписаниеОповещения("РезультатСканирования", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоиска",         "ПоискНоменклатуры");
	ПараметрыФормы.Вставить("Склад",             Склад);
	ПараметрыФормы.Вставить("Помещение",         Помещение);
	ПараметрыФормы.Вставить("ДокументСсылка",    Ссылка);
	ПараметрыФормы.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	ПараметрыФормы.Вставить("Организация",       Организация);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура КодыМаркировки(Команда)
	
	Описание = Новый ОписаниеОповещения("ИзменитьШтрихкодыУпаковок", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СписокКодовМаркировки",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОтгрузкуОрдера(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Завершить", НСтр("ru = 'Пометить ордер на удаление';
												|en = 'Mark the note for deletion'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить отгрузку';
												|en = 'Continue the shipment'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОтменитьОтгрузку", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Для отмены отгрузки необходимо пометить на удаление расходный ордер. Вернуться к редактированию будет невозможно. Пометить на удаление?';
									|en = 'To cancel shipment, mark the goods issue document for deletion. It will be impossible to get back to editing. Mark the document for deletion?'"), СписокЗначений, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Информация(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
	СкрытьМеню();
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьКОтборуНеОтгружать(Команда)
	
	ИндексСтроки = ТоварыНеОтгружать.Индекс(Элементы.ТоварыНеОтгружать.ТекущиеДанные);
	ВернутьНеОтгружатьНаСервере(ИндексСтроки, "ТоварыКОтбору");
	
КонецПроцедуры

&НаКлиенте
Процедура ВернутьКОтгрузкеНеОтгружать(Команда)
	
	ИндексСтроки = ТоварыНеОтгружать.Индекс(Элементы.ТоварыНеОтгружать.ТекущиеДанные);
	ВернутьНеОтгружатьНаСервере(ИндексСтроки, "ТоварыКОтгрузке");

КонецПроцедуры

&НаКлиенте
Процедура ВернутьКОтборуПроверка(Команда)
	
	ИндексСтроки = ТоварыКПроверке.Индекс(Элементы.ТоварыКПроверке.ТекущиеДанные);
	ВернутьКОтборуПроверкаНаСервере(ИндексСтроки);
	
КонецПроцедуры  

 &НаКлиенте
Процедура НазначитьМне(Команда) 
	
	НазначитьМнеНаСервере();
	ОткрытьМенюПоСтатусу();
	
КонецПроцедуры
 
&НаКлиенте
Процедура НеОтгружатьКОтбору(Команда)
	
	ИндексСтроки = ТоварыКОтбору.Индекс(Элементы.Товары.ТекущиеДанные);
	НеОтгружатьНаСервере(ИндексСтроки, "ТоварыКОтбору");
	
КонецПроцедуры 

&НаКлиенте
Процедура НеОтгружатьКОтгрузке(Команда)
		
	ИндексСтроки = ТоварыКОтгрузке.Индекс(Элементы.ТоварыКОтгрузке.ТекущиеДанные);
	НеОтгружатьНаСервере(ИндексСтроки, "ТоварыКОтгрузке");

КонецПроцедуры

&НаКлиенте
Процедура ГотовоУказаниеСерий(Команда)
	
	Если ЕстьОшибкиЗаполнения() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьОшибкиУказанияСерий() Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУказаниеСерий", ЭтотОбъект);
		
	СписокЗначений.Добавить("Отобрать", НСтр("ru = 'Перейти к отбору';
											|en = 'Go to filter'"));
	СписокЗначений.Добавить("ОтобратьЗавершить", НСтр("ru = 'Завершить работу с ордером';
														|en = 'Close the note'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить указание серий';
												|en = 'Continue specifying batches'"));
	ТекстСообщения = НСтр("ru = 'После завершения указания серий перейти к отбору ордера или завершить работу с текущим ордером?';
							|en = 'After the batches are specified, do you want to proceed to picking the goods issue or close the current goods issue?'");
		
	ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Если ИспользоватьАдресноеХранение Тогда
		Элементы.ГруппаМенюКПроверке.Видимость = Истина;
	Иначе
		Элементы.ЗонаОтгрузки.Видимость      = Ложь;
		Элементы.ГруппаМенюКПроверке.Видимость       = Ложь;
	КонецЕсли;
	
	Элементы.Помещение.Видимость = ИспользоватьСкладскиеПомещения;
	Элементы.КодыМаркировки.Видимость = ИспользоватьУчетМаркируемойПродукции И ЕстьМаркируемаяПродукция;
	Элементы.ИнформацияОПолучателе.Видимость   = (ТипЗнч(Получатель) = Тип("СправочникСсылка.Партнеры") Или ТипЗнч(Получатель) = Тип("СправочникСсылка.ФизическиеЛица"));	
	
	#Если МобильныйАвтономныйСервер Тогда
		Элементы.ФормаОтменитьОтгрузкуОрдера.Видимость = Ложь;
	#Иначе
		Если Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору 
		И (Константы.РежимФормированияРасходныхОрдеров.Получить() = 
		Перечисления.РежимыФормированияРасходныхОрдеров.Кладовщиком) Тогда
			Элементы.ФормаОтменитьОтгрузкуОрдера.Видимость = Истина;
		Иначе
			Элементы.ФормаОтменитьОтгрузкуОрдера.Видимость = Ложь;
		КонецЕсли;
	#КонецЕсли
	
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ТоварыКПроверкеКоличествоУпаковокРазница.Имя);
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТоварыКПроверке.КоличествоУпаковокРазница");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = 0;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаПроблема);
		
	Элементы.ГруппаМенюКОтбору.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
	Элементы.ГруппаМенюКПроверке.Видимость = ИспользоватьСтатусыРасходныхОрдеров;

	Элементы.ТоварыОсновнаяЯчейка.Видимость = ИспользоватьАдресноеХранениеСправочно;
	
	Элементы.Контролер.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
	Элементы.Отгрузил.Видимость = ИспользоватьСтатусыРасходныхОрдеров;
	
	Элементы.НеОтгружатьКОтгрузке.Видимость = Не ИспользоватьСтатусыРасходныхОрдеров;
	Элементы.ВернутьКОтгрузкеНеОтгружать.Видимость = Не ИспользоватьСтатусыРасходныхОрдеров;

	Элементы.НеОтгружатьКОтбору.Видимость = Не ИспользоватьАдресноеХранение И ИспользоватьСтатусыРасходныхОрдеров; 
	Элементы.ВернутьКОтборуНеОтгружать.Видимость = ЭтоОтбор() И ИспользоватьСтатусыРасходныхОрдеров;
	
	Элементы.ВернутьКОтборуПроверка.Видимость = ЭтоОтбор();
	
	Элементы.ГруппаПереключательНеОтгружать.Видимость = ТоварыНеОтгружать.Количество();
	ФильтрТовары = "Отгружаемые";
	
	УстановитьДоступностьРедактированияОрдера();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатСканирования(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Результат.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Результат.Характеристика);
	Если Результат.Свойство("Серия") Тогда
		ПараметрыФормы.Вставить("Серия", Результат.Серия);
	Иначе
		ПараметрыФормы.Вставить("Серия", ПустаяСерия);
	КонецЕсли;
	ПараметрыФормы.Вставить("Упаковка", Результат.Упаковка);
	Если Результат.Свойство("Количество") Тогда
		ПараметрыФормы.Вставить("Количество", Результат.Количество);
	Иначе
		ПараметрыФормы.Вставить("Количество", 1);
	КонецЕсли;
	Если ЭтоОтбор() Тогда 
		ПараметрыФормы.Вставить("Режим", "ОтборИзЯчейкиОтгрузка");
	ИначеЕсли ЭтоПроверка() Тогда
		ПараметрыФормы.Вставить("Режим", "ПересчетОтгрузка"); 
	ИначеЕсли УказаниеСерий Тогда
		ПараметрыФормы.Вставить("Режим", "УказаниеСерий");
	Иначе
		ПараметрыФормы.Вставить("Режим", "Просмотр"); 
	КонецЕсли;
	ПараметрыФормы.Вставить("НомерСтроки", 0);
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	Если Результат.Свойство("ШтрихкодУпаковки") Тогда
		ПараметрыФормы.Вставить("ШтрихкодУпаковки", Результат.ШтрихкодУпаковки);
	Иначе
		ПараметрыФормы.Вставить("ШтрихкодУпаковки", Неопределено);
	КонецЕсли;
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("ЭтоСканирование", Истина);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ОформитьМеню(ИмяВкладки)
	
	ПоказатьМеню();
	
	Элементы.РамкаКОтгрузке.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаКОтгрузке.ЦветТекста = WebЦвета.ТемноСерый;
	
	Элементы.РамкаКОтбору.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаКОтбору.ЦветТекста = WebЦвета.ТемноСерый;
	
	Элементы.РамкаКПроверке.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаКПроверке.ЦветТекста = WebЦвета.ТемноСерый;
	
	Если ИмяВкладки = "КОтбору" Тогда 
		Элементы.КомандаКОтбору.ЦветТекста = WebЦвета.Черный;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКОтбору;
		Элементы.РамкаКОтбору.Картинка     = БиблиотекаКартинок.РамкаМенюЧерная;
		Элементы.ЗавершитьОтбор.Видимость = ЭтоОтбор();
		Элементы.ЗавершитьПроверку.Видимость = Ложь;
		
		Если ТоварыНеОтгружать.Количество() Тогда
			ЭлементОтгружаемые = Элементы.ФильтрТовары.СписокВыбора.НайтиПоЗначению("Отгружаемые");
			ЭлементОтгружаемые.Представление = НСтр("ru = 'К отбору';
													|en = 'Ready for picking'");
		КонецЕсли;
		
	ИначеЕсли ИмяВкладки = "КПроверке" Тогда  
		Элементы.КомандаКПроверке.ЦветТекста = WebЦвета.Черный;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКПроверке;
		Элементы.РамкаКПроверке.Картинка     = БиблиотекаКартинок.РамкаМенюЧерная;
		Элементы.ЗавершитьПроверку.Видимость = ЭтоПроверка();
		Элементы.ЗавершитьОтбор.Видимость = Ложь;
		ОбновитьПодсказкуКПроверке();
		
		Если ТоварыНеОтгружать.Количество() Тогда
			ЭлементОтгружаемые = Элементы.ФильтрТовары.СписокВыбора.НайтиПоЗначению("Отгружаемые");
			ЭлементОтгружаемые.Представление = НСтр("ru = 'К проверке';
													|en = 'To verify'");
		КонецЕсли;

	ИначеЕсли ИмяВкладки = "КОтгрузке" Тогда
		Элементы.КомандаКОтгрузке.ЦветТекста = WebЦвета.Черный;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКОтгрузке;
		Элементы.РамкаКОтгрузке.Картинка     = БиблиотекаКартинок.РамкаМенюЧерная;
		Элементы.ЗавершитьОтгрузку.Видимость = 
		Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.КОтгрузке")
		Или (Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Отгружен")
		И Не ИспользоватьСтатусыРасходныхОрдеров);
		Элементы.ЗавершитьПроверку.Видимость = Ложь;
		Элементы.ЗавершитьОтбор.Видимость = Ложь;

		Если ТоварыНеОтгружать.Количество() Тогда
			ЭлементОтгружаемые = Элементы.ФильтрТовары.СписокВыбора.НайтиПоЗначению("Отгружаемые");
			ЭлементОтгружаемые.Представление = НСтр("ru = 'К отгрузке';
													|en = 'To ship'");
		КонецЕсли;

	КонецЕсли;
	
	Если ФильтрТовары = "НеОтгружаемые" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНеОтгружать;
	КонецЕсли;
	
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Функция ЭтоОтбор()
	
	Возврат Не ИспользоватьАдресноеХранение И Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	
КонецФункции
		
&НаСервере
Функция ЭтоПроверка()
	
	ПроверкаСвоегоОрдера = ПроверкаТолькоСвоихОрдеров И Исполнитель = Пользователи.ТекущийПользователь();
	ПроверкаЧужогоОрдера = ПроверкаТолькоЧужихОрдеров И Исполнитель <> Пользователи.ТекущийПользователь();
	
	Возврат ((ПроверкаСвоегоОрдера Или ПроверкаЧужогоОрдера Или ПроверкаВсехОрдеров)
		И Статус = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки);
	
КонецФункции

&НаСервере
Процедура СформироватьЗаголовкиКомандМеню()
	
	Элементы.КомандаКОтбору.Заголовок = СтрШаблон(НСтр("ru = 'К отбору %1';
														|en = 'To pick %1'"), ТоварыКОтбору.Количество()); 
	Элементы.КомандаКПроверке.Заголовок = СтрШаблон(НСтр("ru = 'К проверке %1';
														|en = 'To check %1'"), ТоварыКПроверке.Количество());
	Элементы.КомандаКОтгрузке.Заголовок = СтрШаблон(НСтр("ru = 'К отгрузке %1';
														|en = 'To ship %1'"), ТоварыКОтгрузке.Количество());
	
	Если ТоварыНеОтгружать.Количество() Тогда 
		Элементы.ГруппаПереключательНеОтгружать.Видимость = Истина;
		ЭлементНеОтгружаемые = Элементы.ФильтрТовары.СписокВыбора.НайтиПоЗначению("НеОтгружаемые");
		ЭлементНеОтгружаемые.Представление = СтрШаблон(НСтр("ru = 'Не отгружать %1';
															|en = 'Do not ship %1'"), ТоварыНеОтгружать.Количество());
	Иначе
		Элементы.ГруппаПереключательНеОтгружать.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьПодсказкуКПроверке()
	
	План = ТоварыКПроверке.Итог("КоличествоУпаковок");
	Факт = ТоварыКПроверке.Итог("КоличествоУпаковокФакт");
	Разница = План - Факт;
	
	Элементы.ЗаголовокПодсказкаФакт.Заголовок = Факт;
	Элементы.ЗаголовокПодсказкаПлан.Заголовок = План;
	Элементы.ЗаголовокПодсказкаРазница.Заголовок = Разница;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму()
	
	ТоварыКОтбору.Очистить();
	ТоварыКПроверке.Очистить();
	ТоварыКОтгрузке.Очистить();
	ТоварыНеОтгружать.Очистить();
	
	ДанныеСтруктура = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "Статус, Склад, Помещение, ДатаОтгрузки");
	
	СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(ДанныеСтруктура.Склад);
	ИспользоватьАдресноеХранение            = СвойстваСклада.ИспользоватьАдресноеХранение;
	ИспользоватьСкладскиеПомещения          = СвойстваСклада.ИспользоватьСкладскиеПомещения;
	ИспользоватьСтатусыРасходныхОрдеров		= СвойстваСклада.ИспользоватьСтатусыРасходныхОрдеров;
	Если ИспользоватьСкладскиеПомещения Тогда
		СтруктураПомещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДанныеСтруктура.Помещение,
			"ИспользоватьАдресноеХранение, ИспользоватьАдресноеХранениеСправочно");
		ИспользоватьАдресноеХранение = СтруктураПомещения.ИспользоватьАдресноеХранение;
		ИспользоватьАдресноеХранениеСправочно = СтруктураПомещения.ИспользоватьАдресноеХранениеСправочно;
		ДатаНачалаИспользованияСкладскихПомещений = СвойстваСклада.ДатаНачалаИспользованияСкладскихПомещений;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДанныеДокументаТаблицыТоваров();
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ИспользоватьАдресноеХранение", ИспользоватьАдресноеХранение);
	Запрос.УстановитьПараметр("Склад", ДанныеСтруктура.Склад);

	Если ИспользоватьАдресноеХранениеСправочно Тогда 
		ДатаНачалаИспользованияСкладскихПомещений = СвойстваСклада.ДатаНачалаИспользованияСкладскихПомещений;
		ДатаОтгрузки = ДанныеСтруктура.ДатаОтгрузки;
		Запрос.УстановитьПараметр("ИспользоватьСкладскиеПомещения", ИспользоватьСкладскиеПомещения);
		Запрос.УстановитьПараметр("ДатаОтгрузки", ДатаОтгрузки);
		Запрос.УстановитьПараметр("ДатаНачалаИспользованияСкладскихПомещений", ДатаНачалаИспользованияСкладскихПомещений);
		Запрос.УстановитьПараметр("Помещение", ДанныеСтруктура.Помещение);
	КонецЕсли;
	
	СтатусВПроверке = (ДанныеСтруктура.Статус = Перечисления.СтатусыРасходныхОрдеров.КПроверке
		Или ДанныеСтруктура.Статус = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки); 
		
	Запрос.УстановитьПараметр("ЭтоПроверка", СтатусВПроверке);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса[0].Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);
	ТоварыПоРаспоряжениям.Загрузить(ВыборкаДетальныеЗаписи.ТаблицаТоварыПоРаспоряжениям.Выгрузить());
	
	ВыборкаТаблицаКОтгрузке = РезультатЗапроса[2].Выгрузить();
	ВыборкаТаблицаНеОтгружать = РезультатЗапроса[3].Выгрузить();
			
	Если Статус = Перечисления.СтатусыРасходныхОрдеров.Подготовлен 
		Или Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору Тогда
		УказаниеСерий = Обработки.МобильноеРабочееМестоКладовщика.ПроверитьСтатусУказанияСерийПриПланированииОтбора(Ссылка, 
			Склад);
	КонецЕсли; 
		
	Если ИспользоватьАдресноеХранение Тогда 
		
		ШтрихкодыУпаковок.Загрузить(ВыборкаДетальныеЗаписи.РасходныйОрдерШтрихкодыУпаковок.Выгрузить());
	
		Если СтатусВПроверке Или Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору Тогда 			
			ТоварыКПроверке.Загрузить(РезультатЗапроса[13].Выгрузить());			
		КонецЕсли;
		
		Если УказаниеСерий Тогда 
			ВыборкаТаблицаОтобрать = РезультатЗапроса[4].Выгрузить();
			ТоварыКОтбору.Загрузить(ВыборкаТаблицаОтобрать);
		Иначе 
			ВыборкаТаблицаОстаткиКОтбору = РезультатЗапроса[7].Выгрузить();
			ТоварыКОтбору.Загрузить(ВыборкаТаблицаОстаткиКОтбору);
		КонецЕсли;
		
		ТоварыКОтгрузке.Загрузить(ВыборкаТаблицаКОтгрузке);
		ТоварыНеОтгружать.Загрузить(ВыборкаТаблицаНеОтгружать);
			
	Иначе
		
		ВыборкаТаблицаОтобрать = РезультатЗапроса[4].Выгрузить();
			
		Если СтатусВПроверке Тогда
			ТоварыКПроверке.Загрузить(ВыборкаТаблицаОтобрать);
		Иначе
			ТоварыКОтбору.Загрузить(ВыборкаТаблицаОтобрать);
		КонецЕсли;
		
		ТоварыКОтгрузке.Загрузить(ВыборкаТаблицаКОтгрузке);
		ТоварыНеОтгружать.Загрузить(ВыборкаТаблицаНеОтгружать);
		
		ШтрихкодыУпаковок.Загрузить(ВыборкаДетальныеЗаписи.РасходныйОрдерШтрихкодыУпаковок.Выгрузить());
		
	КонецЕсли;
		
	ИнформацияОДокументе = СтрШаблон(НСтр("ru = '№ %1 от %2, %3';
											|en = '%1 dated %2, %3'"), 
		ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер), Формат(Дата, НСтр("ru = 'ДФ=''дд.ММ.гг ЧЧ:мм''';
																				|en = 'DF=''MM/dd/yy hh:mm tt'''")), Статус);
		
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаДанныеДокументаТаблицыТоваров()
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	РасходныйОрдерНаТовары.Склад КАК Склад,
		|	РасходныйОрдерНаТовары.Комментарий КАК Комментарий,
		|	РасходныйОрдерНаТовары.Помещение КАК Помещение,
		|	РасходныйОрдерНаТовары.ЗонаОтгрузки КАК ЗонаОтгрузки,
		|	РасходныйОрдерНаТовары.Получатель КАК Получатель,
		|	РасходныйОрдерНаТовары.Номер КАК Номер,
		|	РасходныйОрдерНаТовары.Дата КАК Дата,
		|	РасходныйОрдерНаТовары.Статус КАК Статус,
		|	РасходныйОрдерНаТовары.Отгрузил КАК Отгрузил,
		|	РасходныйОрдерНаТовары.Контролер КАК Контролер,
		|	РасходныйОрдерНаТовары.Ответственный КАК Исполнитель,
		|	РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям.(
		|		Номенклатура КАК Номенклатура,
		|		Номенклатура.Артикул КАК Артикул,
		|		Номенклатура.ЕдиницаИзмерения КАК Упаковка,
		|		Характеристика КАК Характеристика,
		|		Серия КАК Серия,
		|		Назначение КАК Назначение,
		|		Количество КАК Количество,
		|		СтатусУказанияСерий КАК СтатусУказанияСерий,
		|		Распоряжение КАК Распоряжение
		|	) КАК ТаблицаТоварыПоРаспоряжениям,
		|	РасходныйОрдерНаТовары.ШтрихкодыУпаковок.(
		|		ШтрихкодУпаковки КАК ШтрихкодУпаковки,
		|		ШтрихкодУпаковки.Номенклатура КАК Номенклатура,
		|		ШтрихкодУпаковки.Характеристика КАК Характеристика
		|	) КАК РасходныйОрдерШтрихкодыУпаковок
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары КАК РасходныйОрдерНаТовары
		|ГДЕ
		|	РасходныйОрдерНаТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтгружаемыеТовары.Номенклатура КАК Номенклатура,
		|	ОтгружаемыеТовары.Номенклатура.Артикул КАК Артикул,
		|	ОтгружаемыеТовары.Характеристика КАК Характеристика,
		|	ОтгружаемыеТовары.Назначение КАК Назначение,
		|	ОтгружаемыеТовары.Серия КАК Серия,
		|	ОтгружаемыеТовары.Количество КАК Количество,
		|	ОтгружаемыеТовары.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ОтгружаемыеТовары.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ОтгружаемыеТовары.Действие КАК Действие,
		|	ВЫБОР
		|		КОГДА ОтгружаемыеТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
		|				И НЕ &ИспользоватьАдресноеХранение
		|			ТОГДА ОтгружаемыеТовары.Номенклатура.ЕдиницаИзмерения
		|		ИНАЧЕ ОтгружаемыеТовары.Упаковка
		|	КОНЕЦ КАК Упаковка,
		|	ОтгружаемыеТовары.ЭтоУпаковочныйЛист КАК ЭтоУпаковочныйЛист
		|ПОМЕСТИТЬ ОтгружаемыеТовары
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
		|ГДЕ
		|	ОтгружаемыеТовары.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаКОтгрузке.Номенклатура КАК Номенклатура,
		|	ТаблицаКОтгрузке.Артикул КАК Артикул,
		|	ТаблицаКОтгрузке.Характеристика КАК Характеристика,
		|	ТаблицаКОтгрузке.Упаковка КАК Упаковка,
		|	ТаблицаКОтгрузке.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ТаблицаКОтгрузке.Серия КАК Серия,
		|	ТаблицаКОтгрузке.Назначение КАК Назначение,
		|	ТаблицаКОтгрузке.Количество КАК Количество,
		|	ТаблицаКОтгрузке.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаКОтгрузке.Действие КАК Действие
		|ИЗ
		|	ОтгружаемыеТовары КАК ТаблицаКОтгрузке
		|ГДЕ
		|	ТаблицаКОтгрузке.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНеОтгружать.Номенклатура КАК Номенклатура,
		|	ТаблицаНеОтгружать.Артикул КАК Артикул,
		|	ТаблицаНеОтгружать.Характеристика КАК Характеристика,
		|	ТаблицаНеОтгружать.Упаковка КАК Упаковка,
		|	ТаблицаНеОтгружать.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ТаблицаНеОтгружать.Серия КАК Серия,
		|	ТаблицаНеОтгружать.Назначение КАК Назначение,
		|	ТаблицаНеОтгружать.Количество КАК Количество,
		|	ТаблицаНеОтгружать.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаНеОтгружать.Действие КАК Действие
		|ИЗ
		|	ОтгружаемыеТовары КАК ТаблицаНеОтгружать
		|ГДЕ
		|	ТаблицаНеОтгружать.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать)";
	
	Если Не ИспользоватьАдресноеХранениеСправочно Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОтобрать.Номенклатура КАК Номенклатура,
		|	ТаблицаОтобрать.Артикул КАК Артикул,
		|	ТаблицаОтобрать.Характеристика КАК Характеристика,
		|	ТаблицаОтобрать.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЭтоПроверка И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора 
		|		ТОГДА 1
    	|		ИНАЧЕ ТаблицаОтобрать.СтатусУказанияСерий
    	|	КОНЕЦ КАК СтатусУказанияСерий,		
		|	ТаблицаОтобрать.Серия КАК Серия,
		|	ТаблицаОтобрать.Назначение КАК Назначение,
		|	ТаблицаОтобрать.Количество КАК Количество,
		|	ТаблицаОтобрать.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаОтобрать.КоличествоУпаковок КАК КоличествоУпаковокРазница,
		|	ТаблицаОтобрать.Действие КАК Действие
		|ИЗ
		|	ОтгружаемыеТовары КАК ТаблицаОтобрать 
		|       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
    	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
    	|			ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
    	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
    	|			И ТаблицаОтобрать.Номенклатура.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка    	
		|ГДЕ
		|	ТаблицаОтобрать.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать)";
	Иначе
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОтобрать.Номенклатура КАК Номенклатура,
		|	ТаблицаОтобрать.Артикул КАК Артикул,
		|	ТаблицаОтобрать.Характеристика КАК Характеристика,
		|	ТаблицаОтобрать.Упаковка КАК Упаковка,
		|	ВЫБОР
		|		КОГДА &ЭтоПроверка И ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора
		|		ТОГДА 1
    	|		ИНАЧЕ ТаблицаОтобрать.СтатусУказанияСерий
		|	ТаблицаОтобрать.Серия КАК Серия,
		|	ТаблицаОтобрать.Назначение КАК Назначение,
		|	ТаблицаОтобрать.Количество КАК Количество,
		|	ТаблицаОтобрать.КоличествоУпаковок КАК КоличествоУпаковок,
		|	ТаблицаОтобрать.КоличествоУпаковок КАК КоличествоУпаковокРазница,
		|	ТаблицаОтобрать.Действие КАК Действие,
		|	ЕСТЬNULL(ОсновныеЯчейки.Ячейка.Код, """") КАК ОсновнаяЯчейка
		|ИЗ
		|	ОтгружаемыеТовары КАК ТаблицаОтобрать
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК ОсновныеЯчейки
		|		ПО ТаблицаОтобрать.Номенклатура = ОсновныеЯчейки.Номенклатура
		|			И ОсновныеЯчейки.ОсновнаяЯчейка
		|			И ОсновныеЯчейки.Склад = &Склад 
		|			И ВЫБОР
		|				КОГДА &ИспользоватьСкладскиеПомещения
		|						И &ДатаОтгрузки >= &ДатаНачалаИспользованияСкладскихПомещений
		|					ТОГДА ОсновныеЯчейки.Помещение = &Помещение
		|				ИНАЧЕ ОсновныеЯчейки.Помещение = ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)
		|			КОНЕЦ
		|       ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
    	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
    	|			ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
    	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
    	|			И ТаблицаОтобрать.Номенклатура.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
		|ГДЕ
		|	ТаблицаОтобрать.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать)";
	КонецЕсли;
	
	Если ИспользоватьАдресноеХранение Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтборТоваров.Номенклатура КАК Номенклатура,
		|	ОтборТоваров.Ссылка КАК Ссылка,
		|	ОтборТоваров.Характеристика КАК Характеристика,
		|	ОтборТоваров.Упаковка КАК Упаковка,
		|	ОтборТоваров.Серия КАК Серия,
		|	ОтборТоваров.СтатусУказанияСерий КАК СтатусУказанияСерий,
		|	ОтборТоваров.Назначение КАК Назначение,
		|	ОтборТоваров.КоличествоОтобрано КАК КоличествоОтобрано,
		|	ОтборТоваров.КоличествоУпаковокОтобрано КАК КоличествоУпаковокОтобрано
		|ПОМЕСТИТЬ ТаблицаОтобрано
		|ИЗ
		|	Документ.ОтборРазмещениеТоваров.ТоварыОтбор КАК ОтборТоваров
		|ГДЕ
		|	ОтборТоваров.Ссылка.Распоряжение = &Ссылка 
		|	И ОтборТоваров.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Отбор)
		|	И ОтборТоваров.Ссылка.Проведен
		|	И (ОтборТоваров.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок)
		|			ИЛИ ОтборТоваров.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОтборовРазмещенийТоваров.ВыполненоСОшибками))
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Назначение,
		|	ВложенныйЗапрос.Серия, 
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
		|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ТаблицаОстаткиКОтбору
		|ИЗ
		|	(ВЫБРАТЬ
		|		ТаблицаОтобрано.Номенклатура КАК Номенклатура,
		|		ТаблицаОтобрано.Характеристика КАК Характеристика,
		|		ТаблицаОтобрано.Назначение КАК Назначение,
		|		ТаблицаОтобрано.Серия КАК Серия,
		|		-ТаблицаОтобрано.КоличествоОтобрано КАК Количество,
		|		ТаблицаОтобрано.КоличествоУпаковокОтобрано КАК КоличествоУпаковок
		|	ИЗ
		|		ТаблицаОтобрано КАК ТаблицаОтобрано
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ОтгружаемыеТовары.Номенклатура, 
		|		ОтгружаемыеТовары.Характеристика,
		|		ОтгружаемыеТовары.Назначение,
		|		ВЫБОР
		|			КОГДА ОтгружаемыеТовары.СтатусУказанияСерий В (1, 2)
		|				ТОГДА ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
		|			ИНАЧЕ ОтгружаемыеТовары.Серия
		|		КОНЕЦ КАК Серия,
		|		ОтгружаемыеТовары.Количество,
		|		0
		|	ИЗ
		|		ОтгружаемыеТовары КАК ОтгружаемыеТовары
		|	ГДЕ
		|		ОтгружаемыеТовары.Действие <> ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать) 
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ИзлишкиНедостачи.Номенклатура,
		|		ИзлишкиНедостачи.Характеристика,
		|		ИзлишкиНедостачи.Назначение,
		|		ИзлишкиНедостачи.Серия,
		|		ВЫБОР
		|			КОГДА ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
		|				ТОГДА ИзлишкиНедостачи.Количество
		|			ИНАЧЕ -ИзлишкиНедостачи.Количество
		|		КОНЕЦ,
		|		0
		|	ИЗ
		|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК ИзлишкиНедостачи
		|	ГДЕ
		|		ИзлишкиНедостачи.Ссылка.Проведен
		|		И ИзлишкиНедостачи.Ссылка.Ордер = &Ссылка
		|		И (ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
		|				ИЛИ ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек))) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО 
		|	ВложенныйЗапрос.Серия,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Характеристика,
		|	ВложенныйЗапрос.Назначение
		|;
		| 
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОстаткиКОтбору.Номенклатура,
		|	ТаблицаОстаткиКОтбору.Номенклатура.Артикул КАК Артикул,
		|	ТаблицаОстаткиКОтбору.Характеристика,
		|	ТаблицаОстаткиКОтбору.Назначение,
		|	ТаблицаОстаткиКОтбору.Количество,
		|	ТаблицаОстаткиКОтбору.Количество КАК КоличествоУпаковок,
		|	ТаблицаОстаткиКОтбору.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
		|	ТаблицаОстаткиКОтбору.Серия,
		|	ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать) КАК Действие
		|ИЗ
		|	ТаблицаОстаткиКОтбору КАК ТаблицаОстаткиКОтбору
		|	ГДЕ
		|		ТаблицаОстаткиКОтбору.Количество > 0";
		
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаТаблицаТоваровПоОтобранным();
	КонецЕсли;

	Возврат ТекстЗапроса;
	
КонецФункции

&НаСервере
Функция ТекстЗапросаТаблицаТоваровПоОтобранным()
	
	ТекстЗапроса = "
	|; 
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТараПоРаспоряжениям.Номенклатура КАК Номенклатура,
	|	ТараПоРаспоряжениям.Характеристика КАК Характеристика,
	|	СУММА(ТараПоРаспоряжениям.Количество) КАК Количество
	|ПОМЕСТИТЬ ТараПоРаспоряжениям
	|ИЗ
	|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК ТараПоРаспоряжениям
	|ГДЕ
	|	ТараПоРаспоряжениям.Ссылка = &Ссылка
	|		
	|	И ТараПоРаспоряжениям.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.МногооборотнаяТара)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТараПоРаспоряжениям.Номенклатура,
	|	ТараПоРаспоряжениям.Характеристика,
	|	ТараПоРаспоряжениям.Ссылка
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Отбор КАК Отбор,
	|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
	|	ВложенныйЗапрос.Номенклатура.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ВложенныйЗапрос.Характеристика КАК Характеристика,
	|	ВложенныйЗапрос.Назначение КАК Назначение,
	|	ВложенныйЗапрос.Упаковка КАК Упаковка,
	|	ВложенныйЗапрос.Серия КАК Серия,
	|	СУММА(ВложенныйЗапрос.СтатусУказанияСерий) КАК СтатусУказанияСерий,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ врТоварыСРасхождениямиПоУпаковкам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОтбор.Ссылка КАК Отбор,
	|		ТоварыОтбор.Номенклатура КАК Номенклатура,
	|		ТоварыОтбор.Характеристика КАК Характеристика,
	|		ТоварыОтбор.Назначение КАК Назначение,
	|		ТоварыОтбор.Упаковка КАК Упаковка,
	|		ТоварыОтбор.Серия КАК Серия,
	|		ТоварыОтбор.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|		ТоварыОтбор.КоличествоОтобрано КАК Количество,
	|		ТоварыОтбор.КоличествоУпаковокОтобрано КАК КоличествоУпаковок
	|	ИЗ
	|		ТаблицаОтобрано КАК ТоварыОтбор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		ИзлишкиНедостачи.Номенклатура,
	|		ИзлишкиНедостачи.Характеристика,
	|		ИзлишкиНедостачи.Назначение,
	|		ИзлишкиНедостачи.Упаковка,
	|		ИзлишкиНедостачи.Серия,
	|		0,
	|		ВЫБОР
	|			КОГДА ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|				ТОГДА -ИзлишкиНедостачи.Количество
	|			ИНАЧЕ ИзлишкиНедостачи.Количество
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|				ТОГДА -ИзлишкиНедостачи.КоличествоУпаковок
	|			ИНАЧЕ ИзлишкиНедостачи.КоличествоУпаковок
	|		КОНЕЦ
	|	ИЗ
	|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК ИзлишкиНедостачи
	|	ГДЕ
	|		ИзлишкиНедостачи.Ссылка.Проведен
	|		И ИзлишкиНедостачи.Ссылка.Ордер = &Ссылка
	|		И (ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|				ИЛИ ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек))
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		ИзлишкиНедостачи.Номенклатура,
	|		ИзлишкиНедостачи.Характеристика,
	|		ИзлишкиНедостачи.Назначение,
	|		ИзлишкиНедостачи.Упаковка,
	|		ИзлишкиНедостачи.Серия,
	|		0,
	|		-ИзлишкиНедостачи.Количество,
	|		-ИзлишкиНедостачи.КоличествоУпаковок
	|	ИЗ
	|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК ИзлишкиНедостачи
	|	ГДЕ
	|		ИзлишкиНедостачи.Ссылка.Проведен
	|		И ИзлишкиНедостачи.Ссылка.Ордер = &Ссылка
	|		И ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ПеренестиВДругойОрдер)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		ИзлишкиНедостачи.Номенклатура,
	|		ИзлишкиНедостачи.Характеристика,
	|		ИзлишкиНедостачи.Назначение,
	|		ИзлишкиНедостачи.Упаковка,
	|		ИзлишкиНедостачи.Серия,
	|		0,
	|		ИзлишкиНедостачи.Количество,
	|		ИзлишкиНедостачи.КоличествоУпаковок
	|	ИЗ
	|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК ИзлишкиНедостачи
	|	ГДЕ
	|		ИзлишкиНедостачи.Ссылка.Проведен
	|		И ИзлишкиНедостачи.Ссылка.ОрдерПолучатель = &Ссылка
	|		И ИзлишкиНедостачи.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ПеренестиВДругойОрдер)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		ОтгружаемыеТовары.Номенклатура,
	|		ОтгружаемыеТовары.Характеристика,
	|		ОтгружаемыеТовары.Назначение,
	|		ОтгружаемыеТовары.Упаковка,
	|		ОтгружаемыеТовары.Серия,
	|		0,
	|		-ОтгружаемыеТовары.Количество,
	|		-ОтгружаемыеТовары.КоличествоУпаковок
	|	ИЗ
	|		ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|	ГДЕ
	|		ОтгружаемыеТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить), ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать))
	|		И НЕ ОтгружаемыеТовары.ЭтоУпаковочныйЛист) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Номенклатура.ТипНоменклатуры,
	|	ВложенныйЗапрос.Отбор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСРасхождениямиПоУпаковкам.Номенклатура КАК Номенклатура,
	|	ТоварыСРасхождениямиПоУпаковкам.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТоварыСРасхождениямиПоУпаковкам.Характеристика КАК Характеристика,
	|	ТоварыСРасхождениямиПоУпаковкам.Назначение КАК Назначение,
	|	ТоварыСРасхождениямиПоУпаковкам.Упаковка КАК Упаковка,
	|	ТоварыСРасхождениямиПоУпаковкам.Серия КАК Серия, 
	|	ТоварыСРасхождениямиПоУпаковкам.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	СУММА(ТоварыСРасхождениямиПоУпаковкам.Количество) КАК Количество,
	|	СУММА(ТоварыСРасхождениямиПоУпаковкам.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ ТоварыСРасхождениямиПоУпаковкам
	|ИЗ
	|	врТоварыСРасхождениямиПоУпаковкам КАК ТоварыСРасхождениямиПоУпаковкам
	|
	|СГРУППИРОВАТЬ ПО
	|	ТоварыСРасхождениямиПоУпаковкам.Упаковка,
	|	ТоварыСРасхождениямиПоУпаковкам.Номенклатура,
	|	ТоварыСРасхождениямиПоУпаковкам.ТипНоменклатуры,
	|	ТоварыСРасхождениямиПоУпаковкам.Назначение,
	|	ТоварыСРасхождениямиПоУпаковкам.Характеристика,
	|	ТоварыСРасхождениямиПоУпаковкам.Серия,
	|	ТоварыСРасхождениямиПоУпаковкам.СтатусУказанияСерий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасхожденияПоУпаковкам.Номенклатура КАК Номенклатура,
	|	РасхожденияПоУпаковкам.Характеристика КАК Характеристика,
	|	РасхожденияПоУпаковкам.Назначение КАК Назначение,
	|	РасхожденияПоУпаковкам.Серия КАК Серия,
	|	СУММА(РасхожденияПоУпаковкам.Количество) КАК Количество
	|ПОМЕСТИТЬ СторноРасхожденийПоУпаковкам
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыЕстьВОтборе.Номенклатура КАК Номенклатура,
	|		ТоварыЕстьВОтборе.Характеристика КАК Характеристика,
	|		ТоварыЕстьВОтборе.Назначение КАК Назначение,
	|		ТоварыЕстьВОтборе.Серия КАК Серия,
	|		ТоварыЕстьВОтборе.Количество КАК Количество
	|	ИЗ
	|		ТоварыСРасхождениямиПоУпаковкам КАК ТоварыЕстьВОтборе
	|	ГДЕ
	|		ТоварыЕстьВОтборе.Количество > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоваровНетВОтборе.Номенклатура,
	|		ТоваровНетВОтборе.Характеристика,
	|		ТоваровНетВОтборе.Назначение,
	|		ТоваровНетВОтборе.Серия,
	|		ТоваровНетВОтборе.Количество
	|	ИЗ
	|		ТоварыСРасхождениямиПоУпаковкам КАК ТоваровНетВОтборе
	|	ГДЕ
	|		ТоваровНетВОтборе.Количество < 0) КАК РасхожденияПоУпаковкам
	|
	|СГРУППИРОВАТЬ ПО
	|	РасхожденияПоУпаковкам.Назначение,
	|	РасхожденияПоУпаковкам.Номенклатура,
	|	РасхожденияПоУпаковкам.Характеристика,
	|	РасхожденияПоУпаковкам.Серия
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасхожденияПоУпаковкам.Количество) = 0 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыСРасхождениямиПоУпаковкам.Номенклатура КАК Номенклатура,
	|	ТоварыСРасхождениямиПоУпаковкам.ТипНоменклатуры КАК ТипНоменклатуры,
	|	ТоварыСРасхождениямиПоУпаковкам.Характеристика КАК Характеристика,
	|	ТоварыСРасхождениямиПоУпаковкам.Назначение КАК Назначение,
	|	ТоварыСРасхождениямиПоУпаковкам.Упаковка КАК Упаковка,
	|	ТоварыСРасхождениямиПоУпаковкам.Серия КАК Серия, 
	|	ТоварыСРасхождениямиПоУпаковкам.СтатусУказанияСерий КАК СтатусУказанияСерий,
	|	ТоварыСРасхождениямиПоУпаковкам.Количество КАК Количество,
	|	ТоварыСРасхождениямиПоУпаковкам.КоличествоУпаковок КАК КоличествоУпаковок
	|ПОМЕСТИТЬ РезультатОтбора
	|ИЗ
	|	ТоварыСРасхождениямиПоУпаковкам КАК ТоварыСРасхождениямиПоУпаковкам
	|		ЛЕВОЕ СОЕДИНЕНИЕ СторноРасхожденийПоУпаковкам КАК СторноРасхожденийПоУпаковкам
	|		ПО ТоварыСРасхождениямиПоУпаковкам.Номенклатура = СторноРасхожденийПоУпаковкам.Номенклатура
	|			И ТоварыСРасхождениямиПоУпаковкам.Характеристика = СторноРасхожденийПоУпаковкам.Характеристика
	|			И ТоварыСРасхождениямиПоУпаковкам.Назначение = СторноРасхожденийПоУпаковкам.Назначение
	|			И ТоварыСРасхождениямиПоУпаковкам.Серия = СторноРасхожденийПоУпаковкам.Серия
	|ГДЕ
	|	СторноРасхожденийПоУпаковкам.Номенклатура ЕСТЬ NULL
	|	И ТоварыСРасхождениямиПоУпаковкам.Количество > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РезультатОтбора.Номенклатура КАК Номенклатура,
	|	РезультатОтбора.Номенклатура.Артикул КАК Артикул,
	|	РезультатОтбора.Характеристика КАК Характеристика,
	|	РезультатОтбора.Назначение КАК Назначение,
	|	РезультатОтбора.Упаковка КАК Упаковка,
	|	РезультатОтбора.Серия КАК Серия,
	|	ВЫБОР
	|		КОГДА ПолитикиУчетаСерий.ПолитикаУчетаСерий.УказыватьПоФактуОтбора 
	|			И НЕ ПолитикиУчетаСерий.ПолитикаУчетаСерий.УчитыватьОстаткиСерий 
	|		ТОГДА 1
    |		ИНАЧЕ РезультатОтбора.СтатусУказанияСерий
    |	КОНЕЦ КАК СтатусУказанияСерий,
	|	ЕСТЬNULL(ТараПоРаспоряжениям.Количество, 0) КАК КоличествоТарыВРаспоряжении,
	|	РезультатОтбора.Количество КАК Количество,
	|	РезультатОтбора.КоличествоУпаковок КАК КоличествоУпаковок,
	|	РезультатОтбора.КоличествоУпаковок КАК КоличествоУпаковокРазница,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать) КАК Действие
	|ИЗ
	|	РезультатОтбора КАК РезультатОтбора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТараПоРаспоряжениям КАК ТараПоРаспоряжениям
	|		ПО РезультатОтбора.Номенклатура = ТараПоРаспоряжениям.Номенклатура
	|			И РезультатОтбора.Характеристика = ТараПоРаспоряжениям.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|		ПО РезультатОтбора.Номенклатура = СправочникНоменклатура.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК ХарактеристикиНоменклатуры
	|		ПО РезультатОтбора.Характеристика = ХарактеристикиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Назначения КАК Назначения
	|		ПО РезультатОтбора.Назначение = Назначения.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК УпаковкиНоменклатуры
	|		ПО РезультатОтбора.Упаковка = УпаковкиНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СерииНоменклатуры КАК СерииНоменклатуры
	|		ПО РезультатОтбора.Серия = СерииНоменклатуры.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры.ПолитикиУчетаСерий КАК ПолитикиУчетаСерий
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|			ПО ПолитикиУчетаСерий.Склад = Склады.Ссылка
	|		ПО (ПолитикиУчетаСерий.Склад = &Склад)
	|			И РезультатОтбора.Номенклатура.ВидНоменклатуры = ПолитикиУчетаСерий.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	СправочникНоменклатура.ТипНоменклатуры.Порядок,
	|	СправочникНоменклатура.Наименование,
	|	ХарактеристикиНоменклатуры.Наименование,
	|	Назначения.Наименование,
	|	&ТекстЗапросаКоэффициентУпаковки УБЫВ,
	|	СерииНоменклатуры.ГоденДо,
	|	СерииНоменклатуры.Номер";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"УпаковкиНоменклатуры", Неопределено));
		
	Возврат ТекстЗапроса;

КонецФункции

&НаКлиенте
Процедура ОбновитьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьФормуНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере(Результат)
	
	СерииПриОтгрузке = СерииНоменклатурыПриОтгрузке(Результат.Номенклатура);

	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Результат.Упаковка, Результат.Номенклатура);
	
	СтруктураПоиска = СтруктураПоискаТоваров();
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Результат);
	
	Если Результат.Режим = "РедактированиеОтгрузка" Или Результат.Режим = "РедактированиеСерий" Тогда 
				
		Литерал = "НомерСтроки";
		НомерСтроки = Результат[Литерал];
		
		Количество = Результат.КоличествоУпаковок * КоэффициентУпаковки; 
		ИмяТаблицы = ?(Результат.Режим = "РедактированиеОтгрузка", "ТоварыКОтгрузке", "ТоварыКОтбору");
		
		Если ОтгрузкаРазрешена(Результат, Количество) Тогда
			
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].Упаковка           = Результат.Упаковка;
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].КоличествоУпаковок = Результат.КоличествоУпаковок;
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].Количество         = Количество;
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].Назначение         = Результат.Назначение;
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].Серия              = Результат.Серия;
			ЭтотОбъект[ИмяТаблицы][НомерСтроки].Характеристика     = Результат.Характеристика;
			
		Иначе
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Результат.Режим = "Отгрузка" Тогда 
				
		РезультатПоиска = ТоварыКОтгрузке.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + Результат.КоличествоУпаковок;
			Количество = КоличествоУпаковок * КоэффициентУпаковки;
			
			Если ОтгрузкаРазрешена(Результат, Количество) Тогда
				РезультатПоиска[0].КоличествоУпаковок = КоличествоУпаковок;
				РезультатПоиска[0].Количество = Количество;
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			КоличествоУпаковок = Результат.КоличествоУпаковок;
			Количество = КоличествоУпаковок * КоэффициентУпаковки;

			Если ОтгрузкаРазрешена(Результат, Количество) Тогда
				НоваяСтрока = ТоварыКОтгрузке.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
				НоваяСтрока.Количество = Результат.КоличествоУпаковок * КоэффициентУпаковки;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Результат.Режим = "ОтборИзЯчейкиОтгрузка" Или Результат.Режим = "УказаниеСерий" Тогда
		
		ИмяТаблицы = ?(Результат.Режим = "ОтборИзЯчейкиОтгрузка", "ТоварыКПроверке", "ТоварыКОтбору");
		
		РезультатПоиска = ЭтотОбъект[ИмяТаблицы].НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + Результат.КоличествоУпаковок;
			Количество = КоличествоУпаковок * КоэффициентУпаковки;
			
			Если ОтгрузкаРазрешена(Результат, Количество) Тогда
				РезультатПоиска[0].КоличествоУпаковок = КоличествоУпаковок;
				РезультатПоиска[0].Количество = Количество;
			Иначе
				Возврат;
			КонецЕсли;
		Иначе
			КоличествоУпаковок = Результат.КоличествоУпаковок;
			Количество = КоличествоУпаковок * КоэффициентУпаковки;

			Если ОтгрузкаРазрешена(Результат, Количество) Тогда
				НоваяСтрока = ЭтотОбъект[ИмяТаблицы].Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
				НоваяСтрока.Количество = Результат.КоличествоУпаковок * КоэффициентУпаковки;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
			Иначе
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если Результат.Режим = "ОтборИзЯчейкиОтгрузка" И СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
			СтруктураПоиска.Удалить("Серия");
		ИначеЕсли Результат.Режим = "УказаниеСерий" Тогда
			СтруктураПоиска.Вставить("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка());
		КонецЕсли;
		
		РезультатПоиска = ТоварыКОтбору.НайтиСтроки(СтруктураПоиска);
		Если Не РезультатПоиска.Количество() Тогда
			СтруктураПоиска.Удалить("Упаковка");
			РезультатПоиска = ТоварыКОтбору.НайтиСтроки(СтруктураПоиска);
		КонецЕсли;
		
		КоличествоОтобрано = Результат.КоличествоУпаковок * КоэффициентУпаковки;
		
		Для Каждого Строка Из РезультатПоиска Цикл
			
			ВычитаемоеЗначение = Мин(Строка.Количество, КоличествоОтобрано);
			Строка.Количество = Строка.Количество - ВычитаемоеЗначение;
			КоличествоОтобрано = КоличествоОтобрано - ВычитаемоеЗначение;
			
			КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
			Строка.КоличествоУпаковок = Строка.Количество / КоэффициентУпаковки;
			
		КонецЦикла;
		
		// Удаляем строки если они полностью отработаны
		МассивУдаляемыхСтрок = Новый Массив;
		Для Каждого Строка Из ТоварыКОтбору Цикл
			Если Строка.Количество <= 0 Тогда
				МассивУдаляемыхСтрок.Добавить(Строка);
			КонецЕсли;
		КонецЦикла; 
		
		Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл
			ТоварыКОтбору.Удалить(СтрокаУдалить);
		КонецЦикла;
		
	ИначеЕсли Результат.Режим = "РедактированиеПересчетОтгрузка" Или Результат.Режим = "ПересчетОтгрузка" 
		Или Результат.Режим = "РедактированиеПересчетОтгрузкаБезСерии" Тогда
		
		ВидНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Результат.Номенклатура, "ВидНоменклатуры");
		НастройкаИспользованияСерий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "НастройкаИспользованияСерий");
		ЭкземплярТовара = (НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара); 
		
		Если ЭкземплярТовара И СерииПриОтгрузке.УказыватьПоФактуОтбора Тогда
			СтруктураПоиска.Удалить("Упаковка");
			РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска); 				
			Если РезультатПоиска.Количество() > 0 Тогда
				ШаблонСообщения = НСтр("ru = 'Указанное количество товара ""%1"" с серией ""%2"" будет больше 1. Политика учета серий данного товара предусматривает, что количество по любой серии этого товара всегда будет равно 1.';
										|en = 'The specified quantity of the ""%1"" item with batch ""%2"" is greater than 1. According to the batch accounting policy of this item, the quantity by any item batch always equals 1.'");
				ТекстСообщения = СтрШаблон(ШаблонСообщения, Результат.Номенклатура, Результат.Серия);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если СерииПриОтгрузке.УказыватьПоФактуОтбора И (Результат.Режим = "РедактированиеПересчетОтгрузкаБезСерии"
			Или Результат.Режим = "ПересчетОтгрузка") Тогда
			
			СтруктураПоиска.Вставить("Упаковка", Результат.Упаковка);
			СтруктураПоиска.Вставить("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка());
			РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска);  
				
			Если Не РезультатПоиска.Количество() Тогда
				СтруктураПоиска.Удалить("Упаковка");
				РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска);
			КонецЕсли;
				
			Если Результат.Режим = "ПересчетОтгрузка" И РезультатПоиска.Количество = 0 Тогда 
				Возврат;
			КонецЕсли;
			
			КоличествоОтобрано = Результат.КоличествоУпаковок * КоэффициентУпаковки;
			
			МассивУдаляемыхСтрок = Новый Массив;
			
			Для Каждого Строка Из РезультатПоиска Цикл
				ВычитаемоеЗначение = Мин(Строка.Количество, КоличествоОтобрано);
				Строка.Количество = Строка.Количество - ВычитаемоеЗначение;
				КоличествоОтобрано = КоличествоОтобрано - ВычитаемоеЗначение;
					
				КоэффициентУпаковкиСтроки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
				Строка.КоличествоУпаковок = Строка.Количество / КоэффициентУпаковкиСтроки; 
				Строка.КоличествоУпаковокРазница = Строка.КоличествоУпаковок;
				Если Строка.Количество = 0 Тогда
					МассивУдаляемыхСтрок.Добавить(Строка);
				КонецЕсли;
			КонецЦикла; 

			СтруктураПоиска.Вставить("Серия", Результат.Серия);
			СтруктураПоиска.Вставить("Упаковка", Результат.Упаковка);
			РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска); 
				
			Если РезультатПоиска.Количество() > 0 Тогда
				РезультатПоиска[0].КоличествоУпаковок = ВычитаемоеЗначение / КоэффициентУпаковки + РезультатПоиска[0].КоличествоУпаковок;
				РезультатПоиска[0].Количество = Результат.КоличествоУпаковок * КоэффициентУпаковки; 
				РезультатПоиска[0].КоличествоУпаковокФакт = РезультатПоиска[0].КоличествоУпаковокФакт + Результат.КоличествоУпаковок;
				РезультатПоиска[0].КоличествоФакт = РезультатПоиска[0].КоличествоУпаковокФакт * КоэффициентУпаковки;
				РезультатПоиска[0].КоличествоУпаковокРазница = РезультатПоиска[0].КоличествоУпаковок - РезультатПоиска[0].КоличествоУпаковокФакт;
				РезультатПоиска[0].КоличествоРазница = РезультатПоиска[0].КоличествоУпаковокРазница * КоэффициентУпаковки;
			Иначе  
				СтруктураПоиска.Удалить("Серия");
				СтруктураПоиска.Удалить("Упаковка");
				РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска);
				Если РезультатПоиска.Количество() > 0 Тогда 
					КоличествоУпаковок = Результат.КоличествоУпаковок;
				Иначе
					КоличествоУпаковок = 0; 
				КонецЕсли;
				НоваяСтрока = ТоварыКПроверке.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат); 
				НоваяСтрока.КоличествоУпаковок = КоличествоУпаковок;
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки; 
				НоваяСтрока.КоличествоУпаковокФакт = Результат.КоличествоУпаковок; 
				НоваяСтрока.КоличествоФакт = НоваяСтрока.КоличествоУпаковокФакт * КоэффициентУпаковки;
				НоваяСтрока.КоличествоУпаковокРазница = НоваяСтрока.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковокФакт;
				НоваяСтрока.КоличествоРазница = НоваяСтрока.КоличествоУпаковокРазница * КоэффициентУпаковки;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
			КонецЕсли;
			
			Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл
				ТоварыКПроверке.Удалить(СтрокаУдалить);
			КонецЦикла;
		Иначе	
			РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска);  
			Если Не РезультатПоиска.Количество() Тогда
				СтруктураПоиска.Удалить("Упаковка");
				РезультатПоиска = ТоварыКПроверке.НайтиСтроки(СтруктураПоиска);
			КонецЕсли;
				
			Если РезультатПоиска.Количество() > 0 Тогда  
				Если РезультатПоиска[0].Упаковка <> Результат.Упаковка Тогда 
					РезультатПоиска[0].КоличествоУпаковок = РезультатПоиска[0].Количество / КоэффициентУпаковки;
				КонецЕсли;
				Если Результат.Режим = "ПересчетОтгрузка" Тогда
					РезультатПоиска[0].КоличествоУпаковокФакт = РезультатПоиска[0].КоличествоУпаковокФакт + Результат.КоличествоУпаковок;
				Иначе
					РезультатПоиска[0].КоличествоУпаковокФакт = Результат.КоличествоУпаковок;
				КонецЕсли;
				РезультатПоиска[0].КоличествоФакт = Результат.КоличествоУпаковок * КоэффициентУпаковки;
				РезультатПоиска[0].КоличествоУпаковокРазница = РезультатПоиска[0].КоличествоУпаковок - РезультатПоиска[0].КоличествоУпаковокФакт;
				РезультатПоиска[0].КоличествоРазница = РезультатПоиска[0].КоличествоУпаковокРазница * КоэффициентУпаковки;
				РезультатПоиска[0].Упаковка = Результат.Упаковка;
			Иначе
				НоваяСтрока = ТоварыКПроверке.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат); 
				НоваяСтрока.КоличествоУпаковок = 0;
				НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок; 
				НоваяСтрока.КоличествоУпаковокФакт = Результат.КоличествоУпаковок; 
				НоваяСтрока.КоличествоФакт = НоваяСтрока.КоличествоУпаковокФакт * КоэффициентУпаковки;
				НоваяСтрока.КоличествоУпаковокРазница = НоваяСтрока.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковокФакт;
				НоваяСтрока.КоличествоРазница = НоваяСтрока.КоличествоУпаковокРазница * КоэффициентУпаковки;
				НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;
			КонецЕсли;

		КонецЕсли; 
	
		ОбновитьПодсказкуКПроверке();
		
	КонецЕсли;
	
	Для Каждого СтрокаКодМаркировки Из Результат.ШтрихкодыУпаковок Цикл
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКодМаркировки);
	КонецЦикла;

	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Функция ЕстьОшибкиУказанияСерий()
	
	ОтборПустыеСерии = Новый Структура;
	ОтборПустыеСерии.Вставить("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка());
	
	Если Статус = Перечисления.СтатусыРасходныхОрдеров.Подготовлен 
		Или Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору Тогда 
		
		ОтборПустыеСерии.Вставить("СтатусУказанияСерий", 27);
		МассивПустыеСерии = ТоварыКОтбору.НайтиСтроки(ОтборПустыеСерии);
		
		ОтборПустыеСерии.Вставить("СтатусУказанияСерий", 7);
		МассивПустыеСерии2 = ТоварыКОтбору.НайтиСтроки(ОтборПустыеСерии);
		
		ОтборПустыеСерии.Вставить("СтатусУказанияСерий", 5);
		МассивПустыеСерии3 = ТоварыКОтбору.НайтиСтроки(ОтборПустыеСерии);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПустыеСерии, МассивПустыеСерии2);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПустыеСерии, МассивПустыеСерии3);
		
		ШаблонСообщения = НСтр("ru = 'Не заполнена серия для товара ""%1"" в строке %2 списка ""К отбору""';
								|en = 'The batch for the ""%1"" item in line %2 of the ""To pick"" list is not filled in'");
		
		Для Каждого СтрокаПустаяСерия Из МассивПустыеСерии Цикл
			НомерСтроки = ТоварыКОтбору.Индекс(СтрокаПустаяСерия) + 1;
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаПустаяСерия.Номенклатура, Формат(НомерСтроки, "ЧН=0; ЧГ=")); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	
	ИначеЕсли Статус = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки Тогда 
		
		ОтборПустыеСерии.Вставить("СтатусУказанияСерий", 1);
		МассивПустыеСерии = ТоварыКПроверке.НайтиСтроки(ОтборПустыеСерии);
		
		ОтборПустыеСерии.Вставить("СтатусУказанияСерий", 3);
		МассивПустыеСерии2 = ТоварыКПроверке.НайтиСтроки(ОтборПустыеСерии);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивПустыеСерии, МассивПустыеСерии2);
		
		ШаблонСообщения = НСтр("ru = 'Не заполнена серия для товара ""%1"" в строке %2 списка ""К проверке""';
								|en = 'The batch for the ""%1"" item in line %2 of the ""To verify"" list is not filled in'");
	
		Для Каждого СтрокаПустаяСерия Из МассивПустыеСерии Цикл
			НомерСтроки = ТоварыКПроверке.Индекс(СтрокаПустаяСерия) + 1;
			ТекстСообщения = СтрШаблон(ШаблонСообщения, СтрокаПустаяСерия.Номенклатура, Формат(НомерСтроки, "ЧН=0; ЧГ=")); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
		
	КонецЕсли; 
	
	Возврат МассивПустыеСерии.Количество() <> 0;
	
КонецФункции

&НаСервере
Функция ЗакончитьУказаниеСерий()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПроверке.Номенклатура,
	|	ТоварыКПроверке.Характеристика,
	|	ТоварыКПроверке.Назначение,
	|	ТоварыКПроверке.Упаковка,
	|	ТоварыКПроверке.Серия,
	|	ТоварыКПроверке.Количество КАК Количество,
	|	ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКПроверке.Действие КАК Действие
	|ПОМЕСТИТЬ ТоварыКПроверке
	|ИЗ 
	|	&ТоварыКПроверке КАК ТоварыКПроверке	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтбору.Номенклатура,
	|	ТоварыКОтбору.Характеристика,
	|	ТоварыКОтбору.Назначение,
	|	ТоварыКОтбору.Упаковка,
	|	ТоварыКОтбору.Серия,
	|	ТоварыКОтбору.Количество КАК Количество,
	|	ТоварыКОтбору.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКОтбору.Действие КАК Действие
	|ПОМЕСТИТЬ ТоварыКОтбору
	|ИЗ 
	|	&ТоварыКОтбору КАК ТоварыКОтбору 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНеОтгружать.Номенклатура,
	|	ТоварыНеОтгружать.Характеристика,
	|	ТоварыНеОтгружать.Назначение,
	|	ТоварыНеОтгружать.Упаковка,
	|	ТоварыНеОтгружать.Серия,
	|	ТоварыНеОтгружать.Количество КАК Количество,
	|	ТоварыНеОтгружать.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыНеОтгружать.Действие
	|ПОМЕСТИТЬ ТоварыНеОтгружать
	|ИЗ 
	|	&ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтгрузке.Номенклатура,
	|	ТоварыКОтгрузке.Характеристика,
	|	ТоварыКОтгрузке.Назначение,
	|	ТоварыКОтгрузке.Упаковка,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Количество КАК Количество,
	|	ТоварыКОтгрузке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКОтгрузке.Действие
	|ПОМЕСТИТЬ ТоварыКОтгрузке
	|ИЗ 
	|	&ТоварыКОтгрузке КАК ТоварыКОтгрузке 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВложенныйЗапрос.Действие
	|ИЗ 
	|	(ВЫБРАТЬ
	|		ТоварыКПроверке.Номенклатура,
	|		ТоварыКПроверке.Характеристика,
	|		ТоварыКПроверке.Назначение,
	|		ТоварыКПроверке.Упаковка,
	|		ТоварыКПроверке.Серия,
	|		ТоварыКПроверке.Количество КАК Количество,
	|		ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|		ТоварыКПроверке.Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ТоварыКПроверке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтбору.Номенклатура,
	|		ТоварыКОтбору.Характеристика,
	|		ТоварыКОтбору.Назначение,
	|		ТоварыКОтбору.Упаковка,
	|		ТоварыКОтбору.Серия,
	|		ТоварыКОтбору.Количество,
	|		ТоварыКОтбору.КоличествоУпаковок,
	|		ТоварыКОтбору.Действие
	|	ИЗ
	|		ТоварыКОтбору КАК ТоварыКОтбору
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНеОтгружать.Номенклатура,
	|		ТоварыНеОтгружать.Характеристика,
	|		ТоварыНеОтгружать.Назначение,
	|		ТоварыНеОтгружать.Упаковка,
	|		ТоварыНеОтгружать.Серия,
	|		ТоварыНеОтгружать.Количество,
	|		ТоварыНеОтгружать.КоличествоУпаковок,
	|		ТоварыНеОтгружать.Действие
	|	ИЗ
	|		ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтгрузке.Номенклатура,
	|		ТоварыКОтгрузке.Характеристика,
	|		ТоварыКОтгрузке.Назначение,
	|		ТоварыКОтгрузке.Упаковка,
	|		ТоварыКОтгрузке.Серия,
	|		ТоварыКОтгрузке.Количество,
	|		ТоварыКОтгрузке.КоличествоУпаковок,
	|		ТоварыКОтгрузке.Действие
	|	ИЗ
	|		ТоварыКОтгрузке КАК ТоварыКОтгрузке) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Действие"; 
	
	Запрос.УстановитьПараметр("ТоварыКПроверке", ТоварыКПроверке.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтбору", ТоварыКОтбору.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыНеОтгружать", ТоварыНеОтгружать.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтгрузке", ТоварыКОтгрузке.Выгрузить());
	
	ОтгружаемыеТовары = Запрос.Выполнить().Выгрузить();

	НачатьТранзакцию();
	Попытка
		ОрдерОбъект = Ссылка.ПолучитьОбъект();
			
		ОрдерОбъект.Помещение   = Помещение;
		ОрдерОбъект.Комментарий = Комментарий;
		ОрдерОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
			
		ОрдерОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
		ОрдерОбъект.Ответственный = Пользователи.ТекущийПользователь();
			
		ОрдерОбъект.ОтгружаемыеТовары.Загрузить(ОтгружаемыеТовары); 
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОрдерОбъект, Документы.РасходныйОрдерНаТовары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);
				
		ОрдерОбъект.ШтрихкодыУпаковок.Очистить();
		Для Каждого СтрокаШК Из ШтрихкодыУпаковок Цикл
			НоваяСтрока = ОрдерОбъект.ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ШтрихкодУпаковки = СтрокаШК.ШтрихкодУпаковки;
		КонецЦикла;

		ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ЗафиксироватьТранзакцию();
	Исключение 
		ОтменитьТранзакцию(); 
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
									|en = 'Cannot save %1. %2'"), Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;
		
	Возврат Истина;

КонецФункции

&НаСервере
Функция ЗакончитьОтборНаСервере(Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПроверке.Номенклатура,
	|	ТоварыКПроверке.Характеристика,
	|	ТоварыКПроверке.Назначение,
	|	ТоварыКПроверке.Упаковка,
	|	ТоварыКПроверке.Серия,
	|	ТоварыКПроверке.Количество КАК Количество,
	|	ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКПроверке.Действие
	|ПОМЕСТИТЬ ТоварыКПроверке
	|ИЗ 
	|	&ТоварыКПроверке КАК ТоварыКПроверке	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтбору.Номенклатура,
	|	ТоварыКОтбору.Характеристика,
	|	ТоварыКОтбору.Назначение,
	|	ТоварыКОтбору.Упаковка,
	|	ТоварыКОтбору.Серия,
	|	ТоварыКОтбору.Количество КАК Количество,
	|	ТоварыКОтбору.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать) КАК Действие
	|ПОМЕСТИТЬ ТоварыКОтбору
	|ИЗ 
	|	&ТоварыКОтбору КАК ТоварыКОтбору 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНеОтгружать.Номенклатура,
	|	ТоварыНеОтгружать.Характеристика,
	|	ТоварыНеОтгружать.Назначение,
	|	ТоварыНеОтгружать.Упаковка,
	|	ТоварыНеОтгружать.Серия,
	|	ТоварыНеОтгружать.Количество КАК Количество,
	|	ТоварыНеОтгружать.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыНеОтгружать.Действие
	|ПОМЕСТИТЬ ТоварыНеОтгружать
	|ИЗ 
	|	&ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтгрузке.Номенклатура,
	|	ТоварыКОтгрузке.Характеристика,
	|	ТоварыКОтгрузке.Назначение,
	|	ТоварыКОтгрузке.Упаковка,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Количество КАК Количество,
	|	ТоварыКОтгрузке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКОтгрузке.Действие
	|ПОМЕСТИТЬ ТоварыКОтгрузке
	|ИЗ 
	|	&ТоварыКОтгрузке КАК ТоварыКОтгрузке 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВложенныйЗапрос.Действие
	|ИЗ 
	|	(ВЫБРАТЬ
	|		ТоварыКПроверке.Номенклатура,
	|		ТоварыКПроверке.Характеристика,
	|		ТоварыКПроверке.Назначение,
	|		ТоварыКПроверке.Упаковка,
	|		ТоварыКПроверке.Серия,
	|		ТоварыКПроверке.Количество КАК Количество,
	|		ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|		ТоварыКПроверке.Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ТоварыКПроверке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтбору.Номенклатура,
	|		ТоварыКОтбору.Характеристика,
	|		ТоварыКОтбору.Назначение,
	|		ТоварыКОтбору.Упаковка,
	|		ТоварыКОтбору.Серия,
	|		ТоварыКОтбору.Количество,
	|		ТоварыКОтбору.КоличествоУпаковок,
	|		ТоварыКОтбору.Действие
	|	ИЗ
	|		ТоварыКОтбору КАК ТоварыКОтбору
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНеОтгружать.Номенклатура,
	|		ТоварыНеОтгружать.Характеристика,
	|		ТоварыНеОтгружать.Назначение,
	|		ТоварыНеОтгружать.Упаковка,
	|		ТоварыНеОтгружать.Серия,
	|		ТоварыНеОтгружать.Количество,
	|		ТоварыНеОтгружать.КоличествоУпаковок,
	|		ТоварыНеОтгружать.Действие
	|	ИЗ
	|		ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтгрузке.Номенклатура,
	|		ТоварыКОтгрузке.Характеристика,
	|		ТоварыКОтгрузке.Назначение,
	|		ТоварыКОтгрузке.Упаковка,
	|		ТоварыКОтгрузке.Серия,
	|		ТоварыКОтгрузке.Количество,
	|		ТоварыКОтгрузке.КоличествоУпаковок,
	|		ТоварыКОтгрузке.Действие
	|	ИЗ
	|		ТоварыКОтгрузке КАК ТоварыКОтгрузке) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Действие";
	
	Запрос.УстановитьПараметр("ТоварыКПроверке", ТоварыКПроверке.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтбору", ТоварыКОтбору.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыНеОтгружать", ТоварыНеОтгружать.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтгрузке", ТоварыКОтгрузке.Выгрузить());
	
	ОтгружаемыеТовары = Запрос.Выполнить().Выгрузить();
	
	НачатьТранзакцию();
	Попытка
		
		ОрдерОбъект = Ссылка.ПолучитьОбъект();
		
		ОрдерОбъект.Помещение   = Помещение;
		ОрдерОбъект.Комментарий = Комментарий;
		ОрдерОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
			
		Если Режим = "Проверить" Тогда
			ОрдерОбъект.Статус      = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки;
			ОрдерОбъект.Контролер   = Пользователи.ТекущийПользователь();
		Иначе
			ОрдерОбъект.Статус      = Перечисления.СтатусыРасходныхОрдеров.КПроверке;
		КонецЕсли;
		
		ОрдерОбъект.ОтгружаемыеТовары.Загрузить(ОтгружаемыеТовары);

		СтруктураПоиска = СтруктураПоискаТоваров();
		СтруктураПоиска.Удалить("Упаковка");
		СтруктураПоиска.Удалить("Серия");
		СтруктураПоиска.Удалить("Действие");

		Для Каждого Строка Из ТоварыКОтбору Цикл
		
			КоличествоКРаспределению = Строка.Количество; 
			
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
				
			НайденныеРаспоряжения = ОрдерОбъект.ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
			Для Каждого СтрокаРаспоряжения Из НайденныеРаспоряжения Цикл
				Если КоличествоКРаспределению >= СтрокаРаспоряжения.Количество Тогда
					КоличествоКРаспределению = КоличествоКРаспределению - СтрокаРаспоряжения.Количество;
					ОрдерОбъект.ТоварыПоРаспоряжениям.Удалить(СтрокаРаспоряжения);
				Иначе
					СтрокаРаспоряжения.Количество = СтрокаРаспоряжения.Количество - КоличествоКРаспределению;
					КоличествоКРаспределению = 0;
				КонецЕсли;
					
				Если КоличествоКРаспределению = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;

		КонецЦикла;

		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОрдерОбъект, Документы.РасходныйОрдерНаТовары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ТоварыПоРаспоряжениям);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);
		
		ОрдерОбъект.ШтрихкодыУпаковок.Очистить();
		Для Каждого СтрокаШК Из ШтрихкодыУпаковок Цикл
			НоваяСтрока = ОрдерОбъект.ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ШтрихкодУпаковки = СтрокаШК.ШтрихкодУпаковки;
		КонецЦикла;

		ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ЗафиксироватьТранзакцию();
	Исключение 
		ОтменитьТранзакцию(); 
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
									|en = 'Cannot save %1. %2'"), Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;
		
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗакончитьПроверкуНаСервере(Режим)
	
	Добор = (Режим = "Отобрать" Или Режим = "ОтобратьЗавершить");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПроверке.Номенклатура,
	|	ТоварыКПроверке.Характеристика,
	|	ТоварыКПроверке.Назначение,
	|	ТоварыКПроверке.Упаковка,
	|	ТоварыКПроверке.СтатусУказанияСерий,
	|	ТоварыКПроверке.Серия,
	|	ТоварыКПроверке.Количество,
	|	ТоварыКПроверке.КоличествоУпаковок,
	|	ТоварыКПроверке.КоличествоФакт,
	|	ТоварыКПроверке.КоличествоУпаковокФакт,
	|	ТоварыКПроверке.КоличествоРазница,
	|	ТоварыКПроверке.КоличествоУпаковокРазница,
	|	ТоварыКПроверке.Действие
	|ПОМЕСТИТЬ ТоварыКПроверке
	|ИЗ
	|	&ТоварыКПроверке КАК ТоварыКПроверке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВложенныйЗапрос.Действие
	|ПОМЕСТИТЬ ОтгружаемыеТовары
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВТНедоборОтгрузить.Номенклатура,
	|		ВТНедоборОтгрузить.Характеристика,
	|		ВТНедоборОтгрузить.Назначение,
	|		ВТНедоборОтгрузить.Упаковка,
	|		ВТНедоборОтгрузить.Серия,
	|		ВТНедоборОтгрузить.КоличествоФакт КАК Количество,
	|		ВТНедоборОтгрузить.КоличествоУпаковокФакт КАК КоличествоУпаковок,
	|		ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить) КАК Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ВТНедоборОтгрузить
	|	ГДЕ
	|		ВТНедоборОтгрузить.КоличествоУпаковокРазница > 0
	|		И ВТНедоборОтгрузить.КоличествоУпаковокФакт <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТИзлишекОтгрузить.Номенклатура,
	|		ВТИзлишекОтгрузить.Характеристика,
	|		ВТИзлишекОтгрузить.Назначение,
	|		ВТИзлишекОтгрузить.Упаковка,
	|		ВТИзлишекОтгрузить.Серия,
	|		ВТИзлишекОтгрузить.Количество КАК Количество,
	|		ВТИзлишекОтгрузить.КоличествоУпаковок КАК КоличествоУпаковок,
	|		ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить) КАК Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ВТИзлишекОтгрузить
	|	ГДЕ
	|		ВТИзлишекОтгрузить.КоличествоУпаковокРазница < 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТОтгрузить.Номенклатура,
	|		ВТОтгрузить.Характеристика,
	|		ВТОтгрузить.Назначение,
	|		ВТОтгрузить.Упаковка,
	|		ВТОтгрузить.Серия,
	|		ВТОтгрузить.КоличествоФакт КАК Количество,
	|		ВТОтгрузить.КоличествоУпаковокФакт КАК КоличествоУпаковок,
	|		ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить) КАК Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ВТОтгрузить
	|	ГДЕ
	|		ВТОтгрузить.КоличествоУпаковокРазница = 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТИзлишек.Номенклатура,
	|		ВТИзлишек.Характеристика,
	|		ВТИзлишек.Назначение,
	|		ВТИзлишек.Упаковка,
	|		ВТИзлишек.Серия,
	|		-ВТИзлишек.КоличествоРазница КАК Количество,
	|		-ВТИзлишек.КоличествоУпаковокРазница КАК КоличествоУпаковок,
	|		ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать) КАК Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ВТИзлишек
	|	ГДЕ
	|		ВТИзлишек.КоличествоУпаковокРазница < 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВТНедобор.Номенклатура,
	|		ВТНедобор.Характеристика,
	|		ВТНедобор.Назначение,
	|		ВТНедобор.Упаковка,
	|		ВЫБОР
	|			КОГДА НЕ ВТНедобор.СтатусУказанияСерий В (0, 1, 2, 3, 4)
	|				ТОГДА ВТНедобор.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ КАК Серия,
	|		ВЫБОР
	|			КОГДА ВТНедобор.КоличествоРазница = 0
	|				ТОГДА ВТНедобор.Количество
	|			ИНАЧЕ ВТНедобор.КоличествоРазница
	|		КОНЕЦ КАК Количество,
	|		ВТНедобор.КоличествоУпаковокРазница КАК КоличествоУпаковок,
	|		ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать) КАК Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ВТНедобор
	|	ГДЕ
	|		ВТНедобор.КоличествоУпаковокРазница > 0) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Действие
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтгружаемыеТовары.Номенклатура,
	|	ОтгружаемыеТовары.Характеристика,
	|	ОтгружаемыеТовары.Назначение,
	|	ОтгружаемыеТовары.Упаковка,
	|	ОтгружаемыеТовары.Серия,
	|	ОтгружаемыеТовары.Количество,
	|	ОтгружаемыеТовары.КоличествоУпаковок,
	|	ОтгружаемыеТовары.Действие
	|ИЗ
	|	ОтгружаемыеТовары КАК ОтгружаемыеТовары";
		
	Запрос.УстановитьПараметр("ТоварыКПроверке", ТоварыКПроверке.Выгрузить());
	
	ОтгружаемыеТовары = Запрос.Выполнить().Выгрузить();
	
	ДокументРасхожденийОбъект = СформироватьДокументРасхождений();
	
	НачатьТранзакцию();
	Попытка
		
		ОрдерОбъект = Ссылка.ПолучитьОбъект();

		ОрдерОбъект.Помещение   = Помещение;
		ОрдерОбъект.Комментарий = Комментарий;
		ОрдерОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
		
		Если Режим = "Отгрузить" Или Режим = "ИзлишекОтгрузить" Тогда
			ОрдерОбъект.Статус     = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
			ОрдерОбъект.Отгрузил   = Пользователи.ТекущийПользователь();
		ИначеЕсли Режим = "ОтгрузитьЗавершить" Или Режим = "ИзлишекЗавершить" Тогда
			ОрдерОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.Проверен;
		ИначеЕсли Добор Тогда 
			ОрдерОбъект.Статус     = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
			ОрдерОбъект.Контролер  = Неопределено; 
		КонецЕсли;
		
		УсловиеОтбораСтрок = Новый Структура;
		УсловиеОтбораСтрок.Вставить("Действие", Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать);
		НайденныеСтроки = ОрдерОбъект.ОтгружаемыеТовары.НайтиСтроки(УсловиеОтбораСтрок);
		Для Каждого СтрокаУдалить Из НайденныеСтроки Цикл
			ОрдерОбъект.ОтгружаемыеТовары.Удалить(СтрокаУдалить);
		КонецЦикла;
		
		СтруктураПоиска = СтруктураПоискаТоваров();
		СтруктураПоиска.Удалить("Упаковка");
		СтруктураПоиска.Удалить("Серия");
		СтруктураПоиска.Удалить("Действие");

		Для Каждого СтрокаПроверено Из ОтгружаемыеТовары Цикл
					
			Если СтрокаПроверено.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать
				И Не Добор Тогда
				
				Если СтрокаПроверено.Количество = 0 Тогда
					КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(СтрокаПроверено.Упаковка, СтрокаПроверено.Номенклатура);
					КоличествоКРаспределению = СтрокаПроверено.КоличествоУпаковок * КоэффициентУпаковки;
				Иначе
					КоличествоКРаспределению = СтрокаПроверено.Количество;
				КонецЕсли; 
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаПроверено);
				НайденныеРаспоряжения = ОрдерОбъект.ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
				Для Каждого СтрокаРаспоряжения Из НайденныеРаспоряжения Цикл    
					Если КоличествоКРаспределению >= СтрокаРаспоряжения.Количество Тогда
						КоличествоКРаспределению = КоличествоКРаспределению - СтрокаРаспоряжения.Количество;
						ОрдерОбъект.ТоварыПоРаспоряжениям.Удалить(СтрокаРаспоряжения);
					Иначе
						СтрокаРаспоряжения.Количество = СтрокаРаспоряжения.Количество - КоличествоКРаспределению;
						КоличествоКРаспределению = 0;
						КонецЕсли;
						
					Если КоличествоКРаспределению = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе	
				НоваяСтрока = ОрдерОбъект.ОтгружаемыеТовары.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПроверено);
			КонецЕсли;
		КонецЦикла;

		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОрдерОбъект, Документы.РасходныйОрдерНаТовары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);
	
		ОрдерОбъект.ШтрихкодыУпаковок.Очистить();
		Для Каждого СтрокаШК Из ШтрихкодыУпаковок Цикл		
			НоваяСтрока = ОрдерОбъект.ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ШтрихкодУпаковки = СтрокаШК.ШтрихкодУпаковки;
		КонецЦикла;

		Если ДокументРасхожденийОбъект <> Неопределено Тогда
			ДокументРасхожденийОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		Если ОрдерОбъект.ОтгружаемыеТовары.Количество() = 0 Тогда
			ОрдерОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Иначе
			ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
	Исключение 
		ОтменитьТранзакцию();

		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
									|en = 'Cannot save %1. %2'"), Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция СформироватьДокументРасхождений() 
	
	// 1. Посчитаем по заданиям на отбор, сколько отобрали
	// 2. Посчитаем то, что показывает в тч к Проверке
	// 3. Посчитаем, сколько уже учтено в документах расхождений
	// 4. Посчитаем, сколько в ордере отгружаемых строк и неотгружаемых
	// 5. На разницу создадим новый документ расхождений.
	
	Если Не ИспользоватьАдресноеХранение Тогда
		Возврат Неопределено;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПроверке.Номенклатура,
	|	ТоварыКПроверке.Характеристика,
	|	ТоварыКПроверке.Назначение,
	|	ТоварыКПроверке.Упаковка,
	|	ВЫБОР
	|		КОГДА НЕ ТоварыКПроверке.СтатусУказанияСерий В (0, 1, 2)
	|			ТОГДА ТоварыКПроверке.Серия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Серия,
	|	ТоварыКПроверке.КоличествоФакт,
	|	ТоварыКПроверке.КоличествоУпаковокФакт,
	|	ТоварыКПроверке.Действие
	|ПОМЕСТИТЬ ТоварыКПроверке
	|ИЗ
	|	&ТоварыКПроверке КАК ТоварыКПроверке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок
	|ПОМЕСТИТЬ Расхождения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТоварыОтбор.Номенклатура КАК Номенклатура,
	|		ТоварыОтбор.Характеристика КАК Характеристика,
	|		ТоварыОтбор.Назначение КАК Назначение,
	|		ТоварыОтбор.Упаковка КАК Упаковка,
	|		ТоварыОтбор.Серия КАК Серия,
	|		ТоварыОтбор.КоличествоОтобрано КАК Количество,
	|		ТоварыОтбор.КоличествоУпаковокОтобрано КАК КоличествоУпаковок
	|	ИЗ
	|		Документ.ОтборРазмещениеТоваров.ТоварыОтбор КАК ТоварыОтбор
	|	ГДЕ
	|		ТоварыОтбор.Ссылка.Проведен
	|		И ТоварыОтбор.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОтбораРазмещенияТоваров.Отбор)
	|		И ТоварыОтбор.Ссылка.Распоряжение = &Ордер
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКПроверке.Номенклатура,
	|		ТоварыКПроверке.Характеристика,
	|		ТоварыКПроверке.Назначение,
	|		ТоварыКПроверке.Упаковка,
	|		ТоварыКПроверке.Серия,
	|		-ТоварыКПроверке.КоличествоФакт КАК Количество, 
	|		-ТоварыКПроверке.КоличествоУпаковокФакт КАК КоличествоУпаковок
	|	ИЗ
	|		ТоварыКПроверке КАК ТоварыКПроверке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		Товары.Назначение,
	|		Товары.Упаковка,
	|		Товары.Серия,
	|		ВЫБОР
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ПеренестиВДругойОрдер)
	|				ТОГДА ВЫБОР
	|						КОГДА Товары.Ссылка.Ордер = &Ордер
	|							ТОГДА -Товары.Количество
	|						ИНАЧЕ Товары.Количество
	|					КОНЕЦ
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|				ТОГДА -Товары.Количество
	|			ИНАЧЕ Товары.Количество
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ПеренестиВДругойОрдер)
	|				ТОГДА ВЫБОР
	|						КОГДА Товары.Ссылка.Ордер = &Ордер
	|							ТОГДА -Товары.КоличествоУпаковок
	|						ИНАЧЕ Товары.КоличествоУпаковок
	|					КОНЕЦ
	|			КОГДА Товары.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|				ТОГДА -Товары.КоличествоУпаковок
	|			ИНАЧЕ Товары.КоличествоУпаковок
	|		КОНЕЦ
	|	ИЗ
	|		Документ.КорректировкаПоОрдеруНаТовары.Товары КАК Товары
	|	ГДЕ
	|		Товары.Ссылка.Проведен
	|		И (Товары.Ссылка.Ордер = &Ордер
	|				ИЛИ Товары.Ссылка.ОрдерПолучатель = &Ордер)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОтгружаемыеТовары.Номенклатура,
	|		ОтгружаемыеТовары.Характеристика,
	|		ОтгружаемыеТовары.Назначение,
	|		ОтгружаемыеТовары.Упаковка,
	|		ВЫБОР
	|			КОГДА НЕ ОтгружаемыеТовары.СтатусУказанияСерий В (0, 1, 2)
	|				ТОГДА ОтгружаемыеТовары.Серия
	|			ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СерииНоменклатуры.ПустаяСсылка)
	|		КОНЕЦ,
	|		-ОтгружаемыеТовары.Количество,
	|		-ОтгружаемыеТовары.КоличествоУпаковок
	|	ИЗ
	|		Документ.РасходныйОрдерНаТовары.ОтгружаемыеТовары КАК ОтгружаемыеТовары
	|	ГДЕ
	|		ОтгружаемыеТовары.Ссылка = &Ордер
	|		И ОтгружаемыеТовары.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить), ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать))
	|		) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Расхождения.Номенклатура,
	|	Расхождения.Характеристика,
	|	Расхождения.Назначение,
	|	Расхождения.Упаковка,
	|	Расхождения.Серия,
	|	ВЫБОР
	|		КОГДА Расхождения.КоличествоУпаковок < 0
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьИзлишек)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийКорректировокОстатковТоваров.ОтразитьНедостачу)
	|	КОНЕЦ КАК ВидОперации,
	|	ВЫБОР
	|		КОГДА Расхождения.Количество < 0
	|			ТОГДА -Расхождения.Количество
	|		ИНАЧЕ Расхождения.Количество
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА Расхождения.КоличествоУпаковок < 0
	|			ТОГДА -Расхождения.КоличествоУпаковок
	|		ИНАЧЕ Расхождения.КоличествоУпаковок
	|	КОНЕЦ КАК КоличествоУпаковок
	|ИЗ
	|	Расхождения КАК Расхождения
	|ГДЕ
	|	Расхождения.КоличествоУпаковок <> 0";
	
	Запрос.УстановитьПараметр("ТоварыКПроверке", ТоварыКПроверке.Выгрузить());
	Запрос.УстановитьПараметр("Ордер", Ссылка);
	
	ТаблицаРасхождений = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаРасхождений.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДокументРасхожденийОбъект = Документы.КорректировкаПоОрдеруНаТовары.СоздатьДокумент();
	
	ДокументРасхожденийОбъект.Дата = ТекущаяДатаСеанса();
	ДокументРасхожденийОбъект.Ответственный = Пользователи.ТекущийПользователь();
	ДокументРасхожденийОбъект.Ордер = Ссылка;
	ДокументРасхожденийОбъект.Склад = Склад;
	ДокументРасхожденийОбъект.Помещение = Помещение;
	ДокументРасхожденийОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
	
	ДокументРасхожденийОбъект.Товары.Загрузить(ТаблицаРасхождений);
	ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументРасхожденийОбъект, Документы.КорректировкаПоОрдеруНаТовары);
	НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументРасхожденийОбъект, ПараметрыУказанияСерий); 
	
	Возврат ДокументРасхожденийОбъект;
	
КонецФункции

&НаСервере
Функция ЕстьНедоборИзлишекОшибкаСерии()
	
	Результат = Новый Структура("ОшибкаСерии", Ложь);
	
	Если ЕстьОшибкиУказанияСерий() Тогда  
		Результат.Вставить("ОшибкаСерии", Истина);
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаКПроверке.КоличествоУпаковокРазница КАК Разница
		|ПОМЕСТИТЬ
		|	ВТНедобор
		|ИЗ
		|	&ТаблицаКПроверке КАК ТаблицаКПроверке 
		|ГДЕ
		|	ТаблицаКПроверке.КоличествоУпаковокРазница > 0
		|; 
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаКПроверке.КоличествоУпаковокРазница КАК Разница
		|ПОМЕСТИТЬ
		|	ВТИзлишек
		|ИЗ
		|	&ТаблицаКПроверке КАК ТаблицаКПроверке 
		|ГДЕ
		|	ТаблицаКПроверке.КоличествоУпаковокРазница < 0";
	
	Запрос.УстановитьПараметр("ТаблицаКПроверке", ТоварыКПроверке.Выгрузить());
	
	РезультатЗапроса =  Запрос.ВыполнитьПакет();
	ВыборкаНедобор = РезультатЗапроса[0].Выбрать();
	ВыборкаНедобор.Следующий();
	ВыборкаИзлишек = РезультатЗапроса[1].Выбрать();
	ВыборкаИзлишек.Следующий();
	
	Результат.Вставить("Недобор", ?(ВыборкаНедобор[0] > 0, Истина, Ложь)); 
	Результат.Вставить("Излишек", ?(ВыборкаИзлишек[0] > 0, Истина, Ложь));
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ОтгрузитьБезПроверки(Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТоварыКПроверке.Номенклатура,
	|	ТоварыКПроверке.Характеристика,
	|	ТоварыКПроверке.Назначение,
	|	ТоварыКПроверке.Упаковка,
	|	ТоварыКПроверке.Серия,
	|	ТоварыКПроверке.Количество КАК Количество,
	|	ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ЗНАЧЕНИЕ(Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отгрузить) КАК Действие
	|ПОМЕСТИТЬ ТоварыКПроверке
	|ИЗ 
	|	&ТоварыКПроверке КАК ТоварыКПроверке	
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтбору.Номенклатура,
	|	ТоварыКОтбору.Характеристика,
	|	ТоварыКОтбору.Назначение,
	|	ТоварыКОтбору.Упаковка,
	|	ТоварыКОтбору.Серия,
	|	ТоварыКОтбору.Количество КАК Количество,
	|	ТоварыКОтбору.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКОтбору.Действие КАК Действие
	|ПОМЕСТИТЬ ТоварыКОтбору
	|ИЗ 
	|	&ТоварыКОтбору КАК ТоварыКОтбору 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНеОтгружать.Номенклатура,
	|	ТоварыНеОтгружать.Характеристика,
	|	ТоварыНеОтгружать.Назначение,
	|	ТоварыНеОтгружать.Упаковка,
	|	ТоварыНеОтгружать.Серия,
	|	ТоварыНеОтгружать.Количество КАК Количество,
	|	ТоварыНеОтгружать.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыНеОтгружать.Действие
	|ПОМЕСТИТЬ ТоварыНеОтгружать
	|ИЗ 
	|	&ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыКОтгрузке.Номенклатура,
	|	ТоварыКОтгрузке.Характеристика,
	|	ТоварыКОтгрузке.Назначение,
	|	ТоварыКОтгрузке.Упаковка,
	|	ТоварыКОтгрузке.Серия,
	|	ТоварыКОтгрузке.Количество КАК Количество,
	|	ТоварыКОтгрузке.КоличествоУпаковок КАК КоличествоУпаковок,
	|	ТоварыКОтгрузке.Действие
	|ПОМЕСТИТЬ ТоварыКОтгрузке
	|ИЗ 
	|	&ТоварыКОтгрузке КАК ТоварыКОтгрузке 
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Серия,
	|	СУММА(ВложенныйЗапрос.Количество) КАК Количество,
	|	СУММА(ВложенныйЗапрос.КоличествоУпаковок) КАК КоличествоУпаковок,
	|	ВложенныйЗапрос.Действие
	|ИЗ 
	|	(ВЫБРАТЬ
	|		ТоварыКПроверке.Номенклатура,
	|		ТоварыКПроверке.Характеристика,
	|		ТоварыКПроверке.Назначение,
	|		ТоварыКПроверке.Упаковка,
	|		ТоварыКПроверке.Серия,
	|		ТоварыКПроверке.Количество КАК Количество,
	|		ТоварыКПроверке.КоличествоУпаковок КАК КоличествоУпаковок,
	|		ТоварыКПроверке.Действие
	|	ИЗ
	|		ТоварыКПроверке КАК ТоварыКПроверке
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтбору.Номенклатура,
	|		ТоварыКОтбору.Характеристика,
	|		ТоварыКОтбору.Назначение,
	|		ТоварыКОтбору.Упаковка,
	|		ТоварыКОтбору.Серия,
	|		ТоварыКОтбору.Количество,
	|		ТоварыКОтбору.КоличествоУпаковок,
	|		ТоварыКОтбору.Действие
	|	ИЗ
	|		ТоварыКОтбору КАК ТоварыКОтбору
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыНеОтгружать.Номенклатура,
	|		ТоварыНеОтгружать.Характеристика,
	|		ТоварыНеОтгружать.Назначение,
	|		ТоварыНеОтгружать.Упаковка,
	|		ТоварыНеОтгружать.Серия,
	|		ТоварыНеОтгружать.Количество,
	|		ТоварыНеОтгружать.КоличествоУпаковок,
	|		ТоварыНеОтгружать.Действие
	|	ИЗ
	|		ТоварыНеОтгружать КАК ТоварыНеОтгружать
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТоварыКОтгрузке.Номенклатура,
	|		ТоварыКОтгрузке.Характеристика,
	|		ТоварыКОтгрузке.Назначение,
	|		ТоварыКОтгрузке.Упаковка,
	|		ТоварыКОтгрузке.Серия,
	|		ТоварыКОтгрузке.Количество,
	|		ТоварыКОтгрузке.КоличествоУпаковок,
	|		ТоварыКОтгрузке.Действие
	|	ИЗ
	|		ТоварыКОтгрузке КАК ТоварыКОтгрузке) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Серия,
	|	ВложенныйЗапрос.Номенклатура,
	|	ВложенныйЗапрос.Характеристика,
	|	ВложенныйЗапрос.Назначение,
	|	ВложенныйЗапрос.Упаковка,
	|	ВложенныйЗапрос.Действие"; 
	
	Запрос.УстановитьПараметр("ТоварыКПроверке", ТоварыКПроверке.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтбору", ТоварыКОтбору.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыНеОтгружать", ТоварыНеОтгружать.Выгрузить());
	Запрос.УстановитьПараметр("ТоварыКОтгрузке", ТоварыКОтгрузке.Выгрузить());
	
	ОтгружаемыеТовары = Запрос.Выполнить().Выгрузить();
	
	НачатьТранзакцию();
	Попытка
		ОрдерОбъект = Ссылка.ПолучитьОбъект();
		
		ОрдерОбъект.Помещение   = Помещение;
		ОрдерОбъект.Комментарий = Комментарий;
		ОрдерОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
		
		Если Режим = "ОтгрузитьЗавершить" Тогда
			ОрдерОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.Проверен;
		Иначе
			ОрдерОбъект.Статус     = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
			ОрдерОбъект.Отгрузил   = Пользователи.ТекущийПользователь();
		КонецЕсли;
		
		ОрдерОбъект.ОтгружаемыеТовары.Загрузить(ОтгружаемыеТовары);
		
		ОрдерОбъект.ШтрихкодыУпаковок.Очистить();
		Для Каждого СтрокаШК Из ШтрихкодыУпаковок Цикл
			НоваяСтрока = ОрдерОбъект.ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ШтрихкодУпаковки = СтрокаШК.ШтрихкодУпаковки;
		КонецЦикла;

		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ОрдерОбъект, Документы.РасходныйОрдерНаТовары);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);

		ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение); 
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();

		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
									|en = 'Cannot save %1. %2'"), Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Функция ЗакончитьОтгрузкуНаСервере()
	
	НачатьТранзакцию();
	Попытка
		
		ОрдерОбъект = Ссылка.ПолучитьОбъект();
		
		ОрдерОбъект.Статус      = Перечисления.СтатусыРасходныхОрдеров.Отгружен;
		ОрдерОбъект.Помещение   = Помещение;
		ОрдерОбъект.Комментарий = Комментарий;
		ОрдерОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
		ОрдерОбъект.Отгрузил = Пользователи.ТекущийПользователь();
			
		ПараметрыУказанияСерий = Новый ФиксированнаяСтруктура(НоменклатураСервер.ПараметрыУказанияСерий(ОрдерОбъект, Документы.РасходныйОрдерНаТовары));
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ОрдерОбъект, ПараметрыУказанияСерий.ОтгружаемыеТовары);

		ОрдерОбъект.ШтрихкодыУпаковок.Очистить();
		Для Каждого СтрокаШК Из ШтрихкодыУпаковок Цикл		
			НоваяСтрока = ОрдерОбъект.ШтрихкодыУпаковок.Добавить();
			НоваяСтрока.ШтрихкодУпаковки = СтрокаШК.ШтрихкодУпаковки;
		КонецЦикла;
	
		ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение); 
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();

		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось записать %1. %2';
									|en = 'Cannot save %1. %2'"), Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗонуПриемки()
	
	ЗонаОтгрузки = Справочники.СкладскиеЯчейки.ЗонаПриемкиПоУмолчанию(Склад, Помещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМеню()

	Элементы.ГруппаМеню.Видимость = Истина;
	Элементы.ГруппаКоманды.Видимость = Истина;
	Элементы.ИнформацияОДокументе.Видимость = Истина;
	Элементы.ИнформацияОПолучателе.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьМеню()

	Элементы.ГруппаМеню.Видимость = Ложь;
	Элементы.ГруппаКоманды.Видимость = Ложь;
	Элементы.ИнформацияОДокументе.Видимость = Ложь;
	Элементы.ИнформацияОПолучателе.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Функция ОтгрузкаРазрешена(СвойстваТовара, Количество)
	
	СтруктураПоиска = Новый Структура;
	СтруктураПоиска.Вставить("Номенклатура", СвойстваТовара.Номенклатура);
	СтруктураПоиска.Вставить("Характеристика", СвойстваТовара.Характеристика);
	СтруктураПоиска.Вставить("Назначение", СвойстваТовара.Назначение);
	
	СерииПриОтгрузке = СерииНоменклатурыПриОтгрузке(СвойстваТовара.Номенклатура);

	Если Не СерииПриОтгрузке.УказыватьПриПланированииОтгрузки Тогда
		СтруктураПоиска.Удалить("Серия");
	КонецЕсли;
	
	ДоступноКОтгрузке = 0;
	РезультатПоиска = ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
	
	Для Каждого НайденнаяСтрока Из РезультатПоиска Цикл
		
		КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(НайденнаяСтрока.Упаковка, НайденнаяСтрока.Номенклатура);
		ДоступноКОтгрузке = ДоступноКОтгрузке + НайденнаяСтрока.Количество * КоэффициентУпаковки;
		
	КонецЦикла;
	
	Если Количество > ДоступноКОтгрузке Тогда
		ТекстОшибки = НСтр("ru = 'Количество товара, которое нужно отгрузить по распоряжениям на отгрузку не равно количеству, которое отгружается ордером.';
							|en = 'Quantity of goods that needs to be shipped under shipment references is not equal to the quantity shipped under the note.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	КонецЕсли;
	
	ДоступноеКоличество = 0;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыНаСкладах.КОтгрузке КАК КОтгрузке
		|ИЗ
		|	РегистрНакопления.ТоварыНаСкладах КАК ТоварыНаСкладах
		|ГДЕ
		|	ТоварыНаСкладах.Регистратор = &Регистратор
		|	И ТоварыНаСкладах.Номенклатура = &Номенклатура
		|	И ТоварыНаСкладах.Характеристика = &Характеристика
		|	И ТоварыНаСкладах.Назначение = &Назначение";
	
	Запрос.УстановитьПараметр("Назначение", СвойстваТовара.Назначение);
	Запрос.УстановитьПараметр("Номенклатура", СвойстваТовара.Номенклатура);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
		
	Если Не СерииПриОтгрузке.УказыватьПоФактуОтбора Тогда
		Запрос.Текст = Запрос.Текст + "
		|	И ТоварыНаСкладах.Серия = &Серия";
		Запрос.УстановитьПараметр("Серия", СвойстваТовара.Серия);
	КонецЕсли;
	Запрос.УстановитьПараметр("Характеристика", СвойстваТовара.Характеристика);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ДоступноеКоличество = ДоступноеКоличество + ВыборкаДетальныеЗаписи.КОтгрузке;
	КонецЦикла;
	
	Если ДоступноеКоличество >= Количество Тогда
		Возврат Истина;
	КонецЕсли;
	
	КонтролироватьСвободныеОстатки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "КонтролироватьСвободныеОстатки");

	Если СерииПриОтгрузке.УказыватьПриПланированииОтбора
		И СерииПриОтгрузке.УказыватьПриПланированииОтгрузки
		И КонтролироватьСвободныеОстатки Тогда
	
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЕСТЬNULL(СУММА(ТоварыНаСкладахОстатки.ВНаличииОстаток - ТоварыНаСкладахОстатки.КОтгрузкеОстаток), 0) КАК Количество
			|ИЗ
			|	РегистрНакопления.ТоварыНаСкладах.Остатки КАК ТоварыНаСкладахОстатки
			|ГДЕ
			|	ТоварыНаСкладахОстатки.Номенклатура = &Номенклатура
			|	И ТоварыНаСкладахОстатки.Характеристика = &Характеристика
			|	И ТоварыНаСкладахОстатки.Назначение = &Назначение
			|	И ТоварыНаСкладахОстатки.Серия = &Серия";
		
		Запрос.УстановитьПараметр("Назначение", СвойстваТовара.Назначение);
		Запрос.УстановитьПараметр("Номенклатура", СвойстваТовара.Номенклатура);
		Запрос.УстановитьПараметр("Серия", СвойстваТовара.Серия);
		Запрос.УстановитьПараметр("Характеристика", СвойстваТовара.Характеристика);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			ДоступноеКоличество = ДоступноеКоличество + ВыборкаДетальныеЗаписи.Количество;
		КонецЦикла;
	КонецЕсли;
	
	Если Количество > ДоступноеКоличество Тогда
		ТекстОшибки = НСтр("ru = 'Отгружаемое количество превышает остатки на складе.';
							|en = 'The quantity to ship exceeds the warehouse balance.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция НайтиТоварСервер(Штрихкод)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиТоварПоШК(Штрихкод);
	
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияВопросаПроверки(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = "Продолжить" Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ИмяПроцесса", "ЗавершитьПроверку");
	ДополнительныеПараметры.Вставить("Режим", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДополнительныеПараметры);
	
	СписокЗначений = Новый СписокЗначений();
	
	Если Результат = "Отгрузить" Или Результат = "ОтгрузитьБезПроверки" Тогда 
		
		СписокЗначений.Добавить("Отгрузить", НСтр("ru = 'Перейти к отгрузке';
													|en = 'Go to shipment'"));
		СписокЗначений.Добавить("ОтгрузитьЗавершить", НСтр("ru = 'Завершить работу с ордером';
															|en = 'Close the note'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить проверку';
													|en = 'Continue checking'"));
		ТекстСообщения = НСтр("ru = 'После завершения проверки перейти к отгрузке ордера или завершить работу с текущим ордером?';
								|en = 'After the check is completed, do you want to proceed to shipping the goods issue or close the current goods issue?'");
		
		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
		
	ИначеЕсли Результат = "Отобрать" Тогда 
		
		СписокЗначений.Добавить("Отобрать", НСтр("ru = 'Перейти к отбору';
												|en = 'Go to filter'"));
		СписокЗначений.Добавить("ОтобратьЗавершить", НСтр("ru = 'Завершить работу с ордером';
															|en = 'Close the note'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить проверку';
													|en = 'Continue checking'"));
		ТекстСообщения = НСтр("ru = 'После завершения проверки перейти к отбору ордера или завершить работу с текущим ордером?';
								|en = 'After the check is completed, do you want to proceed to picking the goods issue or close the current goods issue?'");
		
		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
		
	Иначе
		ПослеЗакрытияВопроса(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = "Продолжить" Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ИмяПроцесса = "ЗавершитьОтбор" Тогда
		
		ОтборЗавершен = ЗакончитьОтборНаСервере(Результат);
		Если Результат = "Завершить" И ОтборЗавершен Тогда
			Закрыть();
		ИначеЕсли Результат = "Проверить" И ОтборЗавершен Тогда
			ЗаполнитьФорму();
			УстановитьУсловноеОформление();
			ОформитьМеню("КПроверке");
		КонецЕсли; 
		
	ИначеЕсли ДополнительныеПараметры.ИмяПроцесса = "ЗавершитьПроверку" Тогда
		
		Если ДополнительныеПараметры.Режим = "ОтгрузитьБезПроверки" Тогда
			ПроверкаЗавершена = ОтгрузитьБезПроверки(Результат);
		Иначе
			ПроверкаЗавершена = ЗакончитьПроверкуНаСервере(Результат);
		КонецЕсли;
		
		Если (Результат = "ОтгрузитьЗавершить" Или Результат = "ОтобратьЗавершить"
			Или Результат = "ИзлишекЗавершить") И ПроверкаЗавершена Тогда
			Закрыть();
		ИначеЕсли (Результат = "Отгрузить" Или Результат = "Отобрать"
			Или Результат = "ИзлишекОтгрузить") И ПроверкаЗавершена Тогда
				
			Если ИспользоватьАдресноеХранение И Результат = "Отобрать" Тогда 
				
				СсылкаОтбор = НовыйОтборТоваров(Ссылка);
				Если ЗначениеЗаполнено(СсылкаОтбор) Тогда
					Закрыть();
					ПараметрыФормы = Новый Структура;
					ПараметрыФормы.Вставить("Ссылка", СсылкаОтбор);
					ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
					ПараметрыФормы.Вставить("Бесшовный", Истина);
					ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
					
					ОткрытьФорму(
						"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор", ПараметрыФормы);
					Возврат;
				Иначе
					ТекстОшибки = НСтр("ru = 'Не удалось сформировать задания на отбор товаров согласно правилам отбора.';
										|en = 'Cannot generate goods picking jobs according to the picking rules.'");
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
				КонецЕсли;
				
			КонецЕсли;
			
			ЗаполнитьФорму();
			УстановитьУсловноеОформление();
			ОткрытьМенюПоСтатусу();
		КонецЕсли;
		
	ИначеЕсли ДополнительныеПараметры.ИмяПроцесса = "ЗавершитьОтгрузку" Тогда  
		
		ОтгрузкаЗавершена = ЗакончитьОтгрузкуНаСервере();
		
		Если ОтгрузкаЗавершена Тогда  
			Закрыть();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаУказаниеСерий(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = "Продолжить" Тогда
		Возврат;
	КонецЕсли;
	
	УказаниеСерийЗавершено = ЗакончитьУказаниеСерий();
	
	Если Результат = "Отобрать" И УказаниеСерийЗавершено Тогда 
		
		СсылкаОтбор = НовыйОтборТоваров(Ссылка);
		Если ЗначениеЗаполнено(СсылкаОтбор) Тогда
			Закрыть();
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ссылка", СсылкаОтбор);
			ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
			ПараметрыФормы.Вставить("Бесшовный", Истина);
			ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
			
			ОткрытьФорму(
				"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор", ПараметрыФормы);
			Возврат;
		Иначе
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать задания на отбор товаров согласно правилам отбора.';
								|en = 'Cannot generate goods picking jobs according to the picking rules.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
		КонецЕсли;
		
	ИначеЕсли Результат = "ОтобратьЗавершить" И УказаниеСерийЗавершено Тогда 
		
		Закрыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШтрихкодыУпаковок(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьШтрихкодыУпаковокНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьШтрихкодыУпаковокНаСервере(Результат)
	
	ШтрихкодыУпаковок.Очистить();
	ШтрихкодыУпаковок.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОтменитьОтгрузку(Результат, Параметры) Экспорт
	
	Если Результат = "Завершить" Тогда
		
		ОтгрузкаОтменена = ОтменитьОтгрузкуОрдераНаСервере();
		
		Если ОтгрузкаОтменена Тогда
			Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
				Закрыть();
			КонецЕсли;
			Закрыть();
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтменитьОтгрузкуОрдераНаСервере()
	
	Попытка
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Истина);
		
		ОбеспечениеВДокументахСервер.ПроверитьЗапуститьФоновоеЗаданиеРаспределенияЗапасов();
	Исключение
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось отменить отгрузку.';
													|en = 'Cannot cancel the shipment.'"));
		
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Функция	ЕстьОшибкиЗаполнения()
	
	ОчиститьСообщения();
	
	Если ИспользоватьСкладскиеПомещения И Помещение.Пустая() Тогда
			
		ТекстОшибки = НСтр("ru = 'Не выбрано помещение';
							|en = 'Wareroom is not selected'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
			
		Возврат Истина;
			
	КонецЕсли;
	
	Если ИспользоватьАдресноеХранение И ЗонаОтгрузки.Пустая() Тогда
			
		ТекстОшибки = НСтр("ru = 'Не выбрана зона отгрузки';
							|en = 'Outbound area is not selected'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
			
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
			
		Возврат Истина;
			
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции	
	
&НаСервере
Процедура УстановитьДоступностьРедактированияОрдера()
		
	Если Статус = Перечисления.СтатусыРасходныхОрдеров.Подготовлен
		Или Статус = Перечисления.СтатусыРасходныхОрдеров.Проверен Тогда
		
		Элементы.НазначитьМне.Видимость = Не УказаниеСерий; 
		Элементы.Готово.Видимость = УказаниеСерий;
		Элементы.Сканировать.Видимость = Ложь;
	ИначеЕсли Статус = Перечисления.СтатусыРасходныхОрдеров.КПроверке Тогда 
		
		ПроверкаСвоегоОрдера = ПроверкаТолькоСвоихОрдеров И Исполнитель = Пользователи.ТекущийПользователь();
		ПроверкаЧужогоОрдера = ПроверкаТолькоЧужихОрдеров И Исполнитель <> Пользователи.ТекущийПользователь();
	
		Элементы.НазначитьМне.Видимость = ПроверкаСвоегоОрдера Или ПроверкаЧужогоОрдера Или ПроверкаВсехОрдеров;
		Элементы.Сканировать.Видимость = Ложь;
	Иначе
		Элементы.НазначитьМне.Видимость = Ложь;
		Элементы.Готово.Видимость = УказаниеСерий;
		Элементы.Сканировать.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры 

&НаСервере
Процедура НеОтгружатьНаСервере(ИндексСтроки, ИмяТаблицы)
	
	ТекущаяСтрока = ЭтотОбъект[ИмяТаблицы][ИндексСтроки];
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура);

	СтруктураПоиска = СтруктураПоискаТоваров();
	
	СерииПриОтгрузке = СерииНоменклатурыПриОтгрузке(ТекущаяСтрока.Номенклатура);	
	Если СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
		СтруктураПоиска.Удалить("Серия");
	КонецЕсли; 
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
	
	СтруктураПоиска.Вставить("Действие", Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать);
	
	РезультатПоиска = ТоварыНеОтгружать.НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + ТекущаяСтрока.КоличествоУпаковок;
		Количество = КоличествоУпаковок * КоэффициентУпаковки;
		
		РезультатПоиска[0].КоличествоУпаковок = КоличествоУпаковок;
		РезультатПоиска[0].Количество = Количество;
	Иначе
		НоваяСтрока = ТоварыНеОтгружать.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.НеОтгружать;
		Если СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
			НоваяСтрока.Серия = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	КоличествоКРаспределению = ТекущаяСтрока.Количество;
			
	СтруктураПоиска.Удалить("Упаковка");
	СтруктураПоиска.Удалить("Серия");
	СтруктураПоиска.Удалить("Действие");
		
	НайденныеРаспоряжения = ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
	Для Каждого СтрокаРаспоряжения Из НайденныеРаспоряжения Цикл    
		Если КоличествоКРаспределению >= СтрокаРаспоряжения.Количество Тогда
			КоличествоКРаспределению = КоличествоКРаспределению - СтрокаРаспоряжения.Количество;
			ТоварыПоРаспоряжениям.Удалить(СтрокаРаспоряжения);
		Иначе
			СтрокаРаспоряжения.Количество = СтрокаРаспоряжения.Количество - КоличествоКРаспределению;
			КоличествоКРаспределению = 0;
		КонецЕсли;
			
		Если КоличествоКРаспределению = 0 Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла;
	
	ЭтотОбъект[ИмяТаблицы].Удалить(ИндексСтроки);
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Процедура НазначитьМнеНаСервере()
	
	Если Статус = Перечисления.СтатусыРасходныхОрдеров.Подготовлен Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.КОтбору;
	ИначеЕсли Статус = Перечисления.СтатусыРасходныхОрдеров.КПроверке Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки;
	ИначеЕсли Статус = Перечисления.СтатусыРасходныхОрдеров.Проверен Тогда
		СтатусОрдера = Перечисления.СтатусыРасходныхОрдеров.КОтгрузке;
	КонецЕсли;
	
	Если Не Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(Ссылка, СтатусОрдера, Ложь) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьФорму();
	УстановитьДоступностьРедактированияОрдера();
	
КонецПроцедуры  

&НаСервере
Функция СерииНоменклатурыПриОтгрузке(Знач Номенклатура)
	
	ВидНоменклатуры =  ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры");
	ПолитикаУчетаСерий = Обработки.МобильноеРабочееМестоКладовщика.ПолитикаУчетаСерий(ВидНоменклатуры, Склад);
	Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда
		СерииПриОтгрузке = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолитикаУчетаСерий, "УказыватьПоФактуОтбора,
		|	УказыватьПриПланированииОтбора, УказыватьПриПланированииОтгрузки");
	Иначе
		СерииПриОтгрузке = Новый Структура("УказыватьПоФактуОтбора,
		|	УказыватьПриПланированииОтбора, УказыватьПриПланированииОтгрузки", Ложь, Ложь, Ложь);
	КонецЕсли;
	
	Возврат СерииПриОтгрузке;
	
КонецФункции 

&НаКлиенте
Процедура ОткрытьМенюПоСтатусу()
	
	Если Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.КОтбору")
		Или Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.Подготовлен") Тогда
		ОформитьМеню("КОтбору");
	ИначеЕсли Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.ВПроцессеПроверки")
		Или Статус = ПредопределенноеЗначение("Перечисление.СтатусыРасходныхОрдеров.КПроверке") Тогда
		ОформитьМеню("КПроверке");
	Иначе
		ОформитьМеню("КОтгрузке");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВернутьКОтборуПроверкаНаСервере(ИндексСтроки)
	 		
	ТекущаяСтрока = ТоварыКПроверке[ИндексСтроки];
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура);
	
	СтруктураПоиска = СтруктураПоискаТоваров();
	
	СерииПриОтгрузке = СерииНоменклатурыПриОтгрузке(ТекущаяСтрока.Номенклатура);	
	Если СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
		СтруктураПоиска.Удалить("Серия");
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
	
	СтруктураПоиска.Вставить("Действие", ПредопределенноеЗначение("Перечисление.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать"));
	
	РезультатПоиска = ТоварыКОтбору.НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + ТекущаяСтрока.КоличествоУпаковок;
		Количество = КоличествоУпаковок * КоэффициентУпаковки;
		
		РезультатПоиска[0].КоличествоУпаковок = КоличествоУпаковок;
		РезультатПоиска[0].Количество = Количество;
	Иначе
		НоваяСтрока = ТоварыКОтбору.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока); 
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;

		Если СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
			НоваяСтрока.Серия = Неопределено; 
		КонецЕсли;
		
		Если ИспользоватьАдресноеХранениеСправочно Тогда
			НоваяСтрока.ОсновнаяЯчейка = ЯчейкаНоменклатуры(ТекущаяСтрока.Номенклатура);
		КонецЕсли;

	КонецЕсли;
		
	ТоварыКПроверке.Удалить(ИндексСтроки);
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Процедура ВернутьНеОтгружатьНаСервере(ИндексСтроки, ИмяТаблицы)
	
	ТекущаяСтрока = ТоварыНеОтгружать[ИндексСтроки];
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура);

	СтруктураПоиска = СтруктураПоискаТоваров();
	
	СерииПриОтгрузке = СерииНоменклатурыПриОтгрузке(ТекущаяСтрока.Номенклатура);
	Если СерииПриОтгрузке.УказыватьПриПланированииОтбора 
		Или (Не ИспользоватьСтатусыРасходныхОрдеров И СерииПриОтгрузке.УказыватьПоФактуОтбора) Тогда
		СтруктураПоиска.Удалить("Серия");
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
		
	СтруктураПоиска.Вставить("Действие", Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать);
	
	РезультатПоиска = ЭтотОбъект[ИмяТаблицы].НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + ТекущаяСтрока.КоличествоУпаковок;
		Количество = КоличествоУпаковок * КоэффициентУпаковки;
		
		РезультатПоиска[0].КоличествоУпаковок = КоличествоУпаковок;
		РезультатПоиска[0].Количество = Количество;
	Иначе
		НоваяСтрока = ЭтотОбъект[ИмяТаблицы].Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока); 
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		НоваяСтрока.Действие = Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать;

		Если СерииПриОтгрузке.УказыватьПриПланированииОтбора 
			Или (Не ИспользоватьСтатусыРасходныхОрдеров И СерииПриОтгрузке.УказыватьПоФактуОтбора) Тогда
			НоваяСтрока.Серия = Неопределено;
		КонецЕсли; 
		
		Если ИспользоватьАдресноеХранениеСправочно Тогда
			НоваяСтрока.ОсновнаяЯчейка = ЯчейкаНоменклатуры(ТекущаяСтрока.Номенклатура);
		КонецЕсли;
	КонецЕсли;
	
	СтруктураПоиска.Удалить("Упаковка");
	СтруктураПоиска.Удалить("Серия");
	СтруктураПоиска.Удалить("Действие");
		
	НайденныеРаспоряжения = ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
	
	Если НайденныеРаспоряжения.Количество() > 0 Тогда 	
		НайденныеРаспоряжения[0].Количество = НайденныеРаспоряжения[0].Количество + ТекущаяСтрока.Количество;
	Иначе
		НоваяСтрока = ТоварыПоРаспоряжениям.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Количество = ТекущаяСтрока.КоличествоУпаковок * КоэффициентУпаковки; 
	КонецЕсли;
	
	ТоварыНеОтгружать.Удалить(ИндексСтроки);
	Если Не ТоварыНеОтгружать.Количество() Тогда
		ФильтрТовары = "Отгружаемые";
		ФильтрТоварыОтобразитьТаблицу();
	КонецЕсли;
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Процедура ФильтрТоварыОтобразитьТаблицу()
	
	Если ФильтрТовары = "НеОтгружаемые" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаНеОтгружать;
	ИначеЕсли Элементы.КомандаКОтбору.ЦветТекста = WebЦвета.Черный Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКОтбору;
	ИначеЕсли Элементы.КомандаКПроверке.ЦветТекста = WebЦвета.Черный Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКПроверке;
	ИначеЕсли Элементы.КомандаКОтгрузке.ЦветТекста = WebЦвета.Черный Тогда 
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.СтраницаКОтгрузке;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Функция ЯчейкаНоменклатуры(Номенклатура)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ОсновныеЯчейки.Ячейка.Код, """") КАК ОсновнаяЯчейка
	|ИЗ
	|	РегистрСведений.РазмещениеНоменклатурыПоСкладскимЯчейкам КАК ОсновныеЯчейки
	|ГДЕ
	|	ОсновныеЯчейки.Номенклатура = &Номенклатура 
	|	И ОсновныеЯчейки.ОсновнаяЯчейка
	|	И ОсновныеЯчейки.Склад = &Склад 
	|	И ВЫБОР
	|		КОГДА &ИспользоватьСкладскиеПомещения
	|			И &ДатаОтгрузки >= &ДатаНачалаИспользованияСкладскихПомещений
	|			ТОГДА ОсновныеЯчейки.Помещение = &Помещение
	|		ИНАЧЕ ОсновныеЯчейки.Помещение = ЗНАЧЕНИЕ(Справочник.СкладскиеПомещения.ПустаяСсылка)
	|	КОНЕЦ";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ИспользоватьСкладскиеПомещения", ИспользоватьСкладскиеПомещения);
	Запрос.УстановитьПараметр("ДатаОтгрузки", ДатаОтгрузки);
	Запрос.УстановитьПараметр("ДатаНачалаИспользованияСкладскихПомещений", ДатаНачалаИспользованияСкладскихПомещений);
	Запрос.УстановитьПараметр("Помещение", Помещение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОсновнаяЯчейка;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

&НаСервере
Функция СтруктураПоискаТоваров()
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура","");
	Структура.Вставить("Характеристика","");
	Структура.Вставить("Серия","");
	Структура.Вставить("Назначение","");
	Структура.Вставить("Упаковка","");
	
	Возврат Структура;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйОтборТоваров(СсылкаРасходныйОрдер)
		
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НовыйОтборТоваров(СсылкаРасходныйОрдер);

КонецФункции

#КонецОбласти