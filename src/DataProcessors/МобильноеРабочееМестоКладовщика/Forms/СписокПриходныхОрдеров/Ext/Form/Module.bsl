
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	Склад                 = Параметры.Склад;
	Помещение             = Параметры.Помещение;
	Исполнитель           = Параметры.Исполнитель;
	
	ОбновитьСписокСортировок(Параметры.Исполнитель);
	ОбновитьСписокСтатусов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СформироватьПредставлениеОтборов();
	СформироватьПредставлениеОтбораПомещение();
	
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Склады.Форма.ФормаВыбораМРМ") Тогда
		
		Склад = ВыбранноеЗначение;
		Помещение = Неопределено;
		СформироватьПредставлениеОтборов();
		СформироватьПредставлениеОтбораПомещение();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ") Тогда
		
		Помещение = ВыбранноеЗначение;
		СформироватьПредставлениеОтбораПомещение();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокОрдеров

&НаКлиенте
Процедура СписокОрдеровВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.СписокОрдеров.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ссылка", ТекущаяСтрока.Ссылка);
	ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	
	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтаФорма);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.ПриходныйОрдер",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборыОткрыть(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
		
	Отбор = Новый Структура();
	Отбор.Вставить("ИспользоватьОрдернуюСхемуПриПоступлении", Истина);
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму(
		"Справочник.Склады.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Команда)
	ОбновитьДанныеФормы();
	СформироватьПредставлениеОтборов();
КонецПроцедуры 

&НаКлиенте
Процедура ОтборыОчиститьСклад(Команда)
	
	Склад = Неопределено;
	Помещение = Неопределено;
	СформироватьПредставлениеОтборов();
	СформироватьПредставлениеОтбораПомещение();
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОткрытьПомещение(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Владелец", Склад);
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму(
		"Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтаФорма,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПомещение(Команда)
	
	Помещение = Неопределено;
	СформироватьПредставлениеОтбораПомещение();
	ОбновитьДанныеФормы(); 
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент) 
	
	СформироватьПредставлениеОтборов();
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормыСервер()
	
	ЗаполнитьСписокОрдеров();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокОрдеров()

	ИспользоватьСтатусыПриходныхОрдеров = Ложь; 
	Если ЗначениеЗаполнено(Склад) Тогда
		ИспользоватьСтатусыПриходныхОрдеров = Склад["ИспользоватьСтатусыПриходныхОрдеров"];
	КонецЕсли;	
	Элементы.ОтборыСтатус.Видимость = ИспользоватьСтатусыПриходныхОрдеров;
	
	СписокОрдеров.Очистить();
	
	Запрос = Обработки.МобильноеРабочееМестоКладовщика.СписокПриходныхОрдеров();
	
	Запрос.УстановитьПараметр("Склад", Склад);
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &УсловиеПомещение", "И ПриходныйОрдерНаТовары.Помещение = &Помещение");
		Запрос.УстановитьПараметр("Помещение", Помещение);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &УсловиеПомещение", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Исполнитель) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &УсловиеИсполнитель", "И ПриходныйОрдерНаТовары.Исполнитель = &Исполнитель");
		Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И &УсловиеИсполнитель", "");
	КонецЕсли;
	
	Если ИспользоватьСтатусыПриходныхОрдеров Тогда
		Статусы = Новый Массив;
		Если Статус = "ВсеНепринятые" Тогда 
			Статусы.Добавить(Перечисления.СтатусыПриходныхОрдеров.КПоступлению);
			Статусы.Добавить(Перечисления.СтатусыПриходныхОрдеров.ВРаботе);
		ИначеЕсли Статус = "Все" Тогда
			Для Каждого Значение Из Перечисления.СтатусыПриходныхОрдеров Цикл 
				Статусы.Добавить(Значение);
			КонецЦикла;
		Иначе
			Статусы.Добавить(Перечисления.СтатусыПриходныхОрдеров[Статус]);
		КонецЕсли;
		Запрос.УстановитьПараметр("Статусы", Статусы);
	Иначе
	    Запрос.Текст = СтрЗаменить(Запрос.Текст, "И ПриходныйОрдерНаТовары.Статус В (&Статусы)", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.Текст = Запрос.Текст + "
	|	И ПриходныйОрдерНаТовары.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода + 60*60*24);
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	";
	
	Если Сортировка = "Отправитель" Тогда
		Запрос.Текст = Запрос.Текст + "Отправитель.Наименование";
	Иначе
		Запрос.Текст = Запрос.Текст + Сортировка;
	КонецЕсли;
	
	Если НаправлениеСортировки = "ПоУбыванию" Тогда
		Запрос.Текст = Запрос.Текст + " УБЫВ";
	Иначе
		Запрос.Текст = Запрос.Текст + " ВОЗР";
	КонецЕсли;
	
	СписокОрдеров.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтборов()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Склад) Тогда
		ПредставленияОтборов = Склад;
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчистить.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Склад';
									|en = 'Warehouse'");
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчистить.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Если Склад["ЭтоГруппа"] Тогда
		ИспользоватьСкладскиеПомещения = Ложь;
	Иначе
		ИспользоватьСкладскиеПомещения = Склад["ИспользоватьСкладскиеПомещения"];
	КонецЕсли;
	Элементы.ГруппаКомандаОтборыОткрыть2.Видимость    = ИспользоватьСкладскиеПомещения;
	Элементы.КомандаОтборыОчиститьПомещение.Видимость = ИспользоватьСкладскиеПомещения;
	
	Элементы.КомандаОтборыОткрыть.Заголовок = ПредставленияОтборов;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПредставлениеОтбораПомещение()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Помещение) Тогда
		ПредставленияОтборов = Помещение;
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Выбрать помещение';
									|en = 'Select a wareroom'");
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.КомандаОтборыОткрытьПомещение.Заголовок = ПредставленияОтборов;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСтатусов()
	
	Элементы.Статус.СписокВыбора.Очистить();
	
	Элементы.Статус.СписокВыбора.Добавить("Все", НСтр("ru = 'Все';
														|en = 'All'"));
	Элементы.Статус.СписокВыбора.Добавить("ВсеНепринятые", НСтр("ru = 'Все непринятые';
																|en = 'All rejected'"));		
	Элементы.Статус.СписокВыбора.Добавить("КПоступлению", НСтр("ru = 'К поступлению';
																|en = 'To receive'"));
	Элементы.Статус.СписокВыбора.Добавить("ВРаботе", НСтр("ru = 'В работе';
															|en = 'In progress'"));
	Элементы.Статус.СписокВыбора.Добавить("ТребуетсяОбработка", НСтр("ru = 'Требуется обработка';
																	|en = 'Processing is required'"));
	Элементы.Статус.СписокВыбора.Добавить("Принят", НСтр("ru = 'Принят';
														|en = 'Accepted'"));
	
	Статус = "ВсеНепринятые";
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСортировок(ТекущийПользователь)
	
	Элементы.Исполнитель.СписокВыбора.Очистить();
	
	Элементы.Исполнитель.СписокВыбора.Добавить(ТекущийПользователь, НСтр("ru = '<Мои документы>';
																		|en = '<My documents>'"));
	
	Элементы.Сортировка.СписокВыбора.Очистить();
	Элементы.Сортировка.СписокВыбора.Добавить("Дата", НСтр("ru = 'Дата';
															|en = 'Date'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Номер", НСтр("ru = 'Номер';
															|en = 'Number'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Отправитель", НСтр("ru = 'Отправитель';
																	|en = 'Sender'"));
	Сортировка = "Дата";
	
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	ОбновитьДанныеФормы();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьПериодЗавершение",
		ЭтотОбъект);
		
	МобильноеРабочееМестоКладовщикаКлиент.РедактироватьПериод(
		ЭтотОбъект,
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоПериода", "КонецПериода"),
		Оповещение);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		ПериодПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода Тогда
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если КонецПериода < НачалоПериода И ЗначениеЗаполнено(КонецПериода) Или Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = КонецПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСортировку(Команда)
	
	Сортировка = "Дата";
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПериод(Команда)
	
	НачалоПериода =  '00010101';
	КонецПериода =  '00010101';
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьИсполнителя(Команда)
	
	Исполнитель = Неопределено;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСтатус(Команда)
	
	Статус = "Все";
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаправлениеСортировки(Команда)
	
	Если Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию Тогда
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоУбыванию;
		НаправлениеСортировки = "ПоУбыванию";
	Иначе
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
		НаправлениеСортировки = "ПоВозрастанию";
	КонецЕсли;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

#КонецОбласти
