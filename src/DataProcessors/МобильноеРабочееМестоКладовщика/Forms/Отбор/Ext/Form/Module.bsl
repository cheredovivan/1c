
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Литерал = "Ссылка";
	Ссылка = Параметры[Литерал];
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров; 
	Бесшовный = Параметры.Бесшовный;
	
	ЗаблокироватьДанныеДляРедактирования(Ссылка,,УникальныйИдентификатор);
	
	ЗаполнитьФорму();
		
	//++ Локализация
		
	ИспользоватьУчетМаркируемойПродукции = Константы.ВестиУчетМаркируемойПродукцииИСМП.Получить();
	ПроверкаИПодборПродукцииИСМППереопределяемый.ПриОпределенииОрганизации(Ссылка, Организация);
	
	ЕстьМаркируемаяПродукция = Ложь;
	ВидыПродукцииИСМП = МобильноеРабочееМестоКладовщикаЛокализация.ВидыПродукцииИСМП();
	ПроверкаИПодборПродукцииИСМППереопределяемый.ЕстьМаркируемаяПродукцияВКоллекции(ТоварыОтобрать, ВидыПродукцииИСМП, ЕстьМаркируемаяПродукция);
	Если НЕ ЕстьМаркируемаяПродукция Тогда
		ПроверкаИПодборПродукцииИСМППереопределяемый.ЕстьМаркируемаяПродукцияВКоллекции(ТоварыОтобрано, ВидыПродукцииИСМП, ЕстьМаркируемаяПродукция);
	КонецЕсли;
	
	//-- Локализация
	
	ЕстьПравоНаПроверкуОрдера = Обработки.МобильноеРабочееМестоКладовщика.ЕстьПравоНаПроверкуСвоихОрдеров();
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОформитьМеню("Отобрать");
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
		СтандартнаяОбработка = Ложь;
		Отказ = Истина;
		ОформитьМеню("Отобрать");
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПомещениеПриИзменении(Элемент)
	
	ЗаполнитьЗонуПриемки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыОтобрать

&НаКлиенте
Процедура ТоварыОтобратьВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	ПараметрыФормы.Вставить("Режим", "ОтборИзЯчейкиОтгрузка");
	ПараметрыФормы.Вставить("НомерСтроки", ТоварыОтобрать.Индекс(Элементы.ТоварыОтобрать.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Ячейка", Элемент.ТекущиеДанные.Ячейка);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);

	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТоварыОтобрано

&НаКлиенте
Процедура ТоварыОтобраноВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	ПараметрыФормы.Вставить("Режим", "РедактированиеОтборИзЯчейкиОтгрузка");
	ПараметрыФормы.Вставить("НомерСтроки", ТоварыОтобрано.Индекс(Элементы.ТоварыОтобрано.ТекущиеДанные));
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Ячейка", Элемент.ТекущиеДанные.Ячейка);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);

	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Информация(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
	СкрытьМеню();
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделОтобрать(Команда)
	
	ОформитьМеню("Отобрать");
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВРазделОтобрано(Команда)
	
	ОформитьМеню("Отобрано");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьОтбор(Команда)
	
	Если ТоварыОтобрано.Количество() = 0 Тогда
		ТекстОшибки = НСтр("ru = 'Не возможно закончить отбор, пока не отобрано ни одного товара.';
							|en = 'Cannot finish picking until at least one item is selected.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли; 
	
	Если ИспользоватьСкладскиеПомещения И Помещение.Пустая() Тогда
		ТекстОшибки = НСтр("ru = 'Не выбрано помещение.';
							|en = 'The wareroom is not selected.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
		Возврат;
	КонецЕсли;
	
	Если ЗонаОтгрузки.Пустая() Тогда	
		ТекстОшибки = НСтр("ru = 'Не выбрана зона отгрузки.';
							|en = 'The outbound area is not selected.'");
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = ТекстОшибки;
		СообщениеПользователю.Сообщить();
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений;	

	Если ТоварыОтобрать.Количество() И Бесшовный Тогда
		СписокЗначений.Добавить("НеОтгружать", НСтр("ru = 'Отменить отгрузку товаров';
													|en = 'Cancel goods shipment'"));
		СписокЗначений.Добавить("КОтбору", НСтр("ru = 'Оставить ордер к отбору';
												|en = 'Leave the note for picking'"));
		СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить отбор';
													|en = 'Continue picking'"));
		ТекстСообщения = НСтр("ru = 'Отобраны не все товары, отгружаемые ордером. Отменить отгрузку не отобранных товаров в ордере и перевести ордер к проверке или оставить ордер к отбору?';
								|en = 'Not all the goods to ship under the goods issue are picked. Do you want to cancel shipment of unpicked goods in the goods issue and move the goods issue to check, or leave the goods issue for picking?'");
		
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПродолжение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
	Иначе
		ПослеЗакрытияВопросаПродолжение("Завершить");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СобратьВсе(Команда)
	
	СобратьВсеНаСервере();
	ОформитьМеню("Принято");
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	Описание = Новый ОписаниеОповещения("РезультатПоискаЯчейки", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоиска", "ПоискЯчейки");
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("ДокументСсылка", Ссылка);
	ПараметрыФормы.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура КодыМаркировки(Команда)
	Описание = Новый ОписаниеОповещения("ИзменитьШтрихкодыУпаковок", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СписокКодовМаркировки",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
		
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОтбор(Команда)
	
	ИндексСтроки = ТоварыОтобрано.Индекс(Элементы.ТоварыОтобрано.ТекущиеДанные);
	ОтменитьОтборСервер(ИндексСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьЗаданиеНаОтбор(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Завершить", НСтр("ru = 'Пометить документ на удаление';
												|en = 'Mark the document for deletion'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить отбор';
												|en = 'Continue picking'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОтменитьЗаданиеНаОтбор", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Для отмены отбора необходимо пометить на удаление документ отбора. Вернуться к редактированию будет невозможно. Пометить на удаление?';
									|en = 'To cancel picking, mark the picking document for deletion. It will be impossible to get back to editing. Mark the document for deletion?'"), СписокЗначений, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	Элементы.КодыМаркировки.Видимость = ИспользоватьУчетМаркируемойПродукции И ЕстьМаркируемаяПродукция;
	
	#Если МобильныйАвтономныйСервер Тогда
		Элементы.ФормаОтменитьЗаданиеНаОтбор.Видимость = Ложь;
	#Иначе
		Статус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
		Если Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.Подготовлено 
			И ПравоДоступа("ИнтерактивнаяПометкаУдаления", Метаданные.Документы.ОтборРазмещениеТоваров) Тогда
			Элементы.ФормаОтменитьЗаданиеНаОтбор.Видимость = Истина;
		Иначе
			Элементы.ФормаОтменитьЗаданиеНаОтбор.Видимость = Ложь;
		КонецЕсли;
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатПоискаЯчейки(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ЗавершитьРазмещениеВЯчейку", ЭтотОбъект);
	
	ТоварыВЯчейке = Новый Массив;
	
	Для Каждого Строка Из ТоварыОтобрать Цикл
		
		Если Строка.Ячейка = Результат Тогда
			
			Структура = СтруктураТоваров();
			ЗаполнитьЗначенияСвойств(Структура, Строка);
			
			ТоварыВЯчейке.Добавить(Структура);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ячейка",      Результат);
	ПараметрыФормы.Вставить("Товары",      ТоварыВЯчейке);
	ПараметрыФормы.Вставить("Склад",       Склад);
	ПараметрыФормы.Вставить("Помещение",   Помещение);
	ПараметрыФормы.Вставить("ТипОперации", 2);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Ссылка",      Ссылка);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.РазмещениеВЯчейки",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРазмещениеВЯчейку(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗавершитьРазмещениеВЯчейкуНаСервере(Результат);
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Процедура ЗавершитьРазмещениеВЯчейкуНаСервере(Результат)
	
	ТаблицаТовары = ТоварыОтобрать.Выгрузить();
	
	МассивРазмещений = Результат.МассивРазмещений;
	Ячейка = Результат.Ячейка;
	
	// Массив структур
	Для Каждого Строка Из МассивРазмещений Цикл
		
		Если Строка.КоличествоУпаковок <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТоварыОтобрано.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		Если ЗначениеЗаполнено(Строка.ЯчейкаЗамена) Тогда
			НоваяСтрока.Ячейка = Строка.ЯчейкаЗамена;
		КонецЕсли;
		
		ОтборПоиска = Новый Структура;
		ОтборПоиска.Вставить("Номенклатура",   Строка.Номенклатура);
		ОтборПоиска.Вставить("Характеристика", Строка.Характеристика);
		ОтборПоиска.Вставить("Серия",          Строка.Серия);
		ОтборПоиска.Вставить("Упаковка",       Строка.Упаковка);
		ОтборПоиска.Вставить("Ячейка",         Ячейка);
		
		МассивКПоступлению = ТаблицаТовары.НайтиСтроки(ОтборПоиска);
		Если Не МассивКПоступлению.Количество() Тогда
			ОтборПоиска.Вставить("Серия", Справочники.СерииНоменклатуры.ПустаяСсылка()); 
			МассивКПоступлению = ТаблицаТовары.НайтиСтроки(ОтборПоиска);
		КонецЕсли;
		
		Для Каждого СтрокаПоиск Из МассивКПоступлению Цикл
			СтрокаПоиск.КоличествоУпаковок = СтрокаПоиск.КоличествоУпаковок - НоваяСтрока.КоличествоУпаковок;
			СтрокаПоиск.Количество = СтрокаПоиск.КоличествоУпаковок;
		КонецЦикла;
		
	КонецЦикла;
	
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого Строка Из ТаблицаТовары Цикл
		Если Строка.КоличествоУпаковок <= 0 Тогда
			МассивУдаляемыхСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл
		ТаблицаТовары.Удалить(СтрокаУдалить);
	КонецЦикла;
		
	Для Каждого СтрокаКодМаркировки Из Результат.ШтрихкодыУпаковок Цикл
			
		НоваяСтрока = ШтрихкодыУпаковок.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаКодМаркировки);
			
	КонецЦикла;
	
	ТоварыОтобрать.Загрузить(ТаблицаТовары);
	
КонецПроцедуры

&НаСервере
Функция СтруктураТоваров()
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура","");
	Структура.Вставить("Характеристика","");
	Структура.Вставить("Серия","");
	Структура.Вставить("Упаковка","");
	Структура.Вставить("КоличествоУпаковок","");
	Структура.Вставить("Назначение","");
	
	Возврат Структура;
	
КонецФункции

&НаКлиенте
Процедура ОформитьМеню(ИмяВкладки)
	
	ПоказатьМеню();
	
	Элементы.РамкаОтобрать.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаОтобрать.ЦветТекста = WebЦвета.ТемноСерый;
	
	Элементы.РамкаОтобрано.Картинка = БиблиотекаКартинок.РамкаМенюБелая;
	Элементы.КомандаОтобрано.ЦветТекста = WebЦвета.ТемноСерый;
	
	Элементы["Команда" + ИмяВкладки].ЦветТекста = WebЦвета.Черный;
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["Страница" + ИмяВкладки];
	
	Если ИмяВкладки = "Отобрать" Тогда
		Элементы.РамкаОтобрать.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	ИначеЕсли ИмяВкладки = "Отобрано" Тогда
		Элементы.РамкаОтобрано.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	КонецЕсли;
	
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовкиКомандМеню()
	
	Элементы.КомандаОтобрать.Заголовок = СтрШаблон(НСтр("ru = 'Отобрать (%1)';
														|en = 'Pick (%1)'"), ТоварыОтобрать.Количество());
	Элементы.КомандаОтобрано.Заголовок = СтрШаблон(НСтр("ru = 'Отобрано (%1)';
														|en = 'Selected (%1)'"), ТоварыОтобрано.Количество());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФорму()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	
		"ВЫБРАТЬ
		|	ОтборРазмещениеТоваров.Номер КАК Номер,
		|	ОтборРазмещениеТоваров.Дата КАК Дата,
		|	ОтборРазмещениеТоваров.ЗонаОтгрузки КАК ЗонаОтгрузки,
		|	ОтборРазмещениеТоваров.Исполнитель КАК Исполнитель,
		|	ОтборРазмещениеТоваров.Комментарий КАК Комментарий,
		|	ОтборРазмещениеТоваров.Склад КАК Склад,
		|	ОтборРазмещениеТоваров.Помещение КАК Помещение,
		|	ОтборРазмещениеТоваров.Распоряжение КАК Распоряжение
		|ИЗ
		|	Документ.ОтборРазмещениеТоваров КАК ОтборРазмещениеТоваров
		|ГДЕ
		|	ОтборРазмещениеТоваров.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОтобрано.Номенклатура КАК Номенклатура,
		|	ТоварыОтобрано.Номенклатура.Артикул КАК Артикул,
		|	ТоварыОтобрано.Характеристика КАК Характеристика,
		|	ТоварыОтобрано.Упаковка КАК Упаковка,
		|	ТоварыОтобрано.Серия КАК Серия,
		|	ТоварыОтобрано.Назначение КАК Назначение,
		|	ТоварыОтобрано.Ячейка КАК Ячейка,
		|	ТоварыОтобрано.КоличествоОтобрано КАК Количество,
		|	ТоварыОтобрано.КоличествоУпаковокОтобрано КАК КоличествоУпаковок
		|ИЗ
		|	Документ.ОтборРазмещениеТоваров.ТоварыОтбор КАК ТоварыОтобрано
		|ГДЕ
		|	ТоварыОтобрано.Ссылка = &Ссылка
		|   И ТоварыОтобрано.КоличествоОтобрано > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыОтобрать.Номенклатура КАК Номенклатура,
		|	ТоварыОтобрать.Номенклатура.Артикул КАК Артикул,
		|	ТоварыОтобрать.Характеристика КАК Характеристика,
		|	ТоварыОтобрать.Упаковка КАК Упаковка,
		|	ТоварыОтобрать.Серия КАК Серия,
		|	ТоварыОтобрать.Назначение КАК Назначение,
		|	ТоварыОтобрать.Ячейка КАК Ячейка,
		|	ТоварыОтобрать.КоличествоОтобрано КАК КоличествоОтобрано,
		|	ТоварыОтобрать.Количество КАК Количество,
		|	ТоварыОтобрать.КоличествоУпаковок КАК КоличествоУпаковок
		|ИЗ
		|	Документ.ОтборРазмещениеТоваров.ТоварыОтбор КАК ТоварыОтобрать
		|ГДЕ
		|	ТоварыОтобрать.Ссылка = &Ссылка
		|   И ТоварыОтобрать.КоличествоОтобрано <= 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаДетальныеЗаписи = РезультатЗапроса[0].Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаДетальныеЗаписи);

	ТоварыОтобрано.Загрузить(РезультатЗапроса[1].Выгрузить());
	ТоварыОтобрать.Загрузить(РезультатЗапроса[2].Выгрузить());
	
	ИспользоватьСкладскиеПомещения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ИспользоватьСкладскиеПомещения");
	
КонецПроцедуры

&НаСервере
Функция ЗакончитьОтборНаСервере(Результат, ИмяПроцесса)
	
	ШаблонОшибкиЗаписать = НСтр("ru = 'Не удалось записать %1. %2';
								|en = 'Cannot save %1. %2'");

	НачатьТранзакцию();
	Попытка

		ДокументОбъект = Ссылка.ПолучитьОбъект();

		Если ИмяПроцесса = "КОтбору" Тогда
			ДокументОбъект.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.ВРаботе;
		Иначе
			ДокументОбъект.Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок;
		КонецЕсли;

		ДокументОбъект.Помещение   = Помещение;
		ДокументОбъект.Комментарий = Комментарий;
		ДокументОбъект.ЗонаОтгрузки = ЗонаОтгрузки;
		ДокументОбъект.Исполнитель = Исполнитель;
		
		ДокументОбъект.ТоварыОтбор.Очистить();
			
		Для Каждого Строка Из ТоварыОтобрано Цикл
			
			НоваяСтрока = ДокументОбъект.ТоварыОтбор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка); 
			
			КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.Упаковка, Строка.Номенклатура);
			
			НоваяСтрока.КоличествоОтобрано = Строка.КоличествоУпаковок * КоэффициентУпаковки;
			НоваяСтрока.КоличествоУпаковокОтобрано = Строка.КоличествоУпаковок;
			
			НоваяСтрока.Количество = НоваяСтрока.КоличествоОтобрано;
			НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковокОтобрано;
			
		КонецЦикла;
		
		ПараметрыУказанияСерий = Документы.ОтборРазмещениеТоваров.ПараметрыУказанияСерий(ДокументОбъект);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОбъект, ПараметрыУказанияСерий.Отбор);
	
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = СтрШаблон(ШаблонОшибкиЗаписать, Ссылка,
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
				
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Ссылка);
		Возврат Ложь;
	КонецПопытки;

	Если ИмяПроцесса = "НеОтгружать" Тогда
		
		// Захват объекта для редактирования
		Попытка
			ЗаблокироватьДанныеДляРедактирования(Распоряжение);
		Исключение
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось заблокировать %1. %2';
										|en = 'Cannot block %1. %2'"), Распоряжение,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Распоряжение);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			
			Возврат Ложь;
		КонецПопытки;
		
		НачатьТранзакцию();
		Попытка
				
			ОрдерОбъект = Распоряжение.ПолучитьОбъект();

			Если Результат = "Завершить" Тогда
				ОрдерОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.КПроверке;
			Иначе
				ОрдерОбъект.Статус = Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки;
				ОрдерОбъект.Контролер = Пользователи.ТекущийПользователь();
			КонецЕсли;
			
			СтруктураПоиска = СтруктураТоваров();
			СтруктураПоиска.Удалить("КоличествоУпаковок");
			СтруктураПоиска.Удалить("Упаковка");
			СтруктураПоиска.Вставить("Действие", Перечисления.ДействияСоСтрокамиОрдеровНаОтгрузку.Отобрать);
			
			Для Каждого Строка Из ТоварыОтобрать Цикл
				
				ЗаполнитьЗначенияСвойств(СтруктураПоиска, Строка);
				
				Если Строка.Номенклатура.ВидНоменклатуры.ПолитикаУчетаСерий.УказыватьПоФактуОтбора Тогда
					СтруктураПоиска.Удалить("Серия");
				Иначе
					СтруктураПоиска.Вставить("Серия", Строка.Серия);
				КонецЕсли;
				
				РезультатПоиска = ОрдерОбъект.ОтгружаемыеТовары.НайтиСтроки(СтруктураПоиска);
				
				Если РезультатПоиска.Количество() > 0 Тогда
					КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(
					РезультатПоиска[0].Упаковка, РезультатПоиска[0].Номенклатура);
					
					РезультатПоиска[0].Количество         = РезультатПоиска[0].Количество - Строка.Количество;
					РезультатПоиска[0].КоличествоУпаковок = РезультатПоиска[0].Количество / КоэффициентУпаковки;
				КонецЕсли; 
				
				КоличествоКРаспределению = Строка.Количество; 
				
				СтруктураПоиска.Удалить("Действие");
					
				НайденныеРаспоряжения = ОрдерОбъект.ТоварыПоРаспоряжениям.НайтиСтроки(СтруктураПоиска);
				Для Каждого СтрокаРаспоряжения Из НайденныеРаспоряжения Цикл
					Если КоличествоКРаспределению >= СтрокаРаспоряжения.Количество Тогда
						КоличествоКРаспределению = КоличествоКРаспределению - СтрокаРаспоряжения.Количество;
						ОрдерОбъект.ТоварыПоРаспоряжениям.Удалить(СтрокаРаспоряжения);
					Иначе
						СтрокаРаспоряжения.Количество = СтрокаРаспоряжения.Количество - КоличествоКРаспределению;
						КоличествоКРаспределению = 0;
					КонецЕсли;
						
					Если КоличествоКРаспределению = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
			
			МассивУдаляемыхСтрок = Новый Массив;
			Для Каждого Строка Из ОрдерОбъект.ОтгружаемыеТовары Цикл	
				Если Строка.Количество <= 0 Тогда
					МассивУдаляемыхСтрок.Добавить(Строка);
				КонецЕсли;
			КонецЦикла;	
			
			Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл	
				ОрдерОбъект.ОтгружаемыеТовары.Удалить(СтрокаУдалить);	
			КонецЦикла;
				
			ОрдерОбъект.Записать(РежимЗаписиДокумента.Проведение); 
			ЗафиксироватьТранзакцию();
		Исключение 
			ОтменитьТранзакцию();
			
			ТекстОшибки = СтрШаблон(ШаблонОшибкиЗаписать, Распоряжение,
				ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки, Распоряжение);
			
			ДокументОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
			
			Возврат Ложь;
		КонецПопытки;
		
	ИначеЕсли Не ИмяПроцесса = "КОтбору" Тогда
		Если Результат = "Завершить" Тогда
			Возврат Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(Распоряжение,
				Перечисления.СтатусыРасходныхОрдеров.КПроверке); 
		ИначеЕсли Результат = "Проверить" Тогда
			Возврат Обработки.МобильноеРабочееМестоКладовщика.УстановитьСтатусОрдера(Распоряжение,
				Перечисления.СтатусыРасходныхОрдеров.ВПроцессеПроверки);
		КонецЕсли;
	КонецЕсли;

	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьЗонуПриемки()
	
	ЗонаОтгрузки = Справочники.СкладскиеЯчейки.ЗонаПриемкиПоУмолчанию(Склад, Помещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьМеню()

	Элементы.ГруппаМеню.Видимость = Истина;
	Элементы.ГруппаКоманды.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьМеню()

	Элементы.ГруппаМеню.Видимость = Ложь;
	Элементы.ГруппаКоманды.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СобратьВсеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыКПоступлениюОстатки.ДокументПоступления КАК ДокументПоступления,
		|	ТоварыКПоступлениюОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыКПоступлениюОстатки.Характеристика КАК Характеристика,
		|	ТоварыКПоступлениюОстатки.Назначение КАК Назначение,
		|	ТоварыКПоступлениюОстатки.Склад КАК Склад,
		|	ТоварыКПоступлениюОстатки.Отправитель КАК Отправитель,
		|	ТоварыКПоступлениюОстатки.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		|	ТоварыКПоступлениюОстатки.Серия КАК Серия,
		|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток КАК Количество,
		|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток КАК КоличествоУпаковок,
		|	ТоварыКПоступлениюОстатки.Номенклатура.ЕдиницаИзмерения КАК Упаковка
		|ИЗ
		|	РегистрНакопления.ТоварыКПоступлению.Остатки(, ДокументПоступления = &ДокументПоступления) КАК ТоварыКПоступлениюОстатки
		|ГДЕ
		|	ТоварыКПоступлениюОстатки.КОформлениюОрдеровОстаток > 0";
	
	Запрос.УстановитьПараметр("ДокументПоступления", РаспоряжениеНаОтгрузку);
	
	Остатки = Запрос.Выполнить().Выгрузить();
	ТоварыОтобрано.Загрузить(Остатки);
	
КонецПроцедуры

&НаСервере
Процедура ОтменитьОтборСервер(ИндексСтроки)
	
	ТекущаяСтрока = ТоварыОтобрано[ИндексСтроки];
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(ТекущаяСтрока.Упаковка, ТекущаяСтрока.Номенклатура);
	
	СтруктураПоиска = СтруктураТоваров(); 
	СтруктураПоиска.Удалить("КоличествоУпаковок");
	СтруктураПоиска.Вставить("Ячейка");
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, ТекущаяСтрока);
	
	РезультатПоиска = ТоварыОтобрать.НайтиСтроки(СтруктураПоиска);
	Если РезультатПоиска.Количество() > 0 Тогда
		РезультатПоиска[0].КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + ТекущаяСтрока.КоличествоУпаковок;
		РезультатПоиска[0].Количество = РезультатПоиска[0].КоличествоУпаковок * КоэффициентУпаковки;
	Иначе
		НоваяСтрока = ТоварыОтобрать.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
		НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
	КонецЕсли;
	
	ТоварыОтобрано.Удалить(ИндексСтроки);
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьФормуНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуНаСервере(Результат)
	
	Литерал = "НомерСтроки";
	НомерСтроки = Результат[Литерал];
	
	КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Результат.Упаковка, Результат.Номенклатура);
	
	СтруктураПоиска = СтруктураТоваров(); 
	СтруктураПоиска.Удалить("КоличествоУпаковок");
	СтруктураПоиска.Вставить("Ячейка");
	
	ЗаполнитьЗначенияСвойств(СтруктураПоиска, Результат);
	
	Если Результат.Режим = "РедактированиеОтборИзЯчейкиОтгрузка" Тогда
		
		ИсходноеКоличество = ТоварыОтобрано[Результат.НомерСтроки].КоличествоУпаковок;
		
		ТоварыОтобрано[Результат.НомерСтроки].Упаковка           = Результат.Упаковка;
		ТоварыОтобрано[Результат.НомерСтроки].КоличествоУпаковок = Результат.КоличествоУпаковок;
		ТоварыОтобрано[Результат.НомерСтроки].Количество         = ТоварыОтобрано[Результат.НомерСтроки].КоличествоУпаковок * КоэффициентУпаковки;
		ТоварыОтобрано[Результат.НомерСтроки].Серия              = Результат.Серия;
		ТоварыОтобрано[Результат.НомерСтроки].Назначение         = Результат.Назначение;
		
		РезультатПоиска = ТоварыОтобрать.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			РезультатПоиска[0].КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + ИсходноеКоличество - Результат.КоличествоУпаковок;
			РезультатПоиска[0].Количество = РезультатПоиска[0].КоличествоУпаковок * КоэффициентУпаковки;
		ИначеЕсли ИсходноеКоличество - Результат.КоличествоУпаковок > 0 Тогда
			НоваяСтрока = ТоварыОтобрать.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
			НоваяСтрока.КоличествоУпаковок = ИсходноеКоличество - Результат.КоличествоУпаковок;
			НоваяСтрока.Количество         = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		КонецЕсли;
		
	Иначе
		
		ТоварыОтобрать[НомерСтроки].Упаковка = Результат.Упаковка; 
		ТоварыОтобрать[НомерСтроки].КоличествоУпаковок = ТоварыОтобрать[НомерСтроки].КоличествоУпаковок - Результат.КоличествоУпаковок;
		ТоварыОтобрать[НомерСтроки].Количество = ТоварыОтобрать[НомерСтроки].КоличествоУпаковок * КоэффициентУпаковки;
		
		РезультатПоиска = ТоварыОтобрано.НайтиСтроки(СтруктураПоиска);
		Если РезультатПоиска.Количество() > 0 Тогда
			РезультатПоиска[0].КоличествоУпаковок = РезультатПоиска[0].КоличествоУпаковок + Результат.КоличествоУпаковок;
			РезультатПоиска[0].Количество = РезультатПоиска[0].КоличествоУпаковок * КоэффициентУпаковки;
		ИначеЕсли Результат.КоличествоУпаковок > 0 Тогда
			НоваяСтрока = ТоварыОтобрано.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Результат);
			НоваяСтрока.Количество = НоваяСтрока.КоличествоУпаковок * КоэффициентУпаковки;
		КонецЕсли;
		
	КонецЕсли;
	
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого Строка Из ТоварыОтобрать Цикл
		Если Строка.Количество <= 0 Тогда
			МассивУдаляемыхСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
			
	Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл
		ТоварыОтобрать.Удалить(СтрокаУдалить);
	КонецЦикла;
	
	МассивУдаляемыхСтрок = Новый Массив;
	Для Каждого Строка Из ТоварыОтобрано Цикл	
		Если Строка.Количество <= 0 Тогда
			МассивУдаляемыхСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
			
	Для Каждого СтрокаУдалить Из МассивУдаляемыхСтрок Цикл
		ТоварыОтобрано.Удалить(СтрокаУдалить);
	КонецЦикла;
	
	СформироватьЗаголовкиКомандМеню();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПродолжение(Результат, Параметры = Неопределено) Экспорт
	
	Если Результат = "Продолжить" Тогда
		Возврат;
	КонецЕсли;
	
	СписокЗначений = Новый СписокЗначений();
	
	Если Результат = "НеОтгружать" Или Результат = "Завершить" Тогда	
		
		Если ЕстьПравоНаПроверкуОрдера И Бесшовный Тогда
			СписокЗначений.Добавить("Проверить", НСтр("ru = 'Перейти к проверке ордера';
														|en = 'Go to note check'"));
			СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить работу с ордером';
														|en = 'Close the note'"));
			СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить отбор';
														|en = 'Continue picking'"));
			ТекстСообщения = НСтр("ru = 'После завершения отбора перейти к проверке ордера или завершить работу с текущим ордером?';
									|en = 'After picking is completed, do you want to proceed to checking the goods issue or close the current goods issue?'");
		Иначе
			СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить';
														|en = 'Exit'"));
			СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить';
														|en = 'Continue'"));
			ТекстСообщения = НСтр("ru = 'Завершить отбор?';
									|en = 'Finish picking job?'");
		КонецЕсли; 
		
		ДопПараметры = Новый Структура("ИмяПроцесса", Результат);
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект, ДопПараметры);
		ПоказатьВопрос(Оповещение, ТекстСообщения, СписокЗначений, 0);
		
	Иначе 
		ДопПараметры = Новый Структура("ИмяПроцесса", Результат);
		ПослеЗакрытияВопроса("Завершить", ДопПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = "Продолжить" Тогда
		Возврат;
	КонецЕсли;
			
	ОтборЗавершен = ЗакончитьОтборНаСервере(Результат, ДополнительныеПараметры.ИмяПроцесса);

	Если Результат = "Завершить" И ОтборЗавершен Тогда
		Закрыть();
	ИначеЕсли Результат = "Проверить" И ОтборЗавершен Тогда  
		Закрыть();
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", Распоряжение);
		ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
		ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
			ЭтотОбъект,,,,,
			РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШтрихкодыУпаковок(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьШтрихкодыУпаковокНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьШтрихкодыУпаковокНаСервере(Результат)
	
	ШтрихкодыУпаковок.Очистить();
	ШтрихкодыУпаковок.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОтменитьЗаданиеНаОтбор(Результат, Параметры) Экспорт
	
	Если Результат = "Завершить" Тогда
		
		ОтборОтменен = ОтменитьЗаданиеНаОтборНаСервере();
		
		Если ОтборОтменен Тогда
			Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
				Закрыть();
			КонецЕсли;
			Закрыть();
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтменитьЗаданиеНаОтборНаСервере()
	
	Попытка
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Истина);
		
		ОбеспечениеВДокументахСервер.ПроверитьЗапуститьФоновоеЗаданиеРаспределенияЗапасов();
	Исключение
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось отменить задание на отбор.';
													|en = 'Cannot cancel the picking job.'"));
		
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти