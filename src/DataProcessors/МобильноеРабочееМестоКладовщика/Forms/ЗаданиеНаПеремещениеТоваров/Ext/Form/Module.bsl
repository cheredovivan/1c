
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Ссылка") Тогда
		Ссылка = Параметры.Ссылка;
	Иначе
		Склад = Параметры.Склад;
		Помещение = Параметры.Помещение;
	КонецЕсли;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	
	#Если МобильныйАвтономныйСервер Тогда
		Элементы.ФормаОтменитьПеремещение.Видимость = Ложь;
	#Иначе
		Статус = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "Статус");
		Если Статус = Перечисления.СтатусыОтборовРазмещенийТоваров.Подготовлено И ПравоДоступа("ИнтерактивнаяПометкаУдаления", Метаданные.Документы.ОтборРазмещениеТоваров) Тогда
			Элементы.ФормаОтменитьПеремещение.Видимость = Истина;
		Иначе
			Элементы.ФормаОтменитьПеремещение.Видимость = Ложь;
		КонецЕсли;
	#КонецЕсли
	
	ЗаполнитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"] Тогда
		
		СтандартнаяОбработка = Ложь;
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаКПоступлению"];
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ЗавершитьРазмещениеВЯчейку", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура",                 Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика",               Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия",                        Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Упаковка",                     Элемент.ТекущиеДанные.УпаковкаОтбор);
	ПараметрыФормы.Вставить("УпаковкаРазмещено",            Элемент.ТекущиеДанные.УпаковкаРазмещение);
	ПараметрыФормы.Вставить("КоличествоУпаковок",           Элемент.ТекущиеДанные.КоличествоУпаковокОтбор);
	ПараметрыФормы.Вставить("КоличествоУпаковокРазмещено",  Элемент.ТекущиеДанные.КоличествоУпаковокРазмещение);
	ПараметрыФормы.Вставить("Режим",                        "ПересчетРедактирование");
	ПараметрыФормы.Вставить("НомерСтроки",                  Элемент.ТекущаяСтрока);
	ПараметрыФормы.Вставить("Склад",                        Склад);
	ПараметрыФормы.Вставить("Помещение",                    Помещение);
	ПараметрыФормы.Вставить("Ячейка",                       Элемент.ТекущиеДанные.ЯчейкаОтбор);
	ПараметрыФормы.Вставить("ЯчейкаЗамена",                 Элемент.ТекущиеДанные.ЯчейкаРазмещение);
	ПараметрыФормы.Вставить("ТипОперации",                  3);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);

	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.РазмещениеВЯчейки",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьКонтроль(Команда)
	
	ОчиститьСообщения();
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Завершить", НСтр("ru = 'Завершить';
												|en = 'Finish'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить';
												|en = 'Continue'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Завершить перемещение?';
									|en = 'Do you want to finish the transfer?'"), СписокЗначений, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Сканировать(Команда)
	
	Описание = Новый ОписаниеОповещения("РезультатПоискаЯчейки", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипПоиска", "ПоискЯчейки");
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.СканированиеШтрихкода",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФорму()
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОтборРазмещениеТоваров.Склад КАК Склад,
			|	ОтборРазмещениеТоваров.Помещение КАК Помещение,
			|	ОтборРазмещениеТоваров.Комментарий КАК Комментарий,
			|	ОтборРазмещениеТоваров.Исполнитель КАК Исполнитель,
			|	ОтборРазмещениеТоваров.ТоварыОтбор.(
			|		Номенклатура КАК Номенклатура,
			|		Номенклатура.Артикул КАК Артикул,
			|		Характеристика КАК Характеристика,
			|		Назначение КАК Назначение,
			|		Серия КАК Серия,
			|		СтатусУказанияСерий КАК СтатусУказанияСерий,
			|		Ячейка КАК Ячейка,
			|		Упаковка КАК Упаковка,
			|		Количество КАК Количество,
			|		КоличествоУпаковок КАК КоличествоУпаковок
			|	) КАК ТоварыОтбор,
			|	ОтборРазмещениеТоваров.ТоварыРазмещение.(
			|		Номенклатура КАК Номенклатура,
			|		Характеристика КАК Характеристика,
			|		Назначение КАК Назначение,
			|		Серия КАК Серия,
			|		Ячейка КАК Ячейка,
			|		Упаковка КАК Упаковка,
			|		Количество КАК Количество,
			|		КоличествоУпаковок КАК КоличествоУпаковок
			|	) КАК ТоварыРазмещение
			|ИЗ
			|	Документ.ОтборРазмещениеТоваров КАК ОтборРазмещениеТоваров
			|ГДЕ
			|	ОтборРазмещениеТоваров.Ссылка = &Ссылка";
		
		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		ЗаполнитьЗначенияСвойств(ЭтаФорма, ВыборкаДетальныеЗаписи);
		
		ТоварыОтбор = ВыборкаДетальныеЗаписи.ТоварыОтбор.Выгрузить();
		ТоварыРазмещение = ВыборкаДетальныеЗаписи.ТоварыРазмещение.Выгрузить();
		
		Для Каждого Строка Из ТоварыОтбор Цикл
			
			НоваяСтрока = Товары.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
			НоваяСтрока.ЯчейкаОтбор = Строка.Ячейка;
			НоваяСтрока.УпаковкаОтбор = Строка.Упаковка;
			НоваяСтрока.КоличествоОтбор = Строка.Количество;
			НоваяСтрока.КоличествоУпаковокОтбор = Строка.КоличествоУпаковок;
			
			// Поиск строк в размещении
			СтруктураПоиска = Новый Структура;
			СтруктураПоиска.Вставить("Номенклатура",   Строка.Номенклатура);
			СтруктураПоиска.Вставить("Характеристика", Строка.Характеристика);
			СтруктураПоиска.Вставить("Серия",          Строка.Серия);
			СтруктураПоиска.Вставить("Назначение",     Строка.Назначение);
			
			РезультатПоиска = ТоварыРазмещение.НайтиСтроки(СтруктураПоиска);
			Если РезультатПоиска.Количество() > 0 Тогда
				НоваяСтрока.ЯчейкаРазмещение             = РезультатПоиска[0].Ячейка;
				НоваяСтрока.УпаковкаРазмещение           = РезультатПоиска[0].Упаковка;
				НоваяСтрока.КоличествоРазмещение         = РезультатПоиска[0].Количество;
				НоваяСтрока.КоличествоУпаковокРазмещение = РезультатПоиска[0].КоличествоУпаковок;
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РезультатПоискаЯчейки(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ЗавершитьРазмещениеВЯчейку", ЭтаФорма);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ячейка",      Результат);
	ПараметрыФормы.Вставить("Склад",       Склад);
	ПараметрыФормы.Вставить("Помещение",   Помещение);
	ПараметрыФормы.Вставить("ТипОперации", 3);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);

	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.РазмещениеВЯчейки",ПараметрыФормы,
	ЭтаФорма,,,,Описание,
	РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРазмещениеВЯчейку(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Строка Из Результат.МассивРазмещений Цикл
		
		Если Результат.Режим = "ПересчетРедактирование" Тогда
			НоваяСтрока = Товары[Строка.НомерСтроки];
		Иначе
			НоваяСтрока = Товары.Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		НоваяСтрока.ЯчейкаОтбор = Строка.Ячейка;
		НоваяСтрока.ЯчейкаРазмещение = Строка.ЯчейкаЗамена;
		
		НоваяСтрока.УпаковкаОтбор = Строка.Упаковка;
		НоваяСтрока.УпаковкаРазмещение = Строка.УпаковкаРазмещено;
		
		НоваяСтрока.КоличествоУпаковокОтбор = Строка.КоличествоУпаковок;
		НоваяСтрока.КоличествоУпаковокРазмещение = Строка.КоличествоУпаковокРазмещено;
		
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = "Завершить" Тогда
		
		ОтборЗавершен = ЗакончитьПеремещениеНаСервере();
		
		Если ОтборЗавершен Тогда
			Закрыть();
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗакончитьПеремещениеНаСервере()
	
	Если ЗначениеЗаполнено(Ссылка) Тогда
		ДокументОбъект = Ссылка.ПолучитьОбъект();
	Иначе
		ДокументОбъект                      = Документы.ОтборРазмещениеТоваров.СоздатьДокумент();
		ДокументОбъект.ВидОперации          = Перечисления.ВидыОперацийОтбораРазмещенияТоваров.Перемещение;
		ДокументОбъект.ДатаНачалаВыполнения = ТекущаяДатаСеанса();
		ДокументОбъект.Дата                 = ТекущаяДатаСеанса();
		Исполнитель                         = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ДокументОбъект.Статус      = Перечисления.СтатусыОтборовРазмещенийТоваров.ВыполненоБезОшибок;
	ДокументОбъект.Помещение   = Помещение;
	ДокументОбъект.Комментарий = Комментарий;
	ДокументОбъект.Исполнитель = Исполнитель;
	ДокументОбъект.Склад       = Склад;
	
	ДокументОбъект.ДатаОкончанияВыполнения = ТекущаяДатаСеанса();
	
	ДокументОбъект.ТоварыОтбор.Очистить();
	ДокументОбъект.ТоварыРазмещение.Очистить();
	
	Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ИспользоватьСкладскиеПомещения") И Помещение.Пустая() Тогда
		
		Сообщить(НСтр("ru = 'Не выбрано помещение';
						|en = 'Wareroom is not selected'"));
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаИнформация"];
		Возврат Ложь;
		
	КонецЕсли;
	
	Если Не Товары.Количество() Тогда
		
		Сообщить(НСтр("ru = 'Не выбрано ни одного товара для перемещения';
						|en = 'No goods to transfer are selected'"));
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы["СтраницаКПоступлению"];
		Возврат Ложь;
		
	КонецЕсли;
	
	Для Каждого Строка Из Товары Цикл
		
		НоваяСтрока = ДокументОбъект.ТоварыОтбор.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.УпаковкаОтбор, Строка.Номенклатура);
		НоваяСтрока.КоличествоОтобрано = Строка.КоличествоУпаковокОтбор * КоэффициентУпаковки;
		НоваяСтрока.КоличествоУпаковокОтобрано = Строка.КоличествоУпаковокОтбор;
		НоваяСтрока.Количество = НоваяСтрока.КоличествоОтобрано;
		НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковокОтобрано;
		НоваяСтрока.Ячейка = Строка.ЯчейкаОтбор;
		НоваяСтрока.Упаковка = Строка.УпаковкаОтбор;
		
		НоваяСтрока = ДокументОбъект.ТоварыРазмещение.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Строка);
		
		КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Строка.УпаковкаРазмещение, Строка.Номенклатура);
		НоваяСтрока.КоличествоРазмещено = Строка.КоличествоУпаковокРазмещение * КоэффициентУпаковки;
		НоваяСтрока.КоличествоУпаковокРазмещено = Строка.КоличествоУпаковокРазмещение;
		НоваяСтрока.Количество = НоваяСтрока.КоличествоРазмещено;
		НоваяСтрока.КоличествоУпаковок = НоваяСтрока.КоличествоУпаковокРазмещено;
		НоваяСтрока.Ячейка = Строка.ЯчейкаРазмещение;
		НоваяСтрока.Упаковка = Строка.УпаковкаРазмещение;
		
	КонецЦикла;
	
	Попытка
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
	Исключение
		ДокументОбъект.Записать(РежимЗаписиДокумента.Запись);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОтменитьПеремещение(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("Завершить", НСтр("ru = 'Пометить документ на удаление';
												|en = 'Mark the document for deletion'"));
	СписокЗначений.Добавить("Продолжить", НСтр("ru = 'Продолжить перемещение';
												|en = 'Continue transfer'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОтменитьПеремещение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Для отмены перемещения необходимо пометить на удаление документ перемещения. Вернуться к редактированию будет невозможно. Пометить на удаление?';
									|en = 'To cancel the transfer, mark the transfer document for deletion. It will be impossible to get back to editing. Mark the document for deletion?'"), СписокЗначений, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОтменитьПеремещение(Результат, Параметры) Экспорт
	
	Если Результат = "Завершить" Тогда
		
		ПеремещениеОтменено = ОтменитьПеремещениеНаСервере();
		
		Если ПеремещениеОтменено Тогда
			Закрыть();
		КонецЕсли;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОтменитьПеремещениеНаСервере()
	
	Попытка
		ДокументОбъект = Ссылка.ПолучитьОбъект();
		ДокументОбъект.УстановитьПометкуУдаления(Истина);
		
		ОбеспечениеВДокументахСервер.ПроверитьЗапуститьФоновоеЗаданиеРаспределенияЗапасов();
	Исключение
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось отменить задание на перемещение.';
													|en = 'Cannot cancel the transfer task.'"));
		
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти
