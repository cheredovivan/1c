
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Склад                 = Параметры.Склад;
	Помещение             = Параметры.Помещение;
	ТипСкладскойЯчейки    = Перечисления.ТипыСкладскихЯчеек.Отгрузка;
	Исполнитель           = Параметры.Исполнитель;
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	Пользователь          = Параметры.Исполнитель;
	
	СформироватьПредставлениеОтборов();
	
	ОбновитьСписокСортировок();
	ОбновитьСписокСтатусов();
	
	ЗаполнитьСписокДокументов();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Склады.Форма.ФормаВыбораМРМ") Тогда
		
		Склад = ВыбранноеЗначение;
		Помещение = Неопределено;
		СформироватьПредставлениеОтборов();
		ЗаполнитьСписокДокументов();
		
	КонецЕсли;
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ") Тогда
		
		Помещение = ВыбранноеЗначение;
		СформироватьПредставлениеОтбораПомещение();
		ЗаполнитьСписокДокументов();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсполнительПриИзменении(Элемент)
	
	ОбновитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗонаОтгрузкиПриИзменении(Элемент)
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	Если НачалоПериода > КонецПериода Тогда
		КонецПериода = НачалоПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	Если КонецПериода < НачалоПериода И ЗначениеЗаполнено(КонецПериода) Или Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = КонецПериода;
	КонецЕсли;
	
	ПериодПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокДокументов

&НаКлиенте
Процедура СписокДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Описание = Новый ОписаниеОповещения("ОбновитьФорму", ЭтотОбъект);
	ТекущаяСтрока = Элементы.СписокДокументов.ТекущиеДанные;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ссылка", ТекущаяСтрока.Ссылка);
	ПараметрыФормы.Вставить("Бесшовный", Ложь);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);

	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборыОткрытьСклад(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
		
	ОткрытьФорму(
		"Справочник.Склады.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОтборыОткрытьПомещение(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	Отбор = Новый Структура();
	Отбор.Вставить("Владелец", Склад);
	
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
	
	ОткрытьФорму(
		"Справочник.СкладскиеПомещения.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры
	
&НаКлиенте
Процедура ОтборыОчиститьСклад(Команда)
	
	Склад       = Неопределено;
	Помещение   = Неопределено;
	ЗонаОтгрузки = Неопределено;
	
	СформироватьПредставлениеОтбораСклад();
	СформироватьПредставлениеОтбораПомещение();
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПомещение(Команда)

	Помещение   = Неопределено;
	ЗонаОтгрузки = Неопределено;
	СформироватьПредставлениеОтбораПомещение();
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыбратьПериодЗавершение",
		ЭтотОбъект);
		
	МобильноеРабочееМестоКладовщикаКлиент.РедактироватьПериод(
		ЭтотОбъект,
		Новый Структура("ДатаНачала, ДатаОкончания", "НачалоПериода", "КонецПериода"),
		Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСортировку(Команда)
	
	Сортировка = "Дата";
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьПериод(Команда)
	
	НачалоПериода =  '00010101';
	КонецПериода =  '00010101';
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьИсполнителя(Команда)
	
	Исполнитель = Неопределено;
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСтатус(Команда)
	
	Статус = "Все";
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьЗонуОтгрузки(Команда)
	
	ЗонаОтгрузки = Неопределено;
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаправлениеСортировки(Команда)
	
	Если Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию Тогда
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоУбыванию;
		НаправлениеСортировки = "ПоУбыванию";
	Иначе
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
		НаправлениеСортировки = "ПоВозрастанию";
	КонецЕсли;
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокДокументов()
	
	СписокДокументов.Очистить();
	
	Если НЕ ЗначениеЗаполнено(ЗонаОтгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Обработки.МобильноеРабочееМестоКладовщика.ЗапросСписокЗаданийНаОтбор();
	
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ЗонаОтгрузки", ЗонаОтгрузки);
	Запрос.УстановитьПараметр("ИсполнительЗаголовок", НСтр("ru = 'Исполнитель';
															|en = 'Assignee'"));
	
	Если ЗначениеЗаполнено(Помещение) Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И ДокументОтборРазмещениеТоваров.Помещение = &Помещение"; 
		Запрос.УстановитьПараметр("Помещение", Помещение);
	КонецЕсли;
			
	Если ЗначениеЗаполнено(Исполнитель) Тогда 
		Запрос.Текст = Запрос.Текст + "
			|	И ДокументОтборРазмещениеТоваров.Исполнитель = &Исполнитель"; 
		Запрос.УстановитьПараметр("Исполнитель", Исполнитель);
	КонецЕсли;
		
	Статусы = Новый Массив;
	Если Статус = "ВсеНевыполненные" Тогда 
		Статусы.Добавить(Перечисления.СтатусыОтборовРазмещенийТоваров.Подготовлено);
		Статусы.Добавить(Перечисления.СтатусыОтборовРазмещенийТоваров.ВРаботе);
	ИначеЕсли Статус = "Все" Тогда
		Для Каждого Значение Из Перечисления.СтатусыОтборовРазмещенийТоваров Цикл 
			Статусы.Добавить(Значение);
		КонецЦикла;
	Иначе
		Статусы.Добавить(Перечисления.СтатусыОтборовРазмещенийТоваров[Статус]);
	КонецЕсли;
	Запрос.УстановитьПараметр("Статусы", Статусы);
	
	Если ЗначениеЗаполнено(НачалоПериода) И ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.Текст = Запрос.Текст + "
			|	И ДокументОтборРазмещениеТоваров.Дата МЕЖДУ &НачалоПериода И &КонецПериода";
		Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
		Запрос.УстановитьПараметр("КонецПериода", КонецПериода + 60*60*24);
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	" + Сортировка;
	
	Если НаправлениеСортировки = "ПоУбыванию" Тогда
		Запрос.Текст = Запрос.Текст + " УБЫВ";
	Иначе
		Запрос.Текст = Запрос.Текст + " ВОЗР";
	КонецЕсли;
	
	ДокументыРезультат = Запрос.Выполнить().Выгрузить();
	
	СписокДокументов.Загрузить(ДокументыРезультат);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтборов()
	
	СформироватьПредставлениеОтбораСклад();
	СформироватьПредставлениеОтбораПомещение();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтбораСклад()
	
	ИспользоватьСкладскиеПомещения = Ложь;
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Склад) Тогда
		ПредставленияОтборов = Склад;
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчистить.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
		
		СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(Склад);
		ИспользоватьСкладскиеПомещения = СвойстваСклада.ИспользоватьСкладскиеПомещения;
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Склад';
									|en = 'Warehouse'");
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчистить.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;

	Элементы.ГруппаКомандаОтборыОткрыть2.Видимость    = ИспользоватьСкладскиеПомещения;
	Элементы.КомандаОтборыОчиститьПомещение.Видимость = ИспользоватьСкладскиеПомещения;
	
	Элементы.КомандаОтборыОткрыть.Заголовок = ПредставленияОтборов;
	ЗонаОтгрузки = ЗонаОтгрузкиПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтбораПомещение()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Помещение) Тогда
		ПредставленияОтборов = Помещение;
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Помещение';
									|en = 'Wareroom'");
		Элементы.КомандаОтборыОткрытьПомещение.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчиститьПомещение.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть2.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.КомандаОтборыОткрытьПомещение.Заголовок = ПредставленияОтборов;
	ЗонаОтгрузки = ЗонаОтгрузкиПоУмолчанию();
	
КонецПроцедуры

&НаСервере
Функция ЗонаОтгрузкиПоУмолчанию()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СкладскиеЯчейки.Ссылка КАК ЗонаОтгрузки
		|ИЗ
		|	Справочник.СкладскиеЯчейки КАК СкладскиеЯчейки
		|ГДЕ
		|	СкладскиеЯчейки.Владелец = &Склад
		|	И СкладскиеЯчейки.Помещение = &Помещение
		|	И СкладскиеЯчейки.ТипСкладскойЯчейки = &ТипСкладскойЯчейкиПриемка";
	
	Запрос.УстановитьПараметр("Помещение", Помещение);
	Запрос.УстановитьПараметр("Склад", Склад);
	Запрос.УстановитьПараметр("ТипСкладскойЯчейкиПриемка", Перечисления.ТипыСкладскихЯчеек.Отгрузка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Справочники.СкладскиеЯчейки.ПустаяСсылка();
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.ЗонаОтгрузки;
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокСтатусов()
	
	Элементы.Статус.СписокВыбора.Очистить();
	
	Элементы.Статус.СписокВыбора.Добавить("Все", НСтр("ru = 'Все';
														|en = 'All'"));
	Элементы.Статус.СписокВыбора.Добавить("ВсеНевыполненные", НСтр("ru = 'Все невыполненные';
																	|en = 'All incomplete'"));
	Элементы.Статус.СписокВыбора.Добавить("Подготовлено", НСтр("ru = 'Подготовлено';
																|en = 'Prepared'"));
	Элементы.Статус.СписокВыбора.Добавить("ВРаботе", НСтр("ru = 'В работе';
															|en = 'In progress'"));
	Элементы.Статус.СписокВыбора.Добавить("ВыполненоБезОшибок", НСтр("ru = 'Выполнено без ошибок';
																	|en = 'Fully completed'"));
	Элементы.Статус.СписокВыбора.Добавить("ВыполненоСОшибками", НСтр("ru = 'Выполнено с ошибками';
																	|en = 'Partially completed'"));
	
	Статус = "ВсеНевыполненные";
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСортировок()
	
	Элементы.Исполнитель.СписокВыбора.Очистить();
	Элементы.Исполнитель.СписокВыбора.Добавить(Пользователь, НСтр("ru = '<Мои документы>';
																	|en = '<My documents>'"));
	
	Элементы.Сортировка.СписокВыбора.Очистить();
	Элементы.Сортировка.СписокВыбора.Добавить("Дата", НСтр("ru = 'Дата';
															|en = 'Date'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Номер", НСтр("ru = 'Номер';
															|en = 'Number'"));
	Сортировка = "Дата";
	
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	
КонецПроцедуры
	
&НаКлиенте
Процедура ВыбратьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		ПериодПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПериодПриИзмененииНаСервере()
	
	ЗаполнитьСписокДокументов();
	
КонецПроцедуры

#КонецОбласти
