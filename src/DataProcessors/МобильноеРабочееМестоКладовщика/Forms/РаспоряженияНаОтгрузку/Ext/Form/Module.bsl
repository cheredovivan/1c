
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	Склад     = Параметры.Склад;
	Исполнитель = Пользователи.ТекущийПользователь();
	
	СформироватьПредставлениеОтбораСклад();
	
	ОбновитьСписокСостояний();
	ОбновитьСписокСортировок();
	
	ЗаполнитьСписокРаспоряжений(); 
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.Склады.Форма.ФормаВыбораМРМ") Тогда
		
		Склад = ВыбранноеЗначение;
		СформироватьПредставлениеОтбораСклад();
		ОбновитьДанныеФормы();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы 

 &НаКлиенте
Процедура СостояниеПоступленияФильтрПриИзменении(Элемент)
	
	СформироватьПредставлениеОтбораСклад();
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПоступленияПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СортировкаПриИзменении(Элемент)
	
	ОбновитьДанныеФормы();  
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписокРаспоряжений

&НаКлиенте
Процедура СписокРаспоряженийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяСтрока = Элементы.СписокРаспоряжений.ТекущиеДанные;
	
	Если ТипЗнч(Элементы.СписокРаспоряжений.ТекущиеДанные.Распоряжение) = Тип("СправочникСсылка.ДоговорыКонтрагентов") 
		И Не ЗначениеЗаполнено(Склад) Тогда
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст =  НСтр("ru = 'Укажите склад в отборе';
											|en = 'Specify a warehouse in the filter'");
		СообщениеПользователю.Сообщить();
		
		Возврат;
	КонецЕсли;
		
	СтруктураЗаполнения = Новый Структура;
	СтруктураЗаполнения.Вставить("Склад", Склад);
	СтруктураЗаполнения.Вставить("Помещение", Помещение);
	СтруктураЗаполнения.Вставить("Распоряжение", ТекущаяСтрока.Распоряжение);
	СтруктураЗаполнения.Вставить("Получатель", ТекущаяСтрока.Получатель);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипДокумента", 2);

	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("НазваниеДокумента", ТекущаяСтрока.ТипДокумента);
	ПараметрыФормы.Вставить("Номер", ТекущаяСтрока.Номер);

	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.Товары",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтборыОткрытьСклад(Команда)
	
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы);
		
	Отбор = Новый Структура();
	Отбор.Вставить("ИспользоватьОрдернуюСхемуПриОтгрузке", Истина);
	ПараметрыОткрытияФормы.Вставить("Отбор", Отбор);
		
	ОткрытьФорму(
		"Справочник.Склады.Форма.ФормаВыбораМРМ",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьФорму(Команда)
	
	ОбновитьДанныеФормы();
	СформироватьПредставлениеОтбораСклад();
	
КонецПроцедуры 

&НаКлиенте
Процедура ОтборыОчиститьСклад(Команда)
	
	Склад = Неопределено;
	СформироватьПредставлениеОтбораСклад();
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьДату(Команда)
	
	ДатаОтгрузки = Неопределено;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСортировку(Команда)
	
	Сортировка = "Дата";
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОчиститьСостояние(Команда)
	
	СостояниеОтгрузки = "Все";
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьНаправлениеСортировки(Команда)
	
	Если Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию Тогда
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоУбыванию;
		НаправлениеСортировки = "ПоУбыванию";
	Иначе
		Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
		НаправлениеСортировки = "ПоВозрастанию";
	КонецЕсли;
	ОбновитьДанныеФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтгрузку(Команда)
	
	ОчиститьСообщения();
	
	СтруктураЗаполнения = СтруктураЗаполненияОрдера();
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ЭтотОбъект);
	
	ТекущаяСтрока = Элементы.СписокРаспоряжений.ТекущиеДанные;
	КоличествоТоваров = ПолучитьКоличествоТоваров(ТекущаяСтрока.Распоряжение);

	Если КоличествоТоваров = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нет товаров для отрузки';
											|en = 'There are no goods to ship'");
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;

	СтруктураЗаполнения.РаспоряженияНаОтгрузку.Добавить(ТекущаяСтрока.Распоряжение);
	СтруктураЗаполнения.Получатель = ТекущаяСтрока.Получатель;
	
	РезультатСоздания = НовыйРасходныйОрдер(СтруктураЗаполнения);
	
	Если Не ЗначениеЗаполнено(РезультатСоздания.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьАдресноеХранение = ЭтоАдресноеХранение(РезультатСоздания.Ссылка);
	
	Описание = Новый ОписаниеОповещения("ОбновитьДанныеФормы", ЭтотОбъект);
	
	Если ИспользоватьАдресноеХранение Тогда
		
		Если РезультатСоздания.ТребуетсяУказаниеСерии Тогда
			
			ОчиститьСообщения();
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ссылка", РезультатСоздания.Ссылка);
			ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров); 
			
			ОткрытьФорму(
				"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
				ЭтотОбъект,,,,Описание,
				РежимОткрытияОкнаФормы.Независимый);
				
			Возврат;
		КонецЕсли;
		
		СсылкаОтбор = НовыйОтборТоваров(РезультатСоздания.Ссылка);

		Если Не ЗначениеЗаполнено(СсылкаОтбор) Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать задания на отбор товаров согласно правилам отбора.';
								|en = 'Cannot generate goods picking jobs according to the picking rules.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", СсылкаОтбор);
		ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
		ПараметрыФормы.Вставить("Бесшовный", Истина);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
				
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
		
	Иначе
		
		Если РезультатСоздания.ЕстьОшибка Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", РезультатСоздания.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьДанныеФормы(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОбновитьДанныеФормыСервер();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормыСервер()
	
	ЗаполнитьСписокРаспоряжений();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокРаспоряжений()	
	#Если НЕ МобильныйАвтономныйСервер Тогда
	Если Не ЗначениеЗаполнено(Склад) Тогда
		СписокРаспоряжений.Очистить();
		Возврат;
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь(Неопределено) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Склады.Ссылка
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	Склады.Ссылка = &Склад";
		
		Запрос.УстановитьПараметр("Склад", Склад);
		
		Если Запрос.Выполнить().Пустой() Тогда
			СписокРаспоряжений.Очистить();
			Возврат;
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	 
	Запрос = Обработки.МобильноеРабочееМестоКладовщика.СписокРаспоряженийНаОтгрузку(Склад);
	
	Запрос.УстановитьПараметр("Склад", 				Склад);
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку", Документы.ЗаданиеНаПеревозку.ПустаяСсылка());
	Запрос.УстановитьПараметр("ДатаОтгрузки", 		?(ЗначениеЗаполнено(ДатаОтгрузки), КонецДня(ДатаОтгрузки) + 1, ДатаОтгрузки));
	ОформлятьСначалаНакладные = Константы.ПорядокОформленияНакладныхРасходныхОрдеров.Получить() = Перечисления.ПорядокОформленияНакладныхРасходныхОрдеров.СначалаНакладные;
	Запрос.УстановитьПараметр("ОформлятьСначалаНакладные", ОформлятьСначалаНакладные);
	Запрос.УстановитьПараметр("ОтображениеДеталейОтгрузка", Ложь);
	
	Состояния = Новый Массив;
	Если СостояниеОтгрузки = "ВсеВРаботе" Тогда 
		Состояния.Добавить(Перечисления.СостоянияОтбораТоваров.ВПроцессеОтбора);
		Состояния.Добавить(Перечисления.СостоянияОтбораТоваров.ОжидаетсяОтбор);
		Состояния.Добавить(Перечисления.СостоянияОтбораТоваров.ОжидаетсяОтгрузка);
	ИначеЕсли СостояниеОтгрузки = "Все" Тогда
		Для Каждого Значение Из Перечисления.СостоянияОтбораТоваров Цикл 
			Состояния.Добавить(Значение);
		КонецЦикла;
	Иначе
		Состояния.Добавить(Перечисления.СостоянияОтбораТоваров[СостояниеОтгрузки]);
	КонецЕсли;
	Запрос.УстановитьПараметр("Состояния", Состояния);
	
	Запрос.Текст = Запрос.Текст + "
	|УПОРЯДОЧИТЬ ПО
	|	";
	
	Если Сортировка = "Получатель" Тогда
		Запрос.Текст = Запрос.Текст + "Получатель.Наименование";
	Иначе
		Запрос.Текст = Запрос.Текст + Сортировка;
	КонецЕсли;
	
	Если НаправлениеСортировки = "ПоУбыванию" Тогда
		Запрос.Текст = Запрос.Текст + " УБЫВ";
	Иначе
		Запрос.Текст = Запрос.Текст + " ВОЗР";
	КонецЕсли;
	
	СписокРаспоряжений.Загрузить(Запрос.Выполнить().Выгрузить());	
	#КонецЕсли 
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставлениеОтбораСклад()
	
	ПредставленияОтборов = "";
	Если ЗначениеЗаполнено(Склад) Тогда
		ПредставленияОтборов = Склад;
		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.Черный;
		Элементы.КомандаОтборыОчистить.Видимость = Истина;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюЧерная;
		
	Иначе
		ПредставленияОтборов = НСтр("ru = 'Выбрать склад';
									|en = 'Select a warehouse'");

		Элементы.КомандаОтборыОткрыть.ЦветТекста = WebЦвета.ТемноСерый;
		Элементы.КомандаОтборыОчистить.Видимость = Ложь;
		Элементы.РамкаОтборыОткрыть.Картинка = БиблиотекаКартинок.РамкаМенюСерая;
	КонецЕсли;
	
	Элементы.КомандаОтборыОткрыть.Заголовок = ПредставленияОтборов;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураЗаполненияОрдера()
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.СтруктураЗаполненияРасходногоОрдера();
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКоличествоТоваров(Распоряжение)
	
	РезультатЗапроса = Обработки.МобильноеРабочееМестоКладовщика.СписокТоваровНаОтгрузкуПриемку(2, Распоряжение);

	Возврат РезультатЗапроса.Количество();
	
КонецФункции

&НаСервере
Процедура ОбновитьСписокСостояний()
	
	ИспользоватьСтатусыРасходныхОрдеров = ПолучитьФункциональнуюОпцию("ИспользоватьСтатусыРасходныхОрдеров", 
																			Новый Структура("Склад", Склад));
	
	Элементы.Состояние.СписокВыбора.Очистить();
	
	Если ИспользоватьСтатусыРасходныхОрдеров Тогда
		Элементы.Состояние.СписокВыбора.Добавить("ВсеВРаботе", НСтр("ru = 'Все в работе';
																	|en = 'All in progress'"));		
		Элементы.Состояние.СписокВыбора.Добавить("ОжидаетсяОтбор", НСтр("ru = 'Ожидается отбор';
																		|en = 'Picking is expected'"));
		Элементы.Состояние.СписокВыбора.Добавить("ОжидаетсяОтгрузка", НСтр("ru = 'Ожидается отгрузка';
																			|en = 'Shipment is expected'"));		
		Элементы.Состояние.СписокВыбора.Добавить("ОжидаетсяОформлениеНакладных", НСтр("ru = 'Ожидается оформление накладных';
																						|en = 'Invoice is expected'"));
		Элементы.Состояние.СписокВыбора.Добавить("Все", НСтр("ru = 'Все';
															|en = 'All'"));
		СостояниеОтгрузки = "ВсеВРаботе";
	Иначе
		Элементы.Состояние.СписокВыбора.Добавить("ОжидаетсяОтбор", НСтр("ru = 'Ожидается отбор';
																		|en = 'Picking is expected'"));
		Элементы.Состояние.СписокВыбора.Добавить("ОжидаетсяОформлениеНакладных", НСтр("ru = 'Ожидается оформление накладных';
																						|en = 'Invoice is expected'"));
		Элементы.Состояние.СписокВыбора.Добавить("Все", НСтр("ru = 'Все';
															|en = 'All'"));		
		СостояниеОтгрузки = "ОжидаетсяОтбор";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокСортировок()
	
	СостояниеОтгрузки = "Все";
	
	Элементы.Сортировка.СписокВыбора.Очистить();
	Элементы.Сортировка.СписокВыбора.Добавить("Дата", НСтр("ru = 'Дата';
															|en = 'Date'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Номер", НСтр("ru = 'Номер';
															|en = 'Number'"));
	Элементы.Сортировка.СписокВыбора.Добавить("Получатель", НСтр("ru = 'Получатель';
																|en = 'Recipient'"));
	Сортировка = "Дата";
	
	Элементы.ИзменитьНаправлениеСортировки.Картинка = БиблиотекаКартинок.СортироватьСтрокиПоВозрастанию;
	НаправлениеСортировки = "ПоВозрастанию";
	
КонецПроцедуры

&НаСервере
Функция ЭтоАдресноеХранение(Знач СсылкаНаРасходныйОрдер)
	
	СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(Склад);	
	ИспользоватьАдресноеХранение = СвойстваСклада.ИспользоватьАдресноеХранение;
	Помещение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаРасходныйОрдер, "Помещение");
	Если СвойстваСклада.ИспользоватьСкладскиеПомещения Тогда
		СтруктураПомещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Помещение, "ИспользоватьАдресноеХранение");
		ИспользоватьАдресноеХранение = СтруктураПомещения.ИспользоватьАдресноеХранение;
    КонецЕсли;
	
	Возврат ИспользоватьАдресноеХранение;
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйРасходныйОрдер(СтруктураЗаполнения)
		
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НовыйРасходныйОрдер(СтруктураЗаполнения);

КонецФункции 

&НаСервереБезКонтекста
Функция НовыйОтборТоваров(СсылкаРасходныйОрдер)
		
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НовыйОтборТоваров(СсылкаРасходныйОрдер);

КонецФункции
#КонецОбласти