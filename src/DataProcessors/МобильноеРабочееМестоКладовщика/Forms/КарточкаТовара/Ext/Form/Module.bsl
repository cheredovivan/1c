
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Номенклатура          = Параметры.Номенклатура;
	Характеристика        = Параметры.Характеристика;
	Упаковка              = Параметры.Упаковка;
	ВидНоменклатуры       = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ВидНоменклатуры");
	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;
	СрокГодности          = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "СрокГодности");

	Если Параметры.Свойство("Склад") Тогда
		Склад = Параметры.Склад;
	КонецЕсли;
	
	Если Параметры.Свойство("Помещение") Тогда
		Помещение = Параметры.Помещение;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Упаковка) Тогда
		Упаковка = ЕдиницаИзмеренияНоменклатуры(Номенклатура);
	КонецЕсли;
	
	Если Параметры.Свойство("Серия") Тогда
		Серия = Параметры.Серия;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьСерииНоменклатуры") Тогда
		Элементы.ГруппаСерия.Видимость = Ложь;
	КонецЕсли;
	
	Если Параметры.Свойство("Артикул") Тогда
		Артикул = Параметры.Артикул;
	КонецЕсли;
	
	Если Параметры.Свойство("ШтрихкодУпаковки") Тогда
		СтрокаШтрихкод = ШтрихкодыУпаковок.Добавить();
		СтрокаШтрихкод.ШтрихкодУпаковки = Параметры.ШтрихкодУпаковки;
		СтрокаШтрихкод.Номенклатура     = Параметры.Номенклатура;
		СтрокаШтрихкод.Характеристика   = Параметры.Характеристика;
	КонецЕсли;
	
	ИспользоватьХарактеристики = Справочники.Номенклатура.ХарактеристикиИспользуются(Номенклатура);
	
	Если Параметры.Свойство("Количество") Тогда
		КоличествоУпаковок             = Параметры.Количество;		
		МаксимальноеКоличествоУпаковок = Параметры.Количество;
	КонецЕсли;

	Если Параметры.Свойство("Назначение") Тогда
		Назначение = Параметры.Назначение;
	КонецЕсли; 
	
	Если Параметры.Свойство("Ячейка") Тогда
		Ячейка = Параметры.Ячейка;
	КонецЕсли;
	
	Режим = Параметры.Режим;
	Литерал = "НомерСтроки";
	НомерСтроки = Параметры[Литерал];
	
	СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(Склад);
	ИспользоватьАдресноеХранение            = СвойстваСклада.ИспользоватьАдресноеХранение;
	ИспользоватьОрдернуюСхемуПриПоступлении = СвойстваСклада.ИспользоватьОрдернуюСхемуПриПоступлении;
	ИспользоватьСкладскиеПомещения          = СвойстваСклада.ИспользоватьСкладскиеПомещения;
	
	Если СвойстваСклада.ИспользоватьСкладскиеПомещения Тогда
		СтруктураПомещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Помещение, "ИспользоватьАдресноеХранение");
		ИспользоватьАдресноеХранение = СтруктураПомещения.ИспользоватьАдресноеХранение;
	КонецЕсли;
	
	Если ПоказыватьФотоТоваров Тогда 
		Элементы.ГруппаФотоДобавить.Доступность = ПравоДоступа("Добавление", Метаданные.Справочники.НоменклатураПрисоединенныеФайлы);
		ЗагрузитьФотоТовара(); 
	Иначе
		Элементы.ФотоТоваров.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подтвердить(Команда)
	
	ЗакрытьКарточкуТовара();
	
КонецПроцедуры

&НаКлиенте
Процедура Упаковка(Команда)
	ПараметрыОткрытияФормы = Новый Структура;
	ПараметрыОткрытияФормы.Вставить("РежимВыбора", Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытияФормы.Вставить("Номенклатура", Номенклатура);
	ПараметрыОткрытияФормы.Вставить("Склад", Склад);
	ПараметрыОткрытияФормы.Вставить("Помещение", Помещение);
		
	ОткрытьФорму(
		"Справочник.УпаковкиЕдиницыИзмерения.Форма.ФормаВыбораИзДокументов",
		ПараметрыОткрытияФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

&НаКлиенте
Процедура УвеличитьКоличество(Команда)
	КоличествоУпаковок = КоличествоУпаковок + 1;
КонецПроцедуры

&НаКлиенте
Процедура УменьшитьКоличество(Команда)
	
	КоличествоУпаковок = КоличествоУпаковок - 1;
	
	Если КоличествоУпаковок < 0 Тогда
		КоличествоУпаковок = 0 ;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЯчейку(Команда)
	
	Описание = Новый ОписаниеОповещения("РезультатЗаменыЯчейки", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("Номенклатура", Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Характеристика);
	ПараметрыФормы.Вставить("Серия", Серия);
	ПараметрыФормы.Вставить("Режим", Режим);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.Ячейки",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура МаксимальноеКоличество(Команда)
	КоличествоУпаковок = МаксимальноеКоличествоУпаковок;
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФото(Команда)
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить("ВыбратьФото", НСтр("ru = 'Выбрать фото';
												|en = 'Select a photo'"));
	СписокЗначений.Добавить("СделатьФото", НСтр("ru = 'Сделать фото';
												|en = 'Take a photo'"));
	СписокЗначений.Добавить("Отмена", НСтр("ru = 'Отмена';
											|en = 'Cancel'"));
	
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаФото", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, НСтр("ru = 'Добавить фотографию к товару';
									|en = 'Add a photo to goods'"), СписокЗначений, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура КодыМаркировки(Команда)
	
	Описание = Новый ОписаниеОповещения("ИзменитьШтрихкодыУпаковок", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ШтрихкодыУпаковок", ШтрихкодыУпаковок);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.СписокКодовМаркировки",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура СерияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Отбор = Новый Структура();
	Отбор.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	Отбор.Вставить("СрокГодности", СрокГодности);
	
	ВыборСерии = Новый ОписаниеОповещения("ВыборСерии", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.СерииНоменклатуры.Форма.ФормаВыбора", Отбор, ЭтотОбъект,,,,ВыборСерии);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		
		ЕстьНеобработанноеСобытие = Ложь;
		Выполнить("ЕстьНеобработанноеСобытие = МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие()");
		
		Если ИмяСобытия = "ScanData" И ЕстьНеобработанноеСобытие Тогда
			
			// Преобразуем предварительно к ожидаемому формату
			Если Параметр[1] = Неопределено Тогда
				Штрихкод = Параметр[0];
			Иначе
				Штрихкод = Параметр[1][1];
			КонецЕсли;
			
			Если ИспользоватьСерии Тогда
				
				ПараметрыСерии = ПолучитьПараметрыСерииНаСервере(Штрихкод, ВидНоменклатуры);
				
				Если ЗначениеЗаполнено(ПараметрыСерии.Серия) Тогда
					Серия = ПараметрыСерии.Серия;
					КоличествоУпаковок = КоличествоУпаковок + 1;
				Иначе
					
				КонецЕсли;
				
				Возврат;
			КонецЕсли;
			
			ПараметрыКарточкаТовара = НайтиТоварСервер(Штрихкод);
			
			Если ПараметрыКарточкаТовара.Номенклатура = Номенклатура
				И ПараметрыКарточкаТовара.Характеристика = Характеристика
				И ПараметрыКарточкаТовара.Упаковка = Упаковка 
				И НЕ ИспользоватьСерии Тогда
				
				КоличествоУпаковок = КоличествоУпаковок + ПараметрыКарточкаТовара.Количество;
				
			Иначе
				
				ЗакрытьКарточкуТовара();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	Если ИмяСобытия = "Запись_СерииНоменклатуры" И ЗначениеЗаполнено(Источник) Тогда
		Серия = Источник;
	КонецЕсли;
	
	ИмяСобытия = Неопределено;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	ПолитикаУчетаСерий = Обработки.МобильноеРабочееМестоКладовщика.ПолитикаУчетаСерий(ВидНоменклатуры, Склад);
	
	Элементы.Характеристика.Видимость = ИспользоватьХарактеристики;
	Элементы.Упаковка.Заголовок = Упаковка;
	
	Элементы.ГруппаФотоДобавить.Видимость = НЕ ЗначениеЗаполнено(ФотоТовара1) 
	И НЕ ЗначениеЗаполнено(ФотоТовара2)
	И НЕ ЗначениеЗаполнено(ФотоТовара3);
	Элементы.ГруппаФото1.Видимость = ЗначениеЗаполнено(ФотоТовара1);
	Элементы.ГруппаФото2.Видимость = ЗначениеЗаполнено(ФотоТовара2);
	Элементы.ГруппаФото3.Видимость = ЗначениеЗаполнено(ФотоТовара3);
	
	Элементы.ВыбратьЯчейку.Видимость      = Ложь;
	
	#Если НЕ МобильныйАвтономныйСервер Тогда
		Элементы.Назначение.Видимость = Константы.ИспользоватьОбособленноеОбеспечениеЗаказов.Получить();
	#Иначе
		Элементы.Назначение.Видимость = Ложь;
	#КонецЕсли
	
	Если Режим = "Просмотр" Тогда
		
		Элементы.Количество.Вид                   = ВидПоляФормы.ПолеНадписи;
		Элементы.Упаковка.Доступность             = Ложь;
		Элементы.Характеристика.Доступность       = Ложь;
		Элементы.Серия.Доступность                = Ложь;
		Элементы.СоздатьСерию.Видимость           = Ложь;
		Элементы.Назначение.Доступность           = Ложь;
		Элементы.Подтвердить.Видимость            = Ложь;
		Элементы.Количество.ТолькоПросмотр        = Истина;
		Элементы.МаксимальноеКоличество.Видимость = Ложь;
		Элементы.Минус.Видимость                  = Ложь;
		Элементы.Плюс.Видимость                   = Ложь;
		
	ИначеЕсли Режим = "Приемка" ИЛИ Режим = "РедактированиеПриемка" Тогда
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Принять';
													|en = 'Accept'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда
			ИспользоватьСерии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПолитикаУчетаСерий, "УказыватьПриПриемке");
		Иначе
			ИспользоватьСерии = Ложь;
		КонецЕсли;

	ИначеЕсли Режим = "ПодборВКорзину" Тогда
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.Упаковка.Доступность       = Ложь;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Подтвердить';
													|en = 'Confirm'");
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;
		
	ИначеЕсли Режим = "Отгрузка" ИЛИ Режим = "РедактированиеОтгрузка" Тогда
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина; 
		Элементы.Подтвердить.Заголовок  = НСтр("ru = 'Отгрузить';
												|en = 'Ship'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда   
			
			СерииПриОтгрузке =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолитикаУчетаСерий,
				"УказыватьПриПланированииОтбора, УказыватьПоФактуОтбора, УказыватьПриПланированииОтгрузки");
						
			ИспользоватьСерии = СерииПриОтгрузке.УказыватьПриПланированииОтбора 
				Или СерииПриОтгрузке.УказыватьПоФактуОтбора;
			
			Если ИспользоватьСерии Тогда
				Элементы.ГруппаСерия.Видимость = Истина;
			ИначеЕсли СерииПриОтгрузке.УказыватьПриПланированииОтгрузки Тогда
				Элементы.ГруппаСерия.Видимость = Истина;
				Элементы.Серия.Доступность = Ложь;
				Элементы.СоздатьСерию.Видимость = Ложь;
			КонецЕсли;
		Иначе
			ИспользоватьСерии = Ложь;
		КонецЕсли;
	
	ИначеЕсли Режим = "ПересчетОтгрузка" ИЛИ Режим = "РедактированиеПересчетОтгрузка" 
		Или Режим = "РедактированиеПересчетОтгрузкаБезСерии" Тогда
		
		Элементы.Количество.Вид                   = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость            = Истина;
		Элементы.МаксимальноеКоличество.Видимость = Ложь;
		
		Элементы.ГруппаСерия.Видимость = Ложь;
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда
			СерииПриОтгрузке =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолитикаУчетаСерий, 
				"УказыватьПоФактуОтбора, УказыватьПриПланированииОтбора, УказыватьПриПланированииОтгрузки");   
				
			ИспользоватьСерии = СерииПриОтгрузке.УказыватьПоФактуОтбора Или СерииПриОтгрузке.УказыватьПриПланированииОтгрузки
				Или СерииПриОтгрузке.УказыватьПриПланированииОтбора; 
				
			Если ИспользоватьСерии Тогда
				Элементы.ГруппаСерия.Видимость = Истина;
			КонецЕсли;
		Иначе
			ИспользоватьСерии = Ложь;
		КонецЕсли;
	
	ИначеЕсли Режим = "Пересчет" ИЛИ Режим = "РедактированиеПересчет" Тогда
		
		Элементы.Количество.Вид                   = ВидПоляФормы.ПолеВвода;
		Элементы.Упаковка.Видимость               = Ложь;
		Элементы.Подтвердить.Видимость            = Истина;
		Элементы.Упаковка.Доступность             = Ложь;
		Элементы.МаксимальноеКоличество.Видимость = Ложь;
		
		Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда
			ИспользоватьСерии = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПолитикаУчетаСерий, "УказыватьПриПересчетеТоваров");
		Иначе
			ИспользоватьСерии = Ложь;
		КонецЕсли;
		
		Элементы.ВыбратьЯчейку.Видимость = ИспользоватьАдресноеХранение;
		Если ЗначениеЗаполнено(Ячейка) Тогда
			Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
		КонецЕсли;
		
	ИначеЕсли Режим = "Перемещение" Тогда
		
		Элементы.Количество.Вид                   = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость            = Истина;
		Элементы.МаксимальноеКоличество.Видимость = Ложь;
		
	ИначеЕсли Режим = "РазмещениеВЯчейку" ИЛИ Режим = "РедактированиеРазмещениеПоЯчейкам" Тогда
		
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.ВыбратьЯчейку.Видимость    = Истина;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Разместить';
													|en = 'Put away'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;
		
		Элементы.ВыбратьЯчейку.Видимость = ИспользоватьАдресноеХранение;
		Если ЗначениеЗаполнено(Ячейка) Тогда
			Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
		КонецЕсли;
		
	ИначеЕсли Режим = "ОтборИзЯчейки" ИЛИ Режим = "РедактированиеОтборИзЯчейки" Тогда
		
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.Упаковка.Доступность       = Ложь;
		Элементы.ВыбратьЯчейку.Видимость    = Истина;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Отобрать';
													|en = 'Pick'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Элементы.ВыбратьЯчейку.Видимость = ИспользоватьАдресноеХранение;
		Если ЗначениеЗаполнено(Ячейка) Тогда
			Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
		КонецЕсли;
		
	ИначеЕсли Режим = "ОтборИзЯчейкиОтгрузка" ИЛИ Режим = "РедактированиеОтборИзЯчейкиОтгрузка" Тогда
		
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Отобрать';
													|en = 'Pick'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Элементы.ВыбратьЯчейку.Видимость = ИспользоватьАдресноеХранение;
		Если ЗначениеЗаполнено(Ячейка) Тогда
			Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Упаковка) Тогда
			Элементы.Упаковка.Заголовок = Упаковка;
		КонецЕсли;

		Элементы.ГруппаСерия.Видимость = Ложь;

		Если ЗначениеЗаполнено(ПолитикаУчетаСерий) Тогда
			
			СерииПриОтгрузке =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПолитикаУчетаСерий, 
				"УказыватьПоФактуОтбора, УказыватьПриПланированииОтбора, УказыватьПриПланированииОтгрузки, УчитыватьОстаткиСерий"); 
			
			Если ИспользоватьАдресноеХранение Тогда
				ИспользоватьСерии = СерииПриОтгрузке.УказыватьПоФактуОтбора И СерииПриОтгрузке.УчитыватьОстаткиСерий; 
			Иначе
				ИспользоватьСерии = СерииПриОтгрузке.УказыватьПриПланированииОтбора;
			КонецЕсли;
				
			Если ИспользоватьСерии Тогда
				Элементы.ГруппаСерия.Видимость = Истина;
			ИначеЕсли СерииПриОтгрузке.УказыватьПриПланированииОтгрузки 
				Или СерииПриОтгрузке.УказыватьПриПланированииОтбора Тогда
				Элементы.ГруппаСерия.Видимость = Истина;
				Элементы.Серия.Доступность = Ложь;
				Элементы.СоздатьСерию.Видимость = Ложь;
			КонецЕсли;
		Иначе
			ИспользоватьСерии = Ложь;
		КонецЕсли;
		
	ИначеЕсли Режим = "УказаниеСерий" Или Режим = "РедактированиеСерий" Тогда
		
		Элементы.Количество.Вид             = ВидПоляФормы.ПолеВвода;
		Элементы.Подтвердить.Видимость      = Истина;
		Элементы.Подтвердить.Заголовок      = НСтр("ru = 'Подтвердить';
													|en = 'Confirm'");
		Элементы.МаксимальноеКоличество.Заголовок = СтрШаблон(НСтр("ru = 'Заполнить %1';
																	|en = 'Fill %1'"), МаксимальноеКоличествоУпаковок);
		
		Элементы.ВыбратьЯчейку.Видимость = ИспользоватьАдресноеХранение;
		Если ЗначениеЗаполнено(Ячейка) Тогда
			Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
		КонецЕсли;
		
		Элементы.ГруппаСерия.Видимость = Истина;
		ИспользоватьСерии = Истина;
		
	КонецЕсли;
	
	Элементы.Количество.Шрифт = Новый Шрифт(, 16, Истина, , , );
	
	Если Не ИспользоватьСерии Тогда
		Элементы.Серия.Доступность = Ложь;
		Элементы.СоздатьСерию.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьФотоТовара()
	#Если НЕ МобильныйАвтономныйСервер Тогда
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НоменклатураПрисоединенныеФайлы.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.НоменклатураПрисоединенныеФайлы КАК НоменклатураПрисоединенныеФайлы
		|ГДЕ
		|	НоменклатураПрисоединенныеФайлы.ВладелецФайла = &ВладелецФайла
		|	И НЕ НоменклатураПрисоединенныеФайлы.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ВладелецФайла", Номенклатура);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Сч = 1;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Если Сч > 3 Тогда
			Продолжить;
		КонецЕсли;
		
		СсылкаНаДДФ = РаботаСФайлами.ДанныеФайла(ВыборкаДетальныеЗаписи.Ссылка, ЭтотОбъект.УникальныйИдентификатор).СсылкаНаДвоичныеДанныеФайла;
		ЭтаФорма["ФотоТовара" + Сч] = СсылкаНаДДФ;
		
		Сч = Сч + 1;
		
	КонецЦикла;
	
	#КонецЕсли
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.УпаковкиЕдиницыИзмерения.Форма.ФормаВыбораИзДокументов") Тогда
		
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Упаковка = ВыбранноеЗначение;
		Иначе
			Упаковка = ЕдиницаИзмеренияНоменклатуры(Номенклатура);
		КонецЕсли;
		
		Элементы.Упаковка.Заголовок = Упаковка;
		
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЕдиницаИзмеренияНоменклатуры(Номенклатура)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.ПолучитьРеквизитСправочника(Номенклатура, "ЕдиницаИзмерения");
	
КонецФункции

&НаКлиенте
Процедура Штрихкоды(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Номенклатура);
	ПараметрыФормы.Вставить("Упаковка", Упаковка);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.Штрихкоды",ПараметрыФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Номенклатура);
	
	ОткрытьФорму(
		"Обработка.МобильноеРабочееМестоКладовщика.Форма.РедактированиеТовара",ПараметрыФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатЗаменыЯчейки(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Ячейка = Результат;
	Элементы.ВыбратьЯчейку.Заголовок = Ячейка;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаФото(Результат, Параметры) Экспорт
	
	Если Результат = "ВыбратьФото" Тогда
		
		#Если МобильныйКлиент Тогда
			Выполнить("РаботаСФайламиКлиент.ДобавитьФайлы(Номенклатура, УникальныйИдентификатор, , Неопределено)");
		#Иначе	
			РаботаСФайламиКлиент.ДобавитьФайлы(Номенклатура, УникальныйИдентификатор, , Неопределено);
		#КонецЕсли
		ЗагрузитьФотоТовара();
		УстановитьУсловноеОформление();
		
	ИначеЕсли Результат = "СделатьФото" Тогда
		
		#Если МобильныйКлиент Тогда
			ДвоичныеДанные = ПолучитьДанныеФотоСнимка();
			ПрисоединитьФайл(ДвоичныеДанные);
			ЗагрузитьФотоТовара();
			УстановитьУсловноеОформление();
		#Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = НСтр("ru = 'Данное устройство не поддерживает фотоснимок!';
												|en = 'This device does not support photo capturing.'");
			СообщениеПользователю.Сообщить();
		#КонецЕсли
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьШтрихкодыУпаковок(Результат, Параметры) Экспорт
	
	ИзменитьШтрихкодыУпаковокНаСервере(Результат);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьШтрихкодыУпаковокНаСервере(Результат) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ШтрихкодыУпаковок.Очистить();
	
	ШтрихкодыУпаковок.Загрузить(Результат.Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДанныеФотоСнимка()
	
	Данные = Неопределено;
	
	#Если МобильныйКлиент Тогда 
		
		Если СредстваМультимедиа.ПоддерживаетсяФотоснимок() Тогда
			
			Данные = СредстваМультимедиа.СделатьФотоснимок();
			
			Возврат Данные.ПолучитьДвоичныеДанные();
			
		Иначе
			СообщениеПользователю = Новый СообщениеПользователю;
			СообщениеПользователю.Текст = НСтр("ru = 'Данное устройство не поддерживает фотоснимок!';
												|en = 'This device does not support photo capturing.'");
			СообщениеПользователю.Сообщить();
		КонецЕсли;
		
	#КонецЕсли
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ПрисоединитьФайл(ДвоичныеДанные)
	#Если НЕ МобильныйАвтономныйСервер Тогда
		
	ВременноеХранилище                = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	ПараметрыФайла                    = РаботаСФайлами.ПараметрыДобавленияФайла();
	ПараметрыФайла.Автор              = Пользователи.ТекущийПользователь();
	ПараметрыФайла.ИмяБезРасширения   = Строка(Номенклатура);
	ПараметрыФайла.РасширениеБезТочки = "jpg";
	ПараметрыФайла.ВладелецФайлов = Номенклатура;
	РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, ВременноеХранилище);
	
	#КонецЕсли
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиТоварСервер(Штрихкод)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НайтиТоварПоШК(Штрихкод);
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьКарточкуТовара()	
	
	ТекстОшибки = "";
	ЗакрытьКарточкуТовараНаСервере(ТекстОшибки);
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = СокрЛП(ТекстОшибки);
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	Структура = Новый Структура;
	Структура.Вставить("Номенклатура",       Номенклатура);
	Структура.Вставить("Характеристика",     Характеристика);
	Структура.Вставить("Серия",              Серия);
	Структура.Вставить("Упаковка",           Упаковка);
	Структура.Вставить("КоличествоУпаковок", КоличествоУпаковок);
	Структура.Вставить("Назначение",         Назначение);
	Структура.Вставить("НомерСтроки",        НомерСтроки);
	Структура.Вставить("Ячейка",             Ячейка);
	Структура.Вставить("Режим",              Режим);
	Структура.Вставить("ШтрихкодыУпаковок",  ШтрихкодыУпаковок);
	Структура.Вставить("Артикул",            Артикул);

	Закрыть(Структура);
	
КонецПроцедуры  

&НаСервере
Процедура ЗакрытьКарточкуТовараНаСервере(ТекстОшибки)	
	
	УпаковкаЗаполнена = Не (Обработки.МобильноеРабочееМестоКладовщика.ПолучитьРеквизитСправочника(Упаковка,
		"Владелец") = Справочники.НаборыУпаковок.БазовыеЕдиницыИзмерения 
		И Обработки.МобильноеРабочееМестоКладовщика.ПолучитьРеквизитСправочника(Упаковка, 
		"ТипИзмеряемойВеличины") = Перечисления.ТипыИзмеряемыхВеличин.КоличествоШтук);
	
	Если Не УпаковкаЗаполнена И Режим = "Приемка" И ИспользоватьАдресноеХранение Тогда
		ТекстОшибки = ТекстОшибки + НСтр("ru = 'Укажите упаковку';
										|en = 'Specify packaging unit'");
	КонецЕсли;
	
	Если Режим = "Приемка" Или Режим = "РедактированиеПриемка" 
		Или Режим = "Отгрузка" Или Режим = "РедактированиеОтгрузка" Или Режим = "Пересчет" Или Режим = "Перемещение" 
		Или Режим = "РедактированиеПересчетОтгрузка" Или Режим = "ПересчетОтгрузка"
		Или Режим = "РедактированиеПересчетОтгрузкаБезСерии"
		Или Режим = "ОтборИзЯчейкиОтгрузка" Или Режим = "РедактированиеОтборИзЯчейкиОтгрузка"
		Или Режим =  "УказаниеСерий" Или Режим =  "РедактированиеСерий" Тогда
	
		Если ИспользоватьХарактеристики и НЕ ЗначениеЗаполнено(Характеристика) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите характеристику';
															|en = 'Specify variant'");
		КонецЕсли;
		
		Если ИспользоватьСерии И НЕ ЗначениеЗаполнено(Серия) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите серию';
															|en = 'Specify batch'");
		КонецЕсли;
		
		Если КоличествоУпаковок = 0 Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите количество';
															|en = 'Specify quantity'");
		КонецЕсли;     
		
		Если ИспользоватьСерии И ЗначениеЗаполнено(Серия) Тогда
			НастройкаИспользованияСерий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидНоменклатуры, "НастройкаИспользованияСерий");
			Если НастройкаИспользованияСерий = Перечисления.НастройкиИспользованияСерийНоменклатуры.ЭкземплярТовара Тогда  
				КоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентУпаковки(Упаковка, Номенклатура);
				Если КоличествоУпаковок > 1 Тогда
					ШаблонСообщения = НСтр("ru = 'Указанное количество товара с серией ""%1"" равно %2. Политика учета серий данного товара предусматривает, что количество по любой серии этого товара всегда будет равно 1.';
											|en = 'The specified quantity of the item with batch ""%1"" is equal to %2. According to the batch accounting policy of this item, the quantity by any item batch always equals 1.'");
					ТекстСообщения = СтрШаблон(ШаблонСообщения, Серия, Формат(КоличествоУпаковок, "ЧН=0; ЧГ="));		
					ТекстОшибки = ТекстОшибки + Символы.ПС + ТекстСообщения; 
				ИначеЕсли КоэффициентУпаковки <> 1 Тогда
					ТекстСообщения = НСтр("ru = 'Политика учета серий товара предусматривает, что серии указываются для каждого экземпляра. Укажите единичную упаковку.';
											|en = 'According to the batch accounting policy of the item, batches are specified for each item. Specify a single packaging unit.'");
					ТекстОшибки = ТекстОшибки + Символы.ПС + ТекстСообщения; 
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Режим = "ОтборИзЯчейкиОтгрузка" И НЕ ЗначениеЗаполнено(Ячейка) И ИспользоватьАдресноеХранение Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите ячейку';
															|en = 'Specify a storage bin'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если (Режим = "РазмещениеВЯчейку" ИЛИ Режим = "ОтборИзЯчейки") И НЕ ЗначениеЗаполнено(Ячейка) Тогда
		ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите ячейку';
														|en = 'Specify a storage bin'");
	КонецЕсли;
	
	Если НЕ МаркиОтсканированыСервер() Тогда
		
		ТекстОшибки = ТекстОшибки + Символы.ПС + НСтр("ru = 'Укажите коды маркировки';
														|en = 'Specify marking codes'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыСерииНаСервере(ЗначениеШтрихкода, ВидНоменклатуры)
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.ПолучитьПараметрыСерииПоШК(ЗначениеШтрихкода, ВидНоменклатуры);
	
КонецФункции

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	Если КоличествоУпаковок < 0 Тогда
		КоличествоУпаковок = 0;
		
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нельзя указать отрицательное значение!';
											|en = 'Negative values are not allowed.'");
		СообщениеПользователю.Сообщить();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция МаркиОтсканированыСервер()
	
	МаркиОтсканированы = Обработки.МобильноеРабочееМестоКладовщика.МаркиОтсканированы(Номенклатура, КоличествоУпаковок, Упаковка, ШтрихкодыУпаковок);
	Возврат МаркиОтсканированы;
	
КонецФункции

&НаКлиенте
Процедура ВыборСерии(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Серия = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура СерияСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СозданиеСерии();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСерию(Команда)
	
	СозданиеСерии();
	
КонецПроцедуры

&НаКлиенте
Процедура СозданиеСерии()
	
	Отбор = Новый Структура();
	Отбор.Вставить("ВидНоменклатуры", ВидНоменклатуры);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения",Отбор);
	ПараметрыФормы.Вставить("СрокГодности", СрокГодности);
	
	ОткрытьФорму("Справочник.СерииНоменклатуры.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти