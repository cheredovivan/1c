#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипДокумента = Параметры.ТипДокумента;
	
	Элементы.НачатьПриемку.Видимость = ТипДокумента = 1;
	Элементы.НачатьОтгрузку.Видимость = ТипДокумента = 2 Или ТипДокумента = 3;
				
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры.СтруктураЗаполнения); 
	
	Если ТипДокумента = 1 Тогда
		Распоряжение = Параметры.СтруктураЗаполнения.Распоряжение;
		РаспоряжениеСтрока = СформироватьПредставлениеРаспоряжения(Параметры.НазваниеДокумента, Параметры.Номер,
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, "Дата"));
		РезультатЗапроса = 
			Обработки.МобильноеРабочееМестоКладовщика.СписокТоваровНаОтгрузкуПриемку(Параметры.ТипДокумента, Распоряжение);
			
		Элементы.Отправитель.Видимость = (ТипЗнч(Отправитель) = Тип("СправочникСсылка.Партнеры") Или ТипЗнч(Отправитель) = Тип("СправочникСсылка.ФизическиеЛица"));
	ИначеЕсли ТипДокумента = 2 Тогда 
		РаспоряжениеОтгрузка = Параметры.СтруктураЗаполнения.Распоряжение;
		РаспоряжениеСтрока = СформироватьПредставлениеРаспоряжения(Параметры.НазваниеДокумента, Параметры.Номер,
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РаспоряжениеОтгрузка, "Дата"));
		РезультатЗапроса = 
			Обработки.МобильноеРабочееМестоКладовщика.СписокТоваровНаОтгрузкуПриемку(Параметры.ТипДокумента, РаспоряжениеОтгрузка);
			
		Элементы.Получатель.Видимость = (ТипЗнч(Получатель) = Тип("СправочникСсылка.Партнеры") Или ТипЗнч(Получатель) = Тип("СправочникСсылка.ФизическиеЛица"));
	Иначе
		РасходныйОрдер = Параметры.СтруктураЗаполнения.Распоряжение;	
		РезультатЗапроса = СписокТоваровНаОтгрузкуПоОрдеру();
	
		ДатаНомерОрдера = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РасходныйОрдер, "Дата, Номер");
		НазваниеДокумента = НСтр("ru = 'Расходный ордер';
								|en = 'Goods issue note'");
		РаспоряжениеСтрока = СформироватьПредставлениеРаспоряжения(НазваниеДокумента, ДатаНомерОрдера.Номер,
				ДатаНомерОрдера.Дата);
		Элементы.Получатель.Видимость = (ТипЗнч(Получатель) = Тип("СправочникСсылка.Партнеры") Или ТипЗнч(Получатель) = Тип("СправочникСсылка.ФизическиеЛица"));
		Элементы.НачатьОтгрузку.Заголовок = НСтр("ru = 'Начать отбор';
												|en = 'Start picking'");
	КонецЕсли;
		
	Товары.Загрузить(РезультатЗапроса);

	ПоказыватьФотоТоваров = Параметры.ПоказыватьФотоТоваров;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Номенклатура", Элемент.ТекущиеДанные.Номенклатура);
	ПараметрыФормы.Вставить("Характеристика", Элемент.ТекущиеДанные.Характеристика);
	ПараметрыФормы.Вставить("Серия", Элемент.ТекущиеДанные.Серия);
	ПараметрыФормы.Вставить("Назначение", Элемент.ТекущиеДанные.Назначение);
	ПараметрыФормы.Вставить("Упаковка", Элемент.ТекущиеДанные.Упаковка);
	ПараметрыФормы.Вставить("Количество", Элемент.ТекущиеДанные.КоличествоУпаковок);
	ПараметрыФормы.Вставить("Режим", "Просмотр");
	ПараметрыФормы.Вставить("НомерСтроки", Элемент.ТекущаяСтрока); 
	ПараметрыФормы.Вставить("Склад", Склад);
	ПараметрыФормы.Вставить("Помещение", Помещение);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
	ПараметрыФормы.Вставить("Артикул", Элемент.ТекущиеДанные.Артикул);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.КарточкаТовара",ПараметрыФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НачатьПриемку(Команда)
	
	Если Товары.Количество() = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нет товаров для приемки';
											|en = 'There are no goods to receive'");
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = ПолучитьСтруктуруЗаполненияОрдера();
	
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ЭтотОбъект);
	
	Описание = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТипДокумента", 1);
	ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);  
	ПараметрыФормы.Вставить("СтруктураЗаполнения", СтруктураЗаполнения);
	
	ОткрытьФорму(
	"Обработка.МобильноеРабочееМестоКладовщика.Форма.ПриходныйОрдер",ПараметрыФормы,
		ЭтотОбъект,,,,Описание,
		РежимОткрытияОкнаФормы.Независимый);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОтгрузку(Команда)
	
	ОчиститьСообщения();
	
	Если Товары.Количество() = 0 Тогда
		СообщениеПользователю = Новый СообщениеПользователю;
		СообщениеПользователю.Текст = НСтр("ru = 'Нет товаров для отгрузки';
											|en = 'There are no goods to ship'");
		СообщениеПользователю.Сообщить();
		Возврат;
	КонецЕсли; 
	
	Описание = Новый ОписаниеОповещения("ЗакрытьФорму", ЭтотОбъект);

	Если ТипДокумента = 3 Тогда
		
		УказаниеСерий = ПроверитьСтатусУказанияСерийПриПланированииОтбора(РасходныйОрдер); 
		
		Если УказаниеСерий Тогда
			
			ОчиститьСообщения();
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ссылка",РасходныйОрдер);
			ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров); 
			
			ОткрытьФорму(
				"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
				ЭтотОбъект,,,,Описание,
				РежимОткрытияОкнаФормы.Независимый);
				
			Возврат;
		КонецЕсли;
		
		СсылкаОтбор = НовыйОтборТоваров(РасходныйОрдер);

		Если Не ЗначениеЗаполнено(СсылкаОтбор) Тогда		
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать задания на отбор товаров согласно правилам отбора.';
								|en = 'Cannot generate goods picking jobs according to the picking rules.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);	
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", СсылкаОтбор);
		ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
		ПараметрыФормы.Вставить("Бесшовный", Истина);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
		
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
			
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = ПолучитьСтруктуруЗаполненияРасходногоОрдера();
	
	ЗаполнитьЗначенияСвойств(СтруктураЗаполнения, ЭтотОбъект);
	СтруктураЗаполнения.РаспоряженияНаОтгрузку.Добавить(РаспоряжениеОтгрузка);
	
	РезультатСоздания = НовыйРасходныйОрдер(СтруктураЗаполнения);
	
	Если Не ЗначениеЗаполнено(РезультатСоздания.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьАдресноеХранение = ЭтоАдресноеХранение(РезультатСоздания.Ссылка);
	
	Если ИспользоватьАдресноеХранение Тогда  
		
		Если РезультатСоздания.ТребуетсяУказаниеСерии Тогда
			
			ОчиститьСообщения();
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ссылка", РезультатСоздания.Ссылка);
			ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров); 
			
			ОткрытьФорму(
				"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
				ЭтотОбъект,,,,Описание,
				РежимОткрытияОкнаФормы.Независимый);
				
			Возврат;
		КонецЕсли;

		СсылкаОтбор = НовыйОтборТоваров(РезультатСоздания.Ссылка);
		
		Если Не ЗначениеЗаполнено(СсылкаОтбор) Тогда
			ТекстОшибки = НСтр("ru = 'Не удалось сформировать задания на отбор товаров согласно правилам отбора.';
								|en = 'Cannot generate goods picking jobs according to the picking rules.'");
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
			Возврат;
		КонецЕсли;
	
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", СсылкаОтбор);
		ПараметрыФормы.Вставить("Исполнитель", Исполнитель);
		ПараметрыФормы.Вставить("Бесшовный", Истина);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);
				
		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.Отбор", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
		
	Иначе
		
		Если РезультатСоздания.ЕстьОшибка Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Ссылка", РезультатСоздания.Ссылка);
		ПараметрыФормы.Вставить("ПоказыватьФотоТоваров", ПоказыватьФотоТоваров);

		ОткрытьФорму(
			"Обработка.МобильноеРабочееМестоКладовщика.Форма.РасходныйОрдер", ПараметрыФормы,
			ЭтотОбъект,,,,Описание,
			РежимОткрытияОкнаФормы.Независимый);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьПредставлениеРаспоряжения(НазваниеДокумента, Номер, Дата)
	
	СокрНомер = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Номер);

	Возврат СтрШаблон(НСтр("ru = '%1 № %2 от %3';
							|en = '%1 No. %2 from %3'"), НазваниеДокумента, СокрНомер, Формат(Дата, НСтр("ru = 'ДФ=''дд.ММ.гг ЧЧ:мм''';
																									|en = 'DF=''MM/dd/yy hh:mm tt'''")));
	
КонецФункции

&НаКлиенте
Процедура ЗакрытьФорму(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ЭтаФорма.Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруЗаполненияОрдера()
	
	Возврат Обработки.МобильноеРабочееМестоКладовщика.СтруктураЗаполненияПриходногоОрдера();
	
КонецФункции 

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруЗаполненияРасходногоОрдера()
	
	Возврат СкладыСервер.ПараметрыПереоформленияРасходныхОрдеров();
	
КонецФункции

&НаСервере
Функция ЭтоАдресноеХранение(Знач СсылкаНаРасходныйОрдер)
	
	СвойстваСклада = Обработки.МобильноеРабочееМестоКладовщика.СвойстваСклада(Склад);	
	ИспользоватьАдресноеХранение = СвойстваСклада.ИспользоватьАдресноеХранение;
	Помещение = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаРасходныйОрдер, "Помещение");
	Если СвойстваСклада.ИспользоватьСкладскиеПомещения Тогда
		СтруктураПомещения = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Помещение, "ИспользоватьАдресноеХранение");
		ИспользоватьАдресноеХранение = СтруктураПомещения.ИспользоватьАдресноеХранение;
	КонецЕсли;
	
	Возврат ИспользоватьАдресноеХранение;
	
КонецФункции

&НаСервере
Функция ПроверитьСтатусУказанияСерийПриПланированииОтбора(Знач СсылкаНаРасходныйОрдер)
	
	УказаниеСерий = 
		Обработки.МобильноеРабочееМестоКладовщика.ПроверитьСтатусУказанияСерийПриПланированииОтбора(СсылкаНаРасходныйОрдер, 
		Склад);
	
	Возврат УказаниеСерий;
	
КонецФункции

&НаСервере
Функция СписокТоваровНаОтгрузкуПоОрдеру()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Номенклатура.Артикул КАК Артикул,
		|	ТоварыПоРаспоряжениям.Номенклатура.ЕдиницаИзмерения КАК Упаковка,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Количество КАК Количество,
		|	ТоварыПоРаспоряжениям.Количество КАК КоличествоУпаковок
		|ИЗ
		|	Документ.РасходныйОрдерНаТовары.ТоварыПоРаспоряжениям КАК ТоварыПоРаспоряжениям
		|ГДЕ
		|	ТоварыПоРаспоряжениям.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", РасходныйОрдер);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Функция НовыйРасходныйОрдер(Знач СтруктураЗаполнения)
		
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НовыйРасходныйОрдер(СтруктураЗаполнения);

КонецФункции 

&НаСервереБезКонтекста
Функция НовыйОтборТоваров(Знач СсылкаРасходныйОрдер)
		
	Возврат Обработки.МобильноеРабочееМестоКладовщика.НовыйОтборТоваров(СсылкаРасходныйОрдер);

КонецФункции

#КонецОбласти