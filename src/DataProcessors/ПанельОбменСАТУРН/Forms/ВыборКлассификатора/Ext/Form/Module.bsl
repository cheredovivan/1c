
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьФорму();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

//@skip-check module-unused-method
//@skip-check module-structure-form-event-regions
&НаКлиенте
Процедура ОткрытьКлассификатор(Команда)
	
	ИмяКлассификатора = СтрШаблон("Справочник.%1.ФормаСписка", Команда.Имя);
	ОткрытьФорму(ИмяКлассификатора,, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФорму()
	
	КлассификаторыСАТУРН = Новый СписокЗначений();
	
	КлассификаторыСАТУРН.Добавить(Метаданные.Справочники.КлассификаторСтранСАТУРН, НСтр("ru = 'Страны';
																						|en = 'Страны'"));
	КлассификаторыСАТУРН.Добавить(Метаданные.Справочники.КлассификаторТранспортныхСредствСАТУРН, НСтр("ru = 'Транспортные средства';
																										|en = 'Транспортные средства'"));
	КлассификаторыСАТУРН.Добавить(Метаданные.Справочники.КлассификаторТаможенныхПунктовСАТУРН, НСтр("ru = 'Таможенные пункты';
																									|en = 'Таможенные пункты'"));
	КлассификаторыСАТУРН.Добавить(Метаданные.Справочники.КлассификаторТерриториальныхУправленийСАТУРН, НСтр("ru = 'Территориальные управления';
																											|en = 'Территориальные управления'"));
	
	Для Каждого Классификатор Из КлассификаторыСАТУРН Цикл
		
		МетаданныеКлассификатора = Классификатор.Значение;
		
		Если ПустаяСтрока(МетаданныеКлассификатора.Комментарий) Тогда
			Подсказка = СтрШаблон(
				НСтр("ru = 'Открыть ""%1""';
					|en = 'Открыть ""%1""'"),
				МетаданныеКлассификатора.Синоним);
		Иначе
			Подсказка = МетаданныеКлассификатора.Комментарий;
		КонецЕсли; 
		
		Команда = Команды.Добавить(МетаданныеКлассификатора.Имя);
		Команда.Заголовок = Классификатор.Представление;
		Команда.Подсказка = Подсказка;
		Команда.Действие  = "ОткрытьКлассификатор";
		
		ЭлементФормы = Элементы.Добавить(МетаданныеКлассификатора.Имя, Тип("КнопкаФормы"), Элементы.ГруппаКлассификаторы);
		ЭлементФормы.ИмяКоманды           = Команда.Имя;
		ЭлементФормы.Вид                  = ВидКнопкиФормы.Гиперссылка;
		ЭлементФормы.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
