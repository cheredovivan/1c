///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	Элементы.ДекорацияСостояние.Заголовок = НСтр("ru = 'Получение списка участников СБП, пожалуйста, подождите...';
												|en = 'Getting the list of FPS parties. Please wait...'");
	
	РежимРаботы = Параметры.РежимРаботы;
	ДополнительныеПараметры = Параметры.ДополнительныеПараметры;
	ОснованиеПлатежа = Параметры.ОснованиеПлатежа;
	
	Если РежимРаботы = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки() Тогда
		НастройкаПодключенияКопирование = Параметры.НастройкаПодключения;
	Иначе
		НастройкаПодключенияРедактируемая = Параметры.НастройкаПодключения;
	КонецЕсли;
	
	ОтображатьОписаниеСервиса = Параметры.ОтображатьОписаниеСервиса
		И Не Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ЕстьНастройкиПодключения();
	ЗаполненыДанныеАутентификации =
		ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
	
	ЕстьОтправкаSMS = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS");
	ЕстьОтправкаПисем = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");
	
	Если ЗначениеЗаполнено(НастройкаПодключенияКопирование) Тогда
		ДанныеЗаполнения = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДанныеНастройкиПодключения(
			НастройкаПодключенияКопирование);
		ЗаполнитьЗначенияСвойств(
			ЭтотОбъект,
			ДанныеЗаполнения,
			"ВариантНастройки, ИдентификаторУчастника, Наименование");
		ДоступенВыборВариантаНастройки = Ложь;
	ИначеЕсли ЗначениеЗаполнено(НастройкаПодключенияРедактируемая) Тогда
		ДанныеЗаполнения = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ДанныеНастройкиПодключения(
			НастройкаПодключенияРедактируемая);
		ЗаполнитьЗначенияСвойств(
			ЭтотОбъект,
			ДанныеЗаполнения,
			"ВариантНастройки, Наименование");
		ДоступенВыборВариантаНастройки = Ложь;
	ИначеЕсли ЗначениеЗаполнено(Параметры.ВариантНастройки) Тогда
		ВариантНастройки = Перечисления.ВариантыНастройкиСБП[Параметры.ВариантНастройки];
		ДоступенВыборВариантаНастройки = Ложь;
	Иначе
		СистемаБыстрыхПлатежейСобытия.ПриОпределенииДоступныхВариантовНастройки(
			ЭтотОбъект,
			ДоступенВыборВариантаНастройки,
			ВариантНастройки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.БИК) Тогда
		ИдентификаторУчастника = СистемаБыстрыхПлатежейСлужебный.ИнтегрированныйУчастникСБППоБИК(
			Параметры.БИК,
			ВариантНастройки);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ОтборУчастников)
		И Не ЗначениеЗаполнено(ИдентификаторУчастника)
		И ВариантНастройки = Перечисления.ВариантыНастройкиСБП.c2b Тогда
		ОтборУчастников = Параметры.ОтборУчастников;
		ЗаполнитьОтборыУчастников();
	КонецЕсли;
	
	Если ОтображатьОписаниеСервиса Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация;
	ИначеЕсли Не ЗаполненыДанныеАутентификации Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
	ИначеЕсли ДоступенВыборВариантаНастройки Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	КонецЕсли;
	
	ПредопределенныеШаблоны = Неопределено;
	СистемаБыстрыхПлатежейСобытия.ПриОпределенииШаблоновНазначений(
		ВариантНастройки,
		СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы(),
		ПредопределенныеШаблоны);
	Если ПредопределенныеШаблоны = Неопределено Или ПредопределенныеШаблоны.Количество() = 0 Тогда
		Элементы.ГруппаШаблоныНазначений.Видимость = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания(
		"ПолучитьУчастниковСБП",
		0.1,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольИПППриИзменении(Элемент)
	
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольИППНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		"ПарольИПП");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПользовательскоеСоглашениеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:userAgreement" Тогда
		СтандартнаяОбработка = Ложь;
		
		НастройкиПлатежныхСистемКлиент.ФормаПользовательскогоСоглашения(ЭтотОбъект);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеРезультатаОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:log" Тогда
		СтандартнаяОбработка = Ложь;
		СистемаБыстрыхПлатежейКлиент.ОткрытьЖурналРегистрации();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПодсказкаНастройкиОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:instrurtions" Тогда
		СтандартнаяОбработка = Ложь;
		Если ВариантНастройки = ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.c2b") Тогда
			ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуИнтегрированногоСайта(
				ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыПорталаПоддержки(
					"/app/1csbp#additionalInfo"));
		Иначе
			ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуИнтегрированногоСайта(
				ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыПорталаПоддержки(
					"/app/1csbpb2b#additionalInfo"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПоясненияПодключенияАвторизацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "action:openPortal" Тогда
		СтандартнаяОбработка = Ложь;
		ИнтернетПоддержкаПользователейКлиент.ОткрытьСтраницуИнтегрированногоСайта(
			ИнтернетПоддержкаПользователейКлиентСервер.URLСтраницыСервисаLogin(
				,
				ИнтернетПоддержкаПользователейКлиент.НастройкиСоединенияССерверами()),
			"",
			Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОписаниеОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "open:sbpDescription" Тогда
		СтандартнаяОбработка = Ложь;
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
			"https://v8.1c.ru/sbp/");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДополнительнаяИнформацияОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
		
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("ПараметрыОплатыСБПc2b", НастройкиОплатыПоДаннымФормы());
	ДанныеФормы.Вставить("НастройкаПодключения", Неопределено);
	ДанныеФормы.Вставить("Модифицированность", Модифицированность);
	
	СистемаБыстрыхПлатежейКлиент.ПриОбработкеНавигационнойСсылкиДополнительнойИнформации(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка,
		ДанныеФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуСпискаШаблоныСообщенийНажатие(Элемент)
	
	Если Не ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура();
	Отбор.Вставить("Ссылка", СозданныеШаблоныСообщений);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("Отбор", Отбор);
	
	ИмяФормыСпискаШаблонов = "Справочник.ШаблоныСообщений.ФормаСписка";
	ОткрытьФорму(ИмяФормыСпискаШаблонов, ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиШаблоновОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "open:paymentPurpose" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ДекорацияНастройкиШаблоновЗавершение",
		ЭтотОбъект);
	
	НастройкиПлатежныхСистемКлиент.ОткрытьНастройкуШаблоновНазначения(
		НастройкаПодключения,
		ЭтотОбъект,
		ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКассовыеСсылкиОбработкаНавигационнойСсылки(
		Элемент,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки <> "open:cashQrc" Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ОповещениеПослеЗакрытияФормыКассовыхСсылок = Новый ОписаниеОповещения(
		"ПослеЗакрытияФормыКассовыхСсылок",
		ЭтотОбъект,
		Новый Структура);
	
	ПараметрыНастройки = Новый Структура;
	ПараметрыНастройки.Вставить("НастройкаПодключения", НастройкаПодключения);
	
	МодульПереводыСБПc2bКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ПереводыСБПc2bКлиент");
	МодульПереводыСБПc2bКлиент.ПриНастройкеКассовыхСсылок(
		ПараметрыНастройки,
		ОповещениеПослеЗакрытияФормыКассовыхСсылок);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблоныПисемПриИзменении(Элемент)
	
	ОбработатьВидимостьПредупрежденияЭП();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьШаблоныSMSПриИзменении(Элемент)
	
	ОбработатьВидимостьПредупрежденияSMS();
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиОплаты(Элемент)
	
	ПриИзмененииНастройкиОплатыНаСервере();
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_ПриИзмененииНастройкиАутентификации(
		Элемент,
		Текст,
		СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ПриИзмененииСекретныхДанных(
		Элемент);
	
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура Подключаемый_НачалоВыбораНастройкиАутентификации(
		Элемент,
		ДанныеВыбора,
		СтандартнаяОбработка)
	
	ИнтернетПоддержкаПользователейКлиент.ОтобразитьСекретныеДанные(
		ЭтотОбъект,
		Элемент,
		Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУчастникиСБП

&НаКлиенте
Процедура УчастникиСБПВыбор(
		Элемент,
		ВыбраннаяСтрока,
		Поле,
		СтандартнаяОбработка)
	
	Далее(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Далее(Команда)
	
	Отказ = Ложь;
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация Тогда
		
		Если Не ЗаполненыДанныеАутентификации Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
		ИначеЕсли ЗначениеЗаполнено(ИдентификаторУчастника) И Не ДоступенВыборВариантаНастройки Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
			НастроитьОтображениеУчастниковСБП();
			ЗаполнитьНастройкиШаблоновСообщений();
		ИначеЕсли ДоступенВыборВариантаНастройки Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки;
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
			НастроитьОтображениеУчастниковСБП();
			ЗаполнитьНастройкиШаблоновСообщений();
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу Тогда
		
		Результат = ИнтернетПоддержкаПользователейКлиентСервер.ПроверитьДанныеАутентификации(
			Новый Структура("Логин, Пароль",
			ЛогинИПП, ПарольИПП));
		
		Если Результат.Отказ Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				Результат.СообщениеОбОшибке,
				,
				Результат.Поле);
			Возврат;
		КонецЕсли;
		
		ПроверитьПодключениеКПорталу1СИТС(
			ЛогинИПП,
			ПарольИПП,
			Отказ);
		
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
		ЛогинИПП = "";
		ПарольИПП = "";
		
		Если ЗначениеЗаполнено(ИдентификаторУчастника) И Не ДоступенВыборВариантаНастройки Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
		ИначеЕсли ДоступенВыборВариантаНастройки Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки;
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
			НастроитьОтображениеУчастниковСБП();
			ЗаполнитьНастройкиШаблоновСообщений();
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки Тогда
		
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
		НастроитьОтображениеУчастниковСБП();
		ЗаполнитьНастройкиШаблоновСообщений();
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника Тогда
		
		Отказ = Ложь;
		Если Элементы.УчастникиСБП.ТекущаяСтрока = Неопределено Тогда
			Отказ = Истина;
		Иначе
			СтрокаУчастникСБП = УчастникиСБППредставление.НайтиПоИдентификатору(
				Элементы.УчастникиСБП.ТекущаяСтрока);
			Если СтрокаУчастникСБП = Неопределено Тогда
				Отказ = Истина;
			КонецЕсли;
		КонецЕсли;
		
		Если Отказ Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не выбран банк или платежный агрегатор для подключения к Системе быстрых платежей.';
					|en = 'A bank or a payment aggregator to connect to Faster Payments System is not selected.'"),
				,
				,
				"УчастникиСБП");
			Возврат;
		КонецЕсли;
		
		ИдентификаторУчастника = СтрокаУчастникСБП.ИдентификаторУчастника;
		ДобавитьНастройкиПодключения(
			СтрокаУчастникСБП.ИдентификаторУчастника);
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП Тогда
		
		ПроверитьПараметрыПодключения(Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки Тогда
		
		Если Не ЗначениеЗаполнено(Наименование) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Не заполнено поле ""Наименование"".';
					|en = 'The field ""Description"" is blank.'"),
				,
				,
				"Наименование");
			Возврат;
		КонецЕсли;
		
		Если ДоступныШаблоныСообщений Тогда
			ОткрытьСтраницуШаблоныСообщений();
		Иначе
			НоваяНастройкаПодключения();
		КонецЕсли;
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаШаблоныСообщений Тогда
		СоздатьШаблоныСообщений();
		НоваяНастройкаПодключения();
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Закрыть(Истина);
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника
			И ДоступенВыборВариантаНастройки Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
		ИдентификаторУчастника = Неопределено;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаШаблоныСообщений Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Если РежимРаботы = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки() Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки;
		Иначе
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьНастройкуОтправкиSMS(Команда)
	
	ОповещениеОЗакрытииПомощника = Новый ОписаниеОповещения(
		"ПослеЗакрытияФормыНастройкиОтправкиSMS",
		ЭтотОбъект);
		
	ОткрытьФорму(
		"ОбщаяФорма.НастройкаОтправкиSMS",
		,
		ЭтотОбъект,
		,
		,
		,
		ОповещениеОЗакрытииПомощника);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьУчетнуюЗаписьЭлектроннойПочты(Команда)
	
	ОповещениеОЗакрытииПомощника = Новый ОписаниеОповещения(
		"ПослеЗакрытияФормыПомощникаСозданияУчетнойЗаписиПочты",
		ЭтотОбъект);
	
	ОткрытьФорму(
		"Справочник.УчетныеЗаписиЭлектроннойПочты.ФормаОбъекта"
		,
		,
		ЭтотОбъект,
		,
		,
		,
		ОповещениеОЗакрытииПомощника);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область УчастникиСБП

&НаКлиенте
Процедура ПолучитьУчастниковСБП()
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ПолучитьУчастниковСБПЗапускЗадания(
		УникальныйИдентификатор,
		ВариантНастройки);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПолучитьУчастниковСБПЗапускЗаданияЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ПолучитьУчастниковСБПЗапускЗаданияЗавершение(
			РезультатВыполнения,
			Неопределено);
		Возврат;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьУчастниковСБПЗапускЗадания(
		Знач УникальныйИдентификатор,
		Знач ВариантНастройки)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение актуального списка участников СБП.';
															|en = 'Get an up-to-date list of FPS parties.'");
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ВариантНастройки", ВариантНастройки);
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"СистемаБыстрыхПлатежейСлужебный.ПолучитьУчастниковСБПВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьУчастниковСБПЗапускЗаданияЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			УстановитьОтображениеИнформацииОбОшибке(
				РезультатОперации.СообщениеОбОшибке,
				Ложь,
				Ложь);
		Иначе
			
			Для Каждого КлючЗначение Из РезультатОперации.ДанныеУчастников Цикл
				НовыйУчастник = УчастникиСБПСервис.Добавить();
				НовыйУчастник.Участник = КлючЗначение.Значение.Представление;
				НовыйУчастник.ИдентификаторУчастника = КлючЗначение.Ключ;
				НовыйУчастник.ПартнерАгентаСБП = КлючЗначение.Значение.ПартнерАгентаСБП;
				НовыйУчастник.АдресСтраницыЗаявкиc2b = КлючЗначение.Значение.АдресСтраницыЗаявкиc2b;
				НовыйУчастник.ПорядокОтображенияc2b = КлючЗначение.Значение.ПорядокОтображенияc2b;
				НовыйУчастник.АдресЛичногоКабинетаc2b = КлючЗначение.Значение.АдресСтраницыПодключенияc2b;
				НовыйУчастник.ТекстПредупрежденияc2b = КлючЗначение.Значение.ТекстПредупрежденияc2b;
				НовыйУчастник.АдресСтраницыЗаявкиb2b = КлючЗначение.Значение.АдресСтраницыЗаявкиb2b;
				НовыйУчастник.ПорядокОтображенияb2b = КлючЗначение.Значение.ПорядокОтображенияb2b;
				НовыйУчастник.АдресЛичногоКабинетаb2b = КлючЗначение.Значение.АдресСтраницыПодключенияb2b;
				НовыйУчастник.ТекстПредупрежденияb2b = КлючЗначение.Значение.ТекстПредупрежденияb2b;
				НовыйУчастник.c2b = КлючЗначение.Значение.c2b;
				НовыйУчастник.b2b = КлючЗначение.Значение.b2b;
				НовыйУчастник.БИК.ЗагрузитьЗначения(КлючЗначение.Значение.БИК);
			КонецЦикла;
			ДанныеУчастниковЗагружены = Истина;
			
			Если ОтображатьОписаниеСервиса Тогда
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация;
			ИначеЕсли Не ЗаполненыДанныеАутентификации Тогда
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу;
			ИначеЕсли ДоступенВыборВариантаНастройки Тогда
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки;
			Иначе
				НастроитьОтображениеУчастниковСБП();
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка подключения к Системе быстрых платежей.';
				|en = 'An error occurred while connecting to Faster Payments System.'"));
		ИнформацияОбОшибке = ДанныеОшибки.Текст;
		УстановитьОтображениеИнформацииОбОшибке(ИнформацияОбОшибке);
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьОтображениеУчастниковСБП()
	
	УчастникиСБППредставление.Очистить();
	Для Каждого УчастникСервис Из УчастникиСБПСервис Цикл
		
		Если ОтборУчастников = "ПлатежныеАгрегаторы"
			И ПлатежныеАгрегаторы.НайтиПоЗначению(УчастникСервис.ИдентификаторУчастника) = Неопределено Тогда
			Продолжить;
		ИначеЕсли ОтборУчастников = "Банки"
			И ПлатежныеАгрегаторы.НайтиПоЗначению(УчастникСервис.ИдентификаторУчастника) <> Неопределено Тогда
			Продолжить;
		ИначеЕсли ОтборУчастников = "КассовыеСсылки"
			И ПоддержкаКассовыхСсылок.НайтиПоЗначению(УчастникСервис.ИдентификаторУчастника) = Неопределено Тогда
			Продолжить;
		ИначеЕсли ВариантНастройки = ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.b2b")
			И Не УчастникСервис.b2b Тогда
			Продолжить;
		ИначеЕсли ВариантНастройки = ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.c2b")
			И Не УчастникСервис.c2b Тогда
			Продолжить;
		КонецЕсли;
		
		УчастникПредставление = УчастникиСБППредставление.Добавить();
		ЗаполнитьЗначенияСвойств(
			УчастникПредставление,
			УчастникСервис,
			"Участник, ИдентификаторУчастника");
		УчастникПредставление.ПорядокОтображения = ?(
			ВариантНастройки = ПредопределенноеЗначение("Перечисление.ВариантыНастройкиСБП.b2b"),
				УчастникСервис.ПорядокОтображенияb2b,
				УчастникСервис.ПорядокОтображенияc2b);
		
	КонецЦикла;
	
	УчастникиСБППредставление.Сортировать("ПорядокОтображения, Участник");
	
	Если ЗначениеЗаполнено(ИдентификаторУчастника) Тогда
		ДобавитьНастройкиПодключения(ИдентификаторУчастника);
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НастройкиУчастникаПоИдентификатору(ИдентификаторОтбор)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИдентификаторУчастника", ИдентификаторОтбор);
	
	НастройкиОтбор = УчастникиСБПСервис.НайтиСтроки(Отбор);
	Если НастройкиОтбор.Количество() = 0 Тогда
		ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настройки участника СБП с идентификатором %1 не найдены.';
				|en = 'Setting of the Faster Payments System participant with ID %1 are not found.'"),
			ИдентификаторОтбор);
	КонецЕсли;
	
	Если ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b Тогда
		НастройкиПодключения = Новый Структура;
		НастройкиПодключения.Вставить("АдресЛичногоКабинета", НастройкиОтбор[0].АдресЛичногоКабинетаb2b);
		НастройкиПодключения.Вставить("ТекстПредупреждения", НастройкиОтбор[0].ТекстПредупрежденияb2b);
		НастройкиПодключения.Вставить("ПартнерАгентаСБП", НастройкиОтбор[0].ПартнерАгентаСБП);
		НастройкиПодключения.Вставить("АдресСтраницыЗаявки", НастройкиОтбор[0].АдресСтраницыЗаявкиb2b);
		НастройкиПодключения.Вставить("ИдентификаторУчастника", НастройкиОтбор[0].ИдентификаторУчастника);
		НастройкиПодключения.Вставить("ВариантНастройки", ВариантНастройки);
	ИначеЕсли ВариантНастройки = Перечисления.ВариантыНастройкиСБП.c2b Тогда
		НастройкиПодключения = Новый Структура;
		НастройкиПодключения.Вставить("АдресЛичногоКабинета", НастройкиОтбор[0].АдресЛичногоКабинетаc2b);
		НастройкиПодключения.Вставить("ТекстПредупреждения", НастройкиОтбор[0].ТекстПредупрежденияc2b);
		НастройкиПодключения.Вставить("ПартнерАгентаСБП", НастройкиОтбор[0].ПартнерАгентаСБП);
		НастройкиПодключения.Вставить("АдресСтраницыЗаявки", НастройкиОтбор[0].АдресСтраницыЗаявкиc2b);
		НастройкиПодключения.Вставить("ИдентификаторУчастника", НастройкиОтбор[0].ИдентификаторУчастника);
		НастройкиПодключения.Вставить("ВариантНастройки", ВариантНастройки);
	Иначе
		СистемаБыстрыхПлатежейСлужебный.ПроверитьВариантНастройки(
			ВариантНастройки);
	КонецЕсли;
	
	Возврат НастройкиПодключения;
	
КонецФункции

#КонецОбласти

#Область Подключение

&НаСервереБезКонтекста
Процедура ПроверитьПодключениеКПорталу1СИТС(
		Знач Логин,
		Знач Пароль,
		Знач Отказ)
	
	РезультатПроверки = ИнтернетПоддержкаПользователей.ПроверитьЛогинИПароль(
		Логин,
		Пароль);
	
	Если РезультатПроверки.Результат Тогда
		
		ДанныеАутентификации = Новый Структура;
		ДанныеАутентификации.Вставить("Логин", Логин);
		ДанныеАутентификации.Вставить("Пароль", Пароль);
		
		УстановитьПривилегированныйРежим(Истина);
		ИнтернетПоддержкаПользователей.СохранитьДанныеАутентификации(
			ДанныеАутентификации);
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ОбщегоНазначения.СообщитьПользователю(
			РезультатПроверки.СообщениеОбОшибке,
			,
			,
			,
			Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПараметрыПодключения(Отказ)
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	РезультатВыполнения = ПроверитьПараметрыПодключенияЗапускЗадания();
	
	Если РезультатВыполнения = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ПроверитьПараметрыПодключенияЗавершение",
		ЭтотОбъект);
		
	Если РезультатВыполнения.Статус = "Выполнено" Или РезультатВыполнения.Статус = "Ошибка" Тогда
		ПроверитьПараметрыПодключенияЗавершение(РезультатВыполнения, Неопределено);
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация;
	Элементы.ДекорацияСостояние.Заголовок   = НСтр("ru = 'Проверка параметров подключения к Системе быстрых платежей.';
													|en = 'Check the parameters of the Faster payments system connection.'");
	
	УстановитьВидимостьДоступность();
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(
		РезультатВыполнения,
		ОповещениеОЗавершении,
		ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьПараметрыПодключенияЗапускЗадания()
	
	Отказ = Ложь;
	СистемаБыстрыхПлатежейСлужебный.ПроверитьНастройкиАутентификации(
		ЭтотОбъект,
		Отказ);
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыАутентификации = СистемаБыстрыхПлатежейСлужебный.НастройкиАутентификацииПоДаннымФормы(
		ЭтотОбъект);
	
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ПараметрыАутентификации", ПараметрыАутентификации);
	ПараметрыПроцедуры.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
	ПараметрыПроцедуры.Вставить("ВариантНастройки", ВариантНастройки);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(
		УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания
		= НСтр("ru = 'Проверка параметров подключения к Системе быстрых платежей.';
				|en = 'Check the parameters of the Faster payments system connection.'");
	
	РезультатВыполнения = ДлительныеОперации.ВыполнитьВФоне(
		"СистемаБыстрыхПлатежейСлужебный.ПроверитьПараметрыПодключенияВФоне",
		ПараметрыПроцедуры,
		ПараметрыВыполнения);
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПараметрыПодключенияЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Выполнено" Тогда
		
		РезультатОперации = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
			Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
			ПоказатьПредупреждение(
				Неопределено,
				РезультатОперации.СообщениеОбОшибке);
		Иначе
			Если РежимРаботы = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки() Тогда
				НастроитьПараметрыПодключения();
				Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки;
			Иначе
				Результат = ИзменитьУчастника();
				Если Не Результат.Ошибка Тогда
					Оповестить("БИПСменаУчастникаСБП");
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
		ДанныеОшибки = ОбщегоНазначенияКлиентСервер.УточнениеИсключения(
			Результат.ИнформацияОбОшибке,
			НСтр("ru = 'Ошибка подключения к Системе быстрых платежей.';
				|en = 'An error occurred while connecting to Faster Payments System.'"));
		ИнформацияОбОшибке = ДанныеОшибки.Текст;
		ПоказатьПредупреждение(
			Неопределено,
			ИнформацияОбОшибке);
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройкиПодключения(Знач ИдентификаторУчастника)
	
	НастройкиПодключения = НастройкиУчастникаПоИдентификатору(
		ИдентификаторУчастника);
	ДобавитьНастройкиАутентификации(
		НастройкиПодключения);
	ДобавитьНастройкиОплаты();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройкиОплаты()
	
	Если ТекущийВариантНастройки = ВариантНастройки Тогда
		Возврат;
	КонецЕсли;
	
	СистемаБыстрыхПлатежейСлужебный.УдалитьНастройкиФормы(
		ЭтотОбъект,
		"НастройкиОплаты");
	
	// Видимость должна быть включена в переопределении.
	Элементы.ДекорацияДополнительнаяИнформация.Видимость = Ложь;
	Если РежимРаботы = СистемаБыстрыхПлатежейКлиентСервер.ИдентификаторОперацииСозданиеНастройки() Тогда
		СистемаБыстрыхПлатежейСлужебный.ДобавитьНастройкиОплаты(
			ЭтотОбъект,
			Элементы.ГруппаНастройкиОплаты,
			ВариантНастройки,
			НастройкаПодключенияКопирование,
			ОснованиеПлатежа,
			Истина);
		СистемаБыстрыхПлатежейСлужебный.НастроитьЭлементыФормыПодключения(
			ЭтотОбъект,
			ВариантНастройки,
			ИдентификаторУчастника,
			Неопределено,
			ДополнительныеПараметры);
	КонецЕсли;
	
	ТекущийВариантНастройки = ВариантНастройки;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьНастройкиАутентификации(НастройкиПодключения)
	
	СистемаБыстрыхПлатежейСлужебный.УдалитьНастройкиФормы(
		ЭтотОбъект,
		"НастройкиАутентификации");
	
	ПараметрыПодсказки = СистемаБыстрыхПлатежейСлужебный.НовыйПараметрПодсказки();
	ПараметрыПодсказки.ОтобразитьПодсказку = Истина;
	ПараметрыПодсказки.АдресЛичногоКабинета = НастройкиПодключения.АдресЛичногоКабинета;
	ПараметрыПодсказки.ПартнерАгентаСБП = НастройкиПодключения.ПартнерАгентаСБП;
	ПараметрыПодсказки.АдресСтраницыЗаявки = НастройкиПодключения.АдресСтраницыЗаявки;
	ПараметрыПодсказки.ИдентификаторУчастника = НастройкиПодключения.ИдентификаторУчастника;
	
	СистемаБыстрыхПлатежейСлужебный.ДобавитьНастройкиАутентификации(
		ЭтотОбъект,
		Элементы.ГруппаНастройкиПодключения,
		Неопределено,
		НастройкиПодключения.ИдентификаторУчастника,
		ПараметрыПодсказки,
		НастройкиПодключения.ВариантНастройки);
	
	НастройкиУчастникаСБП = СистемаБыстрыхПлатежейСлужебный.НастройкиУчастникаСБП(
		НастройкиПодключения.ИдентификаторУчастника);
	НастройкиПодключенияПрограммы = СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b")
		И НастройкиПодключенияПрограммы.c2b.ИспользоватьНастройкуКассовыхСсылок = Истина
		И НастройкиУчастникаСБП.КассовыеСсылкиc2b Тогда
		СозданиеКассовыхСсылокДоступно = Истина;
	Иначе
		СозданиеКассовыхСсылокДоступно = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП;
	Элементы.ДекорацияПредупреждение.Заголовок = НастройкиПодключения.ТекстПредупреждения;
	
КонецПроцедуры

&НаСервере
Процедура НоваяНастройкаПодключения()
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Наименование", Наименование);
	ПараметрыЗаполнения.Вставить("ИдентификаторУчастника", ИдентификаторУчастника);
	
	ПараметрыАутентификации = СистемаБыстрыхПлатежейСлужебный.НастройкиАутентификацииПоДаннымФормы(
		ЭтотОбъект);
	
	ПараметрыОплаты = СистемаБыстрыхПлатежейСлужебный.НастройкиОплатыПоДаннымФормы(
		ЭтотОбъект);
	
	Результат = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.НоваяНастройкаПодключения(
		ПараметрыЗаполнения,
		ПараметрыАутентификации,
		ПараметрыОплаты,
		ВариантНастройки);
	
	Если Результат.Ошибка Тогда
		УстановитьОтображениеИнформацииОбОшибке(
			Результат.СообщениеОбОшибке,
			Истина,
			Ложь);
	Иначе
		НастройкаПодключения = Результат.Ссылка;
		УстановитьОтображениеУспешногоЗавершения();
	КонецЕсли;
	
	УстановитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Функция ИзменитьУчастника();
	
	ПараметрыАутентификации = СистемаБыстрыхПлатежейСлужебный.НастройкиАутентификацииПоДаннымФормы(
		ЭтотОбъект);
		
	Результат = Справочники.НастройкиПодключенияКСистемеБыстрыхПлатежей.ИзменитьУчастника(
		НастройкаПодключенияРедактируемая,
		ИдентификаторУчастника,
		ПараметрыАутентификации,
		ВариантНастройки);
	
	Если Результат.Ошибка Тогда
		УстановитьОтображениеИнформацииОбОшибке(
			Результат.СообщениеОбОшибке,
			Истина,
			Ложь);
	Иначе
		УстановитьОтображениеУспешногоЗавершения();
		НастройкаПодключения = НастройкаПодключенияРедактируемая;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция НастройкиОплатыПоДаннымФормы()
	
	ПараметрыОплатыСБПc2b = СистемаБыстрыхПлатежейСлужебный.НастройкиОплатыПоДаннымФормы(
		ЭтотОбъект);
	
	ОбъектМетаданных = СистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы().c2b.ОбъектМетаданных;
	ИмяАтрибута = СистемаБыстрыхПлатежейСлужебный.АтрибутНастройкаПодключения(
		ОбъектМетаданных);
	ПараметрыОплатыСБПc2b.Вставить(ИмяАтрибута, Неопределено);
	
	Возврат ПараметрыОплатыСБПc2b;
	
КонецФункции

#КонецОбласти

#Область УправлениеЭлементамиФормы

&НаСервере
Процедура УстановитьОтображениеИнформацииОбОшибке(
		Знач ИнформацияОбОшибке,
		Знач Ошибка = Истина,
		Знач ОтображатьЖР = Истина)
	
	Если ОтображатьЖР Тогда
		ПредставлениеОшибки = СтроковыеФункции.ФорматированнаяСтрока(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = '%1
					|
					|
					|Подробную информацию см. в <a href = ""open:log"">Журнале регистрации</a>.';
					|en = '%1
					|
					|
					|For more information see the <a href = ""open:log"">Event log</a>.'"),
				ИнформацияОбОшибке));
		
	Иначе
		ПредставлениеОшибки = ИнформацияОбОшибке;
	КонецЕсли;
	
	Элементы.ГруппаДополнительныеНастройки.Видимость = Ложь;
	Элементы.ГруппаСтраницы.ТекущаяСтраница          = Элементы.ГруппаРезультат;
	Элементы.ДекорацияОписаниеРезультата.Заголовок   = ПредставлениеОшибки;
	Элементы.ДекорацияКартинкаРезультат.Картинка     = ?(Ошибка,
		БиблиотекаКартинок.Ошибка32,
		БиблиотекаКартинок.Предупреждение32);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеУспешногоЗавершения()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат;
	Элементы.ДекорацияКартинкаРезультат.Картинка = БиблиотекаКартинок.Успешно32;
	ПодключениеЗавершено = Истина;
	Элементы.ДекорацияОписаниеРезультата.Заголовок   = НСтр("ru = 'Настройка подключения создана.';
															|en = 'The connection setting is created.'");
	Элементы.ГруппаДополнительныеНастройки.Видимость = Истина;
	Элементы.ГруппаСозданиеКассовыхСсылок.Видимость  = (СозданиеКассовыхСсылокДоступно
		И (ВариантНастройки = Перечисления.ВариантыНастройкиСБП.c2b));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьДоступность()
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОбщаяИнформация
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПодключениеКПорталу
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВариантыНастройки Тогда
		Элементы.Назад.Видимость = Ложь;
		Элементы.Далее.Видимость = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборУчастника Тогда
		Элементы.Назад.Видимость = ДоступенВыборВариантаНастройки;
		Элементы.Далее.Видимость = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройкиУчастникаСБП
		Или Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрикладныеНастройки Тогда
		Элементы.Назад.Видимость = Истина;
		Элементы.Далее.Видимость = Истина;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаДлительнаяОперация Тогда
		Элементы.Назад.Видимость = Ложь;
		Элементы.Далее.Видимость = Ложь;
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаРезультат Тогда
		Если ПодключениеЗавершено Тогда
			Элементы.Назад.Видимость  = Ложь;
			Элементы.Отмена.Видимость = Ложь;
			Элементы.Далее.Видимость  = Истина;
			Элементы.Далее.Заголовок  = НСтр("ru = 'OK';
											|en = 'OK'");
		ИначеЕсли Не ДанныеУчастниковЗагружены Тогда
			Элементы.Назад.Видимость  = Ложь;
			Элементы.Отмена.Видимость = Истина;
			Элементы.Далее.Видимость  = Ложь;
		Иначе
			Элементы.Назад.Видимость  = Истина;
			Элементы.Отмена.Видимость = Истина;
			Элементы.Далее.Видимость  = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Элементы.ДекорацияПредупреждение.Заголовок) Тогда
		Элементы.ГруппаПредупреждение.Видимость = Ложь;
	Иначе
		Элементы.ГруппаПредупреждение.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ШаблоныСообщений

&НаСервере
Процедура ЗаполнитьНастройкиШаблоновСообщений()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПроверки = СистемаБыстрыхПлатежейСлужебный.ШаблоныСозданы(
		ВариантНастройки);
	ШаблоныSMSСозданы = РезультатПроверки.SMS;
	ШаблоныПисемСозданы = РезультатПроверки.Письмо;
	
	Если Не РезультатПроверки.ВсеШаблоны Тогда
		ДоступныШаблоныСообщений = ИспользоватьШаблоныСообщений(
			ЕстьОтправкаПисем,
			ЕстьОтправкаSMS,
			ВариантНастройки);
		СоздатьШаблоныПисем = Истина;
		СоздатьШаблоныSMS = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСтраницуШаблоныСообщений()
	
	Если ЕстьОтправкаПисем И ЕстьОтправкаSMS Тогда
		ПредставлениеТиповШаблонов = НСтр("ru = 'писем и SMS';
											|en = 'emails and messages'");
	ИначеЕсли ЕстьОтправкаПисем Тогда
		ПредставлениеТиповШаблонов = НСтр("ru = 'писем';
											|en = 'emails'");
	ИначеЕсли ЕстьОтправкаSMS Тогда
		ПредставлениеТиповШаблонов = НСтр("ru = 'SMS';
											|en = 'SMS'");
	Иначе
		ПредставлениеТиповШаблонов = "";
	КонецЕсли;
	
	Элементы.ПодсказкаОписаниеШаблоновСообщений.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Элементы.ПодсказкаОписаниеШаблоновСообщений.Заголовок,
		ПредставлениеТиповШаблонов);
	
	Если Не ШаблоныПисемСозданы И ЕстьОтправкаПисем Тогда
		Элементы.ГруппаШаблоныПисем.Видимость = Истина;
		ОбработатьВидимостьПредупрежденияЭП();
	Иначе
		Элементы.ГруппаШаблоныПисем.Видимость = Ложь;
	КонецЕсли;
	
	Если Не ШаблоныSMSСозданы И ЕстьОтправкаSMS Тогда
		Элементы.ГруппаШаблоныSMS.Видимость = Истина;
		ОбработатьВидимостьПредупрежденияSMS();
	Иначе
		Элементы.ГруппаШаблоныSMS.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаШаблоныСообщений;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользоватьШаблоныСообщений(
		ЕстьОтправкаПисем,
		ЕстьОтправкаSMS,
		ВариантНастройки)
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ШаблоныСообщений") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЕстьОтправкаSMS И Не ЕстьОтправкаПисем Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Использовать = Ложь;
	ИнтеграцияПодсистемБИП.ПриПроверкеИспользованияШаблоновСообщений(Использовать);
	СистемаБыстрыхПлатежейПереопределяемый.ПриПроверкеИспользованияШаблоновСообщений(
		Использовать);
	
	Если Не Использовать Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НастройкиШаблонов = СистемаБыстрыхПлатежейСлужебный.НовыйНастройкиШаблоновСообщений();
	СистемаБыстрыхПлатежейСлужебный.ПриОпределенииПредопределенныхШаблоновСообщений(
		НастройкиШаблонов);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b")
		И ВариантНастройки = Перечисления.ВариантыНастройкиСБП.c2b
		И НастройкиШаблонов.c2b.Количество() = 0 Тогда
		Возврат Ложь;
	ИначеЕсли ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.ПереводыСБПc2b")
		И ВариантНастройки = Перечисления.ВариантыНастройкиСБП.b2b
		И НастройкиШаблонов.b2b.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьВидимостьПредупрежденияЭП()
	
	Элементы.ГруппаВниманиеНетУчетнойЗаписиЭлектроннойПочты.Видимость
		= (СоздатьШаблоныПисем И ИспользоватьРаботуСПочтой());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВидимостьПредупрежденияSMS() 
	
	Элементы.ГруппаВниманиеНеНастроенаОтправкаSMS.Видимость
		= (СоздатьШаблоныSMS И ИспользоватьSMS());
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыПомощникаСозданияУчетнойЗаписиПочты(
		РезультатЗакрытия,
		ДополнительныеПараметры) Экспорт
	
	ОбработатьВидимостьПредупрежденияЭП();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыНастройкиОтправкиSMS(
		РезультатЗакрытия,
		ДополнительныеПараметры) Экспорт 
	
	ОбработатьВидимостьПредупрежденияSMS();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользоватьРаботуСПочтой()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МодульРаботаСПочтовымиСообщениями = ОбщегоНазначения.ОбщийМодуль("РаботаСПочтовымиСообщениями");
	ДоступныеУчетныеЗаписиЭП = МодульРаботаСПочтовымиСообщениями.ДоступныеУчетныеЗаписи(Истина);
	
	Возврат МодульРаботаСПочтовымиСообщениями.ДоступнаОтправкаПисем()
		И ДоступныеУчетныеЗаписиЭП.Количество() = 0;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИспользоватьSMS()
	
	Если Не ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ОтправкаSMS") Тогда
		Возврат Ложь;
	КонецЕсли;
	
	МодульОтправкаSMS = ОбщегоНазначения.ОбщийМодуль("ОтправкаSMS");
	
	Возврат (МодульОтправкаSMS.ДоступнаОтправкаSMS()
		И Не МодульОтправкаSMS.НастройкаОтправкиSMSВыполнена());
	
КонецФункции

&НаСервере
Процедура СоздатьШаблоныСообщений()
	
	Шаблоны = Новый Массив;
	Если СоздатьШаблоныПисем Или СоздатьШаблоныSMS Тогда
		СистемаБыстрыхПлатежейСлужебный.УстановитьИспользованиеШаблоновСообщений();
		Шаблоны = СистемаБыстрыхПлатежейСлужебный.СоздатьПредопределенныеШаблоныСообщений(
			ВариантНастройки,
			СистемаБыстрыхПлатежейСлужебный.НовыйНастройкаСозданияШаблонов(
				СоздатьШаблоныПисем,
				СоздатьШаблоныSMS));
		СозданныеШаблоныСообщений.ЗагрузитьЗначения(Шаблоны);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"ГруппаСозданыШаблоны",
		"Видимость",
		Шаблоны.Количество());
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыФункции

&НаСервере
Процедура НастроитьПараметрыПодключения()
	
	СистемаБыстрыхПлатежейСлужебный.НастроитьПараметрыПодключения(
		ЭтотОбъект,
		ВариантНастройки,
		ИдентификаторУчастника,
		ДополнительныеПараметры);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииНастройкиОплатыНаСервере()
	
	СистемаБыстрыхПлатежейСлужебный.НастроитьЭлементыФормыПодключения(
		ЭтотОбъект,
		ВариантНастройки,
		ИдентификаторУчастника);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиШаблоновЗавершение(
		Результат,
		ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Число") И Результат > 0 Тогда
		ПредставлениеСсылки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Настроить (%1)';
				|en = 'Set up (%1)'"),
			Результат);
	Иначе
		ПредставлениеСсылки = НСтр("ru = 'Настроить';
									|en = 'Set up'");
	КонецЕсли;
	
	Элементы.ДекорацияНастройкиШаблонов.Заголовок = СтроковыеФункцииКлиент.ФорматированнаяСтрока(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Шаблоны назначения: <a href = ""open:paymentPurpose"">%1</a>';
				|en = 'Assignment templates: <a href = ""open:paymentPurpose"">%1</a>'"),
			ПредставлениеСсылки))
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияФормыКассовыхСсылок(РезультатВыполнения, Параметры) Экспорт
	
	ЗаполнитьДекорацияКассовыеСсылки();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДекорацияКассовыеСсылки()
	
	СистемаБыстрыхПлатежейСлужебный.ЗаполнитьДекорацияКассовыеСсылки(
		ЭтотОбъект,
		НастройкаПодключения);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтборыУчастников()
	
	Если ОтборУчастников = "ПлатежныеАгрегаторы" Тогда
		КлючиПоиска = Новый Структура;
		КлючиПоиска.Вставить("ПлатежныйАгрегатор", Истина);
		ПлатежныеАгрегаторы.ЗагрузитьЗначения(
			СистемаБыстрыхПлатежейСлужебный.УчастникиСБППоНастройкам(КлючиПоиска));
	КонецЕсли;
	Если ОтборУчастников = "КассовыеСсылки" Тогда
		КлючиПоиска = Новый Структура;
		КлючиПоиска.Вставить("КассовыеСсылкиc2b", Истина);
		ПоддержкаКассовыхСсылок.ЗагрузитьЗначения(
			СистемаБыстрыхПлатежейСлужебный.УчастникиСБППоНастройкам(КлючиПоиска));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
