#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИсходныйОбъект = Параметры.Наименование;
	ИсходныйОбъектID = Параметры.ID;
	ИсходныйОбъектТип = Параметры.Тип;
	ИсходныйОбъектНавигационнаяСсылка = Параметры.НавигационнаяСсылка;
	
	ЗагрузитьНастройкиСвязей();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.Назад.Видимость = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсходныйОбъектПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияС1СДокументооборот3Клиент.ОткрытьОбъектДО(ИсходныйОбъектНавигационнаяСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура СвязанныйОбъектПредставлениеОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(СвязанныйОбъектПредставление) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СвязанныйОбъектСтрока) Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(СвязанныйОбъектСтрока);
	Иначе
		ИнтеграцияС1СДокументооборот3Клиент.ОткрытьОбъектДО(СвязанныйОбъектНавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоВариантов

&НаКлиенте
Процедура ДеревоВариантовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьОбъект();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТипыСвязейТаблица

&НаКлиенте
Процедура ТипыСвязейТаблицаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДобавитьСвязь();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборОбъекта Тогда
		ВыбратьОбъект();
	Иначе
		ДобавитьСвязь();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	ОчиститьСвязанныйОбъект();
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаВыборОбъекта;
	Элементы.Назад.Видимость = Ложь;
	Элементы.Готово.Заголовок = НСтр("ru = 'Далее >';
									|en = 'Next >'");
	ИзменитьПодсказкуКомандыГотово(НСтр("ru = 'Далее';
										|en = 'Next'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ВыбратьОбъект()
	
	ДеревоВариантовТекущиеДанные = Элементы.ДеревоВариантов.ТекущиеДанные;
	Если ДеревоВариантовТекущиеДанные = Неопределено Или ДеревоВариантовТекущиеДанные.ЭтоЗаголовок Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВыбратьОбъектЗавершение", ЭтотОбъект, Параметры);
	
	Если ДеревоВариантовТекущиеДанные.Тип = "ВнешняяСсылка" Тогда
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот3.Форма.ФормаВнешнегоРесурса",,
			ЭтотОбъект,,,,
			Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		ИнтеграцияС1СДокументооборот3Клиент.ВыбратьЗначениеПоТипуОбъекта(Оповещение, ДеревоВариантовТекущиеДанные.Тип);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОбъектЗавершение(Результат, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("РеквизитID")
			И Результат.Свойство("РеквизитТип")
			И Результат.РеквизитID = ИсходныйОбъектID
			И Результат.РеквизитТип = ИсходныйОбъектТип Тогда
		ПоказатьПредупреждение(,
			НСтр("ru = 'Нельзя установить связь объекта с самим собой. Выберите другой объект для создания связи.';
				|en = 'Cannot link an object to itself. Select another object to create a link.'"));
		Возврат;
	КонецЕсли;
	
	Если Результат.Свойство("ВнешнийРесурс") Тогда
		СвязанныйОбъектСтрока = Результат.ВнешнийРесурс;
		СвязанныйОбъектПредставление = СтрШаблон(Нстр("ru = '%1 (Строка)';
														|en = '%1 (String)'"), СвязанныйОбъектСтрока);
		Комментарий = Результат.Комментарий;
	Иначе
		ТипОбъекта = ПредставлениеТипаОбъекта(Результат.РеквизитТип);
		Если Результат.РеквизитТип = "DMDocument" Тогда
			СвязанныйОбъектВидДокументаID = Результат.РеквизитВидДокументаID;
		КонецЕсли;
		Если ЗначениеЗаполнено(ТипОбъекта) Тогда
			СвязанныйОбъект = Результат.РеквизитПредставление;
			СвязанныйОбъектID = Результат.РеквизитID;
			СвязанныйОбъектТип = Результат.РеквизитТип;
			СвязанныйОбъектПредставление =
				СтрШаблон(НСтр("ru = '%1 (%2)';
								|en = '%1 (%2)'"), Результат.РеквизитПредставление, ТипОбъекта);
			СвязанныйОбъектНавигационнаяСсылка = Результат.РеквизитНавигационнаяСсылка;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьТипыСвязей();
	
	Если ЗначениеЗаполнено(ТекстОшибки) Тогда
		ПоказатьПредупреждение(, ТекстОшибки);
		ТекстОшибки = "";
		ОчиститьСвязанныйОбъект();
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаСозданиеСвязи;
	Элементы.Назад.Видимость = Истина;
	Элементы.Готово.Заголовок = НСтр("ru = 'Создать связь';
									|en = 'Create a link'");
	ИзменитьПодсказкуКомандыГотово(НСтр("ru = 'Связать выбранные объекты';
										|en = 'Link selected objects'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСвязь()
	
	ТипыСвязейТаблицаТекущиеДанные = Элементы.ТипыСвязейТаблица.ТекущиеДанные;
	Если ТипыСвязейТаблицаТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ИсходныйОбъектID,
		ИсходныйОбъектТип);
	
	СвязанныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		СвязанныйОбъектID,
		СвязанныйОбъектТип);
	Если ЗначениеЗаполнено(СвязанныйОбъектСтрока) Тогда
		СвязанныйОбъектДО.Представление = СвязанныйОбъектСтрока;
	КонецЕсли;
	
	ТипСвязи = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ТипыСвязейТаблицаТекущиеДанные.ID,
		"DMRelationType");
	
	ИнтеграцияС1СДокументооборот3ВызовСервера.ДобавитьСвязьОбъектовДО(
		ИсходныйОбъектДО,
		СвязанныйОбъектДО,
		ТипСвязи,
		Комментарий);
	
	Оповестить("ДобавлениеСвязиОбъектовДО", ИсходныйОбъектДО);
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСвязанныйОбъект()
	
	СвязанныйОбъект = "";
	СвязанныйОбъектID = "";
	СвязанныйОбъектТип = "";
	СвязанныйОбъектПредставление = "";
	СвязанныйОбъектВидДокументаID = "";
	СвязанныйОбъектНавигационнаяСсылка = "";
	СвязанныйОбъектСтрока = "";
	
КонецПроцедуры

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ЗагрузитьНастройкиСвязей()
	
	СсылкаИз = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ИсходныйОбъектID,
		ИсходныйОбъектТип);
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьПрокси();
	НастройкиСвязейОбъектаДО = ИнтеграцияС1СДокументооборот3.НастройкиСвязей(Прокси, СсылкаИз);
	
	ВозможныеТипы = Новый СписокЗначений;
	ДобавитьВнешнююСсылку = Ложь;
	
	Для Каждого НастройкаСвязи Из НастройкиСвязейОбъектаДО.relationsSettings Цикл
		ТипОбъекта = НастройкаСвязи.relationTo.objectID.type;
		ПредставлениеТипа = ПредставлениеТипаОбъекта(ТипОбъекта);
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("СсылкаНаID", НастройкаСвязи.relationTo.objectID.ID);
		ПараметрыОтбора.Вставить("СсылкаНаТип", ТипОбъекта);
		ПараметрыОтбора.Вставить("ТипСвязиID", НастройкаСвязи.relationType.objectID.ID);
		Если НастройкиСвязей.НайтиСтроки(ПараметрыОтбора).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаНастройкаСвязи = НастройкиСвязей.Добавить();
		СтрокаНастройкаСвязи.СсылкаНаID = НастройкаСвязи.relationTo.objectID.ID;
		СтрокаНастройкаСвязи.СсылкаНаТип = ТипОбъекта;
		СтрокаНастройкаСвязи.СсылкаНаПредставление = НастройкаСвязи.relationTo.objectID.presentation;
		СтрокаНастройкаСвязи.Комментарий = НастройкаСвязи.relationType.comment;
		СтрокаНастройкаСвязи.Важная = НастройкаСвязи.important;
		СтрокаНастройкаСвязи.ТипСвязиID = НастройкаСвязи.relationType.objectID.ID;
		СтрокаНастройкаСвязи.ТипСвязиПредставление = НастройкаСвязи.relationType.name;
		
		Если ЗначениеЗаполнено(ПредставлениеТипа) Тогда
			Если ВозможныеТипы.НайтиПоЗначению(ТипОбъекта) = Неопределено Тогда
				ВозможныеТипы.Добавить(ТипОбъекта, ПредставлениеТипа);
			КонецЕсли;
		Иначе
			ДобавитьВнешнююСсылку = Истина;
		КонецЕсли;
	КонецЦикла;
	
	ВозможныеТипы.СортироватьПоПредставлению();
	
	Если ДобавитьВнешнююСсылку Тогда
		ВозможныеТипы.Добавить("ВнешняяСсылка", НСтр("ru = 'Внешняя ссылка';
													|en = 'External reference'"));
	КонецЕсли;
	
	Дерево = РеквизитФормыВЗначение("ДеревоВариантов");
	
	Дерево.Строки.Очистить();
	
	СтрокаЗаголовка = Дерево.Строки.Добавить();
	СтрокаЗаголовка.ЭтоЗаголовок = Истина;
	СтрокаЗаголовка.Наименование = СтрШаблон(НСтр("ru = 'Возможные (%1)';
													|en = 'Possible (%1)'"), ВозможныеТипы.Количество());
	
	Для Каждого Строка Из ВозможныеТипы Цикл
		СтрокаПредмета = СтрокаЗаголовка.Строки.Добавить();
		СтрокаПредмета.Наименование = Строка.Представление;
		СтрокаПредмета.Тип = Строка.Значение;
	КонецЦикла;
	
	ЗначениеВДанныеФормы(Дерево, ДеревоВариантов);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТипыСвязей()
	
	ТипыСвязейТаблица.Очистить();
	
	СсылкаИз = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ИсходныйОбъектID,
		ИсходныйОбъектТип);
	СсылкаНа = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		СвязанныйОбъектID,
		СвязанныйОбъектТип);
	Если ЗначениеЗаполнено(СвязанныйОбъектСтрока) Тогда
		СсылкаНа.Представление = СвязанныйОбъектСтрока;
	КонецЕсли;
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьПрокси();
	НастройкиСвязейОбъектаДО = ИнтеграцияС1СДокументооборот3.НастройкиСвязей(Прокси, СсылкаИз, СсылкаНа);
	
	Для Каждого НастройкаСвязи Из НастройкиСвязейОбъектаДО.relationsSettings Цикл
		НоваяСтрока = ТипыСвязейТаблица.Добавить();
		НоваяСтрока.ID = НастройкаСвязи.relationType.objectID.ID;
		НоваяСтрока.Наименование = НастройкаСвязи.relationType.name;
		НоваяСтрока.Важная = НастройкаСвязи.important;
		НоваяСтрока.Комментарий = НастройкаСвязи.relationType.comment;
		НоваяСтрока.Представление = НастройкаСвязи.relationType.name;
		Если ЗначениеЗаполнено(НоваяСтрока.Комментарий) Тогда
			НоваяСтрока.Представление =
				СтрШаблон(НСтр("ru = '%1 (%2)';
								|en = '%1 (%2)'"), НоваяСтрока.Представление, НоваяСтрока.Комментарий);
		КонецЕсли;
	КонецЦикла;
	
	ТипыСвязейТаблица.Сортировать("Представление");
	
	Если ТипыСвязейТаблица.Количество() = 0 Тогда
		ТекстОшибки =
			НСтр("ru = 'Нельзя установить связь, так как для выбранных документов не настроено ни одного типа связи';
				|en = 'Cannot set the link as no link type is configured for the selected documents'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьПодсказкуКомандыГотово(Знач Подсказка)
	
	Команды.Готово.Подсказка = Подсказка;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеТипаОбъекта(ТипОбъекта)
	
	ПредставлениеТипа = "";
	Если ТипОбъекта = "DMDocumentType" Или ТипОбъекта = "DMDocument" Тогда
		ПредставлениеТипа = НСтр("ru = 'Документ';
								|en = 'Document'");
		ТипОбъекта = "DMDocument";
		
	ИначеЕсли ТипОбъекта = "DMIncomingEMail" Тогда
		ПредставлениеТипа = НСтр("ru = 'Входящее письмо';
								|en = 'Incoming email'");
		
	ИначеЕсли ТипОбъекта = "DMOutgoingEMail" Тогда
		ПредставлениеТипа = НСтр("ru = 'Исходящее письмо';
								|en = 'Outgoing email'");
		
	ИначеЕсли ТипОбъекта = "DMProject" Тогда
		ПредставлениеТипа = НСтр("ru = 'Проект';
								|en = 'Project'");

	ИначеЕсли ТипОбъекта = "DMMeeting" Тогда
		ПредставлениеТипа = НСтр("ru = 'Мероприятие';
								|en = 'Activity'");
		
	ИначеЕсли ТипОбъекта = "DMFile" Тогда
		ПредставлениеТипа = НСтр("ru = 'Файл';
								|en = 'File'");
		
	КонецЕсли;
	
	Возврат ПредставлениеТипа;
	
КонецФункции

#КонецОбласти

#КонецОбласти