#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДекорацияОписание.Видимость = ЗначениеЗаполнено(Параметры.ДекорацияОписание);
	Элементы.ДекорацияОписание.Заголовок = Параметры.ДекорацияОписание;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВнешнийРесурсОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если ЗначениеЗаполнено(Элемент.ТекстРедактирования) Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(Элемент.ТекстРедактирования);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Введите адрес внешней ссылки.';
										|en = 'Enter the external link address.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВнешнийРесурсПриИзменении(Элемент)
	
	Если СтрНачинаетсяС(ВнешнийРесурс, "e1cib") Тогда
		ТекстПредупреждения = 
			НСтр("ru = 'Для объектов в других информационных базах следует использовать внешние ссылки.';
				|en = 'Use external links for objects in other infobases.'");
	Иначе
		ТекстПредупреждения = "";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Если Не ЗначениеЗаполнено(ВнешнийРесурс) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите адрес внешней ссылки.';
										|en = 'Enter the external link address.'"));
		Возврат;
	КонецЕсли;
	
	Если СтрДлина(ВнешнийРесурс) > 250 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Длина внешней ссылки не может превышать 250 символов.
			|Используйте сервисы сокращения ссылок.';
			|en = 'The length of the external link cannot exceed 250 characters.
			|Use link shortening services.'"));
		Возврат;
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВнешнийРесурс", ВнешнийРесурс);
	Результат.Вставить("Комментарий", Комментарий);
	
	Закрыть(Результат);

КонецПроцедуры

#КонецОбласти