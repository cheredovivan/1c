#Область ОписаниеПеременных

&НаКлиенте
Перем СтрокиКРаскрытию;

&НаКлиенте
Перем ТекущийИдентификаторСтроки;

&НаКлиенте
Перем ТекущийТипСтроки;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ВладелецФайла", Владелец);
	Параметры.Свойство("ВладелецФайлаID", ID);
	Параметры.Свойство("ВладелецФайлаТип", Тип);
	Параметры.Свойство("ПредставлениеВладельца", ПредставлениеВладельца);
	Параметры.Свойство("ТекущийФайл", ТекущийФайл);
	
	ЕстьВладелец = ЗначениеЗаполнено(Владелец);
	
	// Покажем владельца при открытии из карточки файла.
	Элементы.ВладелецФайла.Видимость = ЗначениеЗаполнено(ТекущийФайл) И ЕстьВладелец;
	
	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		Заголовок = Параметры.ЗаголовокФормы;
		ЗаголовокУстановлен = Истина;
	ИначеЕсли ЕстьВладелец Тогда
		Заголовок = СтрШаблон("%1: %2", Заголовок, Владелец);
	КонецЕсли;
	
	ПравилоИнтеграции = Неопределено;
	Если Параметры.Свойство("ПравилоИнтеграции") И Параметры.ПравилоИнтеграции <> Неопределено Тогда
		ПравилоИнтеграции = Параметры.ПравилоИнтеграции;
	ИначеЕсли ЕстьВладелец Тогда
		Правила = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.НайтиСоздатьПодходящиеПравила(
			Владелец);
		Если Правила.Количество() > 0 Тогда
			ПравилоИнтеграции = Правила[0];
		КонецЕсли;
	КонецЕсли;
	
	Если ПравилоИнтеграции <> Неопределено Тогда
		Тип = ПравилоИнтеграции.ТипОбъектаДО;
		ТипВидаДокумента = ПравилоИнтеграции.ТипВидаДокумента;
		ИдентификаторВидаДокумента = ПравилоИнтеграции.ИдентификаторВидаДокумента;
	КонецЕсли;
	
	Если Не ЕстьВладелец И ПравилоИнтеграции = Неопределено И ID = "" И Тип = "" Тогда
		Доступность = Ложь;
	КонецЕсли;
	
	ИспользоватьЭлектронныеПодписиИС =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ИспользоватьЭлектронныеЦифровыеПодписи();
	
	Если Параметры.Свойство("ПростаяФорма") Тогда
		Элементы.ОткрытьДляРедактирования.ТолькоВоВсехДействиях = Истина;
		Элементы.ОткрытьНаЧтение.ТолькоВоВсехДействиях = Истина;
		Элементы.ЗакончитьРедактирование.ТолькоВоВсехДействиях = Истина;
	КонецЕсли;
	
	ТипСправочникаСФайлами = "Неопределено";
	ПредставлениеТипаВладельца = "объекта";
	Если ЕстьВладелец Тогда
		ВладелецМетаданные = Владелец.Метаданные();
		ТипСправочникаСФайлами = ВладелецМетаданные.ПолноеИмя();
		ТипОбъектаМассив = СтрРазделить(ТипСправочникаСФайлами, ".");
		Если ТипОбъектаМассив[0] = "Справочник" Тогда
			ПредставлениеТипаВладельца = "справочника";
		ИначеЕсли ТипОбъектаМассив[0] = "Документ" Тогда
			ПредставлениеТипаВладельца = "документа";
		КонецЕсли;
	КонецЕсли;
	
	СокращенноеНаименованиеКонфигурации =
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.СокращенноеНаименованиеКонфигурации();
	
	ДоступнаОтправкаЧерезИС = ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСПочтовымиСообщениями");
	
	ПредпросмотрХранилище = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		ТипСправочникаСФайлами,
		"ПредпросмотрФайловБИД");
	Если ПредпросмотрХранилище = Неопределено Тогда
		Предпросмотр = Истина;
	Иначе
		Предпросмотр = ПредпросмотрХранилище;
	КонецЕсли;
	
	Элементы.ПереключитьПредпросмотр.Пометка = Предпросмотр;
	Элементы.ГруппаСтраницыПревью.Видимость = Предпросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ОбъектДОИзДанныхФормы(ВладелецФормы);
	Если ЗначениеЗаполнено(ОбъектДО.ID) И ЗначениеЗаполнено(ОбъектДО.Тип) Тогда
		ID = ОбъектДО.ID;
		Тип = ОбъектДО.Тип;
	КонецЕсли;
	
	Если Не ЗаголовокУстановлен Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.УстановитьЗаголовокПриОткрытии(ЭтотОбъект);
	КонецЕсли;
	
	ПроверитьПодключение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	УдалитьФайлыПредпросмотраPDF();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Не Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ID) Тогда
		ЗаполнитьIDПоВладельцу();
	КонецЕсли;
	
	Если ИмяСобытия = "ИнтеграцияС1СДокументооборотом_УспешноеПодключение" Тогда
		Если Источник <> ЭтотОбъект Тогда
			ПриПодключении();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПолученHTMLПредпросмотрОбъекта" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ПредставлениеHTML")
				Или Не Параметр.Свойство("ПредпросмотрУрезан")
				Или Не Параметр.Свойство("ДвоичныеДанные")
				Или Не Параметр.Свойство("ID")
				Или Не Параметр.Свойство("Тип")
				Или Не Параметр.Свойство("Расширение") Тогда
			Возврат;
		КонецЕсли;
		Строка = СтрокаПоID(Параметр.ID, Параметр.Тип);
		Если Строка <> Неопределено Тогда
			Строка.ОжиданиеПредпросмотра = Ложь;
			Если ТипЗнч(Параметр.ДвоичныеДанные) = Тип("ДвоичныеДанные") И НРег(Параметр.Расширение) = "pdf" Тогда
				Строка.ПредставлениеHTML = "";
				Строка.ПредпросмотрУрезан = Ложь;
				Строка.ПолноеИмяФайлаПредпросмотра =
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.СохранитьФайлНаСервере(
						Параметр.ДвоичныеДанные,
						Параметр.Расширение);
				ВременныеФайлыПредпросмотра.Добавить(Строка.ПолноеИмяФайлаПредпросмотра);
			Иначе
				Строка.ПредставлениеHTML = Параметр.ПредставлениеHTML;
				Строка.ПредпросмотрУрезан = Параметр.ПредпросмотрУрезан;
			КонецЕсли;
			ОбновитьПредпросмотр();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотФайл" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ДействиеНадФайлом")
				Или Не Параметр.Свойство("ИдентификаторФайла")
				Или Не Параметр.Свойство("ВладелецФайла")
				Или Не Параметр.Свойство("УникальныйИдентификаторФормы")
				Или Не Параметр.ДействиеНадФайлом.ИнформироватьФормуСпискаФайлов
				Или ID = ""
				Или Параметр.ВладелецФайла <> ID
				Или (Параметр.ДействиеНадФайлом.УдалениеФайла
					И УникальныйИдентификатор = Параметр.УникальныйИдентификаторФормы) Тогда
			Возврат;
		КонецЕсли;
		НужноОбновление = Истина;
		Если Параметр.ДействиеНадФайлом.РедактированиеФайла Тогда
			Строка = СтрокаПоID(Параметр.ИдентификаторФайла, "DMFile");
			Если Строка = Неопределено Тогда
				НужноОбновление = Ложь;
			Иначе
				НужноОбновление = Не Строка.Редактируется;
			КонецЕсли;
		КонецЕсли;
		Если НужноОбновление Тогда
			Если Параметр.УникальныйИдентификаторФормы = Неопределено Тогда
				ВыводитьОкноОжидания = (Источник = ЭтотОбъект);
			Иначе
				ВыводитьОкноОжидания = (УникальныйИдентификатор = Параметр.УникальныйИдентификаторФормы);
			КонецЕсли;
			ОбновитьСписокФайловКлиент(Параметр.ИдентификаторФайла, ВыводитьОкноОжидания);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Документооборот_ДобавлениеСвязи" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ID")
				Или Не Параметр.Свойство("Тип")
				Или Не Параметр.Свойство("Объект")
				Или Не Параметр.Свойство("УникальныйИдентификаторФормы")
				Или Параметр.ID <> ID
				Или Параметр.Тип <> Тип
				Или УникальныйИдентификатор = Параметр.УникальныйИдентификаторФормы Тогда
			Возврат;
		КонецЕсли;
		ПараметрыОбновления = ПараметрыОбновленияФайлов();
		ПараметрыОбновления.ПриОткрытии = Истина;
		ПараметрыОбновления.ЗадаватьВопросОПереносеФайлов = Ложь;
		НачатьОбновлениеФайлов(ПараметрыОбновления);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ИмяФормыПараметров = "Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
	
	ОткрытьФорму(ИмяФормыПараметров,, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ДанныеСобытия.Href, "OpenForEdit") Тогда
		
		ОткрытьТекущийФайлДляРедактирования();
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "OpenForView") Тогда
		
		ОткрытьТекущийФайлНаЧтение();
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "CreatePreview") Тогда
		
		ОбновитьПредпросмотр(Истина, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоФайлов

&НаКлиенте
Процедура ДеревоФайловВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьТекущийФайлНаЧтение();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ОткрытьКарточку();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
			Возврат;
		КонецЕсли;
		ДобавитьКопированием(Элементы.ДеревоФайлов.ТекущиеДанные.ID);
	Иначе
		ДобавитьСДиска(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеУдаленияФайлов", ЭтотОбъект);
	
	ВыделенныеФайлы = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыделенныеФайлы(
		ДеревоФайлов,
		Элементы.ДеревоФайлов.ВыделенныеСтроки);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПометитьНаУдалениеСнятьПометкуФайлов(
		ВыделенныеФайлы,
		ТекущиеДанные.ПометкаУдаления,
		Оповещение,
		ID,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПриАктивизацииСтроки(Элемент)
	
	УстановитьДоступностьКомандСпискаФайлов();
	ПодключитьОбработчикОжидания("ОбновитьПревьюФайла", 0.2, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Отмена;
	
	МассивФайлов = МассивФайловИзПараметровПеретаскивания(ПараметрыПеретаскивания.Значение);
	Если МассивФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРолиФайлов Тогда
		НоваяРольФайлаID = ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(ДеревоФайлов, Строка);
	Иначе
		НоваяРольФайлаID = "";
	КонецЕсли;
	
	Для Каждого ПеретаскиваемыйФайл Из МассивФайлов Цикл
		Если ТипЗнч(ПеретаскиваемыйФайл) = Тип("Структура")
				И (НоваяРольФайлаID = "" Или ПеретаскиваемыйФайл.РольФайлаID = НоваяРольФайлаID) Тогда
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыПеретаскивания.Действие = ДействиеПеретаскивания.Перемещение;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоФайловПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	Если ИспользоватьРолиФайлов Тогда
		РольФайлаID = ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(
			ДеревоФайлов, Строка);
	Иначе
		РольФайлаID = "";
	КонецЕсли;
	
	МассивФайлов = МассивФайловИзПараметровПеретаскивания(ПараметрыПеретаскивания.Значение);
	Если МассивФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(МассивФайлов[0]) = Тип("Структура") Тогда
		ИнтеграцияС1СДокументооборот3Клиент.ЗаменитьРольФайлов(
			МассивФайлов,
			РольФайлаID,
			ID,
			УникальныйИдентификатор);
	ИначеЕсли ТипЗнч(МассивФайлов[0]) = Тип("Файл") Тогда
		ПараметрыОповещения = Новый Структура("МассивФайлов, РольФайлаID", МассивФайлов, РольФайлаID);
		ОписаниеОповещения = Новый ОписаниеОповещения("ФайлыПеретаскиваниеЗавершение", ЭтотОбъект, ПараметрыОповещения);
		Если ЗначениеЗаполнено(ID) Тогда
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
		Иначе
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьПоискСвязанногоОбъектаДО(
				Владелец,
				ОписаниеОповещения,,,
				Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ФайлыПеретаскиваниеЗавершение(Знач Результат, Знач ПараметрыОповещения) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ID = Результат.ID;
		Тип = Результат.type;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ID) Тогда
		Возврат;
	КонецЕсли;
	
	НеобходимоДобавитьСканКопию =
		Ждать ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НеобходимоДобавитьСканКопиюАсинх(
			СостояниеРазрешаетДобавлениеСканКопии,
			СостояниеРазрешаетДобавлениеФайла);
	
	Если НеобходимоДобавитьСканКопию = КодВозвратаДиалога.Да Тогда
		ПараметрыНачалаСоздания = ПараметрыНачалаСозданияФайла();
		ПараметрыНачалаСоздания.ТекущийФайл.РольФайлаID = ПараметрыОповещения.РольФайлаID;
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.СоздатьФайлСДискаПеретаскиванием(
			ПараметрыОповещения.МассивФайлов,
			ПараметрыНачалаСоздания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДокументPDFУвеличить(Команда)
	
	Элементы["ДокументPDFПоле"].Масштаб = Элементы["ДокументPDFПоле"].Масштаб + 10;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFУменьшить(Команда)
	
	Если Элементы["ДокументPDFПоле"].Масштаб <= 10 Тогда
		Возврат;
	КонецЕсли;
	
	Элементы["ДокументPDFПоле"].Масштаб = Элементы["ДокументPDFПоле"].Масштаб - 10;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFПоворотЛево(Команда)
	
	Элементы["ДокументPDFПоле"].Ориентация = Элементы["ДокументPDFПоле"].Ориентация - 90;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFПоворотПраво(Команда)
	
	Элементы["ДокументPDFПоле"].Ориентация = Элементы["ДокументPDFПоле"].Ориентация + 90;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьРольФайла(Команда)
	
	Если Не ИспользоватьРолиФайлов Или ДоступныеРоли.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СписокФайлов = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыделенныеФайлы(
		ДеревоФайлов,
		Элементы.ДеревоФайлов.ВыделенныеСтроки);
	
	Если СписокФайлов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ИдентификаторФормыВладельца", УникальныйИдентификатор);
	ПараметрыФормы.Вставить("ВладелецФайлов", ID);
	ПараметрыФормы.Вставить("ДоступныеРоли", ДоступныеРоли);
	ПараметрыФормы.Вставить("СписокФайлов", СписокФайлов);
	ПараметрыФормы.Вставить("ВыбраннаяРоль",
		ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(
			ДеревоФайлов,
			Элементы.ДеревоФайлов.ТекущаяСтрока));
	
	ФормаДобавления = ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборот3.Форма.ВыборРолейДляФайла",
		ПараметрыФормы,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСДиска(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСДискаЗавершение", ЭтотОбъект);
	Если ЗначениеЗаполнено(ID) Тогда // Связанный объект уже известен.
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
	Иначе // Связанный объект следует найти или создать.
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьПоискСвязанногоОбъектаДО(
			Владелец,
			ОписаниеОповещения,,,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПечатнуюФорму(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьПечатнуюФормуЗавершение", ЭтотОбъект);
	Если ЗначениеЗаполнено(ID) Тогда // Связанный объект уже известен.
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
	Иначе // Связанный объект следует найти или создать.
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьПоискСвязанногоОбъектаДО(
			Владелец,
			ОписаниеОповещения,,,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиФайлыИСвДОКоманда(Команда)
	
	ПеренестиФайлыИСвДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНаЧтение(Команда)
	
	ОткрытьТекущийФайлНаЧтение();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДляРедактирования(Команда)
	
	ОткрытьТекущийФайлДляРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ЗакончитьРедактированиеФайла(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,,
		ID);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаДиск(Команда)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьСохранениеВыделенныхФайлов(
		ДеревоФайлов,
		Элементы.ДеревоФайлов.ВыделенныеСтроки,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзФайлаНаДиске(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОбновитьИзФайлаНаДиске(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		ТекущиеДанные.ДатаМодификации,
		УникальныйИдентификатор,,
		ID);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредпросмотрКоманда(Команда)
	
	ОбновитьПредпросмотр(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьРедактирование(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОтменитьРедактированиеФайла(
		ТекущиеДанные.ID,,
		ID,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьКаталогФайла(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьКаталогФайла(ТекущиеДанные.ID);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИзменения(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.СохранитьИзмененияРедактируемогоФайла(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,,
		ID);
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	Если Не ЗначениеЗаполнено(ID) Тогда
		ЗаполнитьIDПоВладельцу();
	КонецЕсли;
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьУдаленные(Команда)
	
	ПоказыватьУдаленные = Не ПоказыватьУдаленные;
	Элементы.ПоказыватьУдаленные.Пометка = ПоказыватьУдаленные;
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьПредпросмотр(Команда)
	
	Предпросмотр = Не Предпросмотр;
	Элементы.ПереключитьПредпросмотр.Пометка = Предпросмотр;
	Элементы.ГруппаСтраницыПревью.Видимость = Предпросмотр;
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
		ТипСправочникаСФайлами,
		"ПредпросмотрФайловБИД",
		Предпросмотр);
	ОбновитьПредпросмотр();
	
КонецПроцедуры

&НаКлиенте
Процедура Печать(Команда)
	
#Если ВебКлиент Тогда
	ПоказатьПредупреждение(,НСтр("ru = 'В Веб-клиенте печать файлов не поддерживается.';
								|en = 'Printing of files is not supported in the web client.'"));
	Возврат;
#КонецЕсли
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НапечататьФайлы(
		ДеревоФайлов,
		Элементы.ДеревоФайлов.ВыделенныеСтроки,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Отправить(Команда)
	
	ОтправитьЧерезИС();
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////////////////////////
// Файлы (ЭП)

&НаКлиенте
Процедура ПодписатьФайл(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПодписей = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ТаблицаПодписей(
		Подписи,,
		ТекущиеДанные.ID);
	
	СвойстваФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвойстваФайла();
	СвойстваФайла.ИдентификаторФайла = ТекущиеДанные.ID;
	СвойстваФайла.ИмяФайла = ТекущиеДанные.Наименование;
	СвойстваФайла.ВладелецФайла = ID;
	СвойстваФайла.Редактируется = ТекущиеДанные.Редактируется;
	СвойстваФайла.Зашифрован = ТекущиеДанные.Зашифрован;
	СвойстваФайла.ОписаниеФайла = ТекущиеДанные.Описание;
	СвойстваФайла.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	СвойстваФайла.ДанныеПодписейФайла =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ДанныеПодписей(ТаблицаПодписей);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПодписатьФайл(СвойстваФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьЭПИзФайла(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторФайла = ТекущиеДанные.ID;
	ТаблицаПодписей = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ТаблицаПодписей(
		Подписи,,
		ИдентификаторФайла);
	ДанныеПодписейФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ДанныеПодписей(ТаблицаПодписей);
	
	СвойстваФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвойстваФайла();
	СвойстваФайла.ИдентификаторФайла = ИдентификаторФайла;
	СвойстваФайла.ИмяФайла = ТекущиеДанные.Наименование;
	СвойстваФайла.ОписаниеФайла = ТекущиеДанные.Описание;
	СвойстваФайла.Редактируется = ТекущиеДанные.Редактируется;
	СвойстваФайла.Зашифрован = ТекущиеДанные.Зашифрован;
	СвойстваФайла.ДанныеПодписейФайла = ДанныеПодписейФайла;
	СвойстваФайла.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	СвойстваФайла.ВладелецФайла = ID;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьДобавлениеЭПИзФайла(СвойстваФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьФайлВместеСЭП(Команда)
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьСохранениеВместеСЭП(
		ТекущиеДанные.ID,
		ТекущиеДанные.Расширение,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Размер * 1024,
		ТекущиеДанные.ДатаМодификации,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ДобавитьКопированием(ОригиналID)
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.СоздатьФайлКопированием(ОригиналID);
	
КонецПроцедуры

// Конструктор структуры параметров для начала создания файла.
//
// Возвращаемое значение:
//   см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПараметрыНачалаСозданияФайла
//
&НаКлиенте
Функция ПараметрыНачалаСозданияФайла()
	
	ПараметрыНачалаСоздания =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПараметрыНачалаСозданияФайла();
	ПараметрыНачалаСоздания.ЯвляетсяСканКопией =
		(СостояниеРазрешаетДобавлениеСканКопии И Не СостояниеРазрешаетДобавлениеФайла);
	ПараметрыНачалаСоздания.ВладелецID = ID;
	ПараметрыНачалаСоздания.ВладелецТип = Тип;
	ПараметрыНачалаСоздания.УникальныйИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыНачалаСоздания.ВладелецПредставление = Строка(Владелец);
	ПараметрыНачалаСоздания.Владелец = Владелец;
	
	Возврат ПараметрыНачалаСоздания;
	
КонецФункции

&НаКлиенте
Функция ПараметрыОбновленияФайлов()
	
	ПараметрыОбновленияФайлов = Новый Структура;
	ПараметрыОбновленияФайлов.Вставить("ПриОткрытии", Ложь);
	ПараметрыОбновленияФайлов.Вставить("ЗадаватьВопросОПереносеФайлов", Ложь);
	ПараметрыОбновленияФайлов.Вставить("ВыводитьОкноОжидания", Ложь);
	ПараметрыОбновленияФайлов.Вставить("СкрыватьИнтерфейс", Истина);
	
	Возврат ПараметрыОбновленияФайлов;
	
КонецФункции

// Проверяет подключение к ДО, выводя окно авторизации, если необходимо, и изменяя форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключение()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
		ОписаниеОповещения,
		ЭтотОбъект,,
		Ложь,
		Истина);
	
КонецПроцедуры

// Вызывается после проверки подключения к ДО и изменяет форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	Иначе // Не удалось подключиться к ДО.
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключении()
	
	Если ОбработатьФормуСогласноВерсииСервиса() Тогда
		ПараметрыОбновления = ПараметрыОбновленияФайлов();
		ПараметрыОбновления.ПриОткрытии = Истина;
		ПараметрыОбновления.ЗадаватьВопросОПереносеФайлов = Истина;
		НачатьОбновлениеФайлов(ПараметрыОбновления);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьОбновлениеФайлов(ПараметрыОбновления)
	
	ОбновлениеУспешноЗавершено = Ложь;
	Элементы.ГруппаДобавить.Доступность = Ложь;
	Элементы.СписокКонтекстноеМенюГруппаКомандДобавить.Доступность = Ложь;
	УбратьДоступностьКоманд();
	ПредставлениеHTML =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
			ЗаголовокСообщенияВОбластиПредпросмотра);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбновлениеФайловЗавершение", ЭтотОбъект, ПараметрыОбновления);
	ТекстСообщения = НСтр("ru = 'Получение списка файлов из 1С:Документооборот.';
							|en = 'Get a file list from 1C:Document Management.'");
	
	ПолучаемыеПоля = Новый Массив;
	
	Если ЗначениеЗаполнено(ID) Тогда
		
		ПолучаемыеПоля.Добавить("files");
		Если Предпросмотр Тогда
			Если ПоказыватьPDFСредствами1С Тогда
				ПолучаемыеПоля.Добавить("binaryData");
			КонецЕсли;
			ПолучаемыеПоля.Добавить("htmlView");
		КонецЕсли;
		Если ИнтеграцияС1СДокументооборот3КлиентПовтИсп.ЭтоДокументДО3(Тип) Тогда
			ПолучаемыеПоля.Добавить("documentType");
			ПолучаемыеПоля.Добавить("enabledProperties");
		КонецЕсли;
		Если ПоказыватьУдаленные Тогда
			ПолучаемыеПоля.Добавить("ignoreDeletionMark");
		КонецЕсли;
		
		ДлительнаяОперация = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьОбъектАсинхронно(
			УникальныйИдентификатор,
			Тип,
			ID,
			ПолучаемыеПоля,,
			?(ЕстьВладелец, Владелец, Неопределено));
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
			ЭтотОбъект,
			ДлительнаяОперация,
			ОповещениеОЗавершении,
			ПараметрыОбновления.ВыводитьОкноОжидания,
			ТекстСообщения,
			ПараметрыОбновления.СкрыватьИнтерфейс);
		
	ИначеЕсли ИнтеграцияС1СДокументооборот3КлиентПовтИсп.ЭтоДокументДО3(Тип)
			И ЗначениеЗаполнено(ТипВидаДокумента) И ЗначениеЗаполнено(ИдентификаторВидаДокумента) Тогда
		
		ПолучаемыеПоля.Добавить("roles");
		
		ДлительнаяОперация = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьОбъектАсинхронно(
			УникальныйИдентификатор,
			ТипВидаДокумента,
			ИдентификаторВидаДокумента,
			ПолучаемыеПоля);
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
			ЭтотОбъект,
			ДлительнаяОперация,
			ОповещениеОЗавершении,
			ПараметрыОбновления.ВыводитьОкноОжидания,
			ТекстСообщения,
			ПараметрыОбновления.СкрыватьИнтерфейс);
		
	Иначе
		
		Результат = Новый Структура;
		Если ЗначениеЗаполнено(Тип) Тогда
			Результат.Вставить("Статус", "Выполнено");
			Результат.Вставить("РезультатДлительнойОперации", Новый Структура("Тип", Тип));
		Иначе
			Если ЗначениеЗаполнено(ПредставлениеВладельца) Тогда
				ТекстОшибки = СтрШаблон(
					НСтр("ru = 'Просмотр списка файлов объекта ""%1"" доступен только на стороне 1С:Документооборот.';
						|en = 'You can only preview the file list of the ""%1"" object in 1C:Document Management.'"),
					ПредставлениеВладельца);
			Иначе
				ТекстОшибки = НСтр("ru = 'Владелец файлов не поддерживается бесшовной интеграцией.
					|Просмотр списка файлов доступен только на стороне 1С:Документооборот.';
					|en = 'Seamless integration does not support the file owner.
					|You can only preview the file list in 1C:Document Management.'");
			КонецЕсли;
			Результат.Вставить("Статус", "Ошибка");
			Результат.Вставить("КраткоеПредставлениеОшибки", ТекстОшибки);
		КонецЕсли;
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеФайловЗавершение(Результат, ПараметрыОбновления) Экспорт
	
	ОбработатьФорму = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		
		ОбновлениеУспешноЗавершено = Истина;
		
		ДеревоФайлов.ПолучитьЭлементы().Очистить();
		
		Элементы.ГруппаДобавить.Доступность = Истина;
		Элементы.СписокКонтекстноеМенюГруппаКомандДобавить.Доступность = Истина;
		
		ПредпросмотрОбъекта = ПрочитатьИОбновитьСписокФайлов(Результат.РезультатДлительнойОперации);
		Если ПредпросмотрОбъекта <> Неопределено Тогда
			Оповестить("ПолученHTMLПредпросмотрОбъекта", ПредпросмотрОбъекта, ЭтотОбъект);
		КонецЕсли;
		
		ОбновитьЭлементыФормы();
		
		Если ПараметрыОбновления.ПриОткрытии Тогда
			СтрокиКРаскрытию = Новый Массив;
			Если ИспользоватьРолиФайлов Тогда
				Для Каждого Строка Из ДеревоФайлов.ПолучитьЭлементы() Цикл
					Если ЗначениеЗаполнено(Строка.ID) И Строка.РольОбязательная Тогда
						СтрокиКРаскрытию.Добавить(Строка.ID);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			ТекущийИдентификаторСтроки = ТекущийФайл;
			ТекущийТипСтроки = "DMFile";
		КонецЕсли;
		
		ВосстановитьПоложениеВСписке();
		
		Если ПараметрыОбновления.ЗадаватьВопросОПереносеФайлов Тогда
			ДанныеСвязи = Новый Структура;
			ДанныеСвязи.Вставить("ID", ID);
			ДанныеСвязи.Вставить("Тип", Тип);
			ДанныеСвязи.Вставить("Объект", Владелец);
			ПараметрыПереносаФайлов =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПараметрыПереносаФайловВДО(
					ДанныеСвязи,
					ПрисоединенныеФайлыВИС,
					ТипСправочникаСФайлами,
					ПредставлениеТипаВладельца);
			Если ПараметрыПереносаФайлов.НужноЗадатьВопросОПереносеФайловВДО Тогда
				Если СостояниеРазрешаетДобавлениеФайла Или СостояниеРазрешаетДобавлениеСканКопии Тогда
					ИнтеграцияС1СДокументооборот3Клиент.ЗадатьВопросОПереносеФайловВДО(
						ПараметрыПереносаФайлов,
						Новый ОписаниеОповещения("ПеренестиФайлыИСвДОЗавершение", ЭтотОбъект),
						УникальныйИдентификатор);
				Иначе
					ТекстВопроса = СтрШаблон(
						НСтр("ru = 'У %1 ""%2"" есть присоединенные файлы, добавленные в %3 до настройки хранения файлов на стороне 1С:Документооборота.
							|Рекомендуется перенести эти файлы в 1С:Документооборот.
							|
							|Показать файлы, хранящиеся на стороне %3?';
							|en = 'The ""%2"" %1 has attachments that were added to %3 before configuring file storage in 1C:Document Management.
							|We recommended that you transfer these files to 1C:Document management.
							|
							|Show files stored in %3?'"),
						ПараметрыПереносаФайлов.ПредставлениеТипаВладельца,
						Строка(ПараметрыПереносаФайлов.ВладелецИС),
						ПараметрыПереносаФайлов.СокращенноеНаименованиеКонфигурации);
					ПараметрыПредупреждения = СтандартныеПодсистемыКлиент.ПараметрыВопросаПользователю();
					ПараметрыПредупреждения.КнопкаПоУмолчанию = КодВозвратаДиалога.Да;
					ПараметрыПредупреждения.Картинка = БиблиотекаКартинок.Информация32;
					ПараметрыПредупреждения.ПредлагатьБольшеНеЗадаватьЭтотВопрос = Ложь;
					ПараметрыПредупреждения.Заголовок = НСтр("ru = 'Архивные файлы';
															|en = 'Archived files'");
					Кнопки = Новый СписокЗначений;
					Кнопки.Добавить(КодВозвратаДиалога.Да);
					Кнопки.Добавить(КодВозвратаДиалога.Нет);
					СтандартныеПодсистемыКлиент.ПоказатьВопросПользователю(
						Новый ОписаниеОповещения("ПоказатьФайлыИСПродолжение", ЭтотОбъект),
						ТекстВопроса,
						Кнопки,
						ПараметрыПредупреждения);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

// Завершает добавление файла с диска и обновляет форму.
//
&НаКлиенте
Асинх Процедура ДобавитьСДискаЗавершение(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ID = Результат.ID;
		Тип = Результат.type;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ID) Тогда
		Возврат;
	КонецЕсли;
	
	НеобходимоДобавитьСканКопию =
		Ждать ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НеобходимоДобавитьСканКопиюАсинх(
			СостояниеРазрешаетДобавлениеСканКопии,
			СостояниеРазрешаетДобавлениеФайла);
	
	Если НеобходимоДобавитьСканКопию = КодВозвратаДиалога.Да Тогда
		ПараметрыНачалаСоздания = ПараметрыНачалаСозданияФайла();
		Если ИспользоватьРолиФайлов Тогда
			ПараметрыНачалаСоздания.ТекущийФайл.РольФайлаID = ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(
				ДеревоФайлов,
				Элементы.ДеревоФайлов.ТекущаяСтрока);
		КонецЕсли;
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.СоздатьФайлСДиска(ПараметрыНачалаСоздания);
	КонецЕсли;
	
КонецПроцедуры

// Продолжает добавление печатной формы владельца.
//
&НаКлиенте
Процедура ДобавитьПечатнуюФормуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ID = Результат.ID;
		Тип = Результат.type;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ID) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРолиФайлов Тогда
		РольФайлаID = ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(
			ДеревоФайлов,
			Элементы.ДеревоФайлов.ТекущаяСтрока);
	Иначе
		РольФайлаID = "";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектИС", Владелец);
	ПараметрыФормы.Вставить("ИдентификаторОбъектаДО", ID);
	ПараметрыФормы.Вставить("ТипОбъектаДО", Тип);
	ПараметрыФормы.Вставить("РольФайлаID", РольФайлаID);
	ФормаДобавления = ОткрытьФорму(
		"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.ДобавлениеПечатнойФормы",
		ПараметрыФормы,
		ЭтотОбъект,,,,,
		РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Если ФормаДобавления = Неопределено Тогда
		ТекстПредупреждения = СтрШаблон(НСтр("ru = '%1 не имеет печатных форм.';
											|en = '%1 has no print forms.'"), Строка(Владелец));
		ПоказатьПредупреждение(,ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиФайлыИСвДО()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПеренестиФайлыИСвДОЗавершение", ЭтотОбъект);
	Если ЗначениеЗаполнено(ID) Тогда // Связанный объект уже известен.
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Неопределено);
	Иначе // Связанный объект следует найти или создать.
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НачатьПоискСвязанногоОбъектаДО(
			Владелец,
			ОписаниеОповещения,,,
			Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Продолжает перенос файлов из интегрированной системы.
//
&НаКлиенте
Процедура ПеренестиФайлыИСвДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ID = Результат.ID;
		Тип = Результат.type;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ID) Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьРолиФайлов Тогда
		РольФайлаID = ИнтеграцияС1СДокументооборот3Клиент.ТекущаяРольФайла(
			ДеревоФайлов,
			Элементы.ДеревоФайлов.ТекущаяСтрока);
	Иначе
		РольФайлаID = "";
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборот3Клиент.ПеренестиФайлыИСвДО(
		ПрисоединенныеФайлыВИС,
		ID,
		Тип,
		Владелец,
		РольФайлаID,
		УникальныйИдентификатор);
	
	ЕстьПрисоединенныеФайлыВИС = (ПрисоединенныеФайлыВИС.Количество() > 0);
	Элементы.ПеренестиФайлыИСвДО.Видимость = ЕстьПрисоединенныеФайлыВИС;
	Элементы.ДеревоФайловКонтекстноеМенюПеренестиФайлыИСвДО.Видимость = ЕстьПрисоединенныеФайлыВИС;
	
КонецПроцедуры

// Вызывается после вопроса пользователю об открытии файлов ИС.
//
&НаКлиенте
Процедура ПоказатьФайлыИСПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Значение = КодВозвратаДиалога.Да Тогда
		РаботаСФайламиКлиент.ОткрытьФормуСпискаФайлов(Владелец);
	КонецЕсли;
	
КонецПроцедуры

// Обновляет список файлов, заново получая его из ДО.
//
&НаКлиенте
Процедура ОбновитьСписокФайловКлиент(ИдентификаторФайла = Неопределено,
		ВыводитьОкноОжидания = Истина, СкрыватьИнтерфейс = Ложь)
	
	ТекущийИдентификаторСтроки = Неопределено;
	ТекущийТипСтроки = Неопределено;
	Если ИдентификаторФайла <> Неопределено Тогда
		ТекущийИдентификаторСтроки = ИдентификаторФайла;
		ТекущийТипСтроки = "DMFile";
	ИначеЕсли Элементы.ДеревоФайлов.ТекущиеДанные <> Неопределено Тогда
		ТекущийИдентификаторСтроки = Элементы.ДеревоФайлов.ТекущиеДанные.ID;
		ТекущийТипСтроки = Элементы.ДеревоФайлов.ТекущиеДанные.Тип;
	КонецЕсли;
	СтрокиКРаскрытию = Новый Массив;
	Если ИспользоватьРолиФайлов Тогда
		Для Каждого Строка Из ДеревоФайлов.ПолучитьЭлементы() Цикл
			Если Элементы.ДеревоФайлов.Развернут(Строка.ПолучитьИдентификатор()) Тогда
				СтрокиКРаскрытию.Добавить(Строка.ID);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ПараметрыОбновления = ПараметрыОбновленияФайлов();
	ПараметрыОбновления.ВыводитьОкноОжидания = ВыводитьОкноОжидания;
	ПараметрыОбновления.СкрыватьИнтерфейс = СкрыватьИнтерфейс;
	НачатьОбновлениеФайлов(ПараметрыОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыФормы()
	
	Элементы.ВыбратьРольФайла.Видимость = ИспользоватьРолиФайлов;
	Элементы.ВыбратьРольФайлаКонтекст.Видимость = ИспользоватьРолиФайлов;
	
	ЕстьПрисоединенныеФайлыВИС = (ПрисоединенныеФайлыВИС.Количество() > 0);
	Элементы.ПеренестиФайлыИСвДО.Видимость = ЕстьПрисоединенныеФайлыВИС;
	Элементы.ДеревоФайловКонтекстноеМенюПеренестиФайлыИСвДО.Видимость = ЕстьПрисоединенныеФайлыВИС;
	
	Если ЕстьПрисоединенныеФайлыВИС
			И ЗначениеЗаполнено(СокращенноеНаименованиеКонфигурации) Тогда
		ЗаголовокКоманды = СтрШаблон(
			НСтр("ru = 'Файлы %1 из %2...';
				|en = 'The %1 files from %2...'"),
			ПредставлениеТипаВладельца,
			СокращенноеНаименованиеКонфигурации);
		Элементы.ПеренестиФайлыИСвДО.Заголовок = ЗаголовокКоманды;
		Элементы.ДеревоФайловКонтекстноеМенюПеренестиФайлыИСвДО.Заголовок = ЗаголовокКоманды;
	КонецЕсли;
	
	Элементы.ДобавитьПечатнуюФорму.Видимость = ЕстьВладелец;
	Элементы.ДеревоФайловКонтекстноеМенюДобавитьПечатнуюФорму.Видимость = ЕстьВладелец;
	Если ЕстьВладелец Тогда
		ЗаголовокКоманды = СтрШаблон(НСтр("ru = 'Печатную форму из %1...';
											|en = 'Print form from %1...'"), ПредставлениеТипаВладельца);
		Элементы.ДобавитьПечатнуюФорму.Заголовок = ЗаголовокКоманды;
		Элементы.ДеревоФайловКонтекстноеМенюДобавитьПечатнуюФорму.Заголовок = ЗаголовокКоманды;
		Элементы.ДобавитьПечатнуюФорму.Доступность = СостояниеРазрешаетДобавлениеФайла;
		Элементы.ДеревоФайловКонтекстноеМенюДобавитьПечатнуюФорму.Доступность = СостояниеРазрешаетДобавлениеФайла;
	КонецЕсли;
	
	Элементы.Отправить.Видимость = ДоступнаОтправкаЧерезИС;
	
	Элементы.ВыбратьРольФайла.Доступность = СостояниеРазрешаетДобавлениеФайла;
	Элементы.ВыбратьРольФайлаКонтекст.Доступность = СостояниеРазрешаетДобавлениеФайла;
	Элементы.ДобавитьСДиска.Доступность = (СостояниеРазрешаетДобавлениеФайла
		Или СостояниеРазрешаетДобавлениеСканКопии);
	Элементы.ДобавитьСДискаКонтекст.Доступность = (СостояниеРазрешаетДобавлениеФайла
		Или СостояниеРазрешаетДобавлениеСканКопии);
	
	Элементы.ОткрытьДляРедактирования.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.КонтекстОткрытьДляРедактирования.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.ЗакончитьРедактирование.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.КонтекстЗакончитьРедактирование.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.ОбновитьИзФайлаНаДиске.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.ОбновитьИзФайлаНаДискеКонтекст.Доступность = СостояниеРазрешаетРедактированиеФайла;
	Элементы.СохранитьИзменения.Доступность = СостояниеРазрешаетРедактированиеФайла;
	
КонецПроцедуры

&НаКлиенте
Процедура ВосстановитьПоложениеВСписке()
	
	// Раскроем дерево
	Для Каждого СтрокаКРаскрытию Из СтрокиКРаскрытию Цикл
		Строка = СтрокаПоID(СтрокаКРаскрытию);
		Если Строка <> Неопределено Тогда
			Элементы.ДеревоФайлов.Развернуть(Строка.ПолучитьИдентификатор(), Истина);
		КонецЕсли;
	КонецЦикла;
	
	ТекущаяСтрокаУстановлена = Ложь;
	
	// Если передан идентификатор текущей строки - установим текущую строку на него.
	Если ЗначениеЗаполнено(ТекущийИдентификаторСтроки) Тогда
		Строка = СтрокаПоID(ТекущийИдентификаторСтроки, ТекущийТипСтроки);
		Если Строка <> Неопределено Тогда
			Элементы.ДеревоФайлов.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
			ТекущаяСтрокаУстановлена = Истина;
		КонецЕсли;
	КонецЕсли;
	
	// Если идентификатор текущей строки не передан - установим текущей первую строку с HTML представлением файла.
	Если Не ТекущаяСтрокаУстановлена Тогда
		
		Для Каждого Строка Из ДеревоФайлов.ПолучитьЭлементы() Цикл
			
			Если ИспользоватьРолиФайлов Тогда
				Для Каждого СтрокаФайл Из Строка.ПолучитьЭлементы() Цикл
					Если ЗначениеЗаполнено(СтрокаФайл.ПредставлениеHTML) Тогда
						Элементы.ДеревоФайлов.ТекущаяСтрока = СтрокаФайл.ПолучитьИдентификатор();
						ТекущаяСтрокаУстановлена = Истина;
						Прервать;
					КонецЕсли;
				КонецЦикла;
			Иначе
				Если ЗначениеЗаполнено(Строка.ПредставлениеHTML) Тогда
					Элементы.ДеревоФайлов.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
					ТекущаяСтрокаУстановлена = Истина;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущаяСтрокаУстановлена Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Если файлов с HTML представлением нет - установим строку на первый файл.
	Если Не ТекущаяСтрокаУстановлена Тогда
		СтрокаКРаскрытию = Неопределено;
		Если СтрокиКРаскрытию.Количество() > 0 Тогда
			СтрокаКРаскрытию = СтрокаПоID(СтрокиКРаскрытию[0]);
		КонецЕсли;
		Для Каждого Строка Из ДеревоФайлов.ПолучитьЭлементы() Цикл
			Если Строка.Тип = "DMFile" Тогда
				// Роли файлов не используются, установим строку на первый файл.
				Элементы.ДеревоФайлов.ТекущаяСтрока = Строка.ПолучитьИдентификатор();
				ТекущаяСтрокаУстановлена = Истина;
				Прервать;
			КонецЕсли;
			Для Каждого СтрокаФайл Из Строка.ПолучитьЭлементы() Цикл
				Если СтрокаФайл.Тип = "DMFile"
						И (СтрокаКРаскрытию = Неопределено Или СтрокаФайл.РольФайлаID = СтрокаКРаскрытию.ID) Тогда
					Элементы.ДеревоФайлов.ТекущаяСтрока = СтрокаФайл.ПолучитьИдентификатор();
					ТекущаяСтрокаУстановлена = Истина;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			Если ТекущаяСтрокаУстановлена Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Если никаких файлов нет - установим строку на первую обязательную роль файлов.
	Если Не ТекущаяСтрокаУстановлена И СтрокиКРаскрытию.Количество() > 0 Тогда
		СтрокаКРаскрытию = СтрокаПоID(СтрокиКРаскрытию[0]);
		Если СтрокаКРаскрытию <> Неопределено Тогда
			Элементы.ДеревоФайлов.ТекущаяСтрока = СтрокаКРаскрытию.ПолучитьИдентификатор();
			ТекущаяСтрокаУстановлена = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаПоID(ИдентификаторСтроки, ТипСтроки = Неопределено)
	
	Для Каждого Строка Из ДеревоФайлов.ПолучитьЭлементы() Цикл
		
		Если Строка.ID = ИдентификаторСтроки И Строка.Тип = ТипСтроки Тогда
			Возврат Строка;
			
		ИначеЕсли Строка.ID = ИдентификаторСтроки И ТипСтроки = Неопределено Тогда
			Возврат Строка;
			
		КонецЕсли;
		
		Если ИспользоватьРолиФайлов Тогда
			Для Каждого СтрокаФайл Из Строка.ПолучитьЭлементы() Цикл
				Если СтрокаФайл.ID = ИдентификаторСтроки И СтрокаФайл.Тип = ТипСтроки Тогда
					Возврат СтрокаФайл;
					
				ИначеЕсли СтрокаФайл.ID = ИдентификаторСтроки И ТипСтроки = Неопределено Тогда
					Возврат СтрокаФайл;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции

// Открывает карточку выбранного файла.
//
&НаКлиенте
Процедура ОткрытьКарточку()
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("РазрешеноРедактирование", СостояниеРазрешаетРедактированиеФайла);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
		ТекущиеДанные.Тип,
		ТекущиеДанные.ID,
		ЭтотОбъект,
		ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийФайлНаЧтение()
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийФайлДляРедактирования()
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.Тип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
		ТекущиеДанные.ID,
		ТекущиеДанные.Наименование,
		ТекущиеДанные.Расширение,
		УникальныйИдентификатор,
		Ложь,,
		ID);
	
КонецПроцедуры

// Завершает удаление файла после вопроса пользователю.
//
&НаКлиенте
Процедура ПослеУдаленияФайлов(Результат, ПараметрыОповещения) Экспорт
	
	ОбновитьСписокФайловКлиент();
	
КонецПроцедуры

// Меняет доступность команд в зависимости от выбранного файла.
//
&НаКлиенте
Процедура УстановитьДоступностьКомандСпискаФайлов()
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено
			Или Не ОбновлениеУспешноЗавершено
			Или (ТекущиеДанные <> Неопределено И ТекущиеДанные.Тип = "DMFileRole") Тогда
		
		УбратьДоступностьКоманд();
		
	Иначе
		
		Редактируется = ТекущиеДанные.Редактируется;
		РедактируетсяТекущимПользователем = ТекущиеДанные.РедактируетсяТекущимПользователем;
		ЕстьПодписи = ТекущиеДанные.ПодписанЭП;
		Зашифрован = ТекущиеДанные.Зашифрован;
		
		РедактируетсяДругимПользователем =
			Редактируется
			И Не РедактируетсяТекущимПользователем;
		МожноЗахватить =
			СостояниеРазрешаетРедактированиеФайла
			И Не РедактируетсяДругимПользователем
			И Не Зашифрован
			И Не ЕстьПодписи;
		МожноОтпустить =
			СостояниеРазрешаетРедактированиеФайла
			И РедактируетсяТекущимПользователем
			И Не Зашифрован
			И Не ЕстьПодписи;
		
		Элементы.ОткрытьНаЧтение.Доступность = Не Зашифрован;
		Элементы.КонтекстОткрытьНаЧтение.Доступность = Не Зашифрован;
		Элементы.ОткрытьДляРедактирования.Доступность = МожноЗахватить;
		Элементы.КонтекстОткрытьДляРедактирования.Доступность = МожноЗахватить;
		Элементы.ЗакончитьРедактирование.Доступность = МожноОтпустить;
		Элементы.КонтекстЗакончитьРедактирование.Доступность = МожноОтпустить;
		Элементы.СохранитьИзменения.Доступность = МожноОтпустить;
		Элементы.ОтменитьРедактирование.Доступность = МожноОтпустить;
		Элементы.ОтменитьРедактированиеКонтекст.Доступность = МожноОтпустить;
		Элементы.СохранитьНаДиск.Доступность = Не Зашифрован;
		Элементы.СохранитьНаДискКонтекст.Доступность = Не Зашифрован;
		Элементы.ОбновитьИзФайлаНаДиске.Доступность = МожноЗахватить;
		Элементы.ОбновитьИзФайлаНаДискеКонтекст.Доступность = МожноЗахватить;
		Элементы.ОткрытьКарточку.Доступность = Истина;
		Элементы.ОткрытьКарточкуКонтекст.Доступность = Истина;
		Элементы.Удалить.Доступность = Не Редактируется И СостояниеРазрешаетУдалениеФайла;
		Элементы.УдалитьКонтекст.Доступность = Не Редактируется И СостояниеРазрешаетУдалениеФайла;
		Элементы.ПодменюПечать.Доступность = Не Зашифрован;
		Элементы.Отправить.Доступность = Истина;
		Элементы.ГруппаЭП.Доступность = СостояниеРазрешаетИзменениеЭП;
		Элементы.ГруппаЭПКонтекст.Доступность = СостояниеРазрешаетИзменениеЭП;
		Элементы.ФормаГруппаДополнительно.Доступность = Истина;
		Элементы.ВыбратьРольФайлаКонтекст.Доступность = СостояниеРазрешаетДобавлениеФайла;
		Элементы.Скопировать.Доступность = СостояниеРазрешаетДобавлениеФайла;
		Элементы.СкопироватьКонтекст.Доступность = СостояниеРазрешаетДобавлениеФайла;
		Элементы.ОткрытьКаталогФайлаКонтекст.Доступность = Истина;
		Элементы.СохранитьВместеСЭП.Доступность = ЕстьПодписи;
		Элементы.СохранитьВместеСЭПКонтекст.Доступность = ЕстьПодписи;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УбратьДоступностьКоманд()
	
	Элементы.ОткрытьНаЧтение.Доступность = Ложь;
	Элементы.КонтекстОткрытьНаЧтение.Доступность = Ложь;
	Элементы.ОткрытьДляРедактирования.Доступность = Ложь;
	Элементы.КонтекстОткрытьДляРедактирования.Доступность = Ложь;
	Элементы.ЗакончитьРедактирование.Доступность = Ложь;
	Элементы.КонтекстЗакончитьРедактирование.Доступность = Ложь;
	Элементы.СохранитьИзменения.Доступность = Ложь;
	Элементы.ОтменитьРедактирование.Доступность = Ложь;
	Элементы.ОтменитьРедактированиеКонтекст.Доступность = Ложь;
	Элементы.СохранитьНаДиск.Доступность = Ложь;
	Элементы.СохранитьНаДискКонтекст.Доступность = Ложь;
	Элементы.ОбновитьИзФайлаНаДиске.Доступность = Ложь;
	Элементы.ОбновитьИзФайлаНаДискеКонтекст.Доступность = Ложь;
	Элементы.ОткрытьКарточку.Доступность = Ложь;
	Элементы.ОткрытьКарточкуКонтекст.Доступность = Ложь;
	Элементы.Удалить.Доступность = Ложь;
	Элементы.УдалитьКонтекст.Доступность = Ложь;
	Элементы.ПодменюПечать.Доступность = Ложь;
	Элементы.Отправить.Доступность = Ложь;
	Элементы.ГруппаЭП.Доступность = Ложь;
	Элементы.ГруппаЭПКонтекст.Доступность = Ложь;
	Элементы.ФормаГруппаДополнительно.Доступность = Ложь;
	Элементы.ВыбратьРольФайлаКонтекст.Доступность = Ложь;
	Элементы.Скопировать.Доступность = Ложь;
	Элементы.СкопироватьКонтекст.Доступность = Ложь;
	Элементы.ОткрытьКаталогФайлаКонтекст.Доступность = Ложь;
	Элементы.СохранитьВместеСЭП.Доступность = Ложь;
	Элементы.СохранитьВместеСЭПКонтекст.Доступность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПревьюФайла()
	
	ОбновитьПредпросмотр();
	
КонецПроцедуры

// Обновляет картинку предпросмотра.
//
&НаКлиенте
Процедура ОбновитьПредпросмотр(ОбновитьДанные = Ложь, ИгнорироватьМаксРазмер = Ложь)
	
	Если Предпросмотр = Ложь Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницыПревью.ТекущаяСтраница = Элементы.СтраницаЗагрузкаПревью;
	Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHTML;
	
	РедактируетсяТекущимПользователем = Ложь;
	ПредпросмотрУрезан = Ложь;
	
	ТекущиеДанные = Элементы.ДеревоФайлов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ТекущиеДанные.ОжиданиеПредпросмотра Тогда
			Возврат;
		КонецЕсли;
		
		ВернутьДвоичныеДанные = ПоказыватьPDFСредствами1С И (НРег(ТекущиеДанные.Расширение) = "pdf");
		ЕстьФайлПредпросмотра = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ФайлЕстьНаСервере(
			ТекущиеДанные.ПолноеИмяФайлаПредпросмотра);
		
		Если ТекущиеДанные.Тип = "DMFile"
				И (ОбновитьДанные
					Или (ВернутьДвоичныеДанные И Не ЕстьФайлПредпросмотра)
					Или (Не ЗначениеЗаполнено(ТекущиеДанные.ПредставлениеHTML) И Не ЕстьФайлПредпросмотра)) Тогда
			
			ТекущиеДанные.ОжиданиеПредпросмотра = Истина;
			ДлительнаяОперация = ИнтеграцияС1СДокументооборот3ВызовСервера.ПолучитьHTMLПредпросмотрОбъектаАсинхронно(
				УникальныйИдентификатор,
				ТекущиеДанные.ID,
				ТекущиеДанные.Тип,
				ИгнорироватьМаксРазмер,
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(ID, Тип),
				ВернутьДвоичныеДанные);
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения(
				"ОбновитьПредпросмотрЗавершение",
				ЭтотОбъект,
				Новый Структура("ТекущиеДанные", ТекущиеДанные));
			
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
				ЭтотОбъект,
				ДлительнаяОперация,
				ОповещениеОЗавершении,
				Ложь,,
				Ложь);
			
			Возврат;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ПредставлениеHTML) Тогда
			ПредставлениеHTML = ТекущиеДанные.ПредставлениеHTML;
		ИначеЕсли ЕстьФайлПредпросмотра Тогда
			Если ТекущиеДанные.РедактируетсяТекущимПользователем Тогда
				ПредставлениеHTML = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОРедактировании(
					ЗаголовокСообщенияВОбластиПредпросмотра);
			Иначе
				ЭтотОбъект["ДокументPDFРеквизит"] =
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьДокументPDF(
						ТекущиеДанные.ПолноеИмяФайлаПредпросмотра);
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаPDF;
				Элементы["ДокументPDFПоле"].НомерТекущейСтраницы = 1;
			КонецЕсли;
		Иначе
			ПредставлениеHTML = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
				ЗаголовокСообщенияВОбластиПредпросмотра);
		КонецЕсли;
		
		РедактируетсяТекущимПользователем = ТекущиеДанные.РедактируетсяТекущимПользователем;
		ПредпросмотрУрезан = ТекущиеДанные.ПредпросмотрУрезан;
		
	КонецЕсли;
	
	ОбновитьЭлементыПредпросмотра(РедактируетсяТекущимПользователем, ПредпросмотрУрезан);
	
	Элементы.ГруппаСтраницыПревью.ТекущаяСтраница = Элементы.СтраницаПревью;
	
КонецПроцедуры

// Вызывается после получения предпросмотра объекта ДО.
//
// Параметры:
//   Результат - см. ДлительныеОперации.ОперацияВыполнена
//   ПараметрыОповещения - Структура:
//     * ТекущиеДанные - см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеФайла
//
&НаКлиенте
Процедура ОбновитьПредпросмотрЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено И Результат.Статус = "Ошибка" Тогда
		ПредставлениеHTML =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеВПредпросмотр(
				Результат.КраткоеПредставлениеОшибки,
				ЗаголовокСообщенияВОбластиПредпросмотра);
		HTMLПредпросмотрОбъекта = Новый Структура;
		HTMLПредпросмотрОбъекта.Вставить("ID", ПараметрыОповещения.ТекущиеДанные.ID);
		HTMLПредпросмотрОбъекта.Вставить("Тип", ПараметрыОповещения.ТекущиеДанные.Тип);
		HTMLПредпросмотрОбъекта.Вставить("Расширение", ПараметрыОповещения.ТекущиеДанные.Расширение);
		HTMLПредпросмотрОбъекта.Вставить("ПредставлениеHTML", ПредставлениеHTML);
		HTMLПредпросмотрОбъекта.Вставить("ПредпросмотрУрезан", Ложь);
		HTMLПредпросмотрОбъекта.Вставить("ДвоичныеДанные", Неопределено);
		Оповестить("ПолученHTMLПредпросмотрОбъекта", HTMLПредпросмотрОбъекта, ЭтотОбъект);
	КонецЕсли;
	
	ОбработатьФорму = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		HTMLПредпросмотрОбъекта = Результат.РезультатДлительнойОперации;
		HTMLПредпросмотрОбъекта.Вставить("ID", ПараметрыОповещения.ТекущиеДанные.ID);
		HTMLПредпросмотрОбъекта.Вставить("Тип", ПараметрыОповещения.ТекущиеДанные.Тип);
		HTMLПредпросмотрОбъекта.Вставить("Расширение", ПараметрыОповещения.ТекущиеДанные.Расширение);
		Если ПустаяСтрока(HTMLПредпросмотрОбъекта.ПредставлениеHTML) Тогда
			HTMLПредпросмотрОбъекта.ПредставлениеHTML =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
					ЗаголовокСообщенияВОбластиПредпросмотра);
		КонецЕсли;
		Оповестить("ПолученHTMLПредпросмотрОбъекта", HTMLПредпросмотрОбъекта, ЭтотОбъект);
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыПредпросмотра(РедактируетсяТекущимПользователем = Ложь, ПредпросмотрУрезан = Ложь)
	
	Если Предпросмотр Тогда
		Если РедактируетсяТекущимПользователем Тогда
			Элементы.ГруппаПредпросмотрУрезан.Видимость = Ложь;
		Иначе
			Элементы.ГруппаПредпросмотрУрезан.Видимость = ПредпросмотрУрезан;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлыПредпросмотраPDF()
	
	Если Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаPDF Тогда
		ПредставлениеHTML =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
				ЗаголовокСообщенияВОбластиПредпросмотра);
		Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHTML;
	КонецЕсли;
	
	Если ПоказыватьPDFСредствами1С Тогда
		ЭтотОбъект["ДокументPDFРеквизит"] = Неопределено;
	КонецЕсли;
	
	Для Каждого Файл Из ВременныеФайлыПредпросмотра Цикл
		УдалитьФайлыАсинх(Файл.Значение);
	КонецЦикла;
	
	ВременныеФайлыПредпросмотра.Очистить();
	
КонецПроцедуры

&НаКлиенте
Функция МассивФайловИзПараметровПеретаскивания(ЗначениеПеретаскивания)
	
	МассивФайлов = Новый Массив;
	
	Если ТипЗнч(ЗначениеПеретаскивания) = Тип("Файл") И ЗначениеПеретаскивания.ЭтоФайл() Тогда
		
		МассивФайлов.Добавить(ЗначениеПеретаскивания);
		
	ИначеЕсли ТипЗнч(ЗначениеПеретаскивания) = Тип("Массив") Тогда
		
		Для Каждого ПеретаскиваемыйФайл Из ЗначениеПеретаскивания Цикл
			Если ТипЗнч(ПеретаскиваемыйФайл) = Тип("Файл") И ПеретаскиваемыйФайл.ЭтоФайл() Тогда
				МассивФайлов.Добавить(ПеретаскиваемыйФайл);
			ИначеЕсли ТипЗнч(ПеретаскиваемыйФайл) = Тип("Число") Тогда
				ФайлДерева = ДеревоФайлов.НайтиПоИдентификатору(ПеретаскиваемыйФайл);
				Если ФайлДерева <> Неопределено И ФайлДерева.Тип <> "DMFileRole" Тогда
					ДанныеФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеФайла(
						ФайлДерева.Наименование,
						ФайлДерева.ID,
						ФайлДерева.Тип,
						ФайлДерева.Расширение,
						ФайлДерева.РольФайлаID);
					МассивФайлов.Добавить(ДанныеФайла);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат МассивФайлов;
	
КонецФункции

#КонецОбласти

#Область Сервер

&НаСервере
Процедура ЗаполнитьIDПоВладельцу()
	
	Если Не ЕстьВладелец Тогда
		Возврат;
	КонецЕсли;
	
	СвязанныйОбъектДО = РегистрыСведений.ОбъектыИнтегрированныеС1СДокументооборотом.ДанныеОбъектаДОПоВнешнемуОбъекту(
		Владелец);
	Если СвязанныйОбъектДО <> Неопределено Тогда
		ID = СвязанныйОбъектДО.ID;
		Тип = СвязанныйОбъектДО.type;
	КонецЕсли;
	
КонецПроцедуры

// Изменяет форму согласно доступности сервиса ДО и номеру его версии.
//
&НаСервере
Функция ОбработатьФормуСогласноВерсииСервиса()
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотНедоступен;
		Возврат Ложь;
	КонецЕсли;
	
	ФормаОбработанаУспешно = Истина;
	
	Попытка
		
		Если ИнтеграцияС1СДокументооборот3.ДоступенФункционалФайлов() Тогда
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотДоступен;
			
			// Обработаем настройки ЭП.
			НастройкиДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьНастройки();
			ЗаголовокСообщенияВОбластиПредпросмотра = НастройкиДО.ЗаголовокСообщенияВОбластиПредпросмотра;
			ИспользоватьЭлектронныеПодписиДО = (НастройкиДО.ИспользоватьЭлектронныеЦифровыеПодписи = Истина);
			ИспользоватьЭлектронныеПодписи = (ИспользоватьЭлектронныеПодписиДО И ИспользоватьЭлектронныеПодписиИС);
			ПоказыватьPDFСредствами1С = НастройкиДО.ПоказыватьPDFСредствами1С;
			Если ПоказыватьPDFСредствами1С Тогда
				ИнтеграцияС1СДокументооборот3.ВывестиPDFДокументНаФорму(ЭтотОбъект, "ГруппаPDF");
			КонецЕсли;
			
			Элементы.ДеревоФайловНомерКартинкиПодписанЗашифрован.Видимость = ИспользоватьЭлектронныеПодписиДО;
			Элементы.Подписать.Видимость = ИспользоватьЭлектронныеПодписи;
			Элементы.ДобавитьЭПИзФайла.Видимость = ИспользоватьЭлектронныеПодписи;
			Элементы.СохранитьВместеСЭП.Видимость = ИспользоватьЭлектронныеПодписи;
			Элементы.ПодписатьКонтекст.Видимость = ИспользоватьЭлектронныеПодписи;
			Элементы.ДобавитьЭПИзФайлаКонтекст.Видимость = ИспользоватьЭлектронныеПодписи;
			Элементы.СохранитьВместеСЭПКонтекст.Видимость = ИспользоватьЭлектронныеПодписи;
			
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.УстановитьЗначенияВсехПолейНДПС(
				ЭтотОбъект,
				Истина);
			
			ЗаполнитьIDПоВладельцу();
			
		Иначе
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаВерсияНеПоддерживается;
			ФормаОбработанаУспешно = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.НужноОбработатьФорму(ИнформацияОбОшибке()) Тогда
			ОбработатьФормуСогласноВерсииСервиса();
		КонецЕсли;
		
	КонецПопытки;
	
	Возврат ФормаОбработанаУспешно;
	
КонецФункции

&НаСервере
Функция ПрочитатьИОбновитьСписокФайлов(РезультатДлительнойОперации)
	
	HTMLПредпросмотрОбъекта = Неопределено;
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Если ТипЗнч(РезультатДлительнойОперации) = Тип("Структура") Тогда
		ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(
			Прокси,
			РезультатДлительнойОперации.Тип);
	Иначе
		ОбъектXDTO = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(
			Прокси,
			РезультатДлительнойОперации);
	КонецЕсли;
	
	РолиФайлов = Новый Массив;
	Если ИнтеграцияС1СДокументооборот3.ЭтоДокументДО3(ОбъектXDTO.objectID.type) Тогда
		РолиФайлов = ОбъектXDTO.documentType.roles;
	ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "roles") Тогда
		РолиФайлов = ОбъектXDTO.roles;
	КонецЕсли;
	
	СписокФайлов = Новый Массив;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ОбъектXDTO, "files") Тогда
		СписокФайлов = ОбъектXDTO.files; // СписокXDTO
	КонецЕсли;
	
	Для Каждого ФайлXDTO Из СписокФайлов Цикл
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ФайлXDTO, "htmlView")
				Или ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ФайлXDTO, "binaryData") Тогда
			HTMLПредпросмотрОбъекта = Новый Структура("ПредставлениеHTML, ПредпросмотрУрезан, ДвоичныеДанные",
				"", Ложь, Неопределено);
			HTMLПредпросмотрОбъекта.Вставить("ID", ФайлXDTO.ObjectID.ID);
			HTMLПредпросмотрОбъекта.Вставить("Тип", ФайлXDTO.ObjectID.type);
			HTMLПредпросмотрОбъекта.Вставить("Расширение", ФайлXDTO.extension);
			Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.СвойствоУстановлено(ФайлXDTO, "binaryData") Тогда
				HTMLПредпросмотрОбъекта.ДвоичныеДанные = ФайлXDTO.binaryData;
			Иначе
				HTMLПредпросмотрОбъекта.ПредставлениеHTML = ФайлXDTO.htmlView;
				HTMLПредпросмотрОбъекта.ПредпросмотрУрезан = ФайлXDTO.htmlViewTruncated;
			КонецЕсли;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ИспользоватьРолиФайлов = (РолиФайлов.Количество() > 0);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ОбновитьДеревоФайлов(
		ДеревоФайлов,
		ДоступныеРоли,
		СписокФайлов,
		ИспользоватьРолиФайлов,
		РолиФайлов);
	
	Если ЕстьВладелец Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьНеПеренесенныеВДОПрисоединенныеФайлыВИС(
			ПрисоединенныеФайлыВИС,
			Владелец,
			СписокФайлов);
	КонецЕсли;
	
	Если ИспользоватьЭлектронныеПодписи Тогда
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ОбновитьСписокПодписейФайлов(
			СписокФайлов,
			Подписи,
			УникальныйИдентификатор);
	КонецЕсли;
	
	// Уточним доступность команд добавления и редактирования.
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ЗаполнитьНДПСизОбъектаXDTO(ЭтотОбъект, ОбъектXDTO);
	
	Возврат HTMLПредпросмотрОбъекта;
	
КонецФункции

#КонецОбласти

#Область Почта

// Начинает отправку письма средствами ИС, если использование почты ДО выключено.
//
&НаКлиенте
Процедура ОтправитьЧерезИС()
	
	Если ДоступнаОтправкаЧерезИС Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЧерезИСНастройкаУчетнойЗаписиПредложена", ЭтотОбъект);
		МодульРаботаСПочтовымиСообщениямиКлиент =
			ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
		МодульРаботаСПочтовымиСообщениямиКлиент.ПроверитьНаличиеУчетнойЗаписиДляОтправкиПочты(ОписаниеОповещения);
	Иначе
		ПоказатьПредупреждение(,НСтр("ru = 'Отправка файлов не поддерживается';
									|en = 'File sending is not supported'"));
	КонецЕсли;
	
КонецПроцедуры

// Продолжает отправку письма средствами ИС после помещения файлов из ДО в хранилище.
//
&НаКлиенте
Процедура ОтправитьЧерезИСНастройкаУчетнойЗаписиПредложена(УчетнаяЗаписьНастроена, Параметры) Экспорт
	
	Если УчетнаяЗаписьНастроена <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеФайлы = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыделенныеФайлы(
		ДеревоФайлов,
		Элементы.ДеревоФайлов.ВыделенныеСтроки);
	Вложения = Новый Массив;
	Для Каждого ВыделенныйФайл Из ВыделенныеФайлы Цикл
		Представление = СтрШаблон("%1.%2", ВыделенныйФайл.Наименование, ВыделенныйФайл.Расширение);
		Вложение = Новый Структура;
		Вложение.Вставить("Представление", Представление);
		Вложение.Вставить("Идентификатор", Неопределено);
		Вложение.Вставить("ИдентификаторДО", ВыделенныйФайл.ID);
		Вложения.Добавить(Вложение);
	КонецЦикла;
	
	Параметры = ПараметрыИсходящегоПисьма(Владелец, Вложения, УникальныйИдентификатор);
	
	МодульРаботаСПочтовымиСообщениямиКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("РаботаСПочтовымиСообщениямиКлиент");
	МодульРаботаСПочтовымиСообщениямиКлиент.СоздатьНовоеПисьмо(Параметры);
	
КонецПроцедуры

// Формирует параметры исходящего письма со вложениями.
//
// Параметры:
//   Владелец - Произвольный - владелец файлов.
//   Вложения - Массив - массив структур со свойствами:
//     Представление - Строка - представление файла;
//     Идентификатор - Строка - идентификатор файла в ДО.
//   УникальныйИдентификаторФормы - УникальныйИдентификатор - идентификатор вызвавшей формы.
//
&НаСервереБезКонтекста
Функция ПараметрыИсходящегоПисьма(Знач Владелец, Знач Вложения, Знач УникальныйИдентификаторФормы)
	
	// Подготовим заголовок письма.
	ПараметрыПисьма = Новый Структура("УдалятьФайлыПослеОтправки", Истина);
	ОдноВложение = Вложения.Количество() = 1;
	
	// Получатель.
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПодсистемаСуществует("ОтправкаПочтовыхСообщений") Тогда
		МодульОтправкаПочтовыхСообщений = ОбщегоНазначения.ОбщийМодуль("ОтправкаПочтовыхСообщений");
	Иначе
		МодульОтправкаПочтовыхСообщений = Неопределено;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЕстьРеквизитОбъекта("Контрагент", Владелец.Метаданные())
			И МодульОтправкаПочтовыхСообщений <> Неопределено Тогда
		Контрагент = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Контрагент");
		ПараметрыПисьма.Вставить("Получатель", МодульОтправкаПочтовыхСообщений.АдресаЭлектроннойПочты(Контрагент));
	Иначе
		ПараметрыПисьма.Вставить("Получатель", "");
	КонецЕсли;
	
	// Тема.
	НомерДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Номер");
	Если Метаданные.ОбщиеМодули.Найти("ПрефиксацияОбъектовКлиентСервер") <> Неопределено Тогда
		МодульПрефиксацияОбъектовКлиентСервер = ОбщегоНазначения.ОбщийМодуль("ПрефиксацияОбъектовКлиентСервер");
		НомерДокумента = МодульПрефиксацияОбъектовКлиентСервер.НомерНаПечать(НомерДокумента, Истина, Истина);
	КонецЕсли;
	ДатаДокумента = Формат(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "Дата"), "ДЛФ=DD");
	Тема = СтрШаблон(НСтр("ru = '%1, %2 к документу %3 %4 от %5';
							|en = '%1, %2 to document %3 %4 from %5'"),
		?(ОдноВложение, НСтр("ru = 'Файл';
							|en = 'File'"), НСтр("ru = 'Файлы';
												|en = 'Files'")),
		?(ОдноВложение, НСтр("ru = 'присоединенный';
							|en = 'attached'"), НСтр("ru = 'присоединенные';
															|en = 'attached'")),
		ТипЗнч(Владелец),
		НомерДокумента,
		ДатаДокумента);
	ПараметрыПисьма.Вставить("Тема", Тема);
	
	// Текст.
	Текст = СтрШаблон(НСтр("ru = 'К письму %1 %2:';
							|en = 'To email %1 %2:'"),
		?(ОдноВложение, НСтр("ru = 'присоединен';
							|en = 'attached'"), НСтр("ru = 'присоединены';
														|en = 'attached'")),
		?(ОдноВложение, НСтр("ru = 'файл';
							|en = 'file'"), НСтр("ru = 'файлы';
												|en = 'files'")));
	
	// Получим вложения из ДО.
	Для Каждого Вложение Из Вложения Цикл
		ИспользоватьВизуализациюШтампаЭП = Истина;
		АдресВоВременномХранилище =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьФайлИПоместитьВХранилище(
				Вложение.ИдентификаторДО,
				ИспользоватьВизуализациюШтампаЭП,
				УникальныйИдентификаторФормы);
		Вложение.Вставить("АдресВоВременномХранилище", АдресВоВременномХранилище);
		Текст = Текст + Символы.ПС + СтрШаблон(НСтр("ru = '- %1';
													|en = '- %1'"), Вложение.Представление);
	КонецЦикла;
	ПараметрыПисьма.Вставить("Вложения", Вложения);
	
	Если МодульОтправкаПочтовыхСообщений <> Неопределено Тогда
		ПодготовленныйТекст = МодульОтправкаПочтовыхСообщений.ПодготовитьТекстПисьма(Текст);
		ПараметрыПисьма.Вставить("Текст", ПодготовленныйТекст);
		МодульОтправкаПочтовыхСообщений.ДополнитьПараметрыПисьма(ПараметрыПисьма);
	КонецЕсли;
	
	Возврат ПараметрыПисьма;
	
КонецФункции

#КонецОбласти

#КонецОбласти