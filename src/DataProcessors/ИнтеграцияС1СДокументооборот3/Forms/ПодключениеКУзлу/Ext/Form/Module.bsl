#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВебСервиса = Параметры.АдресВебСервиса;
	ИдентификаторУзла = Параметры.ИдентификаторУзла;
	ИмяУзла = Параметры.ИмяУзла;
	
	Если Не ПустаяСтрока(ИмяУзла) Тогда
		Заголовок = СтрШаблон(НСтр("ru = 'Подключение к узлу %1';
									|en = 'Connect to the %1 node'"), ИмяУзла);
		Элементы.ПояснениеАдресВебСервиса.Заголовок =
			СтрШаблон(НСтр("ru = 'Адрес, по которому опубликован веб-сервис узла %1.';
							|en = 'Address where the web service of the %1 node is published.'"), ИмяУзла);
	КонецЕсли;
	
	ИмяПользователяИБ =
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ИмяПользователяИБДляАвторизацииВДО(
			Пользователи.ТекущийПользователь());
#Если Не ВебКлиент Тогда
	Если Не ЗначениеЗаполнено(ИмяПользователяИБ) Тогда
		// Добавим в список выбора имя пользователя ИС.
		ПользовательИБ = ПользователиИнформационнойБазы.ТекущийПользователь();
		ИмяПользователяИБ = ПользовательИБ.Имя;
	КонецЕсли;
#КонецЕсли
	
	Если ЗначениеЗаполнено(ИмяПользователяИБ) Тогда
		// Добавим в список выбора имя пользователя ИС.
		Элементы.ИмяПользователя.СписокВыбора.Добавить(ИмяПользователяИБ);
		Элементы.ИмяПользователя.КнопкаВыпадающегоСписка = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подключиться(Команда)
	
	Закрыть(ПодключитьсяНаСервере());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодключитьсяНаСервере()
	
	НастройкиАвторизации =
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.НастройкиАвторизацииВ1СДокументообороте();
	НастройкиАвторизации.ИмяПользователя = ИмяПользователя;
	НастройкиАвторизации.Пароль = Пароль;
	НастройкиАвторизации.ИспользуетсяАутентификацияОС = Ложь;
	НастройкиАвторизации.ИспользуетсяАутентификацияJWT = Ложь;
	НастройкиАвторизации.ИспользоватьКеш = Ложь;
	
	ВызыватьИсключение = Истина;
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьПрокси(
		ВызыватьИсключение,
		НастройкиАвторизации,
		АдресВебСервиса,,
		Истина);
	
	Запрос = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СоздатьОбъект(Прокси, "DMGetThisNodeRequest");
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапрос(Прокси, Запрос);
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьВозвратВебСервиса(Прокси, Ответ);
	
	Если Ответ.node.objectID.id <> ИдентификаторУзла Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'По адресу ""%1"" опубликован узел ""%2"". Требуется указать адрес публикации узла ""%3"".';
				|en = 'At ""%1"", the ""%2"" node is published. Specify a publication URL for the ""%3"" node.'"),
			АдресВебСервиса,
			Ответ.node.name,
			ИмяУзла);
	КонецЕсли;
	
	ДанныеУзла = РегистрыСведений.ДанныеУзлов1СДокументооборота.НовыеДанныеУзла();
	ДанныеУзла.АдресВебСервиса = АдресВебСервиса;
	
	РегистрыСведений.ДанныеУзлов1СДокументооборота.УстановитьДанныеУзла(ИдентификаторУзла, ДанныеУзла);
	
	Возврат ДанныеУзла;
	
КонецФункции

#КонецОбласти