#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ТипЗнч(Параметры.ОбязательныеСвязи) = Тип("Массив") И Параметры.ОбязательныеСвязи.Количество() > 0 Тогда
		
		НастройкиОбязательныхСвязей =
			Параметры.ОбязательныеСвязи; // Массив из см. ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.НастройкаСвязиОбъектовДО
		
		ИсходныйОбъектID = НастройкиОбязательныхСвязей[0].СсылкаИз.ID;
		ИсходныйОбъектТип = НастройкиОбязательныхСвязей[0].СсылкаИз.Тип;
		
		Для Каждого ОбязательнаяСвязь Из НастройкиОбязательныхСвязей Цикл
			СтрокаСвязь = ОбязательныеСвязи.Добавить();
			
			Если ОбязательнаяСвязь.СсылкаНа.Тип = "DMDocumentType" Тогда
				СтрокаСвязь.СсылкаНаТип = "DMDocument";
				СтрокаСвязь.ВидДокумента = ОбязательнаяСвязь.СсылкаНа.Наименование;
				СтрокаСвязь.ВидДокументаID = ОбязательнаяСвязь.СсылкаНа.ID;
			ИначеЕсли ОбязательнаяСвязь.СсылкаНа.Тип = "" Тогда
				СтрокаСвязь.СсылкаНаТип = "ВнешняяСсылка";
			Иначе
				СтрокаСвязь.СсылкаНаТип = ОбязательнаяСвязь.СсылкаНа.Тип;
			КонецЕсли;
			
			СтрокаСвязь.ТипСвязи = ОбязательнаяСвязь.ТипСвязи.Наименование;
			Если ЗначениеЗаполнено(ОбязательнаяСвязь.ТипСвязиКомментарий) Тогда
				СтрокаСвязь.ТипСвязиПредставление = СтрШаблон(НСтр("ru = '%1 (%2)';
																	|en = '%1 (%2)'"),
					ОбязательнаяСвязь.ТипСвязи.Представление,
					ОбязательнаяСвязь.ТипСвязиКомментарий);
			Иначе
				СтрокаСвязь.ТипСвязиПредставление = ОбязательнаяСвязь.ТипСвязи.Представление;
			КонецЕсли;
			СтрокаСвязь.ТипСвязиID = ОбязательнаяСвязь.ТипСвязи.ID;
			СтрокаСвязь.ТипСвязиТип = ОбязательнаяСвязь.ТипСвязи.Тип;
		КонецЦикла;
		
		ОбязательныеСвязи.Сортировать("ТипСвязиПредставление");
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбязательныеСвязи

&НаКлиенте
Процедура ОбязательныеСвязиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыборОбязательнойСвязиЗавершение", ЭтотОбъект);

	Отбор = Неопределено;
	Если ЗначениеЗаполнено(ТекущиеДанные.ВидДокументаID) Тогда
		Отбор = Новый Структура;
		ОписаниеУсловия = Новый Структура;
		ОписаниеУсловия.Вставить("Значение", ТекущиеДанные.ВидДокумента);
		ОписаниеУсловия.Вставить("ЗначениеID", ТекущиеДанные.ВидДокументаID);
		ОписаниеУсловия.Вставить("ЗначениеТип", "DMDocumentType");
		Отбор.Вставить("documentType", ОписаниеУсловия);
	КонецЕсли;
	
	Если ТекущиеДанные.СсылкаНаТип = "ВнешняяСсылка" Тогда
		ДекорацияОписание = СтрШаблон(
			НСтр("ru = 'Чтобы завершить создание документа, необходимо указать связь ""%1"".
				|Укажите адрес ссылки для создания связи.';
				|en = 'To complete the document creation, specify the ""%1"" link.
				|Specify URL to create the link.'"),
			ТекущиеДанные.ТипСвязи);
		ПараметрыФормы = Новый Структура("ДекорацияОписание", ДекорацияОписание);
		ОткрытьФорму("Обработка.ИнтеграцияС1СДокументооборот3.Форма.ФормаВнешнегоРесурса",
			ПараметрыФормы,
			ЭтотОбъект,,,,
			ОписаниеОповещения,
			РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	Иначе
		ИнтеграцияС1СДокументооборот3Клиент.ВыбратьЗначениеПоТипуОбъекта(
			ОписаниеОповещения,
			ТекущиеДанные.СсылкаНаТип,
			Отбор,
			ТекущиеДанные.СсылкаНаID,
			ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСвязь(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбязательныеСвязиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	Для Каждого Строка Из ОбязательныеСвязи Цикл
		Если Не ЗначениеЗаполнено(Строка.СсылкаНа) Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Нужно указать связанный объект.';
											|en = 'Specify the related object.'"));
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	МассивВозврата = Новый Массив;
	
	Для Каждого ОбязательнаяСвязь Из ОбязательныеСвязи Цикл
		СвязьКСозданию = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СвязьКСозданию();
		ЗаполнитьЗначенияСвойств(СвязьКСозданию, ОбязательнаяСвязь);
		Если СвязьКСозданию.СсылкаНаТип = "ВнешняяСсылка" Тогда
			СвязьКСозданию.СсылкаНаТип = "";
		КонецЕсли;
		МассивВозврата.Добавить(СвязьКСозданию);
	КонецЦикла;
	
	Закрыть(МассивВозврата);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязанныйОбъект(Команда)
	
	ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СсылкаНа) И ТекущиеДанные.СсылкаНаТип = "ВнешняяСсылка" Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(ТекущиеДанные.СсылкаНа);
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.СсылкаНаНавигационнаяСсылка) Тогда
		ИнтеграцияС1СДокументооборот3Клиент.ОткрытьОбъектДО(ТекущиеДанные.СсылкаНаНавигационнаяСсылка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыборОбязательнойСвязиЗавершение(РезультатВыбора, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатВыбора)
			И РезультатВыбора <> КодВозвратаДиалога.Отмена
			И Элементы.ОбязательныеСвязи.ТекущиеДанные <> Неопределено Тогда
		
		ТекущиеДанные = Элементы.ОбязательныеСвязи.ТекущиеДанные;
		
		ОчиститьСвязь(ТекущиеДанные);
		
		Если Не СвязанныйОбъектПодходит(РезультатВыбора, ТекущиеДанные.ТипСвязиID) Тогда
			ТекстПредупреждения = СтрШаблон(
				НСтр("ru = 'Выбранный объект не соответствует типу связи ""%1"".
					|Проверьте настройки связи и выберите подходящий связанный объект.';
					|en = 'The selected object does not match the ""%1"" link type.
					|Check the link settings and select the appropriate related object.'"),
				ТекущиеДанные.ТипСвязиПредставление);
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
		
		Если РезультатВыбора.Свойство("ВнешнийРесурс") Тогда
			ТекущиеДанные.СсылкаНа = РезультатВыбора.ВнешнийРесурс;
			ТекущиеДанные.Комментарий = РезультатВыбора.Комментарий;
		Иначе
			ТекущиеДанные.СсылкаНа = РезультатВыбора.РеквизитПредставление;
			ТекущиеДанные.СсылкаНаID = РезультатВыбора.РеквизитID;
			ТекущиеДанные.СсылкаНаТип = РезультатВыбора.РеквизитТип;
			ТекущиеДанные.СсылкаНаНавигационнаяСсылка = РезультатВыбора.РеквизитНавигационнаяСсылка;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСвязь(ТекущиеДанные)
	
	ТекущиеДанные.СсылкаНа = "";
	ТекущиеДанные.СсылкаНаID = "";
	ТекущиеДанные.СсылкаНаНавигационнаяСсылка = "";
	ТекущиеДанные.Комментарий = "";
	
КонецПроцедуры

&НаСервере
Функция СвязанныйОбъектПодходит(Знач РезультатВыбора, Знач ТипСвязиID)
	
	СвязанныйОбъектПодходит = Ложь;
	
	СсылкаИз = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ИсходныйОбъектID,
		ИсходныйОбъектТип);
	
	СсылкаНа = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО("", "");
	Если РезультатВыбора.Свойство("ВнешнийРесурс") Тогда
		СсылкаНа.Представление = РезультатВыбора.ВнешнийРесурс;
	Иначе
		СсылкаНа.Представление = РезультатВыбора.РеквизитПредставление;
		СсылкаНа.ID = РезультатВыбора.РеквизитID;
		СсылкаНа.Тип = РезультатВыбора.РеквизитТип;
	КонецЕсли;
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПолучитьПрокси();
	НастройкиСвязейОбъектаДО = ИнтеграцияС1СДокументооборот3.НастройкиСвязей(Прокси, СсылкаИз, СсылкаНа);
	
	Для Каждого НастройкаСвязи Из НастройкиСвязейОбъектаДО.relationsSettings Цикл
		Если ТипСвязиID = НастройкаСвязи.relationType.objectID.ID Тогда
			СвязанныйОбъектПодходит = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СвязанныйОбъектПодходит;
	
КонецФункции

#КонецОбласти
