#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МодульИнтеграцияС1СДокументооборот3 = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияС1СДокументооборот3");
	МодульИнтеграцияС1СДокументооборот3.УстановитьУсловноеОформлениеУзловХолдинга(ЭтотОбъект);
	
	ДеревоУзлыКОД = РеквизитФормыВЗначение("УзлыКОД");
	
	Для Каждого СтрокаПараметрыУзлыКОД Из Параметры.УзлыКОД.ПолучитьЭлементы() Цикл
		Если ПустаяСтрока(СтрокаПараметрыУзлыКОД.ИдентификаторУзла) Тогда
			Продолжить;
		КонецЕсли;
		ЗаполнитьУзел(ДеревоУзлыКОД.Строки.Добавить(), СтрокаПараметрыУзлыКОД);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоУзлыКОД, "УзлыКОД");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУзлыКОД

&НаКлиенте
Процедура УзлыКОДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ВыбратьЗавершение();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьЗавершение();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьЗавершение()
	
	ТекущиеДанные = Элементы.УзлыКОД.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.АдресВебСервиса) Тогда
		Закрыть(ТекущиеДанные.ИдентификаторУзла);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Выбрать можно только активный узел';
										|en = 'You can only select an active node'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьУзел(СтрокаДеревоУзлыКОД, СтрокаПараметрыУзлыКОД)
	
	ЗаполнитьЗначенияСвойств(СтрокаДеревоУзлыКОД, СтрокаПараметрыУзлыКОД);
	
	Для Каждого ПодчиненнаяСтрокаПараметрыУзлыКОД Из СтрокаПараметрыУзлыКОД.ПолучитьЭлементы() Цикл
		ЗаполнитьУзел(СтрокаДеревоУзлыКОД.Строки.Добавить(), ПодчиненнаяСтрокаПараметрыУзлыКОД);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти