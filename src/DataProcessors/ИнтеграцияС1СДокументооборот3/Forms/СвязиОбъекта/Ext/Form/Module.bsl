#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбъектДО = Параметры.Наименование;
	ОбъектДОID = Параметры.ID;
	ОбъектДОТип = Параметры.Тип;
	ОбъектДОНавигационнаяСсылка = Параметры.НавигационнаяСсылка;
	
	Заголовок = СтрШаблон(НСтр("ru = 'Связи: %1';
								|en = 'Links: %1'"), ОбъектДО);
	
	ПредварительныйПросмотрСвязейХранилище = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
		"СвязиОбъектовДО",
		"ПредпросмотрФайловБИД");
	Если ПредварительныйПросмотрСвязейХранилище = Неопределено Тогда
		ПредварительныйПросмотрСвязей = Ложь;
	Иначе
		ПредварительныйПросмотрСвязей = ПредварительныйПросмотрСвязейХранилище;
	КонецЕсли;
	
	Элементы.ДеревоСвязейПредварительныйПросмотр.Пометка = ПредварительныйПросмотрСвязей;
	Элементы.ГруппаСтраницыПревью.Видимость = ПредварительныйПросмотрСвязей;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьПодключение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	УдалитьФайлыПредпросмотраPDF();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если Не Открыта() Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "ИнтеграцияС1СДокументооборотом_УспешноеПодключение" Тогда
		Если Источник <> ЭтотОбъект Тогда
			ПриПодключении();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПолученHTMLПредпросмотрОбъекта" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ПредставлениеHTML")
				Или Не Параметр.Свойство("ПредпросмотрУрезан")
				Или Не Параметр.Свойство("ДвоичныеДанные")
				Или Не Параметр.Свойство("ID")
				Или Не Параметр.Свойство("Тип")
				Или Не Параметр.Свойство("Расширение") Тогда
			Возврат;
		КонецЕсли;
		Строка = ИнтеграцияС1СДокументооборот3КлиентСервер.СвязьПоID(
			ДеревоСвязей.ПолучитьЭлементы(),
			Параметр.ID,
			Параметр.Тип);
		Если Строка <> Неопределено Тогда
			Строка.ОжиданиеПредпросмотра = Ложь;
			Если ТипЗнч(Параметр.ДвоичныеДанные) = Тип("ДвоичныеДанные") И НРег(Параметр.Расширение) = "pdf" Тогда
				Строка.ПредставлениеHTML = "";
				Строка.ПредпросмотрУрезан = Ложь;
				Строка.ПолноеИмяФайлаПредпросмотра =
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.СохранитьФайлНаСервере(
						Параметр.ДвоичныеДанные,
						Параметр.Расширение);
				ВременныеФайлыПредпросмотра.Добавить(Строка.ПолноеИмяФайлаПредпросмотра);
			Иначе
				Строка.ПредставлениеHTML = Параметр.ПредставлениеHTML;
				Строка.ПредпросмотрУрезан = Параметр.ПредпросмотрУрезан;
			КонецЕсли;
			ОбновитьПредпросмотрСвязи();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ДокументооборотФайл" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ДействиеНадФайлом")
				Или Не Параметр.Свойство("ИдентификаторФайла")
				Или Не Параметр.Свойство("ВладелецФайла")
				Или Не Параметр.Свойство("ИмяФайла")
				Или Не Параметр.Свойство("УникальныйИдентификаторФормы")
				Или Не Параметр.ДействиеНадФайлом.ИнформироватьФормуСпискаФайлов Тогда
			Возврат;
		КонецЕсли;
		НужноОбновление = Истина;
		Если Параметр.ДействиеНадФайлом.РедактированиеФайла Тогда
			Строка = ИнтеграцияС1СДокументооборот3КлиентСервер.СвязьПоID(
				ДеревоСвязей.ПолучитьЭлементы(),
				Параметр.ИдентификаторФайла,
				"DMFile");
			Если Строка = Неопределено Тогда
				НужноОбновление = Ложь;
			Иначе
				НужноОбновление = Не Строка.Редактируется;
			КонецЕсли;
		КонецЕсли;
		Если НужноОбновление Тогда
			Если Параметр.УникальныйИдентификаторФормы = Неопределено Тогда
				ВыводитьОкноОжидания = (Источник <> ЭтотОбъект);
			Иначе
				ВыводитьОкноОжидания = (УникальныйИдентификатор <> Параметр.УникальныйИдентификаторФормы);
			КонецЕсли;
			НачатьОбновлениеФормы(ВыводитьОкноОжидания);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ДобавлениеСвязиОбъектовДО" Или ИмяСобытия = "УдалениеСвязиОбъектовДО" Тогда
		Если ТипЗнч(Параметр) <> Тип("Структура")
				Или Не Параметр.Свойство("ID")
				Или Не Параметр.Свойство("Тип") Тогда
			Возврат;
		КонецЕсли;
		НужноОбновление = (Параметр.ID = ОбъектДОID) И (Параметр.Тип = ОбъектДОТип);
		Если НужноОбновление Тогда
			ВыводитьОкноОжидания = (Источник <> ЭтотОбъект);
			НачатьОбновлениеФормы(ВыводитьОкноОжидания);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатие(Элемент)
	
	Оповещение = Новый ОписаниеОповещения("ДекорацияНастройкиАвторизацииНажатиеЗавершение", ЭтотОбъект);
	ИмяФормыПараметров =
		"Обработка.ИнтеграцияС1СДокументооборотБазоваяФункциональность.Форма.АвторизацияВ1СДокументооборот";
	
	ОткрытьФорму(ИмяФормыПараметров,, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкиАвторизацииНажатиеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьТолькоОригиналыВСвязяхПриИзменении(Элемент)
	
	НачатьОбновлениеФормы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеСвязиHTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Не ЗначениеЗаполнено(ДанныеСобытия.Href) Тогда
		Возврат;
	КонецЕсли;
	
	Если СтрНайти(ДанныеСобытия.Href, "OpenForEdit") Тогда
		
		ОткрытьТекущийФайлДляРедактирования(Элементы.ДеревоСвязей.ТекущиеДанные);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "OpenForView") Тогда
		
		ОткрытьТекущийФайлНаЧтение(Элементы.ДеревоСвязей.ТекущиеДанные);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "CreatePreview") Тогда
		
		ОбновитьПредпросмотрСвязи(Истина, Истина);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "v8doc:status") Тогда
		
		ИнтеграцияС1СДокументооборот3Клиент.ОткрытьФормуДокументооборотИзДереваСвязей(
			"СтраницаОбработка",
			Элементы.ДеревоСвязей.ТекущиеДанные,
			УникальныйИдентификатор);
		
	ИначеЕсли СтрНайти(ДанныеСобытия.Href, "ShowLinks") Тогда
		
		ДеревоСвязейТекущиеДанные = ИнтеграцияС1СДокументооборот3Клиент.ДеревоСвязейТекущиеДанные(Элементы.ДеревоСвязей);
		Если ДеревоСвязейТекущиеДанные <> Неопределено Тогда
			СвязанныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
				ДеревоСвязейТекущиеДанные.СвязанныйОбъектID,
				ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип,
				ДеревоСвязейТекущиеДанные.СвязанныйОбъект,,
				ДеревоСвязейТекущиеДанные.НавигационнаяСсылка);
			ИнтеграцияС1СДокументооборот3Клиент.ПоказатьСвязиОбъекта(СвязанныйОбъектДО);
		КонецЕсли;
		
	ИначеЕсли ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ЭтоНавигационнаяСсылка(ДанныеСобытия.Href) Тогда
		
		ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьНавигационнуюСсылку(ДанныеСобытия.Href);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоСвязей

&НаКлиенте
Процедура ДеревоСвязейВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИнтеграцияС1СДокументооборот3Клиент.ОткрытьСвязанныйОбъектНаПросмотр(
		Элементы.ДеревоСвязей.ТекущиеДанные,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, ЭтоГруппа, Параметр)
	
	Отказ = Истина;
	
	Если Не ДоступенФункционалСозданияСвязей Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ОбъектДОID,
		ОбъектДОТип,
		ОбъектДО,,
		ОбъектДОНавигационнаяСсылка);
	ИнтеграцияС1СДокументооборот3Клиент.НачатьДобавлениеСвязи(ИсходныйОбъектДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ДеревоСвязейТекущиеДанные = ИнтеграцияС1СДокументооборот3Клиент.ДеревоСвязейТекущиеДанные(Элементы.ДеревоСвязей);
	Если ДеревоСвязейТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.СвязанныйОбъектID)
			Или Не ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип) Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьОбъект(
		ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип,
		ДеревоСвязейТекущиеДанные.СвязанныйОбъектID,
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередРазворачиванием(Элемент, Строка, Отказ)
	
	СтрокаДерева = ДеревоСвязей.НайтиПоИдентификатору(Строка);
	СтрокаРодитель = СтрокаДерева.ПолучитьРодителя();
	
	Если СтрокаРодитель <> Неопределено
			И СтрокаДерева.ЭтоВеткаСвязи
			И СтрокаДерева.ПолучитьЭлементы().Количество() > 0 Тогда
		ПодчиненнаяСтрока = СтрокаДерева.ПолучитьЭлементы()[0];
		Если ПодчиненнаяСтрока.СлужебнаяСтрока Тогда
			Если ДеревоСвязейРазворачиваемаяСтрока <> 0 Тогда
				Отказ = Истина;
				Возврат;
			КонецЕсли;
			ПодчиненнаяСтрока.СвязанныйОбъект = НСтр("ru = 'Ожидается получение данных из 1С:Документооборота...';
													|en = 'Expecting data from 1C:Document Management...'");
			ДеревоСвязейРазворачиваемаяСтрока = СтрокаДерева.ПолучитьИдентификатор();
			НачатьОбновлениеФормы(Ложь);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Не ДоступенФункционалСозданияСвязей Тогда
		Возврат;
	КонецЕсли;
	
	ИсходныйОбъектДО = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
		ОбъектДОID,
		ОбъектДОТип,
		ОбъектДО,,
		ОбъектДОНавигационнаяСсылка);
	ИнтеграцияС1СДокументооборот3Клиент.НачатьУдалениеСвязи(Элементы.ДеревоСвязей, ИсходныйОбъектДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейПриАктивизацииСтроки(Элемент)
	
	ДеревоСвязейТекущиеДанные = ИнтеграцияС1СДокументооборот3Клиент.ДеревоСвязейТекущиеДанные(Элементы.ДеревоСвязей);
	Если ДеревоСвязейТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СвязьМожноУдалить = ИнтеграцияС1СДокументооборот3Клиент.СвязьМожноУдалить(ДеревоСвязейТекущиеДанные);
	Элементы.ДеревоСвязейУдалить.Доступность = СвязьМожноУдалить;
	Элементы.ДеревоСвязейКонтекстноеМенюУдалить.Доступность = СвязьМожноУдалить;
	
	ЭтоСсылочныйОбъект = ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.СвязанныйОбъектID)
		И ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип);
	ЭтоФайл = (ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип = "DMFile");
	
	Элементы.ДеревоСвязейОткрыть.Доступность = ЭтоСсылочныйОбъект;
	Элементы.ДеревоСвязейКонтекстноеМенюКнопкаОткрыть.Доступность = ЭтоСсылочныйОбъект;
	Элементы.ДеревоСвязейИзменить.Доступность = ЭтоСсылочныйОбъект;
	Элементы.ДеревоСвязейСохранитьКак.Доступность = ЭтоФайл;
	Элементы.ДеревоСвязейНапечататьФайлы.Доступность = ЭтоФайл;
	Элементы.ДеревоСвязейКонтекстноеМенюКнопкаИзменить.Доступность = ЭтоСсылочныйОбъект;
	Элементы.ДеревоСвязейКонтекстноеМенюКнопкаСохранитьКак.Доступность = ЭтоФайл;
	Элементы.ДеревоСвязейКонтекстноеМенюКнопкаНапечататьФайлы.Доступность = ЭтоФайл;
	
	ПодключитьОбработчикОжидания("ОбновитьПредпросмотрСвязиОбработчикОжидания", 0.2, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДокументPDFПоворотЛево(Команда)
	
	Элементы["ДокументPDFПоле"].Ориентация = Элементы["ДокументPDFПоле"].Ориентация - 90;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFПоворотПраво(Команда)
	
	Элементы["ДокументPDFПоле"].Ориентация = Элементы["ДокументPDFПоле"].Ориентация + 90;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFУвеличить(Команда)
	
	Элементы["ДокументPDFПоле"].Масштаб = Элементы["ДокументPDFПоле"].Масштаб + 10;
	
КонецПроцедуры

&НаКлиенте
Процедура ДокументPDFУменьшить(Команда)
	
	Если Элементы["ДокументPDFПоле"].Масштаб <= 10 Тогда
		Возврат;
	КонецЕсли;
	
	Элементы["ДокументPDFПоле"].Масштаб = Элементы["ДокументPDFПоле"].Масштаб - 10;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредпросмотрСвязиКоманда(Команда)
	
	ОбновитьПредпросмотрСвязи(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСвязиКоманда(Команда)
	
	НачатьОбновлениеФормы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСвязь(Команда)
	
	ИнтеграцияС1СДокументооборот3Клиент.ОткрытьСвязанныйОбъектНаПросмотр(
		Элементы.ДеревоСвязей.ТекущиеДанные,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьПредварительныйПросмотрСвязей(Команда)
	
	ПредварительныйПросмотрСвязей = Не ПредварительныйПросмотрСвязей;
	Элементы.ДеревоСвязейПредварительныйПросмотр.Пометка = ПредварительныйПросмотрСвязей;
	Элементы.ГруппаСтраницыПревью.Видимость = ПредварительныйПросмотрСвязей;
	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить(
		"СвязиОбъектовДО",
		"ПредпросмотрФайловБИД",
		ПредварительныйПросмотрСвязей);
	ОбновитьПредпросмотрСвязи();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВсеСвязи(Команда)
	
	ТолькоВажныеСвязи = Ложь;
	Элементы.ДеревоСвязейПоказатьВсеСвязи.Доступность = Ложь;
	Элементы.ГруппаИнфоСвязи.Видимость = Ложь;
	
	НачатьОбновлениеФормы(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейСохранитьКак(Команда)
	
	ТекущиеДанные = Элементы.ДеревоСвязей.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СвязанныйОбъектТип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.СохранитьФайлКак(
		ТекущиеДанные.СвязанныйОбъектID,
		ТекущиеДанные.СвязанныйОбъект,
		ТекущиеДанные.РасширениеФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоСвязейНапечататьФайлы(Команда)
	
#Если ВебКлиент Тогда
	ПоказатьПредупреждение(,НСтр("ru = 'В Веб-клиенте печать файлов не поддерживается.';
								|en = 'Printing of files is not supported in the web client.'"));
	Возврат;
#КонецЕсли
	
	ВыделенныеФайлы = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из Элементы.ДеревоСвязей.ВыделенныеСтроки Цикл
		Данные = ДеревоСвязей.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если Данные.СвязанныйОбъектТип <> "DMFileRole" Тогда
			ДанныеФайла = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеФайла(
				Данные.СвязанныйОбъект,
				Данные.СвязанныйОбъектID,
				Данные.СвязанныйОбъектТип,
				Данные.РасширениеФайла,
				Данные.СвязанныйОбъектID);
			ВыделенныеФайлы.Добавить(ДанныеФайла);
		КонецЕсли;
	КонецЦикла;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.НапечататьФайлы(
		ВыделенныеФайлы,
		Элементы.ДеревоСвязей.ВыделенныеСтроки,
		УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Клиент

&НаКлиенте
Процедура ОбновитьПредпросмотрСвязиОбработчикОжидания()
	
	ОбновитьПредпросмотрСвязи();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредпросмотрСвязи(ОбновитьДанные = Ложь, ИгнорироватьМаксРазмер = Ложь)
	
	Если Не ПредварительныйПросмотрСвязей Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ГруппаСтраницыПревью.ТекущаяСтраница = Элементы.СтраницаЗагрузкаПревью;
	Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHTML;
	
	СвязьРедактируетсяТекущимПользователем = Ложь;
	СвязьПредпросмотрУрезан = Ложь;
	
	ДеревоСвязейТекущиеДанные = ИнтеграцияС1СДокументооборот3Клиент.ДеревоСвязейТекущиеДанные(Элементы.ДеревоСвязей);
	Если ДеревоСвязейТекущиеДанные <> Неопределено Тогда
		
		Если ДеревоСвязейТекущиеДанные.ОжиданиеПредпросмотра Тогда
			Возврат;
		КонецЕсли;
		
		// Получение НДПС документа совмещено с получением предпросмотра для оптимизации.
		// Получим НДПС в одном пакетном запросе вместе с предпросмотром, за один вызов сервиса ДО.
		// Заранее, при получении списка задач, получить НДПС всех предметов нельзя, т.к. это
		// существенно замедлит обновление списка.
		ОбъектДляПолученияНДПС = Неопределено;
		СтрокаДокумента = ИнтеграцияС1СДокументооборот3КлиентСервер.СтрокаДокументаВДеревеСвязей(ДеревоСвязейТекущиеДанные);
		Если СтрокаДокумента <> Неопределено Тогда
			ОбъектДляПолученияНДПС =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
					СтрокаДокумента.СвязанныйОбъектID,
					СтрокаДокумента.СвязанныйОбъектТип);
		КонецЕсли;
		
		ВернутьДвоичныеДанные = ПоказыватьPDFСредствами1С И (НРег(ДеревоСвязейТекущиеДанные.РасширениеФайла) = "pdf");
		ЕстьФайлПредпросмотра = ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ФайлЕстьНаСервере(
			ДеревоСвязейТекущиеДанные.ПолноеИмяФайлаПредпросмотра);
		
		Если (ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип = "DMFile"
					Или ИнтеграцияС1СДокументооборот3КлиентПовтИсп.ЭтоИзвестныйОбъектОбзора(ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип))
				И (ОбновитьДанные
					Или (ВернутьДвоичныеДанные И Не ЕстьФайлПредпросмотра)
					Или (Не ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.ПредставлениеHTML) И Не ЕстьФайлПредпросмотра)) Тогда
			
			ДеревоСвязейТекущиеДанные.ОжиданиеПредпросмотра = Истина;
			ДлительнаяОперация = ИнтеграцияС1СДокументооборот3ВызовСервера.ПолучитьHTMLПредпросмотрОбъектаАсинхронно(
				УникальныйИдентификатор,
				ДеревоСвязейТекущиеДанные.СвязанныйОбъектID,
				ДеревоСвязейТекущиеДанные.СвязанныйОбъектТип,
				ИгнорироватьМаксРазмер,
				ОбъектДляПолученияНДПС,
				ВернутьДвоичныеДанные);
			
			ОповещениеОЗавершении = Новый ОписаниеОповещения(
				"ОбновитьПредпросмотрСвязиЗавершение",
				ЭтотОбъект,
				Новый Структура("ТекущиеДанные", ДеревоСвязейТекущиеДанные));
			
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
				ЭтотОбъект,
				ДлительнаяОперация,
				ОповещениеОЗавершении,
				Ложь,,
				Ложь);
			
			Возврат;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДеревоСвязейТекущиеДанные.ПредставлениеHTML) Тогда
			ПредставлениеСвязиHTML = ДеревоСвязейТекущиеДанные.ПредставлениеHTML;
		ИначеЕсли ЕстьФайлПредпросмотра Тогда
			Если ДеревоСвязейТекущиеДанные.РедактируетсяТекущимПользователем Тогда
				ПредставлениеСвязиHTML = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОРедактировании(
					ЗаголовокСообщенияВОбластиПредпросмотра);
			Иначе
				ЭтотОбъект["ДокументPDFРеквизит"] =
					ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.ПолучитьДокументPDF(
						ДеревоСвязейТекущиеДанные.ПолноеИмяФайлаПредпросмотра);
				Элементы.ГруппаСтраницыПревью.ТекущаяСтраница = Элементы.СтраницаПревью;
				Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаPDF;
				Элементы["ДокументPDFПоле"].НомерТекущейСтраницы = 1;
			КонецЕсли;
		Иначе
			ПредставлениеСвязиHTML = ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
				ЗаголовокСообщенияВОбластиПредпросмотра);
		КонецЕсли;
		
		СвязьРедактируетсяТекущимПользователем = ДеревоСвязейТекущиеДанные.РедактируетсяТекущимПользователем;
		СвязьПредпросмотрУрезан = ДеревоСвязейТекущиеДанные.ПредпросмотрУрезан;
		
	КонецЕсли;
	
	ОбновитьЭлементыПредпросмотраСвязей(СвязьРедактируетсяТекущимПользователем, СвязьПредпросмотрУрезан);
	
	Элементы.ГруппаСтраницыПревью.ТекущаяСтраница = Элементы.СтраницаПревью;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПредпросмотрСвязиЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат <> Неопределено И Результат.Статус = "Ошибка" Тогда
		ПредставлениеСвязиHTML =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеВПредпросмотр(
				Результат.КраткоеПредставлениеОшибки,
				ЗаголовокСообщенияВОбластиПредпросмотра);
		HTMLПредпросмотрОбъекта = Новый Структура;
		HTMLПредпросмотрОбъекта.Вставить("ID", ПараметрыОповещения.ТекущиеДанные.СвязанныйОбъектID);
		HTMLПредпросмотрОбъекта.Вставить("Тип", ПараметрыОповещения.ТекущиеДанные.СвязанныйОбъектТип);
		HTMLПредпросмотрОбъекта.Вставить("Расширение", ПараметрыОповещения.ТекущиеДанные.РасширениеФайла);
		HTMLПредпросмотрОбъекта.Вставить("ПредставлениеHTML", ПредставлениеСвязиHTML);
		HTMLПредпросмотрОбъекта.Вставить("ПредпросмотрУрезан", Ложь);
		HTMLПредпросмотрОбъекта.Вставить("ДвоичныеДанные", Неопределено);
		Оповестить("ПолученHTMLПредпросмотрОбъекта", HTMLПредпросмотрОбъекта, ЭтотОбъект);
	КонецЕсли;
	
	ОбработатьФорму = Ложь;
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		HTMLПредпросмотрОбъекта = Результат.РезультатДлительнойОперации;
		HTMLПредпросмотрОбъекта.Вставить("ID", ПараметрыОповещения.ТекущиеДанные.СвязанныйОбъектID);
		HTMLПредпросмотрОбъекта.Вставить("Тип", ПараметрыОповещения.ТекущиеДанные.СвязанныйОбъектТип);
		HTMLПредпросмотрОбъекта.Вставить("Расширение", ПараметрыОповещения.ТекущиеДанные.РасширениеФайла);
		Если ПустаяСтрока(Результат.РезультатДлительнойОперации.ПредставлениеHTML) Тогда
			ПредставлениеСвязиHTML =
				ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
					ЗаголовокСообщенияВОбластиПредпросмотра);
		КонецЕсли;
		Оповестить("ПолученHTMLПредпросмотрОбъекта", HTMLПредпросмотрОбъекта, ЭтотОбъект);
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЭлементыПредпросмотраСвязей(РедактируетсяТекущимПользователем = Ложь, ПредпросмотрУрезан = Ложь)
	
	Если ПредварительныйПросмотрСвязей Тогда
		Если РедактируетсяТекущимПользователем Тогда
			Элементы.ГруппаПредпросмотрУрезан.Видимость = Ложь;
		Иначе
			Элементы.ГруппаПредпросмотрУрезан.Видимость = ПредпросмотрУрезан;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийФайлДляРедактирования(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СвязанныйОбъектТип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецID = Неопределено;
	СтрокаДокумента = ИнтеграцияС1СДокументооборот3КлиентСервер.СтрокаДокументаВДеревеСвязей(ТекущиеДанные);
	Если СтрокаДокумента <> Неопределено Тогда
		ВладелецID = СтрокаДокумента.СвязанныйОбъектID;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
		ТекущиеДанные.СвязанныйОбъектID,
		ТекущиеДанные.СвязанныйОбъект,
		ТекущиеДанные.РасширениеФайла,
		УникальныйИдентификатор,
		Ложь,,
		ВладелецID);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьТекущийФайлНаЧтение(ТекущиеДанные)
	
	Если ТекущиеДанные = Неопределено Или ТекущиеДанные.СвязанныйОбъектТип <> "DMFile" Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОткрытьФайл(
		ТекущиеДанные.СвязанныйОбъектID,
		ТекущиеДанные.СвязанныйОбъект,
		ТекущиеДанные.РасширениеФайла,
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьФайлыПредпросмотраPDF()
	
	Если Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаPDF Тогда
		ПредставлениеСвязиHTML =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
				ЗаголовокСообщенияВОбластиПредпросмотра);
		Элементы.ГруппаПредпросмотрСтраницы.ТекущаяСтраница = Элементы.ГруппаHTML;
	КонецЕсли;
	
	Если ПоказыватьPDFСредствами1С Тогда
		ЭтотОбъект["ДокументPDFРеквизит"] = Неопределено;
	КонецЕсли;
	
	Для Каждого Файл Из ВременныеФайлыПредпросмотра Цикл
		УдалитьФайлыАсинх(Файл.Значение);
	КонецЦикла;
	
	ВременныеФайлыПредпросмотра.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область Подключение

// Проверяет подключение к ДО, выводя окно авторизации, если необходимо, и изменяя форму согласно результату.
//
&НаКлиенте
Процедура ПроверитьПодключение()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодключениеЗавершение", ЭтотОбъект);
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьПодключение(
		ОписаниеОповещения,
		ЭтотОбъект,,
		Ложь,
		Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодключениеЗавершение(Результат, Параметры) Экспорт
	
	Если Результат = Истина Тогда
		ПриПодключении();
	Иначе // Не удалось подключиться к ДО.
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПодключении()
	
	Если ОбработатьФормуСогласноВерсииСервиса() Тогда
		ТолькоВажныеСвязи = Истина;
		ПоказыватьТолькоОригиналыВСвязях = Ложь;
		Элементы.ДеревоСвязейПоказатьВсеСвязи.Доступность = Истина;
		Элементы.ГруппаИнфоСвязи.Видимость = Истина;
		
		НачатьОбновлениеФормы(Ложь, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбработатьФормуСогласноВерсииСервиса()
	
	ВерсияСервиса = ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВерсияСервиса();
	
	Если Не ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СервисДоступен(ВерсияСервиса) Тогда
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотНедоступен;
		Возврат Ложь;
	КонецЕсли;
	
	ФормаОбработанаУспешно = Истина;
	
	Попытка
		
		ДоступенФункционалСозданияСвязей = ИнтеграцияС1СДокументооборот3.ДоступенФункционалСозданияСвязей();
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ДоступенФункционалВерсииСервиса("3.0.17.36") Тогда
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаДокументооборотДоступен;
			
			Если (ИнтеграцияС1СДокументооборот3.ЭтоДокументДО3(ОбъектДОТип) Или ОбъектДОТип = "DMMeeting")
					И СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ОбъектДОID) Тогда
				
				Элементы.ГруппаСтраницыПолученияДанныхСвязи.ТекущаяСтраница = Элементы.СтраницаДанныеПолученыСвязи;
				
				Если Не ДоступенФункционалСозданияСвязей Тогда
					Элементы.ДеревоСвязейДобавить.Видимость = Ложь;
					Элементы.ДеревоСвязейКонтекстноеМенюДобавить.Видимость = Ложь;
					Элементы.ДеревоСвязейКонтекстноеМенюУдалить.Видимость = Ложь;
				КонецЕсли;
				
				НастройкиДокументооборота = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьНастройки();
				ЗаголовокСообщенияВОбластиПредпросмотра = НастройкиДокументооборота.ЗаголовокСообщенияВОбластиПредпросмотра;
				
				ПоказыватьPDFСредствами1С = НастройкиДокументооборота.ПоказыватьPDFСредствами1С;
				Если ПоказыватьPDFСредствами1С Тогда
					ИнтеграцияС1СДокументооборот3.ВывестиPDFДокументНаФорму(ЭтотОбъект, "ГруппаPDF");
				КонецЕсли;
				
			Иначе
				
				Элементы.ГруппаСтраницыПолученияДанныхСвязи.ТекущаяСтраница = Элементы.СтраницаСвязиНедоступны;
				ФормаОбработанаУспешно = Ложь;
				
			КонецЕсли;
			
		Иначе
			
			Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаВерсияНеПоддерживается;
			ФормаОбработанаУспешно = Ложь;
			
		КонецЕсли;
		
	Исключение
		
		Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьВызовСервера.НужноОбработатьФорму(ИнформацияОбОшибке()) Тогда
			ОбработатьФормуСогласноВерсииСервиса();
		КонецЕсли;
		
	КонецПопытки;
	
	Возврат ФормаОбработанаУспешно;
	
КонецФункции

#КонецОбласти

#Область ОбновитьФорму

&НаКлиенте
Процедура НачатьОбновлениеФормы(ВыводитьОкноОжидания = Истина, СкрыватьИнтерфейс = Ложь)
	
	Если ДеревоСвязейРазворачиваемаяСтрока = 0 Тогда
		ПредставлениеСвязиHTML =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.СообщениеОбОтсутствииПредпросмотра(
				ЗаголовокСообщенияВОбластиПредпросмотра);
		ПрочитаноСвязей = 0;
		ВсегоСвязей = 0;
	КонецЕсли;
	
	ДлительнаяОперация = ОбновитьФорму();
	
	ПараметрыОповещения = Новый Структура("ВыводитьОкноОжидания",
		ВыводитьОкноОжидания);
	ОповещениеОЗавершении = Новый ОписаниеОповещения(
		"ОбновлениеФормыЗавершение",
		ЭтотОбъект,
		ПараметрыОповещения);
	
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьЗапросАсинхронно(
		ЭтотОбъект,
		ДлительнаяОперация,
		ОповещениеОЗавершении,
		ВыводитьОкноОжидания,
		НСтр("ru = 'Получение данных из 1С:Документооборота.';
			|en = 'Get data from 1C:Document Management.'"),
		СкрыватьИнтерфейс);
	
КонецПроцедуры

&НаСервере
Функция ОбновитьФорму()
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	
	Запрос = Неопределено;
	
	Если ДеревоСвязейРазворачиваемаяСтрока <> 0 Тогда
		СтрокаДереваСвязей = ДеревоСвязей.НайтиПоИдентификатору(ДеревоСвязейРазворачиваемаяСтрока).ПолучитьРодителя();
		РеквизитыОбъекта =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
				СтрокаДереваСвязей.СвязанныйОбъектID, СтрокаДереваСвязей.СвязанныйОбъектТип);
	Иначе
		РеквизитыОбъекта =
			ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиентСервер.ДанныеСсылочногоОбъектаДО(
				ОбъектДОID, ОбъектДОТип);
	КонецЕсли;
	Запрос = ИнтеграцияС1СДокументооборот3.СвязиОбъектаЗапрос(Прокси, РеквизитыОбъекта);
	
	Если Запрос <> Неопределено Тогда
		Возврат ИнтеграцияС1СДокументооборотБазоваяФункциональность.ВыполнитьЗапросАсинхронно(
			Прокси,
			Запрос,
			УникальныйИдентификатор,
			НСтр("ru = 'Получение данных из 1С:Документооборота';
				|en = 'Get data from 1C:Document Management'"));
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновлениеФормыЗавершение(Результат, ПараметрыОповещения) Экспорт
	
	ОбработатьФорму = Ложь;
	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПроверитьОшибкиПриОбновленииФормы(
		ЭтотОбъект,
		Результат,
		ОбработатьФорму);
	Если ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ОперацияВыполнена(Результат, ОбработатьФорму) Тогда
		ОбновитьФормуЗавершение(Результат.РезультатДлительнойОперации, ПараметрыОповещения);
		ИнтеграцияС1СДокументооборот3Клиент.РазвернутьУровеньДереваСвязей(
			ДеревоСвязейРазворачиваемаяСтрока,
			ДеревоСвязей,
			Элементы.ДеревоСвязей);
		ДеревоСвязейРазворачиваемаяСтрока = 0;
	ИначеЕсли ОбработатьФорму Тогда
		ОбработатьФормуСогласноВерсииСервиса();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФормуЗавершение(Знач ОтветСтрока, Знач ПараметрыОповещения)
	
	Прокси = ИнтеграцияС1СДокументооборотБазоваяФункциональностьПовтИсп.ПолучитьПрокси();
	Ответ = ИнтеграцияС1СДокументооборотБазоваяФункциональность.СтрокаВОбъектXDTO(Прокси, ОтветСтрока);
	
	Если ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПроверитьТип(Прокси, Ответ, "DMError") Тогда
		ТекстОшибкиПриОбновлении = Ответ.description;
		Элементы.ГруппаСтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаОшибкаПриОбновлении;
	Иначе
		ИнтеграцияС1СДокументооборот3.ЗаполнитьДеревоСвязейВФорме(ЭтотОбъект, Ответ);
		Если Не ПараметрыОповещения.ВыводитьОкноОжидания Тогда
			Элементы.ГруппаСтраницыПолученияДанныхСвязи.ТекущаяСтраница = Элементы.СтраницаДанныеПолученыСвязи;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти