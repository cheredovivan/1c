

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДолевыеНачисления = ПланыВидовРасчета.Начисления.НачисленияОплатыДолейРКиСН();
	ОсновныеНачисления = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ДолевыеНачисления,"ОсновнойВидРасчета");
	МассивСсылок = Новый Массив;
	Для Каждого ЭлементСоответствия Из ОсновныеНачисления Цикл
		МассивСсылок.Добавить(ЭлементСоответствия.Значение);
	КонецЦикла;
	КодыИКатегории = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(МассивСсылок,"КатегорияДохода,КодДоходаНДФЛ");
	
	ВсегоСтрок = ДолевыеНачисления.Количество();
	Для НомерСтроки = 0 По ВсегоСтрок - 1 Цикл
		СтрокаНачисление = Объект.ДолевыеНачисления.Добавить();
		СтрокаНачисление.Начисление = ДолевыеНачисления[НомерСтроки];
		КодИКатегория = КодыИКатегории[ОсновныеНачисления[СтрокаНачисление.Начисление]];
		Если ЗначениеЗаполнено(КодИКатегория) Тогда
			СтрокаНачисление.КодДоходаНДФЛ = КодИКатегория.КодДоходаНДФЛ;
			СтрокаНачисление.КатегорияДохода = КодИКатегория.КатегорияДохода;
		КонецЕсли;
	КонецЦикла;
	Объект.Период.ДатаНачала = '20250101';
	Объект.Ответственный = Пользователи.ТекущийПользователь();
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.ЗарплатаКадрыДляНебольшихОрганизаций.РасчетЗарплаты") Тогда
		МодульРасчетЗарплатыДляНебольшихОрганизаций = ОбщегоНазначения.ОбщийМодуль("РасчетЗарплатыДляНебольшихОрганизаций");
		Объект.ПрименяетсяАУСН = МодульРасчетЗарплатыДляНебольшихОрганизаций.ПрименяетсяАУСН();
		Элементы.ПрименяетсяАУСН.Видимость = Истина;
		Элементы.ГруппаИсправления.Видимость = Ложь;
	Иначе
		Объект.ПрименяетсяАУСН = Ложь;
		Элементы.ПрименяетсяАУСН.Видимость = Ложь;
		Элементы.ГруппаИсправления.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если СоответствиеСкрытыхПанелей = Неопределено Тогда
		ВременноеСоответствие = Новый Соответствие;
		
		ВременноеСоответствие.Вставить("ГруппаФизическиеЛицаСведенияОДоходах",Элементы.ГруппаФизическиеЛицаСведенияОДоходах.Скрыта());
		ВременноеСоответствие.Вставить("ГруппаФизическиеЛицаПредоставленныеВычеты",Элементы.ГруппаФизическиеЛицаПредоставленныеВычеты.Скрыта());
		ВременноеСоответствие.Вставить("ГруппаНДФЛИсчисленныйПоФизЛицу",Элементы.ГруппаНДФЛИсчисленныйПоФизЛицу.Скрыта());
		ВременноеСоответствие.Вставить("ГруппаНДФЛУдержанныйПоФизЛицу",Элементы.ГруппаНДФЛУдержанныйПоФизЛицу.Скрыта());
		
		СоответствиеСкрытыхПанелей = Новый ФиксированноеСоответствие(ВременноеСоответствие);
	КонецЕсли;
	
	Если ТекущаяСтраница <> Элементы.ГруппаФизическиеЛица Тогда
		Элементы.ГруппаФизическиеЛицаСведенияОДоходах.Скрыть();
		Элементы.ГруппаФизическиеЛицаПредоставленныеВычеты.Скрыть();
		Элементы.ГруппаНДФЛИсчисленныйПоФизЛицу.Скрыть();
		Элементы.ГруппаНДФЛУдержанныйПоФизЛицу.Скрыть();
	Иначе
		Если Не СоответствиеСкрытыхПанелей["ГруппаФизическиеЛицаСведенияОДоходах"] Тогда
			Элементы.ГруппаФизическиеЛицаСведенияОДоходах.Показать();
		КонецЕсли;
		Если Не СоответствиеСкрытыхПанелей["ГруппаФизическиеЛицаПредоставленныеВычеты"] Тогда
			Элементы.ГруппаФизическиеЛицаПредоставленныеВычеты.Показать();
		КонецЕсли;
		Если Не СоответствиеСкрытыхПанелей["ГруппаНДФЛИсчисленныйПоФизЛицу"] Тогда
			Элементы.ГруппаНДФЛИсчисленныйПоФизЛицу.Показать();
		КонецЕсли;
		Если Не СоответствиеСкрытыхПанелей["ГруппаНДФЛУдержанныйПоФизЛицу"] Тогда
			Элементы.ГруппаНДФЛУдержанныйПоФизЛицу.Показать();
		КонецЕсли;
		СоответствиеСкрытыхПанелей = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыФизическиеЛица

&НаКлиенте
Процедура ФизическиеЛицаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Для Каждого ФизическоеЛицо Из ВыбранноеЗначение Цикл
		
		НоваяСтрока = Объект.ФизическиеЛица.Добавить();
		НоваяСтрока.ФизическоеЛицо = ФизическоеЛицо;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаПередУдалением(Элемент, Отказ)
	
	ВыделенныеСтроки = Элементы.ФизическиеЛица.ВыделенныеСтроки;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		СтрокаДанных = Объект.ФизическиеЛица.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если СтрокаДанных <> Неопределено Тогда
			ОчиститьДанныеПоФизическомуЛицу(СтрокаДанных.ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ФизическиеЛицаПриАктивизацииСтроки(Элемент)
	УстановитьОтборыСтрокПоФизическомуЛицу();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДокументамиИсправлениями(Команда)
	
	Если ВыполнитьПроверкиПередЗаполнением() Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьДокументамиИсправлениямиНаСервере(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыИсправления

&НаКлиенте
Процедура ДокументыИсправленияПередУдалением(Элемент, Отказ)
	
	ПараметрыОтбора = Новый Структура("ИсходныйДокумент,ИсправленныйДокумент");
			
	ВыделенныеСтроки = Элементы.ДокументыИсправления.ВыделенныеСтроки;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		СтрокаДанных = Объект.ДокументыИсправления.НайтиПоИдентификатору(ВыделеннаяСтрока);
		Если СтрокаДанных <> Неопределено Тогда
			
			ПараметрыОтбора.ИсходныйДокумент = СтрокаДанных.ДокументОснование;
			ПараметрыОтбора.ИсправленныйДокумент = СтрокаДанных.ИсправленныйДокумент;
			
			СтрокиКУдалению = Объект.СведенияОДоходахИсправления.НайтиСтроки(ПараметрыОтбора);
			Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
				Объект.СведенияОДоходахИсправления.Удалить(УдаляемаяСтрока);
			КонецЦикла;
			
			СтрокиКУдалению = Объект.СведенияОДоходахКорректировкаИсправлений.НайтиСтроки(ПараметрыОтбора);
			Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
				Объект.СведенияОДоходахКорректировкаИсправлений.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Заполнить(Команда)
	
	Если ВыполнитьПроверкиПередЗаполнением() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.СведенияОДоходах.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗаполнитьНаСервереЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Данные о доходах, налоге будут перезаполнены по всем физ. лицам. Продолжить?';
							|en = 'Income and tax data will be refilled for all persons. Continue?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьНаСервере();
		УстановитьОтборыСтрокПоФизическомуЛицу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьНаСервереЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере();
		УстановитьОтборыСтрокПоФизическомуЛицу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборСотрудников(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора", Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор", Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("АдресСпискаПодобранныхСотрудников", АдресСпискаПодобранныхФизическихЛиц());
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Организация", Объект.Организация);
	РолиФизическихЛиц = СотрудникиКлиент.РолиФизическихЛицПолучателейДохода();
	Если РолиФизическихЛиц.Количество() <> 0 Тогда
		СтруктураОтбора.Вставить("Роль", РолиФизическихЛиц);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора", ПараметрыФормы, Элементы.ФизическиеЛица);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьФизЛицамиПолучающимиДоходы(Команда)
	
	Если Объект.Организация.Пустая() Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Выберите организацию';
										|en = 'Select company'"));
		Возврат;
	КонецЕсли;
	
	Если Объект.ФизическиеЛица.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросЗаполнитьФизЛицамиПолучающимиДоходыЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Физические лица будет очищены и перезаполнены. Данные по доходам и налогу очищены. Продолжить?';
							|en = 'Persons will be cleared and refilled. Income and tax data is cleared. Continue?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьФизическихЛицНаСервере();
		УстановитьОтборыСтрокПоФизическомуЛицу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьФизЛицамиПолучающимиДоходыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьФизическихЛицНаСервере();
		УстановитьОтборыСтрокПоФизическомуЛицу();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	Если Объект.ФизическиеЛица.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Не выбраны физические лица для обработки';
										|en = 'No persons are selected for processing'"));
		Возврат;
	КонецЕсли;
	Если Объект.СведенияОДоходах.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Не заполнены данные для обработки';
										|en = 'Data for processing is not filled in'"));
		Возврат;
	КонецЕсли;
	
	ОбработаноФизЛиц = СоздатьДокументыНаСервере();
	Если ОбработаноФизЛиц > 0 Тогда
		НавигационнаяСсылка = "e1cib/list/Документ.ОперацияНалоговогоУчетаПоНДФЛ";
		ТекстСообщения = Новый ФорматированнаяСтрока(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обработано физических лиц: %1';
								|en = 'Persons processed: %1'") + Символы.ПС, ОбработаноФизЛиц),
			Новый ФорматированнаяСтрока(НСтр("ru = 'Созданы документы ""Операция учета НДФЛ""';
											|en = 'The ""PIT accounting transaction"" documents are created'") + Символы.ПС,,,,НавигационнаяСсылка),
			НСтр("ru = 'Значение реквизита ""Дата операции"" в документах соответствует дате фактического получения дохода';
				|en = 'The value of the ""Transaction date"" attribute in the documents corresponds to the date of actual income receipt'"));
		ПоказатьПредупреждение(, ТекстСообщения,,);
		ОповеститьОбИзменении(Тип("ДокументСсылка.ОперацияНалоговогоУчетаПоНДФЛ"));
	Иначе
		ПоказатьПредупреждение( ,НСтр("ru = 'Выполнение обработки завершено. Отсутствовали данные или данные были обработаны ранее';
										|en = 'Processing is completed. Data was missing or data was processed earlier'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПоФизЛицу(Команда)
	
	Если Элементы.ФизическиеЛица.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.ФизическиеЛица.ВыделенныеСтроки.Количество() > 1 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Выделите только 1 строку для обновления данных по физическому лицу.
				|Если необходимо заполнить данные по всем физическим лицам, нажмите: ""Заполнить доходы, налоги""';
				|en = 'Select only 1 row to update the person''s data.
				|If you need to fill in data for all persons, click: ""Fill in income, taxes""'"));
		Возврат;
	КонецЕсли;
	
	ФизическоеЛицо = Элементы.ФизическиеЛица.ТекущиеДанные.ФизическоеЛицо;
	Если ФизическоеЛицо.Пустая() Тогда
		Возврат;
	КонецЕсли;
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	
	ЕстьДанныеПоФизЛицуВТаблицах = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора).Количество() > 0
								Или Объект.ПредоставленныеВычеты.НайтиСтроки(ПараметрыОтбора).Количество() > 0
								Или Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора).Количество() > 0
								Или Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора).Количество() > 0;
	
	Если ЕстьДанныеПоФизЛицуВТаблицах Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ВопросОбновитьПоФизЛицуЗавершение", ЭтотОбъект, ФизическоеЛицо);
		ТекстВопроса = НСтр("ru = 'На вкладках присутствуют данные по физическому лицу. Они будут перезаполнены. Продолжить?';
							|en = 'The tabs contain person''s data. The data will be refilled. Continue?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
	Иначе
		ЗаполнитьНаСервере(ФизическоеЛицо);
	КонецЕсли;
	УстановитьОтборыСтрокПоФизическомуЛицу();
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОбновитьПоФизЛицуЗавершение(РезультатВопроса, ФизическоеЛицо) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНаСервере(ФизическоеЛицо);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументыПоИсправлениям(Команда)
	
	Если Объект.ДокументыИсправления.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Не выбраны физические лица для обработки';
										|en = 'No persons are selected for processing'"));
		Возврат;
	КонецЕсли;
	Если Объект.СведенияОДоходахКорректировкаИсправлений.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Не заполнены данные для обработки';
										|en = 'Data for processing is not filled in'"));
		Возврат;
	КонецЕсли;
	
	ОбработаноДокументов = СоздатьДокументыПоИсправлениямНаСервере();
	Если ОбработаноДокументов > 0 Тогда
		НавигационнаяСсылка = "e1cib/list/Документ.ОперацияНалоговогоУчетаПоНДФЛ";
		ТекстСообщения = Новый ФорматированнаяСтрока(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Обработано документов: %1';
								|en = 'Processed documents: %1'") + Символы.ПС, ОбработаноДокументов),
			Новый ФорматированнаяСтрока(НСтр("ru = 'Созданы документы ""Операция учета НДФЛ""';
											|en = 'The ""PIT accounting transaction"" documents are created'") + Символы.ПС,,,,НавигационнаяСсылка));
		ПоказатьПредупреждение(, ТекстСообщения,,);
		ОповеститьОбИзменении(Тип("ДокументСсылка.ОперацияНалоговогоУчетаПоНДФЛ"));
	Иначе
		ПоказатьПредупреждение( ,НСтр("ru = 'Выполнение обработки завершено. Отсутствовали данные или данные были обработаны ранее';
										|en = 'Processing is completed. Data was missing or data was processed earlier'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНаСервере(ФизическоеЛицо = Неопределено)
	
	РеквизитыДольныхНачислений = Объект.ДолевыеНачисления.Выгрузить();
	НачисленияОплатыДолейРКиСН = РеквизитыДольныхНачислений.ВыгрузитьКолонку("Начисление");
	НачисленияОплатыДолейРКиСН.Добавить(Неопределено); // Для учета записей, которые были добавлены через "Операция учета НДФЛ"
	НачалоПериода = Объект.Период.ДатаНачала;
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = '20250101';
		Объект.Период.ДатаНачала = НачалоПериода;
	КонецЕсли;
	КонецПериода = Объект.Период.ДатаОкончания;
	Организация = Объект.Организация;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК ОбщаяСуммаДоходаПоРКиСН,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаДохода,
	|	СведенияОДоходахНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	СведенияОДоходахНДФЛ.КодВычета КАК КодВычета,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВычета,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление
	|ПОМЕСТИТЬ ВТСтрокиДоходовПоВсемРКиСН
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛВторичный КАК СтатусФизЛицНаНачалоГода
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = СтатусФизЛицНаНачалоГода.ФизическоеЛицо
	|			И (&НачалоПериода >= СтатусФизЛицНаНачалоГода.ДатаНачала)
	|			И (&НачалоПериода <= СтатусФизЛицНаНачалоГода.ДатаОкончания)
	|ГДЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)
	|	И СведенияОДоходахНДФЛ.КатегорияДохода = &КатегорияРКиСН
	|	И СведенияОДоходахНДФЛ.Период >= &НачалоПериода
	|	И СведенияОДоходахНДФЛ.Период <= &КонецПериода
	|	И СведенияОДоходахНДФЛ.Организация = &Организация
	|	И НЕ(СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|				И СведенияОДоходахНДФЛ.ДокументОснование ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ)
	|	И ЕСТЬNULL(СтатусФизЛицНаНачалоГода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) В (ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент), ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.ГражданинСтраныЕАЭС))
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.Подразделение,
	|	СведенияОДоходахНДФЛ.ДокументОснование,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	СведенияОДоходахНДФЛ.КатегорияДохода,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.КодВычета
	|
	|ИМЕЮЩИЕ
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ДокументОснование,
	|	ДатаПолученияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДатаПолученияДохода КАК ДатаПолученияДохода
	|ПОМЕСТИТЬ ВТДатыПолученияДохода
	|ИЗ
	|	ВТСтрокиДоходовПоВсемРКиСН КАК СтрокиДоходовПоВсемРКиСН
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДатаПолученияДохода
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтрокиДоходовПоВсемРКиСН.СуммаДохода) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ДатаПолученияДохода
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование КАК ДокументОснование,
	|	СУММА(СтрокиДоходовПоВсемРКиСН.ОбщаяСуммаДоходаПоРКиСН) <> СУММА(СтрокиДоходовПоВсемРКиСН.СуммаДохода) КАК ЕстьДругиеДоходыПоРКиСН
	|ПОМЕСТИТЬ ВТДокументыОснования
	|ИЗ
	|	ВТСтрокиДоходовПоВсемРКиСН КАК СтрокиДоходовПоВсемРКиСН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДатыПолученияДохода КАК ДатыПолученияДохода
	|		ПО СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо = ДатыПолученияДохода.ФизическоеЛицо
	|			И СтрокиДоходовПоВсемРКиСН.ДатаПолученияДохода = ДатыПолученияДохода.ДатаПолученияДохода
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование
	|
	|ИМЕЮЩИЕ
	|	СУММА(СтрокиДоходовПоВсемРКиСН.СуммаДохода) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СтрокиДоходовПоВсемРКиСН.КодДохода КАК КодДохода,
	|	СтрокиДоходовПоВсемРКиСН.КатегорияДохода КАК КатегорияДохода,
	|	СтрокиДоходовПоВсемРКиСН.ОбщаяСуммаДоходаПоРКиСН КАК ОбщаяСуммаДоходаПоРКиСН,
	|	СтрокиДоходовПоВсемРКиСН.СуммаДохода КАК СуммаДохода,
	|	СтрокиДоходовПоВсемРКиСН.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СтрокиДоходовПоВсемРКиСН.КодВычета КАК КодВычета,
	|	СтрокиДоходовПоВсемРКиСН.СуммаВычета КАК СуммаВычета,
	|	СтрокиДоходовПоВсемРКиСН.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СтрокиДоходовПоВсемРКиСН.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СтрокиДоходовПоВсемРКиСН.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование КАК ДокументОснование,
	|	СтрокиДоходовПоВсемРКиСН.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	СтрокиДоходовПоВсемРКиСН.Начисление КАК Начисление
	|ПОМЕСТИТЬ ВТСтрокиДоходов
	|ИЗ
	|	ВТСтрокиДоходовПоВсемРКиСН КАК СтрокиДоходовПоВсемРКиСН
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И СтрокиДоходовПоВсемРКиСН.ДокументОснование = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	СтрокиДоходовПоВсемРКиСН.Начисление В(&НачисленияОплатыДолейРКиСН)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыОснования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.ЕстьДругиеДоходыПоРКиСН КАК ЕстьДругиеДоходыПоРКиСН
	|ИЗ
	|	ВТДокументыОснования КАК ДокументыОснования
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыОснования.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыОснования.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТДокументыОснования КАК ДокументыОснования
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыОснования.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДолевыеНачисления.Начисление КАК Начисление,
	|	ДолевыеНачисления.КодДоходаНДФЛ КАК КодДохода,
	|	ДолевыеНачисления.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТДолевыеНачисления
	|ИЗ
	|	&ДолевыеНачисления КАК ДолевыеНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтрокиДоходов.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходов.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	ДолевыеНачисления.КодДохода КАК КодДохода,
	|	ДолевыеНачисления.КатегорияДохода КАК КатегорияДохода,
	|	СтрокиДоходов.СуммаДохода КАК СуммаДохода,
	|	СтрокиДоходов.ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	СтрокиДоходов.КодВычета КАК КодВычета,
	|	СтрокиДоходов.СуммаВычета КАК СуммаВычета,
	|	СтрокиДоходов.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СтрокиДоходов.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СтрокиДоходов.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СтрокиДоходов.ДокументОснование КАК ДокументОснование,
	|	СтрокиДоходов.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	0 КАК ПризнакИсходнойЗаписи
	|ИЗ
	|	ВТСтрокиДоходов КАК СтрокиДоходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДолевыеНачисления КАК ДолевыеНачисления
	|		ПО СтрокиДоходов.Начисление = ДолевыеНачисления.Начисление
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтрокиДоходов.ФизическоеЛицо,
	|	СтрокиДоходов.ДатаПолученияДохода,
	|	СтрокиДоходов.КодДохода,
	|	СтрокиДоходов.КатегорияДохода,
	|	-СтрокиДоходов.СуммаДохода,
	|	СтрокиДоходов.ОбособленноеПодразделение,
	|	СтрокиДоходов.КодВычета,
	|	-СтрокиДоходов.СуммаВычета,
	|	СтрокиДоходов.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СтрокиДоходов.ИсточникДоходаЗаПределамиРФ,
	|	СтрокиДоходов.СтрокаРаздела2Расчета6НДФЛ,
	|	СтрокиДоходов.ДокументОснование,
	|	СтрокиДоходов.ПодразделениеСотрудника,
	|	1
	|ИЗ
	|	ВТСтрокиДоходов КАК СтрокиДоходов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическоеЛицо,
	|	ДатаПолученияДохода,
	|	ДокументОснование,
	|	КатегорияДохода,
	|	КодДохода,
	|	ПризнакИсходнойЗаписи
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета КАК КодВычета,
	|	СУММА(ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Сумма) КАК Сумма,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцПериодаПредоставленияВычета КАК МесяцПериодаПредоставленияВычета,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.НалоговаяБаза КАК НалоговаяБаза,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Регистратор КАК ДокументОснование
	|ИЗ
	|	РегистрНакопления.ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ КАК ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Регистратор = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Организация = &Организация
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.НалоговаяБаза = &НалоговаяБазаРКиСН
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода >= &НачалоПериода
	|	И ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода <= &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.КодВычета,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Подразделение,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ФизическоеЛицо,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.НалоговаяБаза,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцПериодаПредоставленияВычета,
	|	ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.Регистратор
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
	|	ИмущественныеВычетыНДФЛ.МесяцРегистрации,
	|	ИмущественныеВычетыНДФЛ.КодВычета,
	|	СУММА(ИмущественныеВычетыНДФЛ.Сумма),
	|	ИмущественныеВычетыНДФЛ.Подразделение,
	|	ЛОЖЬ,
	|	ДАТАВРЕМЯ(1, 1, 1),
	|	ИмущественныеВычетыНДФЛ.НалоговаяБаза,
	|	ИмущественныеВычетыНДФЛ.Регистратор
	|ИЗ
	|	РегистрНакопления.ИмущественныеВычетыНДФЛ КАК ИмущественныеВычетыНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО ИмущественныеВычетыНДФЛ.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И ИмущественныеВычетыНДФЛ.Регистратор = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	ИмущественныеВычетыНДФЛ.Организация = &Организация
	|	И ИмущественныеВычетыНДФЛ.НалоговаяБаза = &НалоговаяБазаРКиСН
	|	И ИмущественныеВычетыНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И ИмущественныеВычетыНДФЛ.МесяцРегистрации >= &НачалоПериода
	|	И ИмущественныеВычетыНДФЛ.МесяцРегистрации <= &КонецПериода
	|
	|СГРУППИРОВАТЬ ПО
	|	ИмущественныеВычетыНДФЛ.МесяцРегистрации,
	|	ИмущественныеВычетыНДФЛ.ФизическоеЛицо,
	|	ИмущественныеВычетыНДФЛ.Подразделение,
	|	ИмущественныеВычетыНДФЛ.НалоговаяБаза,
	|	ИмущественныеВычетыНДФЛ.КодВычета,
	|	ИмущественныеВычетыНДФЛ.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) КАК Сумма,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения) КАК СуммаСПревышения,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке18) КАК СуммаСПревышенияПоСтавке18,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке20) КАК СуммаСПревышенияПоСтавке20,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышенияПоСтавке22) КАК СуммаСПревышенияПоСтавке22,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = &КатегорияРКиСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль
	|
	|ИМЕЮЩИЕ
	|	(СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) <> 0
	|		ИЛИ СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения) <> 0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложения,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка13) КАК Ставка,
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) КАК Сумма,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода КАК КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления КАК СрокПеречисления,
	|	0 КАК СуммаВыплаченногоДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = &КатегорияРКиСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Сумма) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента,
	|	ЗНАЧЕНИЕ(Перечисление.НДФЛСтавки.Ставка15),
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения),
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления,
	|	0,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование
	|ИЗ
	|	РегистрНакопления.РасчетыНалогоплательщиковСБюджетомПоНДФЛ КАК РасчетыНалогоплательщиковСБюджетомПоНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо = ДокументыОснования.ФизическоеЛицо
	|			И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование = ДокументыОснования.ДокументОснование
	|ГДЕ
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Организация = &Организация
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода >= &НачалоПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонецПериода
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента = ЗНАЧЕНИЕ(Перечисление.НДФЛСтавкиНалогообложенияРезидента.Ставка13)
	|	И РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода = &КатегорияРКиСН
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Подразделение,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ФизическоеЛицо,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ДокументОснование,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КатегорияДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СрокПеречисления,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.КодДохода,
	|	РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СтавкаНалогообложенияРезидента
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыНалогоплательщиковСБюджетомПоНДФЛ.СуммаСПревышения) > 0";
	Запрос.УстановитьПараметр("НачисленияОплатыДолейРКиСН",НачисленияОплатыДолейРКиСН);
	Запрос.УстановитьПараметр("ДолевыеНачисления",РеквизитыДольныхНачислений);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Если Объект.ПрименяетсяАУСН Тогда
		Запрос.УстановитьПараметр("КатегорияРКиСН", Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавкиАУСН);
	Иначе
		Запрос.УстановитьПараметр("КатегорияРКиСН", Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавки);
	КонецЕсли;
	Запрос.УстановитьПараметр("НалоговаяБазаРКиСН", Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавки);
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.УстановитьПараметр("КонецПериода",КонецПериода);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"СведенияОДоходахНДФЛ.Период <= &КонецПериода","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ИмущественныеВычетыНДФЛ.МесяцРегистрации <= &КонецПериода","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПредоставленныеСтандартныеИСоциальныеВычетыНДФЛ.МесяцНалоговогоПериода <= &КонецПериода","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"РасчетыНалогоплательщиковСБюджетомПоНДФЛ.Период <= &КонецПериода","Истина");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"РасчетыНалогоплательщиковСБюджетомПоНДФЛ.МесяцНалоговогоПериода <= &КонецПериода","Истина");
	КонецЕсли;
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица",ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	ИначеЕсли Объект.ФизическиеЛица.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица",Объект.ФизическиеЛица.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо"));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)","Истина");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	ДокументыОснования = РезультатыЗапросов[4].Выгрузить();
	СведенияОДоходах = РезультатыЗапросов[7].Выгрузить();
	ПредоставленныеВычеты = РезультатыЗапросов[8].Выгрузить();
	НДФЛИсчисленный = РезультатыЗапросов[9].Выгрузить();
	НДФЛУдержанный = РезультатыЗапросов[10].Выгрузить();
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ОчиститьДанныеПоФизическомуЛицу(ФизическоеЛицо);
		Для Каждого СтрокаТаблицы Из ДокументыОснования Цикл
			НоваяСтрока = Объект.ДокументыОснования.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
		КонецЦикла;
		Для Каждого СтрокаТаблицы Из СведенияОДоходах Цикл
			НоваяСтрока = Объект.СведенияОДоходах.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
		КонецЦикла;
		Для Каждого СтрокаТаблицы Из ПредоставленныеВычеты Цикл
			НоваяСтрока = Объект.ПредоставленныеВычеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
		КонецЦикла;
		Для Каждого СтрокаТаблицы Из НДФЛИсчисленный Цикл
			НоваяСтрока = Объект.НДФЛИсчисленныйПоСтавке13.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
		КонецЦикла;
		Для Каждого СтрокаТаблицы Из НДФЛУдержанный Цикл
			НоваяСтрока = Объект.НДФЛУдержанный.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
		КонецЦикла;
		ОбработатьДанныеПослеЗагрузки(ФизическоеЛицо);
	Иначе
		Если Объект.ФизическиеЛица.Количество() = 0 Тогда
			Объект.ФизическиеЛица.Загрузить(РезультатыЗапросов[5].Выгрузить());
		КонецЕсли;
		Объект.ДокументыОснования.Загрузить(ДокументыОснования);
		Объект.СведенияОДоходах.Загрузить(СведенияОДоходах);
		Объект.ПредоставленныеВычеты.Загрузить(ПредоставленныеВычеты);
		Объект.НДФЛИсчисленныйПоСтавке13.Загрузить(НДФЛИсчисленный);
		Объект.НДФЛУдержанный.Загрузить(НДФЛУдержанный);
		ОбработатьДанныеПослеЗагрузки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьДанныеПослеЗагрузки(ФизическоеЛицо = Неопределено)
	
	Если Объект.ПрименяетсяАУСН Тогда
		КатегорияРКиСН = Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавкиАУСН;
	Иначе
		КатегорияРКиСН = Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ОбработатьДанныеПослеЗагрузкиПоФизическомуЛицу(ФизическоеЛицо,КатегорияРКиСН);
	Иначе
		Для Каждого СтрокаФизическоеЛицо Из Объект.ФизическиеЛица Цикл
			ОбработатьДанныеПослеЗагрузкиПоФизическомуЛицу(СтрокаФизическоеЛицо.ФизическоеЛицо,КатегорияРКиСН);
		КонецЦикла;
	КонецЕсли;
	
	Объект.ПредоставленныеВычеты.Сортировать("ФизическоеЛицо,ДокументОснование,КодВычета,ПризнакИсходнойЗаписи");
	Объект.НДФЛИсчисленныйПоСтавке13.Сортировать("ФизическоеЛицо,ДокументОснование,КатегорияДохода,ПризнакИсходнойЗаписи");
	Объект.НДФЛУдержанный.Сортировать("ФизическоеЛицо,ДокументОснование,КатегорияДохода,Ставка,ПризнакИсходнойЗаписи");
	Объект.ФизическиеЛица.Сортировать("ФизическоеЛицо");
	Объект.ДокументыОснования.Сортировать("ФизическоеЛицо,ДокументОснование");

КонецПроцедуры

&НаСервере
Процедура ОбработатьДанныеПослеЗагрузкиПоФизическомуЛицу(ФизическоеЛицо,КатегорияРКиСН)
	
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	ДанныеПоДоходам = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора);
	
	Если ДанныеПоДоходам.Количество() = 0 Тогда
		ОчиститьДанныеПоФизическомуЛицу(ФизическоеЛицо);
		Возврат;
	КонецЕсли;
	
	ДанныеПоДокументамОснованиям = Объект.ДокументыОснования.НайтиСтроки(ПараметрыОтбора);
	ПараметрыОтбора.Вставить("ДокументОснование");
	
	// Существует два алгоритма обработки данных:
	// 1. В документе доход по РК и СН только по долевым начислениям.
	// Тогда переносим доход, вычеты, налог "как есть" с плюсом и минусом по категориям/налоговым базам.
	// 2. В документе был рассчитан доход по РК и СН не только по долевым начислениям.
	// (например, мат. помощь к отпуску или рассчитана зарплата в отпуске)
	// 2.1. Определяем сумму дохода, которая подлежит пересмотру. 
	// Для этого по документу-основанию получаем сумму дохода по долевым начислениям в среднем заработке.
	// 2.2. Определяем сумму вычетов, которую нужно перенести с базы РКСН на основную базу.
	// Для этого сравниваем сумму фактически предоставленных в документе-основании по базе РКСН вычетов с суммой дохода,
	//	которая подлежит пересмотру (п. 2.1) - и берем меньшую из них. 
	// 2.3. Определяем налоговую базу, которая подлежит пересмотру. Для этого доход из п. 2.1 уменьшаем на вычеты из п. 2.2.
	// 2.4. Определяем исчисленный налог, подлежащий пересмотру.
	// a) Если ступень одна (13% или 15%), то сумму налоговой базы из п. 2.3 умножаем на ставку РКСН (13% или 15%) - 
	// полученный исчисленный налог переносим с базы РКСН на основную базу.
	// b) Если в документе-основании произошел переход с 13% на 15%, то:
	// 	- если фактически исчисленный в документе налог по ставке 15% оказался больше, чем <Доход из п. 2.1> * 15%, 
	// 	то исчисленный налог по ставке 15% переносим в размере <Доход из п. 2.1> * 15%
	// 	- иначе:
	// 	по ставке 15% переносим всю сумму фактически исчисленного в документе налога по ставке 15% 
	// 	по ставке 13% переносим исчисленный налог в размере (<Доход из п. 2.1> - (<налог по ставке 15%>*100/15)) * 13%
	// Важно! При переносе исчисленного налога ставка не меняется.
	
	Для Каждого СтрокаОснование Из ДанныеПоДокументамОснованиям Цикл
		
		ПараметрыОтбора.ДокументОснование = СтрокаОснование.ДокументОснование;
		ДанныеПоДоходам = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора);
		ДоходыПоРКиСН = 0;
		КатегорияДохода = Неопределено;
		Для Каждого СтрокаДоход Из ДанныеПоДоходам Цикл
			Если СтрокаДоход.КатегорияДохода = КатегорияРКиСН Тогда
				ДоходыПоРКиСН = ДоходыПоРКиСН - СтрокаДоход.СуммаДохода;
			Иначе
				КатегорияДохода = СтрокаДоход.КатегорияДохода;
			КонецЕсли;
		КонецЦикла;
		
		// Вычеты
		ДанныеПоВычетам = Объект.ПредоставленныеВычеты.НайтиСтроки(ПараметрыОтбора);
		Если ДанныеПоВычетам.Количество() > 0 Тогда
			// Данные по вычетам могут быть скорректированы не более чем на сумму корректировки дохода
			СтрокиКУдалению = Новый Массив;
			Для Каждого СтрокаТаблицы Из ДанныеПоВычетам Цикл
				Если СтрокаТаблицы.Сумма > 0 И ДоходыПоРКиСН > 0 Тогда
					СуммаСписания = Мин(СтрокаТаблицы.Сумма, ДоходыПоРКиСН);
					СтрокаТаблицы.Сумма = -СуммаСписания;
					ДоходыПоРКиСН = ДоходыПоРКиСН - СуммаСписания;
					
					НоваяСтрока = Объект.ПредоставленныеВычеты.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы,,"Сумма,НалоговаяБаза");
					НоваяСтрока.Сумма = -СтрокаТаблицы.Сумма;
					НоваяСтрока.НалоговаяБаза = Перечисления.КатегорииДоходовНДФЛ.ОплатаТруда;
					СтрокаТаблицы.ПризнакИсходнойЗаписи = Истина;
				Иначе
					СтрокиКУдалению.Добавить(СтрокаТаблицы);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
				Объект.ПредоставленныеВычеты.Удалить(УдаляемаяСтрока);
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаОснование.ЕстьДругиеДоходыПоРКиСН Тогда
			
			Если ДоходыПоРКиСН = 0 Тогда
				// К "долевому" доходу целиком применен вычет. Т.к. мы его перенесли на предыдущем шаге,
				// данные по налогам очищаем
				
				СтрокиКУдалению = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
				Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
					Объект.НДФЛИсчисленныйПоСтавке13.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
				СтрокиКУдалению = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
				Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
					Объект.НДФЛУдержанный.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
			Иначе
				// Исчисленный налог
				СтрокиКУдалению = Новый Массив;
				НалогКПереносуПо15 = 0;
				НалогКПереносуПо13 = 0;
				
				ДоходыПоРКиСНИсчисленныйНалог = ДоходыПоРКиСН;
				ДанныеПоИсчисленному = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
				Для Каждого СтрокаТаблицы Из ДанныеПоИсчисленному Цикл
					Если ДоходыПоРКиСНИсчисленныйНалог <= 0 Тогда
						СтрокиКУдалению.Добавить(СтрокаТаблицы);
						Продолжить;
					КонецЕсли;
					Если СтрокаТаблицы.СуммаСПревышения > 0 Тогда
						Если СтрокаТаблицы.Сумма <= 0 Тогда
							// Налог только по 15%
							НалогПоДоходу = Окр(ДоходыПоРКиСНИсчисленныйНалог * 0.15);
							СтрокаТаблицы.СуммаСПревышения = НалогПоДоходу;
							НалогКПереносуПо15 = НалогПоДоходу;
							СтрокаТаблицы.Сумма = 0;
						Иначе 
							// Есть налог по 15% и по 13%
							НалогПоДоходу = Окр(ДоходыПоРКиСНИсчисленныйНалог * 0.15);
							Если СтрокаТаблицы.СуммаСПревышения > НалогПоДоходу Тогда
								СтрокаТаблицы.Сумма = 0;
								СтрокаТаблицы.СуммаСПревышения = НалогПоДоходу;
								НалогКПереносуПо15 = НалогПоДоходу;
							Иначе
								НалогКПереносуПо15 = СтрокаТаблицы.СуммаСПревышения;
								НалогПоДоходу = Окр((ДоходыПоРКиСНИсчисленныйНалог-СтрокаТаблицы.СуммаСПревышения/0.15) * 0.13);
								СтрокаТаблицы.Сумма = НалогПоДоходу;
								НалогКПереносуПо13 = НалогПоДоходу;
							КонецЕсли;
						КонецЕсли;
					Иначе
						// Есть налог только по 13%
						СтрокаТаблицы.СуммаСПревышения = 0;
						НалогПоДоходу = Окр(ДоходыПоРКиСНИсчисленныйНалог * 0.13);
						СтрокаТаблицы.Сумма = НалогПоДоходу;
						НалогКПереносуПо13 = НалогПоДоходу; 
					КонецЕсли;
					
					ДоходыПоРКиСНИсчисленныйНалог = 0;
					НоваяСтрока = Объект.НДФЛИсчисленныйПоСтавке13.Добавить();
					
					СтрокаТаблицы.Сумма = -СтрокаТаблицы.Сумма;
					СтрокаТаблицы.СуммаСПревышения = -СтрокаТаблицы.СуммаСПревышения;
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы,,"Сумма,СуммаСПревышения,КатегорияДохода");
					НоваяСтрока.Сумма = -СтрокаТаблицы.Сумма;
					НоваяСтрока.СуммаСПревышения = -СтрокаТаблицы.СуммаСПревышения;
					НоваяСтрока.КатегорияДохода = КатегорияДохода;
					СтрокаТаблицы.ПризнакИсходнойЗаписи = Истина;
				КонецЦикла;
				Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
					Объект.НДФЛИсчисленныйПоСтавке13.Удалить(УдаляемаяСтрока);
				КонецЦикла;
				
				// Удержанный налог
				СтрокиКУдалению = Новый Массив;
				
				ДанныеПоУдержанному = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
				Для Каждого СтрокаТаблицы Из ДанныеПоУдержанному Цикл
					Если СтрокаТаблицы.Ставка = Перечисления.НДФЛСтавки.Ставка13 Тогда
						Если НалогКПереносуПо13 = 0 Тогда
							СтрокиКУдалению.Добавить(СтрокаТаблицы);
							Продолжить;
						Иначе
							Если СтрокаТаблицы.Сумма >= НалогКПереносуПо13 Тогда
								СтрокаТаблицы.Сумма = НалогКПереносуПо13;
								НалогКПереносуПо13 = 0;
							Иначе
								НалогКПереносуПо13 = НалогКПереносуПо13 - СтрокаТаблицы.Сумма;
							КонецЕсли;
						КонецЕсли;
					ИначеЕсли СтрокаТаблицы.Ставка = Перечисления.НДФЛСтавки.Ставка15 Тогда
						Если НалогКПереносуПо15 = 0 Тогда
							СтрокиКУдалению.Добавить(СтрокаТаблицы);
							Продолжить;
						Иначе
							Если СтрокаТаблицы.Сумма >= НалогКПереносуПо15 Тогда
								СтрокаТаблицы.Сумма = НалогКПереносуПо15;
								НалогКПереносуПо15 = 0;
							Иначе
								НалогКПереносуПо15 = НалогКПереносуПо15 - СтрокаТаблицы.Сумма;
							КонецЕсли;
						КонецЕсли;
					Иначе
						СтрокиКУдалению.Добавить(СтрокаТаблицы);
						Продолжить;
					КонецЕсли;
					
					НоваяСтрока = Объект.НДФЛУдержанный.Добавить();
					СтрокаТаблицы.Сумма = -СтрокаТаблицы.Сумма;
					
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы,,"Сумма,КатегорияДохода");
					НоваяСтрока.Сумма = -СтрокаТаблицы.Сумма;
					НоваяСтрока.КатегорияДохода = КатегорияДохода;
					СтрокаТаблицы.ПризнакИсходнойЗаписи = Истина;
				КонецЦикла; 
				
				Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
					Объект.НДФЛУдержанный.Удалить(УдаляемаяСтрока);
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			// Исчисленный налог
			ДанныеПоИсчисленному = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
			Для Каждого СтрокаТаблицы Из ДанныеПоИсчисленному Цикл
				НоваяСтрока = Объект.НДФЛИсчисленныйПоСтавке13.Добавить();
				СтрокаТаблицы.Сумма = -СтрокаТаблицы.Сумма;
				СтрокаТаблицы.СуммаСПревышения = -СтрокаТаблицы.СуммаСПревышения;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы,,"Сумма,СуммаСПревышения,КатегорияДохода");
				НоваяСтрока.Сумма = -СтрокаТаблицы.Сумма;
				НоваяСтрока.СуммаСПревышения = -СтрокаТаблицы.СуммаСПревышения;
				НоваяСтрока.КатегорияДохода = КатегорияДохода;
				СтрокаТаблицы.ПризнакИсходнойЗаписи = Истина;
			КонецЦикла;
			
			// Удержанный налог
			ДанныеПоУдержанному = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
			Для Каждого СтрокаТаблицы Из ДанныеПоУдержанному Цикл
				НоваяСтрока = Объект.НДФЛУдержанный.Добавить();
				СтрокаТаблицы.Сумма = -СтрокаТаблицы.Сумма;
				
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы,,"Сумма,КатегорияДохода");
				НоваяСтрока.Сумма = -СтрокаТаблицы.Сумма;
				НоваяСтрока.КатегорияДохода = КатегорияДохода;
				СтрокаТаблицы.ПризнакИсходнойЗаписи = Истина;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОчиститьДанныеПоФизическомуЛицу(ФизическоеЛицо)
	
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	
	СтрокиКУдалению = Объект.ДокументыОснования.НайтиСтроки(ПараметрыОтбора);
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		Объект.ДокументыОснования.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	СтрокиКУдалению = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора);
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		Объект.СведенияОДоходах.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	СтрокиКУдалению = Объект.ПредоставленныеВычеты.НайтиСтроки(ПараметрыОтбора);
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		Объект.ПредоставленныеВычеты.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	СтрокиКУдалению = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		Объект.НДФЛИсчисленныйПоСтавке13.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	СтрокиКУдалению = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		Объект.НДФЛУдержанный.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьФизическихЛицНаСервере()
	
	НачисленияОплатыДолейРКиСН = Объект.ДолевыеНачисления.Выгрузить(,"Начисление").ВыгрузитьКолонку("Начисление");
	НачалоПериода = Объект.Период.ДатаНачала;
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = '20250101';
		Объект.Период.ДатаНачала = НачалоПериода;
	КонецЕсли;
	КонецПериода = Объект.Период.ДатаОкончания;
	Организация = Объект.Организация;

	Запрос = Новый Запрос;
	Если Объект.ЗаполнятьВсемиФизическимиЛицами Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 0
		|	ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка) КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаКИсключению
		|;";
	Иначе
		ТекстЗапроса = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОперацияНалоговогоУчетаПоНДФЛ.Сотрудник КАК ФизическоеЛицо
		|ПОМЕСТИТЬ ВТФизическиеЛицаКИсключению
		|ИЗ
		|	Документ.ОперацияНалоговогоУчетаПоНДФЛ КАК ОперацияНалоговогоУчетаПоНДФЛ
		|ГДЕ
		|	ОперацияНалоговогоУчетаПоНДФЛ.ПометкаУдаления = ЛОЖЬ
		|	И ОперацияНалоговогоУчетаПоНДФЛ.Организация = &Организация
		|	И ПОДСТРОКА(ОперацияНалоговогоУчетаПоНДФЛ.Комментарий, 1, 53) = &ТекстКомментария
		|
		|СГРУППИРОВАТЬ ПО
		|	ОперацияНалоговогоУчетаПоНДФЛ.Сотрудник
		|;";
		Запрос.УстановитьПараметр("ТекстКомментария",НСтр("ru = 'Создан автоматически обработкой уточнения учета НДФЛ.';
															|en = 'Created automatically by the data processor for refining PIT accounting.'"));
	КонецЕсли;
		
	ТекстЗапроса = ТекстЗапроса + Символы.ПС +
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо
	|ПОМЕСТИТЬ ФизическиеЛица
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛВторичный КАК СтатусФизЛицНаНачалоГода
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = СтатусФизЛицНаНачалоГода.ФизическоеЛицо
	|			И (&НачалоПериода >= СтатусФизЛицНаНачалоГода.ДатаНачала)
	|			И (&НачалоПериода <= СтатусФизЛицНаНачалоГода.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТФизическиеЛицаКИсключению КАК ФизическиеЛицаКИсключению
	|		ПО (ФизическиеЛицаКИсключению.ФизическоеЛицо = СведенияОДоходахНДФЛ.ФизическоеЛицо)
	|ГДЕ
	|	ФизическиеЛицаКИсключению.ФизическоеЛицо ЕСТЬ NULL
	|	И СведенияОДоходахНДФЛ.Начисление В(&НачисленияОплатыДолейРКиСН)
	|	И СведенияОДоходахНДФЛ.Период >= &НачалоПериода
	|	И СведенияОДоходахНДФЛ.Период <= &КонецПериода
	|	И СведенияОДоходахНДФЛ.Организация = &Организация
	|	И ЕСТЬNULL(СтатусФизЛицНаНачалоГода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) В (ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент), ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.ГражданинСтраныЕАЭС))
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ФизическиеЛица.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ФизическиеЛица КАК ФизическиеЛица
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизическиеЛица.ФизическоеЛицо.ФИО";
	Запрос.УстановитьПараметр("НачисленияОплатыДолейРКиСН",НачисленияОплатыДолейРКиСН);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода",НачалоПериода);
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.УстановитьПараметр("КонецПериода",КонецПериода);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"СведенияОДоходахНДФЛ.Период <= &КонецПериода","Истина");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Объект.ФизическиеЛица.Загрузить(РезультатЗапроса[2].Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументыНаСервере()
	
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо,ОперацияУчета,ДокументОснование",);
	// Исключаем переобор строк по которой уже есть операция в рамках текущей обработки
	Комментарий = НСтр("ru = 'Создан автоматически обработкой уточнения учета НДФЛ.';
						|en = 'Created automatically by the data processor for refining PIT accounting.'");
	ОбработаноФизЛиц = 0;
	ПараметрыОтбора.ОперацияУчета = Документы.ОперацияНалоговогоУчетаПоНДФЛ.ПустаяСсылка();
	
	Для Каждого СтрокаФизическоеЛицо Из Объект.ФизическиеЛица Цикл
		
		ПараметрыОтбора.ФизическоеЛицо = СтрокаФизическоеЛицо.ФизическоеЛицо;
		ОбработаноФизЛицо = Ложь;
		
		ПараметрыОтбора.Удалить("ДокументОснование");
		ДанныеПоДокументамОснованиям = Объект.ДокументыОснования.НайтиСтроки(ПараметрыОтбора);
		ПараметрыОтбора.Вставить("ДокументОснование");
		
		Для Каждого СтрокаДокументОснование Из ДанныеПоДокументамОснованиям Цикл
			ПараметрыОтбора.ДокументОснование = СтрокаДокументОснование.ДокументОснование;
			СтрокиВычетовОтражены = Ложь;
			СозданДокументОперация = Ложь;
			
			СтрокиДоходов = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора);
			СтрокиВычетов = Объект.ПредоставленныеВычеты.НайтиСтроки(ПараметрыОтбора);
			СтрокиИсчисленный = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
			СтрокиУдержанный = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
						
			ДатыКОбработке = Новый Соответствие;
			Для Каждого СтрокаТаблицы Из СтрокиДоходов Цикл
				ДатыКОбработке.Вставить(СтрокаТаблицы.ДатаПолученияДохода,СтрокаТаблицы.ДатаПолученияДохода);
			КонецЦикла;
			Для Каждого СтрокаТаблицы Из СтрокиИсчисленный Цикл
				ДатыКОбработке.Вставить(СтрокаТаблицы.МесяцНалоговогоПериода,СтрокаТаблицы.МесяцНалоговогоПериода);
			КонецЦикла;
			Для Каждого СтрокаТаблицы Из СтрокиУдержанный Цикл
				ДатыКОбработке.Вставить(СтрокаТаблицы.МесяцНалоговогоПериода,СтрокаТаблицы.МесяцНалоговогоПериода);
			КонецЦикла;
			СписокДат = Новый СписокЗначений;
			Для Каждого ДатаКОбработке Из ДатыКОбработке Цикл
				СписокДат.Добавить(ДатаКОбработке.Ключ);
			КонецЦикла;
			СписокДат.СортироватьПоЗначению();
			
			Для Каждого ДатаКОбработке Из СписокДат Цикл
				ДатаОперации = ДатаКОбработке.Значение;
				
				ПараметрыОтбора.Вставить("ДатаПолученияДохода");
				ПараметрыОтбора.ДатаПолученияДохода = ДатаОперации;
				СтрокиДоходов = Объект.СведенияОДоходах.НайтиСтроки(ПараметрыОтбора);
				ПараметрыОтбора.Удалить("ДатаПолученияДохода");
				ПараметрыОтбора.Вставить("МесяцНалоговогоПериода");
				ПараметрыОтбора.МесяцНалоговогоПериода = ДатаОперации;
				СтрокиИсчисленный = Объект.НДФЛИсчисленныйПоСтавке13.НайтиСтроки(ПараметрыОтбора);
				СтрокиУдержанный = Объект.НДФЛУдержанный.НайтиСтроки(ПараметрыОтбора);
				ПараметрыОтбора.Удалить("МесяцНалоговогоПериода");
				
				ОперацияУчета = Документы.ОперацияНалоговогоУчетаПоНДФЛ.СоздатьДокумент();
				ОперацияУчета.Дата = ТекущаяДатаСеанса();
				ОперацияУчета.ДатаОперации = ДатаОперации;
				ОперацияУчета.Организация = Объект.Организация;
				ОперацияУчета.Сотрудник = СтрокаФизическоеЛицо.ФизическоеЛицо;
				ОперацияУчета.Ответственный = Объект.Ответственный;
				ОперацияУчета.Комментарий = Комментарий;
				
				Если Не СтрокиВычетовОтражены Тогда
					СтрокиВычетов = Объект.ПредоставленныеВычеты.НайтиСтроки(ПараметрыОтбора);
					Если СтрокиВычетов.Количество() > 0 Тогда
						Для Каждого СтрокаТаблицы Из СтрокиВычетов Цикл
							НоваяСтрока = ОперацияУчета.ПредоставленныеВычеты.Добавить();
							ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
						КонецЦикла;
						ОперацияУчета.Комментарий = ОперацияУчета.Комментарий + " " + НСтр("ru = 'Вычеты.';
																							|en = 'Deductions.'");
					КонецЕсли;
					СтрокиВычетовОтражены = Истина;
				КонецЕсли;
				
				Если СтрокиДоходов.Количество() > 0 Тогда
					ОперацияУчета.Комментарий = ОперацияУчета.Комментарий + " " + НСтр("ru = 'Доходы.';
																						|en = 'Income.'");
					Для Каждого СтрокаТаблицы Из СтрокиДоходов Цикл
						НоваяСтрока = ОперацияУчета.СведенияОДоходах.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
					КонецЦикла;
				КонецЕсли;
				
				Если СтрокиИсчисленный.Количество() > 0 Тогда
					ОперацияУчета.Комментарий = ОперацияУчета.Комментарий + " " + НСтр("ru = 'Исчисленный налог.';
																						|en = 'Calculated tax.'");
					Для Каждого СтрокаТаблицы Из СтрокиИсчисленный Цикл
						НоваяСтрока = ОперацияУчета.НДФЛИсчисленныйПоСтавке13.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
					КонецЦикла;
				КонецЕсли;
				
				Если СтрокиУдержанный.Количество() > 0 Тогда
					ОперацияУчета.Комментарий = ОперацияУчета.Комментарий + " " + НСтр("ru = 'Удержанный налог.';
																						|en = 'Deducted tax.'");;
					Для Каждого СтрокаТаблицы Из СтрокиУдержанный Цикл
						НоваяСтрока = ОперацияУчета.НДФЛУдержанный.Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
					КонецЦикла;
				КонецЕсли;
				
				Попытка
					ОперацияУчета.Записать(РежимЗаписиДокумента.Запись);
				Исключение
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Выполнение обработки прервано. Не удалось записать документ по физ. лицу: %1, по причине: %2.';
						|en = 'Processing is canceled. Cannot save the document for the person: %1, due to: %2.'"),
					СтрокаФизическоеЛицо.ФизическоеЛицо,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
					ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
					Возврат Неопределено;
				КонецПопытки;
				Для Каждого СтрокаТаблицы Из СтрокиДоходов Цикл
					СтрокаТаблицы.ОперацияУчета = ОперацияУчета.Ссылка;
				КонецЦикла;
				Для Каждого СтрокаТаблицы Из СтрокиВычетов Цикл
					СтрокаТаблицы.ОперацияУчета = ОперацияУчета.Ссылка;
				КонецЦикла;
				Для Каждого СтрокаТаблицы Из СтрокиИсчисленный Цикл
					СтрокаТаблицы.ОперацияУчета = ОперацияУчета.Ссылка;
				КонецЦикла;
				Для Каждого СтрокаТаблицы Из СтрокиУдержанный Цикл
					СтрокаТаблицы.ОперацияУчета = ОперацияУчета.Ссылка;
				КонецЦикла;
				ОбработаноФизЛицо = Истина;
				
			КонецЦикла;
		КонецЦикла;
		
		Если ОбработаноФизЛицо Тогда
			ОбработаноФизЛиц = ОбработаноФизЛиц + 1;
		КонецЕсли;
	КонецЦикла;
	Возврат ОбработаноФизЛиц;
	
КонецФункции

&НаСервере
Функция АдресСпискаПодобранныхФизическихЛиц()
	
	МассивФизическихЛиц = Объект.ФизическиеЛица.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо");
	Возврат ПоместитьВоВременноеХранилище(МассивФизическихЛиц, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура УстановитьОтборыСтрокПоФизическомуЛицу()
	
	ТекущиеДанные = Элементы.ФизическиеЛица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		СтруктураОтбора = Новый ФиксированнаяСтруктура("ФизическоеЛицо", Неопределено);
	Иначе
		СтруктураОтбора = Новый ФиксированнаяСтруктура("ФизическоеЛицо", ТекущиеДанные.ФизическоеЛицо);
	КонецЕсли;
	Элементы.СведенияОДоходахПоФизЛицу.ОтборСтрок			= СтруктураОтбора;
	Элементы.ПредоставленныеВычетыПоФизЛицу.ОтборСтрок		= СтруктураОтбора;
	Элементы.НДФЛИсчисленныйПоСтавке13ПоФизЛицу.ОтборСтрок	= СтруктураОтбора;
	Элементы.НДФЛУдержанныйПоФизЛицу.ОтборСтрок				= СтруктураОтбора;
	
КонецПроцедуры

&НаКлиенте
Функция ВыполнитьПроверкиПередЗаполнением()
	
	Если Объект.Организация.Пустая() Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Выберите организацию';
										|en = 'Select company'"));
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ДолевыеНачисления.Количество() = 0 Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Нет данных на вкладке ""Долевые начисления""';
										|en = 'No data on the ""Shared accruals"" tab'"));
		Возврат Истина;
	КонецЕсли;
	ЕстьНезаполненныеРеквизиты = Ложь;
	Для Каждого ДольноеНачисление Из Объект.ДолевыеНачисления Цикл
		Если ДольноеНачисление.КатегорияДохода.Пустая() Или ДольноеНачисление.КодДоходаНДФЛ.Пустая() Тогда
			ЕстьНезаполненныеРеквизиты = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьНезаполненныеРеквизиты Тогда
		ПоказатьПредупреждение( ,НСтр("ru = 'Все колонки с реквизитами НДФЛ на вкладке ""Долевые начисления"" должны быть заполнены';
										|en = 'All columns with PIT attributes on the ""Shared accruals"" tab must be populated'"));
		Возврат Истина;
	КонецЕсли;
	Возврат Ложь;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДокументамиИсправлениямиНаСервере(ФизическоеЛицо)
	
	РеквизитыДольныхНачислений = Объект.ДолевыеНачисления.Выгрузить();
	НачисленияОплатыДолейРКиСН = РеквизитыДольныхНачислений.ВыгрузитьКолонку("Начисление");
	НачисленияОплатыДолейРКиСН.Добавить(Неопределено); // Для учета записей, которые были добавлены через "Операция учета НДФЛ"
	НачалоПериода = Объект.Период.ДатаНачала;
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = '20250101';
		Объект.Период.ДатаНачала = НачалоПериода;
	КонецЕсли;
	КонецПериода = Объект.Период.ДатаОкончания;
	Организация = Объект.Организация;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК ОбщаяСуммаДоходаПоРКиСН,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаДохода,
	|	СведенияОДоходахНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	СведенияОДоходахНДФЛ.КодВычета КАК КодВычета,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВычета,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника КАК ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление
	|ПОМЕСТИТЬ ВТСтрокиДоходовПоВсемРКиСН
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусФизическихЛицКакНалогоплательщиковНДФЛВторичный КАК СтатусФизЛицНаНачалоГода
	|		ПО СведенияОДоходахНДФЛ.ФизическоеЛицо = СтатусФизЛицНаНачалоГода.ФизическоеЛицо
	|			И (&НачалоПериода >= СтатусФизЛицНаНачалоГода.ДатаНачала)
	|			И (&НачалоПериода <= СтатусФизЛицНаНачалоГода.ДатаОкончания)
	|ГДЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)
	|	И СведенияОДоходахНДФЛ.КатегорияДохода = &КатегорияРКиСН
	|	И СведенияОДоходахНДФЛ.Период >= &НачалоПериода
	|	И СведенияОДоходахНДФЛ.Период <= &КонецПериода
	|	И СведенияОДоходахНДФЛ.Организация = &Организация
	|	И НЕ(СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|				И СведенияОДоходахНДФЛ.ДокументОснование ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ)
	|	И ЕСТЬNULL(СтатусФизЛицНаНачалоГода.Статус, ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент)) В (ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.Резидент), ЗНАЧЕНИЕ(Справочник.СтатусыНалогоплательщиковПоНДФЛ.ГражданинСтраныЕАЭС))
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.Подразделение,
	|	СведенияОДоходахНДФЛ.ДокументОснование,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	СведенияОДоходахНДФЛ.КатегорияДохода,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.ПодразделениеСотрудника,
	|	СведенияОДоходахНДФЛ.КодВычета
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаДохода
	|			ИНАЧЕ 0
	|		КОНЕЦ) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование КАК ДокументОснование,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование.ИсправленныйДокумент КАК ИсправленныйДокумент,
	|	СУММА(СтрокиДоходовПоВсемРКиСН.ОбщаяСуммаДоходаПоРКиСН) <> СУММА(СтрокиДоходовПоВсемРКиСН.СуммаДохода) КАК ЕстьДругиеДоходыПоРКиСН
	|ПОМЕСТИТЬ ВТДокументыОснования
	|ИЗ
	|	ВТСтрокиДоходовПоВсемРКиСН КАК СтрокиДоходовПоВсемРКиСН
	|ГДЕ
	|	НЕ СтрокиДоходовПоВсемРКиСН.ДокументОснование.ИсправленныйДокумент В (&МассивПустыхСсылок)
	|
	|СГРУППИРОВАТЬ ПО
	|	СтрокиДоходовПоВсемРКиСН.ФизическоеЛицо,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование,
	|	СтрокиДоходовПоВсемРКиСН.ДокументОснование.ИсправленныйДокумент
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическоеЛицо,
	|	ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыОснования.ФизическоеЛицо КАК ФизическоеЛицо,
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.ЕстьДругиеДоходыПоРКиСН КАК ЕстьДругиеДоходыПоРКиСН,
	|	ДокументыОснования.ИсправленныйДокумент КАК ИсправленныйДокумент
	|ИЗ
	|	ВТДокументыОснования КАК ДокументыОснования
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументыОснования.ФизическоеЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументыОснования.ФизическоеЛицо КАК ФизическоеЛицо
	|ИЗ
	|	ВТДокументыОснования КАК ДокументыОснования
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДолевыеНачисления.Начисление КАК Начисление,
	|	ДолевыеНачисления.КодДоходаНДФЛ КАК КодДохода,
	|	ДолевыеНачисления.КатегорияДохода КАК КатегорияДохода
	|ПОМЕСТИТЬ ВТДолевыеНачисления
	|ИЗ
	|	&ДолевыеНачисления КАК ДолевыеНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо КАК ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода КАК ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.КодДохода КАК КодДохода,
	|	ЕСТЬNULL(ДолевыеНачисления.КодДохода, СведенияОДоходахНДФЛ.КодДохода) КАК КодДоходаНовый,
	|	ЕСТЬNULL(ДолевыеНачисления.КатегорияДохода, СведенияОДоходахНДФЛ.КатегорияДохода) КАК КатегорияДоходаНовый,
	|	СведенияОДоходахНДФЛ.КатегорияДохода КАК КатегорияДохода,
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК ОбщаяСуммаДоходаПоРКиСН,
	|	СУММА(СведенияОДоходахНДФЛ.СуммаДохода) КАК СуммаДохода,
	|	СведенияОДоходахНДФЛ.Подразделение КАК ОбособленноеПодразделение,
	|	СведенияОДоходахНДФЛ.КодВычета КАК КодВычета,
	|	СУММА(ВЫБОР
	|			КОГДА СведенияОДоходахНДФЛ.Начисление В (&НачисленияОплатыДолейРКиСН)
	|				ТОГДА СведенияОДоходахНДФЛ.СуммаВычета
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаВычета,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль КАК ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ КАК ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ КАК СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.ДокументОснование КАК ИсходныйДокумент,
	|	ДокументыОснования.ИсправленныйДокумент КАК ИсправленныйДокумент,
	|	СведенияОДоходахНДФЛ.Начисление КАК Начисление,
	|	ВЫБОР
	|		КОГДА ДокументыОснования.ИсправленныйДокумент = СведенияОДоходахНДФЛ.ДокументОснование
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоИсправленныйДокумент
	|ИЗ
	|	РегистрНакопления.СведенияОДоходахНДФЛ КАК СведенияОДоходахНДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДокументыОснования КАК ДокументыОснования
	|		ПО (ДокументыОснования.ФизическоеЛицо = СведенияОДоходахНДФЛ.ФизическоеЛицо)
	|			И (ДокументыОснования.ДокументОснование = СведенияОДоходахНДФЛ.ДокументОснование
	|				ИЛИ ДокументыОснования.ИсправленныйДокумент = СведенияОДоходахНДФЛ.ДокументОснование)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДолевыеНачисления КАК ДолевыеНачисления
	|		ПО СведенияОДоходахНДФЛ.Начисление = ДолевыеНачисления.Начисление
	|ГДЕ
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)
	|	И СведенияОДоходахНДФЛ.Период >= &НачалоПериода
	|	И СведенияОДоходахНДФЛ.Период <= &КонецПериода
	|	И СведенияОДоходахНДФЛ.Организация = &Организация
	|	И НЕ(СведенияОДоходахНДФЛ.Регистратор ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ
	|				И СведенияОДоходахНДФЛ.ДокументОснование ССЫЛКА Документ.ОперацияНалоговогоУчетаПоНДФЛ)
	|
	|СГРУППИРОВАТЬ ПО
	|	СведенияОДоходахНДФЛ.Подразделение,
	|	СведенияОДоходахНДФЛ.ДокументОснование,
	|	СведенияОДоходахНДФЛ.КодДохода,
	|	СведенияОДоходахНДФЛ.ФизическоеЛицо,
	|	СведенияОДоходахНДФЛ.КатегорияДохода,
	|	СведенияОДоходахНДФЛ.СтрокаРаздела2Расчета6НДФЛ,
	|	СведенияОДоходахНДФЛ.ДатаПолученияДохода,
	|	СведенияОДоходахНДФЛ.ВключатьВДекларациюПоНалогуНаПрибыль,
	|	СведенияОДоходахНДФЛ.ИсточникДоходаЗаПределамиРФ,
	|	СведенияОДоходахНДФЛ.КодВычета,
	|	ДокументыОснования.ИсправленныйДокумент,
	|	ДокументыОснования.ДокументОснование,
	|	СведенияОДоходахНДФЛ.Начисление,
	|	ЕСТЬNULL(ДолевыеНачисления.КодДохода, СведенияОДоходахНДФЛ.КодДохода),
	|	ЕСТЬNULL(ДолевыеНачисления.КатегорияДохода, СведенияОДоходахНДФЛ.КатегорияДохода)";
	Запрос.УстановитьПараметр("НачисленияОплатыДолейРКиСН",НачисленияОплатыДолейРКиСН);
	Запрос.УстановитьПараметр("ДолевыеНачисления",РеквизитыДольныхНачислений);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Если Объект.ПрименяетсяАУСН Тогда
		Запрос.УстановитьПараметр("КатегорияРКиСН", Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавкиАУСН);
	Иначе
		Запрос.УстановитьПараметр("КатегорияРКиСН", Перечисления.КатегорииДоходовНДФЛ.РайонныеСеверныеНадбавки);
	КонецЕсли;
	
	МассивПустыхСсылок = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыРасширеннаяПодсистемы") Тогда
		МодульЗарплатаКадрыРасширенныйПовтИсп = ОбщегоНазначения.ОбщийМодуль("ЗарплатаКадрыРасширенныйПовтИсп");
		МассивПустыхСсылок = МодульЗарплатаКадрыРасширенныйПовтИсп.МассивПустыхСсылокСторнируемогоДокумента();
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивПустыхСсылок",МассивПустыхСсылок);
	
	Если ЗначениеЗаполнено(КонецПериода) Тогда
		Запрос.УстановитьПараметр("КонецПериода",КонецПериода);
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"СведенияОДоходахНДФЛ.Период <= &КонецПериода","Истина");;
	КонецЕсли;
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица",ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
	ИначеЕсли Объект.ФизическиеЛица.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("ФизическиеЛица",Объект.ФизическиеЛица.Выгрузить(,"ФизическоеЛицо").ВыгрузитьКолонку("ФизическоеЛицо"));
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"СведенияОДоходахНДФЛ.ФизическоеЛицо В(&ФизическиеЛица)","Истина");
	КонецЕсли;
	Запрос.Текст = ТекстЗапроса;
	РезультатыЗапросов = Запрос.ВыполнитьПакет();
	ДокументыИсправления = РезультатыЗапросов[2].Выгрузить();
	Если Объект.ФизическиеЛица.Количество() = 0 Тогда
		Объект.ФизическиеЛица.Загрузить(РезультатыЗапросов[3].Выгрузить());
	КонецЕсли;
	СведенияОДоходахИсправления = РезультатыЗапросов[5].Выгрузить();
	Объект.ДокументыИсправления.Загрузить(ДокументыИсправления);
	Объект.СведенияОДоходахИсправления.Загрузить(СведенияОДоходахИсправления);
	ОбработатьИсправленияПослеЗагрузки();
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИсправленияПослеЗагрузки(ФизическоеЛицо = Неопределено)
	
	ТаблицаРезультата = Объект.СведенияОДоходахКорректировкаИсправлений.Выгрузить();
	ТаблицаРезультата.Очистить();
	Если ЗначениеЗаполнено(ФизическоеЛицо) Тогда
		ОбработатьИсправленияПослеЗагрузкиПоФизическомуЛицу(ФизическоеЛицо,ТаблицаРезультата);
	Иначе
		Объект.СведенияОДоходахКорректировкаИсправлений.Очистить();
		Для Каждого СтрокаФизическоеЛицо Из Объект.ФизическиеЛица Цикл
			ОбработатьИсправленияПослеЗагрузкиПоФизическомуЛицу(СтрокаФизическоеЛицо.ФизическоеЛицо,ТаблицаРезультата);
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаРезультата.Свернуть("ДатаПолученияДохода, КодДохода, КатегорияДохода, ОбособленноеПодразделение, КодВычета, 
			|ВключатьВДекларациюПоНалогуНаПрибыль,ИсточникДоходаЗаПределамиРФ,НалогНаПрибыльДляДивидендов,СтрокаРаздела2Расчета6НДФЛ,
			|ДокументОснование,ПодразделениеСотрудника,ФизическоеЛицо,ИсходныйДокумент,ИсправленныйДокумент"
			,"СуммаДохода, Количество, СуммаВычета");

	СтрокиКУдалению = Новый Массив;
	Для Каждого СтрокаТаблицы Из ТаблицаРезультата Цикл
		Если СтрокаТаблицы.СуммаДохода = 0 Тогда
			СтрокиКУдалению.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из СтрокиКУдалению Цикл
		ТаблицаРезультата.Удалить(УдаляемаяСтрока);
	КонецЦикла;
			
	Объект.СведенияОДоходахИсправления.Сортировать("ФизическоеЛицо, ДокументОснование, ЭтоИсправленныйДокумент");
	Объект.СведенияОДоходахКорректировкаИсправлений.Загрузить(ТаблицаРезультата);
	Объект.СведенияОДоходахКорректировкаИсправлений.Сортировать("ФизическоеЛицо, ДокументОснование, ЭтоИсправленныйДокумент");
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьИсправленияПослеЗагрузкиПоФизическомуЛицу(ФизическоеЛицо,ТаблицаРезультата)
	
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо", ФизическоеЛицо);
	
	ДанныеПоДокументамОснованиям = Объект.ДокументыИсправления.НайтиСтроки(ПараметрыОтбора);
	ПараметрыОтбора.Вставить("ЭтоИсправленныйДокумент");
	ПараметрыОтбора.Вставить("ИсходныйДокумент");
	ПараметрыОтбора.Вставить("ИсправленныйДокумент");
	
	Для Каждого СтрокаОснование Из ДанныеПоДокументамОснованиям Цикл
		
		КодДоходаЗамена = Новый Соответствие;
		ПараметрыОтбора.ИсходныйДокумент		= СтрокаОснование.ДокументОснование;
		ПараметрыОтбора.ИсправленныйДокумент	= СтрокаОснование.ИсправленныйДокумент;
		ПараметрыОтбора.ЭтоИсправленныйДокумент = Ложь;
		
		// Сторнируем движение документа-исправления и накапливаем сумму дохода по коду дохода
		ДанныеПоДоходамИсправление = Объект.СведенияОДоходахИсправления.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого СтрокаТаблицы Из ДанныеПоДоходамИсправление Цикл
			Если СтрокаТаблицы.КодДоходаНовый <> СтрокаТаблицы.КодДохода Тогда
				КодДоходаЗамена.Вставить(СтрокаТаблицы.КодДохода,СтрокаТаблицы.КодДоходаНовый);
			КонецЕсли;
		КонецЦикла;
		
		Если КодДоходаЗамена.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СуммыЗаменНаДаты = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из ДанныеПоДоходамИсправление Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Начисление) И КодДоходаЗамена.Получить(СтрокаТаблицы.КодДохода) <> Неопределено 
				Или СтрокаТаблицы.КодДоходаНовый <> СтрокаТаблицы.КодДохода Тогда
				
				ДатаПолученияДохода = СтрокаТаблицы.ДатаПолученияДохода;
				Если СуммыЗаменНаДаты.Получить(ДатаПолученияДохода) = Неопределено Тогда
					СуммыЗаменНаДаты.Вставить(ДатаПолученияДохода,СтрокаТаблицы.СуммаДохода);
				Иначе
					СуммыЗаменНаДаты[ДатаПолученияДохода] = СуммыЗаменНаДаты[ДатаПолученияДохода] + СтрокаТаблицы.СуммаДохода;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ЕстьСуммаЗамены = Ложь;
		Для Каждого СуммаЗамены Из СуммыЗаменНаДаты Цикл
			Если СуммаЗамены.Значение <> 0 Тогда
				ЕстьСуммаЗамены = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если Не ЕстьСуммаЗамены Тогда
			Продолжить;
		КонецЕсли;
		
		КодСуммаДоходаИсходный = Новый Соответствие;
		Для Каждого СтрокаТаблицы Из ДанныеПоДоходамИсправление Цикл
			НоваяСтрока = ТаблицаРезультата.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
			КодДохода = СтрокаТаблицы.КодДоходаНовый;
			
			Если КодСуммаДоходаИсходный.Получить(КодДохода) = Неопределено Тогда
				КодСуммаДоходаИсходный.Вставить(КодДохода,СтрокаТаблицы.СуммаДохода);
			Иначе
				КодСуммаДоходаИсходный[КодДохода] = КодСуммаДоходаИсходный[КодДохода] + СтрокаТаблицы.СуммаДохода;
			КонецЕсли;
			Если КодСуммаДоходаИсходный.Получить(СтрокаТаблицы.КодДохода) = Неопределено Тогда
				КодСуммаДоходаИсходный.Вставить(СтрокаТаблицы.КодДохода,0);
			КонецЕсли;
			НоваяСтрока.СуммаДохода = -НоваяСтрока.СуммаДохода;
			НоваяСтрока.СуммаВычета = -НоваяСтрока.СуммаВычета;
		КонецЦикла;
		
		ПараметрыОтбора.ЭтоИсправленныйДокумент = Истина;
		ПараметрыОтбора.Вставить("КодДоходаНовый");
		
		// Алгоритм:
		// - доход по "основному" начислению в размере, равным по модулю сторно "долевого" начисления, 
		//   в учете НДФЛ записываем на дату фактического получения дохода "долевого" начисления, которое сторнируется;
		// - остаток дохода по "основному" начислению относим на планируемую дату выплаты из документа.
		
		Для Каждого КодСуммаДохода Из КодСуммаДоходаИсходный Цикл
			КодДохода = КодСуммаДохода.Ключ;
			СуммаДохода = КодСуммаДохода.Значение;
			
			Для Каждого СтрокаТаблицы Из ДанныеПоДоходамИсправление Цикл
				
				Если СтрокаТаблицы.КодДоходаНовый = КодДохода Тогда
					НоваяСтрока = ТаблицаРезультата.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
					
					НоваяСтрока.КодДохода = СтрокаТаблицы.КодДоходаНовый;
					НоваяСтрока.КатегорияДохода = СтрокаТаблицы.КатегорияДоходаНовый;
					Если СуммаДохода <= 0 Тогда
						ПараметрыОтбора.КодДоходаНовый = КодДохода;
						ДанныеПоДоходамИсходный = Объект.СведенияОДоходахИсправления.НайтиСтроки(ПараметрыОтбора); 
						Если ДанныеПоДоходамИсходный.Количество() > 0 Тогда
							НоваяСтрока.ДатаПолученияДохода = ДанныеПоДоходамИсходный[0].ДатаПолученияДохода;
						КонецЕсли;
					Иначе
						ДатаПолученияДохода = Неопределено;
						Для Каждого СтрокаТаблицы Из ДанныеПоДоходамИсправление Цикл
							Если СтрокаТаблицы.КодДохода = КодДохода Тогда
								ДатаПолученияДохода = СтрокаТаблицы.ДатаПолученияДохода;
							КонецЕсли;
							Если ЗначениеЗаполнено(ДатаПолученияДохода) Тогда
								НоваяСтрока.ДатаПолученияДохода = ДатаПолученияДохода;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументыПоИсправлениямНаСервере()
	
	ПараметрыОтбора = Новый Структура("ФизическоеЛицо,ОперацияУчета,ДокументОснование",);
	// Исключаем переобор строк по которой уже есть операция в рамках текущей обработки
	Комментарий = НСтр("ru = 'Создан автоматически обработкой уточнения учета НДФЛ.';
						|en = 'Created automatically by the data processor for refining PIT accounting.'");
	ОбработаноДокументов = 0;
	ПараметрыОтбора.ОперацияУчета = Документы.ОперацияНалоговогоУчетаПоНДФЛ.ПустаяСсылка();
	
	Для Каждого СтрокаФизическоеЛицо Из Объект.ФизическиеЛица Цикл
		
		ПараметрыОтбора.ФизическоеЛицо = СтрокаФизическоеЛицо.ФизическоеЛицо;
		
		ПараметрыОтбора.Удалить("ДокументОснование");
		ДанныеПоДокументамОснованиям = Объект.ДокументыИсправления.НайтиСтроки(ПараметрыОтбора);
		ПараметрыОтбора.Вставить("ДокументОснование");
		
		Для Каждого СтрокаДокументОснование Из ДанныеПоДокументамОснованиям Цикл
			
			ПараметрыОтбора.ДокументОснование = СтрокаДокументОснование.ДокументОснование;
			СтрокиДоходов = Объект.СведенияОДоходахКорректировкаИсправлений.НайтиСтроки(ПараметрыОтбора);
			
			Если СтрокиДоходов.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			ДатаОперации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаДокументОснование.ДокументОснование,"ПланируемаяДатаВыплаты");
			
			ОперацияУчета = Документы.ОперацияНалоговогоУчетаПоНДФЛ.СоздатьДокумент();
			ОперацияУчета.Дата = ТекущаяДатаСеанса();
			ОперацияУчета.ДатаОперации = ДатаОперации;
			ОперацияУчета.Организация = Объект.Организация;
			ОперацияУчета.Сотрудник = СтрокаФизическоеЛицо.ФизическоеЛицо;
			ОперацияУчета.Ответственный = Объект.Ответственный;
			ОперацияУчета.Комментарий = Комментарий;
			
			ОперацияУчета.Комментарий = ОперацияУчета.Комментарий + " " + НСтр("ru = 'Доходы по документам-исправлениям.';
																				|en = 'Income by correction documents.'");
			Для Каждого СтрокаТаблицы Из СтрокиДоходов Цикл
				НоваяСтрока = ОперацияУчета.СведенияОДоходах.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока,СтрокаТаблицы);
			КонецЦикла;
			
			Попытка
				ОперацияУчета.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Выполнение обработки прервано. Не удалось записать документ по физ. лицу: %1, по причине: %2.';
					|en = 'Processing is canceled. Cannot save the document for the person: %1, due to: %2.'"),
				СтрокаФизическоеЛицо.ФизическоеЛицо,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
				Возврат Неопределено;
			КонецПопытки;
			СтрокаДокументОснование.ОперацияУчета = ОперацияУчета.Ссылка;
			Для Каждого СтрокаТаблицы Из СтрокиДоходов Цикл
				СтрокаТаблицы.ОперацияУчета = ОперацияУчета.Ссылка;
			КонецЦикла;
			
			ОбработаноДокументов = ОбработаноДокументов + 1;
		КонецЦикла;
	КонецЦикла;
	Возврат ОбработаноДокументов;
	
КонецФункции

#КонецОбласти

