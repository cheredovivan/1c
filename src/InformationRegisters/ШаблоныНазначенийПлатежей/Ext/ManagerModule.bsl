///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Получает данные шаблонов из настройки подключения.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//                       - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключение к
//                         Интернет-эквайрингу.
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП, ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга -
//    документ, который отражает оплату в информационной базе.
//  ВариантНастройки - ПеречислениеСсылка.ВариантыНастройкиСБП - вариант настройкиСистемы быстрых платежей.
//                   - Неопределено - для настройки подключения к Интернет-Эквайрингу.
//
// Возвращаемое значение:
//  ТаблицаЗначений - см. НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений.
//
Функция ШаблоныНазначенийДокументаОперации(
		НастройкаПодключения,
		ДокументОперации,
		ВариантНастройки = Неопределено) Экспорт
	
	ОбъектМетаданных = ДокументОперации.Метаданные().Имя;
	ШаблоныНазначений = НастройкиПлатежныхСистемСлужебный.НовыйШаблоныНазначений();
	ШаблоныНазначений.Колонки.Добавить("НазначениеПлатежа", ОбщегоНазначения.ОписаниеТипаСтрока(140));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШаблоныНазначенийПлатежей.ОбъектМетаданных КАК ОбъектМетаданных,
		|	ШаблоныНазначенийПлатежей.Идентификатор КАК Идентификатор,
		|	ШаблоныНазначенийПлатежей.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ШаблоныНазначенийПлатежей.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.ШаблоныНазначенийПлатежей КАК ШаблоныНазначенийПлатежей
		|ГДЕ
		|	ШаблоныНазначенийПлатежей.НастройкаПодключения = &НастройкаПодключения
		|	И ШаблоныНазначенийПлатежей.ОбъектМетаданных = &ОбъектМетаданных
		|	И &СценарийСБП";
	
	Запрос.УстановитьПараметр("ОбъектМетаданных", ОбъектМетаданных);
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	ЭтоСБП = Ложь;
	МодульСистемаБыстрыхПлатежейСлужебный = Неопределено;
	МодульСистемаБыстрыхПлатежейСобытия   = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСлужебный  = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		МодульСистемаБыстрыхПлатежейСобытия    = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСобытия");
		ЭтоСБП = МодульСистемаБыстрыхПлатежейСлужебный.ЭтоНастройкаПодключения(НастройкаПодключения);
	КонецЕсли;
	
	ЭтоЭквайринг = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.ИнтернетЭквайринг") Тогда
		МодульИнтернетЭквайрингСлужебный = ОбщегоНазначения.ОбщийМодуль("ИнтернетЭквайрингСлужебный");
		ЭтоЭквайринг = МодульИнтернетЭквайрингСлужебный.ЭтоНастройкаПодключения(НастройкаПодключения);
	КонецЕсли;
	
	Если ЭтоСБП Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&СценарийСБП",
			"ШаблоныНазначенийПлатежей.СценарийСБП = &СценарийСБП");
		Запрос.УстановитьПараметр(
			"СценарийСБП",
			МодульСистемаБыстрыхПлатежейСлужебный.ИдентификаторВариантаНастройки(
				ВариантНастройки));
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&СценарийСБП", "ИСТИНА");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ШаблоныНазначений;
	КонецЕсли;
	
	ПредопределенныеШаблоны = Неопределено;
	
	Если ЭтоСБП Тогда
		МодульСистемаБыстрыхПлатежейСобытия.ПриОпределенииШаблоновНазначений(
			ВариантНастройки,
			МодульСистемаБыстрыхПлатежейСлужебный.НастройкиПодключенияПрограммы(),
			ПредопределенныеШаблоны);
	ИначеЕсли ЭтоЭквайринг Тогда
		ПредопределенныеШаблоны = МодульИнтернетЭквайрингСлужебный.ПрикладныеНастройкиПодключения().ШаблоныНазначений
	КонецЕсли;
	
	Если ПредопределенныеШаблоны = Неопределено Тогда
		Возврат ШаблоныНазначений;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Для Каждого Настройка Из ПредопределенныеШаблоны Цикл
		Если Настройка.ОбъектМетаданных = ОбъектМетаданных Тогда
			Отбор = Новый Структура;
			Отбор.Вставить("Идентификатор", Настройка.Идентификатор);
			Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
				НовыйШаблон = ШаблоныНазначений.Добавить();
				ЗаполнитьЗначенияСвойств(
					НовыйШаблон,
					ВыборкаДетальныеЗаписи,
					"ОбъектМетаданных, Идентификатор, НазначениеПлатежа, Наименование");
				НовыйШаблон.Параметры = Настройка.Параметры;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ШаблоныНазначений;
	
КонецФункции

// Определяет настроенные шаблоны назначений платежей.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//                       - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключение к
//                         Интернет-эквайрингу.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - настроенные шаблоны:
//    * Значение - Строка - настроенное назначение платежа.
//    * Ключ - Строка - идентификатор шаблона.
//
Функция ШаблоныНазначенийПлатежей(НастройкаПодключения) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ШаблоныНазначенийПлатежей.Идентификатор КАК Идентификатор,
		|	ШаблоныНазначенийПлатежей.НазначениеПлатежа КАК НазначениеПлатежа
		|ИЗ
		|	РегистрСведений.ШаблоныНазначенийПлатежей КАК ШаблоныНазначенийПлатежей
		|ГДЕ
		|	ШаблоныНазначенийПлатежей.НастройкаПодключения = &НастройкаПодключения";
	
	Запрос.УстановитьПараметр("НастройкаПодключения", НастройкаПодключения);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Вставить(
			ВыборкаДетальныеЗаписи.Идентификатор,
			ВыборкаДетальныеЗаписи.НазначениеПлатежа);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Определяет настроенные шаблоны назначений платежей.
//
// Параметры:
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения к СБП.
//                       - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключение к
//                         Интернет-эквайрингу.
//  ШаблоныНазначений - ТаблицаЗначений - настройки шаблонов.
//  ВариантНастройки - ПеречислениеСсылка.ВариантыНастройкиСБП - вариант настройкиСистемы быстрых платежей.
//                   - Неопределено - для настройки подключения к Интернет-Эквайрингу.
Процедура ОбновитьШаблоныНазначений(
		НастройкаПодключения,
		ШаблоныНазначений,
		ВариантНастройки = Неопределено) Экспорт
		
	СценарийСБП = "-";
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.СистемаБыстрыхПлатежей.БазоваяФункциональностьСБП") Тогда
		МодульСистемаБыстрыхПлатежейСлужебный  = ОбщегоНазначения.ОбщийМодуль("СистемаБыстрыхПлатежейСлужебный");
		Если МодульСистемаБыстрыхПлатежейСлужебный.ЭтоНастройкаПодключения(НастройкаПодключения) Тогда
			СценарийСБП = МодульСистемаБыстрыхПлатежейСлужебный.ИдентификаторВариантаНастройки(ВариантНастройки);
		КонецЕсли;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ШаблоныНазначенийПлатежей");
		ЭлементБлокировки.УстановитьЗначение("НастройкаПодключения", НастройкаПодключения);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Для Каждого ДанныеШаблона Из ШаблоныНазначений Цикл
			
			НовыйШаблон = СоздатьМенеджерЗаписи();
			НовыйШаблон.Заполнить(Неопределено);
			НовыйШаблон.СценарийСБП = СценарийСБП;
			НовыйШаблон.НастройкаПодключения = НастройкаПодключения;
			НовыйШаблон.Идентификатор = ДанныеШаблона.Идентификатор;
			НовыйШаблон.Прочитать();
			
			Если ЗначениеЗаполнено(ДанныеШаблона.НазначениеПлатежа) Тогда
				НовыйШаблон.СценарийСБП = СценарийСБП;
				НовыйШаблон.НастройкаПодключения = НастройкаПодключения;
				ЗаполнитьЗначенияСвойств(
					НовыйШаблон,
					ДанныеШаблона,
					"ОбъектМетаданных, НазначениеПлатежа, Наименование, Идентификатор");
				НовыйШаблон.Записать();
			ИначеЕсли НовыйШаблон.Выбран() Тогда
				НовыйШаблон.Удалить();
			КонецЕсли;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
