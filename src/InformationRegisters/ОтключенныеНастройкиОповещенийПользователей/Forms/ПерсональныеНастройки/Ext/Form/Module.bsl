
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	Пользователь = Пользователи.АвторизованныйПользователь();
	ВидОповещения = Параметры.ВидОповещения;
	ЗаполнитьДеревоВидовОповещений();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревоНастроек

&НаКлиенте
Процедура ДеревоНастроекВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если Поле.Имя = "ВидОповещения" Тогда
		ДанныеСтроки = Элементы.ДеревоНастроек.ДанныеСтроки(ВыбраннаяСтрока);
		Если ДанныеСтроки <> Неопределено Тогда
			Значение = ДанныеСтроки.ВидОповещения;
			ОткрытьЗначениеАсинх(Значение);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
	СохранитьНаСервере();
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Сохранение настроек:';
			|en = 'Save settings:'"),,
		НСтр("ru = 'Персональные настройки оповещений сохранены.';
			|en = 'Personal notification settings are saved.'"),
		БиблиотекаКартинок.Информация32);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Включено.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДеревоНастроек.ЭтоГруппа");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоВидовОповещений()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыОповещений.Ссылка КАК ВидОповещения,
		|	ОтключенныеНастройкиОповещенийПользователей.Пользователь ЕСТЬ NULL КАК Включено,
		|	ВидыОповещений.ЭтоГруппа КАК ЭтоГруппа,
		|	ВЫБОР
		|		КОГДА ВидыОповещений.ЭтоГруппа
		|				И НЕ ВидыОповещений.ПометкаУдаления
		|			ТОГДА 0
		|		КОГДА ВидыОповещений.ЭтоГруппа
		|				И ВидыОповещений.ПометкаУдаления
		|			ТОГДА 1
		|		КОГДА НЕ ВидыОповещений.ЭтоГруппа
		|				И НЕ ВидыОповещений.ПометкаУдаления
		|				И НЕ ВидыОповещений.Поставляемый
		|			ТОГДА 3
		|		КОГДА НЕ ВидыОповещений.ЭтоГруппа
		|				И ВидыОповещений.ПометкаУдаления
		|			ТОГДА 4
		|		КОГДА НЕ ВидыОповещений.ЭтоГруппа
		|				И НЕ ВидыОповещений.ПометкаУдаления
		|				И ВидыОповещений.Поставляемый
		|			ТОГДА 5
		|	КОНЕЦ КАК ИндексКартинки
		|ИЗ
		|	Справочник.ВидыОповещенийПользователей КАК ВидыОповещений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтключенныеНастройкиОповещенийПользователей КАК ОтключенныеНастройкиОповещенийПользователей
		|		ПО ВидыОповещений.Ссылка = ОтключенныеНастройкиОповещенийПользователей.ВидОповещения
		|			И (ОтключенныеНастройкиОповещенийПользователей.Пользователь = &Пользователь)
		|ГДЕ
		|	ВЫБОР
		|		КОГДА НЕ ВидыОповещений.ЭтоГруппа
		|			ТОГДА ВидыОповещений.Активен
		|		КОНЕЦ
		|	И НЕ ВидыОповещений.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидыОповещений.Наименование
		|ИТОГИ ПО
		|	ВидОповещения ИЕРАРХИЯ";
	
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаСИерархией=РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией,"ВидОповещения");
	ЗаполнитьСтрокиДерева(ВыборкаСИерархией, ДеревоНастроек.ПолучитьЭлементы());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтрокиДерева(Выборка, ЭлементыДерева)
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ТипЗаписи()=ТипЗаписиЗапроса.ИтогПоГруппировке Тогда
			ЗаполнитьСтрокиДерева(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам), ЭлементыДерева);
			Продолжить;
		КонецЕсли;
		ЭлементДерева = ЭлементыДерева.Добавить();
		ЗаполнитьЗначенияСвойств(ЭлементДерева, Выборка);
		Если ЗначениеЗаполнено(ВидОповещения) И Выборка.ВидОповещения = ВидОповещения Тогда
			Элементы.ДеревоНастроек.ТекущаяСтрока = ЭлементДерева.ПолучитьИдентификатор();
		КонецЕсли;
		Если Выборка.ТипЗаписи()=ТипЗаписиЗапроса.ИтогПоИерархии Тогда
			ЗаполнитьСтрокиДерева(Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией,"ВидОповещения"), ЭлементДерева.ПолучитьЭлементы());
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	НаборЗаписей = РегистрыСведений.ОтключенныеНастройкиОповещенийПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь);
	ДобавитьВыключенныеВидыОповещенийВНабор(НаборЗаписей, ДеревоНастроек.ПолучитьЭлементы());
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать(РежимЗамещения.Замещение);
	УстановитьПривилегированныйРежим(Ложь);
	Модифицированность = Ложь;
КонецПроцедуры

&НаСервере
Процедура ДобавитьВыключенныеВидыОповещенийВНабор(НаборЗаписей, ЭлементыДерева)
	
	Для Каждого СтрокаТаблицы Из ЭлементыДерева Цикл
		Если НЕ СтрокаТаблицы.ЭтоГруппа И НЕ СтрокаТаблицы.Включено Тогда
			НоваяЗапись = НаборЗаписей.Добавить();
			НоваяЗапись.Пользователь = Пользователь;
			НоваяЗапись.ВидОповещения = СтрокаТаблицы.ВидОповещения;
		КонецЕсли;
		ДобавитьВыключенныеВидыОповещенийВНабор(НаборЗаписей, СтрокаТаблицы.ПолучитьЭлементы());
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти