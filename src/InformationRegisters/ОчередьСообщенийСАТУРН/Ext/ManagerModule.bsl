#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	УправлениеДоступомИСПереопределяемый.ПриЗаполненииОграниченияДоступа(
		Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН, Ограничение);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	ОбновлениеИнформационнойБазыСАТУРН.ДобавитьОбработчикПриПереходеНаНовуюВерсиюAPI(
		Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН,
		Новый УникальныйИдентификатор("82ffa7b9-ccd1-4b88-b482-8f9837ca6e7e"),
		Обработчики);
	
КонецПроцедуры

// Регистрирует данные для обработчика обновления перехода на новую версию API
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ОбъектМетаданных                      = Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН;
	ПолноеИмяОбъекта                      = ОбъектМетаданных.ПолноеИмя();
	ПараметрыВыборки                      = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяОбъекта;
	ПараметрыВыборки.СпособВыборки        = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра             = ПолноеИмяОбъекта;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОчередьСообщенийСАТУРН.Сообщение
		|ИЗ
		|	РегистрСведений.ОчередьСообщенийСАТУРН КАК ОчередьСообщенийСАТУРН
		|ГДЕ
		|	ОчередьСообщенийСАТУРН.Документ В (&ПустыеДокументы)";
	
	ПустыеДокументы = Новый Массив;
	ПустыеДокументы.Добавить(Неопределено);
	ПустыеДокументы.Добавить(Документы.НакладнаяСАТУРН.ПустаяСсылка());
	
	Запрос.УстановитьПараметр("ПустыеДокументы", ПустыеДокументы);
	Данные = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Данные, ДополнительныеПараметры);
	
КонецПроцедуры

// Обработка данных перехода на новую версию API
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ОбъектМетаданных = Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН;
	ПолноеИмяОбъекта     = ОбъектМетаданных.ПолноеИмя();
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов   = 0;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаКлюча Из ОбновляемыеДанные Цикл
		
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Сообщение.Установить(СтрокаКлюча.Сообщение);
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Сообщение", СтрокаКлюча.Сообщение);
			Блокировка.Заблокировать();
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать ""%1"" при переходе на новую версию API ФГИС ""Сатурн"" по причине:
					       |%2';
					       |en = 'Не удалось обработать ""%1"" при переходе на новую версию API ФГИС ""Сатурн"" по причине:
					       |%2'"),
				ПолноеИмяОбъекта,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать некоторые данные ""%1"": %2';
								|en = 'Не удалось обработать некоторые данные ""%1"": %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектМетаданных.Синоним, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Обработана очередная порция ""%1"": %2';
								|en = 'Обработана очередная порция ""%1"": %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектМетаданных.Синоним, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			ОбъектМетаданных,, ТекстСообщения);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов
		+ ОбработанныхОбъектов;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// Очередь сообщений по документу.
// Параметры:
//  Документ - ОпределяемыйТип.ДокументыСАТУРН - ссылка на документ.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Описание:
//   * Сообщение - ОпределяемыйТип.УникальныйИдентификаторИС - идентификатор сообщения.
//   * Операция - ПеречислениеСсылка.ВидыОперацийСАТУРН - операция.
//   * Организация - ОпределяемыйТип.Организация - организация.
//
Функция ОчередьСообщенийПоДокументу(Документ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОчередьСообщенийСАТУРН.Сообщение         КАК Сообщение,
	|	ОчередьСообщенийСАТУРН.Операция          КАК Операция,
	|	ОчередьСообщенийСАТУРН.ОрганизацияСАТУРН КАК ОрганизацияСАТУРН
	|ИЗ
	|	РегистрСведений.ОчередьСообщенийСАТУРН КАК ОчередьСообщенийСАТУРН
	|ГДЕ
	|	ОчередьСообщенийСАТУРН.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли