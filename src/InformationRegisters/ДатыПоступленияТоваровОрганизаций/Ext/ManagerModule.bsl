#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.62";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("93c9920d-a0c8-42bc-91d7-e956bc1cd320");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	
	ТекстКомментария = Новый Массив;
	ТекстКомментария.Добавить(НСтр("ru = 'Актуализирует данные в регистре сведений ""Даты поступления товаров организаций"" по данным документов:';
									|en = 'Updates data in the ""Dates of company goods receipt"" information register based on document data:'"));
	//++ НЕ УТ
	ТекстКомментария.Добавить(НСтр("ru = '- ""Поступление товаров от хранителя"" с операцией ""Поступление от переработчика"";';
									|en = '-  VMI return with the ""Goods receipt — External subcontracting"" transaction'"));
	//-- НЕ УТ

	//++ Локализация
	ТекстКомментария.Добавить(НСтр("ru = '- ""Корректировка обособленного учета запасов"";';
									|en = '- ""Adjustment of inventory separate accounting""'"));
	//-- Локализация
	ТекстКомментария.Добавить(НСтр("ru = '- ""Корректировка назначения товаров"".';
									|en = '- Inventory assignment adjustment'"));
	
	Обработчик.Комментарий = СтрСоединить(ТекстКомментария, Символы.ПС);
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Документы.КорректировкаНазначенияТоваров.ПолноеИмя());
	//++ Локализация
	Читаемые.Добавить(Метаданные.Документы.КорректировкаОбособленногоУчетаЗапасов.ПолноеИмя());
	//-- Локализация

	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.Документы.ПоступлениеТоваровОтХранителя.ПолноеИмя());
	//-- НЕ УТ
	Читаемые.Добавить(Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
КонецПроцедуры

// Выполняет регистрацию данных к обновлению.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт 
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДанныеКОбновлению.ВидЗапасов		КАК ВидЗапасов,
	|	ДанныеКОбновлению.Номенклатура		КАК Номенклатура,
	|	ДанныеКОбновлению.Характеристика	КАК Характеристика,
	|	ДанныеКОбновлению.Серия				КАК Серия,
	|	ДанныеКОбновлению.Назначение		КАК Назначение,
	|	ДанныеКОбновлению.НомерГТД			КАК НомерГТД
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПоступленияТоваров.ВидЗапасов		КАК ВидЗапасов,
	|		ПоступленияТоваров.Номенклатура		КАК Номенклатура,
	|		ПоступленияТоваров.Характеристика	КАК Характеристика,
	|		ПоступленияТоваров.Серия			КАК Серия,
	|		ПоступленияТоваров.Назначение		КАК Назначение,
	|		ПоступленияТоваров.НомерГТД			КАК НомерГТД
	|	ИЗ
	|		Документ.КорректировкаНазначенияТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаНазначенияТоваров КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиОприходование
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыОприходование = КлючиАналитикиОприходование.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|			ПО ТаблицаВидыЗапасов.ВидЗапасовОприходование = ПоступленияТоваров.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ПоступленияТоваров.Номенклатура
	|				И КлючиАналитики.Характеристика = ПоступленияТоваров.Характеристика
	|				И КлючиАналитики.Серия = ПоступленияТоваров.Серия
	|				И КлючиАналитикиОприходование.Назначение = ПоступленияТоваров.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ПоступленияТоваров.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ПоступленияТоваров.ДатаПоступления < ДанныеДокумента.Дата
	//++ Локализация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ПоступленияТоваров.ВидЗапасов		КАК ВидЗапасов,
	|		ПоступленияТоваров.Номенклатура		КАК Номенклатура,
	|		ПоступленияТоваров.Характеристика	КАК Характеристика,
	|		ПоступленияТоваров.Серия			КАК Серия,
	|		ПоступленияТоваров.Назначение		КАК Назначение,
	|		ПоступленияТоваров.НомерГТД			КАК НомерГТД
	|	ИЗ
	|		Документ.КорректировкаОбособленногоУчетаЗапасов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаОбособленногоУчетаЗапасов КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|			ПО ТаблицаВидыЗапасов.ВидЗапасовОприходование = ПоступленияТоваров.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ПоступленияТоваров.Номенклатура
	|				И КлючиАналитики.Характеристика = ПоступленияТоваров.Характеристика
	|				И КлючиАналитики.Серия = ПоступленияТоваров.Серия
	|				И КлючиАналитики.Назначение = ПоступленияТоваров.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ПоступленияТоваров.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ПоступленияТоваров.ДатаПоступления < ДанныеДокумента.Дата
	//-- Локализация

	//++ НЕ УТ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		ТаблицаВидыЗапасов.КорВидЗапасов	КАК ВидЗапасов,
	|		КлючиАналитики.Номенклатура			КАК Номенклатура,
	|		КлючиАналитики.Характеристика		КАК Характеристика,
	|		КлючиАналитики.Серия				КАК Серия,
	|		КлючиАналитики.Назначение			КАК Назначение,
	|		ТаблицаВидыЗапасов.НомерГТД			КАК НомерГТД
	|	ИЗ
	|		Документ.ПоступлениеТоваровОтХранителя.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровОтХранителя КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДатыПоступленияТоваровОрганизаций КАК ПоступленияТоваров
	|			ПО ТаблицаВидыЗапасов.КорВидЗапасов = ПоступленияТоваров.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ПоступленияТоваров.Номенклатура
	|				И КлючиАналитики.Характеристика = ПоступленияТоваров.Характеристика
	|				И КлючиАналитики.Серия = ПоступленияТоваров.Серия
	|				И КлючиАналитики.Назначение = ПоступленияТоваров.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ПоступленияТоваров.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОтПереработчика2_5)
	|		И (ПоступленияТоваров.ДатаПоступления ЕСТЬ NULL
	|			ИЛИ ПоступленияТоваров.ДатаПоступления < ДанныеДокумента.Дата)
	//-- НЕ УТ
	|	
	|	) КАК ДанныеКОбновлению";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить(), ДополнительныеПараметры);
	
КонецПроцедуры

// Выполняет обработку зарегистрированных к обновлению данных.
//
// Параметры:
//	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций.ПолноеИмя();
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
																							ПолноеИмяРегистра);
		
		Возврат;
	КонецЕсли;
	
	ИменаДокументов = Новый Массив;
	ИменаДокументов.Добавить(Метаданные.Документы.КорректировкаНазначенияТоваров.ПолноеИмя());
	//++ Локализация
	ИменаДокументов.Добавить(Метаданные.Документы.КорректировкаОбособленногоУчетаЗапасов.ПолноеИмя());
	//-- Локализация

	//++ НЕ УТ
	ИменаДокументов.Добавить(Метаданные.Документы.ПоступлениеТоваровОтХранителя.ПолноеИмя());
	//-- НЕ УТ
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ВТЗаблокированныеСсылки = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуЗаблокированныхДляЧтенияИИзмененияСсылок(
								Параметры.Очередь,
								ИменаДокументов,
								МенеджерВременныхТаблиц);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ОбновляемыеДанные.ВидЗапасов		КАК ВидЗапасов,
	|	ОбновляемыеДанные.Номенклатура		КАК Номенклатура,
	|	ОбновляемыеДанные.Характеристика	КАК Характеристика,
	|	ОбновляемыеДанные.Серия				КАК Серия,
	|	ОбновляемыеДанные.Назначение		КАК Назначение,
	|	ОбновляемыеДанные.НомерГТД			КАК НомерГТД
	|ПОМЕСТИТЬ ОбновляемыеДанные
	|ИЗ
	|	&ОбновляемыеДанные КАК ОбновляемыеДанные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидЗапасов,
	|	Номенклатура,
	|	Характеристика,
	|	Серия,
	|	Назначение,
	|	НомерГТД
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеКОбновлению.ВидЗапасов		КАК ВидЗапасов,
	|	ДанныеКОбновлению.Номенклатура		КАК Номенклатура,
	|	ДанныеКОбновлению.Характеристика	КАК Характеристика,
	|	ДанныеКОбновлению.Серия				КАК Серия,
	|	ДанныеКОбновлению.Назначение		КАК Назначение,
	|	ДанныеКОбновлению.НомерГТД			КАК НомерГТД,
	|	МАКСИМУМ(ДанныеКОбновлению.Дата)	КАК ДатаПоступления
	|ИЗ
	|	(ВЫБРАТЬ
	|		ОбновляемыеДанные.ВидЗапасов		КАК ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура		КАК Номенклатура,
	|		ОбновляемыеДанные.Характеристика	КАК Характеристика,
	|		ОбновляемыеДанные.Серия				КАК Серия,
	|		ОбновляемыеДанные.Назначение		КАК Назначение,
	|		ОбновляемыеДанные.НомерГТД			КАК НомерГТД,
	|		МАКСИМУМ(ДанныеДокумента.Дата)		КАК Дата
	|	ИЗ
	|		Документ.КорректировкаНазначенияТоваров.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаНазначенияТоваров КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ВТЗаблокировано
	|			ПО ТаблицаВидыЗапасов.Ссылка = ВТЗаблокировано.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитикиОприходование
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатурыОприходование = КлючиАналитикиОприходование.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанные КАК ОбновляемыеДанные
	|			ПО ТаблицаВидыЗапасов.ВидЗапасовОприходование = ОбновляемыеДанные.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ОбновляемыеДанные.Номенклатура
	|				И КлючиАналитики.Характеристика = ОбновляемыеДанные.Характеристика
	|				И КлючиАналитики.Серия = ОбновляемыеДанные.Серия
	|				И КлючиАналитикиОприходование.Назначение = ОбновляемыеДанные.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ОбновляемыеДанные.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ВТЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОбновляемыеДанные.ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура,
	|		ОбновляемыеДанные.Характеристика,
	|		ОбновляемыеДанные.Серия,
	|		ОбновляемыеДанные.Назначение,
	|		ОбновляемыеДанные.НомерГТД
	//++ Локализация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОбновляемыеДанные.ВидЗапасов		КАК ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура		КАК Номенклатура,
	|		ОбновляемыеДанные.Характеристика	КАК Характеристика,
	|		ОбновляемыеДанные.Серия				КАК Серия,
	|		ОбновляемыеДанные.Назначение		КАК Назначение,
	|		ОбновляемыеДанные.НомерГТД			КАК НомерГТД,
	|		МАКСИМУМ(ДанныеДокумента.Дата)		КАК Дата
	|	ИЗ
	|		Документ.КорректировкаОбособленногоУчетаЗапасов.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаОбособленногоУчетаЗапасов КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ВТЗаблокировано
	|			ПО ТаблицаВидыЗапасов.Ссылка = ВТЗаблокировано.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанные КАК ОбновляемыеДанные
	|			ПО ТаблицаВидыЗапасов.ВидЗапасовОприходование = ОбновляемыеДанные.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ОбновляемыеДанные.Номенклатура
	|				И КлючиАналитики.Характеристика = ОбновляемыеДанные.Характеристика
	|				И КлючиАналитики.Серия = ОбновляемыеДанные.Серия
	|				И КлючиАналитики.Назначение = ОбновляемыеДанные.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ОбновляемыеДанные.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ВТЗаблокировано.Ссылка ЕСТЬ NULL
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОбновляемыеДанные.ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура,
	|		ОбновляемыеДанные.Характеристика,
	|		ОбновляемыеДанные.Серия,
	|		ОбновляемыеДанные.Назначение,
	|		ОбновляемыеДанные.НомерГТД
	//-- Локализация

	//++ НЕ УТ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОбновляемыеДанные.ВидЗапасов		КАК ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура		КАК Номенклатура,
	|		ОбновляемыеДанные.Характеристика	КАК Характеристика,
	|		ОбновляемыеДанные.Серия				КАК Серия,
	|		ОбновляемыеДанные.Назначение		КАК Назначение,
	|		ОбновляемыеДанные.НомерГТД			КАК НомерГТД,
	|		МАКСИМУМ(ДанныеДокумента.Дата)		КАК Дата
	|	ИЗ
	|		Документ.ПоступлениеТоваровОтХранителя.ВидыЗапасов КАК ТаблицаВидыЗапасов
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПоступлениеТоваровОтХранителя КАК ДанныеДокумента
	|			ПО ТаблицаВидыЗапасов.Ссылка = ДанныеДокумента.Ссылка
	|			
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаблокировано КАК ВТЗаблокировано
	|			ПО ТаблицаВидыЗапасов.Ссылка = ВТЗаблокировано.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.КлючиАналитикиУчетаНоменклатуры КАК КлючиАналитики
	|			ПО ТаблицаВидыЗапасов.АналитикаУчетаНоменклатуры = КлючиАналитики.Ссылка
	|			
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОбновляемыеДанные КАК ОбновляемыеДанные
	|			ПО ТаблицаВидыЗапасов.КорВидЗапасов = ОбновляемыеДанные.ВидЗапасов
	|				И КлючиАналитики.Номенклатура = ОбновляемыеДанные.Номенклатура
	|				И КлючиАналитики.Характеристика = ОбновляемыеДанные.Характеристика
	|				И КлючиАналитики.Серия = ОбновляемыеДанные.Серия
	|				И КлючиАналитики.Назначение = ОбновляемыеДанные.Назначение
	|				И ТаблицаВидыЗапасов.НомерГТД = ОбновляемыеДанные.НомерГТД
	|	
	|	ГДЕ
	|		ДанныеДокумента.Проведен
	|		И ВТЗаблокировано.Ссылка ЕСТЬ NULL
	|		И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОтПереработчика2_5)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ОбновляемыеДанные.ВидЗапасов,
	|		ОбновляемыеДанные.Номенклатура,
	|		ОбновляемыеДанные.Характеристика,
	|		ОбновляемыеДанные.Серия,
	|		ОбновляемыеДанные.Назначение,
	|		ОбновляемыеДанные.НомерГТД
	//-- НЕ УТ
	|	
	|	) КАК ДанныеКОбновлению
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеКОбновлению.ВидЗапасов,
	|	ДанныеКОбновлению.Номенклатура,
	|	ДанныеКОбновлению.Характеристика,
	|	ДанныеКОбновлению.Серия,
	|	ДанныеКОбновлению.Назначение,
	|	ДанныеКОбновлению.НомерГТД";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
	ЭлементБлокировки.ИсточникДанных = ОбновляемыеДанные;
	
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ВидЗапасов",		"ВидЗапасов");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура",		"Номенклатура");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика",	"Характеристика");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Серия",			"Серия");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Назначение",		"Назначение");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("НомерГТД",			"НомерГТД");
	
	НачатьТранзакцию();
	
	Попытка
		Блокировка.Заблокировать();
		
		РезультатыЗапроса = Запрос.Выполнить();
		
		Если Не РезультатыЗапроса.Пустой() Тогда
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Загрузить(РезультатыЗапроса.Выгрузить());
			НаборЗаписей.Записать(РежимЗамещения.Слияние);
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбновляемыеДанные, ДополнительныеПараметры);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
			
			ТекстСообщения = НСтр("ru = 'Не удалось обновить данные в регистре %1 по причине: %2';
									|en = 'Cannot update data in the %1 register due to: %2'");
			ТекстСообщения = СтрШаблон(ТекстСообщения,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
										ПолноеИмяРегистра);
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
										УровеньЖурналаРегистрации.Предупреждение,
										Метаданные.РегистрыСведений.ДатыПоступленияТоваровОрганизаций,
										,
										ТекстСообщения);
	КонецПопытки;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь,
																						ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
