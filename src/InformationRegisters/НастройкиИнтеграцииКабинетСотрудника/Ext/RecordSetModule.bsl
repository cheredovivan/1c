#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ОбновитьНастройкиФункциональностьСервиса") Тогда
		РегистрыСведений.НастройкиСервисаКабинетСотрудника.УстановитьТребуетсяОбновитьНастройкиФункциональности(Истина);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	Если ЭтотОбъект.Количество() > 0 И Не РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		ИспользуетсяЭДОБЗК = ЭтотОбъект[0].ИспользуетсяЭДОБЗК;
		БизнесПроцессыЗаявокСотрудников.ПроверитьИИзменитьЗначениеНастроек(ИспользуетсяЭДОБЗК);
		ЭДОБЗК.УстановитьЗначениеВедетсяУчетСогласийНаПрисоединениеКЭДОБЗК(ИспользуетсяЭДОБЗК);
		Настройки = РегистрыСведений.НастройкиКабинетСотрудника.Настройки();
		Если ИспользуетсяЭДОБЗК Тогда
			Если Не Настройки.РазделДокументы Или Не Настройки.ДоступноПолучениеДокументаСЭП Тогда
				Настройки.РазделДокументы = Истина;
				Настройки.ДоступноПолучениеДокументаСЭП = Истина;
				РегистрыСведений.НастройкиКабинетСотрудника.СохранитьНовыеНастройки(Настройки)
			КонецЕсли;
		Иначе
			Если Настройки.ДоступенОтказОтПодписания Тогда
				Настройки.ДоступенОтказОтПодписания = Ложь;
				РегистрыСведений.НастройкиКабинетСотрудника.СохранитьНовыеНастройки(Настройки)
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЭДОБЗК.УстановитьЗначениеВедетсяУчетСогласийНаПрисоединениеКЭДОБЗК(Ложь);
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли