#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Устанавливает состояние согласия на присоединение к ЭДО и факт наличия подписи.
//	Параметры:
//		Ссылка - ДокументСсылка.СогласиеНаПрисоединениеКЭДОБЗК - документ для которого изменяется состояние;
//		Состояние - ПеречислениеСсылка.СостоянияСогласийНаПрисоединениеКЭДОБЗК - устанавливаемое состояние;
//		СостояниеПриема = ПеречисленияСсылка.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК
//
Процедура УстановитьСостояние(Ссылка, Состояние, СостояниеПриема = Ложь) Экспорт
	МенеджерЗаписи = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Состояние = Состояние;
	МенеджерЗаписи.СостояниеПриема = СостояниеПриема;
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

// Устанавливает дату окончания действия согласия.
//	Параметры:
//		Ссылка - ДокументСсылка.СогласиеНаПрисоединениеКЭДОБЗК - документ прекращающий свое действие;
//		Дата - Дата - дата окончания действия согласия.
Процедура УстановитьДатуПрекращенияДействия(Ссылка, Дата) Экспорт
	МенеджерЗаписи = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.ДатаОкончания = Дата;
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

// Устанавливает дату начала действия согласия.
//	Параметры:
//      Ссылка - ДокументСсылка.СогласиеНаПрисоединениеКЭДОБЗК - документ для которого указывается дата начала; 
//		Дата - Дата - дата начала действия согласия.
Процедура УстановитьДатуНачалаДействия(Ссылка, Дата) Экспорт
	МенеджерЗаписи = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.ДатаНачала = Дата;
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

Процедура УстановитьСостояниеПриема(Ссылка, СостояниеПриема) Экспорт
	МенеджерЗаписи = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Ссылка = Ссылка;
	МенеджерЗаписи.Прочитать();
	Если МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.СостояниеПриема = СостояниеПриема;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
	Если СостояниеПриема = Перечисления.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено Тогда
		ДанныеСогласия = ЭДОБЗК.ДокументЭДОБЗКСогласияНаПрисоединениеКЭДОБЗК(Ссылка);
		Если ДанныеСогласия <> Неопределено Тогда
			РегистрыСведений.СостоянияДокументовЭДОБЗК.ЗарегистрироватьПолучениеПодписанныхОригиналов(
				ДанныеСогласия.ДокументЭДОБЗК, Истина);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДатыНачалаИОкончанияСогласийНаПрисоединениеКЭДОБЗКОбновление(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ВедетсяУчетСогласийНаПрисоединениеКЭДОБЗК") Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СогласиеНаПрисоединениеКЭДОБЗК.Ссылка КАК Ссылка,
	               |	СогласиеНаПрисоединениеКЭДОБЗК.Организация КАК Организация,
	               |	СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо,
	               |	СогласиеНаПрисоединениеКЭДОБЗК.Дата КАК Дата
	               |ПОМЕСТИТЬ ВТСогласия
	               |ИЗ
	               |	Документ.СогласиеНаПрисоединениеКЭДОБЗК КАК СогласиеНаПрисоединениеКЭДОБЗК
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
	               |		ПО (СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка = СогласиеНаПрисоединениеКЭДОБЗК.Ссылка)
	               |ГДЕ
	               |	(СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаНачала = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ИЛИ СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаНачала ЕСТЬ NULL)
	               |	И (СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	               |			ИЛИ СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания ЕСТЬ NULL)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТСогласия.Ссылка КАК Ссылка,
	               |	ВТСогласия.Дата КАК Дата,
	               |	ТекущиеКадровыеДанныеСотрудников.ДатаУвольнения КАК ДатаУвольнения
	               |ИЗ
	               |	ВТСогласия КАК ВТСогласия
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТекущиеКадровыеДанныеСотрудников КАК ТекущиеКадровыеДанныеСотрудников
	               |		ПО ВТСогласия.Организация = ТекущиеКадровыеДанныеСотрудников.ГоловнаяОрганизация
	               |			И ВТСогласия.ФизическоеЛицо = ТекущиеКадровыеДанныеСотрудников.ФизическоеЛицо";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	ОбработкаЗавершена = Истина;
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления,
																							 "Документ.СогласиеНаПрисоединениеКЭДОБЗК",
																							 "Ссылка",
																							 Выборка.Ссылка) Тогда
			ОбработкаЗавершена = Ложь;
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		
		Запись = Неопределено;
		Если НаборЗаписей.Количество() = 0 Тогда
			Запись = НаборЗаписей.Добавить();
			Запись.Ссылка = Выборка.Ссылка;
		Иначе
			Запись = НаборЗаписей[0];
		КонецЕсли;
		Запись.ДатаНачала = Выборка.Дата;
		Если ЗначениеЗаполнено(Выборка.ДатаУвольнения) И Выборка.ДатаУвольнения > Выборка.Дата Тогда
			Запись.ДатаОкончания = Выборка.ДатаУвольнения;
		КонецЕсли;
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", ОбработкаЗавершена);
	
КонецПроцедуры

#Область КадровыеДанныеСотрудников

Функция ДобавитьПолеСведенийОСогласияхПодключенийКЭДОБЗК(ИмяПоля, ТекстыОписанияПолей, ИсточникиДанных) Экспорт
	
	ДобавленоПолеСведений = Ложь;
	Если НеобходимыДанныеОСогласияхПодключенийКЭДОБЗК(ИмяПоля) Тогда
		
		ДобавленоПолеСведений = Истина;
		ИсточникиДанных.Вставить("СостояниеСогласияНаПрисоединениеКЭДОБЗК", Истина);
		
		ПутьКДанным = ПутьКДаннымСведенийОСогласияхПодключенийКЭДОБЗК(ИмяПоля);
		ТекстыОписанияПолей.Добавить(ПутьКДанным + " КАК " + ИмяПоля);
		
	КонецЕсли;
	
	Возврат ДобавленоПолеСведений;
	
КонецФункции

Функция НеобходимыДанныеОСогласияхПодключенийКЭДОБЗК(ИмяПоля) Экспорт
	
	ИмяПоля = ВРег(ИмяПоля);
	Возврат ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗК")
		Или ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗКСостояние")
		Или ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗКСостояниеПриема")
		Или ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗКПодтвержден")
		Или ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗКДатаНачала")
		Или ИмяПоля = ВРег("СогласиеНаПрисоединениеКЭДОБЗКДатаОкончания");
	
КонецФункции

Функция ПутьКДаннымСведенийОСогласияхПодключенийКЭДОБЗК(ИмяПоля)
	
	ИмяПоляВВерхнемРегистре = ВРег(ИмяПоля);
	ПутьКДанным = "";
	
	Если ИмяПоляВВерхнемРегистре = ВРег("СогласиеНаПрисоединениеКЭДОБЗК") Тогда
		ПутьКДанным = "	СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("СогласиеНаПрисоединениеКЭДОБЗКСостояние") Тогда
		ПутьКДанным = "	СостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("СогласиеНаПрисоединениеКЭДОБЗКСостояниеПриема") Тогда
		ПутьКДанным = "	СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("СогласиеНаПрисоединениеКЭДОБЗКДатаНачала") Тогда
		ПутьКДанным = "	СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаНачала";
	ИначеЕсли ИмяПоляВВерхнемРегистре = ВРег("СогласиеНаПрисоединениеКЭДОБЗКДатаОкончания") Тогда
		ПутьКДанным = "	СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания";
	КонецЕсли;
	
	Возврат ПутьКДанным;
	
КонецФункции

Процедура ДобавитьТекстЗапросаВТСведенияОСогласияхПодключенийКЭДОБЗК(Запрос, ТолькоРазрешенные, ОписательВременнойТаблицыОтборов, ИсточникиДанных) Экспорт
	
	Если ИсточникиДанных.Получить("СостояниеСогласияНаПрисоединениеКЭДОБЗК") = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЧастиЗапроса = Новый Массив;
	ЧастиЗапроса.Добавить(Запрос.Текст);
	ЧастиЗапроса.Добавить(
		"	{ЛЕВОЕ СОЕДИНЕНИЕ Документ.СогласиеНаПрисоединениеКЭДОБЗК Как СогласиеНаПрисоединениеКЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
		|			ПО СогласиеНаПрисоединениеКЭДОБЗК.Ссылка = СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка
		|		ПО СогласиеНаПрисоединениеКЭДОБЗК.Проведен
		|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + ".ФизическоеЛицо = СогласиеНаПрисоединениеКЭДОБЗК.ФизическоеЛицо
		|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляСотрудник + ".ГоловнаяОрганизация = СогласиеНаПрисоединениеКЭДОБЗК.Организация
		|			И ТаблицаОтборов." + ОписательВременнойТаблицыОтборов.ИмяПоляПериод + " МЕЖДУ СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаНачала
		|			И ВЫБОР
		|				КОГДА СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|					ТОГДА ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59)
		|				ИНАЧЕ СостояниеСогласияНаПрисоединениеКЭДОБЗК.ДатаОкончания
		|			КОНЕЦ}");
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, Символы.ПС);
	
КонецПроцедуры

#КонецОбласти

Процедура ЗаполнитьСостоянияПриема(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбработанных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбработанных") Тогда
			МассивОбработанных = ПараметрыОбновления.МассивОбработанных;
		Иначе
			МассивОбработанных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбработанных", МассивОбработанных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбработанных", МассивОбработанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА СостояниеСогласияНаПрисоединениеКЭДОБЗК.УдалитьПодтвержден
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ОжиданиеОригинала)
		|	КОНЕЦ КАК СостояниеПриема
		|ИЗ
		|	РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
		|ГДЕ
		|	НЕ СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка В (&МассивОбработанных)
		|	И СостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеКЭДОБЗК.Согласие)
		|	И СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема = ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбработанных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК", "Ссылка", Выборка.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Ссылка.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Выбран() Тогда
			Запись = НаборЗаписей[0];
			Запись.СостояниеПриема = Выборка.СостояниеПриема;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
