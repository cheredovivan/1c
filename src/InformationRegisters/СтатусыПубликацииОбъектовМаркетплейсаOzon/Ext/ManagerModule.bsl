// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Позволяет указать объекты метаданных, для которых задана логика ограничения доступа к данным.
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(УчетнаяЗаписьМаркетплейса.Организация, ПустаяСсылка КАК Истина)
		|	И ЗначениеРазрешено(УчетнаяЗаписьМаркетплейса.Партнер, ПустаяСсылка КАК Истина)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Заполняет пустую номенклатуру партнера.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗаполнитьНоменклатуруПартнера(УчетнаяЗапись) Экспорт
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ПолноеИмя();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
		|	ИзмеренияРегистра.Номенклатура КАК Номенклатура,
		|	ИзмеренияРегистра.Характеристика КАК Характеристика,
		|	ИзмеренияРегистра.Упаковка КАК Упаковка,
		|	ИзмеренияРегистра.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ИзмеренияРегистра.ОбъектПубликации КАК ОбъектПубликации
		|ПОМЕСТИТЬ ВТИзмеренияРегистраПредварительно
		|ИЗ
		|	&ПолноеИмяРегистра КАК ИзмеренияРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
		|		ПО ИзмеренияРегистра.НоменклатураПартнера = НоменклатураКонтрагентов.Ссылка
		|ГДЕ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
		|	И ИзмеренияРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
		|	И ИзмеренияРегистра.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И НоменклатураКонтрагентов.Ссылка ЕСТЬ NULL";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолноеИмяРегистра", ПолноеИмяРегистра);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Выполнить();
	
	ДанныеДляПерехода = ДанныеДляЗаполненияНоменклатурыПартнера(МенеджерВременныхТаблиц);
	ОбработатьДанныеДляЗаполненияНоменклатурыПартнера(ПолноеИмяРегистра, ДанныеДляПерехода,, Ошибка);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Ошибка;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ОбработатьДанныеДляПереходаНаНовуюВерсию_ПереходНаНоменклатуруПартнера";
	Обработчик.Версия = "2.5.24.25";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("2356e570-8cad-48be-91ac-690dc8ea7998");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_ПереходНаНоменклатуруПартнера";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Заполняет измерение ""Номенклатура партнера"" в регистре сведений ""Статусы публикации объектов маркетплейса Ozon"" для существующих записей. 
	|Проверяет, заполнен ли партнер в учетной записи маркетплейса. Если партнер заполнен - создает карточку номенклатуры партнера и указывает ее в записи регистра.';
	|en = 'Fills in the ""Partner products"" dimension in the ""Publication status of Ozon objects"" information register for existing records. 
	|Checks whether the partner is filled in the marketplace account. If the partner is filled in, creates a partner product card and specifies it in the register record.'");
	
	Читаемые = Новый Массив; // Массив из Строка
	Читаемые.Добавить(Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив; // Массив из Строка
	Изменяемые.Добавить(Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Справочники.НоменклатураКонтрагентов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Новые = Новый Массив; // Массив из Строка
	Новые.Добавить(Метаданные.Справочники.НоменклатураКонтрагентов.ПолноеИмя());
	Обработчик.НовыеОбъекты = СтрСоединить(Новые, ",");
	
	Блокируемые = Новый Массив; // Массив из Строка
	Блокируемые.Добавить(Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
КонецПроцедуры

// Регистрирует данные регистра к обработке в отложенном обработчике обновления.
//
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию_ПереходНаНоменклатуруПартнера(Параметры) Экспорт
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки(ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиМаркетплейсов.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ ВТУчетныеЗаписи
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
		|ГДЕ
		|	УчетныеЗаписиМаркетплейсов.ВидМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.МаркетплейсOzon)
		|	И УчетныеЗаписиМаркетплейсов.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
		|	ДанныеРегистра.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
		|	ДанныеРегистра.Номенклатура КАК Номенклатура,
		|	ДанныеРегистра.Характеристика КАК Характеристика,
		|	ДанныеРегистра.Упаковка КАК Упаковка,
		|	ДанныеРегистра.ОбъектПубликации КАК ОбъектПубликации,
		|	ДанныеРегистра.НоменклатураПартнера КАК НоменклатураПартнера
		|ИЗ
		|	РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК ДанныеРегистра
		|ГДЕ
		|	ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
		|	И ДанныеРегистра.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И ДанныеРегистра.НоменклатураПартнера = ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)
		|	И ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ВТУчетныеЗаписи КАК УчетныеЗаписи
		|			ГДЕ
		|				УчетныеЗаписи.Ссылка = ДанныеРегистра.УчетнаяЗаписьМаркетплейса)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРегистра = Новый ТаблицаЗначений;
	
	Для Каждого Колонка Из РезультатЗапроса.Колонки Цикл
		ТаблицаРегистра.Колонки.Добавить(Колонка.Имя, Колонка.ТипЗначения);
	КонецЦикла;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	КоличествоДляРегистрации = 1000;
	
	НачатьТранзакцию();
	
	Попытка
		
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(ТаблицаРегистра.Добавить(), Выборка);
			
			Если ТаблицаРегистра.Количество() = КоличествоДляРегистрации Тогда
				
				ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаРегистра, ДополнительныеПараметры);
				ТаблицаРегистра.Очистить();
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаРегистра.Количество() > 0 Тогда
			ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаРегистра, ДополнительныеПараметры);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось зарегистрировать данные регистра ""%1"" к обработке для перехода на новую версию.';
				|en = 'Cannot register the ""%1"" register data for processing to migrate to a new version.'"),
			Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon.Представление());
			
		ВызватьИсключение(ТекстОшибки, КатегорияОшибки.ОшибкаХранимыхДанных, , , ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

// Обработчик обновления.
// Перебираются все записи регистра, в которых не заполнена номенклатура партнера, затем создается карточка
// номенклатуры партнера и указывается в записи регистра.
//
// Параметры:
//  Параметры - Структура:
//    * Очередь            - Число - очередь обработки, в которой выполняется текущий обработчик.
//    * ОбработкаЗавершена - Булево - признак окончания обработки.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию_ПереходНаНоменклатуруПартнера(Параметры) Экспорт
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon;
	ПолноеИмяРегистра = МетаданныеРегистра.ПолноеИмя();
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	
	ДополнительныеПараметры.ИмяВременнойТаблицы = "ВТИзмеренияРегистраПредварительно";
	ДополнительныеПараметры.ВыбиратьПорциями    = Истина;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь, ПолноеИмяРегистра, МенеджерВременныхТаблиц, ДополнительныеПараметры);
		
	Если Не Результат.ЕстьЗаписиВоВременнойТаблице Или Не Результат.ЕстьДанныеДляОбработки Тогда
		
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
		
	КонецЕсли;
	
	ДанныеДляПерехода = ДанныеДляЗаполненияНоменклатурыПартнера(МенеджерВременныхТаблиц);
	ОбработанныеЗаписи = НовыеОбработанныеЗаписиРегистра(МетаданныеРегистра);
	ОбработатьДанныеДляЗаполненияНоменклатурыПартнера(ПолноеИмяРегистра, ДанныеДляПерехода, ОбработанныеЗаписи);
	
	Параметры.ОбработкаЗавершена = 
		Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
		
КонецПроцедуры

Функция ДополнительныеПараметрыОтметкиОбработки(ПолноеИмяРегистра)
	
	Результат = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	
	Результат.ПолноеИмяРегистра             = ПолноеИмяРегистра;
	Результат.ЭтоНезависимыйРегистрСведений = Истина;
	
	Возврат Результат;
	
КонецФункции

Функция НовыеОбработанныеЗаписиРегистра(МетаданныеРегистра)
	
	Результат = Новый ТаблицаЗначений;
	
	Для Каждого Измерение Из МетаданныеРегистра.Измерения Цикл
		Результат.Колонки.Добавить(Измерение.Имя, Измерение.Тип);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеНоменклатурыПартнера


// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц, содержащий временную таблицу с 
//                            измерениями регистра, по которым необходимо получить данные для обработки.
//
// Возвращаемое значение:
//  Структура:
//   * РезультатЗапросаДляБлокировки    - РезультатЗапроса - результат запроса для блокировки данных регистра.
//   * ВыборкаСтатусыПубликации         - ВыборкаИзРезультатаЗапроса - содержит все поля регистра, а также дополнительно:
//      ** Партнер                      - СправочникСсылка.Партнеры - владелец создаваемой номенклатуры партнера.
//      ** НоваяНоменклатураПартнера    - СправочникСсылка.НоменклатураКонтрагентов - используется для поиска 
//                                        существующих элементов справочника Номенклатура контрагентов.
//   * ВыборкаИзображенияСертификаты    - ВыборкаИзРезультатаЗапроса - содержит все поля регистра.
//   * ВыборкаДанныеНоменклатуры        - ВыборкаИзРезультатаЗапроса:
//      ** Номенклатура                 - СправочникСсылка.Номенклатура - номенклатура.
//      ** ЕдиницаИзмерения             - СправочникСсылка.УпаковкиЕдиницыИзмерения - единица измерения.
//      ** ЕдиницаИзмеренияНаименование - Строка - наименование единицы измерения.
//      ** ЕдиницаИзмеренияКод          - Строка - код единицы измерения.
//      ** СтавкаНДСЧислом              - Число - ставка НДС.
//   * ВыборкаДанныеУпаковок            - ВыборкаИзРезультатаЗапроса:
//      ** Упаковка                     - СправочникСсылка.УпаковкиЕдиницыИзмерения - упаковка.
//      ** НаименованиеУпаковки         - Строка - наименование упаковки.
//      ** КоэффициентУпаковки          - Число - коэффициент упаковки.
//
Функция ДанныеДляЗаполненияНоменклатурыПартнера(МенеджерВременныхТаблиц)
	
	Тексты = Новый Массив; // Массив из Строка
	
	// Переформируем временную таблицу измерений регистра для индексирования по нужным наборам.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
		|	ИзмеренияРегистра.Номенклатура КАК Номенклатура,
		|	ИзмеренияРегистра.Характеристика КАК Характеристика,
		|	ИзмеренияРегистра.Упаковка КАК Упаковка,
		|	ИзмеренияРегистра.НоменклатураПартнера КАК НоменклатураПартнера,
		|	ИзмеренияРегистра.ОбъектПубликации КАК ОбъектПубликации
		|ПОМЕСТИТЬ ВТИзмеренияРегистра
		|ИЗ
		|	ВТИзмеренияРегистраПредварительно КАК ИзмеренияРегистра
		|
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ
		|(
		|	(УчетнаяЗаписьМаркетплейса,
		|	Номенклатура,
		|	Характеристика,
		|	Упаковка,
		|	НоменклатураПартнера,
		|	ОбъектПубликации),
		|	(УчетнаяЗаписьМаркетплейса,
		|	Номенклатура,
		|	Характеристика,
		|	Упаковка,
		|	НоменклатураПартнера),
		|	(УчетнаяЗаписьМаркетплейса),
		|	(УчетнаяЗаписьМаркетплейса,
		|	Номенклатура),
		|	(Номенклатура),
		|	(Номенклатура,
		|	Упаковка)
		|)";
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Для получения ставок НДС по номенклатуре нужна страна. Страну можно взять из учетной записи.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса
		|ПОМЕСТИТЬ ВТУчетныеЗаписи
		|ИЗ
		|	ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УчетнаяЗаписьМаркетплейса
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеУчетныхЗаписей.Ссылка КАК УчетнаяЗаписьМаркетплейса,
		|	ДанныеУчетныхЗаписей.Партнер КАК Партнер,
		|	ВЫБОР
		|		КОГДА Организации.СтранаРегистрации ЕСТЬ NULL
		|			ТОГДА &ОсновнаяСтрана
		|		КОГДА Организации.СтранаРегистрации = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
		|			ТОГДА &ОсновнаяСтрана
		|		ИНАЧЕ Организации.СтранаРегистрации
		|	КОНЕЦ КАК СтранаРегистрации
		|ПОМЕСТИТЬ ВТДанныеУчетныхЗаписей
		|ИЗ
		|	Справочник.УчетныеЗаписиМаркетплейсов КАК ДанныеУчетныхЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО ДанныеУчетныхЗаписей.Организация = Организации.Ссылка
		|ГДЕ
		|	ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ВТУчетныеЗаписи КАК УчетныеЗаписи
		|			ГДЕ
		|				УчетныеЗаписи.УчетнаяЗаписьМаркетплейса = ДанныеУчетныхЗаписей.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	УчетнаяЗаписьМаркетплейса";
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Получаем дополнительные данные по номенклатуре, которые необходимо будет заполнить в номенклатуре партнера.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отбор.Номенклатура КАК Номенклатура
		|ПОМЕСТИТЬ ВТОтборНоменклатуры
		|ИЗ
		|	ВТИзмеренияРегистра КАК Отбор
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СпрНоменклатура.Ссылка КАК Номенклатура,
		|	ЕСТЬNULL(СпрНоменклатура.ЕдиницаИзмерения, ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)) КАК ЕдиницаИзмерения,
		|	ЕСТЬNULL(ДанныеЕдиницыНоменклатуры.Наименование, ЕСТЬNULL(ДанныеЕдиницыПоУмолчанию.Наименование, """")) КАК ЕдиницаИзмеренияНаименование,
		|	ЕСТЬNULL(ДанныеЕдиницыНоменклатуры.Код, ЕСТЬNULL(ДанныеЕдиницыПоУмолчанию.Код, """")) КАК ЕдиницаИзмеренияКод
		|ПОМЕСТИТЬ ВТДанныеНоменклатуры
		|ИЗ
		|	Справочник.Номенклатура КАК СпрНоменклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ДанныеЕдиницыПоУмолчанию
		|		ПО (ДанныеЕдиницыПоУмолчанию.Ссылка = &ЕдиницаИзмеренияКоличестваШтук)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК ДанныеЕдиницыНоменклатуры
		|		ПО СпрНоменклатура.ЕдиницаИзмерения = ДанныеЕдиницыНоменклатуры.Ссылка
		|ГДЕ
		|	ИСТИНА В
		|			(ВЫБРАТЬ ПЕРВЫЕ 1
		|				ИСТИНА
		|			ИЗ
		|				ВТОтборНоменклатуры КАК Отбор
		|			ГДЕ
		|				Отбор.Номенклатура = СпрНоменклатура.Ссылка)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура";
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Получаем дополнительные данные по упаковкам, которые необходимо будет заполнить в номенклатуре партнера.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отбор.Номенклатура КАК Номенклатура,
		|	Отбор.Упаковка КАК Упаковка
		|ПОМЕСТИТЬ ВТОтборУпаковок
		|ИЗ
		|	ВТИзмеренияРегистра КАК Отбор
		|
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ
		|(
		|	(Номенклатура),
		|	(Упаковка)
		|)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Отбор.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(Упаковки.ЕдиницаИзмерения.Наименование, """") КАК НаименованиеУпаковки,
		|	ЕСТЬNULL(&ТекстЗапросаКоэффициентаУпаковки, 1) КАК КоэффициентУпаковки
		|ПОМЕСТИТЬ ВТДанныеУпаковок
		|ИЗ
		|	ВТОтборУпаковок КАК Отбор
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО Отбор.Номенклатура = СпрНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УпаковкиЕдиницыИзмерения КАК Упаковки
		|		ПО Отбор.Упаковка = Упаковки.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Упаковка";
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаКоэффициентаУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки("Упаковки", "СпрНоменклатура"));
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Соединяем данные учетных записей с данными номенклатуры, готовим отборы для получения ставок НДС.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеУчетныхЗаписей.СтранаРегистрации КАК СтранаРегистрации,
		|	ИзмеренияРегистра.Номенклатура КАК Номенклатура,
		|	ДанныеНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ДанныеНоменклатуры.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
		|	ДанныеНоменклатуры.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод
		|ПОМЕСТИТЬ ВТДополнительныеДанные
		|ИЗ
		|	ВТДанныеУчетныхЗаписей КАК ДанныеУчетныхЗаписей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|		ПО ДанныеУчетныхЗаписей.УчетнаяЗаписьМаркетплейса = ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеНоменклатуры КАК ДанныеНоменклатуры
		|		ПО ИзмеренияРегистра.Номенклатура = ДанныеНоменклатуры.Номенклатура
		|
		|ИНДЕКСИРОВАТЬ ПО НАБОРАМ
		|(
		|	(Номенклатура,
		|	СтранаРегистрации),
		|	(Номенклатура),
		|	(СтранаРегистрации)
		|)";
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Поиск неиспользуемой в регистре номенклатуры партнера по артикулу.
	//
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеУчетныхЗаписей.Партнер КАК Партнер,
		|	ДанныеРегистра.ИдентификаторПубликации КАК ИдентификаторПубликации,
		|	МАКСИМУМ(НоменклатураПоИдентификаторуПубликации.Ссылка) КАК Ссылка
		|ПОМЕСТИТЬ ВТНоменклатураПоИдентификаторуПубликации
		|ИЗ
		|	ВТДанныеУчетныхЗаписей КАК ДанныеУчетныхЗаписей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|		ПО ДанныеУчетныхЗаписей.УчетнаяЗаписьМаркетплейса = ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК ДанныеРегистра
		|		ПО ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса = ДанныеРегистра.УчетнаяЗаписьМаркетплейса
		|			И (ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
		|			И ИзмеренияРегистра.Номенклатура = ДанныеРегистра.Номенклатура
		|			И ИзмеренияРегистра.Характеристика = ДанныеРегистра.Характеристика
		|			И ИзмеренияРегистра.Упаковка = ДанныеРегистра.Упаковка
		|			И ИзмеренияРегистра.ОбъектПубликации = ДанныеРегистра.ОбъектПубликации
		|			И ИзмеренияРегистра.НоменклатураПартнера = ДанныеРегистра.НоменклатураПартнера
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураПоИдентификаторуПубликации
		|		ПО (ДанныеУчетныхЗаписей.Партнер = НоменклатураПоИдентификаторуПубликации.Владелец)
		|			И (ДанныеРегистра.НоменклатураПартнера <> НоменклатураПоИдентификаторуПубликации.Ссылка)
		|			И (ДанныеРегистра.ИдентификаторПубликации = НоменклатураПоИдентификаторуПубликации.Артикул)
		|			И (НЕ НоменклатураПоИдентификаторуПубликации.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликацииОбъектов
		|		ПО (СтатусыПубликацииОбъектов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
		|			И (СтатусыПубликацииОбъектов.ОбъектПубликации = НЕОПРЕДЕЛЕНО)
		|			И (НоменклатураПоИдентификаторуПубликации.Ссылка = СтатусыПубликацииОбъектов.НоменклатураПартнера)
		|ГДЕ
		|	СтатусыПубликацииОбъектов.НоменклатураПартнера ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеУчетныхЗаписей.Партнер,
		|	ДанныеРегистра.ИдентификаторПубликации
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Партнер,
		|	ИдентификаторПубликации";
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Результат пакета запросов.
	//
	ТекстЗапросаПоляРегистра = ИнтеграцияСМаркетплейсамиСервер.ТекстЗапросаПолейОбъектаМетаданных(
		"РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon", "ДанныеРегистра");
	
	// Товары
	//
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеУчетныхЗаписей.Партнер КАК Партнер,
		|	ЕСТЬNULL(НоменклатураПоИдентификаторуМаркетплейса.Ссылка, ЕСТЬNULL(НоменклатураПоИдентификаторуПубликации.Ссылка, ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка))) КАК НоваяНоменклатураПартнера,
		|	&ТекстЗапросаПоляРегистра КАК Поля
		|ИЗ
		|	ВТДанныеУчетныхЗаписей КАК ДанныеУчетныхЗаписей
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|		ПО ДанныеУчетныхЗаписей.УчетнаяЗаписьМаркетплейса = ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК ДанныеРегистра
		|		ПО ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса = ДанныеРегистра.УчетнаяЗаписьМаркетплейса
		|			И (ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
		|			И ИзмеренияРегистра.Номенклатура = ДанныеРегистра.Номенклатура
		|			И ИзмеренияРегистра.Характеристика = ДанныеРегистра.Характеристика
		|			И ИзмеренияРегистра.Упаковка = ДанныеРегистра.Упаковка
		|			И ИзмеренияРегистра.ОбъектПубликации = ДанныеРегистра.ОбъектПубликации
		|			И ИзмеренияРегистра.НоменклатураПартнера = ДанныеРегистра.НоменклатураПартнера
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураПоИдентификаторуМаркетплейса
		|		ПО (ДанныеУчетныхЗаписей.Партнер = НоменклатураПоИдентификаторуМаркетплейса.Владелец)
		|			И (ДанныеРегистра.ИдентификаторОбъектаМаркетплейса = НоменклатураПоИдентификаторуМаркетплейса.Идентификатор)
		|			И (НЕ НоменклатураПоИдентификаторуМаркетплейса.ПометкаУдаления)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНоменклатураПоИдентификаторуПубликации КАК НоменклатураПоИдентификаторуПубликации
		|		ПО (ДанныеУчетныхЗаписей.Партнер = НоменклатураПоИдентификаторуПубликации.Партнер)
		|			И (ДанныеРегистра.ИдентификаторПубликации = НоменклатураПоИдентификаторуПубликации.ИдентификаторПубликации)";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаПоляРегистра КАК Поля", ТекстЗапросаПоляРегистра);
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Изображения и сертификаты
	//
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса КАК ПорядокУчетнаяЗаписьМаркетплейса,
		|	ИзмеренияРегистра.Номенклатура КАК ПорядокНоменклатура,
		|	ИзмеренияРегистра.Характеристика КАК ПорядокХарактеристика,
		|	ИзмеренияРегистра.Упаковка КАК ПорядокУпаковка,
		|	ИзмеренияРегистра.НоменклатураПартнера КАК ПорядокНоменклатураПартнера,
		|	&ТекстЗапросаПоляРегистра
		|ИЗ
		|	ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК ДанныеРегистра
		|		ПО ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса = ДанныеРегистра.УчетнаяЗаписьМаркетплейса
		|			И (ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Изображение))
		|			И ИзмеренияРегистра.Номенклатура = ДанныеРегистра.Номенклатура
		|			И ИзмеренияРегистра.Характеристика = ДанныеРегистра.Характеристика
		|			И ИзмеренияРегистра.Упаковка = ДанныеРегистра.Упаковка
		|			И ИзмеренияРегистра.НоменклатураПартнера = ДанныеРегистра.НоменклатураПартнера
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса,
		|	ИзмеренияРегистра.Номенклатура,
		|	ИзмеренияРегистра.Характеристика,
		|	ИзмеренияРегистра.Упаковка,
		|	ИзмеренияРегистра.НоменклатураПартнера,
		|	&ТекстЗапросаПоляРегистра
		|ИЗ
		|	ВТИзмеренияРегистра КАК ИзмеренияРегистра
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК ДанныеРегистра
		|		ПО ИзмеренияРегистра.УчетнаяЗаписьМаркетплейса = ДанныеРегистра.УчетнаяЗаписьМаркетплейса
		|			И (ДанныеРегистра.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Сертификат))
		|			И ИзмеренияРегистра.Номенклатура = ДанныеРегистра.Номенклатура
		|			И ИзмеренияРегистра.Характеристика = ДанныеРегистра.Характеристика
		|			И ИзмеренияРегистра.Упаковка = ДанныеРегистра.Упаковка
		|			И ИзмеренияРегистра.НоменклатураПартнера = ДанныеРегистра.НоменклатураПартнера
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПорядокУчетнаяЗаписьМаркетплейса,
		|	ПорядокНоменклатура,
		|	ПорядокХарактеристика,
		|	ПорядокУпаковка,
		|	ПорядокНоменклатураПартнера";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаПоляРегистра", ТекстЗапросаПоляРегистра);
	
	Тексты.Добавить(ТекстЗапроса);
	
	// Данные номенклатуры
	//
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДополнительныеДанные.Номенклатура КАК Номенклатура,
		|	ДополнительныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ДополнительныеДанные.ЕдиницаИзмеренияНаименование КАК ЕдиницаИзмеренияНаименование,
		|	ДополнительныеДанные.ЕдиницаИзмеренияКод КАК ЕдиницаИзмеренияКод,
		|	ЕСТЬNULL(ВЫРАЗИТЬ(&СтавкаНДС КАК Справочник.СтавкиНДС).Ставка, 0) КАК СтавкаНДСЧислом
		|ИЗ
		|	ВТДополнительныеДанные КАК ДополнительныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
		|		ПО ДополнительныеДанные.Номенклатура = СпрНоменклатура.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(
		|				&Дата,
		|				(Номенклатура, Страна) В
		|					(ВЫБРАТЬ
		|						Отбор.Номенклатура,
		|						Отбор.СтранаРегистрации
		|					ИЗ
		|						ВТДополнительныеДанные КАК Отбор)) КАК СтавкиНДСНоменклатуры
		|		ПО ДополнительныеДанные.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|			И ДополнительныеДанные.СтранаРегистрации = СтавкиНДСНоменклатуры.Страна
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(
		|				&Дата,
		|				Страна В
		|					(ВЫБРАТЬ
		|						Отбор.СтранаРегистрации
		|					ИЗ
		|						ВТДополнительныеДанные КАК Отбор)) КАК ОсновныеСтавкиНДС
		|		ПО (ДополнительныеДанные.СтранаРегистрации = СтавкиНДСНоменклатуры.Страна)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Номенклатура";
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&СтавкаНДС",
		УчетНДСУП.ТекстЗапросаСтавкаНДС(
			"&НалогообложениеНДС", "СпрНоменклатура", "&ВозвращатьМногооборотнуюТару"));
		
	Тексты.Добавить(ТекстЗапроса);
	
	// Данные упаковок
	//
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеУпаковок.Упаковка КАК Упаковка,
		|	ДанныеУпаковок.НаименованиеУпаковки КАК НаименованиеУпаковки,
		|	ДанныеУпаковок.КоэффициентУпаковки КАК КоэффициентУпаковки
		|ИЗ
		|	ВТДанныеУпаковок КАК ДанныеУпаковок
		|
		|УПОРЯДОЧИТЬ ПО
		|	Упаковка";
	
	Тексты.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = СтрСоединить(Тексты, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("ОсновнаяСтрана",                 Константы.ОсновнаяСтрана.Получить());
	Запрос.УстановитьПараметр("ЕдиницаИзмеренияКоличестваШтук", Константы.ЕдиницаИзмеренияКоличестваШтук.Получить());
	
	Запрос.УстановитьПараметр("ВозвращатьМногооборотнуюТару", Ложь);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС") Тогда
		НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаОблагаетсяНДС;
	Иначе
		НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НалогообложениеНДС", НалогообложениеНДС);
	
	УчетНДСУП.УстановитьПараметрыЗапросаСтавкиНДС(
		Запрос.Параметры, Справочники.Организации.ПустаяСсылка(), ТекущаяДатаСеанса());
	
	ПакетЗапросов = Запрос.ВыполнитьПакет();
	
	МенеджерВременныхТаблиц.Закрыть();
	
	КоличествоЗапросов = ПакетЗапросов.Количество();
	
	Результат = Новый Структура;
	
	Результат.Вставить("РезультатЗапросаДляБлокировки", ПакетЗапросов[КоличествоЗапросов - 4]);
	Результат.Вставить("ВыборкаСтатусыПубликации",      ПакетЗапросов[КоличествоЗапросов - 4].Выбрать());
	Результат.Вставить("ВыборкаИзображенияСертификаты", ПакетЗапросов[КоличествоЗапросов - 3].Выбрать());
	Результат.Вставить("ВыборкаДанныеНоменклатуры",     ПакетЗапросов[КоличествоЗапросов - 2].Выбрать());
	Результат.Вставить("ВыборкаДанныеУпаковок",         ПакетЗапросов[КоличествоЗапросов - 1].Выбрать());
	
	Возврат Результат;
	
КонецФункции

Процедура ОбработатьДанныеДляЗаполненияНоменклатурыПартнера(ПолноеИмяРегистра, ДанныеДляПерехода,
			ОбработанныеЗаписи = Неопределено, Ошибка = Неопределено)
	
	РезультатЗапросаДляБлокировки = ДанныеДляПерехода.РезультатЗапросаДляБлокировки;
	ВыборкаСтатусыПубликации      = ДанныеДляПерехода.ВыборкаСтатусыПубликации;
	ВыборкаИзображенияСертификаты = ДанныеДляПерехода.ВыборкаИзображенияСертификаты;
	
	ОбъектМетаданных = ОбщегоНазначения.ОбъектМетаданныхПоПолномуИмени(ПолноеИмяРегистра); // ОбъектМетаданныхРегистрСведений
	ИзмеренияРегистра = ОбъектМетаданных.Измерения;
	
	КонстантыСервиса = ИнтеграцияСМаркетплейсомOzonКлиентСервер.КонстантыСервиса();
	
	НачатьТранзакцию();
	
	Попытка
		
		ПричинаИсключения = "Блокировка";
		
		Блокировка = БлокировкаРегистраПоДаннымДляПереходаНаНоменклатуруПартнера(
			ИзмеренияРегистра, РезультатЗапросаДляБлокировки);
		
		Блокировка.Заблокировать();
		
		ПричинаИсключения = "ПлохиеДанные";
		
		СтатусыПубликацииСтарые = СоздатьНаборЗаписей();
		СтатусыПубликацииНовые  = СоздатьНаборЗаписей();
		
		Пока ВыборкаСтатусыПубликации.Следующий() Цикл
			
			Если ОбработанныеЗаписи <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(ОбработанныеЗаписи.Добавить(), ВыборкаСтатусыПубликации);
			КонецЕсли;
			
			НоменклатураПартнера = НоваяНоменклатураПартнераПоДаннымСтатусовПубликации(
				КонстантыСервиса, ДанныеДляПерехода, ВыборкаСтатусыПубликации);
			
			СтараяСтрока = СтатусыПубликацииСтарые.Добавить();
			ЗаполнитьЗначенияСвойств(СтараяСтрока, ВыборкаСтатусыПубликации);
			
			НоваяСтрока = СтатусыПубликацииНовые.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаСтатусыПубликации);
			
			НоваяСтрока.НоменклатураПартнера = НоменклатураПартнера;
			
			Отбор = Новый Структура;
			
			Отбор.Вставить("УчетнаяЗаписьМаркетплейса", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
			Отбор.Вставить("Номенклатура",              Справочники.Номенклатура.ПустаяСсылка());
			Отбор.Вставить("Характеристика",            Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
			Отбор.Вставить("Упаковка",                  Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
			Отбор.Вставить("НоменклатураПартнера",      Справочники.НоменклатураКонтрагентов.ПустаяСсылка());
			
			ЗаполнитьЗначенияСвойств(Отбор, ВыборкаСтатусыПубликации);
			
			Пока ВыборкаИзображенияСертификаты.НайтиСледующий(Отбор) Цикл
				
				СтараяСтрока = СтатусыПубликацииСтарые.Добавить();
				ЗаполнитьЗначенияСвойств(СтараяСтрока, ВыборкаИзображенияСертификаты);
				
				НоваяСтрока = СтатусыПубликацииНовые.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаИзображенияСертификаты);
				
				НоваяСтрока.НоменклатураПартнера = НоменклатураПартнера;
				
			КонецЦикла;
			
		КонецЦикла;
		
		СтатусыПубликацииСтарые.Записать(РежимЗамещения.Удаление);
		СтатусыПубликацииНовые.Записать(РежимЗамещения.Добавление);
		
		Если ОбработанныеЗаписи <> Неопределено Тогда
			ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки(ПолноеИмяРегистра);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбработанныеЗаписи, ДополнительныеПараметры);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Если Ошибка <> Неопределено Тогда
			Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаЗаполненияДанных;
			Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Для записей с пустой номенклатурой партнера не удалось заполнить данные по причине: %1.';
					|en = 'Cannot fill in the data for records with an empty partner product. Reason: %1.'"),
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецЕсли;
		
		Если ОбработанныеЗаписи <> Неопределено Тогда
			НаборЗаписей = СоздатьНаборЗаписей();
			
			Если ОбработанныеЗаписи.Количество() > 0 Тогда
				
				Строка = ОбработанныеЗаписи[ОбработанныеЗаписи.Количество() - 1];
				
				Для Каждого Измерение Из ИзмеренияРегистра Цикл // ОбъектМетаданныхИзмерение
					НаборЗаписей.Отбор[Измерение.Имя].Установить(Строка [Измерение.Имя]);
				КонецЦикла;
				
			КонецЕсли;
			
			//@skip-check invocation-parameter-type-intersect
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), НаборЗаписей);
			
			Если ПричинаИсключения = "ПлохиеДанные" Тогда
				
				ДополнительныеПараметры = ДополнительныеПараметрыОтметкиОбработки(ПолноеИмяРегистра);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ОбработанныеЗаписи, ДополнительныеПараметры);
				
			КонецЕсли;
		КонецЕсли;
		
	КонецПопытки;
	
КонецПроцедуры

Функция БлокировкаРегистраПоДаннымДляПереходаНаНоменклатуруПартнера(ИзмеренияРегистра, РезультатЗапроса)
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	НеблокируемыеИзмерения = Новый Массив; // Массив из Строка
	
	НеблокируемыеИзмерения.Добавить("ВидОбъектаМаркетплейса");
	НеблокируемыеИзмерения.Добавить("ОбъектПубликации");
	
	Для Каждого Измерение Из ИзмеренияРегистра Цикл
		
		ИмяИзмерения = Измерение.Имя;
		
		Если НеблокируемыеИзмерения.Найти(ИмяИзмерения) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(ИмяИзмерения, ИмяИзмерения);
		
	КонецЦикла;
	
	Возврат Блокировка;
	
КонецФункции

Функция НоваяНоменклатураПартнераПоДаннымСтатусовПубликации(КонстантыСервиса, ДанныеДляПерехода,
			ВыборкаСтатусыПубликации)
	
	ДанныеНоменклатуры = ИнтеграцияСМаркетплейсомOzonСервер.ДанныеНоменклатурыДляОбновленияПоДаннымЗаписи(
		ВыборкаСтатусыПубликации, КонстантыСервиса);
	
	ДанныеНоменклатуры.НоменклатураПартнера = ВыборкаСтатусыПубликации.НоваяНоменклатураПартнера;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Номенклатура", Справочники.Номенклатура.ПустаяСсылка());
	
	ЗаполнитьЗначенияСвойств(Отбор, ВыборкаСтатусыПубликации);
	
	ДанныеДляПерехода.ВыборкаДанныеНоменклатуры.Сбросить();
	Если ДанныеДляПерехода.ВыборкаДанныеНоменклатуры.НайтиСледующий(Отбор) Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ДанныеДляПерехода.ВыборкаДанныеНоменклатуры);
		
		ДанныеНоменклатуры.СтавкаНДС = Формат(
			ДанныеДляПерехода.ВыборкаДанныеНоменклатуры.СтавкаНДСЧислом, "ЧН=; ЧРД=.; ЧГ=");
		
		ДанныеНоменклатуры.ЕдиницаИзмерения = ДанныеДляПерехода.ВыборкаДанныеНоменклатуры.ЕдиницаИзмеренияНаименование;
		
		Если Не ЗначениеЗаполнено(ДанныеНоменклатуры.Упаковка) Тогда
			ДанныеНоменклатуры.Упаковка = ДанныеДляПерехода.ВыборкаДанныеНоменклатуры.ЕдиницаИзмерения;
		КонецЕсли;
		
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Упаковка", Справочники.УпаковкиЕдиницыИзмерения.ПустаяСсылка());
	
	ЗаполнитьЗначенияСвойств(Отбор, ВыборкаСтатусыПубликации);
	
	ДанныеДляПерехода.ВыборкаДанныеУпаковок.Сбросить();
	Если ДанныеДляПерехода.ВыборкаДанныеУпаковок.НайтиСледующий(Отбор) Тогда
		ЗаполнитьЗначенияСвойств(ДанныеНоменклатуры, ДанныеДляПерехода.ВыборкаДанныеУпаковок);
	КонецЕсли;
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.СоздатьОбновитьНоменклатуруПартнера(
		ВыборкаСтатусыПубликации.Партнер, ДанныеНоменклатуры);
		
	Если Результат.Отказ Тогда
		ВызватьИсключение(Результат.ТекстОшибки, КатегорияОшибки.ОшибкаХранимыхДанных);
	КонецЕсли;
	
	Возврат Результат.НоменклатураПартнера;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
