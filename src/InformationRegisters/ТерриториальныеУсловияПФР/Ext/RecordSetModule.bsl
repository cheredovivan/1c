#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Подсистема "Управление доступом".

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора.
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	// Логика ограничения:
	// объект доступен, если доступна организация или владелец подразделения.
	
	Для Каждого СтрокаНабора Из ЭтотОбъект Цикл
		СтрокаТаб = Таблица.Добавить();
		СтрокаТаб.ЗначениеДоступа = СтрокаНабора.СтруктурнаяЕдиница;
		Если ТипЗнч(СтрокаНабора.СтруктурнаяЕдиница) <> Тип("СправочникСсылка.Организации") Тогда
			СтрокаТаб.ЗначениеДоступа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаНабора.СтруктурнаяЕдиница, "Владелец");
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Подсистема "Управление доступом".

#КонецОбласти

#Область ОбработчикиСобытий


Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Справочники.ТерриториальныеУсловияПФР.СписокСеверныхТерриториальныхУсловий().НайтиПоЗначению(Запись.ТерриториальныеУсловияПФР) <> Неопределено Тогда
			Запись.ПрименятьСевернуюНадбавку = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктурныеЕдиницы = РегистрыБЗК.ЗначенияОтбораЗаписейНабора(ЭтотОбъект, Замещение, "СтруктурнаяЕдиница", Истина);
	Если СтруктурныеЕдиницы <> Неопределено Тогда
		ОбратныйИндекс = СтруктурныеЕдиницы.Количество();
		Пока ОбратныйИндекс > 0 Цикл
			ОбратныйИндекс = ОбратныйИндекс - 1;
			Если ТипЗнч(СтруктурныеЕдиницы[ОбратныйИндекс]) <> Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда
				СтруктурныеЕдиницы.Удалить(ОбратныйИндекс);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.ПодразделенияЗаПределамиРФ.ЗаполнитьВторичныеДанные(СтруктурныеЕдиницы);

КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли