#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Объект)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДатыПубликации(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	ТаблицаДокументов.Ссылка КАК Ссылка,
		|	ЕСТЬNULL(РасчетныеЛисткиКабинетСотрудника.ДатаПубликации, ТаблицаДокументов.Дата) КАК ДатаПубликации
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ТаблицаДокументов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатыПубликацииДокументовЭДОБЗК КАК РезультатыПубликацииДокументовЭДОБЗК
		|		ПО ТаблицаДокументов.Ссылка = РезультатыПубликацииДокументовЭДОБЗК.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РасчетныеЛисткиКабинетСотрудника КАК РасчетныеЛисткиКабинетСотрудника
		|		ПО ТаблицаДокументов.Ссылка = РасчетныеЛисткиКабинетСотрудника.ДокументЭДОБЗК
		|ГДЕ
		|	НЕ ТаблицаДокументов.Ссылка В (&МассивОбновленных)
		|	И РезультатыПубликацииДокументовЭДОБЗК.Объект ЕСТЬ NULL
		|	И ТаблицаДокументов.КатегорияДокумента <> ЗНАЧЕНИЕ(Перечисление.КатегорииДокументовЭДОБЗК.ЗаявлениеСотрудника)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.РезультатыПубликацииДокументовЭДОБЗК", "Объект", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ЭДОБЗК.ЗарегистрироватьПубликациюДокументаЭДОБЗК(Выборка.Ссылка, Выборка.ДатаПубликации, , Истина);
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



