///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область СтатусыОпераций

// Выполняет установку признака загрузки статуса регламентным заданием.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию в
//    информационной базе;
//  Значение - Булево - если Истина, данные статуса будут загружены регламентным заданием;
//
// Возвращаемое значение:
//  Булево - Истина, если признак отложенной загрузки статуса установлен, Ложь если операция не найдена.
//
Функция УстановитьОтложенноеПолучениеСтатуса(ДокументОперации, Значение) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		// Если записи нет, обновление данных не имеет смысла.
		Если Не ЗначениеЗаполнено(Запись.ИдентификаторОперации) Тогда
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецЕсли;
		
		// Включать отложенную загрузку имеет смысл только для операций,
		// которые не находятся в терминальном состоянии.
		Если Значение И Запись.СтатусОперации <> НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВПроцессе() Тогда
			ОтменитьТранзакцию();
			Возврат Ложь;
		КонецЕсли;
		
		Запись.ОтложенноеПолучениеСтатуса = Значение;
		Если Запись.ОтложенноеПолучениеСтатуса Тогда
			Запись.КоличествоПопыток = 0;
		КонецЕсли;
		
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При установке отложенного получения статуса возникли ошибки:
				|%1';
				|en = 'Errors occurred when setting the deferred status receipt:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Получает данные для определения статуса выполнения операции через Интернет-эквайринг.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий оплату или возврат
//    в информационной базе;
//  ПериодИспользования - Дата - срок действия ссылки.
//
// Возвращаемое значение:
//  Неопределено - не найдена информация по указанному документу операции.
//  Структура - параметры операции:
//    * ИдентификаторОплаты - Строка - идентификатор операции оплаты.
//    * ИдентификаторОперации - Строка - идентификатор текущей операции.
//    * ДатаЗапросаСтатуса - Строка - дата запроса статуса.
//    * СтатусОперации - Строка - текущий статус операции.
//    * ПериодИспользования - Дата - срок действия ссылки на оплату.
//    * ДатаОперации - Дата - дата выполнения операции.
//    * СуммаОперации - Число - фактическая сумма операции.
Функция ПараметрыОпределенияСтатусаОперации(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОпераций.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОпераций.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса,
		|	ИдентификаторыОпераций.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОпераций.ПериодИспользования КАК ПериодИспользования,
		|	ИдентификаторыОпераций.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОпераций.СуммаОперации КАК СуммаОперации
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("ИдентификаторОплаты",   Выборка.ИдентификаторОплаты);
		Результат.Вставить("ИдентификаторОперации", Выборка.ИдентификаторОперации);
		Результат.Вставить("ДатаЗапросаСтатуса",    Выборка.ДатаЗапросаСтатуса);
		Результат.Вставить("СтатусОперации",        Выборка.СтатусОперации);
		Результат.Вставить("ПериодИспользования",   Выборка.ПериодИспользования);
		Результат.Вставить("ДатаОперации",          Выборка.ДатаОперации);
		Результат.Вставить("СуммаОперации",         Выборка.СуммаОперации);

		// Если не было успешного получения статуса, необходимо
		// запрашивать наличие callback.
		Если Не ЗначениеЗаполнено(Результат.ДатаЗапросаСтатуса) Тогда
			Результат.ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Выполняет поиск и информации об операции в регистре и устанавливает новое значение
// идентификатора операции, статуса операции, даты операции и даты определения статуса.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий оплату
//    в информационной базе.
//  ИдентификаторОперации - Строка - идентификатор оплаты на портале.
//  ДатаОперации - Дата - дата выполнения операции.
//  СтатусОперации - Строка - новый статус операции.
//  СуммаОперации - Неопределено, Число - фактическая сумма операции.
//  ДатаЗапросаСтатуса - Неопределено, Дата - дата последней операции получения статуса.
//
Процедура ЗаписатьСтатусОперации(
		ДокументОперации,
		ИдентификаторОперации,
		ДатаОперации,
		СтатусОперации,
		СуммаОперации = Неопределено,
		ДатаЗапросаСтатуса = Неопределено) Экспорт

	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		Если Не Запись.Выбран() Тогда
			ВызватьИсключение НСтр("ru = 'Информация о документе операции не обнаружена, невозможно записать данные.';
									|en = 'Information about the transaction document is not found. Cannot save the data.'");
		КонецЕсли;
		
		Запись.ИдентификаторОперации = ИдентификаторОперации;
		Запись.ДатаОперации          = ДатаОперации;
		Запись.СтатусОперации        = СтатусОперации;
		Запись.КоличествоПопыток     = Запись.КоличествоПопыток + 1;
	
		Если СуммаОперации <> Неопределено Тогда
			Запись.СуммаОперации = СуммаОперации;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ДатаЗапросаСтатуса) Тогда
			Запись.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
		КонецЕсли;
		
		МаксимальноеКоличествоПопыток = НастройкиПлатежныхСистемСлужебный.МаксимальноеКоличествоПопытокЗапросаСтатуса(
			"ИнтернетЭквайринг",
			ЗначениеЗаполнено(Запись.ПериодИспользования));
		Если Запись.КоличествоПопыток > МаксимальноеКоличествоПопыток Тогда
			Запись.ОтложенноеПолучениеСтатуса = Ложь;
		КонецЕсли;
		
		Запись.Заполнить(Неопределено);
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the data:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаписатьСтатусОперацииОшибка(ДокументОперации, СтатусОперации) Экспорт
	
		НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		Если Не Запись.Выбран() Тогда
			ВызватьИсключение НСтр("ru = 'Информация о документе операции не обнаружена, невозможно записать данные.';
									|en = 'Information about the transaction document is not found. Cannot save the data.'");
		КонецЕсли;
		
		Запись.СтатусОперации = СтатусОперации;
		
		Запись.Заполнить(Неопределено);
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи статуса возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the status:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ИдентификаторыОпераций

// Производит поиск идентификатора заказа на оплату или возврата.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий операцию в
//    информационной базе;
//
// Возвращаемое значение:
//   Строка - идентификатор операции. Неопределено, если информация о документе не найдена.
//
Функция ИдентификаторОперации(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.Идентификатор;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Производит поиск идентификаторов операции, оплаты и участника по ссылке на документ, отражающий операцию.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий операцию в
//    информационной базе.
//
// Возвращаемое значение:
//   Неопределено - если не удалось найти информацию по указанному документу.
//   Структура - идентификаторы:
//     * ИдентификаторОплаты - Строка - идентификатор операции оплаты;
//     * ИдентификаторОперации - Строка - идентификатор текущей операции.
//     * ИдентификаторУчастника - Строка -идентификатор банка на портале.
//
Функция ИдентификаторыОперации(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОпераций.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОпераций.ИдентификаторУчастника КАК ИдентификаторУчастника
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Результат = Новый Структура;
		Результат.Вставить("ИдентификаторОплаты",    ВыборкаДетальныеЗаписи.ИдентификаторОплаты);
		Результат.Вставить("ИдентификаторОперации",  ВыборкаДетальныеЗаписи.ИдентификаторОперации);
		Результат.Вставить("ИдентификаторУчастника", ВыборкаДетальныеЗаписи.ИдентификаторУчастника);
		Возврат Результат;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Производит поиск идентификатора заказа на оплату или возврата, если идентификатор еще не был создан, создает новый.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий оплату в информационной
//    базе.
//  ПараметрыНастройкиПодключения - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//  ИдентификаторОплаты - Строка - идентификатор операции оплаты. Передается если известен на момент проведения операции.
//  КонтролироватьСтатусОперации - Булево - признак контроля потребности в генерации нового идентификатора.
//  ОтложенноеПолучениеСтатуса - Булево - признак загрузки статуса оплаты регламентным заданием.
//
// Возвращаемое значение:
//  Строка - идентификатор операции (внешний идентификатор 1С по отношению к системам Интернет-эквайринга).
//
Функция НовыйИдентификаторОперации(
		ДокументОперации,
		ПараметрыНастройкиПодключения,
		ИдентификаторОплаты = "",
		КонтролироватьСтатусОперации = Истина,
		ОтложенноеПолучениеСтатуса = Ложь) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		ТребуетсяГенерация = НастройкиПлатежныхСистемСлужебный.ТребуетсяГенерацияНовогоИдентификатора(
			Запись.Идентификатор,
			Запись.СтатусОперации);
		
		Если Не КонтролироватьСтатусОперации Или ТребуетсяГенерация Тогда
			Запись.Идентификатор              = Новый УникальныйИдентификатор;
			Запись.ДокументОперации           = ДокументОперации;
			Запись.ИдентификаторМерчанта      = ПараметрыНастройкиПодключения.ИдентификаторМерчанта;
			Запись.НастройкаПодключения       = ПараметрыНастройкиПодключения.НастройкаПодключения;
			Запись.ИдентификаторУчастника     = ПараметрыНастройкиПодключения.ИдентификаторУчастника;
			Запись.ИдентификаторОплаты        = ИдентификаторОплаты;
			Запись.ИдентификаторОперации      = "";
			Запись.СтатусОперации             = "";
			Запись.КоличествоПопыток          = 0;
			Запись.ДатаОперации               = Дата(1, 1, 1);
			Запись.СуммаОперации              = 0;
			Запись.ПериодИспользования        = Дата(1, 1, 1);
			Запись.ДатаЗапросаСтатуса         = Дата(1, 1, 1);
			Запись.ОтложенноеПолучениеСтатуса = ОтложенноеПолучениеСтатуса;
			Запись.Заполнить(Неопределено);
			Запись.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При формировании идентификатора операции возникли ошибки:
				|%1';
				|en = 'Errors occurred when generating the transaction ID:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	Возврат Запись.Идентификатор;
	
КонецФункции

#КонецОбласти

#Область ДанныеОпераций

// Получает данные по документам операций.
//
// Параметры:
//  ДокументыОпераций - Массив Из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документы отражающие операции
//    в информационной базе;
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - данные операций:
//    Ключ - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, отражающий операцию в информационной базе.
//    Значение - Структура - Новый данные операции:
//      * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, по которой была
//          выполнена операция.
//      * Идентификатор - Строка - идентификатор операции.
//      * ИдентификаторОплаты - Строка - идентификатор операции оплаты в платежной системе.
//      * ИдентификаторОперации - Строка - идентификатор текущей операции в платежной системе.
//      * ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//      * НазначениеПлатежа - Строка - назначение платежа при оплате по карте.
//      * СтатусОперации - Строка - текущий статус операции.
//      * ДатаОперации - Дата - дата выполнения операции.
//      * СуммаОперации - Число - фактическая сумма операции.
//      * Оплата - Булево - Истина, если это операция оплаты.
//      * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//
Функция ДанныеОпераций(ДокументыОпераций) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.Идентификатор КАК Идентификатор,
		|	ИдентификаторыОпераций.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОпераций.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОпераций.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОпераций.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОпераций.СуммаОперации КАК СуммаОперации,
		|	ИдентификаторыОпераций.Оплата КАК Оплата,
		|	ИдентификаторыОпераций.ДокументОперации КАК ДокументОперации,
		|	ИдентификаторыОпераций.НастройкаПодключения КАК НастройкаПодключения,
		|	ИдентификаторыОпераций.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
		|	ИдентификаторыОпераций.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ИдентификаторыОпераций.ИдентификаторУчастника КАК ИдентификаторУчастника,
		|	ИдентификаторыОпераций.НастройкаПодключения.Используется КАК Используется
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.ДокументОперации В (&ДокументыОпераций)";
	
	Запрос.УстановитьПараметр("ДокументыОпераций", ДокументыОпераций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого ДокументОперации Из ДокументыОпераций Цикл
		
		Отбор = Новый Структура("ДокументОперации", ДокументОперации);
		
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			ДанныеОперации = ИнтернетЭквайрингСлужебный.НовыйДанныеОперации();
			ЗаполнитьЗначенияСвойств(ДанныеОперации, ВыборкаДетальныеЗаписи);
		Иначе
			ДанныеОперации = Неопределено;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		
		Результат.Вставить(ДокументОперации, ДанныеОперации);
		
	КонецЦикла;

	Возврат Результат;
	
КонецФункции

// Получает исторические данные заказа на оплату по идентификатору.
//
// Параметры:
//  Идентификатор - Строка - идентификатор заказа на оплату.
//
// Возвращаемое значение:
//  Неопределено - не найдена информация по идентификатору.
//  Структура - данные заказа на оплату:
//    * ДатаОперации - Дата - дата заказа в программе;
//    * СуммаОперации - Число - сумма операции;
//    * НазначениеПлатежа - Строка - назначение платежа при оплате по карте;
//    * ДокументОперации - Неопределено, ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - объект оплаты в
//      информационной базе;
//    * ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//    * Оплата - Булево - Истина, если выполнена операция оплаты; ложь, если операция возврата.
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка подключения, через
//      которую было выполнено создание ссылки на оплату.
//    * ИдентификаторУчастника - Строка - идентификатор участника Интернет-эквайринга.
//
Функция ДанныеОперацииПоИдентификатору(Идентификатор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.ДатаОперации,
		|	ИдентификаторыОпераций.СуммаОперации,
		|	ИдентификаторыОпераций.НазначениеПлатежа,
		|	ИдентификаторыОпераций.ДокументОперации,
		|	ИдентификаторыОпераций.ИдентификаторМерчанта,
		|	ИдентификаторыОпераций.Оплата,
		|	ИдентификаторыОпераций.НастройкаПодключения,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОпераций.ИдентификаторУчастника <> """"
		|			ТОГДА ИдентификаторыОпераций.ИдентификаторУчастника
		|		ИНАЧЕ ИдентификаторыОпераций.НастройкаПодключения.ИдентификаторУчастника
		|	КОНЕЦ КАК ИдентификаторУчастника
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр(
		"Идентификатор",
		Новый УникальныйИдентификатор(Идентификатор));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Результат = Новый Структура();
		Результат.Вставить("ДатаОперации",           Выборка.ДатаОперации);
		Результат.Вставить("СуммаОперации",          Выборка.СуммаОперации);
		Результат.Вставить("НазначениеПлатежа",      Выборка.НазначениеПлатежа);
		Результат.Вставить("ДокументОперации",       Выборка.ДокументОперации);
		Результат.Вставить("ИдентификаторМерчанта",  Выборка.ИдентификаторМерчанта);
		Результат.Вставить("Оплата",                 Выборка.Оплата);
		Результат.Вставить("НастройкаПодключения",   Выборка.НастройкаПодключения);
		Результат.Вставить("ИдентификаторУчастника", Выборка.ИдентификаторУчастника);
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Записывает данные оплаты в информационную базу.
//
// Параметры:
//  Идентификатор - Строка - идентификатор заказа на оплату;
//  ЗаказНаОплату - Структура - данные заказа на оплату:
//    * ДатаОплаты - Дата - дата заказа в программе;
//    * СуммаОплаты - Число - сумма оплаты;
//    * НазначениеПлатежа - Строка - назначение платежа при оплате по карте;
//  ДокументОплаты - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - объект оплаты в информационной базе;
//  ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//
Процедура СохранитьДанныеОплаты(
		Идентификатор,
		ЗаказНаОплату,
		ДокументОплаты,
		ИдентификаторМерчанта) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОплаты);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОплаты;
		Запись.Прочитать();
		
		// Если записи нет, обновление данных не имеет смысла.
		Если (Не Запись.Выбран())
			Или Запись.Идентификатор <> Идентификатор Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось найти запись с указанным идентификатором';
									|en = 'Cannot find the record with the specified ID'");
		КонецЕсли;
		
		Запись.ДатаОперации          = ЗаказНаОплату.ДатаОплаты;
		Запись.СуммаОперации         = ЗаказНаОплату.СуммаОплаты;
		Запись.НазначениеПлатежа     = ЗаказНаОплату.НазначениеПлатежа;
		Запись.ИдентификаторМерчанта = ИдентификаторМерчанта;
		Запись.Оплата                = Истина;
		
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При сохранении данных оплаты возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the payment data:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецПроцедуры

// Записывает данные возврата в информационную базу.
//
// Параметры:
//  Идентификатор - Строка - идентификатор заказа на возврат;
//  ЗаказНаВозврат - Структура - данные заказа на возврат:
//    * ДатаВозврата - Дата - дата возврата в программе;
//    * СуммаВозврата - Число - сумма возврата;
//  ДокументВозврата - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - объект возврата в информационной базе;
//  ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ.
//
Процедура СохранитьДанныеВозврата(
		Идентификатор,
		ЗаказНаВозврат,
		ДокументВозврата,
		ИдентификаторМерчанта) Экспорт

	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументВозврата);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументВозврата;
		Запись.Прочитать();
		
		// Если записи нет, обновление данных не имеет смысла.
		Если (Не Запись.Выбран())
			Или Запись.Идентификатор <> Идентификатор Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось найти запись с указанным идентификатором';
									|en = 'Cannot find the record with the specified ID'");
		КонецЕсли;
		
		Запись.ДатаОперации          = ЗаказНаВозврат.ДатаВозврата;
		Запись.СуммаОперации         = ЗаказНаВозврат.СуммаВозврата;
		Запись.ИдентификаторМерчанта = ИдентификаторМерчанта;
		
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При сохранении данных возврата возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the return data:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

// Создает запись с новыми данными оплаты.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ отражающий оплату
//    в информационной базе;
//  СуммаОперации - Число - Содержит сумму выполненной оплаты.
//  ПараметрыНастройкиОплаты - см. ИнтернетЭквайрингСлужебный.ДанныеНастройкиОперацииПоДаннымОплаты.
//  ПараметрыОперации - см. ИнтернетЭквайрингСлужебный.НовыйДанныеОперации.
//
Процедура ЗаписатьНовыеДанныеОплаты(
	ДокументОперации,
	СуммаОперации,
	ПараметрыНастройкиОплаты,
	ПараметрыОперации) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.ДокументОперации           = ДокументОперации;
	Запись.Идентификатор              = ПараметрыОперации.Идентификатор;
	Запись.ИдентификаторОплаты        = ПараметрыОперации.ИдентификаторОплаты;
	Запись.ИдентификаторОперации      = ПараметрыОперации.ИдентификаторОперации;
	Запись.ИдентификаторУчастника     = ПараметрыОперации.ИдентификаторУчастника;
	Запись.ДатаОперации               = ПараметрыОперации.ДатаОперации;
	Запись.СуммаОперации              = СуммаОперации;
	Запись.ПериодИспользования        = Неопределено;
	Запись.ИдентификаторМерчанта      = ПараметрыОперации.ИдентификаторМерчанта;
	Запись.ДатаЗапросаСтатуса         = Неопределено;
	Запись.СтатусОперации             = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена();
	Запись.НастройкаПодключения       = ПараметрыНастройкиОплаты.НастройкаПодключения;
	Запись.ОтложенноеПолучениеСтатуса = Ложь;
	Запись.КоличествоПопыток          = 0;
	Запись.ИдентификаторУчастника     = ПараметрыНастройкиОплаты.ИдентификаторУчастника;
	Запись.Заполнить(Неопределено);
	Запись.Записать();
	
КонецПроцедуры

// Получает данные отложенных операций для дальнейшей обработки статусов.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - данные операций в разрезе участников Интернет-эквайринга:
//    * Ключ - Строка - идентификатор участника.
//    * Значение - Массив Из Структура - данные по операциям:
//      ** НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКИнтернетЭквайрингу - настройка, по которой была
//         выполнена операция.
//      ** Идентификатор - Строка - идентификатор операции.
//      ** ИдентификаторОплаты - Строка - идентификатор операции оплаты.
//      ** ИдентификаторОперации - Строка - идентификатор текущей операции.
//      ** ИдентификаторМерчанта - Строка - идентификатор мерчанта.
//      ** СтатусОперации - Строка - текущий статус операции.
//      ** СуммаОперации - Число - фактическая сумма операции.
//      ** Оплата - Булево - Истина, если это операция оплаты.
//      ** ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, отражающий операцию в
//         информационной базе.
//      ** ДатаЗапросаСтатуса - Дата - дата и время последнего запроса статуса.
//      ** ПериодИспользования - Дата - период использования.
//      ** КоличествоПопыток - Число - текущее количество попыток.
//
Функция ОтложенныеОперации() Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОпераций.Идентификатор КАК Идентификатор,
		|	ИдентификаторыОпераций.ДокументОперации КАК ДокументОперации,
		|	ИдентификаторыОпераций.Оплата КАК Оплата,
		|	ИдентификаторыОпераций.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОпераций.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОпераций.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
		|	ИдентификаторыОпераций.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса,
		|	ИдентификаторыОпераций.НастройкаПодключения КАК НастройкаПодключения,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОпераций.ИдентификаторУчастника <> """"
		|			ТОГДА ИдентификаторыОпераций.ИдентификаторУчастника
		|		ИНАЧЕ ИдентификаторыОпераций.НастройкаПодключения.ИдентификаторУчастника
		|	КОНЕЦ КАК ИдентификаторУчастника,
		|	ИдентификаторыОпераций.ПериодИспользования КАК ПериодИспользования,
		|	ИдентификаторыОпераций.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОпераций.КоличествоПопыток КАК КоличествоПопыток,
		|	ИдентификаторыОпераций.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОпераций.СуммаОперации КАК СуммаОперации
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
		|ГДЕ
		|	ИдентификаторыОпераций.ОтложенноеПолучениеСтатуса
		|	И ИдентификаторыОпераций.НастройкаПодключения.Используется
		|	И ИдентификаторыОпераций.СтатусОперации <> """"
		|ИТОГИ
		|ПО
		|	ИдентификаторУчастника";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИдентификаторУчастника = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаИдентификаторУчастника.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаИдентификаторУчастника.ИдентификаторУчастника) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаИдентификаторУчастника.Выбрать();
		
		ДанныеОпераций = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ДанныеОперации = Новый Структура;
			ДанныеОперации.Вставить("Идентификатор",         ВыборкаДетальныеЗаписи.Идентификатор);
			ДанныеОперации.Вставить("ДокументОперации",      ВыборкаДетальныеЗаписи.ДокументОперации);
			ДанныеОперации.Вставить("Оплата",                ВыборкаДетальныеЗаписи.Оплата);
			ДанныеОперации.Вставить("ИдентификаторОплаты",   ВыборкаДетальныеЗаписи.ИдентификаторОплаты);
			ДанныеОперации.Вставить("ИдентификаторОперации", ВыборкаДетальныеЗаписи.ИдентификаторОперации);
			ДанныеОперации.Вставить("ДатаЗапросаСтатуса",    ВыборкаДетальныеЗаписи.ДатаЗапросаСтатуса);
			ДанныеОперации.Вставить("НастройкаПодключения",  ВыборкаДетальныеЗаписи.НастройкаПодключения);
			ДанныеОперации.Вставить("ПериодИспользования",   ВыборкаДетальныеЗаписи.ПериодИспользования);
			ДанныеОперации.Вставить("СтатусОперации",        ВыборкаДетальныеЗаписи.СтатусОперации);
			ДанныеОперации.Вставить("КоличествоПопыток",     ВыборкаДетальныеЗаписи.КоличествоПопыток);
			
			ПараметрыОперации = ИнтернетЭквайрингСлужебный.НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(ПараметрыОперации, ВыборкаДетальныеЗаписи);

			ДанныеОперации.Вставить("ПараметрыОперации", ПараметрыОперации);
			
			ДанныеОпераций.Добавить(ДанныеОперации);
			
		КонецЦикла;
		
		Результат.Вставить(
			ВыборкаИдентификаторУчастника.ИдентификаторУчастника,
			ДанныеОпераций);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Увеличивает значение количества попыток запроса статуса.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ, который отражает операцию в
//    информационной базе;
//
Процедура УвеличитьКоличествоПопытокЗапросаСтатуса(ДокументОперации) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		Если Не ЗначениеЗаполнено(Запись.ДокументОперации) Тогда
			ВызватьИсключение НСтр("ru = 'Информация о документе операции не обнаружена, невозможно записать данные.';
									|en = 'Information about the transaction document is not found. Cannot save the data.'");
		КонецЕсли;
		
		Запись.КоличествоПопыток = Запись.КоличествоПопыток + 1;
		МаксимальноеКоличествоПопыток = НастройкиПлатежныхСистемСлужебный.МаксимальноеКоличествоПопытокЗапросаСтатуса(
			"ИнтернетЭквайринг",
			ЗначениеЗаполнено(Запись.ПериодИспользования));
		Если Запись.КоличествоПопыток > МаксимальноеКоличествоПопыток Тогда
			Запись.ОтложенноеПолучениеСтатуса = Ложь;
		КонецЕсли;
		
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the data:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ИнтернетЭквайрингСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
КонецПроцедуры

// Формирует настройки подключения для выполнения запросов.
//
// Параметры:
//  ДокументыОперации - Массив Из ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документы, по данным которых
//    необходимо получить список настроек.
//
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение - Описывает настройки подключения для переданных документов:
//    * Ключ - ОпределяемыйТип.ДокументОперацииИнтернетЭквайринга - документ по которому получен список настроек.
//    * Значение - см. ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения.
//
Функция ПараметрыНастроекПодключенияПоДокументам(ДокументыОперации) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИдентификаторыОпераций.ДокументОперации КАК ДокументОперации,
	|	ИдентификаторыОпераций.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
	|	ВЫБОР
	|		КОГДА ИдентификаторыОпераций.ИдентификаторУчастника <> """"
	|			ТОГДА ИдентификаторыОпераций.ИдентификаторУчастника
	|		ИНАЧЕ ИдентификаторыОпераций.НастройкаПодключения.ИдентификаторУчастника
	|	КОНЕЦ КАК ИдентификаторУчастника,
	|	ИдентификаторыОпераций.НастройкаПодключения.Ссылка КАК НастройкаПодключения,
	|	ИдентификаторыОпераций.НастройкаПодключения.Наименование КАК Наименование,
	|	ИдентификаторыОпераций.НастройкаПодключения.Используется КАК Используется
	|ИЗ
	|	РегистрСведений.ИдентификаторыОперацийИнтернетЭквайринга КАК ИдентификаторыОпераций
	|ГДЕ
	|	ИдентификаторыОпераций.ДокументОперации В (&ДокументыОперации)";
	
	Запрос.УстановитьПараметр("ДокументыОперации", ДокументыОперации);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыНастройкиПодключения = ИнтернетЭквайрингСлужебный.НовыйПараметрыНастройкиПодключения();
		
		ЗаполнитьЗначенияСвойств(
			ПараметрыНастройкиПодключения,
			Выборка);
		
		Результат.Вставить(Выборка.ДокументОперации, ПараметрыНастройкиПодключения);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
