#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ПередЗаписью(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() > 0 И Не РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Регистратор = Отбор.Регистратор.Значение;
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.ДокументОснование = Регистратор;
		КонецЦикла;
	КонецЕсли;
	
	РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера.ОбновляемыеСотрудникиПередЗаписью(ЭтотОбъект, Замещение);
	
	// Обновление доступа к сотрудникам по организациям
	РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера.ОрганизацииСотрудниковПередЗаписью(ЭтотОбъект, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)

	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ОбновляемыеСотрудники") Тогда
		КадровыйУчет.ОбновитьДанныеДляПодбораДоговорниковГПХ(ДополнительныеСвойства.ОбновляемыеСотрудники);
		КадровыйУчет.ОбновитьОсновныхСотрудниковФизическихЛицСотрудниковПоДоговорамГПХ(ДополнительныеСвойства.ОбновляемыеСотрудники);
	КонецЕсли;
	
	// Обновление доступа к сотрудникам по организациям
	ТаблицаАнализаИзменений = РегистрыСведений.ПериодыДействияДоговоровГражданскоПравовогоХарактера.ТаблицаАнализаИзменений(ЭтотОбъект, Замещение);
	Если ТаблицаАнализаИзменений.Количество() > 0 Тогда
		КадровыйУчет.ОбработатьИзменениеОрганизацийВНабореПоТаблицеИзменений(ТаблицаАнализаИзменений);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли