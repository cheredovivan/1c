#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПередЗаписью(Объект, Замещение) Экспорт
	
	ОбновляемыеСотрудникиПередЗаписью(Объект, Замещение);
	ОрганизацииСотрудниковПередЗаписью(Объект, Замещение);
	
КонецПроцедуры

Процедура ПодготовитьОбновлениеЗависимыхДанныхПриОбменеПриЗаписи(Объект, Замещение) Экспорт
	
	ТаблицаАнализаИзменений = ТаблицаАнализаИзменений(Объект, Замещение);
	Объект.ДополнительныеСвойства.Вставить("ТаблицаАнализаИзменений", ТаблицаАнализаИзменений);
	
КонецПроцедуры

Функция ТаблицаАнализаИзменений(НаборЗаписей, Замещение) Экспорт
	
	ТаблицаАнализаИзменений = КадровыйУчет.ТаблицаАнализаИзменений();
	Если НаборЗаписей.ДополнительныеСвойства.Свойство("ОрганизацииСотрудников") Тогда
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = НаборЗаписей.ДополнительныеСвойства.ОрганизацииСотрудников;
		Запрос.УстановитьПараметр("НаборЗаписей", НаборЗаписей.Выгрузить(, "Сотрудник,Организация"));
		Запрос.Текст =
			"ВЫБРАТЬ
			|	НаборЗаписей.Сотрудник КАК Сотрудник,
			|	НаборЗаписей.Организация КАК Организация
			|ПОМЕСТИТЬ ВТТекущиеОрганизацииСотрудников
			|ИЗ
			|	&НаборЗаписей КАК НаборЗаписей
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ЕСТЬNULL(ТекущиеОрганизацииСотрудников.Сотрудник, ОрганизацииСотрудников.Сотрудник) КАК Сотрудник,
			|	ЕСТЬNULL(ТекущиеОрганизацииСотрудников.Организация, ОрганизацииСотрудников.Организация) КАК Организация,
			|	ВЫБОР
			|		КОГДА ТекущиеОрганизацииСотрудников.Сотрудник ЕСТЬ NULL
			|			ТОГДА -1
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК ФлагИзменений
			|ИЗ
			|	ВТТекущиеОрганизацииСотрудников КАК ТекущиеОрганизацииСотрудников
			|		ПОЛНОЕ СОЕДИНЕНИЕ ВТОрганизацииСотрудников КАК ОрганизацииСотрудников
			|		ПО ТекущиеОрганизацииСотрудников.Сотрудник = ОрганизацииСотрудников.Сотрудник
			|			И ТекущиеОрганизацииСотрудников.Организация = ОрганизацииСотрудников.Организация
			|ГДЕ
			|	ЕСТЬNULL(ОрганизацииСотрудников.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) <> ЕСТЬNULL(ТекущиеОрганизацииСотрудников.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))";
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(ТаблицаАнализаИзменений.Добавить(), Выборка);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	Возврат ТаблицаАнализаИзменений;
КонецФункции

Процедура ОбновляемыеСотрудникиПередЗаписью(НаборЗаписей, Замещение) Экспорт
	
	СотрудникиНабора = Новый Соответствие;
	ИзменениеЗаписейНабора = РегистрыБЗК.ИзменениеЗаписейНабора(
		РегистрыБЗК.ЗаписиНабораИзБазыДанных(
			НаборЗаписей, Замещение),НаборЗаписей, Замещение, Истина);
	Для Каждого СтрокаИзменений Из ИзменениеЗаписейНабора Цикл
		СотрудникиНабора.Вставить(СтрокаИзменений.Сотрудник);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СотрудникиНабора) Тогда
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОбновляемыеСотрудники",
			ОбщегоНазначения.ВыгрузитьКолонку(СотрудникиНабора, "Ключ", Истина));
	КонецЕсли;
	
КонецПроцедуры

Процедура ОрганизацииСотрудниковПередЗаписью(НаборЗаписей, Замещение) Экспорт
	
	ОрганизацииСотрудников = Новый ТаблицаЗначений;
	ОрганизацииСотрудников.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ОрганизацииСотрудников.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ЗаписиНабораИзБазыДанных = РегистрыБЗК.ЗаписиНабораИзБазыДанных(НаборЗаписей, Замещение);
	Для Каждого ЗаписьНабора Из ЗаписиНабораИзБазыДанных Цикл
		ЗаполнитьЗначенияСвойств(ОрганизацииСотрудников.Добавить(), ЗаписьНабора);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ОрганизацииСотрудников) Тогда
		
		ОрганизацииСотрудников.Свернуть("Сотрудник,Организация");
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("ОрганизацииСотрудников", ОрганизацииСотрудников);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ОрганизацииСотрудников.Сотрудник КАК Сотрудник,
			|	ОрганизацииСотрудников.Организация КАК Организация
			|ПОМЕСТИТЬ ВТОрганизацииСотрудников
			|ИЗ
			|	&ОрганизацииСотрудников КАК ОрганизацииСотрудников";
		Запрос.Выполнить();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОрганизацииСотрудников", Запрос.МенеджерВременныхТаблиц);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
