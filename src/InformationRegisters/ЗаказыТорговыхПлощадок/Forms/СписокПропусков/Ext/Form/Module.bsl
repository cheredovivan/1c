
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись           = Параметры.УчетнаяЗапись;
	ИдентификаторСклада     = Параметры.ИдентификаторСклада;
	ИдентификаторОтгрузки   = Параметры.ИдентификаторОтгрузки;
	ПорядковыйНомерОтгрузки = Параметры.ПорядковыйНомерОтгрузки;
	ДатаОтгрузки            = Параметры.ДатаОтгрузки;
	ИдентификаторыПропусков.ЗагрузитьЗначения(Параметры.ИдентификаторыПропусков);
	
	УстановитьУсловноеОформление();
	ЗаполнитьНаСервере();
	
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.ФизическиеЛица) Тогда
		Элементы.СписокВодитель.КнопкаВыбора = Ложь;
	КонецЕсли;
	Если Не ПравоДоступа("Чтение", Метаданные.Справочники.ТранспортныеСредства) Тогда
		Элементы.СписокМодельАвтомобиля.КнопкаВыбора = Ложь;
		Элементы.СписокНомерАвтомобиля.КнопкаВыбора  = Ложь;
	КонецЕсли;
	
	// Временно блокируем создание пропусков возврата.
	Элементы.СписокЦельПриезда.СписокВыбора.Удалить(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОЗавершении,
			НСтр("ru = 'Данные были изменены. Сохранить изменения?';
				|en = 'The data has changed. Do you want to save the changes?'"),
			РежимДиалогаВопрос.ДаНетОтмена,
			,
			КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Копирование Тогда
		ТекущиеДанные.ИдентификаторПропуска = "";
	ИначеЕсли НоваяСтрока Тогда
		ТекущиеДанные.ИдентификаторСклада = ИдентификаторСклада;
		ТекущиеДанные.ЦельПриезда         = "FBS_DELIVERY";
		ТекущиеДанные.ДатаВремяВъезда     = ДатаОтгрузки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВодительНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТекущаяСтрока",      НайтиФизическоеЛицо(ТекущиеДанные.Водитель));
	ПараметрыОткрытия.Вставить("РежимВыбора",        Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ФизическоеЛицоЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ФизическиеЛица.ФормаВыбора",
		ПараметрыОткрытия,
		ЭтотОбъект,
		,,,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокМодельАвтомобиляНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокНомерАвтомобиляНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОчиститьСообщения();
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ОчиститьСообщения();
	Результат = СохранитьНаСервере();
	
	Если Результат <> Неопределено Тогда
		Оповестить("МП_ИзменениеСпискаПропусков", Результат, ИдентификаторОтгрузки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЦельПриезда.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.ЦельПриезда");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "FBS_DELIVERY";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отгрузка';
																					|en = 'Shipment'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЦельПриезда.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.ЦельПриезда");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "FBS_DELIVERY_RETURN";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Отгрузка и вывоз возвратов';
																					|en = 'Ship and transport returns'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокЦельПриезда.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.ЦельПриезда");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "FBS_RETURN";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'Вывоз возвратов';
																					|en = 'Transport returns'"));
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокДатаВремяВъезда.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.ЦельПриезда");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеРавно;
	ОтборЭлемента.ПравоеЗначение = "FBS_RETURN";
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	Модифицированность = Ложь;
	Список.Очистить();
	
	Отказ = Ложь;
	СписокПропусков = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьСписокПропусков(УчетнаяЗапись, ИдентификаторыПропусков, Отказ);
	
	Если Отказ Тогда
		ТекстСообщения = НСтр("ru = 'При получении списка пропусков возникли ошибки. Подробности см. в журнале регистрации.';
								|en = 'Errors occurred while getting the list of pass certificates. For details, see the Event log.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	Для Каждого ЭлементКоллекции Из СписокПропусков Цикл
		ЗаполнитьЗначенияСвойств(Список.Добавить(), ЭлементКоллекции);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНаСервере()
	
	Отказ     = Ложь;
	Результат = ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьСписокПропусков(УчетнаяЗапись, 
		ИдентификаторОтгрузки, ИдентификаторыПропусков, Список, Отказ);
	
	ИдентификаторыПропусков.ЗагрузитьЗначения(Результат);
	
	Если Отказ Тогда
		ТекстСообщения = НСтр("ru = 'При обновлении списка пропусков возникли ошибки. Подробности см. в журнале регистрации.';
								|en = 'Errors occurred while updating the list of pass certificates. For details, see the Event log.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		
		Возврат Неопределено;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ОчиститьСообщения();
		Результат = СохранитьНаСервере();
		
		Если Результат <> Неопределено Тогда
			Оповестить("МП_ИзменениеСпискаПропусков", Результат, ИдентификаторОтгрузки);
		КонецЕсли;
		
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиФизическоеЛицо(Знач Наименование)
	
	Возврат Справочники.ФизическиеЛица.НайтиПоНаименованию(Наименование, Истина);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТелефонФизическогоЛица(Знач ФизическоеЛицо)
	
	ВидыКИ = Новый Массив;
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонМобильныйФизическиеЛица);
	ВидыКИ.Добавить(Справочники.ВидыКонтактнойИнформации.ТелефонРабочийФизическиеЛица);
	
	ОтборКонтактнойИнформации = УправлениеКонтактнойИнформацией.ОтборКонтактнойИнформации();
	ОтборКонтактнойИнформации.ВидыКонтактнойИнформации = ВидыКИ;
	ОтборКонтактнойИнформации.Дата                     = ТекущаяДатаСеанса();
	
	КонтактнаяИнформация = УправлениеКонтактнойИнформацией.КонтактнаяИнформация(ФизическоеЛицо, ОтборКонтактнойИнформации);
	
	Для Каждого НайденныйКонтакт Из КонтактнаяИнформация Цикл
		СведенияОТелефоне = УправлениеКонтактнойИнформацией.СведенияОТелефоне(НайденныйКонтакт.Значение);
		
		Если ЗначениеЗаполнено(СведенияОТелефоне.Представление) Тогда
			Возврат СведенияОТелефоне.Представление;
		КонецЕсли;
	КонецЦикла;
	
	Возврат "";
	
КонецФункции

&НаКлиенте
Процедура ФизическоеЛицоЗавершениеВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Реквизиты = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(РезультатЗакрытия, "Наименование, ФИО");
		
		ТекущиеДанные.Водитель        = ?(ЗначениеЗаполнено(Реквизиты.ФИО), Реквизиты.ФИО, Реквизиты.Наименование);
		ТекущиеДанные.ТелефонВодителя = ПолучитьТелефонФизическогоЛица(РезультатЗакрытия);
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НайтиТранспортноеСредство(Знач Модель, Знач Номер)
	
	Если ЗначениеЗаполнено(Номер) Тогда
		Возврат Справочники.ТранспортныеСредства.НайтиПоКоду(Номер);
	КонецЕсли;
	
	Возврат Справочники.ТранспортныеСредства.НайтиПоРеквизиту("Марка", Модель);
	
КонецФункции

&НаКлиенте
Процедура ТранспортноеСредствоНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ТекущаяСтрока",      НайтиТранспортноеСредство(ТекущиеДанные.МодельАвтомобиля, ТекущиеДанные.НомерАвтомобиля));
	ПараметрыОткрытия.Вставить("РежимВыбора",        Истина);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ТранспортноеСредствоЗавершениеВыбора", ЭтотОбъект);
	
	ОткрытьФорму("Справочник.ТранспортныеСредства.ФормаВыбора",
		ПараметрыОткрытия,
		ЭтотОбъект,
		,,,
		ОповещениеОЗакрытии,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ТранспортноеСредствоЗавершениеВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ТекущиеДанные = Элементы.Список.ТекущиеДанные;
		Если ТекущиеДанные = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Реквизиты = ОбщегоНазначенияУТВызовСервера.ЗначенияРеквизитовОбъекта(РезультатЗакрытия, "Марка, Код");
		
		ТекущиеДанные.МодельАвтомобиля = Реквизиты.Марка;
		ТекущиеДанные.НомерАвтомобиля  = Реквизиты.Код;
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
