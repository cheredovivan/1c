
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УчетнаяЗапись         = Параметры.УчетнаяЗапись;
	ИдентификаторСклада   = Параметры.ИдентификаторСклада;
	СписокМетодовДоставки = Параметры.СписокМетодовДоставки;
	ИдентификаторОтгрузки = Параметры.ИдентификаторОтгрузки;
	
	Для Каждого ЭлементСписка Из Параметры.СписокСкладов Цикл
		Если ПустаяСтрока(ЭлементСписка.Значение) Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы.СкладТорговойПлощадки.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
	КонецЦикла;
	
	Если ПустаяСтрока(ИдентификаторОтгрузки) Тогда
		ДатаОтгрузки = ТекущаяДатаСеанса();
	КонецЕсли;
	
	ПолучитьДанныеПоТорговойПлощадке(
		УчетнаяЗапись,
		ДанныеТорговойПлощадки,
		ИнформацияОбОтгрузкеШаблон,
		Элементы.Логотип.Картинка);
	
	ИнформацияОбОтгрузкеШаблон.ИдентификаторСклада = ИдентификаторСклада;
	
	ИнформацияОбОтгрузке = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузкеШаблон);
	ИнформацияОбОтгрузке.ИдентификаторОтгрузки = ИдентификаторОтгрузки;
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, Список);
	СвойстваСписка.ТекстЗапроса =
		РегистрыСведений.ЗаказыТорговыхПлощадок.ТекстЗапросаДинамическогоСписка(Истина, Истина);
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусыЗаказовРаботе",
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе(Истина,, Истина));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусыЗаказовКонечные",
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные());
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ПометкаУдаления",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
	
	ОписаниеЭлементаДоступаКФайлам = Новый Структура;
	ОписаниеЭлементаДоступаКФайлам.Вставить("ПутьКДаннымВладельца", "ТранспортнаяНакладная");
	ОписаниеЭлементаДоступаКФайлам.Вставить("ОтображатьКоличество", Истина);
	
	ОписанияЭлементовФормы = Новый Массив;
	ОписанияЭлементовФормы.Добавить(ОписаниеЭлементаДоступаКФайлам);
	
	ПараметрыРаботыСФайлами = Новый Структура("ОписанияЭлементовФормы", ОписанияЭлементовФормы);
	
	ОбновитьПодменюПечать(ЭтотОбъект);
	
	УстановитьВидимостьЭлементов();
	
	ИзменитьПоведениеФормыНаСервере();
	
	УстановитьУсловноеОформление();
	
	НавигационнаяСсылка = "";

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьПараметрыСписка();
	
	Если Элементы.СкладТорговойПлощадки.СписокВыбора.Количество() = 0 Тогда
		ЗаполнитьСписокСкладов();
	Иначе
		Если ПустаяСтрока(ИнформацияОбОтгрузкеШаблон.ИдентификаторСклада) Тогда
			ОпределитьСкладПоУмолчанию();
		КонецЕсли;
		
		ЗаполнитьНастройкиСклада();
		
		ПолучитьИнформациюОбОтгрузке();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "МП_ВыполненаЗагрузкаЗаказовТорговойПлощадки"
			И ЗначениеЗаполнено(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		Элементы.Список.Обновить();
		ВыполнитьПроверкуОтправленийВОтгрузке();
		
	ИначеЕсли ИмяСобытия = "МП_ОбновлениеСведенийПоЗаказамИОтправлениям"
			И ЗначениеЗаполнено(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		Элементы.Список.Обновить();
		
		Если Параметр = ИнформацияОбОтгрузке.ИдентификаторОтгрузки Тогда
			Если Источник = "ОтменаЗадания" Тогда
				СообщенияПользователюПоСостояниюОтгрузки(ИнформацияОбОтгрузке, ИнформацияОбОтгрузкеШаблон.МожноРедактировать);
			Иначе
				ДатаОтгрузки = ОбщегоНазначенияКлиент.ДатаСеанса();
				
				ОчиститьИнформациюОбОтгрузке(Истина);
				ПолучитьИнформациюОбОтгрузке();
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "МП_ИзменениеСпискаПропусков"
			И Источник = ИнформацияОбОтгрузке.ИдентификаторОтгрузки Тогда
		ИнформацияОбОтгрузке.Пропуска = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметр);
		ИзменитьПоведениеФормыНаСервере();
	
	ИначеЕсли ИмяСобытия = "Запись_ЗаказКлиента"
			Или ИмяСобытия = "Запись_ПередачаТоваровХранителю"
			Или ИмяСобытия = "Запись_РеализацияТоваровУслуг" Тогда
		ЗаполнитьМассыИзНакладных();
		Элементы.Список.Обновить();
		
	ИначеЕсли ИмяСобытия = "Запись_ТранспортнаяНакладная" Тогда
		Если ТипЗнч(Параметр) <> Тип("Массив") Тогда
			ОбновитьТекстДокументыНаОсновании();
		ИначеЕсли ЗначениеЗаполнено(ЗаданиеНаПеревозку) И Параметр.Найти(ЗаданиеНаПеревозку) <> Неопределено Тогда
			ОбновитьТекстДокументыНаОсновании(Ложь);
		ИначеЕсли ЗначениеЗаполнено(ТранспортнаяНакладная) Тогда
			ОбработатьПараметрОповещения(Параметр);
		КонецЕсли;
		
		ОбновитьЗаголовокКомандыПрисоединенныхФайлов();
		
	ИначеЕсли ИмяСобытия = "Запись_ЗаданиеНаПеревозку" Тогда
		Если ЗначениеЗаполнено(Источник) И Источник = ЗаданиеНаПеревозку Тогда
			ОбновитьТекстДокументыНаОсновании(Ложь);
		Иначе
			ОбновитьТекстДокументыНаОсновании();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант"
			И (Источник = "ИспользоватьОбменБизнесСеть"
				Или Источник = "ИспользоватьУправлениеДоставкой"
				Или Источник = "ИспользоватьСервис1СДоставка"
				Или Источник = "ИспользоватьСервис1СКурьер"
				Или Источник = "ИспользоватьСервис1СКурьерика"
				Или Источник = "ИспользоватьТТН") Тогда
		ОбновитьТекстДокументыНаОсновании(, Истина);
		УстановитьВидимостьЭлементов();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант"
				И (Источник = "ИспользоватьЗаказыКлиентов"
					Или Источник = "ИспользоватьРасширенныеВозможностиЗаказаКлиента") Тогда
		УстановитьВидимостьЭлементов();
		
	ИначеЕсли ИмяСобытия = "Запись_Файл"
			И ТипЗнч(Источник) = Тип("Массив") Тогда
		Для Каждого ЭлементИсточника Из Источник Цикл
			Если ТипЗнч(ЭлементИсточника) = Тип("СправочникСсылка.ТранспортнаяНакладнаяПрисоединенныеФайлы") Тогда
				ОбновитьЗаголовокКомандыПрисоединенныхФайлов();
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура УчетнаяЗаписьПриИзменении(Элемент)

	ОчиститьСообщения();
	
	Если ИнформацияОбОтгрузке.УчетнаяЗапись <> УчетнаяЗапись Тогда
		ИдентификаторСклада = "";
		
		ПолучитьДанныеПоТорговойПлощадке(
			УчетнаяЗапись,
			ДанныеТорговойПлощадки,
			ИнформацияОбОтгрузкеШаблон,
			Элементы.Логотип.Картинка);
		
		ОчиститьИнформациюОбОтгрузке();
		ЗаполнитьСписокСкладов();
	Иначе
		ПолучитьИнформациюОбОтгрузке();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СкладТорговойПлощадкиПриИзменении(Элемент)

	ОчиститьСообщения();
	
	ИнформацияОбОтгрузкеШаблон.ИдентификаторСклада = ИдентификаторСклада;
	ИнформацияОбОтгрузке.ИдентификаторСклада = ИдентификаторСклада;
	
	ЗаполнитьНастройкиСклада();
	
	ИнформацияОбОтгрузкеШаблон.СмещениеЧасовогоПояса = 0;
	
	ОчиститьИнформациюОбОтгрузке();
	ПолучитьИнформациюОбОтгрузке();

КонецПроцедуры

&НаКлиенте
Процедура ДатаОтгрузкиПриИзменении(Элемент)

	ОчиститьСообщения();
	
	ОчиститьИнформациюОбОтгрузке();
	ПолучитьИнформациюОбОтгрузке();

КонецПроцедуры

&НаКлиенте
Процедура КоличествоГрузовыхМестПриИзменении(Элемент)

	ОчиститьСообщения();

КонецПроцедуры

&НаКлиенте
Процедура КоличествоГрузовыхМестИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)

	ОчиститьСообщения();

КонецПроцедуры

&НаКлиенте
Процедура ТекстДокументыНаОснованииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	ОткрытьКакНавигационнуюСсылку = Ложь;
	
	КомандаТТН = "ОткрытьТТН_";
	КомандаПеревозки = "ОткрытьЗаданиеНаПеревозку_";
	ДлинаКоманды = 0;
	
	ИмяОткрываемойФормы = "";
	ПараметрыОткрываемойФормы = Новый Структура("Ключ", Неопределено);
	
	Если СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, КомандаТТН) > 0 Тогда
		ДлинаКоманды = СтрДлина(КомандаТТН);
		
		ИмяОткрываемойФормы = "Документ.ТранспортнаяНакладная.ФормаОбъекта";
		ПараметрыОткрываемойФормы.Вставить("Ключ", ТранспортнаяНакладная);
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, КомандаПеревозки) > 0 Тогда
		ДлинаКоманды = СтрДлина(КомандаПеревозки);
		
		ИмяОткрываемойФормы = "Документ.ЗаданиеНаПеревозку.ФормаОбъекта";
		ПараметрыОткрываемойФормы.Вставить("Ключ", ЗаданиеНаПеревозку);
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "Оформить") > 0 Тогда
		Если Элементы.ТипТранспортногоСредства.Видимость
				И Не ЗначениеЗаполнено(ТипТранспортногоСредства) Тогда
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не выбран ""%1"".';
					|en = '""%1"" is not selected.'"), Элементы.ТипТранспортногоСредства.Заголовок);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,, "ТипТранспортногоСредства");
			Возврат;
		КонецЕсли;
		
		ОформитьДокументыПеревозки(НавигационнаяСсылкаФорматированнойСтроки);
		ОбновитьЗаголовокКомандыПрисоединенныхФайлов();
		
		Состояние(
			ДанныеТорговойПлощадки.ВидМаркетплейса,
			,
			НСтр("ru = 'Оформление документов завершено.';
				|en = 'Documents are registered.'"),
			Элементы.Логотип.Картинка);
		Возврат;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ОткрытьКакНавигационнуюСсылку И ДлинаКоманды > 0 Тогда
		ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(
			Прав(НавигационнаяСсылкаФорматированнойСтроки, СтрДлина(НавигационнаяСсылкаФорматированнойСтроки) - ДлинаКоманды));
	ИначеЕсли Не ПустаяСтрока(ИмяОткрываемойФормы)
			И (ЗначениеЗаполнено(ПараметрыОткрываемойФормы.Ключ) Или ПараметрыОткрываемойФормы.Количество() > 1) Тогда
		ОповещениеОЗакрытииФормы = Новый ОписаниеОповещения("ЗакрытиеНавигационнойСсылки", ЭтотОбъект, ПараметрыОткрываемойФормы);
		ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыОткрываемойФормы,,,,, ОповещениеОЗакрытииФормы);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ИнформацияОПунктеПриемаСсылкаНажатие(Элемент)

	ОчиститьСообщения();
	
	Если ДанныеТорговойПлощадки.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		НавигационнаяСсылкаКоманды = "https://seller-edu.ozon.ru/fbs/punkty-priema";
	Иначе
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаКоманды);

КонецПроцедуры

&НаКлиенте
Процедура АдресДоставкиСсылкаНажатие(Элемент)

	ОчиститьСообщения();
	
	Если ДанныеТорговойПлощадки.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		НавигационнаяСсылкаКоманды = "https://seller.ozon.ru/app/warehouse";
	Иначе
		Возврат;
	КонецЕсли;
	
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку(НавигационнаяСсылкаКоманды);

КонецПроцедуры

&НаКлиенте
Процедура ЗаголовокСпискаОтправленийОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ЗаголовокФормы = "";
	СписокОбъектов = Новый Массив;
	
	ПараметрыЗавершенияВыбора = Новый Структура;
	ПараметрыЗавершенияВыбора.Вставить("Действие", НавигационнаяСсылкаФорматированнойСтроки);
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСписокОтправленийЗагруженных" Тогда
		ЗаголовокФормы = НСтр("ru = 'Загруженные отправления';
								|en = 'Loaded shipments'");
		СписокОбъектов = ОтправленияЗагруженные.ВыгрузитьЗначения();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСписокОтправленийВОтгрузке" Тогда
		ЗаголовокФормы = НСтр("ru = 'Отправления в отгрузке';
								|en = 'Shipments in shipment'");
		СписокОбъектов = ОтправленияВОтгрузке.ВыгрузитьЗначения();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСписокОтправленийНезагруженных" Тогда
		ЗаголовокФормы = НСтр("ru = 'Незагруженные отправления';
								|en = 'Unloaded shipments'");
		СписокОбъектов = ОтправленияНезагруженные.ВыгрузитьЗначения();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСписокОтправленийНеВОтгрузке" Тогда
		ЗаголовокФормы = НСтр("ru = 'Исключенные отправления';
								|en = 'Excluded shipments'");
		СписокОбъектов = ОтправленияНеВОтгрузке.ВыгрузитьЗначения();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьСписокОтгрузок" Тогда
		ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Отгрузки на %1';
				|en = 'Shipments to %1'"),
			Формат(ДатаОтгрузки, "ДЛФ=DD"));
		
		Если Отгрузки.Количество() = 1
				И ПустаяСтрока(Отгрузки[0].Значение) Тогда
			ПросмотрСпискаОбъектовЗавершение(Отгрузки[0].Значение, ПараметрыЗавершенияВыбора);
			Возврат;
		Иначе
			СписокОбъектов = Отгрузки;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(СписокОбъектов) = Тип("СписокЗначений")
			И СписокОбъектов.Количество() > 0 Тогда
		
		ОповещениеОВыборе = Новый ОписаниеОповещения(
			"ПросмотрСпискаОбъектовЗавершение",
			ЭтотОбъект,
			ПараметрыЗавершенияВыбора);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Заголовок", ЗаголовокФормы);
		ПараметрыФормы.Вставить("ЗначенияДляВыбора", СписокОбъектов);
		
		ОткрытьФорму(
			"ОбщаяФорма.ВыборЗначенияИзСписка",
			ПараметрыФормы,
			ЭтотОбъект,
			,,,
			ОповещениеОВыборе,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ТипЗнч(СписокОбъектов) = Тип("Массив")
			И СписокОбъектов.Количество() > 0 Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗаголовокФормы",   ЗаголовокФормы);
		ПараметрыФормы.Вставить("МассивДокументов", СписокОбъектов);
		
		ОткрытьФорму(
			"ОбщаяФорма.СписокПроизвольныхОбъектовУП",
			ПараметрыФормы,
			ЭтотОбъект,
			,,,,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нет данных для просмотра.';
														|en = 'No data to view.'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТипТранспортногоСредстваПриИзменении(Элемент)

	ОчиститьСообщения();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Элементы.СписокПометкаСнята Или Поле = Элементы.СписокПометкаУстановлена Тогда
		СтандартнаяОбработка = Ложь;
		
		ТекущиеДанные.Пометка = Не ТекущиеДанные.Пометка;
		УстановитьПометку(ТекущиеДанные.Пометка, ТекущиеДанные.НомерОтправления);
	ИначеЕсли Поле.Имя = "СписокСостояние" Тогда
		СписокДокументов = Новый СписокЗначений;
		СписокДокументов.Добавить(ТекущиеДанные.Ссылка);
		
		Если ТекущиеДанные.ЭтоЗаказКакСчет Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьФорму("Отчет.СостояниеВыполненияДокументов.Форма.ФормаОтчета",
			Новый Структура("ВходящиеДокументы", СписокДокументов),
			ЭтотОбъект,
			Ложь);
		
	Иначе
		ПоказатьСодержимоеТекущейЯчейки(ТекущиеДанные, Поле);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьНастройкиСклада(Команда)

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись",       УчетнаяЗапись);
	ПараметрыФормы.Вставить("Партнер",             ДанныеТорговойПлощадки.Партнер);
	ПараметрыФормы.Вставить("ИдентификаторСклада", ИдентификаторСклада);
	
	ОбработчикЗакрытияФормы = Новый ОписаниеОповещения("ЗавершениеЗаполненияНастроекСпособаДоставки", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.СоответствияОбъектовМаркетплейсов.Форма.НастройкиСклада",
		ПараметрыФормы,
		ЭтотОбъект,
		,,,
		ОбработчикЗакрытияФормы);

КонецПроцедуры

&НаКлиенте
Процедура СкопироватьВБуферОбмена(Команда)

	Если ТекущийЭлемент.Имя = "СкопироватьВБуферОбменаАдресОтгрузки" Тогда
		ИнтеграцияСМаркетплейсамиКлиент.СкопироватьВБуферОбмена(Элементы.АдресДоставки.Заголовок);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)

	ОбновитьПараметрыСписка();
	ЗаполнитьМассыИзНакладных();
	
	Элементы.Список.Обновить();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКОтгрузке(Команда)

	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'У выделенных в списке заказов будет установлен статус ""К отгрузке"". Продолжить?';
						|en = 'Status ""To ship"" will be set for the orders selected in the list. Continue?'");
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ВыделенныеСтроки",ВыделенныеСтроки);
	ДопПараметры.Вставить("СтатусДокументов", "КОтгрузке");
	ДопПараметры.Вставить("ЗаголовокСтатуса", НСтр("ru = 'К отгрузке';
													|en = 'To ship'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСтатусДокументовЗавершение", ЭтотОбъект, ДопПараметры);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура СформироватьОтгрузку(Команда)

	ОчиститьСообщения();
	
	Если ОбновляетсяИнформацияОбОтгрузке Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИнформацияОбОтгрузке.УчетнаяЗапись) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбрана учетная запись.';
														|en = 'The account is not selected.'"),,, "УчетнаяЗапись");
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не выбран склад.';
														|en = 'Warehouse is not selected.'"),,, "ИдентификаторСклада");
		Возврат;
	КонецЕсли;
	
	Если МетодДоставки = Неопределено
			Или ПустаяСтрока(МетодДоставки.ИдентификаторМетодаДоставки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не удалось определить метод доставки для указанного склада. Возможно склад удален из личного кабинета.';
				|en = 'Cannot identify the delivery method for the specified warehouse. The warehouse might have been deleted from your personal account.'"),
			,,
			"ИдентификаторСклада");
		Возврат;
	КонецЕсли;
	
	Если МетодДоставки.ДоверительнаяПриемка
			И Не ЗначениеЗаполнено(КоличествоГрузовыхМест)
			И Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаУказание
			И Элементы.СтраницыДоступаКГрузовымМестам.Видимость Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено поле ""Количество грузовых мест"".';
								|en = 'The ""Number of cargo units"" field is not filled.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,, "КоличествоГрузовыхМест");
		
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		ВыполнитьФормированиеОтгрузки();
	ИначеЕсли ИнформацияОбОтгрузке.МожноРедактировать
		И (ИнформацияОбОтгрузке.ОтгрузкаСформирована Или ИнформацияОбОтгрузке.ОтгрузкаАктуальна) Тогда
		ПодтвердитьОтгрузку();
	Иначе
		Если ИнформацияОбОтгрузке.ОтгрузкаОтменена
				И ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Количество() = 0 Тогда
			ОчиститьИнформациюОбОтгрузке();
		КонецЕсли;
		
		ПолучитьИнформациюОбОтгрузке(Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьОтгрузку(Команда)

	ОчиститьСообщения();
	
	ВыполнитьОтменуОтгрузки();

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОбОтгрузке(Команда)

	ОчиститьСообщения();
	
	Если ОбновляетсяИнформацияОбОтгрузке Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьСвязьСОтгрузкой = ИнформацияОбОтгрузке.ОтгрузкаАктуальна
		И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована
		Или ИнформацияОбОтгрузке.ПовторитьЗапрос;
	
	ПолучитьИнформациюОбОтгрузке(ЗаполнитьСвязьСОтгрузкой);

КонецПроцедуры

&НаКлиенте
Процедура ВыбратьВсе(Команда)

	УстановитьПометку(Истина);

КонецПроцедуры

&НаКлиенте
Процедура СброситьВсе(Команда)

	УстановитьПометку(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокДоверенностей(Команда)

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДоступностьРаспоряженийТовары", Истина);
	
	СтруктураБыстрогоОтбора = Новый Структура;
	СтруктураБыстрогоОтбора.Вставить("Ответственный", ПользователиКлиент.ТекущийПользователь());
	
	ПараметрыФормы.Вставить("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	Если НастройкиСклада <> Неопределено И ЗначениеЗаполнено(НастройкиСклада.ПеревозчикПартнер) Тогда
		ЗначениеОтбора = Новый Структура("Партнер", НастройкиСклада.ПеревозчикПартнер);
	Иначе
		ЗначениеОтбора = Новый Структура("Партнер", ДанныеТорговойПлощадки.Партнер);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Отбор", ЗначениеОтбора);
	
	ОткрытьФорму("Документ.ДоверенностьВыданная.Форма.ФормаСпискаДокументов", ПараметрыФормы,, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПропусков(Команда)

	ОчиститьСообщения();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись",           ИнформацияОбОтгрузке.УчетнаяЗапись);
	ПараметрыФормы.Вставить("ИдентификаторСклада",     ИнформацияОбОтгрузке.ИдентификаторСклада);
	ПараметрыФормы.Вставить("ИдентификаторОтгрузки",   ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
	ПараметрыФормы.Вставить("ПорядковыйНомерОтгрузки", ИнформацияОбОтгрузке.ПорядковыйНомерОтгрузки);
	ПараметрыФормы.Вставить("ДатаОтгрузки",            ДатаОтгрузки);
	ПараметрыФормы.Вставить("ИдентификаторыПропусков", ИнформацияОбОтгрузке.Пропуска);
	
	Если Не ИнформацияОбОтгрузке.ОтгрузкаАктуальна Или Не ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
		ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ЗавершениеЗаполненияПропусков", ЭтотОбъект);
	
	ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СписокПропусков",
		ПараметрыФормы,
		ЭтотОбъект,
		,,,
		ОповещениеОЗакрытии);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭтикетокГрузовыхМест(Команда)

	ОчиститьСообщения();
	
	ПечатныеДокументы = Новый Соответствие;
	ПодготовитьПечатьЭтикетокГрузовыхМест(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	
	ПоказатьПечатныеФормы(ПечатныеДокументы);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьКомплектаДокументов(Команда)

	ОчиститьСообщения();
	
	ПечатныеДокументы = Новый Соответствие;
	
	ПодготовитьПечатьКомплектаДокументов(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	
	ПоказатьПечатныеФормы(ПечатныеДокументы);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьЛистаОтгрузки(Команда)

	ОчиститьСообщения();
	
	ПечатныеДокументы = Новый Соответствие;
	ПодготовитьПечатьЛистаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	
	ПоказатьПечатныеФормы(ПечатныеДокументы);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьШтрихкодаОтгрузки(Команда)

	ОчиститьСообщения();
	
	ПечатныеДокументы = Новый Соответствие;
	ПодготовитьПечатьШтрихкодаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	
	ПоказатьПечатныеФормы(ПечатныеДокументы);

КонецПроцедуры

&НаКлиенте
Процедура ПечатьНакладнойАкта(Команда)

	ОчиститьСообщения();
	
	ПечатныеДокументы = Новый Соответствие;
	ПодготовитьПечатьНакладнойАкта(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	
	ПоказатьПечатныеФормы(ПечатныеДокументы);

КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, ТранспортнаяНакладная);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ТранспортнаяНакладная);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// Показ снятой пометки.
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокПометкаСнята.Имя);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Пометка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Показ установленной пометки.
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СписокПометкаУстановлена.Имя);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Пометка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Условное оформление динамического списка "Список"
	СписокУсловноеОформление = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	СписокУсловноеОформление.Элементы.Очистить();
	
	// Документ отгрузки не оформлен.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Документ отгрузки не оформлен';
											|en = 'The shipment document is not registered'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("ДокументОтгрузки");
	
	СписокДокументовОтгрузки = Новый СписокЗначений;
	СписокДокументовОтгрузки.Добавить(Неопределено);
	СписокДокументовОтгрузки.Добавить(Документы.ПередачаТоваровХранителю.ПустаяСсылка());
	СписокДокументовОтгрузки.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДокументОтгрузки");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = СписокДокументовОтгрузки;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Требуется оформить>';
																			|en = '<To register>'"));
	
	// Представление даты сборки в прошлом.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Представление даты отгрузки - в прошлом';
											|en = 'Shipment date presentation: in the past'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДЛФ=D");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
	// Представление даты отгрузки сегодня.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Представление даты отгрузки - сегодня';
											|en = 'Shipment date presentation: today'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоСледующегоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", НСтр("ru = 'ДФ=ЧЧ:мм';
																			|en = 'DF=hh:mm'"));
	
	// Представление даты отгрузки в будущем.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Представление даты отгрузки - в будущем';
											|en = 'Shipment date presentation: in the future'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоСледующегоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='dd.MM.yyyy ЧЧ:мм'");
	
	// Выделение цветом просроченного заказа
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Выделение цветом просроченного отправления';
											|en = 'Highlight an overdue shipment'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("НомерОтправления");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Статус");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Просрочен");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныйДокумент);
	
	// Выделение цветом конечного статуса
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Выделение цветом конечного статуса';
											|en = 'Highlight the final status with a color'");
	
	СписокКонечныхСтатусов = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = СписокКонечныхСтатусов;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()

	ЕстьДоступНаИзменение = ПравоДоступа("Изменение", Метаданные.Документы.ЗаказКлиента);
	ИспользоватьРасширенныеВозможностиЗаказаКлиента = ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента");
	ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента    = ПолучитьФункциональнуюОпцию("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента");
	ИспользоватьУправлениеДоставкой                 = ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой");
	
	Элементы.УстановитьСтатусКОтгрузке.Видимость = ИспользоватьРасширенныеВозможностиЗаказаКлиента
		И Не ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента;
	Элементы.СписокОтгрузитьЗаказ.Видимость = ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента
		И ЕстьДоступНаИзменение;
	
	Элементы.СтраницыИнформацияПоНастройкамСклада.Видимость = ИспользоватьУправлениеДоставкой;
	Элементы.ИнформацияОПунктеПриема2.Видимость             = ИспользоватьУправлениеДоставкой;
	Элементы.АдресДоставки1.Видимость                       = ИспользоватьУправлениеДоставкой;
	Элементы.ИнформацияОНастройкахСклада.Видимость          = ИспользоватьУправлениеДоставкой;
	Элементы.ИнформацияОНастройкахСклада1.Видимость         = ИспользоватьУправлениеДоставкой;

КонецПроцедуры

&НаСервере
Процедура ИзменитьПоведениеФормыНаСервере()

	Заголовок = ПредставлениеОтгрузки(ИнформацияОбОтгрузке);
	
	Если ПустаяСтрока(Статус) Тогда
		Статус = Элементы.СтатусПустой.ПодсказкаВвода;
	КонецЕсли;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиПустой;
		Элементы.СтраницыОтправленияОтгрузки.ТекущаяСтраница = Элементы.СтраницаОтгрузкаНеСформирована;
		Элементы.СтраницыФоновогоЗадания.ТекущаяСтраница = Элементы.СтраницаФоновогоЗаданияПустая;
		
		Элементы.ОбновитьИнформациюОбОтгрузке.Видимость = Ложь;
		Элементы.КомандыПечати.Видимость = Ложь;
		Элементы.КомандыГиперссылки.Видимость = Ложь;
		
		Элементы.ТекстДокументыНаОсновании.Видимость = Ложь;
		
		Элементы.СформироватьОтгрузку.Заголовок = НСтр("ru = 'Сформировать';
														|en = 'Generate'");
		Команды.СформироватьОтгрузку.Подсказка = НСтр("ru = 'Сформировать новую отгрузку';
														|en = 'Create a new shipment'");
		
		ОбновитьТекстКоличествоДоступныхОтправлений();
	Иначе
		Если ИнформацияОбОтгрузке.ОтгрузкаОтменена Тогда
			Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиПустой;
		ИначеЕсли ИнформацияОбОтгрузке.ОтгрузкаСформирована
				И Не ИнформацияОбОтгрузке.ПовторитьЗапрос Тогда
			Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиЗаполнен;
		Иначе
			Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиОжидаетДействий;
		КонецЕсли;
		
		Элементы.СтраницыОтправленияОтгрузки.ТекущаяСтраница = Элементы.СтраницаИнформацияПоОтправлениям;
		
		Элементы.ОбновитьИнформациюОбОтгрузке.Видимость = Истина;
		Элементы.КомандыПечати.Видимость = Истина;
		Элементы.КомандыГиперссылки.Видимость = Истина;
		Элементы.ОткрытьСписокДоверенностей.Видимость =
			ПолучитьФункциональнуюОпцию("ИспользоватьДоверенностиНаПолучениеТМЦ")
			И ПравоДоступа("Просмотр", Метаданные.Документы.ДоверенностьВыданная);
		
		Если ИнформацияОбОтгрузке.МожноРедактировать Тогда
			Элементы.СформироватьОтгрузку.Заголовок = НСтр("ru = 'Подтвердить';
															|en = 'Confirm'");
			Команды.СформироватьОтгрузку.Подсказка = НСтр("ru = 'Подтвердить отгрузку и ее состав';
															|en = 'Confirm the shipment and its content'");
		Иначе
			Элементы.СформироватьОтгрузку.Заголовок = НСтр("ru = 'Обновить';
															|en = 'Refresh'");
			Команды.СформироватьОтгрузку.Подсказка = НСтр("ru = 'Обновить сведения об отгрузке';
															|en = 'Update shipment details'");
		КонецЕсли;
		
		Элементы.ДекорацияИнформацияОСтатусеОтгрузки.Видимость = Не ИнформацияОбОтгрузке.ОтгрузкаСформирована;
		
		ОбновитьТекстСписокОтправлений();
		
		Элементы.КомандыГиперссылки.Доступность = ИнформацияОбОтгрузке.ОтгрузкаСформирована;
		
		КомандыПечати = КомандыПечатиСформированнойОтгрузки(
			ПодключенныеПечатныеФормы,
			ИнформацияОбОтгрузке.ДоступныеПечатныеФормы);
		
		УстановитьДоступностьПечатныхФорм(
			Элементы,
			КомандыПечати,
			ИнформацияОбОтгрузке.ОтгрузкаСформирована И ИнформацияОбОтгрузке.ОтгрузкаАктуальна);
		
		ИмяКоманды = "ПечатьЭтикетокГрузовыхМест";
		Если КомандыПечати.НайтиПоЗначению(ИмяКоманды) <> Неопределено Тогда
			Элементы[ИмяКоманды].Доступность = Элементы[ИмяКоманды].Доступность И МетодДоставки.ДоверительнаяПриемка;
		КонецЕсли;
		
		Если ИнформацияОбОтгрузкеШаблон.МожноРедактировать Тогда
			ШаблонСтроки = "";
		Иначе
			Если ИнформацияОбОтгрузке.ОтгрузкаАктуальна Или ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
				ШаблонСтроки = НСтр("ru = 'Отгрузку можно отменить только <a href = ""%1"">в личном кабинете</a>.';
									|en = 'You can cancel the shipment only in the <a href = ""%1"">personal account</a>.'");
			Иначе
				ШаблонСтроки = НСтр("ru = 'Отгрузку можно подтвердить или отменить только <a href = ""%1"">в личном кабинете</a>.';
									|en = 'You can confirm or cancel the shipment only in the <a href = ""%1"">personal account</a>.'");
			КонецЕсли;
		КонецЕсли;
			
		Если ИнформацияОбОтгрузке.ОтгрузкаАктуальна И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
			Элементы.СтраницыДокументов.ТекущаяСтраница = Элементы.СтраницыДокументовОтгрузкаФормируется;
		Иначе
			Элементы.СтраницыДокументов.ТекущаяСтраница = Элементы.СтраницаДокументовОтгрузкаСформирована;
		КонецЕсли;
		
		Элементы.ДекорацияИнформацияОДействияхСОтгрузкой.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			ШаблонСтроки,
			ПутьКОтгрузкамВЛичномКабинете(ИнформацияОбОтгрузке.ВидМаркетплейса));
		
		Элементы.ЗагрузитьЗаказы.Видимость = Не ИнформацияОбОтгрузке.ОтгрузкаОтменена
			И ИнформацияОбОтгрузке.ОтправленияНезагруженные.Количество() > 0;
		
		КоличествоПропусков = ИнформацияОбОтгрузке.Пропуска.Количество();
		
		Элементы.ОткрытьСписокПропусков.Доступность =
			ИнформацияОбОтгрузке.ОтгрузкаСформирована И ИнформацияОбОтгрузке.ОтгрузкаАктуальна
			Или КоличествоПропусков > 0;
		
		Если ИнформацияОбОтгрузке.ТребуетсяПропуск И КоличествоПропусков = 0 Тогда
			Элементы.ОткрытьСписокПропусков.Отображение = ОтображениеКнопки.КартинкаИТекст;
			Элементы.ОткрытьСписокПропусков.ЦветТекста = ЦветаСтиля.ПросроченныеДанныеЦвет;
			Элементы.ОткрытьСписокПропусков.Заголовок = НСтр("ru = 'Оформить пропуск';
															|en = 'Issue a pass'");
		Иначе
			Элементы.ОткрытьСписокПропусков.Отображение = ОтображениеКнопки.Текст;
			Элементы.ОткрытьСписокПропусков.ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
			Элементы.ОткрытьСписокПропусков.Заголовок = НСтр("ru = 'Список пропусков';
															|en = 'List of passes'");
		КонецЕсли;
		
		Элементы.ТекстДокументыНаОсновании.Видимость = Истина;
		Элементы.ТекстДокументыНаОсновании.Доступность = ИнформацияОбОтгрузке.ОтгрузкаСформирована
			И ИнформацияОбОтгрузке.ОтгрузкаАктуальна;
		
		ОбновитьТекстДокументыНаОсновании();
	КонецЕсли;
	
	Элементы.ИнформацияПоСкладу.Заголовок = ИнформацияПоСкладу;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада) Тогда
		Элементы.СтраницыИнформацияПоНастройкамСклада.ТекущаяСтраница = Элементы.ИнформацияПоНастройкамСкладаЗаполняется;
	Иначе
		Элементы.СтраницыИнформацияПоНастройкамСклада.ТекущаяСтраница = Элементы.ИнформацияПоНастройкамСклада;
	КонецЕсли;
	
	Элементы.АдресДоставки.Видимость = Не ПустаяСтрока(ИнформацияОбОтгрузке.АдресПунктаПриема);
	
	Элементы.КоманднаяПанель.Доступность = (Элементы.СкладТорговойПлощадки.СписокВыбора.Количество() > 0);
	Элементы.СформироватьОтгрузку.Доступность = ИнформацияОбОтгрузке.ОтгрузкаСформирована
		Или ИнформацияОбОтгрузке.МожноРедактировать
		Или (ДатаОтгрузки >= НачалоДня(ТекущаяДатаСеанса()));
	
	Элементы.ОтменитьОтгрузку.Видимость = ИнформацияОбОтгрузкеШаблон.МожноРедактировать
		И (ИнформацияОбОтгрузке.ОтгрузкаСформирована Или ИнформацияОбОтгрузке.ОтгрузкаАктуальна);
	Элементы.ОтменитьОтгрузку.Доступность = Элементы.ОтменитьОтгрузку.Видимость
		И ИнформацияОбОтгрузке.МожноОтменить
		И Не ИнформацияОбОтгрузке.ПовторитьЗапрос;
	
	Элементы.СписокГруппаКомандыПометки.Видимость = ИнформацияОбОтгрузке.МожноРедактировать
		И (ИнформацияОбОтгрузке.ОтгрузкаСформирована Или ИнформацияОбОтгрузке.ОтгрузкаАктуальна);
	Элементы.СписокПометкаУстановлена.Видимость = Элементы.СписокГруппаКомандыПометки.Видимость;
	Элементы.СписокПометкаСнята.Видимость = Элементы.СписокГруппаКомандыПометки.Видимость;
	
	ИзменитьОтображениеВремениОтгрузки();
	ИзменитьВидимостьДоступностьЭлементовПоНастройкамСклада();
	
	ОбновитьТекстСписокОтгрузок();

КонецПроцедуры

&НаСервере
Процедура ИзменитьОтображениеВремениОтгрузки()

	Если Не ЗначениеЗаполнено(ВремяОтгрузки)
			Или ИнформацияОбОтгрузке.ОтгрузкаСформирована И Не ИнформацияОбОтгрузке.ОтгрузкаАктуальна Тогда
		Элементы.СтраницыВремяОтгрузки.ТекущаяСтраница = Элементы.СтраницаВремяОтгрузкиНеопределено;
	Иначе
		ДатаСеанса = ТекущаяДатаСеанса();
		
		Если ЗначениеЗаполнено(ВремяОтгрузкиРекомендуемое)
				И ДатаСеанса <= (ДатаОтгрузки + (ВремяОтгрузкиРекомендуемое - Дата(1,1,1))) Тогда
			Элементы.СтраницыВремяОтгрузки.ТекущаяСтраница = Элементы.СтраницаВремяОтгрузкиАктуальное;
		Иначе
			Элементы.СтраницыВремяОтгрузки.ТекущаяСтраница = Элементы.СтраницаВремяОтгрузкиПросроченное;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ИзменитьВидимостьДоступностьЭлементовПоНастройкамСклада()

	Элементы.ОткрытьНастройкиСклада.Доступность = Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада)
		И (НастройкиСклада <> Неопределено)
		И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована;
	
	Элементы.СтраницыДоступаКГрузовымМестам.Видимость =
		МетодДоставки <> Неопределено
		И МетодДоставки.ДоверительнаяПриемка
		И (Не ИнформацияОбОтгрузкеШаблон.МожноРедактировать И ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки)
				Или ИнформацияОбОтгрузкеШаблон.МожноРедактировать И Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки));
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Или ОтправленияВОтгрузке.Количество() = 0 Тогда
		Если ИнформацияОбОтгрузке.МожноРедактировать Тогда
			Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаТолькоПросмотр;
		Иначе
			Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаУказание;
		КонецЕсли;
	Иначе
		Если ИнформацияОбОтгрузке.МожноРедактировать
				И (ИнформацияОбОтгрузке.ОтгрузкаСформирована Или ИнформацияОбОтгрузке.ОтгрузкаАктуальна) Тогда
			Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаУказание;
		Иначе
			Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаТолькоПросмотр;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбработатьПараметрОповещения(Параметр, ОбновитьТекстДокументыНаОсновании = Истина)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	Документ.ТранспортнаяНакладная.ДокументыОснования КАК ТранспортнаяНакладнаяДокументыОснования
		|ГДЕ
		|	ТранспортнаяНакладнаяДокументыОснования.Ссылка = &ТранспортнаяНакладная
		|	И ТранспортнаяНакладнаяДокументыОснования.ДокументОснование В(&ДокументыОснования)";
	
	Запрос.УстановитьПараметр("ТранспортнаяНакладная", ТранспортнаяНакладная);
	Запрос.УстановитьПараметр("ДокументыОснования",    Параметр);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если ОбновитьТекстДокументыНаОсновании Тогда
		ОбновитьТекстДокументыНаОсновании(Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеНавигационнойСсылки(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура") И ЗначениеЗаполнено(ДополнительныеПараметры.Ключ) Тогда
		ОбновитьТекстДокументыНаОсновании();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстКоличествоДоступныхОтправлений()

	Если ИнформацияОбОтгрузке.КоличествоОтправленийВОтгрузке = 0 Тогда
		Шаблон = НСтр("ru = 'Нет доступных к отгрузке отправлений.';
						|en = 'There are no shipments available for shipment.'");
	ИначеЕсли Число(Прав(ИнформацияОбОтгрузке.КоличествоОтправленийВОтгрузке, 1)) = 1 Тогда
		Шаблон = НСтр("ru = 'К отгрузке доступно 1 отправление.';
						|en = 'There is 1 shipment available for shipment.'");
	ИначеЕсли Число(Прав(ИнформацияОбОтгрузке.КоличествоОтправленийВОтгрузке, 1)) <= 4 Тогда
		Шаблон = НСтр("ru = 'К отгрузке доступно %1 отправления.';
						|en = 'There are %1 shipments available for shipment.'");
	Иначе
		Шаблон = НСтр("ru = 'К отгрузке доступно %1 отправлений.';
						|en = 'There are %1 shipments available for shipment.'");
	КонецЕсли;
	
	Элементы.ИнформацияОКоличествеОтправлений.Заголовок =
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			ИнформацияОбОтгрузке.КоличествоОтправленийВОтгрузке);

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстСписокОтгрузок()

	Элементы.СтраницыСписокОтгрузок.ТекущаяСтраница = Элементы.СтраницаИнформацияОбОтгрузках;
	
	КоличествоОтгрузок = Отгрузки.Количество();
	
	Если КоличествоОтгрузок > 0 Тогда
		Если КоличествоОтгрузок = 1 И ПустаяСтрока(Отгрузки[0].Значение) Тогда
			ШаблонЗаголовка = НСтр("ru = 'Перейти к новой отгрузке';
									|en = 'Go to new shipment'");
		Иначе
			ШаблонЗаголовка = НСтр("ru = 'Перейти к отгрузке (еще %1)';
									|en = 'Go to shipment (%1 more)'");
		КонецЕсли;
		
		ШаблонСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"<a href = ""ОткрытьСписокОтгрузок"">%1</a>",
			ШаблонЗаголовка);
	Иначе
		ШаблонСтроки = "";
	КонецЕсли;
	
	Элементы.ЗаголовокСпискаОтгрузок.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
		ШаблонСтроки,
		КоличествоОтгрузок);

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстСписокОтправлений(ПоказыватьИсключенияОтправлений = Ложь)

	Если ИнформацияОбОтгрузке.ОтгрузкаСформирована Или ИнформацияОбОтгрузке.ОтгрузкаАктуальна Тогда
		КоличествоОтправленийВОтгрузке     = ОтправленияВОтгрузке.Количество();
		КоличествоОтправленийНезагруженных = ОтправленияНезагруженные.Количество();
		КоличествоОтправленийНеВОтгрузке   = ОтправленияНеВОтгрузке.Количество();
		
		Если ПоказыватьИсключенияОтправлений
			И ИнформацияОбОтгрузке.ОтгрузкаСформирована И КоличествоОтправленийНеВОтгрузке > 0 Тогда
			ДополнениеШаблона = ", " + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'исключено %1';
					|en = 'excluded %1'"),
				"<a href = ""ОткрытьСписокОтправленийНеВОтгрузке"">%4</a>");
		Иначе
			ДополнениеШаблона = "";
		КонецЕсли;
		
		Если КоличествоОтправленийНезагруженных > 0 Тогда
			ШаблонСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Список отправлений (%2 из %1, не загружено %3%4):';
					|en = 'Shipment list (%2 out of %1, not imported %3%4)'"),
				"<a href = ""ОткрытьСписокОтправленийВОтгрузке"">%1</a>",
				"<a href = ""ОткрытьСписокОтправленийЗагруженных"">%2</a>",
				"<a href = ""ОткрытьСписокОтправленийНезагруженных"">%3</a>",
				ДополнениеШаблона);
		ИначеЕсли КоличествоОтправленийВОтгрузке > 0 Тогда
			ШаблонСтроки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Список отправлений (%1%2):';
					|en = 'Shipment list (%1%2):'"),
				"<a href = ""ОткрытьСписокОтправленийВОтгрузке"">%1</a>",
				ДополнениеШаблона);
		Иначе
			ШаблонСтроки = НСтр("ru = 'Список отправлений:';
								|en = 'Shipment list:'");
		КонецЕсли;
		
		Элементы.ЗаголовокСпискаОтправлений.Заголовок = СтроковыеФункции.ФорматированнаяСтрока(
			ШаблонСтроки,
			КоличествоОтправленийВОтгрузке,
			КоличествоОтправленийВОтгрузке - КоличествоОтправленийНезагруженных,
			КоличествоОтправленийНезагруженных,
			КоличествоОтправленийНеВОтгрузке);
	Иначе
		Элементы.ЗаголовокСпискаОтправлений.Заголовок = НСтр("ru = 'Список отправлений (неутвержденный):';
															|en = 'Shipment list (unapproved):'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьТекстДокументыНаОсновании(ЗаполнитьСсылки = Истина, ОбновитьПодключаемыеКоманды = Ложь)

	ДанныеДокументов = ДанныеДокументовПеревозки(
		ЗаданиеНаПеревозку,
		ТранспортнаяНакладная,
		ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
		ЗаполнитьСсылки);
	
	Если ЗначениеЗаполнено(ДанныеДокументов.ТТН_ТипТС) Тогда
		ТипТранспортногоСредства = ДанныеДокументов.ТТН_ТипТС;
	ИначеЕсли ЗначениеЗаполнено(ДанныеДокументов.ЗаданиеНаПеревозку_ТипТС) Тогда
		ТипТранспортногоСредства = ДанныеДокументов.ЗаданиеНаПеревозку_ТипТС;
	КонецЕсли;
	
	ТекстыПоляДокументыНаОсновании.Очистить();
	
	Если НастройкиСклада <> Неопределено Тогда
		ТипИсполнителя =
			РегистрыСведений.ЗаказыТорговыхПлощадок.ТипИсполнителяЗаданияНаПеревозку(НастройкиСклада.СпособДоставки);
		
		ДоступнаДоставка = ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой")
			И (ТипИсполнителя = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.НашаТранспортнаяСлужба
				Или ТипИсполнителя = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик
					И ПолучитьФункциональнуюОпцию("ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками"));
	Иначе
		ДоступнаДоставка = Ложь;
	КонецЕсли;
	
	ДоступенПросмотрДоставки = ПравоДоступа("Просмотр", Метаданные.Документы.ЗаданиеНаПеревозку);
	ДоступнаДоставка = ДоступнаДоставка И ДоступенПросмотрДоставки;
	
	ДоступенПросмотрТТН = ПравоДоступа("Просмотр", Метаданные.Документы.ТранспортнаяНакладная);
	ИспользоватьТТН = ПолучитьФункциональнуюОпцию("ИспользоватьТТН");
	ДоступнаТТН = ИспользоватьТТН И (Не ДоступнаДоставка Или ДоступнаДоставка И ДоступенПросмотрДоставки);
	
	ДоступноДобавлениеТТН       = ДоступенПросмотрТТН И ДоступнаТТН;
	ДоступноДобавлениеПеревозки = ДоступенПросмотрДоставки И ДоступнаДоставка;
	
	Если ДоступноДобавлениеТТН И ДоступноДобавлениеПеревозки Тогда
		ПредставлениеДокументаШаблон = НСтр("ru = 'Оформить задание на перевозку и ТТН';
											|en = 'Register an itinerary and a waybill'");
		НавигационнаяСсылкаДокументаШаблон = "ОформитьЗаданиеНаПеревозку_";
	ИначеЕсли ДоступноДобавлениеТТН Тогда
		ПредставлениеДокументаШаблон = НСтр("ru = 'Оформить ТТН';
											|en = 'Create waybill'");
		НавигационнаяСсылкаДокументаШаблон = "ОформитьТТН_";
	ИначеЕсли ДоступноДобавлениеПеревозки Тогда
		ПредставлениеДокументаШаблон = НСтр("ru = 'Оформить задание на перевозку';
											|en = 'Register an itinerary'");
		НавигационнаяСсылкаДокументаШаблон = "ОформитьЗаданиеНаПеревозку_";
	Иначе
		ПредставлениеДокументаШаблон = "";
		НавигационнаяСсылкаДокументаШаблон = "";
	КонецЕсли;
	
	ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
	
	Если ЗначениеЗаполнено(ТранспортнаяНакладная) И ИспользоватьТТН Тогда
		ШапкаДокумента = Новый Структура;
		ШапкаДокумента.Вставить("Дата",  ДанныеДокументов.ТТН_Дата);
		ШапкаДокумента.Вставить("Номер", ДанныеДокументов.ТТН_Номер);
		
		ПредставлениеДокумента = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
									ШапкаДокумента,
									НСтр("ru = 'ТТН';
										|en = 'Waybill'"));
			
		Если ДоступенПросмотрТТН И ДоступнаТТН Тогда
			Если Не ДанныеДокументов.ТТН_Проведен Тогда
				ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
			КонецЕсли;
			
			НавигационнаяСсылкаДокумента = "ОткрытьТТН_" + ПолучитьНавигационнуюСсылку(ТранспортнаяНакладная);
		Иначе
			НавигационнаяСсылкаДокумента = "";
		КонецЕсли;
		
	ИначеЕсли ИнформацияОбОтгрузке.ОтгрузкаСформирована
			И ИспользоватьТТН
			И Не ДанныеДокументов.ЗаданиеНаПеревозку_ПометкаУдаления Тогда
		ПредставлениеДокумента = НСтр("ru = 'ТТН не оформлена';
										|en = 'The waybill is not registered'");
		НавигационнаяСсылкаДокумента = "";
		
	Иначе
		ПредставлениеДокумента = "";
		НавигационнаяСсылкаДокумента = "";
	КонецЕсли;
	
	Если ПустаяСтрока(ПредставлениеДокумента) Тогда
		ТекстДокументаТТН = "";
	Иначе
		Если ПустаяСтрока(НавигационнаяСсылкаДокумента) Тогда
			ТекстДокументаТТН = ПредставлениеДокумента;
		Иначе
			ТекстДокументаТТН = Новый ФорматированнаяСтрока(ПредставлениеДокумента,, ЦветТекста,, НавигационнаяСсылкаДокумента);
		КонецЕсли;
	КонецЕсли;
	
	Если ДоступнаДоставка
			И ЗначениеЗаполнено(ЗаданиеНаПеревозку)
			И (Не ДанныеДокументов.ЗаданиеНаПеревозку_ПометкаУдаления
				Или Не ПустаяСтрока(ТекстДокументаТТН)) Тогда
		ШапкаДокумента = Новый Структура;
		ШапкаДокумента.Вставить("Дата",  ДанныеДокументов.ЗаданиеНаПеревозку_Дата);
		ШапкаДокумента.Вставить("Номер", ДанныеДокументов.ЗаданиеНаПеревозку_Номер);
		
		ПредставлениеДокумента = ОбщегоНазначенияУТКлиентСервер.СформироватьЗаголовокДокумента(
									ШапкаДокумента,
									НСтр("ru = 'Задание на перевозку';
										|en = 'Itinerary'"));
		
		Если ДоступнаДоставка Тогда
			Если ДанныеДокументов.ЗаданиеНаПеревозку_Проведен Тогда
				ЦветТекста = ЦветаСтиля.ГиперссылкаЦвет;
			Иначе
				ЦветТекста = ЦветаСтиля.ПоясняющийТекст;
			КонецЕсли;
			
			НавигационнаяСсылкаДокумента = "ОткрытьЗаданиеНаПеревозку_" + ПолучитьНавигационнуюСсылку(ЗаданиеНаПеревозку);
			ТекстДокумента = Новый ФорматированнаяСтрока(ПредставлениеДокумента,, ЦветТекста,, НавигационнаяСсылкаДокумента);
		Иначе
			ТекстДокумента = ПредставлениеДокумента;
		КонецЕсли;
		
		ТекстыПоляДокументыНаОсновании.Добавить(ТекстДокумента);
		
	ИначеЕсли Не ПустаяСтрока(ТекстДокументаТТН) И ДоступнаДоставка Тогда
		ТекстДокумента = НСтр("ru = 'Задание на перевозку не оформлено';
								|en = 'The itinerary is not registered'");
		ТекстыПоляДокументыНаОсновании.Добавить(ТекстДокумента);
		
	ИначеЕсли ПустаяСтрока(ТекстДокументаТТН) И Не ПустаяСтрока(НавигационнаяСсылкаДокументаШаблон) Тогда
		ТекстДокумента = Новый ФорматированнаяСтрока(ПредставлениеДокументаШаблон,
			,
			ЦветТекста,
			,
			НавигационнаяСсылкаДокументаШаблон);
		ТекстыПоляДокументыНаОсновании.Добавить(ТекстДокумента);
	КонецЕсли;
	
	Если Не ПустаяСтрока(ТекстДокументаТТН) Тогда
		ТекстыПоляДокументыНаОсновании.Добавить(ТекстДокументаТТН);
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ТранспортнаяНакладная) Или ЗначениеЗаполнено(ЗаданиеНаПеревозку))
			И ЗначениеЗаполнено(НавигационнаяСсылкаДокументаШаблон)
			И (ДоступнаДоставка И Не ДанныеДокументов.ЗаданиеНаПеревозку_ПометкаУдаления
				Или ИспользоватьТТН И Не ДанныеДокументов.ТТН_ПометкаУдаления) Тогда
		ТекстДокумента =
			СтроковыеФункции.ФорматированнаяСтрока(НСтр("ru = '<a href=""%1"" ><img src=""%2"">Перезаполнить</a>';
														|en = '<a href=""%1"" ><img src=""%2"">Refill</a>'"),
				НавигационнаяСсылкаДокументаШаблон,
				"ЗаполнитьПоОснованию");
		ТекстыПоляДокументыНаОсновании.Добавить(ТекстДокумента);
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.ОбновитьТекстДокументыНаОсновании(ЭтотОбъект);
	
	Элементы.ТипТранспортногоСредства.Видимость = ДоступноДобавлениеПеревозки
		И Элементы.ТекстДокументыНаОсновании.Видимость;
	
	Элементы.УправлениеПрисоединеннымиФайламиОткрытьСписок0.Видимость = ЗначениеЗаполнено(ТранспортнаяНакладная)
		И Не ДанныеДокументов.ТТН_ПометкаУдаления;
	
	Если ОбновитьПодключаемыеКоманды Тогда
		ОбновитьПодменюПечать(ЭтотОбъект);
	КонецЕсли;
	
	ДоступностьПечатныхФорм = ИнформацияОбОтгрузке.ОтгрузкаСформирована И ИнформацияОбОтгрузке.ОтгрузкаАктуальна;
	
	ТипПараметра = Строка(ТипЗнч(ТранспортнаяНакладная));
	
	УстановитьДоступностьПечатныхФорм(
		Элементы,
		ПодключенныеПечатныеФормы,
		ДоступностьПечатныхФорм И ЗначениеЗаполнено(ТранспортнаяНакладная),
		ТипПараметра);
	
	ТипПараметра = Строка(ТипЗнч(ЗаданиеНаПеревозку));
	
	УстановитьДоступностьПечатныхФорм(
		Элементы,
		ПодключенныеПечатныеФормы,
		ДоступностьПечатныхФорм И ЗначениеЗаполнено(ЗаданиеНаПеревозку),
		ТипПараметра);

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗаголовокКомандыПрисоединенныхФайлов()

	Если Элементы.УправлениеПрисоединеннымиФайламиОткрытьСписок0.Видимость Тогда
		РаботаСФайламиКлиент.ОбработкаОповещения(ЭтотОбъект, "Запись_Файл");
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьДанныеПоТорговойПлощадке(УчетнаяЗапись, ДанныеТорговойПлощадки,
			ИнформацияОбОтгрузкеШаблон, Логотип)

	ДанныеТорговойПлощадки = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись);
	ИнформацияОбОтгрузкеШаблон = РегистрыСведений.ЗаказыТорговыхПлощадок.НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДанныеТорговойПлощадки.ВидМаркетплейса);

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьСодержимоеТекущейЯчейки(ТекущиеДанные, Поле)

	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументОтгрузки)
			И (Поле.Имя = "СписокНомерОтправления"
				Или Поле.Имя = "СписокСтатус"
				Или Поле.Имя = "СписокДокументОтгрузки") Тогда
		ПоказатьЗначение(, ТекущиеДанные.ДокументОтгрузки);
		
	Иначе
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПараметрыСписка()

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "УчетнаяЗапись",
		ИнформацияОбОтгрузке.УчетнаяЗапись);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ИдентификаторСклада",
		ИнформацияОбОтгрузке.ИдентификаторСклада);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ОтправленияВОтгрузке",
		ОтправленияВОтгрузке);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ЭтоУстановкаПометокДляВсехПозиций",
		ЭтоУстановкаПометокДляВсехПозиций);
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НомераОтправленийВыбранные",
		ОтправленияДляФлаговВыбранные.ВыгрузитьЗначения());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "НомераОтправленийИсключенные",
		ОтправленияДляФлаговИсключенные.ВыгрузитьЗначения());

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометку(Пометка, НомерОтправления = "")

	ОчиститьСообщения();
	
	Если ПустаяСтрока(НомерОтправления) Тогда
		ОтправленияДляФлаговВыбранные.Очистить();
		ОтправленияДляФлаговИсключенные.Очистить();
		ЭтоУстановкаПометокДляВсехПозиций = Пометка;
		
		ОбновитьПараметрыСписка();
		Элементы.Список.Обновить();
	Иначе
		ПараметрыНомераОтправленийВыбранные =
			Список.Параметры.Элементы.Найти("НомераОтправленийВыбранные").Значение;
		ПараметрыНомераОтправленийИсключенные =
			Список.Параметры.Элементы.Найти("НомераОтправленийИсключенные").Значение;
		
		Если Пометка Тогда
			ПозицияУдаления = ОтправленияДляФлаговИсключенные.НайтиПоЗначению(НомерОтправления);
			ИндексПозицииУдаления = ПараметрыНомераОтправленийИсключенные.Найти(НомерОтправления);
			
			Если ПозицияУдаления <> Неопределено Тогда
				ОтправленияДляФлаговИсключенные.Удалить(ПозицияУдаления);
				ПараметрыНомераОтправленийИсключенные.Удалить(ИндексПозицииУдаления);
			Иначе
				ОтправленияДляФлаговВыбранные.Добавить(НомерОтправления);
				ПараметрыНомераОтправленийВыбранные.Добавить(НомерОтправления);
			КонецЕсли;
		Иначе
			ПозицияУдаления = ОтправленияДляФлаговВыбранные.НайтиПоЗначению(НомерОтправления);
			ИндексПозицииУдаления = ПараметрыНомераОтправленийВыбранные.Найти(НомерОтправления);
			
			Если ПозицияУдаления <> Неопределено Тогда
				ОтправленияДляФлаговВыбранные.Удалить(ПозицияУдаления);
				ПараметрыНомераОтправленийВыбранные.Удалить(ИндексПозицииУдаления);
			Иначе
				ОтправленияДляФлаговИсключенные.Добавить(НомерОтправления);
				ПараметрыНомераОтправленийИсключенные.Добавить(НомерОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияРаспределенияЗапасовПоЗаказам()

	ОбеспечениеВДокументахКлиент.ПроверитьВыполнениеЗаданияРаспределенияЗапасовПоЗаказам(ЭтотОбъект, Элементы.Список);

КонецПроцедуры

#Область СкладыСлужебные

&НаКлиенте
Процедура ЗаполнитьСписокСкладов()

	Элементы.СкладТорговойПлощадки.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ЗаполнитьНастройкиСклада();
		Возврат;
	КонецЕсли;
	
	Элементы.СтраницыИнформацияПоНастройкамСклада.ТекущаяСтраница = Элементы.ИнформацияПоНастройкамСкладаЗаполняется;
	Элементы.СтраницыВремяОтгрузки.ТекущаяСтраница = Элементы.СтраницаВремяОтгрузкиНеопределено;
	Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиПроверяется;
	Элементы.СтраницыСписокОтгрузок.ТекущаяСтраница = Элементы.СтраницаОбновлениеОтгрузки;
	Элементы.СтраницыОтправленияОтгрузки.ТекущаяСтраница = Элементы.СтраницаОтгрузкаНеСформирована;
	Элементы.СтраницыФоновогоЗадания.ТекущаяСтраница = Элементы.СтраницаФоновогоЗаданияОбновляетсяСписокСкладов;
	
	Элементы.ОбновитьИнформациюОбОтгрузке.Видимость = Ложь;
	Элементы.КомандыГиперссылки.Видимость = Ложь;
	Элементы.ОткрытьНастройкиСклада.Доступность = Ложь;
	Элементы.КоманднаяПанель.Доступность = Ложь;
	
	Элементы.СкладТорговойПлощадки.ПодсказкаВвода = НСтр("ru = '<Заполняется список...>';
														|en = '<Filling the list...>'");
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучениеСпискаСкладовЗавершение", ЭтотОбъект);
	
	ДлительнаяОперация = ЗапуститьПолучениеСпискаСкладов(
		УчетнаяЗапись,
		ДанныеТорговойПлощадки.ВидМаркетплейса,
		УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьПолучениеСпискаСкладов(УчетнаяЗапись, ВидМаркетплейса, Знач УникальныйИдентификатор = Неопределено)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Получение списка складов.';
			|en = '%1. Get a warehouse list.'"),
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса));
	
	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.ПолучитьСкладыСервиса";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		ИмяМетода,
		ВидМаркетплейса,
		УчетнаяЗапись);

КонецФункции

&НаКлиенте
Процедура ПолучениеСпискаСкладовЗавершение(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт

	ОчиститьСообщения();
	
	Элементы.СкладТорговойПлощадки.СписокВыбора.Очистить();
	СписокМетодовДоставки = Неопределено;
	
	Если РезультатЗадания <> Неопределено
			И РезультатЗадания.Статус = "Выполнено" Тогда
		Ошибка = ЗаполнитьСписокСкладовИМетодовДоставки(РезультатЗадания.АдресРезультата);
		
		ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Элементы.Логотип.Картинка);
		
	Иначе
		ШаблонОшибки = НСтр("ru = 'Не удалось получить склады учетной записи ""%1"" по причине: %2';
							|en = 'Cannot get the ""%1"" account warehouses due to: %2'");
		ПредставлениеНеизвестнойОшибки = НСтр("ru = 'Неизвестная ошибка выполнения операции';
												|en = 'Unknown operation error'");
		ПодробноеПредставлениеОшибки = ?(РезультатЗадания = Неопределено,
			ПредставлениеНеизвестнойОшибки,
			РезультатЗадания.ПодробноеПредставлениеОшибки);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			УчетнаяЗапись,
			ПодробноеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		ОпределитьСкладПоУмолчанию();
	КонецЕсли;
	
	Если Элементы.СкладТорговойПлощадки.СписокВыбора.Количество() = 0 Тогда
		Элементы.СкладТорговойПлощадки.ПодсказкаВвода = НСтр("ru = '<Не удалось получить список складов>';
															|en = '<Cannot get the list of warehouses>'");
	Иначе
		Элементы.СкладТорговойПлощадки.ПодсказкаВвода = НСтр("ru = '<Необходимо выбрать склад>';
															|en = '<Select a warehouse>'");
	КонецЕсли;
	
	ЗаполнитьНастройкиСклада();
	
	ПолучитьИнформациюОбОтгрузке();

КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокСкладовИМетодовДоставки(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если Результат = Неопределено Тогда
		Возврат ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСервер.ЗаполнитьСписокСкладовИМетодовДоставки(
		ДанныеТорговойПлощадки.ВидМаркетплейса,
		Результат.ТаблицаСкладов,
		Элементы.СкладТорговойПлощадки.СписокВыбора,
		СписокМетодовДоставки);
	
	Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада)
			И Элементы.СкладТорговойПлощадки.СписокВыбора.НайтиПоЗначению(ИнформацияОбОтгрузке.ИдентификаторСклада) = Неопределено Тогда
		Элементы.СкладТорговойПлощадки.СписокВыбора.Вставить(
			0,
			ИнформацияОбОтгрузке.ИдентификаторСклада,
			НСтр("ru = '<Неизвестный склад>';
				|en = '<Unknown warehouse>'"));
	КонецЕсли;
	
	Возврат Результат.Ошибка; // ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка

КонецФункции

&НаКлиенте
Процедура ЗавершениеЗаполненияНастроекСпособаДоставки(Результат, ДополнительныеПараметры = Неопределено) Экспорт

	ЗаполнитьНастройкиСклада();

КонецПроцедуры

&НаКлиенте
Процедура ОпределитьСкладПоУмолчанию()

	Если ПустаяСтрока(ИдентификаторСклада) Тогда
		Если Элементы.СкладТорговойПлощадки.СписокВыбора.Количество() > 1 Тогда
			КоличествоАктивныхСкладов = 0;
			
			Если СписокМетодовДоставки <> Неопределено Тогда
				Для Каждого МетодДоставки Из СписокМетодовДоставки Цикл
					Если МетодДоставки.Значение.АктивныйСклад Тогда
						ИдентификаторСклада = СтрЗаменить(МетодДоставки.Ключ, "Склад_", "");
						КоличествоАктивныхСкладов = КоличествоАктивныхСкладов + 1;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если КоличествоАктивныхСкладов > 1 Тогда
				ИдентификаторСклада = "";
			КонецЕсли;
		ИначеЕсли Элементы.СкладТорговойПлощадки.СписокВыбора.Количество() > 0 Тогда
			ИдентификаторСклада = Элементы.СкладТорговойПлощадки.СписокВыбора[0];
		КонецЕсли;
	КонецЕсли;
	
	ИнформацияОбОтгрузкеШаблон.ИдентификаторСклада = ИдентификаторСклада;
	ИнформацияОбОтгрузке.ИдентификаторСклада = ИдентификаторСклада;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройкиСклада()

	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Или ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада) Тогда
		ВремяОтгрузки = Дата(1,1,1);
		
		НастройкиСклада = Неопределено;
		МетодДоставки = Неопределено;
		ИнформацияОбОтгрузкеШаблон.ИдентификаторМетодаДоставки = "";
		
		ИнформацияПоСкладу = НСтр("ru = 'Настройки способа доставки не определены, т.к не выбран склад.';
									|en = 'The delivery method settings are not defined as the warehouse is not selected.'");
		ИнформацияОбАктуальностиНастроекСклада = "";
	Иначе
		НастройкиСклада = НастройкиСклада(УчетнаяЗапись, ИнформацияОбОтгрузке.ИдентификаторСклада);
		
		Если ПустаяСтрока(НастройкиСклада.ПредставлениеСпособаДоставки) Тогда
			ИнформацияПоСкладу = НСтр("ru = 'Не заданы настройки способа доставки.';
										|en = 'The delivery method settings are not set.'");
			ИнформацияОбАктуальностиНастроекСклада = "";
		Иначе
			Если ЗначениеЗаполнено(НастройкиСклада.ПеревозчикПартнер)
					И Не ПустаяСтрока(НастройкиСклада.АдресДоставкиПеревозчика) Тогда
				Шаблон = НСтр("ru = '%1 %2 по адресу: %3.';
								|en = '%1 %2 at: %3.'");
			Иначе
				Шаблон = НСтр("ru = '%1 %2.';
								|en = '%1 %2.'");
			КонецЕсли;
			
			ИнформацияПоСкладу = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Шаблон,
				НастройкиСклада.ПредставлениеСпособаДоставки,
				НастройкиСклада.ПеревозчикПартнер,
				НастройкиСклада.АдресДоставкиПеревозчика);
			
			ИнформацияОбАктуальностиНастроекСклада = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Настройки способа доставки установлены %1.';
					|en = 'The delivery method settings are set %1.'"),
				НастройкиСклада.ДатаАктуальности) + " ";
		КонецЕсли;
		
		МетодДоставки = Неопределено;
		
		Если СписокМетодовДоставки <> Неопределено Тогда
			КлючСклада = "Склад_" + ИнформацияОбОтгрузкеШаблон.ИдентификаторСклада;
			ИскомыйМетодДоставки = Новый Структура(КлючСклада, Неопределено);
			ЗаполнитьЗначенияСвойств(ИскомыйМетодДоставки, СписокМетодовДоставки);
			
			МетодДоставки = ИскомыйМетодДоставки[КлючСклада];
		КонецЕсли;
		
		Если МетодДоставки <> Неопределено Тогда
			ИнформацияОбОтгрузкеШаблон.ИдентификаторМетодаДоставки = МетодДоставки.ИдентификаторМетодаДоставки;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ИнформацияОбОтгрузке.ИдентификаторОтгрузки)
				И МетодДоставки <> Неопределено Тогда
			ВремяОтгрузки = МетодДоставки.ВремяОтгрузки;
			ВремяОтгрузкиРекомендуемое = МетодДоставки.ВремяОтгрузкиРекомендуемое;
		КонецЕсли;
	КонецЕсли;
	
	Если ПустаяСтрока(ИнформацияОбАктуальностиНастроекСклада) Тогда
		Элементы.ИнформацияОНастройкахСклада.Подсказка =
			НСтр("ru = 'Перед формированием отгрузки заполните настройки способа доставки (способ доставки, перевозчик, адрес пункта приема) в соответствии с настройками склада в личном кабинете.';
				|en = 'Before generating a shipment, fill the delivery method settings (delivery method, carrier, pickup point address) according to the warehouse settings in your personal account.'");
	Иначе
		Элементы.ИнформацияОНастройкахСклада.Подсказка = ИнформацияОбАктуальностиНастроекСклада
			+ НСтр("ru = 'Перед формированием отгрузки проверьте, что установленные настройки (способ доставки, перевозчик, адрес пункта приема) соответствуют настройкам склада в личном кабинете.';
					|en = 'Before generating a shipment, make sure the set settings (delivery method, carrier, pickup point address) match the warehouse settings in your personal account.'");
	КонецЕсли;
	
	ИзменитьПоведениеФормыНаСервере();

КонецПроцедуры

&НаСервереБезКонтекста
Функция НастройкиСклада(УчетнаяЗапись, ИдентификаторСклада)

	Возврат ИнтеграцияСМаркетплейсамиСервер.НастройкиСклада(УчетнаяЗапись, ИдентификаторСклада);

КонецФункции

#КонецОбласти

#Область ФормированиеОтгрузкиСлужебные

&НаКлиенте
Процедура ОчиститьИнформациюОбОтгрузке(ОбновитьДатуОтгрузки = Ложь)

	ДатаСеанса = НачалоДня(ОбщегоНазначенияКлиент.ДатаСеанса());
	
	Если ОбновитьДатуОтгрузки Тогда
		ДатаОтгрузки = ДатаСеанса;
	КонецЕсли;
	
	Статус = "";
	КоличествоГрузовыхМест = 0;
	
	Если МетодДоставки <> Неопределено Тогда
		ВремяОтгрузки = МетодДоставки.ВремяОтгрузки;
		ВремяОтгрузкиРекомендуемое = МетодДоставки.ВремяОтгрузкиРекомендуемое;
	КонецЕсли;
	
	ИнформацияОбОтгрузке = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ИнформацияОбОтгрузкеШаблон);
	
	Отгрузки.Очистить();
	ОтправленияВОтгрузке.Очистить();
	ОтправленияЗагруженные.Очистить();
	ОтправленияНеВОтгрузке.Очистить();
	ОтправленияНезагруженные.Очистить();

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИнформациюОбОтгрузке(ЗаполнитьСвязьСОтгрузкой = Ложь)

	Если ЗаполнитьСвязьСОтгрузкой И ОтправленияВОтгрузке.Количество() > 0 Тогда
		НомераОтправлений = ПолучитьОтправленияДляПерезаполненияНастроекДоставки(ИнформацияОбОтгрузке, НастройкиСклада);
		
		Если ЗначениеЗаполнено(НомераОтправлений) Тогда
			ОбработчикОповещения = Новый ОписаниеОповещения("ОбработкаОтветаПродолженияПолученияИнформацииОбОтгрузке", ЭтотОбъект);
			
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'В соответствии с текущими настройками склада будут изменены настройки способа доставки для документов отправлений: %1, если не отменены.
							|
							|Продолжить?';
							|en = 'In accordance with the current warehouse settings, the delivery method settings for shipment documents will be changed: %1 if not cancelled.
							|
							|Continue?'"),
				СтрСоединить(НомераОтправлений, ", "));
			ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПродолжитьПолучениеИнформацииОбОтгрузке(ЗаполнитьСвязьСОтгрузкой);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОтправленияДляПерезаполненияНастроекДоставки(ИнформацияОбОтгрузке, НастройкиСклада)

	НомераОтправленийДляЗаменыНастроекСклада = Новый Массив;
	
	ПерезаполняемыеОтправления =
		РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьОтправленияДляПерезаполненияНастроекДоставки(
			ИнформацияОбОтгрузке,
			НастройкиСклада);
	
	Для Каждого ПерезаполняемоеОтправление Из ПерезаполняемыеОтправления Цикл
		НомераОтправленийДляЗаменыНастроекСклада.Добавить(ПерезаполняемоеОтправление.Ключ);
	КонецЦикла;
	
	Возврат НомераОтправленийДляЗаменыНастроекСклада;

КонецФункции

&НаКлиенте
// Обработка ответа на вопрос.
// 
// Параметры:
//   Ответ                   - КодВозвратаДиалога - ответ.
//   ДополнительныеПараметры - Неопределено, Структура из КлючИЗначение - дополнительные параметры.
//
Процедура ОбработкаОтветаПродолженияПолученияИнформацииОбОтгрузке(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПродолжитьПолучениеИнформацииОбОтгрузке(Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПолучениеИнформацииОбОтгрузке(ЗаполнитьСвязьСОтгрузкой = Ложь)

	ДлительнаяОперация = ЗапуститьЗаданиеПоОтгрузке("ИнформацияОбОтгрузке", ЗаполнитьСвязьСОтгрузкой);
	
	ОчиститьИнформациюОбОтгрузке();
	
	ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, "ПолучениеИнформацииОбОтгрузкеЗавершение");

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьФормированиеОтгрузки()

	ДлительнаяОперация = ЗапуститьЗаданиеПоОтгрузке("СформироватьОтгрузку");
	
	ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, "ПолучениеИнформацииОбОтгрузкеЗавершение");

КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьОтгрузку()

	КоличествоОтправленийВОтгрузке = ОтправленияВОтгрузке.Количество();
	КоличествоОтправленийНезагруженных = ОтправленияНезагруженные.Количество();
	
	Если КоличествоОтправленийВОтгрузке = КоличествоОтправленийНезагруженных Тогда
		ОбработчикОповещения = Новый ОписаниеОповещения("ОбработкаОтветаПродолженияПодтвержденияОтгрузки", ЭтотОбъект);
		
		ТекстВопроса = НСтр("ru = 'В состав отгрузки войдут все незагруженные отправления.
								|
								|Продолжить подтверждение отгрузки?';
								|en = 'The shipment will include all unloaded shipments.
								|
								|Continue shipment confirmation?'");
		ПоказатьВопрос(ОбработчикОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
		Возврат;
	Иначе
		ПродолжитьПодтверждениеОтгрузки();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
// Обработка ответа на вопрос.
// 
// Параметры:
//   Ответ                   - КодВозвратаДиалога - ответ.
//   ДополнительныеПараметры - Неопределено, Структура из КлючИЗначение - дополнительные параметры.
//
Процедура ОбработкаОтветаПродолженияПодтвержденияОтгрузки(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		СписокНезагруженныхОтправлений =
			ОбщегоНазначенияКлиент.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияНезагруженные);
		ИнформацияОбОтгрузке.ОтправленияНезагруженные = Новый Массив;
		
		ПродолжитьПодтверждениеОтгрузки();
		
		ИнформацияОбОтгрузке.ОтправленияНезагруженные = СписокНезагруженныхОтправлений;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеОтгрузки()

	ДлительнаяОперация = ЗапуститьЗаданиеПоОтгрузке("ПодтвердитьОтгрузку");
	
	ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, "ПолучениеИнформацииОбОтгрузкеЗавершение");

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОтменуОтгрузки()

	ДлительнаяОперация = ЗапуститьЗаданиеПоОтгрузке("ОтменитьОтгрузку");
	
	ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, "ПолучениеИнформацииОбОтгрузкеЗавершение");

КонецПроцедуры

&НаСервере
Функция ЗапуститьЗаданиеПоОтгрузке(ИмяЗадания, ЗаполнитьСвязьСОтгрузкой = Истина)

	ДлительнаяОперация = Неопределено;
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись)
			Или МетодДоставки = Неопределено
			Или ПустаяСтрока(МетодДоставки.ИдентификаторМетодаДоставки) Тогда
		ИзменитьПоведениеФормыНаСервере();
		Возврат ДлительнаяОперация;
	КонецЕсли;
	
	ОбновляетсяИнформацияОбОтгрузке = Истина;
	
	ИнформацияОбОтгрузке.ДатаВыполнения = НачалоДня(ДатаОтгрузки);
	
	Заголовок = ПредставлениеОтгрузки(ИнформацияОбОтгрузке);
	Элементы.ЗаголовокСпискаОтгрузок.Заголовок = "";
	
	Элементы.СтраницыВремяОтгрузки.ТекущаяСтраница = Элементы.СтраницаВремяОтгрузкиНеопределено;
	Элементы.СтраницыСтатусОтгрузки.ТекущаяСтраница = Элементы.СтраницаСтатусОтгрузкиПроверяется;
	Элементы.СтраницыОтправленияОтгрузки.ТекущаяСтраница = Элементы.СтраницаОтгрузкаНеСформирована;
	Элементы.СтраницыСписокОтгрузок.ТекущаяСтраница = Элементы.СтраницаОбновлениеОтгрузки;
	Элементы.СтраницыФоновогоЗадания.ТекущаяСтраница = Элементы.СтраницаФоновогоЗаданияОбновляетсяОтгрузка;
	Элементы.СтраницыДоступаКГрузовымМестам.ТекущаяСтраница = Элементы.СтраницаГрузовыеМестаТолькоПросмотр;
	
	Элементы.ОбновитьИнформациюОбОтгрузке.Видимость = Ложь;
	Элементы.КомандыГиперссылки.Видимость = Ложь;
	Элементы.ОткрытьНастройкиСклада.Доступность = Ложь;
	Элементы.КоманднаяПанель.Доступность = Ложь;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ПредставлениеВидаТорговойПлощадки =
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ДанныеТорговойПлощадки.ВидМаркетплейса);
	
	Если ИмяЗадания = "СформироватьОтгрузку" Тогда
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Формирование отгрузки.';
				|en = '%1. Generate a shipment.'"),
			ПредставлениеВидаТорговойПлощадки);
		
		ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.СформироватьОтгрузку";
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяМетода,
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			МетодДоставки,
			ИнформацияОбОтгрузке.ДатаВыполнения,
			КоличествоГрузовыхМест);
		
	ИначеЕсли ИмяЗадания = "ИнформацияОбОтгрузке" Тогда
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Получение информации об отгрузке.';
				|en = '%1. Get shipment details.'"),
			ПредставлениеВидаТорговойПлощадки);
		
		ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьИнформациюОбОтгрузке";
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяМетода,
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			МетодДоставки,
			ИнформацияОбОтгрузке.ДатаВыполнения,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			Истина,
			ЗаполнитьСвязьСОтгрузкой);
		
	ИначеЕсли ИмяЗадания = "ПодтвердитьОтгрузку" Тогда
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Подтверждение отгрузки и ее состава.';
				|en = '%1. Confirm the shipment and its content.'"),
			ПредставлениеВидаТорговойПлощадки);
		
		ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ПодтвердитьОтгрузку";
		
		СписокОтправленийВОтгрузке = СписокОтправленийДляПодтвержденияОтгрузки(
			ИнформацияОбОтгрузке,
			ЭтоУстановкаПометокДляВсехПозиций,
			ОтправленияДляФлаговВыбранные,
			ОтправленияДляФлаговИсключенные);
		
		Если СписокОтправленийВОтгрузке.Количество() = 0 Тогда
			ИзменитьПоведениеФормыНаСервере();
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не выбраны отправления для подтверждения отгрузки.';
														|en = 'No shipments are selected to confirm the shipment.'"));
			Возврат ДлительнаяОперация;
		КонецЕсли;
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяМетода,
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			МетодДоставки,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			СписокОтправленийВОтгрузке,
			КоличествоГрузовыхМест);
		
	ИначеЕсли ИмяЗадания = "ОтменитьОтгрузку" Тогда
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1. Отмена отгрузки.';
				|en = '%1. Cancel shipment.'"),
			ПредставлениеВидаТорговойПлощадки);
		
		ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОтменитьОтгрузку";
		
		ДлительнаяОперация = ДлительныеОперации.ВыполнитьФункцию(
			ПараметрыВыполнения,
			ИмяМетода,
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			МетодДоставки,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
		
	КонецЕсли;
	
	Возврат ДлительнаяОперация;

КонецФункции

&НаКлиенте
Процедура ОжидатьЗавершениеДлительнойОперации(ДлительнаяОперация, ИмяПроцедурыЗавершения)

	Если ДлительнаяОперация <> Неопределено Тогда
		ОповещениеОЗавершении = Новый ОписаниеОповещения(ИмяПроцедурыЗавершения, ЭтотОбъект);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	Иначе
		ОбновляетсяИнформацияОбОтгрузке = Ложь;
	КонецЕсли;
	
	ОбновитьПараметрыСписка();

КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокОтправленийДляПодтвержденияОтгрузки(ИнформацияОбОтгрузке, ЭтоУстановкаПометокДляВсехПозиций,
			ОтправленияДляФлаговВыбранные, ОтправленияДляФлаговИсключенные)

	Если ОтправленияДляФлаговВыбранные.Количество() > 0 Тогда
		СписокОтправленийВОтгрузке = ОтправленияДляФлаговВыбранные.ВыгрузитьЗначения();
	ИначеЕсли ЭтоУстановкаПометокДляВсехПозиций Тогда
		СписокОтправленийВОтгрузке = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияВОтгрузке);
		Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки)
				И ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СписокОтправленийВОтгрузке,
				ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки,
				Истина);
		КонецЕсли;
		
		Если ОтправленияДляФлаговИсключенные.Количество() > 0 Тогда
			СписокОтправленийВОтгрузке = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
				СписокОтправленийВОтгрузке,
				ОтправленияДляФлаговИсключенные.ВыгрузитьЗначения());
		КонецЕсли;
	Иначе
		СписокОтправленийВОтгрузке = Новый Массив;
	КонецЕсли;
	
	СписокОтправленийВОтгрузке = ОбщегоНазначенияКлиентСервер.СвернутьМассив(СписокОтправленийВОтгрузке);
	
	Если ИнформацияОбОтгрузке.ОтправленияОтмененные.Количество() > 0 Тогда
		СписокОтправленийВОтгрузке = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СписокОтправленийВОтгрузке,
			ИнформацияОбОтгрузке.ОтправленияОтмененные);
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ОтправленияНезагруженные.Количество() > 0 Тогда
		СписокОтправленийВОтгрузке = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			СписокОтправленийВОтгрузке,
			ИнформацияОбОтгрузке.ОтправленияНезагруженные);
	КонецЕсли;
	
	Возврат СписокОтправленийВОтгрузке;

КонецФункции

&НаКлиенте
// Обработка результата выполнения фонового задания.
// 
// Параметры:
//   РезультатЗадания        - Неопределено, Структура из см. ДлительныеОперации.ВыполнитьФункцию.ИмяФункции
//   ДополнительныеПараметры - Неопределено, Структура из КлючИЗначение - дополнительные параметры.
//
Процедура ПолучениеИнформацииОбОтгрузкеЗавершение(РезультатЗадания, ДополнительныеПараметры = Неопределено) Экспорт

	Если РезультатЗадания <> Неопределено
			И РезультатЗадания.Статус = "Выполнено" Тогда
		ОбработатьПолучениеИнформацииОбОтгрузке(РезультатЗадания.АдресРезультата);
		
		ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(ИнформацияОбОтгрузке.Ошибка, Неопределено, Истина);
		
		Оповестить("МП_ОбновлениеСведенийПоЗаказамИОтправлениям");
	Иначе
		ИзменитьПоведениеФормыНаСервере();
	КонецЕсли;
	
	ОбновитьПараметрыСписка();
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаАктуальности",
		ОбщегоНазначенияКлиент.ДатаСеанса());
	
	ОбновитьЗаголовокКомандыПрисоединенныхФайлов();
	
	ОбновляетсяИнформацияОбОтгрузке = Ложь;

КонецПроцедуры

&НаСервере
Процедура ОбработатьПолучениеИнформацииОбОтгрузке(АдресРезультата = "")

	Если Не ПустаяСтрока(АдресРезультата) Тогда
		ИнформацияОбОтгрузке = ПолучитьИзВременногоХранилища(АдресРезультата);
		УдалитьИзВременногоХранилища(АдресРезультата);
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ОтгрузкаОтменена
			И ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Количество() = 0 Тогда
		Результат = ОбщегоНазначенияКлиентСервер.РазностьМассивов(
			ИнформацияОбОтгрузке.ОтправленияВОтгрузке,
			ИнформацияОбОтгрузке.ОтправленияНезагруженные);
		
		Если Результат.Количество() = 0 И Не ЭтоПовторныйЗапрос Тогда
			ПоляЗаполнения = "АдресПунктаПриема, ВремяОтгрузкиРекомендуемое, ВремяПриемаНачало, ВремяПриемаОкончание,
							|КоличествоОтправленийВОтгрузке, КоличествоОтправленийВСборке, КоличествоСобранныхОтправлений";
			
			ИнформацияОбОтгрузкеНов = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузкеШаблон);
			ЗаполнитьЗначенияСвойств(ИнформацияОбОтгрузкеНов, ИнформацияОбОтгрузке, ПоляЗаполнения);
			
			ИнформацияОбОтгрузке = ИнформацияОбОтгрузкеНов;
		КонецЕсли;
	КонецЕсли;
	
	Если ИнформацияОбОтгрузкеШаблон.ИдентификаторМетодаДоставки = ИнформацияОбОтгрузке.ИдентификаторМетодаДоставки Тогда
		ИнформацияОбОтгрузкеШаблон.СмещениеЧасовогоПояса = ИнформацияОбОтгрузке.СмещениеЧасовогоПояса;
	КонецЕсли;
	
	ЭтоПовторныйЗапрос     = ИнформацияОбОтгрузке.ПовторитьЗапрос;
	КоличествоГрузовыхМест = ИнформацияОбОтгрузке.КоличествоГрузовыхМест;
	МассаБрутто            = ИнформацияОбОтгрузке.МассаБрутто;
	МассаНетто             = ИнформацияОбОтгрузке.МассаНетто;
	Статус                 = ИнформацияОбОтгрузке.Статус;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада) Тогда
		ЗаполнитьЗначенияСвойств(ИнформацияОбОтгрузке, ИнформацияОбОтгрузкеШаблон,
			"ИдентификаторСклада");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИнформацияОбОтгрузке.ДатаВыполнения) Тогда
		ДатаОтгрузки = ИнформацияОбОтгрузке.ДатаВыполнения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИнформацияОбОтгрузке.СрокОтгрузки)
			Или ЗначениеЗаполнено(ИнформацияОбОтгрузке.ВремяОтгрузкиРекомендуемое) Тогда
		ВремяОтгрузки = Дата(1,1,1) + (ИнформацияОбОтгрузке.СрокОтгрузки - НачалоДня(ИнформацияОбОтгрузке.СрокОтгрузки));
		ВремяОтгрузкиРекомендуемое = ИнформацияОбОтгрузке.ВремяОтгрузкиРекомендуемое;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИнформацияОбОтгрузке.АдресПунктаПриема) Тогда
		Шаблон = НСтр("ru = 'Отгрузка в пункт приема %1 по адресу: %3';
						|en = 'Shipment to the %1 pickup point at: %3'");
		
		Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ВремяПриемаНачало)
				И Не ПустаяСтрока(ИнформацияОбОтгрузке.ВремяПриемаОкончание) Тогда
			ШаблонВремени = " " +НСтр("ru = 'с %1 до %2';
										|en = 'from %1 to %2'");
		ИначеЕсли Не ПустаяСтрока(ИнформацияОбОтгрузке.ВремяПриемаНачало) Тогда
			ШаблонВремени = " " +НСтр("ru = 'с %1';
										|en = 'from %1'");
		ИначеЕсли Не ПустаяСтрока(ИнформацияОбОтгрузке.ВремяПриемаОкончание) Тогда
			ШаблонВремени = " " +НСтр("ru = 'до %2';
										|en = 'to %2'");
		Иначе
			ШаблонВремени = "";
		КонецЕсли;
		
		Шаблон = СтрЗаменить(Шаблон, " %1", ШаблонВремени);
	Иначе
		Шаблон = "";
	КонецЕсли;
	
	Элементы.АдресДоставки.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Шаблон,
		ИнформацияОбОтгрузке.ВремяПриемаНачало,
		ИнформацияОбОтгрузке.ВремяПриемаОкончание,
		ИнформацияОбОтгрузке.АдресПунктаПриема);
	
	СписокОтправленийВОтгрузке = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияВОтгрузке);
	Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки)
			И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			СписокОтправленийВОтгрузке,
			ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки,
			Истина);
		
		Если ИнформацияОбОтгрузке.ОтгрузкаАктуальна И Не ИнформацияОбОтгрузке.ПовторитьЗапрос Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
				СписокОтправленийВОтгрузке,
				ИнформацияОбОтгрузке.ОтправленияНеВОтгрузке,
				Истина);
		КонецЕсли;
	КонецЕсли;
	
	ОтправленияВОтгрузке.ЗагрузитьЗначения(СписокОтправленийВОтгрузке);
	ОтправленияНезагруженные.ЗагрузитьЗначения(ИнформацияОбОтгрузке.ОтправленияНезагруженные);
	ОтправленияЗагруженные.ЗагрузитьЗначения(ОбщегоНазначенияКлиентСервер.РазностьМассивов(
		ИнформацияОбОтгрузке.ОтправленияВОтгрузке,
		ИнформацияОбОтгрузке.ОтправленияНезагруженные));
	ОтправленияНеВОтгрузке.ЗагрузитьЗначения(ИнформацияОбОтгрузке.ОтправленияНеВОтгрузке);
	
	СписокОтправленийИсключенных = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияОтмененные);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		СписокОтправленийИсключенных,
		ИнформацияОбОтгрузке.ОтправленияНеВОтгрузке,
		Истина);
	
	ЗаполнитьСписокОтгрузок(Отгрузки, ИнформацияОбОтгрузке);
	
	ЭтоУстановкаПометокДляВсехПозиций = Истина;
	ОтправленияДляФлаговВыбранные.Очистить();
	ОтправленияДляФлаговИсключенные.Очистить();
	
	ОтправленияДляФлаговИсключенные.ЗагрузитьЗначения(СписокОтправленийИсключенных);
	
	СообщенияПользователюПоСостояниюОтгрузки(ИнформацияОбОтгрузке, ИнформацияОбОтгрузкеШаблон.МожноРедактировать);
	
	ИзменитьПоведениеФормыНаСервере();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СообщенияПользователюПоСостояниюОтгрузки(ИнформацияОбОтгрузке, ТребуетсяПодтверждениеОтгрузки)

	СписокСообщений = Новый Массив;
	
	Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		Если ИнформацияОбОтгрузке.ОтгрузкаАктуальна Тогда
			Если Не ИнформацияОбОтгрузке.МожноРедактировать И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
				СписокСообщений.Добавить(НСтр("ru = 'Отгрузка обрабатывается на торговой площадке. Обновите информацию через некоторое время.';
												|en = 'The shipment is processed on the marketplace. Update the information after a while.'"));
			ИначеЕсли ИнформацияОбОтгрузке.ОтгрузкаСформирована
					И ИнформацияОбОтгрузке.ОтправленияНезагруженные.Количество() = 0
					И ИнформацияОбОтгрузке.ОтправленияВОтгрузке.Количество() <> ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Количество() Тогда
				СписокСообщений.Добавить(НСтр("ru = 'Обнаружены не связанные с отгрузкой отправления. Обновите информацию для заполнения связи отправлений с текущей отгрузкой.';
												|en = 'Shipments unrelated to the shipment are found. Update the information to link the shipments to the current shipment.'"));
			КонецЕсли;
		ИначеЕсли ИнформацияОбОтгрузке.ОтгрузкаОтменена Тогда
			Если ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Количество() > 0 Тогда
				СписокСообщений.Добавить(НСтр("ru = 'Отгрузка отменена. Обновите статусы заказов (в подменю ""Действия""), т.к. они могли быть переданы в доставку другой отгрузкой.';
												|en = 'The shipment is canceled. Update the order statuses (in the ""Actions"" submenu) as they may have been transferred to the delivery service by another shipment.'"));
			ИначеЕсли ИнформацияОбОтгрузке.ОтправленияВОтгрузке.Количество() <> ИнформацияОбОтгрузке.ОтправленияНезагруженные.Количество() Тогда
				Если ТребуетсяПодтверждениеОтгрузки Тогда
					СписокСообщений.Добавить(НСтр("ru = 'Отгрузка отменена. Доступно формирование новой отгрузки.';
													|en = 'The shipment is canceled. You can generate a new shipment.'"));
				Иначе
					СписокСообщений.Добавить(НСтр("ru = 'Отгрузка отменена. Для возможности формирования новой отгрузки обновите информацию.';
													|en = 'The shipment is canceled. To generate a new shipment, update the information.'"));
				КонецЕсли;
			ИначеЕсли ИнформацияОбОтгрузке.ОтправленияВОтгрузке.Количество() > 0 Тогда
				СписокСообщений.Добавить(НСтр("ru = 'Отгрузка отменена. Доступно формирование новой отгрузки.';
												|en = 'The shipment is canceled. You can generate a new shipment.'"));
			КонецЕсли;
		ИначеЕсли ИнформацияОбОтгрузке.ПовторитьЗапрос Тогда
			СписокСообщений.Добавить(НСтр("ru = 'Отгрузка обрабатывается на торговой площадке. Обновите информацию через некоторое время.';
											|en = 'The shipment is processed on the marketplace. Update the information after a while.'"));
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) И Не ИнформацияОбОтгрузке.ОтгрузкаСформирована Тогда
		Если ИнформацияОбОтгрузке.ПовторитьЗапрос Тогда
			Если Не ТребуетсяПодтверждениеОтгрузки Тогда
				СписокСообщений.Добавить(НСтр("ru = 'Список отправлений не утвержден. Необходимо проверить формирование отгрузки.';
												|en = 'The list of shipments is not confirmed. Check the shipment generation.'"));
			КонецЕсли;
		Иначе
			СписокСообщений.Добавить(НСтр("ru = 'Список отправлений не утвержден. Необходимо завершить формирование отгрузки.';
											|en = 'The list of shipments is not confirmed. Complete the shipment generation.'"));
		КонецЕсли;
	КонецЕсли;
	
	Для Каждого ТекстСообщения Из СписокСообщений Цикл
		#Если Сервер Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		#Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		#КонецЕсли
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверкуОтправленийВОтгрузке()

	РегистрыСведений.ЗаказыТорговыхПлощадок.ВыполнитьПроверкуОтправленийВОтгрузке(ИнформацияОбОтгрузке);
	ЗаполнитьМассыИзНакладныхНаСервере(ИнформацияОбОтгрузке);
	ОбработатьПолучениеИнформацииОбОтгрузке();

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПутьКОтгрузкамВЛичномКабинете(ВидМаркетплейса)

	ПутьКОтгрузкамВЛичномКабинете = "";
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ПутьКОтгрузкамВЛичномКабинете = "https://seller.ozon.ru/app/postings/fbs?tab=awaiting_deliver&show=postings";
	КонецЕсли;
	
	Возврат ПутьКОтгрузкамВЛичномКабинете;

КонецФункции

&НаКлиенте
// Обработка результата открытия формы просмотра значений.
// 
// Параметры:
//   РезультатЗадания        - Неопределено, Структура из см. ДлительныеОперации.ВыполнитьФункцию.ИмяФункции
//   ДополнительныеПараметры - Неопределено, Структура из КлючИЗначение - дополнительные параметры.
//
Процедура ПросмотрСпискаОбъектовЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Если ДополнительныеПараметры.Действие = "ОткрытьСписокОтгрузок" Тогда
		ОчиститьИнформациюОбОтгрузке(ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки));
		ИнформацияОбОтгрузке.ИдентификаторОтгрузки = Результат;
		ПолучитьИнформациюОбОтгрузке();
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокОтгрузок(Отгрузки, ИнформацияОбОтгрузке)

	Отгрузки.Очистить();
	
	Для Каждого Отгрузка Из ИнформацияОбОтгрузке.СписокОтгрузок Цикл
		Если ПустаяСтрока(Отгрузка.ИдентификаторОтгрузки) Тогда
			Если ЗначениеЗаполнено(Отгрузка.КоличествоОтправленийВОтгрузке) Тогда
				ПредставлениеОтгрузки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Новая отгрузка (%1)';
						|en = 'New shipment (%1)'"),
					КоличествоОтправленийСтрокой(Отгрузка.КоличествоОтправленийВОтгрузке));
			Иначе
				ПредставлениеОтгрузки = НСтр("ru = 'Новая отгрузка';
											|en = 'New shipment'");
			КонецЕсли;
		Иначе
			ПредставлениеОтгрузки = ПредставлениеОтгрузки(Отгрузка, Истина);
		КонецЕсли;
		
		Отгрузки.Добавить(Отгрузка.ИдентификаторОтгрузки, ПредставлениеОтгрузки);
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеОтгрузки(ИнформацияОбОтгрузке, ДополнительнаяИнформация = Ложь,
			ВыводитьИдентификаторОтгрузки = Ложь)

	ЧастиНомераОтгрузки = Новый Массив;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		НомерОтгрузки = ИнформацияОбОтгрузке.ПорядковыйНомерОтгрузки;
	Иначе
		НомерОтгрузки = ИнформацияОбОтгрузке.НомерОтгрузки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(НомерОтгрузки) Тогда
		ЧастиНомераОтгрузки.Добавить(НомерОтгрузки);
	КонецЕсли;
	
	Если ВыводитьИдентификаторОтгрузки
			И Не ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторОтгрузки) Тогда
		ЧастиНомераОтгрузки.Добавить("(id " + ИнформацияОбОтгрузке.ИдентификаторОтгрузки + ")");
	КонецЕсли;
	
	Если ЧастиНомераОтгрузки.Количество() > 0 Тогда
		ШаблонЗаголовка = НСтр("ru = 'Отгрузка %1 на %2';
								|en = 'Shipment %1 to %2'");
	Иначе
		ШаблонЗаголовка = НСтр("ru = 'Отгрузка на %2';
								|en = 'Shipment to %2'");
	КонецЕсли;
	
	ПредставлениеОтгрузки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ШаблонЗаголовка,
		СтрСоединить(ЧастиНомераОтгрузки, " "),
		Формат(ИнформацияОбОтгрузке.ДатаВыполнения, "ДЛФ=DD"));
	
	Если ДополнительнаяИнформация Тогда
		ПредставлениеДопИнформации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'в статусе ""%1"", создана %2, %3';
				|en = 'in the ""%1"" status, created %2, %3'"),
			ИнформацияОбОтгрузке.Статус,
			ИнформацияОбОтгрузке.ДатаСоздания,
			КоличествоОтправленийСтрокой(ИнформацияОбОтгрузке.КоличествоОтправленийВОтгрузке));
		
		ПредставлениеОтгрузки = ПредставлениеОтгрузки + " " + ПредставлениеДопИнформации;
	КонецЕсли;
	
	Возврат ПредставлениеОтгрузки;

КонецФункции

&НаСервереБезКонтекста
Функция КоличествоОтправленийСтрокой(КоличествоОтправлений = 0);

	Если КоличествоОтправлений = 0 Тогда
		Шаблон = НСтр("ru = 'нет отправлений';
						|en = 'no shipments'");
	ИначеЕсли Число(Прав(КоличествоОтправлений, 1)) = 1 Тогда
		Шаблон = НСтр("ru = '1 отправление';
						|en = '1 shipment'");
	ИначеЕсли Число(Прав(КоличествоОтправлений, 1)) <= 4 Тогда
		Шаблон = НСтр("ru = '%1 отправления';
						|en = '%1 shipment'");
	Иначе
		Шаблон = НСтр("ru = '%1 отправлений';
						|en = '%1 shipments'");
	КонецЕсли;
	
	КоличествоОтправленийСтрокой = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		Шаблон,
		КоличествоОтправлений);
		
	Возврат КоличествоОтправленийСтрокой;

КонецФункции

#КонецОбласти

#Область ЗаполнениеМассСлужебные

&НаКлиенте
Процедура ЗаполнитьМассыИзНакладных()

	ЗаполнитьМассыИзНакладныхНаСервере(ИнформацияОбОтгрузке);
	МассаБрутто = ИнформацияОбОтгрузке.МассаБрутто;
	МассаНетто  = ИнформацияОбОтгрузке.МассаНетто;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьМассыИзНакладныхНаСервере(ИнформацияОбОтгрузке)

	РегистрыСведений.ЗаказыТорговыхПлощадок.ЗаполнитьМассыИзНакладных(ИнформацияОбОтгрузке);

КонецПроцедуры

#КонецОбласти

#Область РаботаСДокументамиСлужебные

&НаКлиенте
Процедура УстановитьСтатусДокументовЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	СтатусДокументов = ДополнительныеПараметры.СтатусДокументов;
	ЗаголовокСтатуса = ДополнительныеПараметры.ЗаголовокСтатуса;
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	КоличествоОбработанных = УстановитьСтатусДокументовНаСервере(ВыделенныеСтроки, СтатусДокументов);
	ОбщегоНазначенияУТКлиент.ОповеститьПользователяОбУстановкеСтатуса(
		Элементы.Список,
		КоличествоОбработанных,
		ВыделенныеСтроки.Количество(),
		ЗаголовокСтатуса);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьСтатусДокументовНаСервере(ВыделенныеСтроки, СтатусДокументов)

	МассивДокументов = ВыделенныеЗаказы(ВыделенныеСтроки);
	КоличествоОбработанных =
		ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(МассивДокументов, СтатусДокументов);
	
	Возврат КоличествоОбработанных;

КонецФункции

&НаСервереБезКонтекста
Функция ВыделенныеЗаказы(ВыделенныеСтроки)

	МассивДокументов = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		МассивДокументов.Добавить(ВыделеннаяСтрока.Заказ);
	КонецЦикла;

	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивДокументов);

КонецФункции

&НаСервереБезКонтекста
Функция ДанныеДокументовПеревозки(ЗаданиеНаПеревозку, ТранспортнаяНакладная, ИдентификаторОтгрузки, ЗаполнитьСсылки = Истина)

	ДанныеДокументовПеревозки = Новый Структура;
	
	ДанныеДокументовПеревозки.Вставить("ТТН_Дата", Дата(1,1,1));
	ДанныеДокументовПеревозки.Вставить("ТТН_Номер", "");
	ДанныеДокументовПеревозки.Вставить("ТТН_Проведен", Ложь);
	ДанныеДокументовПеревозки.Вставить("ТТН_ПометкаУдаления", Истина);
	ДанныеДокументовПеревозки.Вставить("ТТН_ТипТС", Справочники.ТипыТранспортныхСредств.ПустаяСсылка());
	
	ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_Дата", Дата(1,1,1));
	ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_Номер", "");
	ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_Проведен", Ложь);
	ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_ПометкаУдаления", Истина);
	ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_ТипТС", Справочники.ТипыТранспортныхСредств.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Если ЗаполнитьСсылки Тогда
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТранспортныеНакладные.Ссылка КАК ТранспортнаяНакладная,
			|	ТранспортныеНакладные.Дата КАК Дата,
			|	ТранспортныеНакладные.Номер КАК Номер,
			|	ТранспортныеНакладные.Проведен КАК Проведен,
			|	ТранспортныеНакладные.ПометкаУдаления КАК ПометкаУдаления,
			|	ЕСТЬNULL(ТипыТранспортныхСредств.Ссылка, ЗНАЧЕНИЕ(Справочник.ТипыТранспортныхСредств.ПустаяСсылка)) КАК ТипТранспортногоСредства,
			|	ТранспортныеНакладные.ЗаданиеНаПеревозку КАК ЗаданиеНаПеревозку
			|ПОМЕСТИТЬ ДанныеТТН
			|ИЗ
			|	Документ.ТранспортнаяНакладная КАК ТранспортныеНакладные
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыТранспортныхСредств КАК ТипыТранспортныхСредств
			|		ПО ТранспортныеНакладные.АвтомобильТип = ТипыТранспортныхСредств.Наименование
			|ГДЕ
			|	ТранспортныеНакладные.Идентификатор = &ИдентификаторОтгрузки
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗаданияНаПеревозку.Ссылка КАК Ссылка,
			|	ЗаданияНаПеревозку.Дата КАК Дата,
			|	ЗаданияНаПеревозку.Номер КАК Номер,
			|	ЗаданияНаПеревозку.Проведен КАК Проведен,
			|	ЗаданияНаПеревозку.ПометкаУдаления КАК ПометкаУдаления,
			|	ВЫБОР
			|		КОГДА ЗаданияНаПеревозку.ТранспортноеСредство ССЫЛКА Справочник.ТипыТранспортныхСредств
			|			ТОГДА ЗаданияНаПеревозку.ТранспортноеСредство
			|		ИНАЧЕ ЕСТЬNULL(ТранспортныеСредства.Тип, ЗНАЧЕНИЕ(Справочник.ТипыТранспортныхСредств.ПустаяСсылка))
			|	КОНЕЦ КАК ТипТранспортногоСредства
			|ИЗ
			|	Документ.ЗаданиеНаПеревозку КАК ЗаданияНаПеревозку
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеТТН КАК ДанныеТТН
			|		ПО ЗаданияНаПеревозку.Ссылка = ДанныеТТН.ЗаданиеНаПеревозку
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТранспортныеСредства КАК ТранспортныеСредства
			|		ПО ЗаданияНаПеревозку.ТранспортноеСредство = ТранспортныеСредства.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДанныеТТН.ТранспортнаяНакладная КАК Ссылка,
			|	ДанныеТТН.Дата КАК Дата,
			|	ДанныеТТН.Номер КАК Номер,
			|	ДанныеТТН.Проведен КАК Проведен,
			|	ДанныеТТН.ПометкаУдаления КАК ПометкаУдаления,
			|	ДанныеТТН.ТипТранспортногоСредства КАК ТипТранспортногоСредства
			|ИЗ
			|	ДанныеТТН КАК ДанныеТТН
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|УНИЧТОЖИТЬ ДанныеТТН";
		
		ПозицияЗаданияНаПеревозку = 1;
		ПозицияТТН = 2;
	Иначе
		ТекстЗапроса =
			"ВЫБРАТЬ
			|	ТранспортныеНакладные.Ссылка КАК Ссылка,
			|	ТранспортныеНакладные.Дата КАК Дата,
			|	ТранспортныеНакладные.Номер КАК Номер,
			|	ТранспортныеНакладные.Проведен КАК Проведен,
			|	ТранспортныеНакладные.ПометкаУдаления КАК ПометкаУдаления,
			|	ЕСТЬNULL(ТипыТранспортныхСредств.Ссылка, ЗНАЧЕНИЕ(Справочник.ТипыТранспортныхСредств.ПустаяСсылка)) КАК ТипТранспортногоСредства
			|ИЗ
			|	Документ.ТранспортнаяНакладная КАК ТранспортныеНакладные
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТипыТранспортныхСредств КАК ТипыТранспортныхСредств
			|		ПО ТранспортныеНакладные.АвтомобильТип = ТипыТранспортныхСредств.Наименование
			|ГДЕ
			|	ТранспортныеНакладные.Ссылка = &ТранспортнаяНакладная
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ЗаданияНаПеревозку.Ссылка КАК Ссылка,
			|	ЗаданияНаПеревозку.Дата КАК Дата,
			|	ЗаданияНаПеревозку.Номер КАК Номер,
			|	ЗаданияНаПеревозку.Проведен КАК Проведен,
			|	ЗаданияНаПеревозку.ПометкаУдаления КАК ПометкаУдаления,
			|	ВЫБОР
			|		КОГДА ЗаданияНаПеревозку.ТранспортноеСредство ССЫЛКА Справочник.ТипыТранспортныхСредств
			|			ТОГДА ЗаданияНаПеревозку.ТранспортноеСредство
			|		ИНАЧЕ ЕСТЬNULL(ТранспортныеСредства.Тип, ЗНАЧЕНИЕ(Справочник.ТипыТранспортныхСредств.ПустаяСсылка))
			|	КОНЕЦ КАК ТипТранспортногоСредства
			|ИЗ
			|	Документ.ЗаданиеНаПеревозку КАК ЗаданияНаПеревозку
			|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ТранспортныеСредства КАК ТранспортныеСредства
			|		ПО ЗаданияНаПеревозку.ТранспортноеСредство = ТранспортныеСредства.Ссылка
			|ГДЕ
			|	ЗаданияНаПеревозку.Ссылка = &ЗаданиеНаПеревозку";
		
		ПозицияЗаданияНаПеревозку = 1;
		ПозицияТТН = 0;
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ИдентификаторОтгрузки", ИдентификаторОтгрузки);
	Запрос.УстановитьПараметр("ЗаданиеНаПеревозку",    ЗаданиеНаПеревозку);
	Запрос.УстановитьПараметр("ТранспортнаяНакладная", ТранспортнаяНакладная);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ТранспортнаяНакладная = Документы.ТранспортнаяНакладная.ПустаяСсылка();
	ДанныеТранспортнойНакладной = Новый Структура;
	ТранспортнаяНакладнаяПометкаУдаления = Истина;
	
	ЗаданиеНаПеревозку = Документы.ЗаданиеНаПеревозку.ПустаяСсылка();
	ДанныеЗаданияНаПеревозку = Новый Структура;
	ЗаданиеНаПеревозкуПометкаУдаления = Истина;
	
	ВыборкаТТН = Результат[ПозицияТТН].Выбрать();
	Если ВыборкаТТН.Следующий() И ЗначениеЗаполнено(ВыборкаТТН.Ссылка) Тогда
		ТранспортнаяНакладная = ВыборкаТТН.Ссылка;
		ТранспортнаяНакладнаяПометкаУдаления = ВыборкаТТН.ПометкаУдаления;
		
		ДанныеТранспортнойНакладной.Вставить("ТТН_Дата",            ВыборкаТТН.Дата);
		ДанныеТранспортнойНакладной.Вставить("ТТН_Номер",           ВыборкаТТН.Номер);
		ДанныеТранспортнойНакладной.Вставить("ТТН_Проведен",        ВыборкаТТН.Проведен);
		ДанныеТранспортнойНакладной.Вставить("ТТН_ПометкаУдаления", ВыборкаТТН.ПометкаУдаления);
		
		ДанныеДокументовПеревозки.Вставить("ТТН_ТипТС", ВыборкаТТН.ТипТранспортногоСредства);
	КонецЕсли;
	
	ВыборкаЗаданийНаПеревозку = Результат[ПозицияЗаданияНаПеревозку].Выбрать();
	Если ВыборкаЗаданийНаПеревозку.Следующий() И ЗначениеЗаполнено(ВыборкаЗаданийНаПеревозку.Ссылка) Тогда
		ЗаданиеНаПеревозку = ВыборкаЗаданийНаПеревозку.Ссылка;
		ЗаданиеНаПеревозкуПометкаУдаления = ВыборкаЗаданийНаПеревозку.ПометкаУдаления;
		
		ДанныеЗаданияНаПеревозку.Вставить("ЗаданиеНаПеревозку_Дата",            ВыборкаЗаданийНаПеревозку.Дата);
		ДанныеЗаданияНаПеревозку.Вставить("ЗаданиеНаПеревозку_Номер",           ВыборкаЗаданийНаПеревозку.Номер);
		ДанныеЗаданияНаПеревозку.Вставить("ЗаданиеНаПеревозку_Проведен",        ВыборкаЗаданийНаПеревозку.Проведен);
		ДанныеЗаданияНаПеревозку.Вставить("ЗаданиеНаПеревозку_ПометкаУдаления", ВыборкаЗаданийНаПеревозку.ПометкаУдаления);
		
		ДанныеДокументовПеревозки.Вставить("ЗаданиеНаПеревозку_ТипТС", ВыборкаЗаданийНаПеревозку.ТипТранспортногоСредства);
	КонецЕсли;
	
	Если Не ТранспортнаяНакладнаяПометкаУдаления Или Не ЗаданиеНаПеревозкуПометкаУдаления Тогда
		ЗаполнитьЗначенияСвойств(ДанныеДокументовПеревозки, ДанныеТранспортнойНакладной);
		ЗаполнитьЗначенияСвойств(ДанныеДокументовПеревозки, ДанныеЗаданияНаПеревозку);
	ИначеЕсли ТранспортнаяНакладнаяПометкаУдаления И ЗаданиеНаПеревозкуПометкаУдаления Тогда
		ТранспортнаяНакладная = Документы.ТранспортнаяНакладная.ПустаяСсылка();
		ЗаданиеНаПеревозку = Документы.ЗаданиеНаПеревозку.ПустаяСсылка();
	ИначеЕсли ТранспортнаяНакладнаяПометкаУдаления Тогда
		ТранспортнаяНакладная = Документы.ТранспортнаяНакладная.ПустаяСсылка();
	ИначеЕсли ЗаданиеНаПеревозкуПометкаУдаления Тогда
		ЗаданиеНаПеревозку = Документы.ЗаданиеНаПеревозку.ПустаяСсылка();
	КонецЕсли;
	
	Возврат ДанныеДокументовПеревозки;

КонецФункции

&НаСервере
Процедура ОформитьДокументыПеревозки(НавигационнаяСсылкаКоманды)

	ИнформацияОбОтгрузке.ТипТранспортногоСредства = ТипТранспортногоСредства;
	
	Если НавигационнаяСсылкаКоманды = "ОформитьТТН_" Тогда
		ТранспортнаяНакладная = ОформитьТТН(ИнформацияОбОтгрузке, НастройкиСклада);
	ИначеЕсли НавигационнаяСсылкаКоманды = "ОформитьЗаданиеНаПеревозку_" Тогда
		ЗаданиеНаПеревозку = ОформитьЗаданиеНаПеревозку(ИнформацияОбОтгрузке, НастройкиСклада);
		
		Если ЗначениеЗаполнено(ЗаданиеНаПеревозку) Тогда
			ТранспортнаяНакладная = ОформитьТТН(ИнформацияОбОтгрузке, НастройкиСклада, ЗаданиеНаПеревозку);
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбновитьТекстДокументыНаОсновании(Ложь);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ОформитьЗаданиеНаПеревозку(ИнформацияОбОтгрузке, НастройкиСклада = Неопределено)

	ЗаданиеНаПеревозку = РегистрыСведений.ЗаказыТорговыхПлощадок.СформироватьЗаданиеНаПеревозку(
		ИнформацияОбОтгрузке,
		НастройкиСклада); // ДокументСсылка.ЗаданиеНаПеревозку
	
	Возврат ЗаданиеНаПеревозку;

КонецФункции

&НаСервереБезКонтекста
Функция ОформитьТТН(ИнформацияОбОтгрузке, НастройкиСклада = Неопределено, ЗаданиеНаПеревозку = Неопределено)

	ТранспортнаяНакладная = РегистрыСведений.ЗаказыТорговыхПлощадок.СформироватьТранспортнуюНакладную(
		ИнформацияОбОтгрузке,
		НастройкиСклада,
		ЗаданиеНаПеревозку); // ДокументСсылка.ТранспортнаяНакладная
	
	Возврат ТранспортнаяНакладная;

КонецФункции

#КонецОбласти

#Область РаботаСПропускамиСлужебные

&НаКлиенте
Процедура ЗавершениеЗаполненияПропусков(РезультатЗакрытия, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ИнформацияОбОтгрузке.Пропуска = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(РезультатЗакрытия);
		ИзменитьПоведениеФормыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПечатныеФормыСлужебные

&НаСервереБезКонтекста
Процедура ОбновитьПодменюПечать(Объект)

	ПанельПодключаемыхКоманд = Объект.Элементы.ПодключаемыеКоманды;
	
	ИсточникиКоманд = Новый Массив;
	ИсточникиКоманд.Добавить(Метаданные.Документы.ТранспортнаяНакладная);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = ПанельПодключаемыхКоманд;
	ПараметрыРазмещения.Источники = ИсточникиКоманд;
	ПодключаемыеКоманды.ПриСозданииНаСервере(Объект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	СкорректироватьСписокПодключаемыхКоманд(
		Объект,
		Объект.ПараметрыПодключаемыхКоманд.АдресТаблицыКоманд,
		ПанельПодключаемыхКоманд);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СкорректироватьСписокПодключаемыхКоманд(Форма, АдресТаблицыКоманд, ПанельПодключаемыхКоманд)

	Форма.ПодключенныеПечатныеФормы.Очистить();
	
	ТребуемыеКоманды = Новый Массив;
	ТребуемыеКоманды.Добавить("ТранспортнаяНакладная");
	
	ТаблицаКоманд = ПолучитьИзВременногоХранилища(АдресТаблицыКоманд); // ТаблицаЗначений
	
	Для Каждого СтрокаКоманды Из ТаблицаКоманд Цикл
		ЭлементФормы = Форма.Элементы.Найти(СтрокаКоманды.ИмяВФорме);
		Если ЭлементФормы <> Неопределено Тогда
			Если ТребуемыеКоманды.Найти(СтрокаКоманды.Вид) = Неопределено
					И ТребуемыеКоманды.Найти(СтрокаКоманды.Идентификатор) = Неопределено Тогда
				ЭлементФормы.Видимость = Ложь;
			ИначеЕсли СтрокаКоманды.Вид = "Печать" Тогда
				ЗаполнитьПодключенныеПечатныеФормы(
					Форма.ПодключенныеПечатныеФормы,
					СтрокаКоманды.ТипПараметра,
					СтрокаКоманды.ИмяВФорме);
				
				УстановитьВидЭлемента(ЭлементФормы.Родитель, ВидГруппыФормы.ГруппаКнопок, ПанельПодключаемыхКоманд);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьВидЭлемента(Элемент, ВидЭлемента, КонечныйРодитель = Неопределено)

	ИмяТипаПодменюФормы = "ПодменюФормы";
	ИмяТипаГруппаФормы = "ГруппаФормы";
	ИмяТипаГруппаКнопокФормы = "ГруппаКнопокФормы";
	
	Если Элемент <> Неопределено
			И Элемент <> КонечныйРодитель
			И (ТипЗнч(Элемент) = Тип(ИмяТипаПодменюФормы)
				Или ТипЗнч(Элемент) = Тип(ИмяТипаГруппаФормы)
				Или ТипЗнч(Элемент) = Тип(ИмяТипаГруппаКнопокФормы)) Тогда
		Элемент.Вид = ВидЭлемента;
		
		УстановитьВидЭлемента(Элемент.Родитель, ВидЭлемента, КонечныйРодитель);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПодключенныеПечатныеФормы(ПодключенныеПечатныеФормы, Знач ТипПараметра, Знач ИмяКомандыНаФорме)

	Если ТипЗнч(ТипПараметра) = Тип("ОписаниеТипов") Тогда
		МассивТипов = ТипПараметра.Типы();
	Иначе
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ТипПараметра);
	КонецЕсли;
	
	Для Каждого ТипПараметра Из МассивТипов Цикл
		ПодключенныеПечатныеФормы.Добавить(ИмяКомандыНаФорме, Строка(ТипПараметра));
	КонецЦикла;

КонецПроцедуры

&НаСервереБезКонтекста
Функция КомандыПечатиСформированнойОтгрузки(ПодключенныеПечатныеФормы, ДоступныеПечатныеФормы)

	КомандыПечати = Новый СписокЗначений;
	
	КомандыПечати.ЗагрузитьЗначения(ДоступныеПечатныеФормы);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСписок(КомандыПечати, ПодключенныеПечатныеФормы);
	
	Возврат КомандыПечати;

КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьДоступностьПечатныхФорм(Элементы, ПодключенныеПечатныеФормы, Доступность, ТипПараметра = Неопределено)

	Для Каждого КомандаНаФорме Из ПодключенныеПечатныеФормы Цикл
		Если ТипПараметра <> Неопределено И ТипПараметра <> КомандаНаФорме.Представление Тогда
			Продолжить;
		КонецЕсли;
		
		Элементы[КомандаНаФорме.Значение].Доступность = Доступность;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПечатныеФормы(ПечатныеДокументы, ПечатьСразуНаПринтер = Ложь)

	МассивУдаляемыхДокументов = Новый Массив;
	
	Для Каждого ПечатнаяФорма Из ПечатныеДокументы Цикл
		Если ПустаяСтрока(ПечатнаяФорма.Значение.АдресДвоичныхДанных) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Нет данных для отображения печатной формы ""%1"".';
						|en = 'There is no data to display the ""%1"" print form.'"),
					ПечатнаяФорма.Ключ));
			
			МассивУдаляемыхДокументов.Добавить(ПечатнаяФорма.Ключ);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого Ключ Из МассивУдаляемыхДокументов Цикл
		ПечатныеДокументы.Удалить(Ключ);
	КонецЦикла;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ПечатныеДокументы", ПечатныеДокументы);
	ПараметрыОткрытия.Вставить("ПечатьСразу",       ПечатьСразуНаПринтер);
	
	ОткрытьФорму("Справочник.УчетныеЗаписиМаркетплейсов.Форма.ПредварительныйПросмотрПечатныхФорм",
		ПараметрыОткрытия,
		ЭтотОбъект);
	
	ОбновитьЗаголовокКомандыПрисоединенныхФайлов();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПечатьЭтикетокГрузовыхМест(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная)

	НаименованиеФайла = НСтр("ru = 'Этикетки грузовых мест отгрузки';
							|en = 'Labels of shipment cargo units'");
	Адрес = Новый УникальныйИдентификатор();
	
	ДанныеФайла = РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьФайлЭтикетокГрузовыхМестОтгрузки(
		ИнформацияОбОтгрузке,
		ТранспортнаяНакладная,
		НаименованиеФайла,
		Адрес);
	
	ПечатныеДокументы.Вставить(НаименованиеФайла, ДанныеФайла);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПечатьКомплектаДокументов(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная)

	Если ИнформацияОбОтгрузке.ДоступныеПечатныеФормы.Найти("ПечатьНакладнойАкта") <> Неопределено Тогда
		ПодготовитьПечатьНакладнойАкта(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ДоступныеПечатныеФормы.Найти("ПечатьЛистаОтгрузки") <> Неопределено Тогда
		ПодготовитьПечатьЛистаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ДоступныеПечатныеФормы.Найти("ПечатьШтрихкодаОтгрузки") <> Неопределено Тогда
		ПодготовитьПечатьШтрихкодаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПечатьЛистаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная)

	НаименованиеФайла = НСтр("ru = 'Лист отгрузки';
							|en = 'Shipment list'");
	Адрес = Новый УникальныйИдентификатор();
	
	ДанныеФайла = РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьФайлЛистаОтгрузки(
		ИнформацияОбОтгрузке,
		ТранспортнаяНакладная,
		НаименованиеФайла,
		Адрес);
	
	ПечатныеДокументы.Вставить(НаименованиеФайла, ДанныеФайла);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПечатьШтрихкодаОтгрузки(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная)

	НаименованиеФайла = НСтр("ru = 'Штрихкод отгрузки';
							|en = 'Shipment barcode'");
	Адрес = Новый УникальныйИдентификатор();
	
	ДанныеФайла = РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьФайлШтрихкодаОтгрузки(
		ИнформацияОбОтгрузке,
		ТранспортнаяНакладная,
		НаименованиеФайла,
		Адрес);
	
	ПечатныеДокументы.Вставить(НаименованиеФайла, ДанныеФайла);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПодготовитьПечатьНакладнойАкта(ПечатныеДокументы, ИнформацияОбОтгрузке, ТранспортнаяНакладная)

	НаименованиеФайла = НСтр("ru = 'Транспортная накладная или акт';
							|en = 'Waybill or certificate'");
	Адрес = Новый УникальныйИдентификатор();
	
	ДанныеФайла = РегистрыСведений.ЗаказыТорговыхПлощадок.ПолучитьФайлНакладнойАкта(
		ИнформацияОбОтгрузке,
		ТранспортнаяНакладная,
		НаименованиеФайла,
		Адрес);
	
	ПечатныеДокументы.Вставить(НаименованиеФайла, ДанныеФайла);

КонецПроцедуры

#КонецОбласти

#КонецОбласти
