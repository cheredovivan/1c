
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ПричинаОтмены = Параметры.ПричинаОтмены;
	
	Элементы.ПричинаОтмены.ТолькоПросмотр = Не ПустаяСтрока(Параметры.ПричинаОтмены);
	
	Если Не ПустаяСтрока(Параметры.Пояснение) Тогда
		Элементы.Пояснение.Заголовок = Параметры.Пояснение;
	КонецЕсли;
	
	ИспользоватьПричиныОтменыЗаказовКлиентов = ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыЗаказовКлиентов");
	
	Элементы.ПричинаОтмены.Видимость        = ИспользоватьПричиныОтменыЗаказовКлиентов;
	Элементы.ПричинаОтменыСтрокой.Видимость = Не ИспользоватьПричиныОтменыЗаказовКлиентов;
	
	Если Не ИспользоватьПричиныОтменыЗаказовКлиентов Тогда
		ЗаполнитьПричиныОтменыСтрокойНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ЗаполнитьПричиныОтменыНаКлиенте", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодПричиныПриИзменении(Элемент)
	
	Если Не Элементы.ПричинаОтмены.ТолькоПросмотр Тогда
		КодПричиныПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Не ЗначениеЗаполнено(КодПричины) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указана причина отмены';
				|en = 'Cancelation reason is not specified'"),
			, 
			"КодПричины",
			, 
			Истина);
			
	ИначеЕсли Не ЗначениеЗаполнено(ПричинаОтмены) И ИспользоватьПричиныОтменыЗаказовКлиентов Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указана причина отмены';
				|en = 'Cancelation reason is not specified'"),
			, 
			"ПричинаОтмены",
			, 
			Истина);
			
	ИначеЕсли Не ЗначениеЗаполнено(ПричинаОтменыСтрокой) И Не ИспользоватьПричиныОтменыЗаказовКлиентов Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указана причина отмены';
				|en = 'Cancelation reason is not specified'"),
			, 
			"ПричинаОтменыСтрокой",
			, 
			Истина);
		
	Иначе
		Результат = ВыбратьНаСервере();
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьПричиныОтменыНаКлиенте()
	
	ДлительнаяОперация    = ЗаполнитьПричиныОтменыНаСервере();
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ЗаполнитьПричиныОтменыФрагмент", ЭтотОбъект);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ДлительнаяОперация);
		
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПричиныОтменыНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение            = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания  = НСтр("ru = 'Заполнение списка причин отмены для торговой площадки.';
															|en = 'Fill the list of cancelation reasons for the marketplace.'");
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;

	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.ПолучитьПричиныОтмены";

	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		ИмяМетода, 
		УчетнаяЗапись,
		Неопределено);
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПричиныОтменыФрагмент(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		
		ИначеЕсли Результат.Статус = "Выполнено" 
		 			И Результат.Свойство("АдресРезультата") Тогда
			Ошибка = ЗаполнитьПричиныОтменыЗавершениеНаСервере(Результат.АдресРезультата);
			ИнтеграцияСМаркетплейсомOzonКлиент.ВывестиСостояние(Ошибка, ДополнительныеПараметры, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьПричиныОтменыЗавершениеНаСервере(Знач АдресРезультата)
	
	Элементы.КодПричины.СписокВыбора.Очистить();
	Элементы.КодПричины.ПодсказкаВвода = НСтр("ru = 'Заполнение списка ...';
												|en = 'Populating the list ...'");
	
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
		
	Если ПустаяСтрока(Результат.КодОшибки)
		И Результат.Детализация <> Неопределено Тогда
		Для Каждого ЭлементКоллекции Из Результат.Детализация Цикл
			Элементы.КодПричины.СписокВыбора.Добавить(ЭлементКоллекции.Идентификатор, ЭлементКоллекции.Наименование);
		КонецЦикла;
		
	Иначе
		Элементы.КодПричины.СписокВыбора.Добавить("352", НСтр("ru = 'Товар закончился на складе продавца';
																|en = 'No items are left in the seller warehouse'"));
		Элементы.КодПричины.СписокВыбора.Добавить("400", НСтр("ru = 'Остался только бракованный товар';
																|en = 'Only nonconforming items are left'"));
		Элементы.КодПричины.СписокВыбора.Добавить("402", НСтр("ru = 'Другое (вина продавца)';
																|en = 'Other (seller''s fault)'"));
	КонецЕсли;
	
	Элементы.КодПричины.ПодсказкаВвода = "";
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура КодПричиныПриИзмененииНаСервере()
	
	ПричинаОтмены = РегистрыСведений.СоответствияОбъектовМаркетплейсов.ПолучитьОбъектСоответствия(
		УчетнаяЗапись, 
		ПредопределенноеЗначение("Перечисление.ВидыОбъектовМаркетплейсов.ПричинаОтмены"), 
		КодПричины,
		Тип("СправочникСсылка.ПричиныОтменыЗаказовКлиентов"));
		
	Если Не ИспользоватьПричиныОтменыЗаказовКлиентов Тогда
		ПричинаОтменыСтрокой = Строка(ПричинаОтмены);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПричиныОтменыСтрокойНаСервере()
	
	Элементы.ПричинаОтменыСтрокой.СписокВыбора.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПричиныОтменыЗаказовКлиентов.Ссылка КАК Ссылка,
		|	ПричиныОтменыЗаказовКлиентов.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПричиныОтменыЗаказовКлиентов КАК ПричиныОтменыЗаказовКлиентов";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",          УчетнаяЗапись);
	Запрос.УстановитьПараметр("ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.ПричинаОтмены);
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаДанных = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока ВыборкаДанных.Следующий() Цикл
		Элементы.ПричинаОтменыСтрокой.СписокВыбора.Добавить(ВыборкаДанных.Ссылка, ВыборкаДанных.Наименование);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВыбратьНаСервере()
	
	Если Не ИспользоватьПричиныОтменыЗаказовКлиентов Тогда
		ДанныеПричиныОтмены = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеПричиныОтменыЗаказа();
		ДанныеПричиныОтмены.Идентификатор = КодПричины;
		ДанныеПричиныОтмены.Наименование = ПричинаОтменыСтрокой;
		
		ПричинаОтмены = ИнтеграцияСМаркетплейсамиСервер.ПричинаОтменыЗаказаКлиента(УчетнаяЗапись, ДанныеПричиныОтмены);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПричинаОтмены) Тогда
		НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись);
		ПричинаОтмены          = НастройкиУчетнойЗаписи.ПричинаОтменыПоУмолчанию;
	КонецЕсли;
	
	Результат = Новый Структура; 
	Результат.Вставить("Идентификатор", КодПричины);
	Результат.Вставить("Наименование",  Строка(ПричинаОтмены));
	Результат.Вставить("Ссылка",        ПричинаОтмены);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
