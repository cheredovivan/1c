#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ТорговаяПлощадка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	НачалоПериода = НачалоДня(ТекущаяДатаСеанса());
	КонецПериода  = КонецДня(ТекущаяДатаСеанса());
	
	Заголовок = Параметры.ЗаголовокФормы;
	ЭтоFBO    = Параметры.ЭтоFBO;
		
	Если ЭтоFBO Тогда
		Элементы.ДекорацияФильтрСтатуса.Ширина                  = 5;
		Элементы.ГруппаФильтрПериода.Видимость                  = Ложь;
		Элементы.ФильтрСопоставления.Видимость                  = Ложь;
		Элементы.ГруппаДокументы.Видимость                      = Ложь;
		Элементы.Действия.Видимость                             = Ложь;
		Элементы.КонтекстноеМенюКомандыFBS.Видимость            = Ложь;
		Элементы.ТаблицаЗаказовДатаОтгрузки.Видимость           = Ложь;
		Элементы.ТаблицаЗаказовГруппаДанныеДоставки.Видимость   = Ложь;
		Элементы.ТаблицаЗаказовГородДоставки.Видимость          = Ложь;
		Элементы.ТаблицаЗаказовДополнительныеСведения.Видимость = Ложь;
	Иначе
		ФильтрДаты          = "ДатаСоздания";
		ФильтрСопоставления = "Все";
	КонецЕсли;
	
	НастроитьФормуПоУчетнойЗаписи();
	УстановитьУсловноеОформление();
	УправлениеВидимостьюДоступностью();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПроверитьПериодИПолучитьОтправленияНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ИмяСобытия = "МП_ВыполненаЗагрузкаЗаказовТорговойПлощадки"
				Или ИмяСобытия = "МП_ВыполненаОтменаСборкиЗаказовНаТорговойПлощадке"
				Или ИмяСобытия = "МП_ВыполненоПодтверждениеСборкиЗаказовНаТорговойПлощадке"
				Или ИмяСобытия = "МП_ВыполненоОткрытиеСпораНаТорговойПлощадке"
				Или ИмяСобытия = "МП_ВыполненВозвратКОтгрузкеНаТорговойПлощадке")
			И Не ЭтоFBO
			И Параметр = УчетнаяЗапись Тогда
		ОпределитьДокументыFBS(Ложь);
		ОбновитьТекстДокументыНаОсновании();
		
	ИначеЕсли (ИмяСобытия = "МП_ОбновлениеСведенийПоЗаказамИОтправлениям"
					Или ИмяСобытия = "Запись_ЗаказКлиента"
					Или ИмяСобытия = "Запись_РеализацияТоваровУслуг"
					Или ИмяСобытия = "Запись_ПередачаТоваровХранителю")
				И Не ЭтоFBO Тогда
		ОпределитьДокументыFBS(Ложь);
		ОбновитьТекстДокументыНаОсновании();
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант"
				И (Источник = "ИспользоватьЗаказыКлиентов"
					Или Источник = "ИспользоватьРасширенныеВозможностиЗаказаКлиента") Тогда
		УправлениеВидимостьюДоступностью();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НачалоПериодаПриИзменении(Элемент)
	
	ДатаАктуальностиДанных = Дата(1, 1, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоПериодаОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, СтандартнаяОбработка)
	
	ВыбранноеЗначение      = НачалоДня(ВыбранноеЗначение);
	ДатаАктуальностиДанных = Дата(1, 1, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаПриИзменении(Элемент)
	
	ДатаАктуальностиДанных = Дата(1, 1, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура КонецПериодаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ВыбранноеЗначение      = КонецДня(ВыбранноеЗначение);
	ДатаАктуальностиДанных = Дата(1, 1, 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрСтатусаПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрСтатусаОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ФильтрСтатуса = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка");
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрПоСкладамПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ФильтрСопоставленияПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ДокументОтгрузкиНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ТаблицаЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущиеДанные.ДокументОтгрузки) = Тип("СписокЗначений") Тогда
		Результат = Ждать ТекущиеДанные.ДокументОтгрузки.ВыбратьЭлементАсинх(НСтр("ru = 'Документ отгрузки';
																					|en = 'Shipping document'"));
		
		Если Результат <> Неопределено Тогда
			ПоказатьЗначение(Неопределено, Результат.Значение);
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ТекущиеДанные.ДокументОтгрузки) Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.ДокументОтгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстДокументыНаОснованииОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ТаблицаЗаказов.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьЗаказ" Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.ДокументЗаказа);
	
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "ОткрытьДокументОтгрузки" Тогда
		ПоказатьЗначение(Неопределено, ТекущиеДанные.ДокументОтгрузки);
	
	ИначеЕсли СтрНайти(НавигационнаяСсылкаФорматированнойСтроки, "ОткрытьДокументОтгрузки") > 0 Тогда
		Индекс = Число(СтрЗаменить(НавигационнаяСсылкаФорматированнойСтроки, "ОткрытьДокументОтгрузки", ""));
		ПоказатьЗначение(Неопределено, ТекущиеДанные.ДокументОтгрузки[Индекс].Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаЗаказов

&НаКлиенте
Процедура ТаблицаЗаказовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущаяСтрока = Элементы.ТаблицаЗаказов.ТекущаяСтрока;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа",
		ПараметрыПросмотраЗаказа(ТекущаяСтрока),
		ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаказовПриАктивизацииСтроки(Элемент)
	
	ТекущиеДанные            = Элементы.ТаблицаЗаказов.ТекущиеДанные;
	ПредставлениеОтправления = "";
	
	Если Элементы.ГруппаПодвал.Видимость 
			И ТекущиеДанные <> Неопределено Тогда
		ПредставлениеОтправления = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данные по отправлению %1 заказа %2';
					|en = 'Data on the %1 shipment of the %2 order'"),
				ТекущиеДанные.НомерОтправления,
				ТекущиеДанные.НомерЗаказа);
	КонецЕсли;
		
	ОбновитьТекстДокументыНаОсновании();
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаказовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаказовПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗаказовПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьПериод(Элемент)
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработкаВыбораПериода", ЭтотОбъект);
	
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Новый СтандартныйПериод(НачалоПериода, КонецПериода);
	Диалог.Показать(ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПериодИПолучитьОтправления(Команда)
	
	ПроверитьПериодИПолучитьОтправленияНаКлиенте();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Дата актуальности
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаАктуальностиДанных.Имя);
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ДатаАктуальностиДанных");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Дата(1, 1, 1);
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийОшибкуТекст);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Шрифт",      ШрифтыСтиля.ОбычныйШрифтТекста);
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст",      НСтр("ru = '<требуется обновление данных>';
																						|en = '<data updated is required>'"));
	
	// Незагруженные заказы
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ЭлементыТаблицы = ПолучитьПодчиненныеЭлементыФормы(Элементы.ТаблицаЗаказов);
	Для Каждого ЭлементКоллекции Из ЭлементыТаблицы Цикл
		ПолеЭлемента      = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ЭлементКоллекции.Имя);
	КонецЦикла;
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ФильтрСопоставления");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = "Все";
	
	ОтборЭлемента                = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ТаблицаЗаказов.ДокументЗаказа");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюДоступностью()
	
	Если ЭтоFBO Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияФункциональныхОпций = ИнтеграцияСМаркетплейсамиСервер.ПолучитьФункциональныеОпции(Неопределено);
	ДоступностьКоманд           = ЗначенияФункциональныхОпций.ИспользоватьЗаказыКлиентов
									И ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	Элементы.Действия.Доступность                  = ДоступностьКоманд;
	Элементы.КонтекстноеМенюКомандыFBS.Доступность = ДоступностьКоманд;
	
КонецПроцедуры  

// Настраивает элементы формы в соответствии с данными о торговой площадке.
&НаСервере
Процедура НастроитьФормуПоУчетнойЗаписи()
	
	Элементы.Логотип.Картинка = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ТорговаяПлощадка);
	
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Элементы.ГруппаФильтрПоСкладам.Видимость = Истина;
		Элементы.ДекорацияТестовыйРежим.Видимость = Ложь;
		Элементы.ФильтрДаты.СписокВыбора.Очистить();
		Элементы.ФильтрДаты.СписокВыбора.Добавить("ДатаСоздания",НСтр("ru = 'создания';
																		|en = 'creation'"));  
		Элементы.ФильтрДаты.СписокВыбора.Добавить("ДатаСборки",НСтр("ru = 'отгрузки';
																	|en = 'shipment'"));                       
		Элементы.ФильтрДаты.СписокВыбора.Добавить("ДатаПередачиВДоставку",НСтр("ru = 'передачи в доставку';
																				|en = 'transfer to delivery service'")); 
		Элементы.ДекорацияФильтрДатыПодсказка.Подсказка = НСтр("ru = 'Вариант отображения заказов:
													|- по дате создания - заказы в любом статусе;
													|- по дате сборки - только незавершенные заказы;
													|- по дате передачи в доставку - только переданные в доставку заказы.';
													|en = 'Order display option:
													|- By creation date. Orders in any status.
													|- By picking date. Only open orders.
													|- By date of transfer to delivery service. Only orders transferred to the delivery service.'");
		
		Элементы.ОтменитьСборкуЗаказов.Заголовок = НСтр("ru = 'Отменить сборку/доставку';
														|en = 'Cancel picking/delivery'"); 
		Элементы.КонтекстноеМенюОтменитьСборкуЗаказов.Заголовок = НСтр("ru = 'Отменить сборку/доставку';
																		|en = 'Cancel picking/delivery'");
		Элементы.ГруппаКомандыПередачиВДоставку.Видимость = Истина;
		Элементы.ГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость = Истина;
		Элементы.КонтекстноеМенюГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость = Истина; 
		Элементы.ТаблицаЗаказовДополнительныеСведения.Видимость = Истина;
		
	ИначеЕсли ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Элементы.ГруппаФильтрПоСкладам.Видимость = Ложь;
		ДанныеУчетнойЗаписи = ИнтеграцияСЯндексМаркетСервер.ДанныеУчетнойЗаписиЯндексМаркет(УчетнаяЗапись);
		Элементы.ДекорацияТестовыйРежим.Видимость = ДанныеУчетнойЗаписи.ТестовыйРежимЗаказовFBS;
		
		Элементы.ФильтрДаты.СписокВыбора.Очистить();
		Элементы.ФильтрДаты.СписокВыбора.Добавить(
			ИнтеграцияСЯндексМаркетСервер.ВидФильтраПоДатеСоздания(), НСтр("ru = 'создания';
																			|en = 'creation'"));
		Элементы.ФильтрДаты.СписокВыбора.Добавить(
			ИнтеграцияСЯндексМаркетСервер.ВидФильтраПоДатеОтгрузки(), НСтр("ru = 'отгрузки';
																			|en = 'shipment'"));
		Элементы.ФильтрДаты.СписокВыбора.Добавить(
			ИнтеграцияСЯндексМаркетСервер.ВидФильтраПоДатеОбновленияСтатуса(), НСтр("ru = 'обновления статуса заказа';
																					|en = 'order status updates'"));
			
		Элементы.ДекорацияФильтрДатыПодсказка.Подсказка = НСтр("ru = 'Вариант отображения заказов:
													|- по дате создания - заказы, с датой создания в указанном периоде;
													|- по дате отгрузки - заказы, с датой отгрузки в доставку до торговой площадки в указанном периоде;
													|- по дате обновления статуса заказа - заказы, обновление статусов которых произошло в указанном периоде 
													|  (рекомендуется использовать фильтр по дате обновления статуса для отслеживании заказов, переданных в доставку до покупателя).';
													|en = 'Order display option:
													|- By creation date. Orders whose creation date is in the specified period.
													|- By shipment date. Orders whose date of shipment for delivery to the trading platform is in the specified period.
													|- By order status update date. Orders whose statuses were updated in the specified period (
													|). We recommend that you use the filter by status update date to track orders transferred to delivery to the customer.'");
		
		Элементы.ОтменитьСборкуЗаказов.Заголовок = НСтр("ru = 'Отменить сборку';
														|en = 'Cancel picking'");
		Элементы.КонтекстноеМенюОтменитьСборкуЗаказов.Заголовок = НСтр("ru = 'Отменить сборку';
																		|en = 'Cancel picking'"); 
		Элементы.ГруппаКомандыПередачиВДоставку.Видимость = Ложь;
		Элементы.ГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость = Ложь;
		Элементы.КонтекстноеМенюГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость = Ложь;
		Элементы.ТаблицаЗаказовДатаНачалаОбработкиОтправления.Заголовок = НСтр("ru = 'Дата обновления статуса';
																				|en = 'Status update date'");
		Элементы.ТаблицаЗаказовДополнительныеСведения.Видимость = Ложь;
		
		Элементы.НачалоПериода.ФорматРедактирования = "ДЛФ=D";
		Элементы.КонецПериода.ФорматРедактирования = "ДЛФ=D";
		Элементы.ТаблицаЗаказовДатаОтгрузки.Формат = "ДЛФ=D";
		
		Элементы.ТаблицаЗаказовТипДоставки.Видимость = Ложь;
		
	КонецЕсли;
	
	Перечисления.СтатусыЗаказовТорговыхПлощадок.ЗаполнитьСписокВыбора(Элементы.ФильтрСтатуса.СписокВыбора, ТорговаяПлощадка);
	
КонецПроцедуры

// Определяет подчиненные элементы.
//
// Параметры:
//   Элемент - ЭлементыФормы - родительский элемент.
//
// Возвращаемое значение:
//   Массив Из ЭлементыФормы - подчиненные элементы.
//
&НаСервере
Функция ПолучитьПодчиненныеЭлементыФормы(Знач Элемент)
	
	Результат = Новый Массив;
	
	Для Каждого ЭлементКоллекции Из Элемент.ПодчиненныеЭлементы Цикл
		Если ТипЗнч(ЭлементКоллекции) = Тип("ГруппаФормы") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(Результат, ПолучитьПодчиненныеЭлементыФормы(ЭлементКоллекции));
		Иначе
			Результат.Добавить(ЭлементКоллекции);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Обработчик события выбора периода.
//
// Параметры:
//   Период - СтандартныйПериод - выбранный период.
//   ДополнительныеПараметры - Неопределено, Произвольный - переданные дополнительные параметры.
//
&НаКлиенте
Процедура ОбработкаВыбораПериода(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		НачалоПериода = НачалоДня(Период.ДатаНачала);
		КонецПериода  = КонецДня(Период.ДатаОкончания);
		
		ДатаАктуальностиДанных = Дата(1, 1, 1);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтбор()
	
	Отбор = Новый Структура;
	
	Если ЗначениеЗаполнено(ФильтрСтатуса) Тогда
		Отбор.Вставить("СтатусОтправления", ФильтрСтатуса);
	КонецЕсли;
	
	Если Не ЭтоFBO Тогда   
		Если ЗначениеЗаполнено(ФильтрПоСкладам) Тогда
			Отбор.Вставить("ИдентификаторСклада", ФильтрПоСкладам);
		КонецЕсли;
		Если ФильтрСопоставления = "Незагруженные" Тогда
			Отбор.Вставить("Сопоставлено", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Если Отбор.Количество() > 0 Тогда
		Элементы.ТаблицаЗаказов.ОтборСтрок = Новый ФиксированнаяСтруктура(Отбор);
	Иначе
		Элементы.ТаблицаЗаказов.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПериодИПолучитьОтправленияНаКлиенте()
	
	ОчиститьСообщения();   
	
	Если НачалоПериода > КонецПериода Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Некорректно задан период: дата окончания ранее даты начала.';
				|en = 'Incorrect period: the end date is earlier than the start date.'"), , "НачалоПериода");
		Возврат;
	КонецЕсли;
	
	РазницаВДнях = (НачалоДня(КонецПериода) - НачалоДня(НачалоПериода)) / (60 * 60 * 24);     
	
	Если РазницаВДнях > 30 Тогда
		ТекстВопроса = Нстр("ru = 'Период запроса данных превышает месяц. 
								  |Чтобы избежать долгой загрузки данных рекомендуется сократить запрашиваемый период.
								  |Продолжить получение данных?';
								  |en = 'The requested period for importing data exceeds one month. 
								  |To avoid a long data import, shorten the requested period.
								  |Continue data import?'");
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПроверитьПериодИПолучитьОтправленияЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);
		
	Иначе
		ПроверитьПериодИПолучитьОтправленияЗавершение(КодВозвратаДиалога.Да, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПериодИПолучитьОтправленияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ПолучитьОтправленияНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОтправленияНаКлиенте()
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучитьОтправленияЗавершениеФоновогоЗадания", ЭтотОбъект);
	ДлительнаяОперация    = ПолучитьОтправленияНаСервере();
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, ДлительнаяОперация);
		
	Иначе
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания			   = Истина;
		ПараметрыОжидания.ТекстСообщения				   = НСтр("ru = 'Получение данных о заказах';
																	|en = 'Get order data'") + " " + ?(ЭтоFBO, "FBO", "FBS") + ".";
		ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;   
		Если ТорговаяПлощадка = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда    
			НаименованиеТорговойПлощадки = НСтр("ru = 'Ozon';
												|en = 'Ozon'");	
		ИначеЕсли ТорговаяПлощадка = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
			НаименованиеТорговойПлощадки = НСтр("ru = 'Яндекс Маркет';
												|en = 'Yandex.Market'");
		КонецЕсли;			
		ПараметрыОжидания.ОповещениеПользователя.Текст 	   = НаименованиеТорговойПлощадки;
		ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершено получение данных о заказах FBS';
																	|en = 'FBS order data is received'") + " " + ?(ЭтоFBO, "FBO", "FBS") + ".";
		Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ТорговаяПлощадка);
		ПараметрыОжидания.ОповещениеПользователя.Картинка  = Логотип;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьОтправленияНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение            = 0;   
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда    
		НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Получение данных о заказах';
											|en = 'Ozon. Get order data'") + " " + ?(ЭтоFBO, "FBO", "FBS") + ".";	
	ИначеЕсли ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		НаименованиеФоновогоЗадания = НСтр("ru = 'Яндекс Маркет. Получение данных о заказах FBS.';
											|en = 'Yandex.Market. Get FBS order data'");
	КонецЕсли;	
	ПараметрыВыполнения.НаименованиеФоновогоЗадания  = НаименованиеФоновогоЗадания;
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;
	
	Если ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("НачалоПериода",       НачалоПериода);
		ПараметрыФункции.Вставить("КонецПериода",        КонецПериода);
		ПараметрыФункции.Вставить("ВидФильтраПоПериоду", ФильтрДаты);
		ПараметрыФункции.Вставить("СтатусОтправления",   "");
		ПараметрыФункции.Вставить("ЭтоFBO",              ЭтоFBO);
		
		ИмяМетода = "ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьОтправления";   
	ИначеЕсли ТорговаяПлощадка = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("НачалоПериода",       НачалоПериода);
		ПараметрыФункции.Вставить("КонецПериода",        КонецПериода);
		ПараметрыФункции.Вставить("ВидФильтраПоПериоду", ФильтрДаты);
		ПараметрыФункции.Вставить("СтатусЗаказа",   	 ФильтрСтатуса);
		
		ИмяМетода = "ИнтеграцияСЯндексМаркетСервер.ПолучитьЗаказыFBS";
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения,
		ИмяМетода,
		УчетнаяЗапись,
		ПараметрыФункции,
		РеквизитФормыВЗначение("ТаблицаЗаказов"));
	
КонецФункции

&НаКлиенте
Процедура ПолучитьОтправленияЗавершениеФоновогоЗадания(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.ПодробноеПредставлениеОшибки);
		
		ИначеЕсли Результат.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьОтправленияЗавершениеФоновогоЗаданияНаСервере(Результат.АдресРезультата);
			ИнтеграцияСМаркетплейсомOzonКлиент.ВывестиСостояние(Ошибка, ДополнительныеПараметры, Истина);
			
			Элементы.ТаблицаЗаказов.Обновить();
			ОбновитьТекстДокументыНаОсновании();
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоFBO Тогда
		УстановитьОтбор();
	Иначе
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ПолучениеСпискаСкладовЗавершение", ЭтотОбъект);
		ДлительнаяОперация    = ЗапуститьПолучениеСпискаСкладов(
			ТорговаяПлощадка, УчетнаяЗапись, УникальныйИдентификатор);
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает результат получения отправлений.
//
// Параметры:
//   АдресРезультата - Строка - адрес временного хранилища, в которое будет помещен (или уже помещен) результат работы 
//                       процедуры длительной операции.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
&НаСервере
Функция ПолучитьОтправленияЗавершениеФоновогоЗаданияНаСервере(Знач АдресРезультата)
	
	ТаблицаЗаказов.Очистить();
	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	
	Если ПустаяСтрока(Результат.Ошибка.КодОшибки) Тогда
		ДатаАктуальностиДанных = ТекущаяДатаСеанса();
		ЗначениеВРеквизитФормы(Результат.ТаблицаЗаказов, "ТаблицаЗаказов");
		
		Если Не ЭтоFBO Тогда
			ОпределитьДокументыFBS(Ложь, Истина);
		КонецЕсли;
		
	Иначе
		ДатаАктуальностиДанных = Дата(1, 1, 1);
	КонецЕсли;
	
	Возврат Результат.Ошибка;
	
КонецФункции

&НаСервере
Функция ПараметрыПросмотраЗаказа(ИдентификаторСтроки)
	
	ТекущиеДанные = ТаблицаЗаказов.НайтиПоИдентификатору(ИдентификаторСтроки);
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись",           УчетнаяЗапись);
	ПараметрыФормы.Вставить("ИдентификаторЗаказа",     ТекущиеДанные.ИдентификаторЗаказа);
	ПараметрыФормы.Вставить("НомерЗаказа",             ТекущиеДанные.НомерЗаказа);
	ПараметрыФормы.Вставить("КомментарийКЗаказу",      ТекущиеДанные.КомментарийКЗаказу);
	ПараметрыФормы.Вставить("ДатаОтправления", 		   ТекущиеДанные.ДатаОтправления);
	ПараметрыФормы.Вставить("НомерОтправления",        ТекущиеДанные.НомерОтправления);
	ПараметрыФормы.Вставить("СтатусОтправления",       ТекущиеДанные.СтатусОтправления);
	ПараметрыФормы.Вставить("ДатаОтгрузки",            ТекущиеДанные.ДатаОтгрузки);
	ПараметрыФормы.Вставить("ДатаДоставки",            ТекущиеДанные.ДатаДоставки);
	ПараметрыФормы.Вставить("ДатаПередачиВДоставку",   ТекущиеДанные.ДатаПередачиВДоставку);
	ПараметрыФормы.Вставить("СпособДоставки",          ТекущиеДанные.СпособДоставки);
	ПараметрыФормы.Вставить("ТипДоставки",             ТекущиеДанные.ТипДоставки);
	ПараметрыФормы.Вставить("РегионДоставки",          ТекущиеДанные.РегионДоставки);
	ПараметрыФормы.Вставить("ГородДоставки",           ТекущиеДанные.ГородДоставки);
	ПараметрыФормы.Вставить("СпособОплаты",            ТекущиеДанные.СпособОплаты);
	ПараметрыФормы.Вставить("ПричинаОтмены",           ТекущиеДанные.ПричинаОтменыОтправления);
	ПараметрыФормы.Вставить("ТрекНомерОтправления",    ТекущиеДанные.ТрекНомерОтправления);
	ПараметрыФормы.Вставить("НаименованиеСклада",      ТекущиеДанные.НаименованиеСклада);
	ПараметрыФормы.Вставить("АдресСодержимогоЗаказа",  ПоместитьВоВременноеХранилище(ТекущиеДанные.СписокТоваровВОтправлении.Выгрузить()));
	ПараметрыФормы.Вставить("ДокументЗаказа",          ТекущиеДанные.ДокументЗаказа);
	ПараметрыФормы.Вставить("ДокументОтгрузки",        ТекущиеДанные.ДокументОтгрузки);
	ПараметрыФормы.Вставить("ДополнительныеСведения",  ТекущиеДанные.ДополнительныеСведения);
	ПараметрыФормы.Вставить("СписокДоступныхДействий", ТекущиеДанные.СписокДоступныхДействий);
	ПараметрыФормы.Вставить("ЭтоFBO",                  ЭтоFBO);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗапуститьПолучениеСпискаСкладов(ВидМаркетплейса, УчетнаяЗапись, УникальныйИдентификатор)
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Ozon. Получение списка складов.';
															|en = 'Ozon. Get a warehouse list.'");
	ПараметрыВыполнения.ЗапуститьВФоне              = Истина;
	
	ИмяМетода = "ИнтеграцияСМаркетплейсамиСервер.ПолучитьСкладыСервиса";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		ИмяМетода,
		ВидМаркетплейса,
		УчетнаяЗапись);
	
КонецФункции

&НаКлиенте
Процедура ПолучениеСпискаСкладовЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	ОчиститьСообщения();
	
	ФильтрПоСкладам = "";
	СписокМетодовДоставки = Новый Структура;
	
	Элементы.ФильтрПоСкладам.СписокВыбора.Очистить();
	Элементы.ФильтрПоСкладам.СписокВыбора.Добавить("", НСтр("ru = 'Все';
															|en = 'All'"));
	
	Если РезультатЗадания <> Неопределено 
			И РезультатЗадания.Статус = "Выполнено" Тогда
		Ошибка = ЗаполнитьСписокСкладовИМетодовДоставки(РезультатЗадания.АдресРезультата);
		
		ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(
			Ошибка,
			,
			Истина,
			Элементы.Логотип.Картинка);
		
	Иначе
		ШаблонОшибки = НСтр("ru = 'Не удалось получить склады учетной записи ""%1"" по причине: %2';
							|en = 'Cannot get the ""%1"" account warehouses due to: %2'");
		ПредставлениеНеизвестнойОшибки = НСтр("ru = 'Неизвестная ошибка выполнения операции';
												|en = 'Unknown operation error'");
		ПодробноеПредставлениеОшибки = ?(РезультатЗадания = Неопределено, ПредставлениеНеизвестнойОшибки, РезультатЗадания.ПодробноеПредставлениеОшибки);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки,
				УчетнаяЗапись,
				ПодробноеПредставлениеОшибки);
				
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокСкладовИМетодовДоставки(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если Результат = Неопределено Тогда
		Возврат ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСервер.ЗаполнитьСписокСкладовИМетодовДоставки(
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса"),
		Результат.ТаблицаСкладов,
		Элементы.ФильтрПоСкладам.СписокВыбора,
		СписокМетодовДоставки);
	
	Элементы.ФильтрПоСкладам.СписокВыбора.Вставить(0, "", НСтр("ru = 'Все';
																|en = 'All'"));
	
	Возврат Результат.Ошибка; // ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка

КонецФункции

&НаСервере
Процедура ОпределитьДокументыFBS(Знач ТолькоНеСопоставленные = Ложь, Знач СтатусыОбновлены = Ложь)
	
	Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗаказов Цикл
		СтрокаТаблицыЗначений.ДокументЗаказа   = Документы.ЗаказКлиента.ПустаяСсылка();
		СтрокаТаблицыЗначений.ДокументОтгрузки = Неопределено;
		СтрокаТаблицыЗначений.Сопоставлено     = Ложь;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаЗаказов.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерЗаказа КАК НомерЗаказа,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерОтправления,
		|	ТаблицаЗаказов.СтатусОтправления КАК СтатусОтправления
		|ПОМЕСТИТЬ ВТ_ТаблицаЗаказов
		|ИЗ
		|	&ТаблицаЗаказов КАК ТаблицаЗаказов
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерЗаказа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗаказов.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерЗаказа КАК НомерЗаказа,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерОтправления,
		|	ВЫБОР
		|		КОГДА &СтатусыОбновлены
		|			ТОГДА ТаблицаЗаказов.СтатусОтправления
		|		ИНАЧЕ ЕСТЬNULL(ЗаказыТорговыхПлощадок.Статус, ТаблицаЗаказов.СтатусОтправления)
		|	КОНЕЦ КАК СтатусОтправления,
		|	ЕСТЬNULL(ЗаказыТорговыхПлощадок.Заказ, ЗНАЧЕНИЕ(Документ.ЗаказКлиента.ПустаяСсылка)) КАК Заказ,
		|	ЕСТЬNULL(ДокументЗаказКлиента.ПометкаУдаления, ИСТИНА) КАК ЗаказКлиентаПометкаУдаления,
		|	ЕСТЬNULL(ЗаказыТорговыхПлощадок.ДокументОтгрузки, НЕОПРЕДЕЛЕНО) КАК ДокументОтгрузки
		|ИЗ
		|	ВТ_ТаблицаЗаказов КАК ТаблицаЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО (ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись)
		|			И ТаблицаЗаказов.НомерОтправления = ЗаказыТорговыхПлощадок.НомерОтправления
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ПО (ЗаказыТорговыхПлощадок.Заказ = ДокументЗаказКлиента.Ссылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ТаблицаЗаказов.ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления,
		|	ЗаказыТорговыхПлощадок.Статус,
		|	ЗаказыТорговыхПлощадок.Заказ,
		|	ЕСТЬNULL(ДокументЗаказКлиента.ПометкаУдаления, ЛОЖЬ),
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки
		|ИЗ
		|	ВТ_ТаблицаЗаказов КАК ТаблицаЗаказов
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО (ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись)
		|			И ТаблицаЗаказов.НомерОтправления = ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления
		|			И ТаблицаЗаказов.НомерОтправления ПОДОБНО &ПостфиксОтправления
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументЗаказКлиента
		|		ПО (ЗаказыТорговыхПлощадок.Заказ = ДокументЗаказКлиента.Ссылка)
		|ГДЕ
		|	НЕ ЗаказыТорговыхПлощадок.Заказ ЕСТЬ NULL";
	
	Если ТолькоНеСопоставленные Тогда
		Запрос.Текст = Запрос.Текст
			+ "
			|ГДЕ
			|	ЗаказыТорговыхПлощадок.Заказ ЕСТЬ NULL
			|		ИЛИ ДокументЗаказКлиента.ПометкаУдаления";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",       УчетнаяЗапись);
	Запрос.УстановитьПараметр("ТаблицаЗаказов",      ТаблицаЗаказов.Выгрузить());
	Запрос.УстановитьПараметр("ПостфиксОтправления", "%" + РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления() + "%");
	Запрос.УстановитьПараметр("СтатусыОбновлены",    СтатусыОбновлены);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДанных = РезультатЗапроса.Выбрать();
		
		Для Каждого СтрокаТаблицыЗначений Из ТаблицаЗаказов Цикл
			Отбор = Новый Структура;
			Отбор.Вставить("НомерЗаказа",      СтрокаТаблицыЗначений.НомерЗаказа);
			Отбор.Вставить("НомерОтправления", СтрокаТаблицыЗначений.НомерОтправления);
			
			ВыборкаДанных.Сбросить();
			Пока ВыборкаДанных.НайтиСледующий(Отбор) Цикл
				СтрокаТаблицыЗначений.СтатусОтправления = ВыборкаДанных.СтатусОтправления;
				Если Не ВыборкаДанных.ЗаказКлиентаПометкаУдаления Тогда
					СтрокаТаблицыЗначений.ДокументЗаказа = ВыборкаДанных.Заказ;
					Если ЗначениеЗаполнено(ВыборкаДанных.ДокументОтгрузки) Тогда
						Если ТипЗнч(СтрокаТаблицыЗначений.ДокументОтгрузки) = Тип("СписокЗначений") Тогда
							СтрокаТаблицыЗначений.ДокументОтгрузки.Добавить(ВыборкаДанных.ДокументОтгрузки);
						ИначеЕсли ЗначениеЗаполнено(СтрокаТаблицыЗначений.ДокументОтгрузки) Тогда
							ДокументОтгрузки = СтрокаТаблицыЗначений.ДокументОтгрузки;
							СтрокаТаблицыЗначений.ДокументОтгрузки = Новый СписокЗначений;
							СтрокаТаблицыЗначений.ДокументОтгрузки.Добавить(ДокументОтгрузки);
							СтрокаТаблицыЗначений.ДокументОтгрузки.Добавить(ВыборкаДанных.ДокументОтгрузки);
						Иначе
							СтрокаТаблицыЗначений.ДокументОтгрузки = ВыборкаДанных.ДокументОтгрузки;
						КонецЕсли;
					КонецЕсли;
					
				Иначе
					СтрокаТаблицыЗначений.ДокументЗаказа   = Документы.ЗаказКлиента.ПустаяСсылка();
					СтрокаТаблицыЗначений.ДокументОтгрузки = Неопределено;
				КонецЕсли;
				
				СтрокаТаблицыЗначений.Сопоставлено = ЗначениеЗаполнено(СтрокаТаблицыЗначений.ДокументЗаказа);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Элементы.ТаблицаЗаказов.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстДокументыНаОсновании()
	
	ТекущиеДанные       = Элементы.ТаблицаЗаказов.ТекущиеДанные;
	ИдентификаторСтроки = -1;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ИдентификаторСтроки = ТекущиеДанные.ПолучитьИдентификатор();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьТекстДокументыНаОсновании", 0.1, Истина);
	
КонецПроцедуры
	
&НаКлиенте
Процедура Подключаемый_ОбновитьТекстДокументыНаОсновании()
	
	ТекущиеДанные = Элементы.ТаблицаЗаказов.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Элементы.ТекстДокументыНаОсновании.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Если ИдентификаторСтроки <> ТекущиеДанные.ПолучитьИдентификатор() Тогда
		Возврат;
	КонецЕсли;
	
	ТекстыПоляДокументыНаОсновании.Очистить();
	ЦветТекста = Элементы.ТекстДокументыНаОсновании.ЦветТекста;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументЗаказа) Тогда
		НавигационнаяСсылкаКоманды = "ОткрытьЗаказ";
		ПредставлениеДокумента = Новый ФорматированнаяСтрока(Строка(ТекущиеДанные.ДокументЗаказа),, ЦветТекста,, НавигационнаяСсылкаКоманды);
		ТекстыПоляДокументыНаОсновании.Добавить(ПредставлениеДокумента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументОтгрузки) Тогда
		Если ТипЗнч(ТекущиеДанные.ДокументОтгрузки) = Тип("СписокЗначений") Тогда
			Для Каждого ЭлементКоллекции Из ТекущиеДанные.ДокументОтгрузки Цикл
				НавигационнаяСсылкаКоманды = "ОткрытьДокументОтгрузки" + Формат(ТекущиеДанные.ДокументОтгрузки.Индекс(ЭлементКоллекции), "ЧН=; ЧГ=0");
				ПредставлениеДокумента = Новый ФорматированнаяСтрока(Строка(ЭлементКоллекции),, ЦветТекста,, НавигационнаяСсылкаКоманды);
				ТекстыПоляДокументыНаОсновании.Добавить(ПредставлениеДокумента);
			КонецЦикла;
		Иначе
			НавигационнаяСсылкаКоманды = "ОткрытьДокументОтгрузки";
			ПредставлениеДокумента = Новый ФорматированнаяСтрока(Строка(ТекущиеДанные.ДокументОтгрузки),, ЦветТекста,, НавигационнаяСсылкаКоманды);
			ТекстыПоляДокументыНаОсновании.Добавить(ПредставлениеДокумента);
		КонецЕсли;
	КонецЕсли;
	
	ОбщегоНазначенияУТКлиентСервер.ОбновитьТекстДокументыНаОсновании(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
