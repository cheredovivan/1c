
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьУсловноеОформление();
	
	Если ЗначениеЗаполнено(Параметры.ЗаголовокФормы) Тогда
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;       
	
	ИспользоватьПричиныОтменыЗаказовКлиентов = ПолучитьФункциональнуюОпцию("ИспользоватьПричиныОтменыЗаказовКлиентов");
	
	ОбщегоНазначенияУТ.НастроитьПодключаемоеОборудование(ЭтотОбъект);
	
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ДанныеТорговойПлощадки = ДанныеПоТорговойПлощадке(УчетнаяЗапись); 
	
	СтруктураБыстрогоОтбора = Неопределено;
	Параметры.Свойство("СтруктураБыстрогоОтбора", СтруктураБыстрогоОтбора);
	
	ВидМаркетплейса = ДанныеТорговойПлощадки.ВидМаркетплейса;
	ЭтоЯндекс = (ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет);
	
	СвойстваСписка = ОбщегоНазначения.СтруктураСвойствДинамическогоСписка();
	ЗаполнитьЗначенияСвойств(СвойстваСписка, Список);
	Если ЭтоЯндекс Тогда
		СвойстваСписка.ОсновнаяТаблица = "";
		СвойстваСписка.ДинамическоеСчитываниеДанных = Истина;
	КонецЕсли;
	СвойстваСписка.ТекстЗапроса =
		РегистрыСведений.ЗаказыТорговыхПлощадок.ТекстЗапросаДинамическогоСписка(, , Не ЭтоЯндекс);
	ОбщегоНазначения.УстановитьСвойстваДинамическогоСписка(Элементы.Список, СвойстваСписка);
	
	Если ЭтоЯндекс Тогда
		ПоляКлюча = Новый Массив; // Массив из Строка
		ПоляКлюча.Добавить("Заказ");
		ПоляКлюча.Добавить("НомерЗаказа");
		ПоляКлюча.Добавить("Статус");
		Список.ПоляКлюча = Новый ФиксированныйМассив(ПоляКлюча);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ВидМаркетплейса", ВидМаркетплейса, , , Истина);
	
	УстановитьОтборПоУчетнойЗаписи(Список, УчетнаяЗапись);
	УстановитьОтборПоСтатусу(Список, ОтборСтатус);
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусыЗаказовВДоставке",
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовВДоставке());
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусыЗаказовРаботе",
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе(Истина,, Истина));
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "СтатусыЗаказовКонечные",
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные());
	
	ОтборыСписковКлиентСервер.ЗаполнитьСписокВыбораОтбораПоАктуальности(Элементы.ОтборСрокВыполнения.СписокВыбора);
	ИндексЭлемента = Элементы.ОтборСрокВыполнения.СписокВыбора.Количество() - 1;
	Элементы.ОтборСрокВыполнения.СписокВыбора.Вставить(ИндексЭлемента, "НаЭтойНеделе", НСтр("ru = 'На этой неделе';
																							|en = 'This week'"));
	ОтборыСписковКлиентСервер.ОтборПоАктуальностиПриСозданииНаСервере(
		Список,
		Актуальность,
		Элементы.ОтборСрокВыполнения.СписокВыбора,
		ДатаСобытия,
		СтруктураБыстрогоОтбора);
	
	Если ОтборыСписковКлиентСервер.НеобходимОтборПоСостояниюПриСозданииНаСервере(Состояние, СтруктураБыстрогоОтбора) Тогда
		УстановитьОтборПоСостояниюСервер();
	КонецЕсли;
	
	ЗаполнитьПараметрыВыбораОтбораПоУчетнойЗаписи(ВидМаркетплейса);
	ЗаполнитьСписокВыбораОтбораПоСостояниюСервер(Элементы.ОтборСостояние.СписокВыбора);
	
	ИспользоватьРеализациюПоНесколькимЗаказам           = ПолучитьФункциональнуюОпцию("ИспользоватьРеализациюПоНесколькимЗаказам");
	ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам = ПолучитьФункциональнуюОпцию("ИспользоватьАктыВыполненныхРаботПоНесколькимЗаказам");
	ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента        = ПолучитьФункциональнуюОпцию("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента");
	ИспользоватьОрдернуюСхемуПриОтгрузке                = ПолучитьФункциональнуюОпцию("ИспользоватьОрдернуюСхемуПриОтгрузке");
	
	УстановитьВидимостьЭлементов(); 
	Перечисления.СтатусыЗаказовТорговыхПлощадок.ЗаполнитьСписокВыбора(
		Элементы.ОтборСтатус.СписокВыбора, ВидМаркетплейса, Ложь);

	Если НЕ ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
		Элементы.УстановитьСтатусКВыполнению.Заголовок = НСтр("ru = 'В резерве';
																|en = 'In reserve'");
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ИспользуемыеТипыДокументов = Новый Массив;
	ИспользуемыеТипыДокументов.Добавить(Тип("ДокументСсылка.ЗаказКлиента"));
	
	ПараметрыРазмещения = ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ГруппаКомандыСпискаОсновные;
	ПараметрыРазмещения.Источники = Новый ОписаниеТипов(ИспользуемыеТипыДокументов);
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// ИнтеграцияС1СДокументооборотом
	ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ГруппаГлобальныеКоманды);
	// Конец ИнтеграцияС1СДокументооборотом
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыПриСозданииНаСервере = ОбменСКонтрагентами.ПараметрыПриСозданииНаСервере_ФормаСписка();
	ПараметрыПриСозданииНаСервере.Форма = ЭтотОбъект;
	ПараметрыПриСозданииНаСервере.МестоРазмещенияКоманд = Элементы.ПодменюЭДО; // ГруппаФормы
	ПараметрыПриСозданииНаСервере.КолонкаСостоянияЭДО = Элементы.ПредставлениеСостояния;
	ОбменСКонтрагентами.ПриСозданииНаСервере_ФормаСписка(ПараметрыПриСозданииНаСервере);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.СервисEDI
	ПараметрыИнтеграцииEDI = ДокументыEDIИнтеграция.ПараметрыФормыСпискаПрикладногоДокументаEDI();
	ПараметрыИнтеграцииEDI.ИмяРеквизитаСписок                                          = "Список";
	ПараметрыИнтеграцииEDI.ТипДокумента                                                = Тип("ДокументСсылка.ЗаказКлиента");
	ПараметрыИнтеграцииEDI.ИмяГруппыРазмещенияЭлементаБыстрогоОтбора                   = "ГруппаБыстрыеОтборы";
	ПараметрыИнтеграцииEDI.ИмяПсевдонимаОсновнойТаблицы                                = "ДокументЗаказКлиента";
	ПараметрыИнтеграцииEDI.СостояниеEDIРазмещатьПередЭлементом                         = "СписокУчетнаяЗапись";
	ПараметрыИнтеграцииEDI.ИмяЭлементаФормыСписок                                      = "Список";
	ПараметрыИнтеграцииEDI.ОтображатьПереходВСписокДокументыКЗагрузке                  = Истина;
	ПараметрыИнтеграцииEDI.ИмяГруппыРазмещенияЭлементаПереходВСписокДокументыКЗагрузке = "ГруппаСмотриТакжеПодвал";
	
	ДокументыEDIИнтеграция.ФормаСпискаПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка, ПараметрыИнтеграцииEDI);
	// Конец ЭлектронноеВзаимодействие.СервисEDI
	
	СобытияФорм.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	НавигационнаяСсылка = "";

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ИзменитьОтборСтрогоПоДатеСобытия();
	УстановитьДатуАктуальности();
	
	Элементы.ГиперссылкаЗаказыНаТорговойПлощадке.Картинка =
		ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ВидМаркетплейса);
	
	МенеджерОборудованияКлиент.НачатьПодключениеОборудованиеПриОткрытииФормы(Неопределено, ЭтотОбъект, "СканерШтрихкода");
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ОбменСКонтрагентамиКлиент.ПриОткрытии(ЭтотОбъект);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	
	// ЭлектронноеВзаимодействие.СервисEDI
	ПодключитьОбработчикОжидания("ОбновлениеИнформацииEDI", 300);
	// Конец ЭлектронноеВзаимодействие.СервисEDI
	
	ПодключитьОбработчикОжидания("ЗаполнитьСведенияПоЗаказамИОтправлениямПриОткрытии", 0.1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()

	МенеджерОборудованияКлиент.НачатьОтключениеОборудованиеПриЗакрытииФормы(Неопределено, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "МП_ОбновлениеСведенийПоЗаказамИОтправлениям"
			Или ИмяСобытия = "Запись_ЗаказКлиента"
			Или ИмяСобытия = "Запись_ПередачаТоваровХранителю"
			Или ИмяСобытия = "Запись_РеализацияТоваровУслуг" Тогда
		Элементы.Список.Обновить();
		
	ИначеЕсли (ИмяСобытия = "МП_ВыполненаЗагрузкаЗаказовТорговойПлощадки"
					Или ИмяСобытия = "МП_ВыполненаОтменаСборкиЗаказовНаТорговойПлощадке"
					Или ИмяСобытия = "МП_ВыполненоПодтверждениеСборкиЗаказовНаТорговойПлощадке"
					Или ИмяСобытия = "МП_ВыполненоОткрытиеСпораНаТорговойПлощадке"
					Или ИмяСобытия = "МП_ВыполненВозвратКОтгрузкеНаТорговойПлощадке")
				И Параметр = УчетнаяЗапись Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
	// ПодключаемоеОборудование
	Если Источник = "ПодключаемоеОборудование" И ВводДоступен() Тогда
		Если ИмяСобытия = "ScanData" И МенеджерОборудованияУТКлиент.ЕстьНеобработанноеСобытие() Тогда
			ПараметрыОповещения = Новый Структура("ИмяСобытия", ИмяСобытия);
			Оповещение = Новый ОписаниеОповещения("ОбработкаОповещенияЗавершение", ЭтотОбъект, ПараметрыОповещения);
			ОбработатьШтрихкоды(Оповещение, МенеджерОборудованияУТКлиент.ПреобразоватьДанныеСоСканераВСтруктуру(Параметр));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	// Конец ПодключаемоеОборудование
	
	// ЭлектронноеВзаимодействие.ОбменСКонтрагентами
	ПараметрыОповещенияЭДО = ОбменСКонтрагентамиКлиент.ПараметрыОповещенияЭДО_ФормаСписка();
	ПараметрыОповещенияЭДО.Форма = ЭтотОбъект;
	ПараметрыОповещенияЭДО.ИмяДинамическогоСписка = "Список";
	ПараметрыОповещенияЭДО.ЕстьОбработчикОбновленияВидимостиСостоянияЭДО = Истина;
	ОбменСКонтрагентамиКлиент.ОбработкаОповещения_ФормаСписка(ИмяСобытия, Параметр, Источник, ПараметрыОповещенияЭДО);
	// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

	// ЭлектронноеВзаимодействие.СервисEDI
	ДокументыEDIИнтеграцияКлиент.ОбработкаОповещенияФормаСписка(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ЭлектронноеВзаимодействие.СервисEDI
	
	ОбработкаОповещенияФрагмент(ИмяСобытия);
	
	Если ИмяСобытия = "Запись_ЗаказКлиента"
			Или ИмяСобытия = "ЗакрытиеЗаказов" Тогда
		// СтандартныеПодсистемы.ПодключаемыеКоманды
		ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
		
	ИначеЕсли ИмяСобытия = "Запись_НаборКонстант"
				И (Источник = "ИспользоватьЗаказыКлиентов"
					Или Источник = "ИспользоватьРасширенныеВозможностиЗаказаКлиента") Тогда
		УстановитьВидимостьЭлементов();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)

	ОтборыСписковКлиентСервер.ОтборПоАктуальностиПриЗагрузкеИзНастроек(
		Список,
		Актуальность,
		Элементы.ОтборСрокВыполнения.СписокВыбора,
		ДатаСобытия,
		СтруктураБыстрогоОтбора,
		Настройки);
		
	ИспользоватьОтбор = (ОтборТипПокупателя <> Перечисления.ЮрФизЛицо.ПустаяСсылка());
	ЭтоЮридическоеЛицо = (ОтборТипПокупателя = Перечисления.ЮрФизЛицо.ЮрЛицо);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ЗаказЮридическогоЛица", ЭтоЮридическоеЛицо, , , ИспользоватьОтбор);
	
	Если ЗначениеЗаполнено(ОтборСтатус) Тогда
		УстановитьОтборПоСтатусу(Список, ОтборСтатус);
	КонецЕсли;
	
	Если ОтборыСписковКлиентСервер.НеобходимОтборПоСостояниюПриЗагрузкеИзНастроек(Состояние, СтруктураБыстрогоОтбора, Настройки) Тогда
		УстановитьОтборПоСостояниюСервер();
	КонецЕсли;
	
	// ЭлектронноеВзаимодействие.СервисEDI
	ДокументыEDIИнтеграция.ПриЗагрузкеДанныхИзНастроекНаСервере(ЭтотОбъект, Настройки);
	// Конец ЭлектронноеВзаимодействие.СервисEDI

КонецПроцедуры

&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)

	// ЭлектронноеВзаимодействие.СервисEDI
	ДокументыEDIИнтеграция.ПриСохраненииДанныхВНастройкахНаСервере(ЭтотОбъект, Настройки);
	// Конец ЭлектронноеВзаимодействие.СервисEDI

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборУчетнаяЗаписьПриИзменении(Элемент)

	ДанныеТорговойПлощадки = ДанныеПоТорговойПлощадке(УчетнаяЗапись);
	УстановитьОтборПоУчетнойЗаписи(Список, УчетнаяЗапись);
	ЗаполнитьСведенияПоЗаказамИОтправлениям(Истина);

КонецПроцедуры

&НаКлиенте
Процедура ОтборСтатусПриИзменении(Элемент)

	УстановитьОтборПоСтатусу(Список, ОтборСтатус);

КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеПриИзменении(Элемент)

	УстановитьОтборПоСостояниюСервер();

КонецПроцедуры

&НаКлиенте
Процедура ОтборСрокВыполненияПриИзменении(Элемент)

	ОтборыСписковКлиентСервер.ПриИзмененииОтбораПоАктуальности(
		Список,
		Актуальность,
		ДатаСобытия,
		Элементы.ОтборСрокВыполнения.СписокВыбора);
	ИзменитьОтборСтрогоПоДатеСобытия();
	УстановитьДатуАктуальности();

КонецПроцедуры

&НаКлиенте
Процедура ОтборСрокВыполненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	ОбщегоНазначенияУТКлиент.ПриВыбореОтбораПоАктуальности(
		ВыбранноеЗначение,
		СтандартнаяОбработка,
		ЭтотОбъект,
		Список,
		"Актуальность",
		"ОтборСрокВыполнения",
		"ДатаСобытия");

КонецПроцедуры

&НаКлиенте
Процедура ОтборСрокВыполненияОчистка(Элемент, СтандартнаяОбработка)

	ОтборыСписковКлиентСервер.ПриОчисткеОтбораПоАктуальности(Список, Актуальность, ДатаСобытия, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ОтборСостояниеОчистка(Элемент, СтандартнаяОбработка)

	Если Состояние = Неопределено Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтборСкладТорговойПлощадкиПриИзменении(Элемент)

	УстановитьОтборПоСкладу();

КонецПроцедуры

&НаКлиенте
Процедура ОтборТипПокупателяПриИзменении(Элемент)
	ОтборТипПокупателяПриИзмененииНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаказыНаOzonНажатие(Элемент)

	ОчиститьСообщения();
	
	ИмяОткрываемойФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов";
	
	ЗаголовокФормы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заказы, отгружаемые со складов продавца <%1>';
			|en = 'Orders to ship from the <%1> seller warehouses'"), ДанныеТорговойПлощадки.ПостфиксЗаголовкаФормы);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УчетнаяЗапись",               УчетнаяЗапись);
	ПараметрыФормы.Вставить("ЗаголовокФормы",              ЗаголовокФормы);
	ПараметрыФормы.Вставить("ЭтоFBO",                      Ложь);
	ПараметрыФормы.Вставить("КлючНазначенияИспользования", "FBS" + ДанныеТорговойПлощадки.ИдентификаторКлиента);
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы,, "FBS");

КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаЖурналПродажиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки,
			СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	
	ИмяОткрываемойФормы = "Обработка.ЖурналДокументовПродажи.Форма.СписокДокументов";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("УникальныйИдентификаторФормы", Ложь);
	
	ДополнительныеСвойства = Новый Структура;
	ДополнительныеСвойства.Вставить("ДанныеТорговойПлощадки", ДанныеТорговойПлощадки);
	ДополнительныеСвойства.Вставить("КлючНазначенияФормы",    "РаботаСПродажамиМП");
	
	ИнтеграцияСМаркетплейсамиКлиент.ДополнитьПараметрыФормы(
		ПараметрыФормы,
		ДополнительныеСвойства,
		ИмяОткрываемойФормы);
	
	ОткрытьФорму(ИмяОткрываемойФормы, ПараметрыФормы,, ПараметрыФормы.УникальныйИдентификаторФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриИзменении(Элемент)

	ОбеспечениеВДокументахКлиент.СписокПриИзменении(ЭтотОбъект, "Документ.ЗаказКлиента");

КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ОчиститьСообщения();
	
	СтандартнаяОбработка = Ложь;
	ПоказатьСодержимоеТекущейЯчейки(Поле);

КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)

	Отказ = Истина;
	ПоказатьСодержимоеТекущейЯчейки(Элемент.ТекущийЭлемент);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)

	// ЭлектронноеВзаимодействие.СервисEDI
	ДокументыEDIИнтеграция.ПриПолученииДанныхНаСервереСписокДокументов(
		Перечисления.ТипыДокументовEDI.ЗаказКлиента,
		ИмяЭлемента,
		Настройки,
		Строки);
	// Конец ЭлектронноеВзаимодействие.СервисEDI

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьСписок(Команда)

	УстановитьДатуАктуальности();
	ЗаполнитьСведенияПоЗаказамИОтправлениям();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКВыполнению(Команда)

	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента Тогда
		ЗаголовокСтатуса = НСтр("ru = 'К выполнению';
								|en = 'Confirmed'");
	Иначе
		ЗаголовокСтатуса = НСтр("ru = 'В резерве';
								|en = 'In reserve'");
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'У выделенных в списке заказов будет установлен статус ""%ИмяСтатуса%"". Продолжить?';
						|en = 'Status ""%ИмяСтатуса%"" will be set for the orders selected in the list. Continue?'");
	ТекстВопроса = СтрЗаменить(ТекстВопроса, "%ИмяСтатуса%", ЗаголовокСтатуса);
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ВыделенныеСтроки",ВыделенныеСтроки);
	ДопПараметры.Вставить("СтатусДокументов", "КОбеспечению");
	ДопПараметры.Вставить("ЗаголовокСтатуса", ЗаголовокСтатуса);
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСтатусДокументовЗавершение", ЭтотОбъект, ДопПараметры);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусКОтгрузке(Команда)

	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'У выделенных в списке заказов будет установлен статус ""К отгрузке"". Продолжить?';
						|en = 'Status ""To ship"" will be set for the orders selected in the list. Continue?'");
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("ВыделенныеСтроки",ВыделенныеСтроки);
	ДопПараметры.Вставить("СтатусДокументов", "КОтгрузке");
	ДопПараметры.Вставить("ЗаголовокСтатуса", НСтр("ru = 'К отгрузке';
													|en = 'To ship'"));
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСтатусДокументовЗавершение", ЭтотОбъект, ДопПараметры);
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусЗакрыт(Команда)

	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеЗаказы = ВыделенныеЗаказы(ВыделенныеСтроки);
	
	СтруктураЗакрытия = Новый Структура;
	СписокЗаказов = Новый СписокЗначений;
	СписокЗаказов.ЗагрузитьЗначения(ВыделенныеЗаказы);
	СтруктураЗакрытия.Вставить("Заказы",                       СписокЗаказов);
	СтруктураЗакрытия.Вставить("ОтменитьНеотработанныеСтроки", Истина);
	СтруктураЗакрытия.Вставить("ЗакрыватьЗаказы",              Истина);
	
	ОткрытьФорму("Обработка.ПомощникЗакрытияЗаказов.Форма.ФормаЗакрытия", СтруктураЗакрытия,
		ЭтотОбъект,,,, Неопределено, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьПометкуУдаленияДокумента(Команда)

	ТипСписка = НСтр("ru = 'Заказы клиентов';
					|en = 'Sales orders'");
	
	ВыделенныеСтроки = ОбщегоНазначенияУТКлиент.ПроверитьПолучитьВыделенныеВСпискеСсылки(Элементы.Список);
	
	Если Не ЗначениеЗаполнено(ВыделенныеСтроки) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ОбщегоНазначенияУТВызовСервера.СсылкиОбъектовПомеченныхНаУдаление(ВыделенныеСтроки, "Заказ");
	
	МассивСсылок = Результат.МассивСсылок;
	ЕстьСтрокиПомеченныеНаУдаление = Результат.ЕстьСтрокиПомеченныеНаУдаление;
	
	ТекстВопроса = ?(Результат.ЕстьСтрокиПомеченныеНаУдаление,
		НСтр("ru = 'Снять с выделенных элементов пометку на удаление?';
			|en = 'Unmark selected items for deletion?'"),
		НСтр("ru = 'Пометить выделенные элементы на удаление?';
			|en = 'Mark the selected items for deletion?'"));
	
	ПараметрыОбновленияСписка = Новый Структура;
	ПараметрыОбновленияСписка.Вставить("Форма", ЭтотОбъект);
	ПараметрыОбновленияСписка.Вставить("ИмяСписка", "Список");
	ПараметрыОбновленияСписка.Вставить("ОбновитьСписок", Истина);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Список", Элементы.Список);
	ДополнительныеПараметры.Вставить("ТипСписка", ТипСписка);
	ДополнительныеПараметры.Вставить("ВыделенныеСтроки", МассивСсылок);
	ДополнительныеПараметры.Вставить("УстановкаПометкиУдаления", Не ЕстьСтрокиПомеченныеНаУдаление);
	ДополнительныеПараметры.Вставить("ОповещениеОЗавершении", Неопределено);
	ДополнительныеПараметры.Вставить("ПараметрыОбновленияСписка", ПараметрыОбновленияСписка);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьПометкуУдаленияЗавершение",
		ОбщегоНазначенияУТКлиент,
		ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);

КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)

	ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(
		Команда,
		ЭтотОбъект,
		Элементы.Список);

КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)

	СобытияФормКлиент.ВыполнитьПереопределяемуюКоманду(ЭтотОбъект, Команда);

КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ОчиститьСообщения();
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);

КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);

КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)

	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьВидимостьСостоянияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОбновленияВидимостьСостоянияЭДО(ЭтотОбъект, Элементы.ПредставлениеСостояния);

КонецПроцедуры

// Конец ЭлектронноеВзаимодействие.ОбменСКонтрагентами

&НаКлиенте
Процедура ПолучитьНастройкуКомплектаДокументов(Команда)

	КлючОбъекта = "Обработка.ЖурналДокументовПродажи.Форма.КОформлениюНакладных/ТекущиеДанные";
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьНастройкуКомплектаДокументовЗавершение", ЭтотОбъект, КлючОбъекта);
	
	ПараметрыФормы = ПродажиВызовСервера.ПолучитьНастройкуКомплектаДокументов(КлючОбъекта);
	ОткрытьФорму("Обработка.ЖурналДокументовПродажи.Форма.ПараметрыОформленияДокументовПродажи",
		ПараметрыФормы,
		ЭтотОбъект,
		,,,
		Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	// Условное оформление динамического списка "Список"
	СписокУсловноеОформление = Список.КомпоновщикНастроек.Настройки.УсловноеОформление;
	СписокУсловноеОформление.Элементы.Очистить();
	
	// Документ имеет высокий приоритет
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Документ имеет высокий приоритет';
											|en = 'High priority document'");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Приоритет");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.Приоритеты.ПолучитьВысшийПриоритет();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.ВысокийПриоритетДокумента);
	
	// Документ имеет низкий приоритет
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Документ имеет низкий приоритет';
											|en = 'Low priority document'");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Приоритет");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Справочники.Приоритеты.ПолучитьНизшийПриоритет();
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.НизкийПриоритетДокумента);
	
	СписокКонечныхСтатусов = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные();
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента") Тогда
		// Выделение цветом состояния "Закрыт"
		ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Представление = НСтр("ru = 'Выделение цветом состояния ""Закрыт""';
												|en = 'Highlighting the ""Closed"" state'");
		
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Состояние");
		ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборЭлемента.ПравоеЗначение = Перечисления.СостоянияЗаказовКлиентов.Закрыт;
		
		ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Статус");
		ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
		ОтборЭлемента.ПравоеЗначение = СписокКонечныхСтатусов;
		
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЗакрытыйДокумент);
	КонецЕсли;
	
	// Представление даты сборки в прошлом.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Представление даты отгрузки - в прошлом';
											|en = 'Shipment date presentation: in the past'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДЛФ=D");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
	Маркетплейс = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет;
	ПредставлениеМаркетплейса = ИнтеграцияСМаркетплейсамиКлиентСервер.ПредставлениеВидаТорговойПлощадки(Маркетплейс);
	
	// Представление даты отгрузки для Яндекс Маркет.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '(%1) Представление даты отгрузки';
			|en = '(%1) Shipment date presentation'"),
		ПредставлениеМаркетплейса);
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидМаркетплейса");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Маркетплейс;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДЛФ=D");
	
	Маркетплейс = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon;
	ПредставлениеМаркетплейса = ИнтеграцияСМаркетплейсамиКлиентСервер.ПредставлениеВидаТорговойПлощадки(Маркетплейс);
	
	// Представление даты отгрузки сегодня.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '(%1) Представление даты отгрузки - сегодня';
			|en = '(%1) Shipment date presentation: today'"),
		ПредставлениеМаркетплейса);
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидМаркетплейса");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Маркетплейс;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоЭтогоДня);
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Меньше;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоСледующегоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", НСтр("ru = 'ДФ=ЧЧ:мм';
																			|en = 'DF=HH:mm tt'"));
	
	// Представление даты отгрузки в будущем.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '(%1) Представление даты отгрузки - в будущем';
			|en = '(%1) Shipment date presentation: in the future'"),
		ПредставлениеМаркетплейса);
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ВидМаркетплейса");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Маркетплейс;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = Новый СтандартнаяДатаНачала(ВариантСтандартнойДатыНачала.НачалоСледующегоДня);
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДФ='dd.MM.yyyy ЧЧ:мм'");
	
	// Выделение цветом просроченной отгрузки
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Выделение цветом просроченной отгрузки отправления';
											|en = 'Highlight an overdue shipment date'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("НомерОтправления");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Статус");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокОтгрузки");
	
	СписокСтатусовВРаботе =
		Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе(Истина,, Истина);
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.ВСписке;
	ОтборЭлемента.ПравоеЗначение = СписокСтатусовВРаботе;
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Просрочен");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныйДокумент);
	
	// Выделение цветом просроченной доставки
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Выделение цветом просроченной доставки отправления';
											|en = 'Highlight an overdue shipment delivery'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("НомерОтправления");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("Статус");
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокДоставки");
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Просрочен");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ОтборЭлемента = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Статус");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеВСписке;
	ОтборЭлемента.ПравоеЗначение = СписокКонечныхСтатусов;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПросроченныйДокумент);
	
	// Представление даты сборки в прошлом.
	ЭлементОформления = СписокУсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Представление = НСтр("ru = 'Представление даты доставки';
											|en = 'Delivery date presentation'");
	
	ЭлементОформления.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных("СрокДоставки");
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Формат", "ДЛФ=D");
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ПоясняющийТекст);
	
	// Представление даты заказа.
	СтандартныеПодсистемыСервер.УстановитьУсловноеОформлениеПоляДата(
		ЭтотОбъект,
		"Список.ДатаЗаказа",
		Элементы.СписокДатаЗаказа.Имя);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеПоТорговойПлощадке(УчетнаяЗапись)

	// Заполнить данные из настроек интеграции.
	ДанныеТорговойПлощадки = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись);
	
	Возврат ДанныеТорговойПлощадки;

КонецФункции

#Область ШтрихкодыИТорговоеОборудование

&НаКлиенте
Функция СсылкаНаЭлементСпискаПоШтрихкоду(Штрихкод)

	Менеджеры = Новый Массив();
	Менеджеры.Добавить(ПредопределенноеЗначение("Документ.ЗаказКлиента.ПустаяСсылка"));
	Возврат ШтрихкодированиеПечатныхФормКлиент.ПолучитьСсылкуПоШтрихкодуТабличногоДокумента(Штрихкод, Менеджеры);

КонецФункции

&НаКлиенте
Процедура ОбработатьШтрихкоды(Знач Оповещение, Данные)

	Если Не ШтрихкодированиеНоменклатурыКлиент.ШтрихкодыВалидны(Данные) Тогда
		Возврат;
	КонецЕсли;
	
	МассивСсылок = СсылкаНаЭлементСпискаПоШтрихкоду(Данные.Штрихкод);
	Если МассивСсылок.Количество() > 0 Тогда
		Ссылка = МассивСсылок[0];
		Элементы.Список.ТекущаяСтрока = Ссылка;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьШтрихкодыЗавершение",
			ЭтотОбъект,
			Новый Структура("Данные, Оповещение", Данные, Оповещение));
		ПоказатьЗначение(ОписаниеОповещения, Ссылка);
		Возврат;
	Иначе
		ШтрихкодированиеПечатныхФормКлиент.ОбъектНеНайден(Данные.Штрихкод);
	КонецЕсли;
	
	ОбработатьШтрихкодыФрагмент(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкодыЗавершение(ДополнительныеПараметры) Экспорт

	Оповещение = ДополнительныеПараметры.Оповещение;
	
	ОбработатьШтрихкодыФрагмент(Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьШтрихкодыФрагмент(Знач Оповещение)

	ВыполнитьОбработкуОповещения(Оповещение);

КонецПроцедуры

&НаСервере
Процедура ОтборТипПокупателяПриИзмененииНаСервере()  
	
	ЭтоФизическоеЛицо = (ОтборТипПокупателя = Перечисления.ЮрФизЛицо.ФизЛицо);
	
	Если ЭтоФизическоеЛицо Тогда
		Если ОтборСтатус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетОплату Тогда
			ОтборСтатус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
	Перечисления.СтатусыЗаказовТорговыхПлощадок.ЗаполнитьСписокВыбора(
		Элементы.ОтборСтатус.СписокВыбора, ВидМаркетплейса, , ЭтоФизическоеЛицо);
	
	ИспользоватьОтбор = (ОтборТипПокупателя <> Перечисления.ЮрФизЛицо.ПустаяСсылка());
	ЭтоЮридическоеЛицо = (ОтборТипПокупателя = Перечисления.ЮрФизЛицо.ЮрЛицо);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список, "ЗаказЮридическогоЛица", ЭтоЮридическоеЛицо, , , ИспользоватьОтбор);

КонецПроцедуры

#КонецОбласти

#Область Прочее

&НаКлиенте
Процедура ПоказатьСодержимоеТекущейЯчейки(Поле)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументОтгрузки)
			И (Поле.Имя = "СписокНомерОтправления"
				Или Поле.Имя = "СписокСтатус"
				Или Поле.Имя = "СписокДокументОтгрузки"
				Или Поле.Имя = "СуммаДокументаОтгрузки") Тогда
		ПоказатьЗначение(, ТекущиеДанные.ДокументОтгрузки);
		
	ИначеЕсли Поле.Имя = "СписокПроцентОтгрузки" Или Поле.Имя = "СписокСостояние" Тогда
		СписокДокументов = Новый СписокЗначений;
		СписокДокументов.Добавить(ТекущиеДанные.Ссылка);
		
		Если ТекущиеДанные.ЭтоЗаказКакСчет Тогда
			Возврат;
		КонецЕсли;
		
		ОткрытьФорму("Отчет.СостояниеВыполненияДокументов.Форма.ФормаОтчета",
			Новый Структура("ВходящиеДокументы", СписокДокументов),
			ЭтотОбъект,
			Ложь);
		
	ИначеЕсли Поле.Имя = "СписокКартинкаДоставки" Или Поле.Имя = "СписокИдентификаторОтгрузки" Тогда
		Если Не ЗначениеЗаполнено(ТекущиеДанные.ИдентификаторОтгрузки) Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нет данных об отгрузке.';
															|en = 'There is no shipment information.'"));
			Возврат;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
		ПараметрыФормы.Вставить("ИдентификаторОтгрузки", ТекущиеДанные.ИдентификаторОтгрузки);
		ПараметрыФормы.Вставить("ИдентификаторСклада",   ТекущиеДанные.ИдентификаторСкладаТорговойПлощадки);
		ПараметрыФормы.Вставить("СписокСкладов",         Элементы.ОтборСкладТорговойПлощадки.СписокВыбора);
		ПараметрыФормы.Вставить("СписокМетодовДоставки", СписокМетодовДоставки);
		
		ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки", ПараметрыФормы, ЭтотОбъект);
		
	ИначеЕсли Поле.Имя = "ДанныеОтправлений" Тогда
		ИнтеграцияСМаркетплейсамиКлиент.ОткрытьФормуЗаполненияДанныхЭкземпляров(ЭтотОбъект, УчетнаяЗапись);
		
	Иначе
		ПоказатьЗначение(, ТекущиеДанные.Ссылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоУчетнойЗаписи(Список, УчетнаяЗапись)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"УчетнаяЗапись",
		УчетнаяЗапись,
		,
		,
		ЗначениеЗаполнено(УчетнаяЗапись));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоСтатусу(Список, ОтборСтатус)

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Статус",
		ОтборСтатус,
		,
		,
		ЗначениеЗаполнено(ОтборСтатус));

КонецПроцедуры

&НаКлиенте
Процедура УстановитьДатуАктуальности()

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "ДатаАктуальности",
		ОбщегоНазначенияКлиент.ДатаСеанса());
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(Список, "Открыть",
		НСтр("ru = 'Открыть';
			|en = 'Open'"));
	ПодключитьОбработчикОжидания("УстановитьДатуАктуальности", 10*60, Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияРаспределенияЗапасовПоЗаказам()

	ОбеспечениеВДокументахКлиент.ПроверитьВыполнениеЗаданияРаспределенияЗапасовПоЗаказам(ЭтотОбъект, Элементы.Список);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыВыбораОтбораПоУчетнойЗаписи(ВидМаркетплейса)
	
	ПараметрыВыбора = Новый Массив; // Массив из ПараметрВыбора
	
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ВидМаркетплейса", ВидМаркетплейса));
	ПараметрыВыбора.Добавить(Новый ПараметрВыбора("Отбор.ПометкаУдаления", Ложь));
	
	Элементы.ОтборУчетнаяЗапись.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбора);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораОтбораПоСостояниюСервер(СписокВыбора)

	СписокВыбора.Добавить("ВсеОткрытые", НСтр("ru = 'Все открытые';
												|en = 'Open orders'"));
	СписокВыбора.Добавить("ВсеОжидающиеИсполнения", НСтр("ru = 'Все ожидающие исполнения';
														|en = 'Unfulfilled orders'"));
	
	СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКОбеспечению);
	СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяОбеспечение);
	СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКОтгрузке);
	СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.ВПроцессеОтгрузки);
	Если ПолучитьФункциональнуюОпцию("НеЗакрыватьЗаказыКлиентовБезПолнойОплаты") 
		ИЛИ ПолучитьФункциональнуюОпцию("НеЗакрыватьЗаказыКлиентовБезПолнойОтгрузки") Тогда
		СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКЗакрытию);
	КонецЕсли;
	СписокВыбора.Добавить(Перечисления.СостоянияЗаказовКлиентов.Закрыт);

КонецПроцедуры

&НаСервере
Процедура УстановитьОтборПоСостояниюСервер()

	Если Состояние = "ВсеОткрытые" Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Состояние",
			Перечисления.СостоянияЗаказовКлиентов.Закрыт, ВидСравненияКомпоновкиДанных.НеРавно,, Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления",
			Ложь,,, Истина);
		
	ИначеЕсли Состояние = "ВсеОжидающиеОплаты" Тогда
		
		МассивСостояний = Новый Массив();
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяАвансДоОбеспечения);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяПредоплатаДоОтгрузки);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяОплатаПослеОтгрузки);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Состояние",
			МассивСостояний, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления",
			Ложь,,, Истина);
		
	ИначеЕсли Состояние = "ВсеОжидающиеИсполнения" Тогда
		
		МассивСостояний = Новый Массив();
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяСогласование);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКОбеспечению);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ОжидаетсяОбеспечение);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКОтгрузке);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ВПроцессеОтгрузки);
		МассивСостояний.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКЗакрытию);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Состояние",
			МассивСостояний, ВидСравненияКомпоновкиДанных.ВСписке,, Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления",
			Ложь,,, Истина);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Состояние",
			Состояние, ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Состояние));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ПометкаУдаления",
			,,, Ложь);
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементов()

	ЕстьДоступНаИзменение = ПравоДоступа("Изменение", Метаданные.Документы.ЗаказКлиента);
	ИспользоватьЗаказыКлиентов                      = ПолучитьФункциональнуюОпцию("ИспользоватьЗаказыКлиентов");
	ИспользоватьРасширенныеВозможностиЗаказаКлиента = ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента");
	ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента    = ПолучитьФункциональнуюОпцию("ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента");
	
	Элементы.УстановитьСтатусКОтгрузке.Видимость = ИспользоватьРасширенныеВозможностиЗаказаКлиента
													И Не ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента;
	
	Элементы.СписокСуммаОтгрузки.Видимость           = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	Элементы.СписокПроцентОтгрузки.Видимость         = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	Элементы.СписокСостояние.Видимость               = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	Элементы.ЕстьРасхожденияОрдерНакладная.Видимость = ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	Элементы.УстановитьСтатусЗакрыт.Видимость        = ПравоДоступа("Изменение",Метаданные.Документы.ЗаказКлиента);
	
	КомандыОбеспеченияВидимы = ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента И ЕстьДоступНаИзменение;
	Элементы.СписокОтгрузитьЗаказ.Видимость                      = КомандыОбеспеченияВидимы;
	Элементы.СписокРезервироватьЗаказ.Видимость                  = КомандыОбеспеченияВидимы;
	Элементы.СписокКОбеспечениюЗаказ.Видимость                   = КомандыОбеспеченияВидимы;
	Элементы.СписокРезервироватьПоМереПоступленияЗаказ.Видимость = КомандыОбеспеченияВидимы;
	Элементы.СписокНеОбеспечиватьЗаказ.Видимость                 = КомандыОбеспеченияВидимы;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Элементы,
		"СписокСостояниеОбеспеченияСпискаЗаказов",
		"Видимость",
		ИспользоватьПострочнуюОтгрузкуВЗаказеКлиента);
	
	ДоступностьКоманд = ИспользоватьЗаказыКлиентов И ИспользоватьРасширенныеВозможностиЗаказаКлиента;
	
	Элементы.ЗагрузитьНовыеЗаказы.Доступность                  = ДоступностьКоманд;
	Элементы.ГруппаКомандыОбработкиДокументов.Доступность      = ДоступностьКоманд;
	Элементы.ГруппаКомандыИзмененияСтатусов.Доступность        = ДоступностьКоманд;
	Элементы.СписокПечатьЭтикеток.Доступность                  = ДоступностьКоманд;
	
	Элементы.КонтекстноеМенюЗагрузитьЗаказы.Доступность                       = ДоступностьКоманд;
	Элементы.КонтекстноеМенюГруппаКомандыОбработкиДокументов.Доступность      = ДоступностьКоманд;
	Элементы.КонтекстноеМенюГруппаКомандыИзмененияСтатусов.Доступность        = ДоступностьКоманд;
	Элементы.КонтекстноеМенюПечатьЭтикеток.Доступность                        = ДоступностьКоманд;  
			
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Элементы.ОтменитьСборкуЗаказов.Заголовок 				 = НСтр("ru = 'Отменить сборку';
																		|en = 'Cancel picking'");
		Элементы.КонтекстноеМенюОтменитьСборкуЗаказов.Заголовок  = НСтр("ru = 'Отменить сборку';
																		|en = 'Cancel picking'");
		Элементы.ГруппаКомандыПередачиВДоставку.Видимость 		 = Ложь;
		Элементы.ГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость = Ложь; 
		Элементы.КонтекстноеМенюГруппаКомандыРаботыСоСпорнымиЗаказами.Видимость  = Ложь;
		Элементы.СписокГруппаЗагрузитьИзСкана.Видимость			 = Ложь;   
		Элементы.ОтборСкладТорговойПлощадки.Видимость 			 = Ложь;
		Элементы.ОтборТипПокупателя.Видимость					 = Истина; 
		Элементы.ОтборУчетнаяЗапись.Заголовок                    = НСтр("ru = 'Магазин';
																		|en = 'Store'");
		Элементы.ГруппаСвернутыеДанныеОтправлений.Видимость      = Истина;
		Элементы.ГруппаДокументОтгрузки.Видимость           	 = Ложь;     
		Элементы.СписокИдентификаторОтгрузки.Видимость           = Ложь;
		Элементы.СписокДокументОтгрузки.Видимость           	 = Ложь;  
		Элементы.СписокСуммаДокументаОтгрузки.Видимость          = Ложь; 
		Элементы.СписокСкладТорговойПлощадки.Видимость           = Ложь; 
		Элементы.СписокДополнительнаяИнформация.Видимость        = Ложь;
		Элементы.ДополнительныеСведения.Видимость				 = Ложь;	
		Элементы.СписокКартинкаДоставки.Видимость       = Ложь;
		Заголовок = НСтр("ru = 'Заказы, загруженные с торговой площадки';
						|en = 'Orders imported from the trading platform'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияПоЗаказамИОтправлениямПриОткрытии()
	ЗаполнитьСведенияПоЗаказамИОтправлениям(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияПоЗаказамИОтправлениям(ЗаполнитьСписокСкладов = Ложь)

	ОчиститьСообщения();
	
	Элементы.КартинкаДлительноеОжиданиеРасширеннаяПодсказка.Ширина = 0;
	Элементы.СтраницыСостояния.ТекущаяСтраница = Элементы.ГруппаЗаполнениеВыполняется;
	
	Если ЗаполнитьСписокСкладов Тогда
		Элементы.ОтборСкладТорговойПлощадки.СписокВыбора.Очистить();
		Элементы.ОтборСкладТорговойПлощадки.СписокВыбора.Добавить("", НСтр("ru = '<Заполнение списка...>';
																			|en = '<Populate the list...>'"));
		
		СписокМетодовДоставки = Новый Структура;
		
		Если Не ПустаяСтрока(ОтборСкладТорговойПлощадки) Тогда
			ОтборСкладТорговойПлощадки = "";
			УстановитьОтборПоСкладу();
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.Заголовок = НСтр("ru = 'Заполнение сведений по заказам и отправлениям';
										|en = 'Fill order and shipment information'");
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ПараметрыЗавершения = Новый Структура("ЗаполнитьСписокСкладов", ЗаполнитьСписокСкладов);
	ОповещениеОЗавершении =
		Новый ОписаниеОповещения("ЗаполнениеСведенийПоЗаказамИОтправлениямЗавершение", ЭтотОбъект, ПараметрыЗавершения);
	
	ДлительнаяОперация = ЗапуститьЗаполнениеСведенийПоЗаказамИОтправлениям(ВидМаркетплейса,
																			УчетнаяЗапись,
																			УникальныйИдентификатор,
																			ЗаполнитьСписокСкладов);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапуститьЗаполнениеСведенийПоЗаказамИОтправлениям(ВидМаркетплейса, УчетнаяЗапись, УникальныйИдентификатор,
		ЗаполнитьСписокСкладов)

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания =
		НСтр("ru = 'Интеграция с маркетплейсами. Заполнение сведений по заказам и отправлениям.';
			|en = 'Integration with marketplaces. Fill information on orders and shipments.'");
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОбновитьСведенияПоЗаказамИОтправлениям";
	Возврат ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, ИмяМетода, ВидМаркетплейса, УчетнаяЗапись, ЗаполнитьСписокСкладов);

КонецФункции

&НаКлиенте
Процедура ЗаполнениеСведенийПоЗаказамИОтправлениямЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт

	Если ДополнительныеПараметры.ЗаполнитьСписокСкладов Тогда
		Элементы.ОтборСкладТорговойПлощадки.СписокВыбора.Очистить();
	КонецЕсли;
	
	Элементы.КартинкаДлительноеОжиданиеРасширеннаяПодсказка.Ширина = 1;
	Элементы.СтраницыСостояния.ТекущаяСтраница = Элементы.ГруппаЗаполнениеЗавершено;
	
	Если РезультатЗадания <> Неопределено
			И РезультатЗадания.Статус = "Выполнено" Тогда
		Если ДополнительныеПараметры.ЗаполнитьСписокСкладов Тогда
			Ошибка = ЗаполнитьСписокСкладовИМетодовДоставки(РезультатЗадания.АдресРезультата);
			
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(
				Ошибка,
				,
				Истина,
				Элементы.ГиперссылкаЗаказыНаТорговойПлощадке.Картинка);
		КонецЕсли;
		
	Иначе
		ШаблонОшибки = НСтр("ru = 'Не удалось заполнить сведения по заказам и отправлениям по причине: %1';
							|en = 'Cannot fill in information on orders and shipments. Reason: %1'");
		ПредставлениеНеизвестнойОшибки = НСтр("ru = 'Неизвестная ошибка выполнения операции';
												|en = 'Unknown operation error'");
		ПодробноеПредставлениеОшибки = ?(РезультатЗадания = Неопределено,
			ПредставлениеНеизвестнойОшибки,
			РезультатЗадания.ПодробноеПредставлениеОшибки);
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки,
				ПодробноеПредставлениеОшибки);
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	УстановитьОтборПоСкладу();
	
	Оповестить("МП_ОбновлениеСведенийПоЗаказамИОтправлениям",,);

КонецПроцедуры

&НаСервере
Функция ЗаполнитьСписокСкладовИМетодовДоставки(АдресРезультата)

	Результат = ПолучитьИзВременногоХранилища(АдресРезультата);
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Если Результат = Неопределено Тогда
		Возврат ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КонецЕсли;
	
	ИнтеграцияСМаркетплейсамиСервер.ЗаполнитьСписокСкладовИМетодовДоставки(
		ВидМаркетплейса,
		Результат.ТаблицаСкладов,
		Элементы.ОтборСкладТорговойПлощадки.СписокВыбора,
		СписокМетодовДоставки);
	
	Возврат Результат.Ошибка;

КонецФункции

&НаКлиенте
Процедура УстановитьОтборПоСкладу()

	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"ИдентификаторСкладаТорговойПлощадки",
		ОтборСкладТорговойПлощадки,
		,
		,
		ЗначениеЗаполнено(ОтборСкладТорговойПлощадки));

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияЗавершение(Результат, ДополнительныеПараметры) Экспорт

	ИмяСобытия = ДополнительныеПараметры.ИмяСобытия;
	ОбработкаОповещенияФрагмент(ИмяСобытия);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияФрагмент(Знач ИмяСобытия)

	Если ИмяСобытия = "ЗачтенаОплата"
			ИЛИ ПродажиКлиент.ИзменилисьДокументыОплатыКлиентам(ИмяСобытия)
			ИЛИ ИмяСобытия = "ЗакрытиеЗаказов" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтатусДокументовЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт

	ВыделенныеСтроки = ДополнительныеПараметры.ВыделенныеСтроки;
	СтатусДокументов = ДополнительныеПараметры.СтатусДокументов;
	ЗаголовокСтатуса = ДополнительныеПараметры.ЗаголовокСтатуса;
	
	Ответ = РезультатВопроса;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	КоличествоОбработанных = УстановитьСтатусДокументовНаСервере(ВыделенныеСтроки, СтатусДокументов);
	ОбщегоНазначенияУТКлиент.ОповеститьПользователяОбУстановкеСтатуса(
		Элементы.Список,
		КоличествоОбработанных,
		ВыделенныеСтроки.Количество(),
		ЗаголовокСтатуса);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьСтатусДокументовНаСервере(ВыделенныеСтроки, СтатусДокументов)

	МассивДокументов = ВыделенныеЗаказы(ВыделенныеСтроки);
	КоличествоОбработанных =
		ОбщегоНазначенияУТВызовСервера.УстановитьСтатусДокументов(МассивДокументов, СтатусДокументов);
	
	Возврат КоличествоОбработанных;

КонецФункции

&НаСервереБезКонтекста
Функция ВыделенныеЗаказы(ВыделенныеСтроки)

	МассивДокументов = Новый Массив;
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		МассивДокументов.Добавить(ВыделеннаяСтрока.Заказ);
	КонецЦикла;

	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивДокументов);

КонецФункции

&НаКлиенте
Процедура ИзменитьОтборСтрогоПоДатеСобытия()

	КоллекцияЭлементов = Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы;
	Представление = НСтр("ru = 'Отбор по дате события';
						|en = 'Filter by event date'");
	ДатаНачалаСобытия = Дата(1, 1, 1);
	
	ГруппаОтбора =
		ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(КоллекцияЭлементов, Представление);
	
	Если ГруппаОтбора <> Неопределено Тогда
		Если Актуальность = "Сегодня"
				Или Актуальность = "Завтра"
				Или Актуальность = "Послезавтра" Тогда
			НовыйВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			// ДатаСобытия не изменяется.
		ИначеЕсли Актуальность = "НаЭтойНеделе" Тогда
			НовыйВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
			ДатаАктуальности = ОбщегоНазначенияКлиент.ДатаСеанса();
			ДатаСобытия = КонецНедели(ОбщегоНазначенияКлиент.ДатаСеанса());
			ДатаНачалаСобытия = НачалоНедели(ДатаАктуальности);
		ИначеЕсли Актуальность = "ЧерезНеделю" Тогда
			НовыйВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
			// ДатаСобытия не изменяется.
		Иначе
			Возврат;
		КонецЕсли;
		
		Для Каждого ЭлементОтбора Из ГруппаОтбора.Элементы Цикл
			Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаСобытия")
					И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно Тогда
				ЭлементОтбора.ВидСравнения = НовыйВидСравнения;
				ЭлементОтбора.ПравоеЗначение = ДатаСобытия;
				ЭлементОтбора.Использование  = Истина;
			ИначеЕсли ЗначениеЗаполнено(ДатаНачалаСобытия)
					И ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаСобытия")
					И Не ЗначениеЗаполнено(ЭлементОтбора.ПравоеЗначение) Тогда
				ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ЭлементОтбора.ПравоеЗначение = ДатаНачалаСобытия;
				ЭлементОтбора.Использование  = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#Область НастройкаКомплектаДокументов

&НаКлиенте
Процедура ПолучитьНастройкуКомплектаДокументовЗавершение(Результат, КлючОбъекта) Экспорт 

	Если Результат <> Неопределено Тогда
		ПродажиВызовСервера.СохранитьНастройкуКомплектаДокументов(Результат, КлючОбъекта);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

// ЭлектронноеВзаимодействие.СервисEDI

#Область EDI

&НаКлиенте
Процедура Подключаемый_СтатусEDIОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	ДокументыEDIИнтеграцияКлиент.ОтборСтатусEDIОбработкаВыбора(
		ЭтотОбъект,
		Элемент,
		ВыбранноеЗначение,
		СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СтатусEDIОчистка(Элемент, СтандартнаяОбработка)

	ДокументыEDIИнтеграцияКлиент.ОтборСтатусEDIОчистка(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОформляемыеДокументыEDIОбработкаНавигационнойСсылки(Элемент,
			НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)

	ДокументыEDIИнтеграцияКлиент.ОформляемыеДокументыEDIОбработкаНавигационнойСсылки(
		ЭтотОбъект,
		НавигационнаяСсылкаФорматированнойСтроки,
		СтандартнаяОбработка);

	КонецПроцедуры                                 	

&НаКлиенте
Процедура ОбновлениеИнформацииEDI()

	ДокументыEDIИнтеграцияКлиент.ВыполнитьОбновлениеИнформацииСпискаДокументов(
		ЭтотОбъект,
		ПредопределенноеЗначение("Перечисление.ТипыДокументовEDI.ЗаказКлиента"));

КонецПроцедуры

#КонецОбласти

// Конец ЭлектронноеВзаимодействие.СервисEDI

#КонецОбласти
