
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Позволяет указать объекты метаданных, для которых задана логика ограничения доступа к данным.
//
// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
		"РазрешитьЧтениеИзменение
		|ГДЕ
		|	ЗначениеРазрешено(УчетнаяЗапись.Организация, ПустаяСсылка КАК Истина)
		|	И ЗначениеРазрешено(УчетнаяЗапись.Партнер, ПустаяСсылка КАК Истина)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Заполняет или обновляет данные по экземплярам на основании данных документов учетной системы.
//
// Параметры:
//   Заказы - Массив из Строка - список обрабатываемых заказов.
//   Автор  - Неопределено, СправочникСсылка.Пользователи - пользователь,
//                инициировавший оформление документов отгрузки.
//
// Возвращаемое значение:
//   Структура - результат заполнения сведений:
//     * ИнформацияОбОшибке       - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//     * РезультатПроверкиЗаказов - Соответствие из см. ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа.
//
Функция ОформитьДокументыОтгрузки(Заказы, Автор = Неопределено) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ИнформацияОбОшибке",       ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка());
	Результат.Вставить("РезультатПроверкиЗаказов", Новый Соответствие);
	
	СписокОшибок = Новый Массив;
	
	ПараметрыЗаполнения =
		РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПараметрыЗаполненияДанныхПоЭкземплярам();
	ПараметрыЗаполнения.ПроверитьЗаказы                = Истина;
	ПараметрыЗаполнения.ВернутьСведенияПоТоварам       = Истина;
	
	РезультатЗаполненияЭкземпляров =
		РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам(
			Заказы, ПараметрыЗаполнения);
	
	СведенияПоТоварам = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПодготовитьСведенияПоТоварамДляЗаполненияДокументовОтгрузки(
		РезультатЗаполненияЭкземпляров.СведенияПоТоварам);
	
	ТекстОшибки = ПерезаполнитьДокументыОтгрузки(
		РезультатЗаполненияЭкземпляров.РезультатПроверкиЗаказов,
		СведенияПоТоварам,
		Автор);
	
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СписокОшибок.Добавить(ТекстОшибки);
	КонецЕсли;
	
	ТекстОшибки = СоздатьДокументыОтгрузки(
		РезультатЗаполненияЭкземпляров.РезультатПроверкиЗаказов,
		СведенияПоТоварам,
		Автор);
		
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		СписокОшибок.Добавить(ТекстОшибки);
	КонецЕсли;
	
	РезультатЗаполненияЭкземпляров =
		РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам(
			Заказы,
			ПараметрыЗаполнения);
	
	Результат.РезультатПроверкиЗаказов = РезультатЗаполненияЭкземпляров.РезультатПроверкиЗаказов;
	
	Если СписокОшибок.Количество() > 0 Тогда
		Результат.ИнформацияОбОшибке.КодОшибки      = "Ошибка_ЗаполнениеДокументовОтгрузки";
		Результат.ИнформацияОбОшибке.ОписаниеОшибки = СтрСоединить(СписокОшибок, Символы.ПС);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Выполняет загрузку заказов (отправлений) с торговой площадки.
// Используется регламентным заданием.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Параметры     - Неопределено, Структура - параметры зависят от торговой площадки.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗагрузитьЗаказыТорговойПлощадки(УчетнаяЗапись, Знач Параметры = Неопределено) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КодыОшибок = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаНеУказанаУчетнаяЗапись;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	НастройкиУчетнойЗаписи      = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись,,,Истина);
	ЗначенияФункциональныхОпций = ИнтеграцияСМаркетплейсамиСервер.ПолучитьФункциональныеОпции(НастройкиУчетнойЗаписи.Соглашение);
	
	Если Не ЗначенияФункциональныхОпций.ИспользоватьЗаказыКлиентов
			Или Не ЗначенияФункциональныхОпций.ИспользоватьРасширенныеВозможностиЗаказаКлиента Тогда
		ТекстОшибки = НСтр("ru = 'Загрузка и обработка заказов доступны при использовании заказов клиентов в режимах ""Заказ только со склада"" или ""Заказ со склада и под заказ"".';
							|en = 'Order import and processing are available when sales orders are used in ""Order only from warehouse"" or ""Order from warehouse and backorders"" modes.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ПараметрыЗаполнения = ПараметрыЗаполненияЗаказаКлиента(УчетнаяЗапись, НастройкиУчетнойЗаписи, КодыОшибок);
	
	Если Не ПроверитьОбязательныеПараметрыЗаполнения(ПараметрыЗаполнения, Ошибка) Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	Если Параметры <> Неопределено Тогда
		Параметры = ОбщегоНазначения.СкопироватьРекурсивно(Параметры);
	КонецЕсли;
	
	Если НастройкиУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Если Параметры = Неопределено Тогда
			Параметры = ИнтеграцияСМаркетплейсомOzonСервер.НовыйПараметрыЗагрузкиЗаказов();
			Параметры.СтатусыОтправлений.Добавить("awaiting_packaging");
			Параметры.СтатусыОтправлений.Добавить("awaiting_deliver");
		КонецЕсли;
		
		ТекстЗапроса = ИнтеграцияСМаркетплейсомOzonСервер.ТекстЗапросаНеобработанныхЗаказовТорговойПлощадки();
		ТаблицаЗаказов = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЗаказыТорговойПлощадки(
			УчетнаяЗапись,
			Параметры,
			Ошибка);
		
		ДанныеПокупателейИзСервиса = Неопределено;
	ИначеЕсли НастройкиУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		Если Параметры = Неопределено Тогда
			Параметры = ИнтеграцияСЯндексМаркетСервер.НовыйПараметрыЗагрузкиЗаказов();
			Параметры.ТолькоНовые = Истина;
		КонецЕсли;
	
		ТекстЗапроса = ИнтеграцияСЯндексМаркетСервер.ТекстЗапросаНеобработанныхЗаказовТорговойПлощадки();
		ТаблицаЗаказов = ИнтеграцияСЯндексМаркетСервер.ПолучитьНеобработанныеЗаказы(
			УчетнаяЗапись,
			Параметры,
			Ошибка);
		
		ДанныеПокупателейИзСервиса = ИнтеграцияСЯндексМаркетСервер.ПолучитьДанныеПокупателейПоЗаказам(
			УчетнаяЗапись,
			ТаблицаЗаказов,
			Ошибка);
	Иначе
		ТекстЗапроса = "";
		ТаблицаЗаказов = Неопределено;
		ДанныеПокупателейИзСервиса = Неопределено;
	КонецЕсли;
	
	ДанныеПокупателейПоЗаказам = ИнтеграцияСМаркетплейсамиСервер.ДанныеПокупателейПоЗаказам(
		УчетнаяЗапись,
		НастройкиУчетнойЗаписи,
		ДанныеПокупателейИзСервиса);
	
	Если Не ПустаяСтрока(Ошибка.КодОшибки) Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТаблицаЗаказов) Или ПустаяСтрока(ТекстЗапроса) Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные для загрузки необработанных заказов с торговой площадки <%1> отсутствуют.';
				|en = 'Data for importing unprocessed orders from the <%1> marketplace is missing.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ТаблицаЗаказов.Колонки.Добавить("Записан", Новый ОписаниеТипов("Булево"));
	
	ПараметрыЗаполнения.ДанныеПокупателейПоЗаказам = ДанныеПокупателейПоЗаказам;
	ПараметрыЗаполнения.ТекстЗапроса = ТекстЗапроса;
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.РезультатЗаписиЗагружаемыхДанных();
	Результат.Служебное = Новый Массив;
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьЗаказыКлиентов(
		Результат,
		ТаблицаЗаказов,
		ПараметрыЗаполнения);
	
	// Обновить исключение из доставки по настройкам складов.
	ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	ИнформацияОбОтгрузке.ОтправленияВОтгрузке =
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТаблицаЗаказов.ВыгрузитьКолонку("НомерОтправления"));
	
	ОбновитьДанныеПоДоставке(ИнформацияОбОтгрузке, Результат.Ссылки);
	
	ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ИнформацияОбОтгрузке.Ошибка);
	
	Возврат Ошибка;

КонецФункции

// Обновляет статусы заказов (отправлений) торговой площадки.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Параметры     - Неопределено, Структура - параметры функции:
//     * НачалоПериода       - Дата, Неопределено - начало периода обновления данных. По умолчанию - текущая дата.
//     * ОкончаниеПериода    - Дата, Неопределено - окончание периода обновления данных. По умолчанию - текущая дата.
//     * ИдентификаторЗаказа - Строка, Массив Из Строка, Неопределено - идентификатор заказа выборочного обновления данных.
//     * Заказ               - ДокументСсылка.ЗаказКлиента, Неопределено - заказ выборочного обновления данных.
//   ИнформацияОбОтгрузке - Неопределено,
//                         см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОбновитьСтатусыЗаказовТорговойПлощадки(УчетнаяЗапись, Знач Параметры = Неопределено,
			Знач ИнформацияОбОтгрузке = Неопределено) Экспорт

	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если Параметры = Неопределено Тогда
		Параметры = Новый Структура;
		
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon
			ИЛИ ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			Параметры.Вставить("НачалоПериода",       Неопределено);
			Параметры.Вставить("ОкончаниеПериода",    Неопределено);
			Параметры.Вставить("ИдентификаторЗаказа", Неопределено);
			Параметры.Вставить("Заказ",               Неопределено);
		КонецЕсли;
	Иначе
		Параметры = ОбщегоНазначения.СкопироватьРекурсивно(Параметры);
	КонецЕсли;
	
	Параметры.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	КодыОшибок = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
							|en = 'Marketplace personal account not specified.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаНеУказанаУчетнаяЗапись;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	Статусы = Новый Массив;
	ИдентификаторыЗаказов = Новый Массив;
	Заказы = Новый Массив;
	Если ТипЗнч(Параметры.ИдентификаторЗаказа) = Тип("Массив") Тогда
		ИдентификаторыЗаказов = ОбщегоНазначения.СкопироватьРекурсивно(Параметры.ИдентификаторЗаказа);
		ИдентификаторыЗаказов = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИдентификаторыЗаказов);
	ИначеЕсли ЗначениеЗаполнено(Параметры.ИдентификаторЗаказа) Тогда
		ИдентификаторыЗаказов.Добавить(Параметры.ИдентификаторЗаказа);
	ИначеЕсли ТипЗнч(Параметры.Заказ) = Тип("Массив") Тогда
		Заказы = Параметры.Заказ;
	Иначе
		Статусы = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетнаяЗапись",         УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИдентификаторыЗаказов", ИдентификаторыЗаказов);
	Запрос.УстановитьПараметр("Заказы",                Заказы);
	Запрос.УстановитьПараметр("Статусы",               Статусы);
	
	Запрос.Текст = ТекстЗапросаОбновленияСтатусовЗаказовТорговойПлощадки(Запрос.Параметры);
	
	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	МетодДоставкиШаблон = ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада();
	
	ОтмененныеОтгрузки = Новый Массив;
	ВыборкаИдентификаторовОтгрузки = ПакетЗапроса[ПакетЗапроса.ВГраница() - 2].Выбрать();
	Пока ВыборкаИдентификаторовОтгрузки.Следующий() Цикл
		Если ИнформацияОбОтгрузке = Неопределено
				Или ИнформацияОбОтгрузке.ИдентификаторОтгрузки <> ВыборкаИдентификаторовОтгрузки.ИдентификаторОтгрузки Тогда
			ИнформацияОбОтгрузкеДляАнализа = ПолучитьИнформациюОбОтгрузке(
				УчетнаяЗапись,
				МетодДоставкиШаблон,
				Дата(1,1,1),
				ВыборкаИдентификаторовОтгрузки.ИдентификаторОтгрузки,
				Ложь);
		Иначе
			ИнформацияОбОтгрузкеДляАнализа = ИнформацияОбОтгрузке;
		КонецЕсли;
		
		Если ИнформацияОбОтгрузкеДляАнализа.ОтгрузкаОтменена Тогда
			ОтмененныеОтгрузки.Добавить(ВыборкаИдентификаторовОтгрузки.ИдентификаторОтгрузки);
		КонецЕсли;
	КонецЦикла;
	
	ВыборкаОбщихИтогов = ПакетЗапроса[ПакетЗапроса.ВГраница() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Если Не ВыборкаОбщихИтогов.Следующий() Тогда
		Если ИнформацияОбОтгрузке = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данные для обновления статусов заказов с торговой площадки <%1> отсутствуют.';
					|en = 'Data for updating statuses of orders from the <%1> marketplace is missing.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецЕсли;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ТаблицаЗаказов = ПолучитьРезультатОбновленияСтатусовЗаказов(
		УчетнаяЗапись,
		Параметры,
		Ошибка,
		ВыборкаОбщихИтогов.ДатаСозданияЗаказа);
	
	Если ТаблицаЗаказов.Количество() = 0 Тогда
		Если ИнформацияОбОтгрузке = Неопределено Тогда
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Данные для обновления статусов заказов с торговой площадки <%1> отсутствуют.';
					|en = 'Data for updating statuses of orders from the <%1> marketplace is missing.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				УчетнаяЗапись);
			
			Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
			Ошибка.ОписаниеОшибки = ТекстОшибки;
		КонецЕсли;
		
		Возврат Ошибка;
	КонецЕсли;
	
	// Ранее замер "ОбщийМодуль.ИнтеграцияСМаркетплейсомOzonСервер.ОбновитьСтатусыЗаказовТорговойПлощадки".
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ОбновитьСтатусыЗаказовТорговойПлощадки");
	
	ИзмеренияРегистра = Метаданные.РегистрыСведений.ЗаказыТорговыхПлощадок.Измерения;
	ТаблицаЗаказов.Колонки.Добавить("ЗаказКлиентаСсылка", ИзмеренияРегистра.Заказ.Тип);
	ТаблицаЗаказов.Колонки.Добавить("ДокументОтгрузкиСсылка", ИзмеренияРегистра.ДокументОтгрузки.Тип);
	ТаблицаЗаказов.Колонки.Добавить("КОбработке", Новый ОписаниеТипов("Булево"));
	
	Если ТаблицаЗаказов.Колонки.Найти("ИдентификаторОтгрузки") = Неопределено Тогда
		ТаблицаЗаказов.Колонки.Добавить("ИдентификаторОтгрузки", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	КонецЕсли;
	
	ТаблицаЗаказов.Индексы.Добавить("ИдентификаторЗаказа, НомерОтправления");
	ТаблицаЗаказов.Индексы.Добавить("КОбработке");
	
	ВыборкаИдентификаторовЗаказов = ВыборкаОбщихИтогов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаИдентификаторовЗаказов.Следующий() Цикл
		ВыборкаОтправлений = ВыборкаИдентификаторовЗаказов.Выбрать();
		Пока ВыборкаОтправлений.Следующий() Цикл
			Отбор = Новый Структура;
			Отбор.Вставить("ИдентификаторЗаказа", ВыборкаОтправлений.ИдентификаторЗаказа);
			Если СтрНайти(ВыборкаОтправлений.НомерОтправления, РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления()) > 0 Тогда
				Отбор.Вставить("НомерОтправления", ВыборкаОтправлений.НомерРодительскогоОтправления);
			Иначе
				Отбор.Вставить("НомерОтправления", ВыборкаОтправлений.НомерОтправления);
			КонецЕсли;
			СтрокиПоиска = ТаблицаЗаказов.НайтиСтроки(Отбор);
			
			Если СтрокиПоиска.Количество() > 0 Тогда
				Если СтрНайти(ВыборкаОтправлений.НомерОтправления, РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления()) > 0 Тогда
					СтрокаТаблицыЗначений = ТаблицаЗаказов.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, СтрокиПоиска[0]);
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаОтправлений, "НомерОтправления, НомерРодительскогоОтправления");
				Иначе
					СтрокаТаблицыЗначений = СтрокиПоиска[0];
				КонецЕсли;
				
				Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПолучитьПоПредставлению(
					СтрокаТаблицыЗначений.СтатусОтправления,
					СтрокаТаблицыЗначений.ПодстатусОтправления,
					ВидМаркетплейса);
				
				Если ВыборкаОтправлений.Статус <> Статус Тогда
					СтрокаТаблицыЗначений.ИдентификаторОтгрузки = ВыборкаОтправлений.ИдентификаторОтгрузки;
					СтрокаТаблицыЗначений.КОбработке = Истина;
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ВыборкаОтправлений.ИдентификаторОтгрузки) Тогда
					Если ОтмененныеОтгрузки.Найти(ВыборкаОтправлений.ИдентификаторОтгрузки) <> Неопределено Тогда
						СтрокаТаблицыЗначений.ИдентификаторОтгрузки = "";
						СтрокаТаблицыЗначений.КОбработке = Истина;
					ИначеЕсли СтрокаТаблицыЗначений.КОбработке
							И Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
						СтрокаТаблицыЗначений.ИдентификаторОтгрузки = "";
					КонецЕсли;
				КонецЕсли;
				
				Если ВыборкаОтправлений.ДополнительныеСведения <> СтрокаТаблицыЗначений.ДополнительныеСведения
						Или ВыборкаОтправлений.СрокОтгрузки <> СтрокаТаблицыЗначений.ДатаОтгрузки
						Или ВыборкаОтправлений.СрокДоставки <> СтрокаТаблицыЗначений.ДатаДоставки Тогда
					СтрокаТаблицыЗначений.КОбработке = Истина;
				КонецЕсли;
				
				Если СтрокаТаблицыЗначений.КОбработке
						И (ЗначениеЗаполнено(ВыборкаОтправлений.ЗаказКлиентаСсылка)
								Или ЗначениеЗаполнено(ВыборкаОтправлений.ДокументОтгрузкиСсылка)) Тогда
					ЗаполнитьЗначенияСвойств(СтрокаТаблицыЗначений, ВыборкаОтправлений, "ЗаказКлиентаСсылка, ДокументОтгрузкиСсылка");
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись,,,Истина);
	ОчищатьДанныеЭкземпляров = ОчищатьДанныеЭкземпляров(НастройкиУчетнойЗаписи);
	ОформлятьСначалаНакладные = ОформлятьСначалаНакладные();
	
	Отказ = Ложь;
	ОбновленныеЗаказы = Новый Массив;
	ПричиныОтмены     = Новый Соответствие;
	
	Отбор = Новый Структура;
	Отбор.Вставить("КОбработке", Истина);
	
	СтрокиЗаказа = ТаблицаЗаказов.НайтиСтроки(Отбор);
	Для Каждого ДанныеОтправления Из СтрокиЗаказа Цикл
		СтатусЗаписан = Ложь;
		
		НачатьТранзакцию();
		
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
			ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", ДанныеОтправления.ЗаказКлиентаСсылка);
			ЭлементБлокировкиДанных.УстановитьЗначение("ДокументОтгрузки", ДанныеОтправления.ДокументОтгрузкиСсылка);
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Заказ.Установить(ДанныеОтправления.ЗаказКлиентаСсылка);
			НаборЗаписей.Отбор.ДокументОтгрузки.Установить(ДанныеОтправления.ДокументОтгрузкиСсылка);
			НаборЗаписей.Прочитать();
			
			Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ПолучитьПоПредставлению(
				ДанныеОтправления.СтатусОтправления, 
				ДанныеОтправления.ПодстатусОтправления,
				ВидМаркетплейса);
			
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, ДанныеОтправления);
				
				Запись.УчетнаяЗапись    = УчетнаяЗапись;
				Запись.Заказ            = ДанныеОтправления.ЗаказКлиентаСсылка;
				Запись.ДокументОтгрузки = ДанныеОтправления.ДокументОтгрузкиСсылка;
			Иначе
				Запись = НаборЗаписей[0];
			КонецЕсли;
			
			Запись.Статус                              = Статус;
			Запись.СрокОтгрузки                        = ДанныеОтправления.ДатаОтгрузки;
			Запись.СрокДоставки                        = ДанныеОтправления.ДатаДоставки;
			Запись.ИдентификаторСкладаТорговойПлощадки = ДанныеОтправления.ИдентификаторСклада;
			Запись.НаименованиеСкладаТорговойПлощадки  = ДанныеОтправления.НаименованиеСклада;
			Запись.НомерРодительскогоОтправления       = ДанныеОтправления.НомерРодительскогоОтправления;
			Запись.ДатаУстановкиСтатуса                = ТекущаяДатаСеанса();
			Запись.ДатаСозданияЗаказа                  = ДанныеОтправления.ДатаОтправления;
			Запись.ТребуетсяПолучениеЭтикеток          = (Запись.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
			
			Запись.ИдентификаторОтгрузки = ДанныеОтправления.ИдентификаторОтгрузки;
			Запись.ДополнительныеСведения = ДанныеОтправления.ДополнительныеСведения;
			
			НаборЗаписей.Записать(Истина);
			
			СтатусЗаписан = Истина;
			ОбновленныеЗаказы.Добавить(ДанныеОтправления.ЗаказКлиентаСсылка);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Отказ = Истина;
			
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'При записи данных по номеру заказа %1 возникла ошибка: %2';
					|en = 'An error occurred while saving the data for order number %1: %2'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				ДанныеОтправления.НомерЗаказа,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ЗаписьЖурналаРегистрации(ИмяСобытия,
				УровеньЖурналаРегистрации.Ошибка,,,
				ТекстОшибки);
		КонецПопытки;
		
		Если Не Отказ И СтатусЗаписан Тогда
			Если Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.Возвращен
					Или Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
				
				ДанныеПричиныОтмены = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеПричиныОтменыЗаказа();
				ДанныеПричиныОтмены.Идентификатор = ДанныеОтправления.ИдентификаторПричиныОтмены;
				ДанныеПричиныОтмены.Наименование = ДанныеОтправления.ПричинаОтмены;
				
				ПричинаОтмены = ИнтеграцияСМаркетплейсамиСервер.ПричинаОтменыЗаказаКлиента(
					УчетнаяЗапись, ДанныеПричиныОтмены, ИмяСобытия, ПричиныОтмены);
				
				ОбработатьСтатусОтправленияВозвращенОтменен(
					УчетнаяЗапись,
					ДанныеОтправления.ЗаказКлиентаСсылка,
					ДанныеОтправления.ДокументОтгрузкиСсылка,
					ПричинаОтмены,
					ОформлятьСначалаНакладные,
					ИмяСобытия);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗакрытьЗаказы(УчетнаяЗапись,
		ОбщегоНазначенияКлиентСервер.СвернутьМассив(ОбновленныеЗаказы),
		ОчищатьДанныеЭкземпляров,
		Ошибка,
		ИмяСобытия);
	
	Если Отказ Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При обновлении статусов заказов торговой площадки <%1> возникли ошибки. Подробнее см. журнал регистрации.';
				|en = 'Errors occurred when updating order statuses of the <%1> marketplace. For more information, see the event log.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		Возврат Ошибка;
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		ТаблицаЗаказов.Количество() / 10);
	
	// Обновить исключение из доставки по настройкам складов.
	Если ОбновленныеЗаказы.Количество() > 0 Тогда
		ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
		
		ОбновитьДанныеПоДоставке(ИнформацияОбОтгрузке, ОбновленныеЗаказы);
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ИнформацияОбОтгрузке.Ошибка);
	КонецЕсли;
	
	Возврат Ошибка;

КонецФункции

// Выполняет подтверждение сборки заказов (отправлений) на торговой площадке.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказы            - Массив Из ДокументСсылка.ЗаказКлиента - список обрабатываемых заказов.
//   НомераОтправлений - Строка, Массив Из Строка - номера отправлений, для которых нужно создать экземпляры товаров.
//   ОбновитьСтатусы   - Булево - обновить статусы отправлений перед сборкой.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПодтвердитьСборкуЗаказовНаТорговойПлощадке(УчетнаяЗапись, Знач Заказы, Знач НомераОтправлений,
			Знач ОбновитьСтатусы = Истина) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	КодыОшибок = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок();
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаНеУказанаУчетнаяЗапись;
		Ошибка.ОписаниеОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
									|en = 'The marketplace account is not specified.'",
			ОбщегоНазначения.КодОсновногоЯзыка());
		
		Возврат Ошибка;
	КонецЕсли;
	
	Если Не Справочники.УчетныеЗаписиМаркетплейсов.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗапись) Тогда
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаОбновлениеДанныхЗапрещено;
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для учетной записи <%1> запрещено обновление данных торговой площадки';
				|en = 'The trading platform data update is not allowed for the <%1> account'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
		
		Возврат Ошибка;
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ОбновитьСтатусы Тогда
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon
			ИЛИ ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			ПараметрыФункции = Новый Структура;
			ПараметрыФункции.Вставить("НачалоПериода",       Неопределено);
			ПараметрыФункции.Вставить("ОкончаниеПериода",    Неопределено);
			ПараметрыФункции.Вставить("ИдентификаторЗаказа", Неопределено);
			ПараметрыФункции.Вставить("Заказ",               Заказы);
		Иначе
			ПараметрыФункции = Неопределено;
		КонецЕсли;
		
		ОбновитьСтатусыЗаказовТорговойПлощадки(УчетнаяЗапись, ПараметрыФункции);
	КонецЕсли;
	
	Детализация = Новый Массив; // Массив из Строка
	Заказы      = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Заказы);
	
	ПараметрыЗаполнения = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПараметрыЗаполненияДанныхПоЭкземплярам();
	ПараметрыЗаполнения.ПроверитьЗаказы          = Истина;
	ПараметрыЗаполнения.ВернутьСведенияПоТоварам = Истина;
	
	РезультатЗаполнения = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам(Заказы, ПараметрыЗаполнения);
		
	Для Каждого Заказ Из Заказы Цикл
		РезультатПроверкиЗаказа = РезультатЗаполнения.РезультатПроверкиЗаказов[Заказ];
		
		Индекс = НомераОтправлений.ВГраница();
		Пока Индекс >= 0 Цикл
			НомерОтправления = НомераОтправлений[Индекс];
			
			// Проверить принадлежность номера отправления заказу
			Если РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений.Получить(НомерОтправления) = Неопределено Тогда
				Индекс = Индекс - 1;
				Продолжить;
			КонецЕсли;
			
			// Проверить готовность отправления
			Если РезультатПроверкиЗаказа.ОтправленияКПодтверждениюСборки.Найти(НомерОтправления) = Неопределено Тогда
				НомераОтправлений.Удалить(Индекс);
			ИначеЕсли СтрНайти(НомерОтправления, РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления()) = 0 Тогда
				Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
					
					ЕстьНеОтмеченныеПодчиненные = Ложь;
					
					Для Каждого ЭлементКоллекции Из РезультатПроверкиЗаказа.ОтправленияВСборке Цикл
						Если ЭлементКоллекции <> НомерОтправления
							И СтрНайти(ЭлементКоллекции, РегистрыСведений.ЗаказыТорговыхПлощадок.ПостфиксНовогоОтправления()) > 0
							И НомераОтправлений.Найти(ЭлементКоллекции) = Неопределено Тогда
							ДанныеОтправления = РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений.Получить(ЭлементКоллекции);
							Если ДанныеОтправления <> Неопределено
								И ДанныеОтправления.НомерРодительскогоОтправления = НомерОтправления Тогда
								ЕстьНеОтмеченныеПодчиненные = Истина;
								Детализация.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Для сборки отправления %1 требуется предварительно собрать или отметить для сборки с основным подчиненное отправление %2.';
									|en = 'To pick the %1 shipment, pick the %2 subordinate shipment or mark it for picking.'",
								ОбщегоНазначения.КодОсновногоЯзыка()),
								НомерОтправления,
								ЭлементКоллекции));
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьНеОтмеченныеПодчиненные Тогда
						НомераОтправлений.Удалить(Индекс);
					КонецЕсли; 
				КонецЕсли;
			КонецЕсли;
			
			Индекс = Индекс - 1;
		КонецЦикла;
	КонецЦикла;
	
	// Подтверждение сборки
	Если ЗначениеЗаполнено(НомераОтправлений) Тогда
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			Ошибка = ИнтеграцияСМаркетплейсомOzonСервер.ПодтвердитьСборкуЗаказовНаТорговойПлощадке(
				УчетнаяЗапись,
				НомераОтправлений); 
		ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			
			ЗаказыДляСборки = ПолностьюСобираемыеЗаказы(Заказы, РезультатЗаполнения, НомераОтправлений);
			ЗаказыНеПопавшиеВСборку = ОбщегоНазначенияКлиентСервер.РазностьМассивов(Заказы, ЗаказыДляСборки);
			
			Ошибка = ИнтеграцияСЯндексМаркетСервер.ПодтвердитьСборкуЗаказовНаТорговойПлощадке(
				УчетнаяЗапись,
				ЗаказыДляСборки);
			
			НомераЗаказов = НомераЗаказовНаТорговойПлощадке(ЗаказыНеПопавшиеВСборку);
			Для Каждого Заказ Из ЗаказыНеПопавшиеВСборку Цикл
				
				НомерЗаказа = НомераЗаказов.Получить(Заказ);
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = '%1: данные для сборки заказа торговой площадки отсутствуют.';
						|en = '%1: data for picking the marketplace order is missing.'"),
					НомерЗаказа);
				Детализация.Добавить(ТекстОшибки);
				
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если РезультатПроверкиЗаказа.ОтправленияСНезаполненнымиДанными.Количество() > 0 Тогда
		Детализация.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Требуется заполнить данные по товарам для сборки отправлений: %1.';
				|en = 'Fill the data on goods for picking shipments: %1.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			СтрСоединить(РезультатПроверкиЗаказа.ОтправленияСНезаполненнымиДанными, ", ")));
	КонецЕсли;
	
	Если Детализация.Количество() > 0 Тогда
		Если Не ПустаяСтрока(Ошибка.ОписаниеОшибки) Тогда
			Детализация.Добавить(Ошибка.ОписаниеОшибки);
		КонецЕсли;
		
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = СтрСоединить(Детализация, Символы.ПС);
	ИначеЕсли Не ЗначениеЗаполнено(НомераОтправлений) Тогда
		Ошибка.КодОшибки      = КодыОшибок.ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Данные для сборки заказов торговой площадки <%1> отсутствуют.';
				|en = 'Data for picking orders of the <%1> marketplace is missing.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			УчетнаяЗапись);
	КонецЕсли;
	
	Возврат Ошибка;

КонецФункции

// Выполняет отмену заказов (отправлений) на торговой площадке.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказы            - Массив Из ДокументСсылка.ЗаказКлиента - список обрабатываемых заказов.
//   НомераОтправлений - Строка, Массив Из Строка - номера отправлений, для которых выполняется отмена.
//   ПричинаОтмены     - Структура - причина отмены:
//     * Идентификатор   - Число - идентификатор причины отмены отправления;
//     * Наименование    - Строка - наименование причины отмены;
//     * Ссылка          - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены 1С.
//   ОбновитьСтатусы   - Булево - обновить статусы отправлений перед отменой.
//
// Возвращаемое значение:
//   См. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОтменитьЗаказыНаТорговойПлощадке(УчетнаяЗапись, Знач Заказы, Знач НомераОтправлений,
			Знач ПричинаОтмены, Знач ОбновитьСтатусы = Истина) Экспорт

	Если ТипЗнч(НомераОтправлений) = Тип("Строка") Тогда
		НомерОтправления = НомераОтправлений;
		
		НомераОтправлений = Новый Массив;
		НомераОтправлений.Добавить(НомерОтправления);
	КонецЕсли;
	
	Если ОбновитьСтатусы Тогда
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("НачалоПериода",       Неопределено);
		ПараметрыФункции.Вставить("ОкончаниеПериода",    Неопределено);
		ПараметрыФункции.Вставить("ИдентификаторЗаказа", Неопределено);
		ПараметрыФункции.Вставить("Заказ",               Заказы);
		
		ОбновитьСтатусыЗаказовТорговойПлощадки(УчетнаяЗапись, ПараметрыФункции);
	КонецЕсли;
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	ПараметрыЗаполнения = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ПараметрыЗаполненияДанныхПоЭкземплярам();
	ПараметрыЗаполнения.ЗаписатьСведенияОбЭкземплярах  = Ложь;
	ПараметрыЗаполнения.ЗаписатьСведенияОбОтправлениях = Ложь;
	ПараметрыЗаполнения.ВернутьСведенияПоТоварам       = Истина;
	ПараметрыЗаполнения.ПроверитьЗаказы                = Истина;
	
	РезультатЗаполнения = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам(Заказы, ПараметрыЗаполнения);
	
	Результат = ПроверитьВозможностьОтменыТоваровОтправления(
		Заказы,
		НомераОтправлений,
		РезультатЗаполнения);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	ОтмененныеНомераОтправлений = Новый Массив;
	
	Если НомераОтправлений.Количество() > 0 Тогда
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			Ошибка = ИнтеграцияСМаркетплейсомOzonСервер.ОтменитьСборкуЗаказовНаТорговойПлощадке(
				УчетнаяЗапись,
				НомераОтправлений,
				ПричинаОтмены,
				ОтмененныеНомераОтправлений);
		ИначеЕсли ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
			Ошибка = ИнтеграцияСЯндексМаркетСервер.ОтменитьСборкуЗаказов(
				УчетнаяЗапись,
				Заказы,
				ОтмененныеНомераОтправлений);
		Иначе
			Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		КонецЕсли;
	КонецЕсли;
	
	ОбработатьОтменуЗаказов(
		УчетнаяЗапись,
		РезультатЗаполнения.РезультатПроверкиЗаказов,
		ОтмененныеНомераОтправлений,
		ПричинаОтмены,
		Ошибка);
	
	Если Не ПустаяСтрока(Результат.ИнформацияДляПользователя) Тогда
		Если ПустаяСтрока(Ошибка.КодОшибки) Тогда
			Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
			Ошибка.ОписаниеОшибки = Результат.ИнформацияДляПользователя;
		Иначе
			Ошибка.ОписаниеОшибки = Результат.ИнформацияДляПользователя + Символы.ПС + Ошибка.ОписаниеОшибки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибка;

КонецФункции

// Меняет статус отправления на "Открыть спор".
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   НомераОтправлений - Массив из Строка - номера отправлений.
// 
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОткрытьСпорНаТорговойПлощадке(УчетнаяЗапись, НомераОтправлений) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	ШаблонОшибки = НСтр("ru = 'Недоступно открытие спора для отправлений: %1.';
						|en = 'Cannot open a dispute for shipments: %1.'");
	
	ДоступныеНомераОтправлений = ПолучитьДоступныеНомераОтправлений(
		ВидМаркетплейса,
		НомераОтправлений,
		Ошибка,
		ШаблонОшибки,
		"ОткрытьСпор");
	
	Если ДоступныеНомераОтправлений.Количество() > 0 Тогда
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			ОшибкаОперации = ИнтеграцияСМаркетплейсомOzonСервер.ОткрытьСпор(УчетнаяЗапись, ДоступныеНомераОтправлений);
			ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ОшибкаОперации);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибка;

КонецФункции

// Меняет статус отправления в сервисе Ozon на "Ожидает отгрузки".
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   НомераОтправлений - Массив из Строка - номера отправлений.
// 
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ВернутьКОтгрузкеНаТорговойПлощадке(УчетнаяЗапись, НомераОтправлений) Экспорт

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	ШаблонОшибки = НСтр("ru = 'Недоступен возврат в отгрузку для отправлений: %1.';
						|en = 'Return to shipment is not available for shipments: %1.'");
	
	ДоступныеНомераОтправлений = ПолучитьДоступныеНомераОтправлений(
		ВидМаркетплейса,
		НомераОтправлений,
		Ошибка,
		ШаблонОшибки,
		"ВернутьВОтгрузку");
	
	Если ДоступныеНомераОтправлений.Количество() > 0 Тогда
		Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
			ОшибкаОперации = ИнтеграцияСМаркетплейсомOzonСервер.ВернутьКОтгрузке(УчетнаяЗапись, ДоступныеНомераОтправлений);
			ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Ошибка, ОшибкаОперации);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ошибка;

КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаСписка" Тогда
		Если Параметры.Свойство("ФиксированныеНастройки") Тогда
			ЭлементыОтбора = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Параметры.ФиксированныеНастройки.Отбор, "УчетнаяЗапись");
			Если ЭлементыОтбора.Количество() > 0
					И ЭлементыОтбора[0].Использование Тогда
				ДанныеТорговойПлощадки = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(ЭлементыОтбора[0].ПравоеЗначение,,,Истина);
				
				Параметры.Вставить("УчетнаяЗапись",               ЭлементыОтбора[0].ПравоеЗначение);
				Параметры.Вставить("КлючНазначенияИспользования", "FBS" + ДанныеТорговойПлощадки.ИдентификаторКлиента);
				
				ВыбраннаяФорма       = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ФормаСпискаЗаказов";
				СтандартнаяОбработка = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ЗаказыИОтправления

// Возвращает текст запроса динамического списка заказов и отправлений.
//
// Параметры:
//   СокращенныйТекст    - Булево - признак возврата сокращенного текста запроса.
//                           Сокращенный используется в форме ПодготовкаОтгрузки, а полный в форме ФормаСпискаЗаказов.
//   ИспользоватьПометки - Булево - признак установки в списке флагов.
//   ВключатьДанныеДокументаОтгрузки - Булево - признак необходимости получения данных документа отгрузки.
//
// Возвращаемое значение:
//   Строка - текст запроса.
//
Функция ТекстЗапросаДинамическогоСписка(СокращенныйТекст = Ложь, ИспользоватьПометки = Ложь,
			ВключатьДанныеДокументаОтгрузки = Истина) Экспорт

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись.ВидМаркетплейса КАК ВидМаркетплейса,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа,
		|	ДокументыЗаказа.ПометкаУдаления КАК ПометкаУдаления,
		|	ДокументыЗаказа.Проведен КАК Проведен,
		|	ВЫБОР
		|		КОГДА ДокументыЗаказа.Проведен
		|			ТОГДА 0
		|		КОГДА ДокументыЗаказа.ПометкаУдаления
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК КартинкаЗаказа,
		|	ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа КАК ДатаЗаказа, 
		|	&Открыть КАК ДанныеОтправлений,
		|	&ТекстЗапросаДанныеДокументаОтгрузки КАК ДанныеДокументаОтгрузки,
		|	ЗаказыТорговыхПлощадок.Статус КАК Статус,
		|	ЗаказыТорговыхПлощадок.СрокОтгрузки КАК СрокОтгрузки,
		|	ВЫБОР
		|		КОГДА ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовРаботе)
		|				И ЗаказыТорговыхПлощадок.СрокОтгрузки <> ДАТАВРЕМЯ(1, 1, 1)
		|				И &ДатаАктуальности > ЗаказыТорговыхПлощадок.СрокОтгрузки
		|			ТОГДА ИСТИНА
		|		КОГДА НЕ ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовКонечные)
		|				И ЗаказыТорговыхПлощадок.СрокОтгрузки <> ДАТАВРЕМЯ(1, 1, 1)
		|				И &ДатаАктуальности > ЗаказыТорговыхПлощадок.СрокДоставки
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Просрочен,
		|	ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки КАК ИдентификаторСкладаТорговойПлощадки,
		|	ЗаказыТорговыхПлощадок.НаименованиеСкладаТорговойПлощадки КАК НаименованиеСкладаТорговойПлощадки,
		|	ДокументыЗаказа.ЭтоЗаказКакСчет КАК ЭтоЗаказКакСчет,
		|	ВЫБОР
		|		КОГДА НЕ ДокументыЗаказа.Проведен
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.СостоянияЗаказовКлиентов.ПустаяСсылка)
		|		ИНАЧЕ ЕСТЬNULL(СостоянияЗаказовКлиентов.Состояние, ЗНАЧЕНИЕ(Перечисление.СостоянияЗаказовКлиентов.Закрыт))
		|	КОНЕЦ КАК Состояние,
		|	ЗаказыТорговыхПлощадок.ДополнительныеСведения КАК ДополнительныеСведения,
		|	ЗаказыТорговыхПлощадок.Заказ КАК Ссылка
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ДокументыЗаказа
		|		ПО ЗаказыТорговыхПлощадок.Заказ = ДокументыЗаказа.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТекстЗапросаСоединенияДокументаОтгрузки ПО ИСТИНА
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказовКлиентов
		|		ПО ЗаказыТорговыхПлощадок.Заказ = СостоянияЗаказовКлиентов.Заказ";
		
	Если ВключатьДанныеДокументаОтгрузки Тогда
		
		ТекстЗапросаДанныеДокументаОтгрузки = "
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ВЫБОР
		|		КОГДА РеестрДокументов.Ссылка ЕСТЬ NULL
		|			ТОГДА 3
		|		КОГДА ЕСТЬNULL(РеестрДокументов.Проведен, ЛОЖЬ)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(РеестрДокументов.ПометкаУдаления, ЛОЖЬ)
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК КартинкаДокументаОтгрузки,";
		
		ТекстЗапросаСоединенияДокументаОтгрузки = "
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеестрДокументов КАК РеестрДокументов
		|		ПО ЗаказыТорговыхПлощадок.ДокументОтгрузки = РеестрДокументов.Ссылка";
		
	Иначе
		
		ТекстЗапросаДанныеДокументаОтгрузки = "
		|	ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка) КАК ДокументОтгрузки,
		|	"""" КАК НомерОтправления,
		|	99 КАК КартинкаДокументаОтгрузки,";
		
		ТекстЗапросаСоединенияДокументаОтгрузки = "";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&Открыть",
		НСтр("ru = '""Открыть""';
			|en = 'Open'"));
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&ТекстЗапросаДанныеДокументаОтгрузки КАК ДанныеДокументаОтгрузки,",
		ТекстЗапросаДанныеДокументаОтгрузки);
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"ЛЕВОЕ СОЕДИНЕНИЕ ТекстЗапросаСоединенияДокументаОтгрузки ПО ИСТИНА",
		ТекстЗапросаСоединенияДокументаОтгрузки);
	
	Если СокращенныйТекст Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|{ГДЕ
		|	(ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись),
		|	(ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки = &ИдентификаторСклада),
		|	(ЗаказыТорговыхПлощадок.НомерОтправления В (&ОтправленияВОтгрузке))}";
	Иначе
		ДополнительныеПоля = "
			|	ЗаказыТорговыхПлощадок.Заказ КАК Ссылка,
			|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗаказыТорговыхПлощадок.Заказ) КАК ПредставлениеДокументаЗаказа,
			|	ВЫБОР
			|		КОГДА ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовРаботе)
			|				И ЗаказыТорговыхПлощадок.СрокОтгрузки <> ДАТАВРЕМЯ(1, 1, 1)
			|			ТОГДА НАЧАЛОПЕРИОДА(ЗаказыТорговыхПлощадок.СрокОтгрузки, ДЕНЬ)
			|		ИНАЧЕ НАЧАЛОПЕРИОДА(ЗаказыТорговыхПлощадок.СрокДоставки, ДЕНЬ)
			|	КОНЕЦ КАК ДатаСобытия,
			|	ЗаказыТорговыхПлощадок.СрокДоставки КАК СрокДоставки,
			|	ДокументыЗаказа.Приоритет КАК Приоритет,
			|	ДокументыЗаказа.Партнер КАК Партнер,
			|	ДокументыЗаказа.Контрагент КАК Контрагент,
			|	ДокументыЗаказа.Организация КАК Организация,
			|	ДокументыЗаказа.Соглашение КАК Соглашение,
			|	ДокументыЗаказа.Договор КАК Договор,
			|	ДокументыЗаказа.Валюта КАК Валюта,
			|	ДокументыЗаказа.СуммаДокумента КАК СуммаЗаказа,
			|	ДокументыЗаказа.Склад КАК Склад,
			|	ДокументыЗаказа.Менеджер КАК Менеджер,
			|	ПОДСТРОКА(ДокументыЗаказа.ДополнительнаяИнформация,1,1000) КАК ДополнительнаяИнформация,
			|	ЗаказыТорговыхПлощадок.ЗаказЮридическогоЛица КАК ЗаказЮридическогоЛица,
			|	ДокументыЗаказа.Автор КАК Автор,
			|	ПОДСТРОКА(ДокументыЗаказа.Комментарий,1,1000) КАК Комментарий,
			|	ДокументыЗаказа.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
			|	ВЫБОР
			|		КОГДА ДокументыЗаказа.Приоритет В
			|				(ВЫБРАТЬ ПЕРВЫЕ 1
			|					Приоритеты.Ссылка КАК Приоритет
			|				ИЗ
			|					Справочник.Приоритеты КАК Приоритеты
			|				УПОРЯДОЧИТЬ ПО
			|					Приоритеты.РеквизитДопУпорядочивания)
			|			ТОГДА 0
			|		КОГДА ДокументыЗаказа.Приоритет В
			|				(ВЫБРАТЬ ПЕРВЫЕ 1
			|					Приоритеты.Ссылка КАК Приоритет
			|				ИЗ
			|					Справочник.Приоритеты КАК Приоритеты
			|				УПОРЯДОЧИТЬ ПО
			|					Приоритеты.РеквизитДопУпорядочивания УБЫВ)
			|			ТОГДА 2
			|		ИНАЧЕ 1
			|	КОНЕЦ КАК КартинкаПриоритета,
			|	СостоянияЭД.СостояниеЭДО КАК СостояниеЭДО,
			|	СостоянияЭД.ПредставлениеСостояния КАК ПредставлениеСостояния,
			|	ЕСТЬNULL(СостоянияЗаказовКлиентов.СуммаОтгрузки, 0) КАК СуммаОтгрузки,
			|	ЕСТЬNULL(СостоянияЗаказовКлиентов.ПроцентОтгрузки, 0) КАК ПроцентОтгрузки,
			|	ЕСТЬNULL(СостоянияЗаказовКлиентов.ЕстьРасхожденияОрдерНакладная, ЛОЖЬ) КАК ЕстьРасхожденияОрдерНакладная,
			|	&ТекстЗапросаДанныеДокументаОтгрузки КАК ДанныеДокументаОтгрузки,
			|	ВЫБОР
			|		КОГДА ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовВДоставке)
			|				И ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки <> """"
			|			ТОГДА 0
			|		ИНАЧЕ -1
			|	КОНЕЦ КАК КартинкаДоставки,
			|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки
			|{ВЫБРАТЬ
			|	ПредставлениеДокументаЗаказа}";
		
		Если ВключатьДанныеДокументаОтгрузки Тогда
			ТекстЗапросаДанныеДокументаОтгрузки = "
			|	ЕСТЬNULL(РеестрДокументов.Сумма, 0) КАК СуммаДокументаОтгрузки,";
		Иначе
			ТекстЗапросаДанныеДокументаОтгрузки = "
			|	0 КАК СуммаДокументаОтгрузки,";
		КонецЕсли;
		
		ДополнительныеПоля = СтрЗаменить(
			ДополнительныеПоля,
			"&ТекстЗапросаДанныеДокументаОтгрузки КАК ДанныеДокументаОтгрузки,",
			ТекстЗапросаДанныеДокументаОтгрузки);
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗаказыТорговыхПлощадок.Заказ КАК Ссылка", ДополнительныеПоля);
		
		ТекстЗапроса = ТекстЗапроса + "
			|		{ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияПоОбъектамУчетаЭДО КАК СостоянияЭД
			|		ПО ЗаказыТорговыхПлощадок.Заказ = СостоянияЭД.СсылкаНаОбъект}";
	КонецЕсли;
	
	Если ИспользоватьПометки Тогда
		ДополнительныеПоля = "
			|	ЗаказыТорговыхПлощадок.Заказ КАК Ссылка,
			|	ВЫБОР
			|		КОГДА &ЭтоУстановкаПометокДляВсехПозиций
			|				И НЕ ЗаказыТорговыхПлощадок.НомерОтправления В (&НомераОтправленийИсключенные)
			|			ТОГДА ИСТИНА
			|		КОГДА ЗаказыТорговыхПлощадок.НомерОтправления В (&НомераОтправленийВыбранные)
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК Пометка,
			|	ИСТИНА КАК ПометкаУстановлена,
			|	ЛОЖЬ КАК ПометкаСнята";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗаказыТорговыхПлощадок.Заказ КАК Ссылка", ДополнительныеПоля);
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

// Возвращает постфикс для формирования нового временного номера отправления.
//
// Возвращаемое значение:
//   Строка - постфикс отправления.
//
Функция ПостфиксНовогоОтправления() Экспорт

	Возврат "-Новое";

КонецФункции

// Генерирует номер нового отправления по номеру заказа.
//
// Параметры:
//   НомерЗаказа                   - Строка - номер заказа по данным торговой площадки.
//   КоличествоОтправленийПоЗаказу - Число - количество номеров отправлений по заказу.
//
// Возвращаемое значение:
//   Строка - номер нового отправления.
//
Функция НомерНовогоОтправления(НомерЗаказа, КоличествоОтправленийПоЗаказу) Экспорт

	НомерОтправления = НомерЗаказа + ПостфиксНовогоОтправления()
		+ Формат(КоличествоОтправленийПоЗаказу + 1, "ЧГ=");
	
	Возврат НомерОтправления;

КонецФункции

// Выделяет из номера отправления порядковый номер.
// Если отправление без ПостфиксНовогоОтправления(), возвращается 0.
//
// Параметры:
//   НомерОтправления - Строка - номер отправления.
//
// Возвращаемое значение:
//   Число - порядковый номер отправления.
//
Функция ПорядковыйНомерНовогоОтправления(Знач НомерОтправления) Экспорт

	Постфикс = ПостфиксНовогоОтправления();
	
	Позиция = СтрНайти(НомерОтправления, Постфикс);
	Если Позиция = 0 Тогда
		ПорядковыйНомер = 0;
	Иначе
		НомерОтправления = Сред(НомерОтправления, Позиция);
		НомерОтправления = СтрЗаменить(НомерОтправления, Постфикс, "");
		
		ПорядковыйНомер = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(НомерОтправления);
	КонецЕсли;
	
	Возврат ПорядковыйНомер;

КонецФункции

// Возвращает ключи записи регистра заказов торговых площадок.
//
// Возвращаемое значение:
//   Строка - строка, содержащая поля РегистрСведений.ЗаказыТорговыхПлощадок.
//
Функция КлючиДанныхДляЗаписиЗаказаТорговойПлощадки() Экспорт

	СтруктураПоМенеджеруЗаписи = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
		Новый Соответствие,
		Метаданные.РегистрыСведений.ЗаказыТорговыхПлощадок);
	
	КлючиДанныхДляЗаписи = ОбщегоНазначенияКлиентСервер.КлючиСтруктурыВСтроку(СтруктураПоМенеджеруЗаписи, ",");
	
	КлючиДанныхДляЗаписи = КлючиДанныхДляЗаписи + ",ДатаДокументаОтгрузки,НомерДокументаОтгрузки";
	
	Возврат КлючиДанныхДляЗаписи;

КонецФункции

// Конструктор списка атрибутов информации об отгрузке.
//
// Возвращаемое значение:
//   Структура - список атрибутов:
//     * Собрать                    - Булево - признак доступности сборки.
//     * СкачатьЭтикетку            - Булево - признак доступности скачивания этикетки.
//     * Отменить                   - Булево - признак доступности отмены.
//     * ОткрытьСпор                - Булево - признак доступности открытия спора.
//     * ВернутьВОтгрузку           - Булево - признак доступности возврата в отгрузку.
//
Функция НовыйСписокДоступныхДействийСЗаказом() Экспорт

	СписокДоступныхДействий = Новый Структура;
	СписокДоступныхДействий.Вставить("Собрать",                    Ложь);
	СписокДоступныхДействий.Вставить("СкачатьЭтикетку",            Ложь);
	СписокДоступныхДействий.Вставить("Отменить",                   Ложь);
	СписокДоступныхДействий.Вставить("ОткрытьСпор",                Ложь);
	СписокДоступныхДействий.Вставить("ВернутьВОтгрузку",           Ложь);
	
	Возврат СписокДоступныхДействий;

КонецФункции

// Обновляет сведения по заказам и отправлениям и возвращает список складов торговых площадок.
//
// Параметры:
//   ВидМаркетплейса        - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   УчетнаяЗапись          - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   ЗаполнитьСписокСкладов - Булево - признак необходимости заполнения списка складов торговой площадки.
//
// Возвращаемое значение:
//   Структура - ответ из ключей:
//     * ТаблицаСкладов - см. ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаСкладов.
//     * Ошибка         - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ОбновитьСведенияПоЗаказамИОтправлениям(ВидМаркетплейса, УчетнаяЗапись, ЗаполнитьСписокСкладов = Истина) Экспорт

	// Получение списка складов.
	Если ЗаполнитьСписокСкладов Тогда
		Результат =
			ИнтеграцияСМаркетплейсамиСервер.ПолучитьСкладыСервиса(ВидМаркетплейса, УчетнаяЗапись, , Истина);
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("ТаблицаСкладов", ИнтеграцияСМаркетплейсамиСервер.НоваяТаблицаСкладов());
		Результат.Вставить("Ошибка",         ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка());
	КонецЕсли;
	
	// Заполнение сведений по заказам и отправлениям.
	ТекстОшибки = ЗаполнитьСведенияПоЗаказамИОтправлениям(ВидМаркетплейса, УчетнаяЗапись);
	Если Не ПустаяСтрока(ТекстОшибки) Тогда
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = ТекстОшибки;
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(Результат.Ошибка, Ошибка);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Актуализирует состав записей регистра РегистрыСведений.ЗаказыТорговыхПлощадок.
//
// Параметры:
//   Заказ                      - ДокументСсылка.ЗаказКлиента - заказ для обновления сведений об отправлениях.
//   ДанныеПоНомерамОтправлений - Соответствие из КлючИЗначение - список номеров отправлений и документов отгрузки,
//                                   где ключ - номер отправления, значение - структура из состава полей
//                                   регистра РегистрыСведений.ЗаказыТорговыхПлощадок.
//
// Возвращаемое значение:
//   Строка - текст ошибки.
//
Функция ЗаписатьСведенияОбОтправлениях(Заказ, ДанныеПоНомерамОтправлений) Экспорт

	ТекстОшибки = "";
	
	Если Не ЗначениеЗаполнено(ДанныеПоНомерамОтправлений) Тогда
		Возврат ТекстОшибки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных =
			БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
		ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", Заказ);
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Заказ.Установить(Заказ);
		НаборЗаписей.Прочитать();
		
		ЗаписиКУдалению = Новый Массив;
		
		Для Каждого Запись Из НаборЗаписей Цикл
			ДанныеПоНомеруОтправления = ДанныеПоНомерамОтправлений[Запись.НомерОтправления];
			Если ДанныеПоНомеруОтправления = Неопределено Тогда
				ЗаписиКУдалению.Добавить(Запись);
			Иначе
				Запись.ДокументОтгрузки              = ДанныеПоНомеруОтправления.ДокументОтгрузки;
				Запись.НомерРодительскогоОтправления = ДанныеПоНомеруОтправления.НомерРодительскогоОтправления;
				ДанныеПоНомерамОтправлений.Удалить(Запись.НомерОтправления);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого Запись Из ЗаписиКУдалению Цикл
			НаборЗаписей.Удалить(Запись);
		КонецЦикла;
		
		Для Каждого ДанныеПоНомеруОтправления Из ДанныеПоНомерамОтправлений Цикл
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ДанныеПоНомеруОтправления.Значение);
			
			Запись.НомерОтправления     = ДанныеПоНомеруОтправления.Ключ;
			Запись.Статус               = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
			Запись.ДатаУстановкиСтатуса = ТекущаяДатаСеанса();
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось записать данные по номерам отправлений для заказа %1 по причине: %2.';
				|en = 'Cannot save the data on the shipment numbers for the %1 order. Reason: %2.'"),
			Заказ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТекстОшибки;

КонецФункции

// Устанавливает статус заказа.
//
// Параметры:
//   Заказ                        - ДокументСсылка.ЗаказКлиента - заказ, для которого изменяется статус.
//   ДокументОтгрузки             - ДокументСсылка.РеализацияТоваровУслуг, ДокументСсылка.ПередачаТоваровХранителю - документ отгрузки.
//   Статус                       - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - устанавливаемый статус.
//   ИмяСобытияЖурналаРегистрации - Строка - см. ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации().
//
// Возвращаемое значение:
//   Булево - признак успешного выполнения.
//
Функция УстановитьСтатус(Заказ, ДокументОтгрузки, Статус, ИмяСобытияЖурналаРегистрации = "") Экспорт
	
	Если ПустаяСтрока(ИмяСобытияЖурналаРегистрации) Тогда
		ИмяСобытияЖурналаРегистрации = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	КонецЕсли;
	
	ВыполненоУспешно = Истина;
	
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
		ЭлементБлокировкиДанных.УстановитьЗначение("Заказ",            Заказ);
		ЭлементБлокировкиДанных.УстановитьЗначение("ДокументОтгрузки", ДокументОтгрузки);
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Заказ.Установить(Заказ);
		НаборЗаписей.Отбор.ДокументОтгрузки.Установить(ДокументОтгрузки);
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей[0].Статус                     = Статус;
			НаборЗаписей[0].ДатаУстановкиСтатуса       = ТекущаяДатаСеанса();
			НаборЗаписей[0].ТребуетсяПолучениеЭтикеток = (НаборЗаписей[0].Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
			
			НаборЗаписей.Записать(Истина);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВыполненоУспешно = Ложь;
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При установке статуса заказа <%1> возникла ошибка: %2.';
				|en = 'An error occurred while setting the <%1> order status: %2.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Заказ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
	КонецПопытки;
	
	Возврат ВыполненоУспешно;
	
КонецФункции

// Устанавливает статус отправления заказа.
//
// Параметры:
//   Заказ            - ДокументСсылка.ЗаказКлиента - заказ.
//   НомерОтправления - Строка - номер отправления по данным учетной системы.
//   Статус           - ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - устанавливаемый статус.
//   ИмяСобытия       - см. ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации.
//
// Возвращаемое значение:
//   Строка - текст ошибки.
//
Функция УстановитьСтатусОтправления(Заказ, НомерОтправления, Статус, ИмяСобытия = "") Экспорт

	ТекстОшибки = "";
	НачатьТранзакцию();
	
	Попытка
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных =
		БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
		ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", Заказ);
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Заказ.Установить(Заказ);
		НаборЗаписей.Прочитать();
		
		Для Каждого Запись Из НаборЗаписей Цикл
			Если Запись.НомерОтправления = НомерОтправления Тогда
				Запись.Статус                     = Статус;
				Запись.ДатаУстановкиСтатуса       = ТекущаяДатаСеанса();
				Запись.ТребуетсяПолучениеЭтикеток = (НаборЗаписей[0].Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
			КонецЕсли;
		КонецЦикла;
		
		НаборЗаписей.Записать();
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось изменить статус заказа <%1> по отправлению <%2> по причине: %3.';
				|en = 'Cannot change the <%1> order status for the <%2> shipment. Reason: %3.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			Заказ,
			НомерОтправления,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			
		ЗаписьЖурналаРегистрации(ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
	КонецПопытки;
	
	Возврат ТекстОшибки;

КонецФункции

// Заменяет номер отправления для заказа после подтверждения сборки выделенных товаров.
//
// Параметры:
//   Заказ                 - ДокументСсылка.ЗаказКлиента - заказ.
//   НомерОтправления      - Строка - номер отправления по данным учетной системы.
//   НовыйНомерОтправления - Строка - номер отправления по данным торговой площадки.
//   Статус                - Неопределено, ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - устанавливаемый статус.
//   ИмяСобытия            - см. ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации.
//
// Возвращаемое значение:
//   Строка - текст ошибки.
//
Функция ЗаменитьНомерОтправления(Заказ, НомерОтправления, НовыйНомерОтправления, Статус = Неопределено, ИмяСобытия = "") Экспорт
	
	ПараметрыЗамены = ИнтеграцияСМаркетплейсамиСервер.НовыеПараметрыЗаменыНомеровОтправленийПоЗаказу();
	
	ПараметрыЗамены.ИмяРегистра = Метаданные.РегистрыСведений.ЗаказыТорговыхПлощадок.ПолноеИмя();
	ПараметрыЗамены.ИмяСобытияЖурналаРегистрации = ИмяСобытия;
	ПараметрыЗамены.Заказ = Заказ;
	
	ДанныеЗаписиДляЗамены = ИнтеграцияСМаркетплейсамиСервер.НовыеДанныеЗаписиДляЗаменыНомераОтправленияПоЗаказу();
	ДанныеЗаписиДляЗамены.НомерОтправления = НовыйНомерОтправления;
	ДанныеЗаписиДляЗамены.Статус = Статус;
	ДанныеЗаписиДляЗамены.ДатаУстановкиСтатуса = ТекущаяДатаСеанса();
	ДанныеЗаписиДляЗамены.ТребуетсяПолучениеЭтикеток =
		(Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
	
	ПараметрыЗамены.ДанныеДляЗамены.Вставить(НомерОтправления, ДанныеЗаписиДляЗамены);
	
	ТекстОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаменитьНомераОтправленийПоЗаказу(ПараметрыЗамены);
	
	Возврат ТекстОшибки;

КонецФункции

// Определяет данные регистра сведений, соответствующие условиям поиска.
//
// Параметры:
//   Заказ            - ДокументСсылка.ЗаказКлиента, Неопределено - заказ.
//   ДокументОтгрузки - ДокументСсылка.РеализацияТоваровУслуг, ДокументСсылка.ПередачаТоваровХранителю, Неопределено - документ отгрузки.
//
// Возвращаемое значение:
//   Неопределено - при пустых значениях параметров;
//   Структура - данные регистра сведений, соответствующие условиям поиска.
//
Функция ПолучитьДанныеЗаказа(Заказ = Неопределено, ДокументОтгрузки = Неопределено) Экспорт
	
	Результат = Неопределено;
	
	Если Заказ = Неопределено
			И ДокументОтгрузки = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	*
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО ЗаказыТорговыхПлощадок.Заказ = ЗаказКлиента.Ссылка
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.Заказ = &Заказ
		|	И ЗаказыТорговыхПлощадок.ДокументОтгрузки = &ДокументОтгрузки";
	
	Запрос.УстановитьПараметр("Заказ",            Заказ);
	Запрос.УстановитьПараметр("ДокументОтгрузки", ДокументОтгрузки);
	
	Если Заказ = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗаказыТорговыхПлощадок.Заказ = &Заказ", "ИСТИНА");
	КонецЕсли;
	Если ДокументОтгрузки = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ЗаказыТорговыхПлощадок.ДокументОтгрузки = &ДокументОтгрузки", "ИСТИНА");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДанных    = РезультатЗапроса.Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ВыборкаДанных.Следующий() Тогда
		Результат = Новый Структура;
		Для Каждого ЭлементКоллекции Из РезультатЗапроса.Колонки Цикл
			Если ЭлементКоллекции.ТипЗначения = Новый ОписаниеТипов("РезультатЗапроса") Тогда
				Продолжить;
			КонецЕсли;
			
			Результат.Вставить(ЭлементКоллекции.Имя, ВыборкаДанных[ЭлементКоллекции.Имя]);
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает список недоступных к отмене заказов и их номеров отправлений.
//
// Параметры:
//   Заказы              - Массив из ДокументСсылка.ЗаказКлиента - заказ.
//   НомераОтправлений   - Массив из Строка - номер отправления.
//   РезультатЗаполнения - см. РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам.
//
// Возвращаемое значение:
//   Структура - результат проверки с ключами:
//     * ИнформацияДляПользователя     - Строка - описание недоступных к отмене заказов и отправлений.
//     * НедоступныеКОтменеОтправления - Соответствие из ДокументСсылка.ЗаказКлиента - список недоступных к отмене заказов в ключе
//                                         и их номеров отправлений в значении.
//
Функция ПроверитьВозможностьОтменыТоваровОтправления(Заказы, НомераОтправлений, РезультатЗаполнения) Экспорт

	Результат = Новый Структура;
	Результат.Вставить("ИнформацияДляПользователя",     "");
	Результат.Вставить("НедоступныеКОтменеОтправления", Новый Соответствие);
	
	СписокСообщений = Новый Массив;
	
	ШаблонСообщения = НСтр("ru = 'Для заказа ""%1"" уже отменены отправления: %2.';
							|en = 'For the ""%1"" order, shipments are already canceled: %2.'");
	
	РезультатПроверкиЗаказов = РезультатЗаполнения.РезультатПроверкиЗаказов;
	
	Если РезультатПроверкиЗаказов = Неопределено Или РезультатПроверкиЗаказов.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из РезультатПроверкиЗаказов Цикл
		Заказ = КлючИЗначение.Ключ;
		РезультатПроверкиЗаказа = КлючИЗначение.Значение;
		
		Если Не ЗначениеЗаполнено(Заказ) Тогда
			Продолжить;
		КонецЕсли;
		
		ОтправленияОтмененные = Новый Массив;
		
		Для Каждого НомерОтправления Из РезультатПроверкиЗаказа.ОтправленияОтмененные Цикл
			Индекс = НомераОтправлений.Найти(НомерОтправления);
			Если Индекс <> Неопределено Тогда
				ОтправленияОтмененные.Добавить(НомерОтправления);
				НомераОтправлений.Удалить(Индекс);
			КонецЕсли;
		КонецЦикла;
		
		Если ОтправленияОтмененные.Количество() > 0 Тогда
			СписокСообщений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										ШаблонСообщения,
										Заказ,
										СтрСоединить(ОтправленияОтмененные, ", ")));
		КонецЕсли;
	КонецЦикла;
	
	СведенияПоТоварам = РезультатЗаполнения.СведенияПоТоварам;
	
	Если СведенияПоТоварам = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СведенияПоТоварам.Заказ КАК Заказ,
		|	СведенияПоТоварам.НомерОтправления КАК НомерОтправления,
		|	СведенияПоТоварам.Номенклатура КАК Номенклатура,
		|	СведенияПоТоварам.Характеристика КАК Характеристика,
		|	СведенияПоТоварам.Склад КАК Склад,
		|	СведенияПоТоварам.ИдентификаторСтрокиЗаполнен КАК ОформленДокументОтгрузки,
		|	СведенияПоТоварам.КОтгрузке КАК КОтгрузке,
		|	СведенияПоТоварам.Отменено КАК Отменено,
		|	СведенияПоТоварам.ДокументОтгрузкиКОформлению КАК ДокументОтгрузкиКОформлению,
		|	СведенияПоТоварам.Количество КАК Количество
		|ПОМЕСТИТЬ СведенияПоТоварам
		|ИЗ
		|	&СведенияПоТоварам КАК СведенияПоТоварам
		|ГДЕ
		|	СведенияПоТоварам.Заказ В(&Заказы)
		|	И СведенияПоТоварам.НомерОтправления В(&НомераОтправлений)
		|	И СведенияПоТоварам.КОтгрузке
		|	И НЕ СведенияПоТоварам.Отменено
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СведенияПоТоварам.Заказ КАК Заказ,
		|	СведенияПоТоварам.НомерОтправления КАК НомерОтправления
		|ПОМЕСТИТЬ ОтгруженныеОтправления
		|ИЗ
		|	СведенияПоТоварам КАК СведенияПоТоварам
		|ГДЕ
		|	СведенияПоТоварам.ОформленДокументОтгрузки
		|	И НЕ СведенияПоТоварам.ДокументОтгрузкиКОформлению
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОтгруженныеОтправления.Заказ КАК Заказ,
		|	ОтгруженныеОтправления.НомерОтправления КАК НомерОтправления
		|ИЗ
		|	ОтгруженныеОтправления КАК ОтгруженныеОтправления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияПоТоварам.Заказ КАК Заказ,
		|	СведенияПоТоварам.НомерОтправления КАК НомерОтправления,
		|	СведенияПоТоварам.Номенклатура КАК Номенклатура,
		|	СведенияПоТоварам.Характеристика КАК Характеристика,
		|	СведенияПоТоварам.Склад КАК Склад,
		|	СУММА(ВЫБОР
		|			КОГДА СведенияПоТоварам.ДокументОтгрузкиКОформлению
		|				ТОГДА СведенияПоТоварам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Количество
		|ИЗ
		|	СведенияПоТоварам КАК СведенияПоТоварам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОтгруженныеОтправления КАК ОтгруженныеОтправления
		|		ПО СведенияПоТоварам.Заказ = ОтгруженныеОтправления.Заказ
		|			И СведенияПоТоварам.НомерОтправления = ОтгруженныеОтправления.НомерОтправления
		|ГДЕ
		|	ОтгруженныеОтправления.НомерОтправления ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияПоТоварам.Заказ,
		|	СведенияПоТоварам.НомерОтправления,
		|	СведенияПоТоварам.Номенклатура,
		|	СведенияПоТоварам.Характеристика,
		|	СведенияПоТоварам.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА СведенияПоТоварам.ДокументОтгрузкиКОформлению
		|				ТОГДА СведенияПоТоварам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) > 0
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СведенияПоТоварам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОтгруженныеОтправления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки КАК Заказ,
		|	ТоварыКОтгрузкеОстатки.Номенклатура КАК Номенклатура,
		|	ТоварыКОтгрузкеОстатки.Характеристика КАК Характеристика,
		|	ТоварыКОтгрузкеОстатки.Склад КАК Склад,
		|	СУММА(ТоварыКОтгрузкеОстатки.ВРезервеОстаток) КАК Зарезервировано,
		|	0 КАК ЗарезервированоОбработано,
		|	СУММА(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - (ТоварыКОтгрузкеОстатки.КСборкеОстаток + ТоварыКОтгрузкеОстатки.СобираетсяОстаток + ТоварыКОтгрузкеОстатки.СобраноОстаток)) КАК КОтгрузкеНаСкладе,
		|	0 КАК КОтгрузкеНаСкладеОбработано,
		|	ЛОЖЬ КАК Обработана
		|ИЗ
		|	РегистрНакопления.ТоварыКОтгрузке.Остатки(&ТекущаяДата, ДокументОтгрузки В (&Заказы)) КАК ТоварыКОтгрузкеОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыКОтгрузкеОстатки.ДокументОтгрузки,
		|	ТоварыКОтгрузкеОстатки.Номенклатура,
		|	ТоварыКОтгрузкеОстатки.Характеристика,
		|	ТоварыКОтгрузкеОстатки.Склад
		|
		|ИМЕЮЩИЕ
		|	СУММА(ТоварыКОтгрузкеОстатки.ВРезервеОстаток) + СУММА(ТоварыКОтгрузкеОстатки.КОтгрузкеОстаток - (ТоварыКОтгрузкеОстатки.КСборкеОстаток + ТоварыКОтгрузкеОстатки.СобираетсяОстаток + ТоварыКОтгрузкеОстатки.СобраноОстаток)) > 0";
	
	Запрос.УстановитьПараметр("ТекущаяДата",       ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("Заказы",            Заказы);
	Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
	Запрос.УстановитьПараметр("СведенияПоТоварам", СведенияПоТоварам);
	
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаОтгруженныхОтправлений = ПакетЗапроса[ПакетЗапроса.ВГраница() - 4].Выбрать();
	Пока ВыборкаОтгруженныхОтправлений.Следующий() Цикл
		НомераОтправленийЗаказа = Результат.НедоступныеКОтменеОтправления[ВыборкаОтгруженныхОтправлений.Заказ];
		Если НомераОтправленийЗаказа = Неопределено Тогда
			НомераОтправленийЗаказа = Новый Массив;
		КонецЕсли;
		НомераОтправленийЗаказа.Добавить(ВыборкаОтгруженныхОтправлений.НомерОтправления);
		Результат.НедоступныеКОтменеОтправления.Вставить(ВыборкаОтгруженныхОтправлений.Заказ, НомераОтправленийЗаказа);
		
		Индекс = НомераОтправлений.Найти(ВыборкаОтгруженныхОтправлений.НомерОтправления);
		Если Индекс <> Неопределено Тогда
			НомераОтправлений.Удалить(Индекс);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаДанныхПоЗаказам = ПакетЗапроса[ПакетЗапроса.ВГраница()].Выгрузить();
	
	ВыборкаНеотгруженныхОтправлений = ПакетЗапроса[ПакетЗапроса.ВГраница() - 3].Выбрать();
	Пока ВыборкаНеотгруженныхОтправлений.Следующий() Цикл
		НомераОтправленийЗаказа = Результат.НедоступныеКОтменеОтправления[ВыборкаНеотгруженныхОтправлений.Заказ];
		Если НомераОтправленийЗаказа = Неопределено Тогда
			НомераОтправленийЗаказа = Новый Массив;
		КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Заказ",          ВыборкаНеотгруженныхОтправлений.Заказ);
		Отбор.Вставить("Номенклатура",   ВыборкаНеотгруженныхОтправлений.Номенклатура);
		Отбор.Вставить("Характеристика", ВыборкаНеотгруженныхОтправлений.Характеристика);
		Отбор.Вставить("Склад",          ВыборкаНеотгруженныхОтправлений.Склад);
		Отбор.Вставить("Обработана",     Ложь);
		
		СтрокиЗаказа = ТаблицаДанныхПоЗаказам.НайтиСтроки(Отбор);
		Если СтрокиЗаказа.Количество() = 0 Тогда
			НомераОтправленийЗаказа.Добавить(ВыборкаНеотгруженныхОтправлений.НомерОтправления);
			
			Индекс = НомераОтправлений.Найти(ВыборкаНеотгруженныхОтправлений.НомерОтправления);
			Если Индекс <> Неопределено Тогда
				НомераОтправлений.Удалить(Индекс);
			КонецЕсли;
		Иначе
			СтрокаЗаказа = СтрокиЗаказа[0];
			
			Если ВыборкаНеотгруженныхОтправлений.Количество <= (СтрокаЗаказа.Зарезервировано - СтрокаЗаказа.ЗарезервированоОбработано) Тогда
				СтрокаЗаказа.ЗарезервированоОбработано   = СтрокаЗаказа.ЗарезервированоОбработано
															+ ВыборкаНеотгруженныхОтправлений.Количество;
			ИначеЕсли ВыборкаНеотгруженныхОтправлений.Количество <= (СтрокаЗаказа.КОтгрузкеНаСкладе -СтрокаЗаказа.КОтгрузкеНаСкладеОбработано) Тогда
				СтрокаЗаказа.КОтгрузкеНаСкладеОбработано = СтрокаЗаказа.КОтгрузкеНаСкладеОбработано
															+ ВыборкаНеотгруженныхОтправлений.Количество;
			Иначе
				НомераОтправленийЗаказа.Добавить(ВыборкаНеотгруженныхОтправлений.НомерОтправления);
				
				Индекс = НомераОтправлений.Найти(ВыборкаНеотгруженныхОтправлений.НомерОтправления);
				Если Индекс <> Неопределено Тогда
					НомераОтправлений.Удалить(Индекс);
				КонецЕсли;
			КонецЕсли;
			
			СтрокаЗаказа.Обработана = (СтрокаЗаказа.Зарезервировано = СтрокаЗаказа.ЗарезервированоОбработано
										И СтрокаЗаказа.КОтгрузкеНаСкладе = СтрокаЗаказа.КОтгрузкеНаСкладеОбработано);
		КонецЕсли;
		
		Если НомераОтправленийЗаказа.Количество() > 0 Тогда
			Результат.НедоступныеКОтменеОтправления.Вставить(ВыборкаНеотгруженныхОтправлений.Заказ, НомераОтправленийЗаказа);
		КонецЕсли;
	КонецЦикла;
	
	ШаблонСообщения1 = НСтр("ru = 'Для заказа ""%1"" недоступна отмена отправления %2, т.к. оно собрано на складе или отгружено. Для отмены исключите товары указанного отправления из складских документов и документов отгрузки.';
							|en = 'Cannot cancel the %2 shipment of the %1 order, as the shipment is already picked in a warehouse or shipped. To cancel, exclude the goods of the specified shipment from the warehouse documents and shipping documents.'");
	ШаблонСообщения2 = НСтр("ru = 'Для заказа ""%1"" недоступна отмена отправлений %2, т.к. они собраны на складе или отгружены. Для отмены исключите товары указанных отправлений из складских документов и документов отгрузки.';
							|en = 'Cannot cancel the %2 shipments of the %1 order, as they are already picked in a warehouse or shipped. To cancel them, exclude the goods of the specified shipments from the warehouse documents and shipping documents.'");
	
	Для Каждого ДанныеЗаказа Из Результат.НедоступныеКОтменеОтправления Цикл
		Если ДанныеЗаказа.Значение.Количество() = 1 Тогда
			Шаблон = ШаблонСообщения1;
		Иначе
			Шаблон = ШаблонСообщения2;
		КонецЕсли;
		
		СписокСообщений.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										Шаблон,
										ДанныеЗаказа.Ключ,
										СтрСоединить(ДанныеЗаказа.Значение, ", ")));
	КонецЦикла;
	
	Результат.ИнформацияДляПользователя = СтрСоединить(СписокСообщений, Символы.ПС);
	
	Возврат Результат;

КонецФункции

// Возвращает список заказов по номерам отправлений.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НомераОтправлений - Массив из Строка - номер отправления.
//
// Возвращаемое значение:
//   Массив из ДокументСсылка.ЗаказКлиента - заказы.
//
Функция ЗаказыОтправлений(УчетнаяЗапись, НомераОтправлений) Экспорт

	Заказы = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.НомерОтправления В(&НомераОтправлений)";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",     УчетнаяЗапись);
	Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Истина);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Заказы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Заказ");
	КонецЕсли;
	
	Возврат Заказы;

КонецФункции

// Проверяет заказ и возвращает сведения по отправлениям.
//
// Параметры:
//   Заказы                              - Массив из ДокументСсылка.ЗаказКлиента - проверяемый заказ.
//   РезультатПроверкиЗаказов            - Соответствие из см. ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа -
//                                       - Неопределено - результат проверки заказов. 
//   ЗаполнитьДанныеПоНомерамОтправлений - Булево - признак заполнения данных по отправлениям.
//   ЗаполнитьСведенияОбЭкземплярах      - Булево - признак заполнения данных по экземплярам.
//
// Возвращаемое значение:
//   Соответствие из см. ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа.
//
Функция ПроверитьЗаказы(Заказы, РезультатПроверкиЗаказов = Неопределено,
			ЗаполнитьДанныеПоНомерамОтправлений = Ложь, ЗаполнитьСведенияОбЭкземплярах = Ложь) Экспорт

	Если РезультатПроверкиЗаказов = Неопределено Тогда
		РезультатПроверкиЗаказов = Новый Соответствие;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УдалитьВсеВхожденияЗначенияИзМассива(Заказы, Документы.ЗаказКлиента.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПроверкиЗаказов(ЗаполнитьСведенияОбЭкземплярах);
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Запрос.УстановитьПараметр("СтатусыКонечные", Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные());
	Запрос.УстановитьПараметр("СтатусыВДоставке", Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовВДоставке(Ложь));
	
	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаОтправленийСНезаполненнымиДанными = ПакетЗапроса[ПакетЗапроса.ВГраница() - 1].Выбрать();
	Пока ВыборкаОтправленийСНезаполненнымиДанными.Следующий() Цикл
		РезультатПроверкиЗаказа = ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа(
			ВыборкаОтправленийСНезаполненнымиДанными.Заказ,
			РезультатПроверкиЗаказов);
		РезультатПроверкиЗаказа.ОтправленияСНезаполненнымиДанными.Добавить(ВыборкаОтправленийСНезаполненнымиДанными.НомерОтправления);
	КонецЦикла;
	
	ВыборкаОтправленийИзмененных = ПакетЗапроса[ПакетЗапроса.ВГраница() - 3].Выбрать();
	Пока ВыборкаОтправленийИзмененных.Следующий() Цикл
		РезультатПроверкиЗаказа = ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа(
			ВыборкаОтправленийИзмененных.Заказ,
			РезультатПроверкиЗаказов);
		РезультатПроверкиЗаказа.ОтправленияИзмененные.Добавить(ВыборкаОтправленийИзмененных.НомерОтправления);
		
		Если ЗначениеЗаполнено(ВыборкаОтправленийИзмененных.ДокументОтгрузки) Тогда
			РезультатПроверкиЗаказа.ИзмененныеДокументыОтгрузки.Вставить(ВыборкаОтправленийИзмененных.ДокументОтгрузки,
				ВыборкаОтправленийИзмененных.НомерОтправления);
		КонецЕсли;
	КонецЦикла;
	
	Если ЗаполнитьДанныеПоНомерамОтправлений Тогда
		КлючиДанныхДляЗаписи = РегистрыСведений.ЗаказыТорговыхПлощадок.КлючиДанныхДляЗаписиЗаказаТорговойПлощадки();
	КонецЕсли;
	
	ВыборкаЗаказов = ПакетЗапроса[ПакетЗапроса.ВГраница() - 5].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаЗаказов.Следующий() Цикл
		РезультатПроверкиЗаказа = ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа(ВыборкаЗаказов.Заказ, РезультатПроверкиЗаказов);
		РезультатПроверкиЗаказа.ХозяйственнаяОперация = ВыборкаЗаказов.ХозяйственнаяОперация;
		РезультатПроверкиЗаказа.УчетнаяЗапись         = ВыборкаЗаказов.УчетнаяЗапись;
		РезультатПроверкиЗаказа.ВидМаркетплейса       = ВыборкаЗаказов.ВидМаркетплейса;
		
		ВыборкаОтправлений = ВыборкаЗаказов.Выбрать();
		Пока ВыборкаОтправлений.Следующий() Цикл
			РезультатПроверкиЗаказа.МаксимальныйИдентификаторЭкземпляра =
				Макс(РезультатПроверкиЗаказа.МаксимальныйИдентификаторЭкземпляра,
					ВыборкаОтправлений.МаксимальныйИдентификаторЭкземпляра);
			
			Если ВыборкаОтправлений.ВДоставке Тогда
				РезультатПроверкиЗаказа.ОтправленияВДоставке.Добавить(
					ВыборкаОтправлений.НомерОтправления);
			КонецЕсли;
			
			Если ВыборкаОтправлений.ЭтоКонечныйСтатус Тогда
				РезультатПроверкиЗаказа.ОтправленияВКонечномСтатусе.Добавить(
					ВыборкаОтправлений.НомерОтправления);
			КонецЕсли;
			
			Если ВыборкаОтправлений.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки Тогда
				РезультатПроверкиЗаказа.ОтправленияВСборке.Добавить(ВыборкаОтправлений.НомерОтправления);
				
				Если ВыборкаОтправлений.КоличествоЗаписей = 0 Тогда
					РезультатПроверкиЗаказа.ОтправленияБезТоваров.Добавить(ВыборкаОтправлений.НомерОтправления);
				ИначеЕсли ВыборкаОтправлений.КоличествоПеренесенныхПозиций > 0 Тогда
					РезультатПроверкиЗаказа.ОтправленияИзмененные.Добавить(ВыборкаОтправлений.НомерОтправления);
				КонецЕсли;
			Иначе
				РезультатПроверкиЗаказа.ОтправленияНеВСборке.Добавить(ВыборкаОтправлений.НомерОтправления);
				
				Если ВыборкаОтправлений.Статус = Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен Тогда
					РезультатПроверкиЗаказа.ОтправленияОтмененные.Добавить(ВыборкаОтправлений.НомерОтправления);
				КонецЕсли;
			КонецЕсли;
				
			Если ВыборкаОтправлений.Статус <> Перечисления.СтатусыЗаказовТорговыхПлощадок.Отменен
				И Не ЗначениеЗаполнено(ВыборкаОтправлений.ДокументОтгрузки) Тогда
				РезультатПроверкиЗаказа.ОтправленияБезДокументовОтгрузки.Добавить(ВыборкаОтправлений.НомерОтправления);
			КонецЕсли;
			
			Если ЗаполнитьДанныеПоНомерамОтправлений Тогда
				ДанныеОтправления = Новый Структура(КлючиДанныхДляЗаписи);
				ЗаполнитьЗначенияСвойств(ДанныеОтправления, ВыборкаОтправлений);
				РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений.Вставить(ВыборкаОтправлений.НомерОтправления, ДанныеОтправления);
			КонецЕсли;
		КонецЦикла;
		
		РезультатПроверкиЗаказа.ОтправленияИзмененные =
			ОбщегоНазначенияКлиентСервер.СвернутьМассив(РезультатПроверкиЗаказа.ОтправленияИзмененные);
		
		ПроверитьГотовностьОтправленийКПодтверждениюСборки(РезультатПроверкиЗаказа);
	КонецЦикла;
	
	Если ЗаполнитьСведенияОбЭкземплярах Тогда
		ВыборкаЗаказов = ПакетЗапроса[ПакетЗапроса.ВГраница() - 8].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаЗаказов.Следующий() Цикл
			РезультатПроверкиЗаказа = ИнтеграцияСМаркетплейсамиКлиентСервер.РезультатПроверкиЗаказа(
				ВыборкаЗаказов.Заказ,
				РезультатПроверкиЗаказов);
			
			ВыборкаЭкземпляров = ВыборкаЗаказов.Выбрать();
			Пока ВыборкаЭкземпляров.Следующий() Цикл
				РезультатПроверкиЗаказа.ЭкземплярыБезДокументовОтгрузки.Добавить(ВыборкаЭкземпляров.ИдентификаторЭкземпляра);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Возврат РезультатПроверкиЗаказов;

КонецФункции

// Выполняет отмену строк заказа и возвращает результат.
//
// Параметры:
//   УчетнаяЗапись     - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   Заказ             - ДокументСсылка.ЗаказКлиента - заказ для отмены строк.
//   ДокументОтгрузки - ДокументСсылка.РеализацияТоваровУслуг, ДокументСсылка.ПередачаТоваровХранителю, Неопределено -
//                                  документ отгрузки.
//   ПричинаОтмены     - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - 
//                     - Структура - причина отмены строк заказа:
//     * Идентификатор - Число - идентификатор причины отмены отправления;
//     * Наименование  - Строка - наименование причины отмены;
//     * Ссылка        - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены 1С.
//   ОтменяемыеПозиции - Неопределено - для отмены всех строк заказа;
//                     - Массив из СтрокаТабличнойЧасти - строка табличной части;
//                     - Массив из СтрокаТабличнойЧасти - строка табличной части;
//                     - Массив из СтрокаГруппировкиДинамическогоСписка - группировка динамического списка;
//                     - Массив из РегистрСведенийКлючЗаписи.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок -
//                                  набор ключей записей, по которым требуется отмена строк заказа.
//   ИмяСобытия        - Строка - имя события журнала регистрации.
//   СообщитьОшибку    - Булево - признак оповещения пользователя сообщением.
// Возвращаемое значение:
//   Булево - результат отмены.
//
Функция ОтменитьЗаказ(Заказ, ДокументОтгрузки, ПричинаОтмены, ОтменяемыеПозиции = Неопределено,
			ИмяСобытия = "", СообщитьОшибку = Истина) Экспорт

	ОтменаВыполнена = Ложь;
	
	Если ПустаяСтрока(ИмяСобытия) Тогда
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	КонецЕсли;
	
	ЗаказОбъект = Заказ.ПолучитьОбъект();
	
	ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ЗаказОбъект, ИмяСобытия);
	Если Не ПустаяСтрока(ОписаниеОшибки) И СообщитьОшибку Тогда
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ОтменаВыполнена;
	КонецЕсли;
	
	Товары = ЗаказОбъект.Товары;
	
	ЗаписатьЗаказ         = Ложь;
	ПропущенныеСтроки     = Новый Массив;
	ПересчитываемыеСтроки = Новый Массив;
	
	ИзменяемыеДокументыОтгрузки = Новый Соответствие;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Если ОтменяемыеПозиции = Неопределено Тогда
			Результат = ОтменитьСтрокиЗаказа(Товары, ПричинаОтмены, Неопределено);
			
			ЗаписатьЗаказ = Результат.ЗаписатьЗаказ;
			ПропущенныеСтроки = Результат.ПропущенныеСтроки;
			ПересчитываемыеСтроки = Результат.ПересчитываемыеСтроки;
		ИначеЕсли ТипЗнч(ОтменяемыеПозиции) = Тип("Массив") Тогда
			// Блокировка изменения сведений по товарам.
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных =
				БлокировкаДанных.Добавить("РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок");
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Разделяемый; // Без записи набора регистра
			ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", Заказ);
			БлокировкаДанных.Заблокировать();
			
			Для Каждого ОтменяемаяПозиция Из ОтменяемыеПозиции Цикл
				Если ТипЗнч(ОтменяемаяПозиция) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
					НаборЗаписей = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Заказ.Установить(Заказ);
					НаборЗаписей.Прочитать();
					
					Для Каждого Запись Из НаборЗаписей Цикл
						Если Запись[ОтменяемаяПозиция.ИмяГруппировки] = ОтменяемаяПозиция.Ключ Тогда
							Результат = ОтменитьСтрокиЗаказаДляЭкземпляра(
								Товары,
								Запись,
								ПричинаОтмены,
								ЗаказОбъект.МаксимальныйКодСтроки);
							
							ЗаписатьЗаказ = Макс(ЗаписатьЗаказ, Результат.ЗаписатьЗаказ);
							ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПропущенныеСтроки,     Результат.ПропущенныеСтроки);
							ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПересчитываемыеСтроки, Результат.ПересчитываемыеСтроки);
							
							ЗаполнитьИзменяемыеСтрокиДокументовОтгрузки(ИзменяемыеДокументыОтгрузки, Результат);
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли ТипЗнч(ОтменяемаяПозиция) = Тип("РегистрСведенийКлючЗаписи.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок") Тогда
					Результат = ОтменитьСтрокиЗаказаДляЭкземпляра(
						Товары,
						ОтменяемаяПозиция,
						ПричинаОтмены,
						ЗаказОбъект.МаксимальныйКодСтроки);
					
					ЗаписатьЗаказ = Макс(ЗаписатьЗаказ, Результат.ЗаписатьЗаказ);
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПропущенныеСтроки,     Результат.ПропущенныеСтроки);
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПересчитываемыеСтроки, Результат.ПересчитываемыеСтроки);
					
					ЗаполнитьИзменяемыеСтрокиДокументовОтгрузки(ИзменяемыеДокументыОтгрузки, Результат);
				Иначе
					Отбор = Новый Структура;
					Отбор.Вставить("КодСтроки", ОтменяемаяПозиция.КодСтроки);
					
					Результат = ОтменитьСтрокиЗаказа(
						Товары,
						ПричинаОтмены,
						Отбор,
						ОтменяемаяПозиция.Количество,
						ОтменяемаяПозиция.Упаковка,
						ЗаказОбъект.МаксимальныйКодСтроки);
					
					ЗаписатьЗаказ = Макс(ЗаписатьЗаказ, Результат.ЗаписатьЗаказ);
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПропущенныеСтроки,     Результат.ПропущенныеСтроки);
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПересчитываемыеСтроки, Результат.ПересчитываемыеСтроки);
					
					Результат.ДокументОтгрузки = ДокументОтгрузки;
					ЗаполнитьИзменяемыеСтрокиДокументовОтгрузки(ИзменяемыеДокументыОтгрузки, Результат);
				КонецЕсли;
			КонецЦикла;
		ИначеЕсли ТипЗнч(ОтменяемыеПозиции) = Тип("ВыборкаИзРезультатаЗапроса") Тогда
			Пока ОтменяемыеПозиции.Следующий() Цикл
				Отбор = Новый Структура;
				Отбор.Вставить("КодСтроки", ОтменяемыеПозиции.КодСтроки);
				
				Результат = ОтменитьСтрокиЗаказа(
					Товары,
					ПричинаОтмены,
					Отбор,
					ОтменяемыеПозиции.КоличествоУпаковок,
					ОтменяемыеПозиции.Упаковка,
					ЗаказОбъект.МаксимальныйКодСтроки);
				
				ЗаписатьЗаказ = Макс(ЗаписатьЗаказ, Результат.ЗаписатьЗаказ);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПропущенныеСтроки,     Результат.ПропущенныеСтроки);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПересчитываемыеСтроки, Результат.ПересчитываемыеСтроки);
				
				Результат.ДокументОтгрузки = ДокументОтгрузки;
				ЗаполнитьИзменяемыеСтрокиДокументовОтгрузки(ИзменяемыеДокументыОтгрузки, Результат);
			КонецЦикла;
		КонецЕсли;
		
		Если ЗаписатьЗаказ Тогда
			Если ЗначениеЗаполнено(ПересчитываемыеСтроки) Тогда
				СтруктураПересчетаСуммы =
					ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВСтрокеТЧ(ЗаказОбъект);
				
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьКоличествоЕдиниц");
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
				СтруктураДействий.Вставить("ПересчитатьСумму");
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Истина));
				СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
				
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(ПересчитываемыеСтроки,
					СтруктураДействий,
					Товары,
					Неопределено);
			КонецЕсли;
			
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(
				ЗаказОбъект,
				ЗаказОбъект.Проведен,
				ИмяСобытия,
				Не ЗаказОбъект.Проведен);
			
			Если Не Результат.ЗаписьВыполнена И СообщитьОшибку Тогда
				ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
		
		ОтменаВыполнена = Истина;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		Если ОтменяемыеПозиции = Неопределено Тогда
			ШаблонСообщения =
				НСтр("ru = 'Не удалось отменить заказ %1 по причине: %2. Повторите действие.';
					|en = 'Cannot cancel the %1 order. Reason: %2. Please try again.'",
					ОбщегоНазначения.КодОсновногоЯзыка());
		Иначе
			ШаблонСообщения =
				НСтр("ru = 'Не удалось отменить выделенные позиции в заказе %1 по причине: %2. Повторите действие.';
					|en = 'Cannot cancel the selected lines in the %1 order. Reason: %2. Please try again.'",
					ОбщегоНазначения.КодОсновногоЯзыка());
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			Заказ,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(ИмяСобытия,
			УровеньЖурналаРегистрации.Ошибка,
			,,
			ТекстСообщения);
		
		Если СообщитьОшибку Тогда
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецПопытки;
	
	Если ПропущенныеСтроки.Количество() > 0 И СообщитьОшибку Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Пропущены отмененные ранее строки заказа: %1.';
				|en = 'The earlier canceled order lines are skipped: %1.'"),
			СтрСоединить(ПропущенныеСтроки, ", "));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	ИзменитьКодыСтрокВДокументахОтгрузкиИСведенияхПоТоварам(
		Заказ,
		ИзменяемыеДокументыОтгрузки,
		ИмяСобытия,
		СообщитьОшибку);
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОтменаВыполнена;

КонецФункции

// Выполняет закрытие заказа.
//
// Параметры:
//   Заказ             - ДокументСсылка.ЗаказКлиента, ДокументОбъект.ЗаказКлиента - заказ для отмены строк.
//   ИмяСобытия        - Строка - имя события журнала регистрации.
//
// Возвращаемое значение:
//   Булево - результат закрытия.
//
Функция ЗакрытьЗаказ(Заказ, ИмяСобытия = "") Экспорт

	ЗаказЗакрыт = Ложь;
	
	Если ПустаяСтрока(ИмяСобытия) Тогда
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	КонецЕсли;
	
	Если Заказ = Неопределено Тогда
		Возврат ЗаказЗакрыт;
	ИначеЕсли ОбщегоНазначения.ЭтоСсылка(ТипЗнч(Заказ)) Тогда
		Если Заказ.Пустая() Тогда
			Возврат ЗаказЗакрыт;
		КонецЕсли;
		
		ЗаказОбъект = Заказ.ПолучитьОбъект();
		ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ЗаказОбъект, ИмяСобытия);
	Иначе
		ЗаказОбъект = Заказ;
		ОписаниеОшибки = "";
	КонецЕсли;
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		СтруктураДополнительныхПараметров = Новый Структура;
		СтруктураДополнительныхПараметров.Вставить("ЗакрыватьЗаказы", Истина);
		СтруктураДополнительныхПараметров.Вставить("ИмяТабличнойЧасти", "Товары");
		СтруктураДополнительныхПараметров.Вставить("ОтменитьНеотработанныеСтроки", Истина);
		СтруктураДополнительныхПараметров.Вставить("ПричинаОтмены", Неопределено);
		СтруктураДополнительныхПараметров.Вставить("ПроверятьОстатки", Ложь);
		СтруктураДополнительныхПараметров.Вставить("СкорректироватьМерныеТовары", Ложь);
		СтруктураДополнительныхПараметров.Вставить("СкорректироватьМерныеТоварыПоПриемке", Ложь);
		СтруктураДополнительныхПараметров.Вставить("СкорректироватьЦены", Ложь);
		
		Если ЗаказОбъект.УстановитьСтатус("Закрыт", СтруктураДополнительныхПараметров) Тогда
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(
				ЗаказОбъект,
				ЗаказОбъект.Проведен,
				ИмяСобытия);
			
			ЗаказЗакрыт = Результат.ЗаписьВыполнена;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ЗаказЗакрыт;

КонецФункции

// Возвращает номера заказов на торговой площадке по переданным заказам клиента.
// 
// Параметры:
//  Заказы - Массив из ДокументСсылка.ЗаказКлиента - заказы, по которым нужно получить номера.
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//   * Ключ - ДокументСсылка.ЗаказКлиента - заказ клиента.
//   * Значение - Строка - номер заказа на торговой площадке.
//
Функция НомераЗаказовНаТорговойПлощадке(Заказы) Экспорт
	
	Результат = Новый Соответствие;
	
	Выборка = ВыборкаДанныхПоНомерамЗаказовНаТорговойПлощадке(Заказы);
	
	Пока Выборка.СледующийПоЗначениюПоля("Заказ") Цикл
		Результат.Вставить(Выборка.Заказ, Выборка.НомерЗаказа);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Отгрузки

// Конструктор списка атрибутов информации об отгрузке.
//
// Параметры:
//   УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//
// Возвращаемое значение:
//   Структура - список атрибутов:
//     * УчетнаяЗапись                   - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//     * ВидМаркетплейса                 - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//     * ИдентификаторОтгрузки           - Строка - идентификатор отгрузки.
//     * ПорядковыйНомерОтгрузки         - Число - порядковый номер отгрузки.
//     * ИдентификаторСклада             - Строка - идентификатор склада.
//     * ИдентификаторМетодаДоставки     - Строка - идентификатор метода доставки.
//     * НомерОтгрузки                   - Строка - номер отгрузки.
//     * ШтрихкодОтгрузки                - Строка - штрихкод отгрузки.
//     * ТипОтгрузки                     - Строка - тип отгрузки.
//     * ДатаВыполнения                  - Дата - дата и время фактического выполнения отгрузки.
//     * ДатаСоздания                    - Дата - дата и время создания отгрузки.
//     * ДатаОбновления                  - Дата - дата и время обновления отгрузки.
//     * КоличествоОбновлений            - Число - количество обновлений отгрузки.
//     * Статус                          - Строка - статус отгрузки.
//     * ЧастичнаяПеревозка              - Булево - признак частичной перевозки.
//     * НомерЧастичнойПеревозки         - Число - номер частичной перевозки.
//     * ТипДокументов                   - Строка - тип документов.
//     * ДоступныеДействия               - Массив из Строка - список доступных действий с отгрузкой.
//     * ДоступныеПечатныеФормы          - Массив из Строка - список доступных печатных форм для отгрузки.
//     * ТребуетсяПропуск                - Булево - признак обязательного оформления пропуска для отгрузки.
//     * МожноРедактировать              - Булево - признак доступности редактирования отгрузки.
//     * МожноОтменить                   - Булево - признак доступности отмены отгрузки.
//     * ПричинаНедоступностиОтмены      - Строка - описание причины недоступности отмены отгрузки.
//     * КоличествоГрузовыхМест          - Число - количество грузовых мест.
//     * РаспечатаныЭтикеткиГрузовыхМест - Булево - признак того, что этикетки грузовых мест уже печатались.
//     * ЕстьОтправленияКОтгрузке        - Булево - принак наличия доступных, но не включенных в отгрузку отправлений.
//     * ОтгрузкаСформирована            - Булево - признак того, что формирование отгрузки завершено, в т.ч. в ЛК площадки.
//     * ОтгрузкаАктуальна               - Булево - признак того, что отгрузка формируется или, если сформирована, актуальна.
//     * ОтгрузкаОтменена                - Булево - признак того, что сформированная отгрузка отменена.
//     * ПовторитьЗапрос                 - Булево - признак необходимости повторить запрос информации об отгрузке.
//     * СрокОтгрузки                    - Дата - срок отгрузки по настройкам в личном кабинете площадки.
//     * СмещениеЧасовогоПояса           - Число - смещение часового пояса.
//     * ВремяОтгрузкиРекомендуемое      - Дата - срок отгрузки со скидкой.
//     * ОтправленияВОтгрузке            - Массив из Строка - список отправлений в отгрузке.
//     * ОтправленияВПланеОтгрузки       - Массив из Строка - список отправлений в формируемой отгрузке.
//     * ОтправленияНеВОтгрузке          - Массив из Строка - список отправлений, исключенных из отгрузки.
//     * ОтправленияНезагруженные        - Массив из Строка - список отправлений отгрузки, незагруженных в учетную систему.
//     * ОтправленияОтмененные           - Массив из Строка - список отправлений отгрузки, отмененных покупателем или продавцом.
//     * ОтправленияПривязанныеКОтгрузке - Массив из Строка - список отправлений отгрузки, непривязанных к идентификатору отгрузки.
//     * КоличествоОтправленийВОтгрузке  - Число - количество отправлений в отгрузке.
//     * КоличествоОтправленийВСборке    - Число - количество отправлений, которые нужно собрать.
//     * КоличествоСобранныхОтправлений  - Число - количество отправлений, которые уже собраны.
//     * СписокОтгрузок                  - Массив из см. НовыйСписокАтрибутовИнформацииОбОтгрузке - список других отгрузок.
//     * Пропуска                        - Массив из Строка - список пропусков, оформленных для отгрузки.
//     * МассаБрутто                     - Число - масса брутто груза отгрузки.
//     * МассаНетто                      - Число - масса нетто груза отгрузки.
//     * ТипТранспортногоСредства        - СправочникСсылка.ТипыТранспортныхСредств - тип транспортного средства для отгрузки.
//     * СпособОтгрузки                  - Строка - способ отгрузки (СЦ, пункт приема и т.п.).
//     * АдресПунктаПриема               - Строка - адрес пункта приема.
//     * ВремяПриемаНачало               - Строка - начало таймслота в точке отгрузки.
//     * ВремяПриемаОкончание            - Строка - Окончание таймслота в точке отгрузки.
//     * Ошибка                          - см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись) Экспорт

	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		МожноРедактировать = Истина;
	Иначе
		МожноРедактировать = Ложь;
	КонецЕсли;
	
	АтрибутыОтвета = Новый Структура;
	
	АтрибутыОтвета.Вставить("УчетнаяЗапись",                   УчетнаяЗапись);
	АтрибутыОтвета.Вставить("ВидМаркетплейса",                 ВидМаркетплейса);
	АтрибутыОтвета.Вставить("ИдентификаторОтгрузки",           "");
	АтрибутыОтвета.Вставить("ПорядковыйНомерОтгрузки",         0);
	АтрибутыОтвета.Вставить("ИдентификаторСклада",             "");
	АтрибутыОтвета.Вставить("ИдентификаторМетодаДоставки",     "");
	АтрибутыОтвета.Вставить("НомерОтгрузки",                   "");
	АтрибутыОтвета.Вставить("ШтрихкодОтгрузки",                "");
	АтрибутыОтвета.Вставить("ТипОтгрузки",                     "");
	АтрибутыОтвета.Вставить("ДатаВыполнения",                  Дата(1,1,1));
	АтрибутыОтвета.Вставить("ДатаСоздания",                    Дата(1,1,1));
	АтрибутыОтвета.Вставить("ДатаОбновления",                  Дата(1,1,1));
	АтрибутыОтвета.Вставить("КоличествоОбновлений",            0);
	АтрибутыОтвета.Вставить("Статус",                          "");
	АтрибутыОтвета.Вставить("ЧастичнаяПеревозка",              Ложь);
	АтрибутыОтвета.Вставить("НомерЧастичнойПеревозки",         0);
	АтрибутыОтвета.Вставить("ТипДокументов",                   "");
	АтрибутыОтвета.Вставить("ДоступныеДействия",               Новый Массив);
	АтрибутыОтвета.Вставить("ДоступныеПечатныеФормы",          Новый Массив);
	АтрибутыОтвета.Вставить("ТребуетсяПропуск",                Ложь);
	АтрибутыОтвета.Вставить("МожноРедактировать",              МожноРедактировать);
	АтрибутыОтвета.Вставить("МожноОтменить",                   Ложь);
	АтрибутыОтвета.Вставить("ПричинаНедоступностиОтмены",      "");
	АтрибутыОтвета.Вставить("КоличествоГрузовыхМест",          0);
	АтрибутыОтвета.Вставить("РаспечатаныЭтикеткиГрузовыхМест", Ложь);
	АтрибутыОтвета.Вставить("ЕстьОтправленияКОтгрузке",        Ложь);
	АтрибутыОтвета.Вставить("ОтгрузкаСформирована",            Ложь);
	АтрибутыОтвета.Вставить("ОтгрузкаАктуальна",               Ложь);
	АтрибутыОтвета.Вставить("ОтгрузкаОтменена",                Ложь);
	АтрибутыОтвета.Вставить("ПовторитьЗапрос",                 Ложь);
	АтрибутыОтвета.Вставить("СрокОтгрузки",                    Дата(1,1,1));
	АтрибутыОтвета.Вставить("СмещениеЧасовогоПояса",           0);
	АтрибутыОтвета.Вставить("ВремяОтгрузкиРекомендуемое",      Дата(1,1,1));
	АтрибутыОтвета.Вставить("ОтправленияВОтгрузке",            Новый Массив);
	АтрибутыОтвета.Вставить("ОтправленияВПланеОтгрузки",       Новый Массив);
	АтрибутыОтвета.Вставить("ОтправленияНеВОтгрузке",          Новый Массив);
	АтрибутыОтвета.Вставить("ОтправленияНезагруженные",        Новый Массив);
	АтрибутыОтвета.Вставить("ОтправленияОтмененные",           Новый Массив);
	АтрибутыОтвета.Вставить("ОтправленияПривязанныеКОтгрузке", Новый Массив);
	АтрибутыОтвета.Вставить("КоличествоОтправленийВОтгрузке",  0);
	АтрибутыОтвета.Вставить("КоличествоОтправленийВСборке",    0);
	АтрибутыОтвета.Вставить("КоличествоСобранныхОтправлений",  0);
	АтрибутыОтвета.Вставить("СписокОтгрузок",                  Новый Массив);
	АтрибутыОтвета.Вставить("Пропуска",                        Новый Массив);
	АтрибутыОтвета.Вставить("МассаБрутто",                     0);
	АтрибутыОтвета.Вставить("МассаНетто",                      0);
	АтрибутыОтвета.Вставить("ТипТранспортногоСредства",        Справочники.ТипыТранспортныхСредств.ПустаяСсылка());
	АтрибутыОтвета.Вставить("СпособОтгрузки",                  "");
	АтрибутыОтвета.Вставить("АдресПунктаПриема",               "");
	АтрибутыОтвета.Вставить("ВремяПриемаНачало",               "");
	АтрибутыОтвета.Вставить("ВремяПриемаОкончание",            "");
	АтрибутыОтвета.Вставить("Ошибка",                          ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка());
	
	Возврат АтрибутыОтвета;

КонецФункции

// Формирует отгрузку на торговой площадке и возвращает информацию о ней.
//
// Параметры:
//   УчетнаяЗапись               - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   МетодДоставки               - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//   ДатаОтгрузки                - Дата - дата отгрузки.
//   КоличествоГрузовыхМест      - Число - количество грузовых мест при доверительной приемке.
//
// Возвращаемое значение:
//   см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Функция СформироватьОтгрузку(УчетнаяЗапись, МетодДоставки, ДатаОтгрузки, КоличествоГрузовыхМест = 0) Экспорт

	ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ИнформацияОбОтгрузке.Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаНеУказанаУчетнаяЗапись;
		ИнформацияОбОтгрузке.Ошибка.ОписаниеОшибки = НСтр("ru = 'Не указана учетная запись торговой площадки.';
															|en = 'The marketplace account is not specified.'");
		Возврат ИнформацияОбОтгрузке;
	КонецЕсли;
	
	Если Не Справочники.УчетныеЗаписиМаркетплейсов.ОбновленияДанныхТорговойПлощадкиРазрешено(УчетнаяЗапись) Тогда
		ИнформацияОбОтгрузке.Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаОбновлениеДанныхЗапрещено;
		ИнформацияОбОтгрузке.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для учетной записи <%1> запрещено обновление данных торговой площадки';
				|en = 'The trading platform data update is not allowed for the <%1> account'"),
			УчетнаяЗапись);
		Возврат ИнформацияОбОтгрузке;
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИнформацияОбОтгрузке = ИнтеграцияСМаркетплейсомOzonСервер.СформироватьОтгрузку(
			УчетнаяЗапись,
			МетодДоставки.ИдентификаторМетодаДоставки,
			ДатаОтгрузки,
			КоличествоГрузовыхМест);
	КонецЕсли;
	
	ВыполнитьДополнительныеДействияСОтгрузкой(ИнформацияОбОтгрузке, МетодДоставки, Истина);
	
	Возврат ИнформацияОбОтгрузке;

КонецФункции

// Получает информацию об отгрузке.
//
// Параметры:
//   УчетнаяЗапись            - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   МетодДоставки            - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//   ДатаОтгрузки             - Дата - дата отгрузки.
//   ИдентификаторОтгрузки    - Строка - идентификатор отгрузки.
//   ОбработатьОтгрузку       - Булево - признак выполнения дополнительных действий с отгрузкой.
//   ЗаполнитьСвязьСОтгрузкой - Булево - признак связки отправлений с отгрузкой.
//
// Возвращаемое значение:
//   см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Функция ПолучитьИнформациюОбОтгрузке(УчетнаяЗапись, МетодДоставки, ДатаОтгрузки,
			ИдентификаторОтгрузки = "", ОбработатьОтгрузку = Истина, ЗаполнитьСвязьСОтгрузкой = Ложь) Экспорт

	ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ИнформацияОбОтгрузке = ИнтеграцияСМаркетплейсомOzonСервер.ИнформацияОбОтгрузке(
			УчетнаяЗапись,
			МетодДоставки.ИдентификаторМетодаДоставки,
			ДатаОтгрузки,
			ИдентификаторОтгрузки);
	КонецЕсли;
	
	Если ОбработатьОтгрузку Тогда
		ВыполнитьДополнительныеДействияСОтгрузкой(ИнформацияОбОтгрузке, МетодДоставки, ЗаполнитьСвязьСОтгрузкой);
	КонецЕсли;
	
	Возврат ИнформацияОбОтгрузке;

КонецФункции

// Получает информацию об отгрузке после подстверждения.
//
// Параметры:
//   УчетнаяЗапись          - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   МетодДоставки          - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//   ИдентификаторОтгрузки  - Строка - идентификатор отгрузки.
//   ОтправленияВОтгрузке   - Массив из Строка - список отправлений в отгрузке.
//   КоличествоГрузовыхМест - Число - количество грузовых мест при доверительной приемке.
//
// Возвращаемое значение:
//   см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Функция ПодтвердитьОтгрузку(УчетнаяЗапись, МетодДоставки, ИдентификаторОтгрузки,
			ОтправленияВОтгрузке = Неопределено, КоличествоГрузовыхМест = 0) Экспорт

	ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Результат = ИнтеграцияСМаркетплейсомOzonСервер.ИзменитьСоставОтгрузки(
			УчетнаяЗапись,
			ИдентификаторОтгрузки,
			ОтправленияВОтгрузке);
		
		Если ПустаяСтрока(Результат) Тогда
			Результат = ИнтеграцияСМаркетплейсомOzonСервер.ПодтвердитьОтгрузку(
				УчетнаяЗапись,
				ИдентификаторОтгрузки,
				КоличествоГрузовыхМест);
		КонецЕсли;
	Иначе
		Результат = "";
	КонецЕсли;
	
	ИнформацияОбОтгрузке = ПолучитьИнформациюОбОтгрузке(
		УчетнаяЗапись,
		МетодДоставки,
		Дата(1,1,1),
		ИдентификаторОтгрузки,
		,
		Истина);
	
	Если Не ПустаяСтрока(Результат) Тогда
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = Результат;
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);
	КонецЕсли;
	
	Возврат ИнформацияОбОтгрузке;

КонецФункции

// Получает информацию об отгрузке после отмены.
//
// Параметры:
//   УчетнаяЗапись         - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись подключения к сервису.
//   МетодДоставки         - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//   ИдентификаторОтгрузки - Строка - идентификатор отгрузки.
//
// Возвращаемое значение:
//   см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Функция ОтменитьОтгрузку(УчетнаяЗапись, МетодДоставки, ИдентификаторОтгрузки) Экспорт

	ИнформацияОбОтгрузке = НовыйСписокАтрибутовИнформацииОбОтгрузке(УчетнаяЗапись);
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Результат = ИнтеграцияСМаркетплейсомOzonСервер.ОтменитьОтгрузку(УчетнаяЗапись, ИдентификаторОтгрузки);
	Иначе
		Результат = Новый Структура;
		Результат.Вставить("Статус",         "");
		Результат.Вставить("ОписаниеОшибки", "");
	КонецЕсли;
	
	ИнформацияОбОтгрузке = ПолучитьИнформациюОбОтгрузке(
		УчетнаяЗапись,
		МетодДоставки,
		Дата(1,1,1),
		ИдентификаторОтгрузки);
	
	Если ИнформацияОбОтгрузке.ОтгрузкаОтменена Тогда
		ВыполнитьДополнительныеДействияСОтгрузкой(ИнформацияОбОтгрузке, МетодДоставки, Истина);
	ИначеЕсли Не ПустаяСтрока(Результат.ОписаниеОшибки) Тогда
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = Результат.ОписаниеОшибки;
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);
	КонецЕсли;
	
	Возврат ИнформацияОбОтгрузке;

КонецФункции

// Проверяет данные по отправлениям отгрузки в учетной системе и дополняет описание ошибки для отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Процедура ВыполнитьПроверкуОтправленийВОтгрузке(ИнформацияОбОтгрузке) Экспорт

	Если ПустаяСтрока(ИнформацияОбОтгрузке.Ошибка.ОписаниеОшибки) Тогда
		Ошибка = ПроверитьОтправления(ИнформацияОбОтгрузке);
		
		Если Не ПустаяСтрока(Ошибка.КодОшибки) И Не ИнформацияОбОтгрузке.ОтгрузкаОтменена Тогда
			ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Формирует задание на перевозку.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   НастройкиСклада      - Неопределено - не передаются настройки склада;
//                          см. ИнтеграцияСМаркетплейсамиСервер.НовыйНастройкиСкладаFBS.
//
// Возвращаемое значение:
//   ДокументСсылка.ЗаданиеНаПеревозку - ссылка на сформированный документ.
//
Функция СформироватьЗаданиеНаПеревозку(ИнформацияОбОтгрузке, НастройкиСклада = Неопределено) Экспорт

	ЗаданиеНаПеревозку = Документы.ЗаданиеНаПеревозку.ПустаяСсылка();
	ПредставлениеДокумента = Метаданные.Документы.ЗаданиеНаПеревозку.Представление();
	
	Если Не ЗначениеЗаполнено(ИнформацияОбОтгрузке.УчетнаяЗапись) Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заполнение документа ""%1"" невозможно, т.к. неопределена учетная запись.';
				|en = 'Cannot fill the ""%1"" document as the account is undefined.'"),
			ПредставлениеДокумента);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ЗаданиеНаПеревозку;
	КонецЕсли;
	
	НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(ИнформацияОбОтгрузке.УчетнаяЗапись, Истина);
	
	ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ИнформацияОбОтгрузке.ВидМаркетплейса);
	
	ВыборкаПеревозок = ВыборкаДокументовПеревозки(НастройкиУчетнойЗаписи.Организация, ИнформацияОбОтгрузке.ИдентификаторОтгрузки, Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ВыборкаПеревозок.Следующий() И ЗначениеЗаполнено(ВыборкаПеревозок.Ссылка) Тогда
		ЗаданиеНаПеревозку = ВыборкаПеревозок.Ссылка;
		
		Если ВыборкаПеревозок.Проведен
				И ВыборкаПеревозок.Статус <> Перечисления.СтатусыЗаданийНаПеревозку.Формируется Тогда
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Невозможно перезаполнить документ ""%1"" в статусе ""%2"".';
					|en = 'Cannot refill the ""%1"" document with the ""%2"" status.'"),
				ПредставлениеДокумента,
				ВыборкаПеревозок.Статус);
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
			Возврат ЗаданиеНаПеревозку;
		КонецЕсли;
		
		ДокументОбъект = ВыборкаПеревозок.Ссылка.ПолучитьОбъект();
		ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
		
		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
			Возврат ЗаданиеНаПеревозку;
		КонецЕсли;
		
		Если ВыборкаПеревозок.Проведен Тогда
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОбъект, Ложь, ИмяСобытия);
			Если Не Результат.ЗаписьВыполнена Тогда
				ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
				Возврат ЗаданиеНаПеревозку;
			КонецЕсли;
		Иначе
			ДокументОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
	Иначе
		ДокументОбъект = Документы.ЗаданиеНаПеревозку.СоздатьДокумент();
	КонецЕсли;
	
	ВыборкаСкладов = ВыборкаДанныхПоДоставке(ИнформацияОбОтгрузке.УчетнаяЗапись, Истина,, ИнформацияОбОтгрузке.ОтправленияВОтгрузке,);
	КоличествоСкладовОтгрузки = ?(ВыборкаСкладов = Неопределено, 0, ВыборкаСкладов.Количество());
	
	Если КоличествоСкладовОтгрузки = 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По отправлениям отгрузки нет товаров, доступных для оформления документа ""%1"".';
				|en = 'For the shipments, there are no goods available for registering the ""%1"" document.'"),
			ПредставлениеДокумента);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ЗаданиеНаПеревозку;
	КонецЕсли;
	
	ДокументОбъект.Заполнить(Неопределено);
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	ДокументОбъект.Операция = Перечисления.ВидыДоставки.СоСклада;
	ДокументОбъект.Статус = Перечисления.СтатусыЗаданийНаПеревозку.Формируется;
	ДокументОбъект.ТранспортноеСредство = ИнформацияОбОтгрузке.ТипТранспортногоСредства;
	
	Если КоличествоСкладовОтгрузки > 1 И НастройкиСклада <> Неопределено Тогда
		ДокументОбъект.Склад = НастройкиСклада.Склад;
		ЗаполнитьСклад = Не ЗначениеЗаполнено(ДокументОбъект.Склад);
	Иначе
		ЗаполнитьСклад = Истина
	КонецЕсли;
	
	Если НастройкиСклада <> Неопределено Тогда
		ДокументОбъект.ЗаданиеВыполняет = ТипИсполнителяЗаданияНаПеревозку(НастройкиСклада.СпособДоставки);
		ДокументОбъект.ДатаВремяРейсаПланС = ИнформацияОбОтгрузке.ДатаВыполнения + (НастройкиСклада.ВремяДоставкиС - Дата(1,1,1));
		ДокументОбъект.ДатаВремяРейсаПланПо =  ИнформацияОбОтгрузке.ДатаВыполнения + (НастройкиСклада.ВремяДоставкиПо - Дата(1,1,1));
		ДокументОбъект.Перевозчик = НастройкиСклада.ПеревозчикПартнер;
		ПартнерыИКонтрагенты.ЗаполнитьКонтрагентаПартнераПоУмолчанию(ДокументОбъект.Перевозчик, ДокументОбъект.Контрагент);
		
		Если ЗначениеЗаполнено(ДокументОбъект.Перевозчик) Тогда
			Если ТипЗнч(ДокументОбъект.Перевозчик) = Тип("СправочникСсылка.Контрагенты") Тогда
				ДокументОбъект.БанковскийСчетПеревозчика =
					Справочники.БанковскиеСчетаКонтрагентов.ПолучитьБанковскийСчетПоУмолчанию(ДокументОбъект.Перевозчик);
			Иначе
				СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
				СтруктураПараметров.Организация = ДокументОбъект.Перевозчик;
				ДокументОбъект.БанковскийСчетПеревозчика = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ДокументОбъект.ДополнительнаяИнформация = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Отгрузка по отправлениям торговой площадки ""%1"".';
			|en = 'Shipment by shipments of the ""%1"" marketplace.'"),
		ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ИнформацияОбОтгрузке.ВидМаркетплейса));
	
	ИсключаемыеТипыДляКолонок = Новый Соответствие;
	ИсключаемыеТипыДляКолонок.Вставить("ПолучательОтправитель", Тип("Строка"));
	
	ТоварыКДоставке = ИнтеграцияСМаркетплейсамиСервер.ПустаяТаблицаОбъектаМетаданных(
		"РегистрСведений.ТоварыКДоставке",
		Истина,
		ИсключаемыеТипыДляКолонок);
	
	ТоварыКИсключениюИзДоставки = ТоварыКДоставке.Скопировать();
	
	КэшМаршрутов = Новый Соответствие;
	
	Ключи = Новый Массив;
	Для Каждого Колонка Из Метаданные.Документы.ЗаданиеНаПеревозку.ТабличныеЧасти.Маршрут.Реквизиты Цикл
		Ключи.Добавить(Колонка.Имя);
	КонецЦикла;
	Ключи = СтрСоединить(Ключи, ",");
	
	Пока ВыборкаСкладов.Следующий() Цикл
		Если ЗаполнитьСклад Тогда
			ДокументОбъект.Склад = ВыборкаСкладов.Склад;
			ЗаполнитьСклад = Не ЗначениеЗаполнено(ДокументОбъект.Склад);
		КонецЕсли;
		
		ВыборкаРаспоряжений = ВыборкаСкладов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРаспоряжений.Следующий() Цикл
			Если Не ВыборкаРаспоряжений.ВсеТоварыКДоставке И ВыборкаРаспоряжений.КоличествоДляДоставки <= 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если ВыборкаРаспоряжений.ВсеТоварыКДоставке Тогда
				СтрокаТаблицы = ТоварыКДоставке.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаРаспоряжений);
				СтрокаТаблицы.ВсеТовары = Истина;
			КонецЕсли;
			
			ВыборкаТоваровКДоставке = ВыборкаРаспоряжений.Выбрать();
			Пока ВыборкаТоваровКДоставке.Следующий() Цикл
				ДанныеМаршрута = Новый Структура(Ключи);
				ЗаполнитьЗначенияСвойств(ДанныеМаршрута, ВыборкаТоваровКДоставке);
				
				ХешСуммаДанныеМаршрута = ОбщегоНазначения.КонтрольнаяСуммаСтрокой(ДанныеМаршрута, ХешФункция.CRC32);
				
				Идентификатор = КэшМаршрутов.Получить(ХешСуммаДанныеМаршрута);
				Если Идентификатор = Неопределено Тогда
					Идентификатор = Строка(Новый УникальныйИдентификатор());
					КэшМаршрутов.Вставить(ХешСуммаДанныеМаршрута, Идентификатор);
					
					СтрокаТаблицы = ДокументОбъект.Маршрут.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТоваровКДоставке);
					СтрокаТаблицы.КлючСвязи = Идентификатор;
				КонецЕсли;
				
				Если ДокументОбъект.Распоряжения.Найти(ВыборкаТоваровКДоставке.Распоряжение, "Распоряжение") = Неопределено Тогда
					СтрокаТаблицы = ДокументОбъект.Распоряжения.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТоваровКДоставке);
					СтрокаТаблицы.ДоставляетсяПолностью = ВыборкаРаспоряжений.ВсеТоварыКДоставке;
					СтрокаТаблицы.КлючСвязи = Идентификатор;
				КонецЕсли;
				
				Если ВыборкаРаспоряжений.ВсеТоварыКДоставке Тогда
					Прервать;
				Иначе
					// К доставке.
					СтрокаТаблицы = ТоварыКДоставке.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТоваровКДоставке);
					СтрокаТаблицы.ВсеТовары = Ложь;
					СтрокаТаблицы.Количество = ВыборкаТоваровКДоставке.КоличествоДляДоставки;
					
					// Исключить из доставки.
					Если ВыборкаТоваровКДоставке.КоличествоИсключитьИзДоставкиПоОтправлениям > 0 Тогда
						СтрокаТаблицы = ТоварыКИсключениюИзДоставки.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТоваровКДоставке);
						СтрокаТаблицы.ВсеТовары = Ложь;
						СтрокаТаблицы.Количество = ВыборкаТоваровКДоставке.КоличествоИсключитьИзДоставкиПоОтправлениям;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если ТоварыКДоставке.Количество() >0 Тогда
		ДокументОбъект.ДополнительныеСвойства.Вставить("ТоварыКДоставке", ТоварыКДоставке);
	КонецЕсли;
	
	Если ДокументОбъект.Распоряжения.Количество() = 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'По отправлениям отгрузки нет товаров, доступных для оформления документа ""%1"".';
				|en = 'For the shipments, there are no goods available for registering the ""%1"" document.'"),
			ПредставлениеДокумента);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ЗаданиеНаПеревозку;
	КонецЕсли;
	
	УчетнаяЗаписьПредставление =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			ИнформацияОбОтгрузке.ВидМаркетплейса,
			ИнформацияОбОтгрузке.УчетнаяЗапись);
	
	ДокументОбъект.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Сформировано для отгрузки %2';
			|en = '%1. Generated for the %2 shipment'"),
		УчетнаяЗаписьПредставление,
		ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
	
	НовыйДокумент = ДокументОбъект.ЭтоНовый();
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОбъект, Истина, ИмяСобытия);
	
	ДокументОбъект.Разблокировать();
	
	Если Не Результат.ЗаписьВыполнена Тогда
		ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
	Иначе
		ЗаписатьИзключенияИзДоставки(ТоварыКИсключениюИзДоставки, ИнформацияОбОтгрузке);
	КонецЕсли;
	
	ЗаданиеНаПеревозку = ДокументОбъект.Ссылка;
	
	// Добавить запись в историю работы пользователя.
	Если НовыйДокумент Тогда
		ИсторияРаботыПользователя.Добавить(ЗаданиеНаПеревозку);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЗаданиеНаПеревозку;

КонецФункции

// Формирует транспортную накладную.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   НастройкиСклада      - Неопределено - не передаются настройки склада;
//                          см. ИнтеграцияСМаркетплейсамиСервер.НовыйНастройкиСкладаFBS.
//   ЗаданиеНаПеревозку   - Неопределено, ДокументСсылка.ЗаданиеНаПеревозку - задание на перевозку, для которого оформляется ТТН.
//
// Возвращаемое значение:
//   ДокументСсылка.ТранспортнаяНакладная - ссылка на сформированный документ.
//
Функция СформироватьТранспортнуюНакладную(ИнформацияОбОтгрузке, НастройкиСклада = Неопределено, ЗаданиеНаПеревозку = Неопределено) Экспорт

	ТранспортнаяНакладная = Документы.ТранспортнаяНакладная.ПустаяСсылка();
	ПредставлениеДокумента = Метаданные.Документы.ТранспортнаяНакладная.Представление();
	
	Если Не ЗначениеЗаполнено(ИнформацияОбОтгрузке.УчетнаяЗапись) Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заполнение документа ""%1"" невозможно, т.к. неопределена учетная запись.';
				|en = 'Cannot fill the ""%1"" document as the account is undefined.'"),
			ПредставлениеДокумента);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ТранспортнаяНакладная;
	КонецЕсли;
	
	НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(ИнформацияОбОтгрузке.УчетнаяЗапись, Истина);
	
	ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ИнформацияОбОтгрузке.ВидМаркетплейса);
	
	ВыборкаТТН = ВыборкаДокументовПеревозки(НастройкиУчетнойЗаписи.Организация, ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
	
	Если ВыборкаТТН.Следующий() Тогда
		ТранспортнаяНакладная = ВыборкаТТН.Ссылка;
	КонецЕсли;
	
	ВыборкаИтогов = ВыборкаДокументовОтгрузки(ИнформацияОбОтгрузке.УчетнаяЗапись, ИнформацияОбОтгрузке.ОтправленияВОтгрузке, Истина);
	
	Отказ = (Не ВыборкаИтогов.Следующий());
	Если Не Отказ Тогда
		Отказ = (ВыборкаИтогов.ЗаказВРаботе = 0);
	КонецЕсли;
	
	Если Отказ Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Заполнение документа ""%1"" невозможно, т.к. не найдены доступные для отгрузки отправления.';
				|en = 'Cannot fill the ""%1"" document as the shipments available for shipment are not found.'"),
			ПредставлениеДокумента);
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		Возврат ТранспортнаяНакладная;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗначениеЗаполнено(ТранспортнаяНакладная) Тогда
		ДокументОбъект = ВыборкаТТН.Ссылка.ПолучитьОбъект();
		ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
		
		Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
			Возврат ТранспортнаяНакладная;
		КонецЕсли;
		
		Если ВыборкаТТН.Проведен Тогда
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОбъект, Ложь, ИмяСобытия);
			Если Не Результат.ЗаписьВыполнена Тогда
				ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
				Возврат ТранспортнаяНакладная;
			КонецЕсли;
		Иначе
			ДокументОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
		
		ДокументОбъект.ДокументыОснования.Очистить();
	Иначе
		ДокументОбъект = Документы.ТранспортнаяНакладная.СоздатьДокумент();
		ДокументОбъект.Идентификатор = ИнформацияОбОтгрузке.ИдентификаторОтгрузки;
	КонецЕсли;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Префикс = "ТН-";
	Иначе
		Префикс = "";
	КонецЕсли;
	
	ДокументОбъект.Организация = НастройкиУчетнойЗаписи.Организация;
	ДокументОбъект.Дата = ТекущаяДатаСеанса();
	ДокументОбъект.Номер = Префикс + ИнформацияОбОтгрузке.НомерОтгрузки;
	ДокументОбъект.Грузоотправитель = НастройкиУчетнойЗаписи.Организация;
	ДокументОбъект.Грузополучатель = НастройкиУчетнойЗаписи.Контрагент;
	ДокументОбъект.Плательщик = НастройкиУчетнойЗаписи.Организация;
	ДокументОбъект.АвтомобильТип = ИнформацияОбОтгрузке.ТипТранспортногоСредства;
	ДокументОбъект.СрокДоставки = ИнформацияОбОтгрузке.ДатаВыполнения;
	
	Если ЗначениеЗаполнено(ДокументОбъект.Плательщик) Тогда
		Если ТипЗнч(ДокументОбъект.Плательщик) = Тип("СправочникСсылка.Контрагенты") Тогда
			ДокументОбъект.БанковскийСчетПлательщика =
				Справочники.БанковскиеСчетаКонтрагентов.ПолучитьБанковскийСчетПоУмолчанию(ДокументОбъект.Плательщик);
		Иначе
			СтруктураПараметров = ДенежныеСредстваСервер.ПараметрыЗаполненияБанковскогоСчетаОрганизацииПоУмолчанию();
			СтруктураПараметров.Организация = ДокументОбъект.Плательщик;
			ДокументОбъект.БанковскийСчетПлательщика = ЗначениеНастроекПовтИсп.ПолучитьБанковскийСчетОрганизацииПоУмолчанию(СтруктураПараметров);
		КонецЕсли;
	КонецЕсли;
	
	ДокументОбъект.ЗаданиеНаПеревозку = ЗаданиеНаПеревозку;
	
	Если НастройкиСклада <> Неопределено Тогда
		ДокументОбъект.АдресПогрузки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(НастройкиСклада.Склад);
		Если ПустаяСтрока(ДокументОбъект.АдресПогрузки) И ЗначениеЗаполнено(ДокументОбъект.Грузоотправитель) Тогда
			ДокументОбъект.АдресПогрузки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(
				ДокументОбъект.Грузоотправитель,
				"Фактический",
				ДокументОбъект.Дата);
			
			Если ПустаяСтрока(ДокументОбъект.АдресПогрузки) Тогда
				ДокументОбъект.АдресПогрузки = ФормированиеПечатныхФорм.ПолучитьАдресИзКонтактнойИнформации(
					ДокументОбъект.Грузоотправитель,
					"Юридический",
					ДокументОбъект.Дата);
			КонецЕсли;
		КонецЕсли;
		
		ДокументОбъект.АдресДоставки = НастройкиСклада.АдресДоставкиПеревозчика;
		ДокументОбъект.АдресДоставкиЗначение = НастройкиСклада.АдресДоставкиПеревозчикаЗначение;
		ДокументОбъект.АдресДоставкиПеревозчикаЗначение = НастройкиСклада.АдресДоставкиПеревозчикаЗначение;
		
		Если ПустаяСтрока(ДокументОбъект.АдресДоставки) Тогда
			ДокументОбъект.АдресДоставки = ИнформацияОбОтгрузке.АдресПунктаПриема;
		КонецЕсли;
	КонецЕсли;
	
	УчетнаяЗаписьПредставление =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			ИнформацияОбОтгрузке.ВидМаркетплейса,
			ИнформацияОбОтгрузке.УчетнаяЗапись);
	
	ДокументОбъект.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Сформировано для отгрузки %2';
			|en = '%1. Generated for the %2 shipment'"),
		УчетнаяЗаписьПредставление,
		ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
	
	РаспоряженияДокументовОтгрузок = Новый Соответствие;
	НеоформленныеДокументыОтгрузки = Новый Массив;
	ОтправленияНезагруженные = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияВОтгрузке);
	
	ВыборкаДокументовОтгрузки = ВыборкаИтогов.Выбрать();
	Пока ВыборкаДокументовОтгрузки.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.Заказ) Тогда
			Отправление = ОтправленияНезагруженные.Найти(ВыборкаДокументовОтгрузки.НомерОтправления);
			Если Отправление <> Неопределено Тогда
				ОтправленияНезагруженные.Удалить(Отправление);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.ДокументОтгрузки) Тогда
			СтрокаДокумента = ДокументОбъект.ДокументыОснования.Добавить();
			СтрокаДокумента.ДокументОснование = ВыборкаДокументовОтгрузки.ДокументОтгрузки;
			
			РаспоряженияДокументовОтгрузок.Вставить(ВыборкаДокументовОтгрузки.ДокументОтгрузки, ВыборкаДокументовОтгрузки.Заказ);
		Иначе
			НеоформленныеДокументыОтгрузки.Добавить(ВыборкаДокументовОтгрузки.НомерОтправления);
		КонецЕсли;
	КонецЦикла;
	
	Отказ = Ложь;
	
	Если ОтправленияНезагруженные.Количество() > 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для заполнения документа ""%1"" выполните загрузку заказов с торговой площадки для отправлений: %2.';
				|en = 'To fill the ""%1"" document, import orders from the marketplace for shipments: %2.'"),
			ПредставлениеДокумента,
			СтрСоединить(ОтправленияНезагруженные, ", "));
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		
		Отказ = Истина;
	КонецЕсли;
	
	Если НеоформленныеДокументыОтгрузки.Количество() > 0 Тогда
		ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Для заполнения документа ""%1"" оформите документы отгрузки для отправлений: %2.';
				|en = 'To fill the ""%1"" document, issue the shipping documents for shipments: %2.'"),
			ПредставлениеДокумента,
			СтрСоединить(НеоформленныеДокументыОтгрузки, ", "));
		ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Если ЗначениеЗаполнено(ДокументОбъект.ЗаданиеНаПеревозку) Тогда
			Результат = ЗаписатьТТНСУчетомПрав(ДокументОбъект, Ложь, ИмяСобытия);
			
			Если Результат.ЗаписьВыполнена Тогда
				ТранспортнаяНакладная = ДокументОбъект.Ссылка;
			Иначе
				ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
		
		Возврат ТранспортнаяНакладная;
	КонецЕсли;
	
	НовыйДокумент = ДокументОбъект.ЭтоНовый();
	
	Если ЗначениеЗаполнено(ЗаданиеНаПеревозку) Тогда
		ОбъектыПроверки = Новый Массив;
		ОбъектыПроверки.Добавить(ЗаданиеНаПеревозку);
	ИначеЕсли НовыйДокумент Тогда
		ОбъектыПроверки = ДокументОбъект;
	Иначе
		ОбъектыПроверки = ДокументОбъект.ДокументыОснования.ВыгрузитьКолонку("ДокументОснование");
	КонецЕсли;
	
	РезультатПроверки = Документы.ТранспортнаяНакладная.ПроверитьДокументыОснования(ОбъектыПроверки,, Истина);
	
	ДоступныеОснования = РезультатПроверки.ОбъектыПоКоторымМожноИНужноСоздатьТранспортныеНакладные;
	
	Если ДоступныеОснования.Количество() = 0 Тогда
		ДокументОбъект.ДокументыОснования.Очистить();
	Иначе
		СтрокиКУдалению = Новый Массив;
		
		Для Каждого СтрокаОснования Из ДокументОбъект.ДокументыОснования Цикл
			Если ДоступныеОснования.Найти(СтрокаОснования.ДокументОснование) = Неопределено Тогда
				СтрокиКУдалению.Добавить(СтрокаОснования);
				РаспоряженияДокументовОтгрузок.Удалить(СтрокаОснования.ДокументОснование);
			КонецЕсли;
		КонецЦикла;
		
		Для Каждого СтрокаУдаленияя Из СтрокиКУдалению Цикл
			ДокументОбъект.ДокументыОснования.Удалить(СтрокаУдаленияя);
		КонецЦикла;
	КонецЕсли;
	
	Если ДокументОбъект.ДокументыОснования.Количество() > 0 Тогда
		ЗаполнитьМассыИзНакладных(ДокументОбъект, РаспоряженияДокументовОтгрузок);
		
		Провести = Истина;
		Если ЗначениеЗаполнено(ЗаданиеНаПеревозку) Тогда
			Провести = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаданиеНаПеревозку, "Проведен");
		КонецЕсли;
		
		ДокументОбъект.Заполнить(Неопределено);
		Результат = ЗаписатьТТНСУчетомПрав(ДокументОбъект, Провести, ИмяСобытия);
		
		Если Не Результат.ЗаписьВыполнена Тогда
			ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
		КонецЕсли;
	КонецЕсли;
	
	ДокументОбъект.Разблокировать();
	
	ТранспортнаяНакладная = ДокументОбъект.Ссылка;
	
	// Добавить запись в историю работы пользователя.
	Если НовыйДокумент Тогда
		ИсторияРаботыПользователя.Добавить(ТранспортнаяНакладная);
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ТранспортнаяНакладная;

КонецФункции

// Заполняет массу нетто и брутто в переданном объекте.
//
// Параметры:
//   Объект                         - ДокументОбъект.ТранспортнаяНакладная,
//   								- см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   РаспоряженияДокументовОтгрузок - Неопределено, Соответствие из КлючИЗначение - в ключе документ отгрузки, в значении распоряжение (заказ).
//
Процедура ЗаполнитьМассыИзНакладных(Объект, Знач РаспоряженияДокументовОтгрузок = Неопределено) Экспорт

	Объект.МассаБрутто = 0;
	Объект.МассаНетто  = 0;
	
	ОтправленияБезДокументовОтгрузок = Новый Массив;
	
	Если РаспоряженияДокументовОтгрузок = Неопределено Тогда
		ОтправленияВОтгрузке = ОбщегоНазначения.СкопироватьРекурсивно(Объект.ОтправленияВОтгрузке);
		Если Не ПустаяСтрока(Объект.ИдентификаторОтгрузки) Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОтправленияВОтгрузке, Объект.ОтправленияВПланеОтгрузки, Истина);
		КонецЕсли;
		
		Если ОтправленияВОтгрузке.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		РаспоряженияДокументовОтгрузок = Новый Соответствие;
		ВыборкаДокументовОтгрузки = ВыборкаДокументовОтгрузки(Объект.УчетнаяЗапись, ОтправленияВОтгрузке);
		Пока ВыборкаДокументовОтгрузки.Следующий() Цикл
			Если Не ВыборкаДокументовОтгрузки.ЗаказВРаботе Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.ДокументОтгрузки)
					И ВыборкаДокументовОтгрузки.ДокументОтгрузкиПроведен Тогда
				РаспоряженияДокументовОтгрузок.Вставить(ВыборкаДокументовОтгрузки.ДокументОтгрузки, ВыборкаДокументовОтгрузки.Заказ);
			Иначе
				ОтправленияБезДокументовОтгрузок.Добавить(ВыборкаДокументовОтгрузки.НомерОтправления);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Если РаспоряженияДокументовОтгрузок.Количество() > 0 Тогда
		СпособыДоставкиПоЗаданиюНаПеревозку = ДоставкаТоваровКлиентСервер.СпособыДоставкиДоКлиентаСНашимУчастием(
			ПолучитьФункциональнуюОпцию("ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками"));
		
		Запрос.УстановитьПараметр("СпособыДоставкиПоЗаданиюНаПеревозку", СпособыДоставкиПоЗаданиюНаПеревозку);
		
		УстановитьТекстЗапросаМассИзНакладных(Запрос, РаспоряженияДокументовОтгрузок);
		
		ЗаполнитьМассыИзЗапроса(Объект, Запрос);
	КонецЕсли;
	
	Если ОтправленияБезДокументовОтгрузок.Количество() > 0 Тогда
		Запрос.УстановитьПараметр("НомераОтправлений", ОтправленияБезДокументовОтгрузок);
		
		УстановитьТекстЗапросаМассИзОтправлений(Запрос);
		
		ЗаполнитьМассыИзЗапроса(Объект, Запрос);
	КонецЕсли;

КонецПроцедуры

// Подготавливает список перезаполняемых отправлений.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   НастройкиСклада      - Неопределено - не передаются настройки склада;
//                          см. ИнтеграцияСМаркетплейсамиСервер.НовыйНастройкиСкладаFBS.
//
// Возвращаемое значение:
//   Соответствие из Строка - данные перезаполняемых отправлений, где в ключе номер отправления,
//                                   в значении структура с ключами "Документ", "Проведен".
//
Функция ПолучитьОтправленияДляПерезаполненияНастроекДоставки(ИнформацияОбОтгрузке, НастройкиСклада = Неопределено) Экспорт

	ПерезаполняемыеОтправления = Новый Соответствие;
	
	Если Не ИнформацияОбОтгрузке.ОтгрузкаАктуальна Тогда
		Возврат ПерезаполняемыеОтправления;
	КонецЕсли;
	
	Если НастройкиСклада = Неопределено Тогда
		НастройкиСклада = ИнтеграцияСМаркетплейсамиСервер.НастройкиСклада(
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			ИнформацияОбОтгрузке.ИдентификаторСклада);
	КонецЕсли;
	
	ПроверяемыеНастройки = ПерезаполняемыеНастройкиСклада();
	
	НомераОтправлений = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияВОтгрузке);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НомераОтправлений, ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки, Истина);
	
	ВыборкаДокументовОтгрузки = ВыборкаДокументовОтгрузки(ИнформацияОбОтгрузке.УчетнаяЗапись, НомераОтправлений,, Истина);
	Пока ВыборкаДокументовОтгрузки.Следующий() Цикл
		Если Не ВыборкаДокументовОтгрузки.ЗаказПередаетсяВДоставку Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьОтличияВЗаказе = Ложь;
		ЕстьОтличияВДокументеОтгрузки = Ложь;
		
		Для Каждого ПроверяемаяНастройка Из ПроверяемыеНастройки Цикл
			Если ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.Заказ)
					И ВыборкаДокументовОтгрузки["Заказ" + ПроверяемаяНастройка] <> НастройкиСклада[ПроверяемаяНастройка] Тогда
				ЕстьОтличияВЗаказе = Истина;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.Заказ)
					И ЗначениеЗаполнено(ВыборкаДокументовОтгрузки.ДокументОтгрузки)
					И ВыборкаДокументовОтгрузки["ДокументОтгрузки" + ПроверяемаяНастройка] <> НастройкиСклада[ПроверяемаяНастройка] Тогда
				Если ВыборкаДокументовОтгрузки.ТипДокументаОтгрузки = Тип("ДокументСсылка.ПередачаТоваровХранителю")
					И ПроверяемаяНастройка = "Курьер" Тогда
					Продолжить;
				КонецЕсли;
				
				ЕстьОтличияВДокументеОтгрузки = Истина;
			КонецЕсли;
		КонецЦикла;
		
		ДокументыОтправления = ПерезаполняемыеОтправления.Получить(ВыборкаДокументовОтгрузки.НомерОтправления);
		Если ДокументыОтправления = Неопределено Тогда
			 ДокументыОтправления = Новый Массив;
		КонецЕсли;
		
		Если ЕстьОтличияВЗаказе Тогда
			ДокументыОтправления.Добавить(Новый Структура("Документ, Проведен",
				ВыборкаДокументовОтгрузки.Заказ, ВыборкаДокументовОтгрузки.ЗаказПроведен));
		КонецЕсли;
		
		Если ЕстьОтличияВДокументеОтгрузки Тогда
			ДокументыОтправления.Добавить(Новый Структура("Документ, Проведен",
				ВыборкаДокументовОтгрузки.ДокументОтгрузки, ВыборкаДокументовОтгрузки.ДокументОтгрузкиПроведен));
		КонецЕсли;
		
		Если ДокументыОтправления.Количество() > 0 Тогда
			ПерезаполняемыеОтправления.Вставить(ВыборкаДокументовОтгрузки.НомерОтправления, ДокументыОтправления);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПерезаполняемыеОтправления;

КонецФункции

// Определяет тип исполнителя задания на перевозку по способу доставки.
//
// Параметры:
//   СпособДоставки - ПеречислениеСсылка.СпособыДоставки - способ доставки.
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ТипыИсполнителейЗаданийНаПеревозку - тип исполнителя задания на перевозку.
//
Функция ТипИсполнителяЗаданияНаПеревозку(СпособДоставки) Экспорт

	ЗаданиеВыполняет = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.НашаТранспортнаяСлужба;
	
	СпособыДоставкиПеревозчиком = Новый Массив;
	
	СпособыДоставкиПеревозчиком.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчикаДоНашегоСклада);
	СпособыДоставкиПеревозчиком.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчика);
	СпособыДоставкиПеревозчиком.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи);
	СпособыДоставкиПеревозчиком.Добавить(Перечисления.СпособыДоставки.ОтОтправителяОпределяетСлужбаДоставки);
	СпособыДоставкиПеревозчиком.Добавить(Перечисления.СпособыДоставки.СиламиПеревозчикаДоПунктаПередачи);
	
	Если СпособыДоставкиПеревозчиком.Найти(СпособДоставки) <> Неопределено Тогда
		ЗаданиеВыполняет = Перечисления.ТипыИсполнителейЗаданийНаПеревозку.Перевозчик;
	КонецЕсли;
	
	Возврат ЗаданиеВыполняет;

КонецФункции

#КонецОбласти

#Область ПечатныеФормы

// Возвращает список статусов, по которым доступна печать этикеток отправлений.
//
// Возвращаемое значение:
//   Массив из ПеречислениеСсылка.СтатусыЗаказовТорговыхПлощадок - список статусов отправлений.
//
Функция СтатусыПечатиЭтикеток() Экспорт

	СтатусыПечатиЭтикетки = Новый Массив;
	
	СтатусыПечатиЭтикетки.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке);
	СтатусыПечатиЭтикетки.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку);
	
	Возврат СтатусыПечатиЭтикетки;

КонецФункции

// Получает печатную форму этикеток грузовых мест отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке  - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   ТранспортнаяНакладная - ДокументСсылка.ТранспортнаяНакладная - транспортная накладная,
//                             в присоединенные файлы которой нужно записать полученную печатную форму.
//   НаименованиеФайла     - Строка - наименование, под которым будет записан файл.
//   Адрес                 - Строка, УникальныйИдентификатор - адрес во временном хранилище.
//
// Возвращаемое значение:
//   Структура - данные файла:
//     * АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы;
//     * РасширениеФайла     - Строка - расширение печатной формы.
//
Функция ПолучитьФайлЭтикетокГрузовыхМестОтгрузки(ИнформацияОбОтгрузке, ТранспортнаяНакладная,
			НаименованиеФайла = "", Адрес = "") Экспорт

	АдресДвоичныхДанных = "";
	РасширениеФайла = "";
	ПреобразоватьВPDF = Ложь;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		АдресДвоичныхДанных = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЭтикеткиГрузовыхМестОтгрузки(
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			Адрес);
		
		РасширениеФайла = "pdf";
	КонецЕсли;
	
	ЗаписатьПрисоединенныйФайл(
		ИнформацияОбОтгрузке.ВидМаркетплейса,
		ТранспортнаяНакладная,
		АдресДвоичныхДанных,
		НаименованиеФайла,
		РасширениеФайла,
		ПреобразоватьВPDF);
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("АдресДвоичныхДанных", АдресДвоичныхДанных);
	ДанныеФайла.Вставить("РасширениеФайла",     РасширениеФайла);
	
	Возврат ДанныеФайла;

КонецФункции

// Получает печатную форму листа отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке  - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   ТранспортнаяНакладная - ДокументСсылка.ТранспортнаяНакладная - транспортная накладная,
//                             в присоединенные файлы которой нужно записать полученную печатную форму.
//   НаименованиеФайла     - Строка - наименование, под которым будет записан файл.
//   Адрес                 - Строка, УникальныйИдентификатор - адрес во временном хранилище.
//
// Возвращаемое значение:
//   Структура - данные файла:
//     * АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы;
//     * РасширениеФайла     - Строка - расширение печатной формы.
//
Функция ПолучитьФайлЛистаОтгрузки(ИнформацияОбОтгрузке, ТранспортнаяНакладная,
			НаименованиеФайла = "", Адрес = "") Экспорт

	АдресДвоичныхДанных = "";
	РасширениеФайла = "";
	ПреобразоватьВPDF = Ложь;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		АдресДвоичныхДанных = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьФайлЛистаОтгрузки(
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			Адрес);
		
		РасширениеФайла = "pdf";
	КонецЕсли;
	
	ЗаписатьПрисоединенныйФайл(
		ИнформацияОбОтгрузке.ВидМаркетплейса,
		ТранспортнаяНакладная,
		АдресДвоичныхДанных,
		НаименованиеФайла,
		РасширениеФайла,
		ПреобразоватьВPDF);
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("АдресДвоичныхДанных", АдресДвоичныхДанных);
	ДанныеФайла.Вставить("РасширениеФайла",     РасширениеФайла);
	
	Возврат ДанныеФайла;

КонецФункции

// Получает печатную форму штрихкода отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке  - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   ТранспортнаяНакладная - ДокументСсылка.ТранспортнаяНакладная - транспортная накладная,
//                             в присоединенные файлы которой нужно записать полученную печатную форму.
//   НаименованиеФайла     - Строка - наименование, под которым будет записан файл.
//   Адрес                 - Строка, УникальныйИдентификатор - адрес во временном хранилище.
//
// Возвращаемое значение:
//   Структура - данные файла:
//     * АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы;
//     * РасширениеФайла     - Строка - расширение печатной формы.
//
Функция ПолучитьФайлШтрихкодаОтгрузки(ИнформацияОбОтгрузке, ТранспортнаяНакладная,
			НаименованиеФайла = "", Адрес = "") Экспорт

	АдресДвоичныхДанных = "";
	РасширениеФайла = "";
	ПреобразоватьВPDF = Ложь;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		АдресДвоичныхДанных = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьФайлШтрихкодаОтгрузки(
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			Адрес);
		
		РасширениеФайла = "png";
		ПреобразоватьВPDF = Истина;
	КонецЕсли;
	
	ЗаписатьПрисоединенныйФайл(
		ИнформацияОбОтгрузке.ВидМаркетплейса,
		ТранспортнаяНакладная,
		АдресДвоичныхДанных,
		НаименованиеФайла,
		РасширениеФайла,
		ПреобразоватьВPDF);
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("АдресДвоичныхДанных", АдресДвоичныхДанных);
	ДанныеФайла.Вставить("РасширениеФайла",     РасширениеФайла);
	
	Возврат ДанныеФайла;

КонецФункции

// Получает печатную форму накладной / акта отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке  - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   ТранспортнаяНакладная - ДокументСсылка.ТранспортнаяНакладная - транспортная накладная,
//                             в присоединенные файлы которой нужно записать полученную печатную форму.
//   НаименованиеФайла     - Строка - наименование, под которым будет записан файл.
//   Адрес                 - Строка, УникальныйИдентификатор - адрес во временном хранилище.
//
// Возвращаемое значение:
//   Структура - данные файла:
//     * АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы;
//     * РасширениеФайла     - Строка - расширение печатной формы.
//
Функция ПолучитьФайлНакладнойАкта(ИнформацияОбОтгрузке, ТранспортнаяНакладная,
			НаименованиеФайла = "", Адрес = "") Экспорт

	АдресДвоичныхДанных = "";
	РасширениеФайла = "";
	ПреобразоватьВPDF = Ложь;
	
	Если ИнформацияОбОтгрузке.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		АдресДвоичныхДанных = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьФайлыДокументовОтгрузки(
			ИнформацияОбОтгрузке.УчетнаяЗапись,
			ИнформацияОбОтгрузке.ИдентификаторОтгрузки,
			Адрес);
		
		РасширениеФайла = "pdf";
	КонецЕсли;
	
	ЗаписатьПрисоединенныйФайл(
		ИнформацияОбОтгрузке.ВидМаркетплейса,
		ТранспортнаяНакладная,
		АдресДвоичныхДанных,
		НаименованиеФайла,
		РасширениеФайла,
		ПреобразоватьВPDF);
	
	ДанныеФайла = Новый Структура;
	ДанныеФайла.Вставить("АдресДвоичныхДанных", АдресДвоичныхДанных);
	ДанныеФайла.Вставить("РасширениеФайла",     РасширениеФайла);
	
	Возврат ДанныеФайла;

КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаказыИОтправленияСлужебный

Функция ТекстЗапросаПроверкиЗаказов(ЗаполнитьСведенияОбЭкземплярах = Ложь)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	СведенияПоТоварам.Заказ КАК Заказ,
		|	СведенияПоТоварам.ИдентификаторЭкземпляра КАК ИдентификаторЭкземпляра,
		|	СведенияПоТоварам.НомерОтправления КАК НомерОтправления,
		|	СведенияПоТоварам.ДокументОтгрузки КАК ДокументОтгрузки,
		|	СведенияПоТоварам.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	НЕ ЕСТЬNULL(ТоварыЗаказов.Отменено, ИСТИНА)
		|		И СведенияПоТоварам.ДокументОтгрузки <> НЕОПРЕДЕЛЕНО
		|		И СведенияПоТоварам.ИдентификаторСтроки = """" КАК ПозицияПеренесена,
		|	НЕ ЕСТЬNULL(ТоварыЗаказов.Отменено, ИСТИНА) КАК ПозицияКОтгрузке,
		|	СведенияПоТоварам.ИдентификаторСтроки = """" КАК НетПривязкиКОтгрузке,
		|	НЕ ЕСТЬNULL(ТоварыЗаказов.Отменено, ИСТИНА)
		|		И (СведенияПоТоварам.ДокументОтгрузки = НЕОПРЕДЕЛЕНО
		|			ИЛИ ВЫБОР
		|				КОГДА СведенияПоТоварам.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		|					ТОГДА НЕ ВЫРАЗИТЬ(СведенияПоТоварам.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Проведен
		|				КОГДА СведенияПоТоварам.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		|					ТОГДА НЕ ВЫРАЗИТЬ(СведенияПоТоварам.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Проведен
		|				ИНАЧЕ ЛОЖЬ
		|			КОНЕЦ) КАК НетОтгрузки,
		|	ВЫБОР
		|		КОГДА СведенияПоТоварам.ТребуетсяСтранаПроисхождения
		|					И СведенияПоТоварам.СтранаПроисхождения = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)
		|				ИЛИ СведенияПоТоварам.ТребуетсяМаркировка
		|					И СведенияПоТоварам.ПолныйКодМаркировки = """"
		|				ИЛИ (СведенияПоТоварам.ТребуетсяНомерГТД
		|					ИЛИ СведенияПоТоварам.ТребуетсяРНПТ)
		|					И СведенияПоТоварам.НомерГТД = ЗНАЧЕНИЕ(Справочник.НомераГТД.ПустаяСсылка)
		|				ИЛИ СведенияПоТоварам.ТребуетсяУИН
		|					И СведенияПоТоварам.УИН = """"
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ТребуетсяЗаполнить
		|ПОМЕСТИТЬ СведенияПоТоварам
		|ИЗ
		|	РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок КАК СведенияПоТоварам
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ТоварыЗаказов
		|		ПО СведенияПоТоварам.Заказ = ТоварыЗаказов.Ссылка
		|			И СведенияПоТоварам.КодСтроки = ТоварыЗаказов.КодСтроки
		|ГДЕ
		|	СведенияПоТоварам.Заказ В(&Заказы)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ,
		|	НомерОтправления";
	
	Если ЗаполнитьСведенияОбЭкземплярах Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()
			+ "ВЫБРАТЬ
			  |	СведенияПоТоварам.Заказ КАК Заказ,
			  |	СведенияПоТоварам.ИдентификаторЭкземпляра КАК ИдентификаторЭкземпляра
			  |ИЗ
			  |	СведенияПоТоварам КАК СведенияПоТоварам
			  |ГДЕ
			  |	СведенияПоТоварам.НетОтгрузки
			  |ИТОГИ ПО
			  |	Заказ";
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()
		+ "ВЫБРАТЬ
		  |	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		  |	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
		  |	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		  |	МИНИМУМ(ЗаказыТорговыхПлощадок.ДокументОтгрузки) КАК ДокументОтгрузки,
		  |	МИНИМУМ(ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления) КАК НомерРодительскогоОтправления,
		  |	МИНИМУМ(ЗаказыТорговыхПлощадок.Статус) КАК Статус,
		  |	ЕСТЬNULL(МАКСИМУМ(СведенияПоТоварам.ИдентификаторЭкземпляра), 0) КАК МаксимальныйИдентификаторЭкземпляра,
		  |	ЕСТЬNULL(МАКСИМУМ(ЗаказыТорговыхПлощадок.ДокументОтгрузки <> НЕОПРЕДЕЛЕНО
		  |				И (СведенияПоТоварам.Заказ ЕСТЬ NULL
		  |					ИЛИ СведенияПоТоварам.ПозицияКОтгрузке
		  |						И (СведенияПоТоварам.НетПривязкиКОтгрузке
		  |							ИЛИ СведенияПоТоварам.ДокументОтгрузки <> ЗаказыТорговыхПлощадок.ДокументОтгрузки))), ИСТИНА) КАК ЕстьРасхождения,
		  |	ЕСТЬNULL(СУММА(ВЫБОР
		  |				КОГДА СведенияПоТоварам.Заказ ЕСТЬ NULL
		  |					ТОГДА 0
		  |				КОГДА НЕ СведенияПоТоварам.НетПривязкиКОтгрузке
		  |					ТОГДА 0
		  |				КОГДА НЕ СведенияПоТоварам.ПозицияПеренесена
		  |					ТОГДА 0
		  |				ИНАЧЕ ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
		  |			КОНЕЦ), 1) КАК КоличествоПеренесенныхПозиций,
		  |	ЕСТЬNULL(СУММА(ВЫБОР
		  |				КОГДА СведенияПоТоварам.Заказ ЕСТЬ NULL
		  |					ТОГДА 0
		  |				ИНАЧЕ ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
		  |			КОНЕЦ), 0) КАК КоличествоЗаписей
		  |ПОМЕСТИТЬ ОтправленияПоЗаказам
		  |ИЗ
		  |	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		  |		ЛЕВОЕ СОЕДИНЕНИЕ СведенияПоТоварам КАК СведенияПоТоварам
		  |		ПО ЗаказыТорговыхПлощадок.Заказ = СведенияПоТоварам.Заказ
		  |			И ЗаказыТорговыхПлощадок.НомерОтправления = СведенияПоТоварам.НомерОтправления
		  |ГДЕ
		  |	ЗаказыТорговыхПлощадок.Заказ В(&Заказы)
		  |
		  |СГРУППИРОВАТЬ ПО
		  |	ЗаказыТорговыхПлощадок.Заказ,
		  |	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
		  |	ЗаказыТорговыхПлощадок.НомерОтправления
		  |
		  |ИНДЕКСИРОВАТЬ ПО
		  |	Заказ,
		  |	НомерОтправления
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ РАЗЛИЧНЫЕ
		  |	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		  |	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись
		  |ПОМЕСТИТЬ ЗаказыТорговыхПлощадок
		  |ИЗ
		  |	ОтправленияПоЗаказам КАК ЗаказыТорговыхПлощадок
		  |
		  |ИНДЕКСИРОВАТЬ ПО
		  |	Заказ
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ
		  |	1 КАК Порядок,
		  |	ОтправленияПоЗаказам.Заказ КАК Заказ,
		  |	ОтправленияПоЗаказам.Заказ.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
		  |	ОтправленияПоЗаказам.УчетнаяЗапись КАК УчетнаяЗапись,
		  |	ОтправленияПоЗаказам.УчетнаяЗапись.ВидМаркетплейса КАК ВидМаркетплейса,
		  |	ОтправленияПоЗаказам.НомерОтправления КАК НомерОтправления,
		  |	ОтправленияПоЗаказам.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		  |	ОтправленияПоЗаказам.ДокументОтгрузки КАК ДокументОтгрузки,
		  |	ВЫБОР
		  |		КОГДА ОтправленияПоЗаказам.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		  |			ТОГДА ВЫРАЗИТЬ(ОтправленияПоЗаказам.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Номер
		  |		КОГДА ОтправленияПоЗаказам.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		  |			ТОГДА ВЫРАЗИТЬ(ОтправленияПоЗаказам.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Номер
		  |		ИНАЧЕ """"
		  |	КОНЕЦ КАК НомерДокументаОтгрузки,
		  |	ВЫБОР
		  |		КОГДА ОтправленияПоЗаказам.ДокументОтгрузки ССЫЛКА Документ.ПередачаТоваровХранителю
		  |			ТОГДА ВЫРАЗИТЬ(ОтправленияПоЗаказам.ДокументОтгрузки КАК Документ.ПередачаТоваровХранителю).Дата
		  |		КОГДА ОтправленияПоЗаказам.ДокументОтгрузки ССЫЛКА Документ.РеализацияТоваровУслуг
		  |			ТОГДА ВЫРАЗИТЬ(ОтправленияПоЗаказам.ДокументОтгрузки КАК Документ.РеализацияТоваровУслуг).Дата
		  |		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		  |	КОНЕЦ КАК ДатаДокументаОтгрузки,
		  |	ОтправленияПоЗаказам.Статус КАК Статус,
		  |	ОтправленияПоЗаказам.Статус В(&СтатусыКонечные) КАК ЭтоКонечныйСтатус,
		  |	ОтправленияПоЗаказам.Статус В(&СтатусыВДоставке) КАК ВДоставке,
		  |	ОтправленияПоЗаказам.МаксимальныйИдентификаторЭкземпляра КАК МаксимальныйИдентификаторЭкземпляра,
		  |	ОтправленияПоЗаказам.КоличествоПеренесенныхПозиций КАК КоличествоПеренесенныхПозиций,
		  |	ОтправленияПоЗаказам.КоличествоЗаписей КАК КоличествоЗаписей
		  |ИЗ
		  |	ОтправленияПоЗаказам КАК ОтправленияПоЗаказам
		  |
		  |ОБЪЕДИНИТЬ ВСЕ
		  |
		  |ВЫБРАТЬ
		  |	2,
		  |	СведенияПоТоварам.Заказ,
		  |	СведенияПоТоварам.Заказ.ХозяйственнаяОперация,
		  |	ЕСТЬNULL(МАКСИМУМ(ЗаказыТорговыхПлощадок.УчетнаяЗапись), ЗНАЧЕНИЕ(Справочник.УчетныеЗаписиМаркетплейсов.ПустаяСсылка)),
		  |	ЕСТЬNULL(МАКСИМУМ(ЗаказыТорговыхПлощадок.УчетнаяЗапись.ВидМаркетплейса), ЗНАЧЕНИЕ(Перечисление.ВидыМаркетплейсов.ПустаяСсылка)),
		  |	СведенияПоТоварам.НомерОтправления,
		  |	СведенияПоТоварам.НомерРодительскогоОтправления,
		  |	НЕОПРЕДЕЛЕНО,
		  |	"""",
		  |	ДАТАВРЕМЯ(1, 1, 1),
		  |	ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки),
		  |	ЛОЖЬ,
		  |	ЛОЖЬ,
		  |	МАКСИМУМ(СведенияПоТоварам.ИдентификаторЭкземпляра),
		  |	СУММА(ВЫБОР
		  |			КОГДА СведенияПоТоварам.НомерРодительскогоОтправления = """"
		  |				ТОГДА 0
		  |			КОГДА СведенияПоТоварам.ДокументОтгрузки <> НЕОПРЕДЕЛЕНО
		  |				ТОГДА ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
		  |			ИНАЧЕ 0
		  |		КОНЕЦ),
		  |	СУММА(ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0)))
		  |ИЗ
		  |	СведенияПоТоварам КАК СведенияПоТоварам
		  |		ЛЕВОЕ СОЕДИНЕНИЕ ОтправленияПоЗаказам КАК ОтправленияПоЗаказам
		  |		ПО СведенияПоТоварам.Заказ = ОтправленияПоЗаказам.Заказ
		  |			И СведенияПоТоварам.НомерОтправления = ОтправленияПоЗаказам.НомерОтправления
		  |		ЛЕВОЕ СОЕДИНЕНИЕ ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		  |		ПО СведенияПоТоварам.Заказ = ЗаказыТорговыхПлощадок.Заказ
		  |ГДЕ
		  |	ОтправленияПоЗаказам.НомерОтправления ЕСТЬ NULL
		  |
		  |СГРУППИРОВАТЬ ПО
		  |	СведенияПоТоварам.Заказ,
		  |	СведенияПоТоварам.НомерОтправления,
		  |	СведенияПоТоварам.НомерРодительскогоОтправления,
		  |	СведенияПоТоварам.Заказ.ХозяйственнаяОперация
		  |
		  |УПОРЯДОЧИТЬ ПО
		  |	НомерОтправления
		  |ИТОГИ
		  |	МАКСИМУМ(ХозяйственнаяОперация),
		  |	МАКСИМУМ(УчетнаяЗапись),
		  |	МАКСИМУМ(ВидМаркетплейса)
		  |ПО
		  |	Заказ
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |УНИЧТОЖИТЬ ЗаказыТорговыхПлощадок
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ РАЗЛИЧНЫЕ
		  |	ИзмененныеОтправления.Порядок КАК Порядок,
		  |	ИзмененныеОтправления.Заказ КАК Заказ,
		  |	ИзмененныеОтправления.НомерОтправления КАК НомерОтправления,
		  |	ИзмененныеОтправления.ДокументОтгрузки КАК ДокументОтгрузки
		  |ИЗ
		  |	(ВЫБРАТЬ
		  |		1 КАК Порядок,
		  |		СведенияПоТоварам.Заказ КАК Заказ,
		  |		СведенияПоТоварам.НомерОтправления КАК НомерОтправления,
		  |		ЕСТЬNULL(ОтправленияПоЗаказам.ДокументОтгрузки, НЕОПРЕДЕЛЕНО) КАК ДокументОтгрузки
		  |	ИЗ
		  |		СведенияПоТоварам КАК СведенияПоТоварам
		  |			ЛЕВОЕ СОЕДИНЕНИЕ ОтправленияПоЗаказам КАК ОтправленияПоЗаказам
		  |			ПО СведенияПоТоварам.Заказ = ОтправленияПоЗаказам.Заказ
		  |				И СведенияПоТоварам.НомерОтправления = ОтправленияПоЗаказам.НомерОтправления
		  |	ГДЕ
		  |		СведенияПоТоварам.ПозицияПеренесена
		  |		И ОтправленияПоЗаказам.ДокументОтгрузки ЕСТЬ NULL
		  |	
		  |	ОБЪЕДИНИТЬ ВСЕ
		  |	
		  |	ВЫБРАТЬ РАЗЛИЧНЫЕ
		  |		2,
		  |		СведенияПоТоварам.Заказ,
		  |		ЕСТЬNULL(ОтправленияПоЗаказам.НомерОтправления, """"),
		  |		СведенияПоТоварам.ДокументОтгрузки
		  |	ИЗ
		  |		СведенияПоТоварам КАК СведенияПоТоварам
		  |			ЛЕВОЕ СОЕДИНЕНИЕ ОтправленияПоЗаказам КАК ОтправленияПоЗаказам
		  |			ПО СведенияПоТоварам.Заказ = ОтправленияПоЗаказам.Заказ
		  |				И СведенияПоТоварам.ДокументОтгрузки = ОтправленияПоЗаказам.ДокументОтгрузки
		  |	ГДЕ
		  |		СведенияПоТоварам.ПозицияПеренесена
		  |		И СведенияПоТоварам.НомерОтправления <> ОтправленияПоЗаказам.НомерОтправления
		  |		И ОтправленияПоЗаказам.ДокументОтгрузки ЕСТЬ НЕ NULL 
		  |	
		  |	ОБЪЕДИНИТЬ ВСЕ
		  |	
		  |	ВЫБРАТЬ РАЗЛИЧНЫЕ
		  |		3,
		  |		ОтправленияПоЗаказам.Заказ,
		  |		ОтправленияПоЗаказам.НомерОтправления,
		  |		ОтправленияПоЗаказам.ДокументОтгрузки
		  |	ИЗ
		  |		ОтправленияПоЗаказам КАК ОтправленияПоЗаказам
		  |	ГДЕ
		  |		ОтправленияПоЗаказам.ЕстьРасхождения
		  |		И ОтправленияПоЗаказам.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.Доставлен)) КАК ИзмененныеОтправления
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |УНИЧТОЖИТЬ ОтправленияПоЗаказам
		  |;
		  |
		  |////////////////////////////////////////////////////////////////////////////////
		  |ВЫБРАТЬ РАЗЛИЧНЫЕ
		  |	СведенияПоТоварам.Заказ КАК Заказ,
		  |	СведенияПоТоварам.НомерОтправления КАК НомерОтправления
		  |ИЗ
		  |	СведенияПоТоварам КАК СведенияПоТоварам
		  |ГДЕ
		  |	СведенияПоТоварам.ТребуетсяЗаполнить
		  |	И СведенияПоТоварам.ПозицияКОтгрузке";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов()
		+ "УНИЧТОЖИТЬ СведенияПоТоварам";
	
	Возврат ТекстЗапроса;

КонецФункции

Процедура ПроверитьГотовностьОтправленийКПодтверждениюСборки(РезультатПроверкиЗаказа)

	ДетализацияПроблемПоОтправлениям = Новый Соответствие;
	
	Для Каждого ЗаписьИзДанныхПоНомерамОтправлений Из РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений Цикл
		НомерОтправления = ЗаписьИзДанныхПоНомерамОтправлений.Ключ;
		
		ВСборке =
			(РезультатПроверкиЗаказа.ОтправленияНеВСборке.Найти(НомерОтправления) = Неопределено);
		ЕстьТовары =
			(РезультатПроверкиЗаказа.ОтправленияБезТоваров.Найти(НомерОтправления) = Неопределено);
		ЕстьНеЗаполненыеДанные =
			(РезультатПроверкиЗаказа.ОтправленияСНезаполненнымиДанными.Найти(НомерОтправления) <> Неопределено);
		ЕстьДокументОтгрузки =
			(РезультатПроверкиЗаказа.ОтправленияБезДокументовОтгрузки.Найти(НомерОтправления) = Неопределено);
		ИзмененДокументОтгрузки =
			(РезультатПроверкиЗаказа.ОтправленияИзмененные.Найти(НомерОтправления) <> Неопределено);
		
		Если ВСборке И ЕстьТовары И Не ЕстьНеЗаполненыеДанные
				И ЕстьДокументОтгрузки И Не ИзмененДокументОтгрузки Тогда
			РезультатПроверкиЗаказа.ОтправленияКПодтверждениюСборки.Добавить(НомерОтправления);
		Иначе
			Проблемы = Новый Массив;
			
			Если Не ВСборке Тогда
				Проблемы.Добавить(НСтр("ru = 'Сборка отправления не требуется.';
										|en = 'The shipment does not require picking.'"));
			ИначеЕсли Не ЕстьТовары Тогда
				Проблемы.Добавить(НСтр("ru = 'Не найдены товары отправления.';
										|en = 'The shipment goods are not found.'"));
			Иначе
				Если ЕстьНеЗаполненыеДанные Тогда
					Проблемы.Добавить(НСтр("ru = 'Есть незаполненные данные по товарам отправления.';
											|en = 'There is blank data on the goods of the shipment.'"));
				КонецЕсли;
				
				Если Не ЕстьДокументОтгрузки Или ИзмененДокументОтгрузки Тогда
					Проблемы.Добавить(НСтр("ru = 'Требуется оформление документа отгрузки.';
											|en = 'Register the shipping document.'"));
				КонецЕсли;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(Проблемы) Тогда
				ДетализацияПроблемПоОтправлениям.Вставить(НомерОтправления, Проблемы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	РезультатПроверкиЗаказа.ДетализацияПроблемПоОтправлениям = ДетализацияПроблемПоОтправлениям;

КонецПроцедуры

Функция ЗаполнитьСведенияПоЗаказамИОтправлениям(ВидМаркетплейса, УчетнаяЗапись)

	ТекстОшибки = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки)
		|	И ЗаказыТорговыхПлощадок.УчетнаяЗапись.ВидМаркетплейса = &ВидМаркетплейса
		|	И &Условие";
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		ТекстЗамены = "ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись";
		Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Иначе
		ТекстЗамены = "ИСТИНА";
	КонецЕсли;
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстЗамены);
	Запрос.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Заказы = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Заказ");
		
		РезультатЗаполнения =
			РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ЗаполнитьДанныеПоЭкземплярам(Заказы);
		
		Если Не ПустаяСтрока(РезультатЗаполнения.ИнформацияОбОшибке.КодОшибки) Тогда
			ТекстОшибки = РезультатЗаполнения.ИнформацияОбОшибке.ОписаниеОшибки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстОшибки;

КонецФункции

Функция ПерезаполнитьДокументыОтгрузки(РезультатПроверкиЗаказов, СведенияПоТоварам, Автор = Неопределено)

	СписокОшибок = Новый Массив;
	
	Для Каждого ДанныеПоЗаказу Из РезультатПроверкиЗаказов Цикл
		Заказ                   = ДанныеПоЗаказу.Ключ;
		РезультатПроверкиЗаказа = ДанныеПоЗаказу.Значение;
		
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(РезультатПроверкиЗаказа.ВидМаркетплейса);
		
		// Отменить проведение всех документов отгрузки для заказа перед перезаполнением.
		ПроблемныеДокументы = Новый Массив;
		
		Для Каждого ДанныеДокументаОтгрузки Из РезультатПроверкиЗаказа.ИзмененныеДокументыОтгрузки Цикл
			ДокументОтгрузки = ДанныеДокументаОтгрузки.Ключ;
			
			ДокументОбъект = ДокументОтгрузки.ПолучитьОбъект();
			ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
			
			Если ПустаяСтрока(ОписаниеОшибки) Тогда
				Если ДокументОбъект.Проведен Или ДокументОбъект.Товары.Количество() > 0 Тогда
					ДокументОбъект.Товары.Очистить();
					ДокументОбъект.ШтрихкодыУпаковок.Очистить();
					
					Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОбъект, Ложь, ИмяСобытия);
					Если Не Результат.ЗаписьВыполнена Тогда
						СписокОшибок.Добавить(Результат.ОписаниеОшибки);
					КонецЕсли;
				КонецЕсли;
			Иначе
				СписокОшибок.Добавить(ОписаниеОшибки);
				ПроблемныеДокументы.Добавить(ДокументОтгрузки);
			КонецЕсли;
			
			ДокументОбъект.Разблокировать();
		КонецЦикла;
		
		// Перезаполнить документы отгрузки.
		ЕстьНеактуальныеДокументыОтгрузки = Ложь;
		
		Если РезультатПроверкиЗаказа.ИзмененныеДокументыОтгрузки.Количество() > 0 Тогда
			ДанныеЗаполнения = ДанныеЗаполненияДокументаОтгрузки(Заказ, РезультатПроверкиЗаказа, Автор);
			
			Для Каждого ДанныеДокументаОтгрузки Из РезультатПроверкиЗаказа.ИзмененныеДокументыОтгрузки Цикл
				ДокументОтгрузки = ДанныеДокументаОтгрузки.Ключ;
				
				Если ПроблемныеДокументы.Найти(ДокументОтгрузки) <> Неопределено Тогда
					Продолжить;
				КонецЕсли;
				
				ДанныеЗаполнения.НомерОтправления              = ДанныеДокументаОтгрузки.Значение;
				ДанныеЗаполнения.НомерРодительскогоОтправления = "";
				ДанныеЗаполнения.СтатусЗаказа                  = РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений.Получить(ДанныеДокументаОтгрузки.Значение).Статус;
				
				ОписаниеОшибки = ОформитьДокументОтгрузки(
					ДокументОтгрузки,
					ДанныеЗаполнения,
					СведенияПоТоварам,
					ИмяСобытия);
					
				Если ДокументОтгрузки = Неопределено Или ДокументОтгрузки <> ДанныеДокументаОтгрузки.Ключ Тогда
					ЕстьНеактуальныеДокументыОтгрузки = Истина;
				КонецЕсли;
				
				Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
					СписокОшибок.Добавить(ОписаниеОшибки);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если ЕстьНеактуальныеДокументыОтгрузки Тогда
			ОписаниеОшибки = ЗаписатьСведенияОбОтправлениях(
								Заказ,
								РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений);
			
			Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
				СписокОшибок.Добавить(ОписаниеОшибки);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрСоединить(СписокОшибок, Символы.ПС);

КонецФункции

Функция СоздатьДокументыОтгрузки(РезультатПроверкиЗаказов, СведенияПоТоварам, Автор = Неопределено)

	СписокОшибок = Новый Массив;
	
	Для Каждого ДанныеПоЗаказу Из РезультатПроверкиЗаказов Цикл
		Заказ                   = ДанныеПоЗаказу.Ключ;
		РезультатПроверкиЗаказа = ДанныеПоЗаказу.Значение;
		
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(РезультатПроверкиЗаказа.ВидМаркетплейса);
		
		ДанныеЗаполнения = ДанныеЗаполненияДокументаОтгрузки(Заказ, РезультатПроверкиЗаказа, Автор);
		
		Для Каждого НомерОтправления Из РезультатПроверкиЗаказа.ОтправленияБезДокументовОтгрузки Цикл
			ДокументОтгрузки = Неопределено;
			
			ДанныеЗаполнения.НомерОтправления              = НомерОтправления;
			ДанныеЗаполнения.НомерРодительскогоОтправления = "";
			ДанныеЗаполнения.СтатусЗаказа                  = РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений.Получить(НомерОтправления).Статус;
			
			ОписаниеОшибки = ОформитьДокументОтгрузки(
				ДокументОтгрузки,
				ДанныеЗаполнения,
				СведенияПоТоварам,
				ИмяСобытия);
			
			Если ЗначениеЗаполнено(ДокументОтгрузки) Тогда
				НачатьТранзакцию();
				
				Попытка
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
					ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", Заказ);
					БлокировкаДанных.Заблокировать();
					
					НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Заказ.Установить(Заказ);
					НаборЗаписей.Прочитать();
					
					Запись = Неопределено;
					Для Каждого Запись Из НаборЗаписей Цикл
						Если Запись.НомерОтправления = НомерОтправления Тогда
							Прервать;
						КонецЕсли;
					КонецЦикла;
					
					Если Запись <> Неопределено Тогда
						Если Запись.НомерОтправления <> НомерОтправления Тогда
							Запись = НаборЗаписей.Добавить();
							ЗаполнитьЗначенияСвойств(Запись, НаборЗаписей[0],,
								"ДокументОтгрузки, Статус, ДатаУстановкиСтатуса, НомерОтправления, НомерРодительскогоОтправления");
							Запись.Статус               = Перечисления.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки;
							Запись.ДатаУстановкиСтатуса = ТекущаяДатаСеанса();
							Запись.НомерОтправления     = НомерОтправления;
						КонецЕсли;
						
						Запись.ДокументОтгрузки              = ДокументОтгрузки;
						Запись.НомерРодительскогоОтправления = ДанныеЗаполнения.НомерРодительскогоОтправления;
						
						НаборЗаписей.Записать(Истина);
					КонецЕсли;
					
					ЗафиксироватьТранзакцию();
					
				Исключение
					ОтменитьТранзакцию();
					
					ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'При установке документа отгрузки <%1> заказу <%2> возникла ошибка: %3.';
							|en = 'An error occurred while setting the <%1> shipment document to the <%2> order: %3.'",
							ОбщегоНазначения.КодОсновногоЯзыка()),
						ДокументОтгрузки,
						Заказ,
						ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						
					ЗаписьЖурналаРегистрации(ИмяСобытия,
						УровеньЖурналаРегистрации.Ошибка,,,
						ТекстОшибки);
				КонецПопытки;
			КонецЕсли;
			
			Если Не ПустаяСтрока(ОписаниеОшибки) Тогда
				СписокОшибок.Добавить(ОписаниеОшибки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
		
	Возврат СтрСоединить(СписокОшибок, Символы.ПС);

КонецФункции

Функция ОформитьДокументОтгрузки(ДокументОтгрузки, ДанныеЗаполнения, СведенияПоТоварам, ИмяСобытия = "")

	ОписаниеОшибки = "";
	
	Если ПустаяСтрока(ИмяСобытия) Тогда
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДанныеЗаполнения.ХозяйственнаяОперация) Тогда
		Возврат ОписаниеОшибки;
	КонецЕсли;
	
	УчетнаяЗаписьПредставление =
		Справочники.УчетныеЗаписиМаркетплейсов.ПолноеПредставлениеУчетнойЗаписи(
			ДанныеЗаполнения.ВидМаркетплейса,
			ДанныеЗаполнения.УчетнаяЗапись);
	
	ДанныеЗаполнения.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = '%1. Отгрузка отправления %2';
											|en = '%1. Ship the %2 shipment'"),
										УчетнаяЗаписьПредставление,
										ДанныеЗаполнения.НомерОтправления);
	
	Отбор = Новый Структура;
	Отбор.Вставить("ЗаказКлиента",     ДанныеЗаполнения.ДокументОснование);
	Отбор.Вставить("НомерОтправления", ДанныеЗаполнения.НомерОтправления);
	Отбор.Вставить("КОтгрузке",        Истина);
	
	СтрокиОтправления = СведенияПоТоварам.НайтиСтроки(Отбор);
	Если СтрокиОтправления.Количество() > 0 
			Или ДанныеЗаполнения.СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен Тогда
		ДанныеЗаполнения.Товары = СтрокиОтправления;
		
		Если СтрокиОтправления.Количество() > 0 Тогда
			ДанныеЗаполнения.НомерРодительскогоОтправления = СтрокиОтправления[0].НомерРодительскогоОтправления;
		КонецЕсли;
		
		ДокументОбъект = Неопределено;
		Если ДокументОтгрузки <> Неопределено Тогда
			ДокументОбъект = ДокументОтгрузки.ПолучитьОбъект();	// ДокументОбъект.РеализацияТоваровУслуг, ДокументОбъект.ПередачаТоваровХранителю
			ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
		КонецЕсли;
		
		Если ПустаяСтрока(ОписаниеОшибки) Тогда
			ОписаниеОшибки = ЗаполнитьДокументОтгрузки(ДокументОбъект, ДанныеЗаполнения, ИмяСобытия);
			Если ДокументОтгрузки = Неопределено И ДокументОбъект <> Неопределено Тогда
				ДокументОтгрузки = ДокументОбъект.Ссылка;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ДокументОтгрузки = Неопределено;
	КонецЕсли;
	
	Отбор.Вставить("КОтгрузке", Ложь);
	Отбор.Вставить("Отменено",  Ложь);
	
	СтрокиБезОтгрузки = СведенияПоТоварам.НайтиСтроки(Отбор);
	Если СтрокиБезОтгрузки.Количество() > 0 Тогда
		Если ДанныеЗаполнения.РасширенныеВозможностиЗаказа Тогда
			ШаблонСообщения =
				НСтр("ru = 'В отправлении ""%1"" есть неотгружаемые товары. Для отгрузки установите товарам действие ""%2"".';
					|en = 'There are non-shipped items in the ""%1"" shipment. For shipment, set the goods action to ""%2"".'");
			Параметр2 = Перечисления.ВариантыОбеспечения.Отгрузить;
		Иначе
			ШаблонСообщения =
				НСтр("ru = 'В отправлении ""%1"" есть неотгружаемые товары. Для отгрузки установите заказу статус ""%2"".';
					|en = 'There are non-shipped items in the ""%1"" shipment. For shipment, set the order status to ""%2"".'");
			Параметр2 = Перечисления.СтатусыЗаказовКлиентов.КОтгрузке;
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								ШаблонСообщения,
								ДанныеЗаполнения.НомерОтправления,
								Параметр2);
		
		ОписаниеОшибки = ?(ПустаяСтрока(ОписаниеОшибки), "", Символы.ПС) + ТекстСообщения;
	КонецЕсли;
	
	Возврат ОписаниеОшибки;

КонецФункции

Функция ЗаполнитьДокументОтгрузки(ДокументОтгрузки, ДанныеЗаполнения, ИмяСобытия = "")

	ТекстОшибки = "";
	
	ДанныеПоМенеджеруНакладной = Новый Структура;
	
	Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
		МенеджерНакладной = Документы.ПередачаТоваровХранителю;
		Обработчик = ПередачаТоваровХранителюЛокализация;
		ИмяТаблицыТоваровДокумента = "ТоварыПередачи";
		
		РасширенноеЗаполнениеОстатков = Истина;
		
	ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности Тогда
		МенеджерНакладной = Документы.РеализацияТоваровУслуг;
		Обработчик = РеализацияТоваровУслугЛокализация;
		ИмяТаблицыТоваровДокумента = "ТоварыРеализации";
		
		РасширенноеЗаполнениеОстатков = Истина;
		
		ДанныеПоМенеджеруНакладной.Вставить("ВариантОформленияПродажи", Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);
		ДанныеПоМенеджеруНакладной.Вставить("Статус", Перечисления.СтатусыРеализацийТоваровУслуг.ВПути);
		
	ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту Тогда
		МенеджерНакладной = Документы.РеализацияТоваровУслуг;
		Обработчик = РеализацияТоваровУслугЛокализация;
		ИмяТаблицыТоваровДокумента = "ТоварыРеализации";
		
		РасширенноеЗаполнениеОстатков = Истина;
		
		ДанныеПоМенеджеруНакладной.Вставить("ВариантОформленияПродажи", Перечисления.ВариантыОформленияПродажи.РеализацияТоваровУслуг);
		
	Иначе
		ТекстОшибки = НСтр("ru = 'Указана неверная хозяйственная операция в заказе.';
							|en = 'An incorrect business transaction is specified in the order.'");
	КонецЕсли;
	
	ЭтоНовыйДокумент = (ДокументОтгрузки = Неопределено
							Или ОбщегоНазначения.МенеджерОбъектаПоСсылке(ДокументОтгрузки.Ссылка) <> МенеджерНакладной);
	
	Если ЭтоНовыйДокумент Тогда
		ДокументОтгрузки = МенеджерНакладной.СоздатьДокумент();
		ДокументОтгрузки.Дата = ТекущаяДатаСеанса();
	Иначе
		ДокументОтгрузки.ПометкаУдаления = Ложь;
		
		ДокументОтгрузки.Товары.Очистить();
		ДокументОтгрузки.ШтрихкодыУпаковок.Очистить();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДокументОтгрузки, ДанныеПоМенеджеруНакладной);
	
	ПараметрыОформления = Новый Структура;
	ПараметрыОформления.Вставить("ВводитьНаОснованииНепроведенного", Истина);
	ПараметрыОформления.Вставить("ЗаполнятьПоОстаткам",              Ложь);
	ПараметрыОформления.Вставить("ПоЗаказам",                        Истина);
	ПараметрыОформления.Вставить("ПоОрдерам",                        Ложь);
	
	ДанныеЗаполнения.Вставить("ПараметрыОформления", ПараметрыОформления);
	
	ДокументОтгрузки.Заполнить(ДанныеЗаполнения);
	
	Если ДокументОтгрузки.Товары.Количество() > 0 Тогда
		Отбор = Новый Структура("ЗаказКлиента", ДанныеЗаполнения.ДокументОснование);
		
		СтрокиКУдалению = ДокументОтгрузки.Товары.НайтиСтроки(Отбор);
		Для Каждого СтрокаТовара Из СтрокиКУдалению Цикл
			ДокументОтгрузки.Товары.Удалить(СтрокаТовара);
		КонецЦикла;
	КонецЕсли;
	
	МассивЗаказов = Новый Массив;
	МассивЗаказов.Добавить(ДанныеЗаполнения.ДокументОснование);
	
	ТипЧисло     = Новый ОписаниеТипов("Число");
	ТипЧисло15_3 = Новый ОписаниеТипов("Число",,, Новый КвалификаторыЧисла(15, 3));
	
	Товары = ДокументОтгрузки.Товары.ВыгрузитьКолонки();
	Товары.Колонки.Добавить("КоличествоВНакладной",         ТипЧисло15_3);
	Товары.Колонки.Добавить("Коэффициент",                  ТипЧисло);
	Товары.Колонки.Добавить("КоличествоВПути",              ТипЧисло15_3);
	Товары.Колонки.Добавить("КоличествоУпаковокВПути",      ТипЧисло15_3);
	Товары.Колонки.Добавить("КоличествоВЗаказе",            ТипЧисло15_3);
	Товары.Колонки.Добавить("КоличествоУпаковокВЗаказе",    ТипЧисло15_3);
	Товары.Колонки.Добавить("КоличествоВОрдере",            ТипЧисло15_3);
	Товары.Колонки.Добавить("КоличествоУпаковокВОрдере",    ТипЧисло15_3);
	Товары.Колонки.Добавить("ОрдернаяСхемаПриОтгрузке",     ТипЧисло15_3);
	Товары.Добавить();
	
	ТоварыДокумента = ДокументОтгрузки.Товары.ВыгрузитьКолонки();
	Для Каждого СтрокаТовара Из ДанныеЗаполнения.Товары Цикл
		ЗаполнитьЗначенияСвойств(ТоварыДокумента.Добавить(), СтрокаТовара);
	КонецЦикла;
	
	ДанныеОтбора = Новый Структура(РеквизитыЗаполнения());
	ЗаполнитьЗначенияСвойств(ДанныеОтбора, ДокументОтгрузки);
	ДанныеОтбора.Вставить(ИмяТаблицыТоваровДокумента, ТоварыДокумента);
	
	ПараметрыЗаполнения = Новый Структура;
	Если ИмяТаблицыТоваровДокумента = "ТоварыРеализации" Тогда
		ПараметрыЗаполнения.Вставить("ВариантОформления",                           ДокументОтгрузки.ВариантОформленияПродажи);
	КонецЕсли;
	ПараметрыЗаполнения.Вставить("ПодборПоЗаказамОрдерам",                          Истина);
	ПараметрыЗаполнения.Вставить("ПараметрыОформления",                             ПараметрыОформления);
	ПараметрыЗаполнения.Вставить("ИспользоватьРасширенныеВозможностиЗаказаКлиента", Истина);
	
	Обработчик.ДополнитьПараметрыЗаполнения(ПараметрыЗаполнения, ДокументОтгрузки);
	
	ШтрихкодыУпаковок = Новый ТаблицаЗначений;
	ШтрихкодыУпаковок.Колонки.Добавить("ШтрихкодУпаковки",     Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ШтрихкодыУпаковок.Колонки.Добавить("ИзОрдера",             Новый ОписаниеТипов("Булево"));
	ШтрихкодыУпаковок.Колонки.Добавить("ВыгружатьВРеализацию", Новый ОписаниеТипов("Булево"));
	ШтрихкодыУпаковок.Колонки.Добавить("ЗаказКлиента",         Новый ОписаниеТипов("ДокументСсылка.ЗаказКлиента"));
	ШтрихкодыУпаковок.Колонки.Добавить("Номенклатура",         Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ШтрихкодыУпаковок.Колонки.Добавить("Характеристика",       Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ШтрихкодыУпаковок.Колонки.Добавить("Серия",                Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры"));
	ШтрихкодыУпаковок.Колонки.Добавить("Склад",                Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ШтрихкодыУпаковок.Колонки.Добавить("Использован",          Новый ОписаниеТипов("Булево"));
	
	Если ИмяТаблицыТоваровДокумента = "ТоварыРеализации" Тогда
		МенеджерНакладной.ЗаполнитьПоОстаткамЗаказов(
			ДанныеОтбора,
			Товары,
			ДокументОтгрузки.СкидкиНаценки,
			ДокументОтгрузки.НачислениеБонусныхБаллов,
			ДокументОтгрузки.Склад,
			МассивЗаказов,
			ПараметрыЗаполнения,
			ШтрихкодыУпаковок);
	Иначе
		МенеджерНакладной.ЗаполнитьПоОстаткамЗаказов(
			ДанныеОтбора,
			Товары,
			ДокументОтгрузки.Склад,
			МассивЗаказов,
			ПараметрыЗаполнения,
			ШтрихкодыУпаковок);
	КонецЕсли;
	
	СтрокиПересчета = Новый Массив;
	
	ЕстьСуммаВзаиморасчетов = (Товары.Колонки.Найти("СуммаВзаиморасчетов") <> Неопределено);
	
	Для Каждого СтрокаИсточник Из Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаИсточник.ЗаказКлиента) Тогда
			Продолжить;
		КонецЕсли;
		
		Если ДанныеЗаполнения.СтатусЗаказа = Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен Тогда
			КоличествоКОтгрузке = СтрокаИсточник.КоличествоВЗаказе - СтрокаИсточник.КоличествоВНакладной;
		Иначе
			КоличествоКОтгрузке = СтрокаИсточник.КоличествоВНакладной;
		КонецЕсли;
		
		Если КоличествоКОтгрузке <> 0 Тогда
			СтрокаТовара = ДокументОтгрузки.Товары.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТовара, СтрокаИсточник);
			Если ЕстьСуммаВзаиморасчетов Тогда
				СтрокаТовара.СуммаВзаиморасчетов = 0;
			КонецЕсли;
			
			Если СтрокаТовара.Количество <> КоличествоКОтгрузке Тогда
				СтрокаТовара.Количество         = КоличествоКОтгрузке;
				СтрокаТовара.КоличествоУпаковок = КоличествоКОтгрузке / СтрокаИсточник.Коэффициент;
				
				СтрокиПересчета.Добавить(СтрокаТовара);
			ИначеЕсли СтрокаТовара.Сумма = 0 Тогда
				СтрокиПересчета.Добавить(СтрокаТовара);
			КонецЕсли;
			
			Если РасширенноеЗаполнениеОстатков Тогда
				ОсталосьРаспределитьМарок = СтрокаТовара.КоличествоУпаковок;
				
				Отбор = Новый Структура;
				Отбор.Вставить("Номенклатура",   СтрокаТовара.Номенклатура);
				Отбор.Вставить("Характеристика", СтрокаТовара.Характеристика);
				Отбор.Вставить("Серия",          СтрокаТовара.Серия);
				Отбор.Вставить("Склад",          СтрокаТовара.Склад);
				Отбор.Вставить("Использован",    Ложь);
				
				СтрокиШтрихкодов = ШтрихкодыУпаковок.НайтиСтроки(Отбор);
				Для Каждого СтрокаШтрихкода Из СтрокиШтрихкодов Цикл
					ЗаполнитьЗначенияСвойств(ДокументОтгрузки.ШтрихкодыУпаковок.Добавить(), СтрокаШтрихкода);
					
					СтрокаШтрихкода.Использован = Истина;
					ОсталосьРаспределитьМарок = ОсталосьРаспределитьМарок - 1;
					Если ОсталосьРаспределитьМарок = 0 Тогда
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если СтрокиПересчета.Количество() > 0 Тогда
		СтруктураДействий = Новый Структура;
		
		Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию Тогда
			СтруктураДействий.Вставить("ПересчитатьСумму");
		ИначеЕсли ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
				Или ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту Тогда
			СтруктураПересчетаСуммы = ПакетнаяОбработкаТабличнойЧастиКлиентСервер.ПараметрыПересчетаСуммыНДСВТЧ(ДокументОтгрузки);
			
			СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСуммуСНДС", СтруктураПересчетаСуммы);
			СтруктураДействий.Вставить("ПересчитатьСумму");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомРучнойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомАвтоматическойСкидки", Новый Структура("Очищать", Ложь));
			СтруктураДействий.Вставить("ПересчитатьСуммуАвтоматическойСкидки");
			СтруктураДействий.Вставить("ПересчитатьСуммуСУчетомСкидкиБонуснымиБаллами");
			СтруктураДействий.Вставить("ОчиститьСуммуВзаиморасчетов");
		КонецЕсли;
		
		ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(СтрокиПересчета, 
			СтруктураДействий, 
			ДокументОтгрузки.Товары, 
			Неопределено);
	КонецЕсли;
	
	Если ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности
			Или ДанныеЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияКлиенту Тогда
		ДокументОтгрузки.СкидкиРассчитаны = Истина;
		
		ЗаказыСервер.ЗаполнитьЗаказВШапкеПоЗаказамВТабличнойЧасти(
			ДанныеЗаполнения.ДокументОснование,
			ДокументОтгрузки.Товары,
			"ЗаказКлиента");
		
		ПараметрыУказанияСерий = НоменклатураСервер.ПараметрыУказанияСерий(ДокументОтгрузки, МенеджерНакладной);
		НоменклатураСервер.ЗаполнитьСтатусыУказанияСерий(ДокументОтгрузки, ПараметрыУказанияСерий);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения.Автор) Тогда
		ДокументОтгрузки.Автор = ДанныеЗаполнения.Автор;
	КонецЕсли;
	
	ДокументОтгрузки.Комментарий = ДанныеЗаполнения.Комментарий;
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОтгрузки, Истина, ИмяСобытия);
	Если Не Результат.ЗаписьВыполнена Тогда
		ТекстОшибки = Результат.ОписаниеОшибки;
	ИначеЕсли Не ДокументОтгрузки.Проведен Тогда
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Документ %1 не проведен.';
								|en = 'Document %1 is not posted.'"),
							ДокументОтгрузки.Ссылка);
	КонецЕсли;
	
	Возврат ТекстОшибки;

КонецФункции

Функция ДанныеЗаполненияДокументаОтгрузки(Заказ, РезультатПроверкиЗаказа, Автор = Неопределено)

	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ДокументОснование",             Заказ);
	ДанныеЗаполнения.Вставить("ХозяйственнаяОперация",         РезультатПроверкиЗаказа.ХозяйственнаяОперация);
	ДанныеЗаполнения.Вставить("УчетнаяЗапись",                 РезультатПроверкиЗаказа.УчетнаяЗапись);
	ДанныеЗаполнения.Вставить("ВидМаркетплейса",               РезультатПроверкиЗаказа.ВидМаркетплейса);
	ДанныеЗаполнения.Вставить("НомерОтправления",              "");
	ДанныеЗаполнения.Вставить("НомерРодительскогоОтправления", "");
	ДанныеЗаполнения.Вставить("Товары",                        Новый Массив);
	ДанныеЗаполнения.Вставить("Комментарий",                   "");
	ДанныеЗаполнения.Вставить("Автор",                         Автор);
	ДанныеЗаполнения.Вставить("РасширенныеВозможностиЗаказа",  ПолучитьФункциональнуюОпцию("ИспользоватьРасширенныеВозможностиЗаказаКлиента"));
	ДанныеЗаполнения.Вставить("СтатусЗаказа",                  Перечисления.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка());
	
	Возврат ДанныеЗаполнения;

КонецФункции

Функция РеквизитыЗаполнения()

	Возврат "Ссылка, Дата, ХозяйственнаяОперация, Организация, Партнер, Контрагент, Соглашение, Договор,
			|Валюта, ВалютаВзаиморасчетов, Сделка, НалогообложениеНДС, ЦенаВключаетНДС, ПорядокРасчетов,
			|НаправлениеДеятельности, ТребуетсяЗалогЗаТару, ВернутьМногооборотнуюТару";

КонецФункции

Функция ТекстЗапросаОбновленияСтатусовЗаказовТорговойПлощадки(ПараметрыЗапроса)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК ЗаказКлиентаСсылка,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузкиСсылка,
		|	ЗаказыТорговыхПлощадок.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ЗаказыТорговыхПлощадок.Статус КАК Статус,
		|	ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа КАК ДатаСозданияЗаказа,
		|	ЗаказыТорговыхПлощадок.СрокОтгрузки КАК СрокОтгрузки,
		|	ЗаказыТорговыхПлощадок.СрокДоставки КАК СрокДоставки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки,
		|	ЗаказыТорговыхПлощадок.ДополнительныеСведения КАК ДополнительныеСведения
		|ПОМЕСТИТЬ ТаблицаЗаказов
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И &Условие
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаЗаказов.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки
		|ИЗ
		|	ТаблицаЗаказов КАК ТаблицаЗаказов
		|ГДЕ
		|	ТаблицаЗаказов.ИдентификаторОтгрузки <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаЗаказов.ЗаказКлиентаСсылка КАК ЗаказКлиентаСсылка,
		|	ТаблицаЗаказов.ДокументОтгрузкиСсылка КАК ДокументОтгрузкиСсылка,
		|	ТаблицаЗаказов.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ТаблицаЗаказов.НомерОтправления КАК НомерОтправления,
		|	ТаблицаЗаказов.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ТаблицаЗаказов.Статус КАК Статус,
		|	ТаблицаЗаказов.ДатаСозданияЗаказа КАК ДатаСозданияЗаказа,
		|	ТаблицаЗаказов.СрокОтгрузки КАК СрокОтгрузки,
		|	ТаблицаЗаказов.СрокДоставки КАК СрокДоставки,
		|	ТаблицаЗаказов.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки,
		|	ТаблицаЗаказов.ДополнительныеСведения КАК ДополнительныеСведения
		|ИЗ
		|	ТаблицаЗаказов КАК ТаблицаЗаказов
		|ИТОГИ
		|	МИНИМУМ(ЗаказКлиентаСсылка),
		|	МИНИМУМ(ДатаСозданияЗаказа)
		|ПО
		|	ОБЩИЕ,
		|	ИдентификаторЗаказа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаЗаказов";
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Заказы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ЗаказыТорговыхПлощадок.Заказ В (&Заказы)");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЗапроса.ИдентификаторыЗаказов) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ЗаказыТорговыхПлощадок.ИдентификаторЗаказа В (&ИдентификаторыЗаказов)");
	ИначеЕсли ЗначениеЗаполнено(ПараметрыЗапроса.Статусы) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ЗаказыТорговыхПлощадок.Статус В (&Статусы)");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Условие", "ИСТИНА");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаСвязанныхДокументов()

	ТекстЗапроса =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СвязанныеДокументы.Ссылка КАК Ссылка,
		|	ВЫБОР
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПередачаТоваровХранителю
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПередачаТоваровХранителю).Менеджер
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РеализацияТоваровУслуг).Менеджер
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РасходныйОрдерНаТовары
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РасходныйОрдерНаТовары).Ответственный
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	КОНЕЦ КАК Менеджер,
		|	ВЫБОР
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПередачаТоваровХранителю
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПередачаТоваровХранителю).Автор
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РеализацияТоваровУслуг).Автор
		|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка)
		|	КОНЕЦ КАК Автор,
		|	ВЫБОР
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПередачаТоваровХранителю
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.ПередачаТоваровХранителю).Проведен
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РеализацияТоваровУслуг).Проведен
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РасходныйОрдерНаТовары
		|			ТОГДА ВЫРАЗИТЬ(СвязанныеДокументы.Ссылка КАК Документ.РасходныйОрдерНаТовары).Проведен
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Проведен,
		|	ВЫБОР
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.ПередачаТоваровХранителю
		|			ТОГДА &ПорядокДокументовОтгрузки
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РеализацияТоваровУслуг
		|			ТОГДА &ПорядокДокументовОтгрузки
		|		КОГДА СвязанныеДокументы.Ссылка ССЫЛКА Документ.РасходныйОрдерНаТовары
		|			ТОГДА &ПорядокСкладскихДокументов
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	КритерийОтбора.СвязанныеДокументы(&ЗаказКлиента) КАК СвязанныеДокументы
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(СвязанныеДокументы.Ссылка) В (&Типы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок УБЫВ";

	Замена = "ТИП(Документ.ПередачаТоваровХранителю), ТИП(Документ.РеализацияТоваровУслуг), ТИП(Документ.РасходныйОрдерНаТовары)";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Типы", Замена);

	Возврат ТекстЗапроса;

КонецФункции

Функция ПолучитьРезультатОбновленияСтатусовЗаказов(УчетнаяЗапись, Параметры, Ошибка, НачалоПериода = Неопределено)

	Если Параметры.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Параметры.ОкончаниеПериода = КонецДня(ТекущаяДатаСеанса());
		Параметры.НачалоПериода    = ?(ЗначениеЗаполнено(НачалоПериода),
			НачалоДня(НачалоПериода),
			ДобавитьМесяц(Параметры.ОкончаниеПериода, -12) + 1);
		
		ТаблицаЗаказов = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьРезультатОбновленияСтатусовЗаказовНаТорговойПлощадке(
			УчетнаяЗапись,
			Параметры.НачалоПериода,
			Параметры.ОкончаниеПериода,
			Неопределено,
			Ошибка);
		
	ИначеЕсли Параметры.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда 
		Параметры.ОкончаниеПериода = КонецДня(ТекущаяДатаСеанса());
		Параметры.НачалоПериода    = ?(ЗначениеЗаполнено(НачалоПериода),
			НачалоДня(НачалоПериода),
			ДобавитьМесяц(Параметры.ОкончаниеПериода, -12) + 1);   

		ТаблицаЗаказов = ИнтеграцияСЯндексМаркетСервер.ПолучитьСтатусыЗаказовНаТорговойПлощадке(
			УчетнаяЗапись,
			Параметры.НачалоПериода,
			Параметры.ОкончаниеПериода, 
			Параметры.Заказ,
			Ошибка);		
	Иначе
		ТаблицаЗаказов =
			ИнтеграцияСМаркетплейсамиСервер.ПустаяТаблицаОбъектаМетаданных("РегистрСведений.ЗаказыТорговыхПлощадок", Истина);
		ТаблицаЗаказов.Колонки.Удалить("СрокОтгрузки");
		ТаблицаЗаказов.Колонки.Удалить("СрокДоставки");
		ТаблицаЗаказов.Колонки.Добавить("ДатаОтгрузки", Новый ОписаниеТипов("Дата"));
		ТаблицаЗаказов.Колонки.Добавить("ДатаДоставки", Новый ОписаниеТипов("Дата"));
	КонецЕсли;
	
	Возврат ТаблицаЗаказов;

КонецФункции

Функция ОчищатьДанныеЭкземпляров(ИсточникНастроекУчетнойЗаписи)

	Если Не ЗначениеЗаполнено(ИсточникНастроекУчетнойЗаписи) Тогда
		Возврат Ложь;
	ИначеЕсли ТипЗнч(ИсточникНастроекУчетнойЗаписи) = Тип("СправочникСсылка.УчетныеЗаписиМаркетплейсов") Тогда
		НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(ИсточникНастроекУчетнойЗаписи);
	Иначе
		НастройкиУчетнойЗаписи = ИсточникНастроекУчетнойЗаписи;
	КонецЕсли;
	
	ОчищатьДанныеЭкземпляров = НастройкиУчетнойЗаписи.ОчищатьДанныеЭкземпляров;
	Если ОчищатьДанныеЭкземпляров = Неопределено Тогда
		ОчищатьДанныеЭкземпляров = Истина;
	КонецЕсли;
	
	Возврат ОчищатьДанныеЭкземпляров;

КонецФункции

Функция ОформлятьСначалаНакладные()

	ОформлятьСначалаНакладные =
		Константы.ПорядокОформленияНакладныхРасходныхОрдеров.Получить() = Перечисления.ПорядокОформленияНакладныхРасходныхОрдеров.СначалаНакладные;
	
	Возврат ОформлятьСначалаНакладные;

КонецФункции

Процедура ОбработатьОтменуЗаказов(УчетнаяЗапись, РезультатПроверкиЗаказов, НомераОтправлений, ПричинаОтмены, Ошибка)

	Если ТипЗнч(РезультатПроверкиЗаказов) <> Тип("Соответствие")
			Или Не ЗначениеЗаполнено(НомераОтправлений) Тогда
		Возврат;
	КонецЕсли;
	
	ОформлятьСначалаНакладные = ОформлятьСначалаНакладные();
	
	ОбработанныеЗаказы = Новый Массив;
	
	Для Каждого РезультатПроверкиЗаказа Из РезультатПроверкиЗаказов Цикл
		Заказ = РезультатПроверкиЗаказа.Ключ;
		ДанныеПоНомерамОтправлений = РезультатПроверкиЗаказа.Значение.ДанныеПоНомерамОтправлений;
		
		Для Каждого ДанныеНомераОтправления Из ДанныеПоНомерамОтправлений Цикл
			Если НомераОтправлений.Найти(ДанныеНомераОтправления.Ключ) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ОбработатьСтатусОтправленияВозвращенОтменен(
				УчетнаяЗапись,
				Заказ,
				ДанныеНомераОтправления.Значение.ДокументОтгрузки,
				ПричинаОтмены,
				ОформлятьСначалаНакладные);
		КонецЦикла;
		
		ОбработанныеЗаказы.Добавить(Заказ);
	КонецЦикла;
	
	ОчищатьДанныеЭкземпляров = ОчищатьДанныеЭкземпляров(УчетнаяЗапись);
	
	ЗакрытьЗаказы(УчетнаяЗапись,
		ОбработанныеЗаказы,
		ОчищатьДанныеЭкземпляров,
		Ошибка);

КонецПроцедуры

Процедура ОбработатьСтатусОтправленияВозвращенОтменен(УчетнаяЗапись, Заказ, Распоряжение,
		ПричинаОтмены = Неопределено, ОформлятьСначалаНакладные = Ложь, ИмяСобытия = "")

	Если ПустаяСтрока(ИмяСобытия) Тогда
		ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаСвязанныхДокументов();
	Запрос.УстановитьПараметр("ЗаказКлиента", Заказ);
	
	// Порядок отмены проведения.
	Если ОформлятьСначалаНакладные Тогда
		Запрос.УстановитьПараметр("ПорядокДокументовОтгрузки",  1);
		Запрос.УстановитьПараметр("ПорядокСкладскихДокументов", 2);
		
		ПорядокРаспоряжения = 1;
	Иначе
		Запрос.УстановитьПараметр("ПорядокДокументовОтгрузки",  2);
		Запрос.УстановитьПараметр("ПорядокСкладскихДокументов", 1);
		
		ПорядокРаспоряжения = 2;
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		РаспоряжениеОтменено = Истина;
	Иначе
		РаспоряжениеОтменено = Ложь;
	КонецЕсли;
	
	ПродолжитьОтменуПроведения = Ложь; // Отмена проведения документов по умолчанию не выполняется.
	
	СвязанныеДокументы = Новый Соответствие;
	
	ВыборкаСвязанныхДокументов = РезультатЗапроса.Выбрать();
	Пока ВыборкаСвязанныхДокументов.Следующий() Цикл
		Если ВыборкаСвязанныхДокументов.Порядок = ПорядокРаспоряжения
				И ВыборкаСвязанныхДокументов.Ссылка <> Распоряжение Тогда
			Продолжить;
		КонецЕсли;
		
		Если Не ВыборкаСвязанныхДокументов.Проведен Тогда
			Если ВыборкаСвязанныхДокументов.Ссылка = Распоряжение Тогда
				РаспоряжениеОтменено = Истина;
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		Если ПродолжитьОтменуПроведения Тогда
			ДобавитьПользователяДляОповещенияВСвязанныеДокументы(
				СвязанныеДокументы, ВыборкаСвязанныхДокументов.Ссылка, ВыборкаСвязанныхДокументов.Менеджер);
			ДобавитьПользователяДляОповещенияВСвязанныеДокументы(
				СвязанныеДокументы, ВыборкаСвязанныхДокументов.Ссылка, ВыборкаСвязанныхДокументов.Автор);
			
			ДокументОбъект = ВыборкаСвязанныхДокументов.Ссылка.ПолучитьОбъект();
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(ДокументОбъект, Ложь, ИмяСобытия, Истина);
			Если Не Результат.ЗаписьВыполнена Тогда
				ПродолжитьОтменуПроведения = Ложь;
			ИначеЕсли ВыборкаСвязанныхДокументов.Ссылка = Распоряжение Тогда
				РаспоряжениеОтменено = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если РаспоряжениеОтменено Тогда
		ИмяТаблицы = "Товары";
		ОтменяемыеПозиции = Неопределено;
		
		Если ЗначениеЗаполнено(Распоряжение) Тогда
			РезультатЗапроса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Распоряжение, ИмяТаблицы); // РезультатЗапроса
			Если РезультатЗапроса <> Неопределено Тогда
				ОтменяемыеПозиции = РезультатЗапроса.Выбрать();
			КонецЕсли;
		КонецЕсли;
		
		ОтменитьЗаказ(Заказ, Распоряжение, ПричинаОтмены, ОтменяемыеПозиции, ИмяСобытия, Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(УчетнаяЗапись) Тогда
		НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(УчетнаяЗапись);
		
		Если НастройкиУчетнойЗаписи.СоздаватьЗадачуОповещение = Истина Тогда
			РеквизитыЗаказа = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Заказ, "Менеджер, Автор");
			
			ДобавитьПользователяДляОповещенияВСвязанныеДокументы(СвязанныеДокументы, Заказ, РеквизитыЗаказа.Менеджер);
			ДобавитьПользователяДляОповещенияВСвязанныеДокументы(СвязанныеДокументы, Заказ, РеквизитыЗаказа.Автор);
			
			ДобавитьНапоминания(СвязанныеДокументы);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  СвязанныеДокументы - Соответствие из КлючИЗначение:
//   * Ключ - ДокументСсылка
//   * Значение - Массив из СправочникСсылка.Пользователи
//  Документ - ДокументСсылка
//  Пользователь - СправочникСсылка.Пользователи
//
Процедура ДобавитьПользователяДляОповещенияВСвязанныеДокументы(СвязанныеДокументы, Документ, Пользователь)
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Возврат;
	КонецЕсли;
	
	ПользователиДокумента = СвязанныеДокументы.Получить(Документ); // Массив из СправочникСсылка.Пользователи
	Если ПользователиДокумента = Неопределено Тогда
		СвязанныеДокументы.Вставить(Документ, Новый Массив);
		ПользователиДокумента = СвязанныеДокументы.Получить(Документ);
	КонецЕсли;
	
	ПользователиДокумента.Добавить(Пользователь);
	
КонецПроцедуры

Процедура ЗакрытьЗаказы(УчетнаяЗапись, Заказы, ОчищатьДанныеЭкземпляров, Ошибка, ИмяСобытия = "")

	ЗапросПоСтатусамОтправлений = Новый Запрос;
	ЗапросПоСтатусамОтправлений.Текст =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	СУММА(ВЫБОР
		|			КОГДА ЗаказыТорговыхПлощадок.Статус В (&КонечныеСтатусы)
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
		|		КОНЕЦ) КАК СчетчикНезавершенныхОтправлений
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияЗаказовКлиентов КАК СостоянияЗаказов
		|		ПО ЗаказыТорговыхПлощадок.Заказ = СостоянияЗаказов.Заказ
		|ГДЕ
		|	(ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|				И ЗаказыТорговыхПлощадок.Заказ В (&Заказы)
		|				И СостоянияЗаказов.Состояние В (&СостоянияЗаказов)
		|			ИЛИ СостоянияЗаказов.Заказ ЕСТЬ NULL)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказыТорговыхПлощадок.Заказ
		|
		|ИМЕЮЩИЕ
		|	СУММА(ВЫБОР
		|			КОГДА ЗаказыТорговыхПлощадок.Статус В (&КонечныеСтатусы)
		|				ТОГДА 0
		|			ИНАЧЕ ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
		|		КОНЕЦ) = 0";
	
	ЗапросПоСтатусамОтправлений.Параметры.Вставить("УчетнаяЗапись",   УчетнаяЗапись);
	ЗапросПоСтатусамОтправлений.Параметры.Вставить("Заказы",          Заказы);
	ЗапросПоСтатусамОтправлений.Параметры.Вставить("КонечныеСтатусы", Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовКонечные());
	
	СостоянияЗаказов = Новый Массив;
	СостоянияЗаказов.Добавить(Перечисления.СостоянияЗаказовКлиентов.ГотовКЗакрытию);
	СостоянияЗаказов.Добавить(Перечисления.СостоянияЗаказовКлиентов.Закрыт);
	
	ЗапросПоСтатусамОтправлений.Параметры.Вставить("СостоянияЗаказов", СостоянияЗаказов);
	
	ВыборкаЗаказов = ЗапросПоСтатусамОтправлений.Выполнить().Выбрать();
	Пока ВыборкаЗаказов.Следующий() Цикл
		ЗаказЗакрыт = ЗакрытьЗаказ(ВыборкаЗаказов.Заказ, ИмяСобытия);
		
		Если ЗаказЗакрыт И ОчищатьДанныеЭкземпляров Тогда
			РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.ОчиститьДанныеЭкземпляров(
				ВыборкаЗаказов.Заказ,
				ИмяСобытия);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Функция РезультатОтменыСтрок()

	Результат = Новый Структура;
	Результат.Вставить("ЗаписатьЗаказ",         Ложь);
	Результат.Вставить("ПропущенныеСтроки",     Новый Массив);
	Результат.Вставить("ПересчитываемыеСтроки", Новый Массив);
	Результат.Вставить("ЗамененныеКодыСтрок",   Новый Массив);
	Результат.Вставить("ДокументОтгрузки",      Неопределено);
	
	Возврат Результат;

КонецФункции

Функция ОтменитьСтрокиЗаказаДляЭкземпляра(Таблица, Знач КлючЗаписи, Знач ПричинаОтмены,
			МаксимальныйКодСтроки = 0)

	Результат = РезультатОтменыСтрок();
	
	ДанныеЗаписи =
			РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(ДанныеЗаписи, КлючЗаписи);
	ДанныеЗаписи.Прочитать();
	Если ДанныеЗаписи.Выбран() Тогда
		Отбор = Новый Структура;
		Отбор.Вставить("КодСтроки", ДанныеЗаписи.КодСтроки);
		Результат = ОтменитьСтрокиЗаказа(Таблица, ПричинаОтмены, Отбор, ДанныеЗаписи.Количество,, МаксимальныйКодСтроки);
		
		Если Результат.ЗамененныеКодыСтрок.Количество() > 0 Тогда
			ДанныеЗаписи.КодСтроки = Результат.ЗамененныеКодыСтрок[0].НовыйКодСтроки;
			ДанныеЗаписи.Записать();
			
			Результат.ДокументОтгрузки = ДанныеЗаписи.ДокументОтгрузки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ОтменитьСтрокиЗаказа(Таблица, Знач ПричинаОтмены, Знач Отбор,
			Знач КоличествоОтмены = Неопределено, УпаковкаОтмены = Неопределено, МаксимальныйКодСтроки = 0)

	Результат = РезультатОтменыСтрок();
	
	Если Отбор = Неопределено Тогда
		СтрокиЗаказа = Таблица;
	Иначе
		СтрокиЗаказа = Таблица.НайтиСтроки(Отбор);
	КонецЕсли;
	
	ОбрабатыватьВсеСтроки = (КоличествоОтмены = Неопределено);
	
	Для Каждого СтрокаЗаказа Из СтрокиЗаказа Цикл
		Если ОбрабатыватьВсеСтроки Тогда
			КоличествоОтмены = СтрокаЗаказа.КоличествоУпаковок;
		КонецЕсли;
		
		Если ОбрабатыватьВсеСтроки Или УпаковкаОтмены = Неопределено Тогда
			УпаковкаОтмены = СтрокаЗаказа.Упаковка;
		КонецЕсли;
		
		ЗакрываемоеКоличество = Мин(КоличествоОтмены, СтрокаЗаказа.КоличествоУпаковок);
		КоличествоОтмены = КоличествоОтмены - ЗакрываемоеКоличество;
		
		РазбитьСтроку = (СтрокаЗаказа.КоличествоУпаковок <> ЗакрываемоеКоличество);
		
		Если СтрокаЗаказа.Отменено Или УпаковкаОтмены <> СтрокаЗаказа.Упаковка Тогда
			Результат.ПропущенныеСтроки.Добавить(СтрокаЗаказа.НомерСтроки);
		Иначе
			Если РазбитьСтроку Тогда
				НоваяСтрока = Таблица.Вставить(Таблица.Индекс(СтрокаЗаказа) + 1);
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаЗаказа);
				НоваяСтрока.КоличествоУпаковок = СтрокаЗаказа.КоличествоУпаковок - ЗакрываемоеКоличество;
				Результат.ПересчитываемыеСтроки.Добавить(НоваяСтрока);
				
				МаксимальныйКодСтроки = МаксимальныйКодСтроки + 1;
				
				СтрокаЗаказа.КоличествоУпаковок = ЗакрываемоеКоличество;
				СтрокаЗаказа.КодСтроки = МаксимальныйКодСтроки;
				СтрокаЗаказа.ИдентификаторСтроки = Новый УникальныйИдентификатор();
				Результат.ПересчитываемыеСтроки.Добавить(СтрокаЗаказа);
				
				РезультатРазделения = Новый Структура;
				РезультатРазделения.Вставить("ТекущийКодСтроки", НоваяСтрока.КодСтроки);
				РезультатРазделения.Вставить("НовыйКодСтроки",   СтрокаЗаказа.КодСтроки);
				Результат.ЗамененныеКодыСтрок.Добавить(РезультатРазделения);
			КонецЕсли;
			
			СтрокаЗаказа.Отменено = Истина;
			
			Если ТипЗнч(ПричинаОтмены) = Тип("Структура") Тогда
				СтрокаЗаказа.ПричинаОтмены = ПричинаОтмены.Ссылка;
			Иначе
				СтрокаЗаказа.ПричинаОтмены = ПричинаОтмены;
			КонецЕсли;
			
			Результат.ЗаписатьЗаказ = Истина;
		КонецЕсли;
			
		Если Не ОбрабатыватьВсеСтроки И КоличествоОтмены = 0 Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

Процедура ЗаполнитьИзменяемыеСтрокиДокументовОтгрузки(ИзменяемыеДокументыОтгрузки, РезультатОтменыСтрок)

	Если РезультатОтменыСтрок.ПересчитываемыеСтроки.Количество() > 1
			И ЗначениеЗаполнено(РезультатОтменыСтрок.ДокументОтгрузки) Тогда
		СтрокиДокументаОтгрузки = ИзменяемыеДокументыОтгрузки[РезультатОтменыСтрок.ДокументОтгрузки];
		Если СтрокиДокументаОтгрузки = Неопределено Тогда
			СтрокиДокументаОтгрузки = Новый Массив;
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СтрокиДокументаОтгрузки, РезультатОтменыСтрок.ЗамененныеКодыСтрок);
		ИзменяемыеДокументыОтгрузки.Вставить(РезультатОтменыСтрок.ДокументОтгрузки, СтрокиДокументаОтгрузки);
	КонецЕсли;

КонецПроцедуры

Процедура ИзменитьКодыСтрокВДокументахОтгрузкиИСведенияхПоТоварам(Заказ, ИзменяемыеДокументыОтгрузки,
			ИмяСобытия = "", СообщитьОшибку = Ложь)

	Если Не ЗначениеЗаполнено(ИзменяемыеДокументыОтгрузки) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаменыКодовСтрок = Новый ТаблицаЗначений;
	ЗаменыКодовСтрок.Колонки.Добавить("ТекущийКодСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ЗаменыКодовСтрок.Колонки.Добавить("НовыйКодСтроки", ОбщегоНазначения.ОписаниеТипаЧисло(10));
	ЗаменыКодовСтрок.Колонки.Добавить("ДокументОтгрузки");
	ЗаменыКодовСтрок.Колонки.Добавить("ИдентификаторСтроки", ОбщегоНазначения.ОписаниеТипаСтрока(36));
	
	Для Каждого ДанныеИзменяемогоДокумента Из ИзменяемыеДокументыОтгрузки Цикл
		Если Не ЗначениеЗаполнено(ДанныеИзменяемогоДокумента.Ключ) Тогда
			Для Каждого ЗамененныйКодСтроки Из ДанныеИзменяемогоДокумента.Значение Цикл
				НоваяСтрока = ЗаменыКодовСтрок.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗамененныйКодСтроки);
			КонецЦикла;
			
			Продолжить;
		КонецЕсли;
		
		ДокументОбъект = ДанныеИзменяемогоДокумента.Ключ.ПолучитьОбъект();
		
		ОписаниеОшибки = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
		Если Не ПустаяСтрока(ОписаниеОшибки) И СообщитьОшибку Тогда
			ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
		КонецЕсли;
		
		ЕстьЗамена = Ложь;
		
		Для Каждого ЗамененныйКодСтроки Из ДанныеИзменяемогоДокумента.Значение Цикл
			Отбор = Новый Структура("ЗаказКлиента, КодСтроки", Заказ, ЗамененныйКодСтроки.ТекущийКодСтроки);
			СтрокиДокументаОтгрузки = ДокументОбъект.Товары.НайтиСтроки(Отбор);
			Для Каждого СтрокаТовара Из СтрокиДокументаОтгрузки Цикл
				СтрокаТовара.КодСтроки = ЗамененныйКодСтроки.НовыйКодСтроки;
				
				НоваяСтрока = ЗаменыКодовСтрок.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ЗамененныйКодСтроки);
				НоваяСтрока.ДокументОтгрузки = ДанныеИзменяемогоДокумента.Ключ;
				НоваяСтрока.ИдентификаторСтроки = СтрокаТовара.ИдентификаторСтроки;
			КонецЦикла;
			
			ЕстьЗамена = Истина;
		КонецЦикла;
		
		Если ЕстьЗамена Тогда
			Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(
				ДокументОбъект,
				ДокументОбъект.Проведен,
				ИмяСобытия,
				Не ДокументОбъект.Проведен);
			
			Если Не Результат.ЗаписьВыполнена Тогда
				ЗаписьЖурналаРегистрации(ИмяСобытия,
					УровеньЖурналаРегистрации.Ошибка,
					,,
					Результат.ОписаниеОшибки);
				
				Если СообщитьОшибку Тогда
					ОбщегоНазначения.СообщитьПользователю(Результат.ОписаниеОшибки);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗаменыКодовСтрок.Количество() > 0 Тогда
		НачатьТранзакцию();
		Попытка
			// Блокировка изменения данных по доставке.
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок");
			ЭлементБлокировкиДанных.УстановитьЗначение("Заказ", Заказ);
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Заказ.Установить(Заказ);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Отбор = Новый Структура;
				Отбор.Вставить("ТекущийКодСтроки", Запись.КодСтроки);
				Отбор.Вставить("ИдентификаторСтроки", Запись.ИдентификаторСтроки);
				Отбор.Вставить("ДокументОтгрузки", Запись.ДокументОтгрузки);
				
				СтрокиЗамены = ЗаменыКодовСтрок.НайтиСтроки(Отбор);
				Если СтрокиЗамены.Количество() > 0 Тогда
					Запись.КодСтроки = СтрокиЗамены[0].НовыйКодСтроки;
				КонецЕсли;
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				НаборЗаписей.Записать();
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось изменить коды строк для %1.';
					|en = 'Cannot change line codes for %1.'",
					ОбщегоНазначения.КодОсновногоЯзыка()),
				Заказ);
			
			ЗаписьЖурналаРегистрации(ИмяСобытия,
				УровеньЖурналаРегистрации.Ошибка,
				,,
				ТекстСообщения);
			
			Если СообщитьОшибку Тогда
				ОбщегоНазначения.СообщитьПользователю(ОписаниеОшибки);
			КонецЕсли;
		КонецПопытки;
	КонецЕсли;

КонецПроцедуры

Процедура ДобавитьНапоминания(СвязанныеДокументы)

	ИспользоватьБизнесПроцессыИЗадачи = ПолучитьФункциональнуюОпцию("ИспользоватьБизнесПроцессыИЗадачи");
	ИспользоватьНапоминанияПользователя = ПолучитьФункциональнуюОпцию("ИспользоватьНапоминанияПользователя");
	
	Если Не ИспользоватьБизнесПроцессыИЗадачи И Не ИспользоватьНапоминанияПользователя Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	ДанныеСозданияЗадач = Новый Структура;
	ДанныеСозданияЗадач.Вставить("ДатаЗадачи", ТекущаяДатаСеанса());
	ДанныеСозданияЗадач.Вставить("СрокИсполнения", 7 * 24 * 60 * 60);
	ДанныеСозданияЗадач.Вставить(
		"Наименование",
		НСтр("ru = 'Изменился состав отгружаемого товара, скорректируйте документы и верните товары на склад.';
			|en = 'The goods to ship changed. Correct the documents and return the goods to the warehouse.'"));
	
	Для Каждого КлючЗначение Из СвязанныеДокументы Цикл
		ДокументСсылка = КлючЗначение.Ключ; // ДокументСсылка
		ПользователиДокумента = КлючЗначение.Значение; // Массив из СправочникСсылка.Пользователи
		ПользователиДокумента = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПользователиДокумента);
		
		Для Каждого Пользователь Из ПользователиДокумента Цикл
			Предмет = ДокументСсылка;
			Если ИспользоватьБизнесПроцессыИЗадачи Тогда
				Предмет = СоздатьЗадачуИсполнителю(Пользователь, Предмет, ДанныеСозданияЗадач);
			КонецЕсли;
			Если ИспользоватьНапоминанияПользователя Тогда
				УстановитьНапоминаниеПользователя(ТекущийПользователь, Предмет, Пользователь, ДанныеСозданияЗадач);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

КонецПроцедуры

Функция СоздатьЗадачуИсполнителю(Пользователь, Предмет, ДанныеСозданияЗадач)
	
	УстановитьПривилегированныйРежим(Истина);
	ЗадачаОбъект = Задачи.ЗадачаИсполнителя.СоздатьЗадачу();
	ЗадачаОбъект.Исполнитель = Пользователь;
	ЗадачаОбъект.Предмет = Предмет;
	ЗадачаОбъект.Наименование = ДанныеСозданияЗадач.Наименование;
	ЗадачаОбъект.Дата = ДанныеСозданияЗадач.ДатаЗадачи;
	ЗадачаОбъект.СрокИсполнения = ЗадачаОбъект.Дата + ДанныеСозданияЗадач.СрокИсполнения;
	ЗадачаОбъект.Важность = Перечисления.ВариантыВажностиЗадачи.Обычная;
	ЗадачаОбъект.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ЗадачаОбъект.Ссылка;
	
КонецФункции

Процедура УстановитьНапоминаниеПользователя(ТекущийПользователь, Предмет, Пользователь, ДанныеСозданияЗадач)
	
	НачатьТранзакцию();
	Попытка
		
		УстановитьПривилегированныйРежим(Истина);
		НапоминанияПользователя.УстановитьНапоминание(
			ДанныеСозданияЗадач.Наименование,
			"СрокИсполнения",
			ДанныеСозданияЗадач.СрокИсполнения,
			Предмет);
			
		Если Пользователь <> ТекущийПользователь Тогда
			УстановленныеНапоминания = НапоминанияПользователя.НайтиНапоминания(Предмет);
			УстановленноеНапоминание = УстановленныеНапоминания[0]; // Структура
			
			НаборЗаписей = РегистрыСведений.НапоминанияПользователя.СоздатьНаборЗаписей();
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, УстановленноеНапоминание);
			Запись.Пользователь = Пользователь;
			НаборЗаписей.Записать(РежимЗамещения.Добавление);
			
			НапоминанияПользователя.УдалитьНапоминание(УстановленноеНапоминание);
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При создании напоминания для пользователя возникла ошибка: %1';
				|en = 'An error occurred when creating a reminder for the user: %1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		ЗаписьЖурналаРегистрации(
			ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка, ,
			Предмет,
			ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

Функция ПолучитьДоступныеНомераОтправлений(ВидМаркетплейса, НомераОтправлений, Ошибка,
			ШаблонОшибки, Действие = "")

	ДоступныеНомераОтправлений = НомераОтправлений;
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
			|	МИНИМУМ(ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа) КАК ДатаНачала,
			|	МАКСИМУМ(ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа) КАК ДатаОкончания,
			|	ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки КАК ИдентификаторСклада
			|ИЗ
			|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
			|ГДЕ
			|	ЗаказыТорговыхПлощадок.НомерОтправления В(&НомераОтправлений)
			|
			|СГРУППИРОВАТЬ ПО
			|	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
			|	ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки
			|ИТОГИ
			|	МИНИМУМ(ДатаНачала),
			|	МАКСИМУМ(ДатаОкончания)
			|ПО
			|	УчетнаяЗапись";
		
		Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
		
		УстановитьПривилегированныйРежим(Истина);
		ВыборкаУчетныхЗаписей = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		УстановитьПривилегированныйРежим(Ложь);
		
		Пока ВыборкаУчетныхЗаписей.Следующий() Цикл
			УчетнаяЗапись         = ВыборкаУчетныхЗаписей.УчетнаяЗапись;
			ДатаНачала            = ВыборкаУчетныхЗаписей.ДатаНачала;
			ДатаОкончания         = ВыборкаУчетныхЗаписей.ДатаОкончания;
			ИдентификаторыСкладов = Новый Массив;
			
			ВыборкаИдентификаторовСкладов = ВыборкаУчетныхЗаписей.Выбрать();
			Пока ВыборкаИдентификаторовСкладов.Следующий() Цикл
				ИдентификаторыСкладов.Добавить(ВыборкаИдентификаторовСкладов.ИдентификаторСклада);
			КонецЦикла;
			
			Параметры = ИнтеграцияСМаркетплейсомOzonСервер.НовыйПараметрыЗагрузкиЗаказов();
			Параметры.НачалоПериода    = ДатаНачала;
			Параметры.ОкончаниеПериода = ДатаОкончания;
			Параметры.Склады           = ИдентификаторыСкладов;
			
			ТаблицаЗаказов = ИнтеграцияСМаркетплейсомOzonСервер.ПолучитьЗаказыТорговойПлощадки(
				УчетнаяЗапись,
				Параметры,
				Ошибка);
			
			Если ЗначениеЗаполнено(ТаблицаЗаказов) Тогда
				ДоступныеНомераОтправлений = Новый Массив;
				
				Для Каждого ДанныеОтправления Из ТаблицаЗаказов Цикл
					Если НомераОтправлений.Найти(ДанныеОтправления.НомерОтправления) <> Неопределено
							И ДанныеОтправления.СписокДоступныхДействий[Действие] Тогда
						ДоступныеНомераОтправлений.Добавить(ДанныеОтправления.НомерОтправления);
					КонецЕсли;
				КонецЦикла;
				
				ДоступныеНомераОтправлений =
					ОбщегоНазначенияКлиентСервер.СвернутьМассив(ДоступныеНомераОтправлений);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ДоступныеНомераОтправлений.Количество() = 0 Тогда
		Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			СтрСоединить(НомераОтправлений, ", "));
	Иначе
		ИсключенныеОтправления =
			ОбщегоНазначенияКлиентСервер.РазностьМассивов(НомераОтправлений, ДоступныеНомераОтправлений);
		
		Если ИсключенныеОтправления.Количество() > 0 Тогда
			Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
			Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки,
				СтрСоединить(ИсключенныеОтправления, ", "));
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоступныеНомераОтправлений;

КонецФункции

// Заполняет параметры для заполнения заказа клиента.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов
//  НастройкиУчетнойЗаписи - Структура:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
//   * СпособОтраженияПродажFBS - Строка
//   * Организация - СправочникСсылка.Организации
//   * Партнер - СправочникСсылка.Партнеры
//   * ПокупательПартнер - СправочникСсылка.Партнеры
//   * Контрагент - СправочникСсылка.Контрагенты
//   * ПокупательКонтрагент - СправочникСсылка.Контрагенты
//   * Соглашение - СправочникСсылка.СоглашенияСКлиентами
//   * ПокупательСоглашение - СправочникСсылка.СоглашенияСКлиентами
//   * Договор - СправочникСсылка.ДоговорыКонтрагентов
//   * ПокупательДоговор - СправочникСсылка.ДоговорыКонтрагентов
//   * ДоговорПродажиЧерезСкладыСобственные - СправочникСсылка.ДоговорыКонтрагентов
//   * ВалютаУчета - СправочникСсылка.Валюты
//   * ПричинаОтменыПоУмолчанию - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//  КодыОшибок - см. ИнтеграцияСМаркетплейсамиСервер.КодыОшибок
//
// Возвращаемое значение:
//  Структура:
//   * ВидТорговойПлощадки - ПеречислениеСсылка.ВидыМаркетплейсов
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов 
//   * ВалютаУчета - СправочникСсылка.Валюты
//   * Организация - СправочникСсылка.Организации
//   * Партнер - СправочникСсылка.Партнеры
//   * Контрагент - СправочникСсылка.Контрагенты
//   * Соглашение - СправочникСсылка.СоглашенияСКлиентами
//   * Договор - СправочникСсылка.ДоговорыКонтрагентов
//   * ДанныеПокупателейПоЗаказам - Неопределено
//                                - см. ИнтеграцияСМаркетплейсамиСервер.ДанныеПокупателейПоЗаказам
//   * ХозяйственнаяОперация - ПеречислениеСсылка.ХозяйственныеОперации
//   * ПричинаОтменыПоУмолчанию - СправочникСсылка.ПричиныОтменыЗаказовКлиентов
//   * КодОшибки - Строка
//   * СобытиеЖурналаРегистрации - Строка
//   * ЗагружатьНезаполненныеСтроки - Булево
//   * ПерезаписатьДокументы - Булево
//   * ОчиститьТабличныеЧасти - Булево
//   * ОбработатьВариантОбеспечения - Булево
//   * ПараметрыОбработки - Неопределено
//   * ТекстЗапроса - Строка
//   * ЗаписатьНеобработанныеЗаказы - Булево
//
Функция ПараметрыЗаполненияЗаказаКлиента(УчетнаяЗапись, НастройкиУчетнойЗаписи, КодыОшибок)
	
	ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(НастройкиУчетнойЗаписи.ВидМаркетплейса);
	
	Если НастройкиУчетнойЗаписи.СпособОтраженияПродажFBS = "РеализацияВПути" Тогда
		Партнер  = НастройкиУчетнойЗаписи.ПокупательПартнер;
		Контрагент = НастройкиУчетнойЗаписи.ПокупательКонтрагент;
		Соглашение = НастройкиУчетнойЗаписи.ПокупательСоглашение;
		Договор = НастройкиУчетнойЗаписи.ПокупательДоговор;
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности;
	Иначе
		Партнер = НастройкиУчетнойЗаписи.Партнер;
		Контрагент = НастройкиУчетнойЗаписи.Контрагент;
		Соглашение = НастройкиУчетнойЗаписи.Соглашение;
		Договор = НастройкиУчетнойЗаписи.ДоговорПродажиЧерезСкладыСобственные;
		ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПередачаНаКомиссию;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("ВидТорговойПлощадки",          НастройкиУчетнойЗаписи.ВидМаркетплейса);
	ПараметрыЗаполнения.Вставить("УчетнаяЗапись",                УчетнаяЗапись);
	ПараметрыЗаполнения.Вставить("ВладелецКаталога",             НастройкиУчетнойЗаписи.Партнер);
	ПараметрыЗаполнения.Вставить("ВалютаУчета",                  НастройкиУчетнойЗаписи.ВалютаУчета);
	ПараметрыЗаполнения.Вставить("Организация",                  НастройкиУчетнойЗаписи.Организация);
	ПараметрыЗаполнения.Вставить("Партнер",                      Партнер);
	ПараметрыЗаполнения.Вставить("Контрагент",                   Контрагент);
	ПараметрыЗаполнения.Вставить("Соглашение",                   Соглашение);
	ПараметрыЗаполнения.Вставить("Договор",                      Договор);
	ПараметрыЗаполнения.Вставить("ДанныеПокупателейПоЗаказам",   Неопределено);
	ПараметрыЗаполнения.Вставить("ХозяйственнаяОперация",        ХозяйственнаяОперация);
	ПараметрыЗаполнения.Вставить("ПричинаОтменыПоУмолчанию",     НастройкиУчетнойЗаписи.ПричинаОтменыПоУмолчанию);
	ПараметрыЗаполнения.Вставить("КодОшибки",                    КодыОшибок.ОшибкаПрочие);
	ПараметрыЗаполнения.Вставить("СобытиеЖурналаРегистрации",    ИмяСобытия);
	ПараметрыЗаполнения.Вставить("ЗагружатьНезаполненныеСтроки", Истина);
	ПараметрыЗаполнения.Вставить("ПерезаписатьДокументы",        Истина);
	ПараметрыЗаполнения.Вставить("ОчиститьТабличныеЧасти",       Истина);
	ПараметрыЗаполнения.Вставить("ОбработатьВариантОбеспечения", Истина);
	ПараметрыЗаполнения.Вставить("ПараметрыОбработки",           Неопределено);
	ПараметрыЗаполнения.Вставить("ТекстЗапроса",                 "");
	ПараметрыЗаполнения.Вставить("ЗаписатьНеобработанныеЗаказы", Истина);
	
	Возврат ПараметрыЗаполнения;
	
КонецФункции

Функция ПроверитьОбязательныеПараметрыЗаполнения(ПараметрыЗаполнения, Ошибка)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(ПараметрыЗаполнения.Организация) Тогда
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(ПараметрыЗаполнения.Партнер) Тогда
		Отказ = Истина;
	ИначеЕсли Не ЗначениеЗаполнено(ПараметрыЗаполнения.Контрагент) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ИспользоватьСоглашенияСКлиентами = ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами");
	Если ИспользоватьСоглашенияСКлиентами И Не ЗначениеЗаполнено(ПараметрыЗаполнения.Соглашение) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	ИспользуютсяДоговорыКонтрагентов = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");
	
	Если ИспользоватьСоглашенияСКлиентами Тогда
		ИспользуютсяДоговорыКонтрагентов =
			ЗначениеЗаполнено(ПараметрыЗаполнения.Соглашение)
			И ОбщегоНазначенияУТ.ЗначениеРеквизитаОбъектаТипаБулево(
				ПараметрыЗаполнения.Соглашение, "ИспользуютсяДоговорыКонтрагентов");
	КонецЕсли;
		
	Если ИспользуютсяДоговорыКонтрагентов И Не ЗначениеЗаполнено(ПараметрыЗаполнения.Договор) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Возврат Истина;
	КонецЕсли;
	
	ТекстОшибки = НСтр("ru = 'Загрузка заказов невозможна';
						|en = 'Cannot import orders'");
	ПредставлениеСведений = "";
	Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаЗаполненияДанных;
	
	Если ПараметрыЗаполнения.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		
		Если ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности Тогда
			ПредставлениеСведений = 
				НСтр("ru = 'необходимо указать сведения о розничном покупателе в настройках учетной записи.';
					|en = 'Specify the information about the retail customer in the account settings.'");
		Иначе
			ПредставлениеСведений = 
				НСтр("ru = 'необходимо указать сведения о юридическом лице торговой площадки в настройках учетной записи.';
					|en = 'Specify the information about the legal entity of the trading platform in the account settings.'");
		КонецЕсли;
		
	ИначеЕсли ПараметрыЗаполнения.ВидТорговойПлощадки = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		
		Если ПараметрыЗаполнения.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.РеализацияБезПереходаПраваСобственности Тогда
			ПредставлениеСведений = 
				НСтр("ru = 'необходимо указать сведения о розничном покупателе в настройках обмена данными магазина.';
					|en = 'Specify the information about the retail customer in the store''s data exchange settings.'");
		Иначе
			ПредставлениеСведений = 
				НСтр("ru = 'необходимо указать сведения о торговой площадке в настройках обмена данными магазина.';
					|en = 'Specify the information about the trading platform in the store''s data exchange settings.'");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПустаяСтрока(ПредставлениеСведений) Тогда
		Ошибка.ОписаниеОшибки = СтрШаблон("%1.", ТекстОшибки);
	Иначе
		Ошибка.ОписаниеОшибки = СтрШаблон("%1: %2", ТекстОшибки, ПредставлениеСведений);
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Параметры:
//  Заказы - Массив из ДокументСсылка.ЗаказКлиента
//  РезультатЗаполнения - Структура:
//   * РезультатПроверкиЗаказов - Соответствие из КлючИЗначение:
//      ** Ключ - ДокументСсылка.ЗаказКлиента
//      ** Значение - Структура:
//          *** ДанныеПоНомерамОтправлений - Соответствие из КлючИЗначение:
//               **** Ключ - Строка
//               **** Значение - Структура
//  НомераОтправленийДляСборки - Массив из Строка
// 
// Возвращаемое значение:
//  Массив из ДокументСсылка.ЗаказКлиента
//
Функция ПолностьюСобираемыеЗаказы(Заказы, РезультатЗаполнения, НомераОтправленийДляСборки)
	
	Результат = Новый Массив; // Массив из ДокументСсылка.ЗаказКлиента
	
	Для Каждого Заказ Из Заказы Цикл
		
		РезультатПроверкиЗаказа = РезультатЗаполнения.РезультатПроверкиЗаказов[Заказ];
		
		НомераОтправленийЗаказа = Новый Массив; // Массив из Строка
		Для Каждого КлючЗначение Из РезультатПроверкиЗаказа.ДанныеПоНомерамОтправлений Цикл
			НомераОтправленийЗаказа.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		
		// Заказ должен собираться полностью. Если хотя бы одно отправление не будет собрано, не собираем заказ
		Отказ = Ложь;
		Для Каждого НомерОтправления Из НомераОтправленийЗаказа Цикл
			Если НомераОтправленийДляСборки.Найти(НомерОтправления) = Неопределено Тогда
				Отказ = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		Результат.Добавить(Заказ);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Заказы - Массив из ДокументСсылка.ЗаказКлиента
//
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//   * Заказ - ДокументСсылка.ЗаказКлиента
//   * НомерЗаказа - Строка
//
Функция ВыборкаДанныхПоНомерамЗаказовНаТорговойПлощадке(Заказы)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.Заказ В(&Заказы)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Заказ";
	
	Запрос.УстановитьПараметр("Заказы", Заказы);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Возврат Выборка;
	
КонецФункции

#КонецОбласти

#Область ОтгрузкиСлужебные

// Возвращает выборку документов отгрузки.
//
// Параметры:
//   УчетнаяЗапись      - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   НомераОтправлений  - Массив из Строка - номера отправлений.
//   Итоги              - Булево - признак включения в выборку общих итогов.
//   СведенияПоДоставке - Булево - признак включения в выборку сведений по доставке из документов отправлений.
//
// Возвращаемое значение:
//   ВыборкаИзРезультатаЗапроса - выборка результата запроса.
//
Функция ВыборкаДокументовОтгрузки(УчетнаяЗапись, НомераОтправлений, Итоги = Ложь, СведенияПоДоставке = Ложь)

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	&СведенияПоДоставке КАК СведенияПоДоставке,
		|	ЕСТЬNULL(ЗаказыКлиентов.Проведен, ЛОЖЬ) КАК ЗаказПроведен,
		|	ЕСТЬNULL(ПередачиТоваровХранителю.Проведен, ЕСТЬNULL(РеализацииТоваровУслуг.Проведен, ЛОЖЬ)) КАК ДокументОтгрузкиПроведен,
		|	ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовРаботе) КАК ЗаказВРаботе
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказыКлиентов
		|		ПО ЗаказыТорговыхПлощадок.Заказ = ЗаказыКлиентов.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПередачаТоваровХранителю КАК ПередачиТоваровХранителю
		|		ПО ЗаказыТорговыхПлощадок.ДокументОтгрузки = ПередачиТоваровХранителю.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацииТоваровУслуг
		|		ПО ЗаказыТорговыхПлощадок.ДокументОтгрузки = РеализацииТоваровУслуг.Ссылка
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.НомерОтправления В(&НомераОтправлений)";
	
	Если Итоги Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовРаботе)",
			"ВЫБОР
			|		КОГДА ЗаказыТорговыхПлощадок.Статус В (&СтатусыЗаказовРаботе)
			|			ТОГДА ВЫРАЗИТЬ(1 КАК ЧИСЛО(17, 0))
			|		ИНАЧЕ 0
			|	КОНЕЦ");
		
		Запрос.Текст = Запрос.Текст + "
			|ИТОГИ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ НомерОтправления),
			|	СУММА(ЗаказВРаботе)
			|ПО
			|	ОБЩИЕ";
	КонецЕсли;
	
	ТекстСведенийПоДоставке = "";
	Если СведенияПоДоставке Тогда
		ТекстСведенийПоДоставке = "
			|	ЗаказыТорговыхПлощадок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку) КАК ЗаказПередаетсяВДоставку,
			|	ЕСТЬNULL(ЗаказыКлиентов.СпособДоставки, ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка)) КАК ЗаказСпособДоставки,
			|	ЕСТЬNULL(ЗаказыКлиентов.ЗонаДоставки, ЗНАЧЕНИЕ(Справочник.ЗоныДоставки.ПустаяСсылка)) КАК ЗаказЗонаДоставки,
			|	ЕСТЬNULL(ЗаказыКлиентов.ПеревозчикПартнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)) КАК ЗаказПеревозчикПартнер,
			|	ЕСТЬNULL(ЗаказыКлиентов.Курьер, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ЗаказКурьер,
			|	ЕСТЬNULL(ЗаказыКлиентов.АдресДоставкиПеревозчика, """") КАК ЗаказАдресДоставкиПеревозчика,
			|	ЕСТЬNULL(ЗаказыКлиентов.АдресДоставкиПеревозчикаЗначение, """") КАК ЗаказАдресДоставкиПеревозчикаЗначение,
			|	ЕСТЬNULL(ЗаказыКлиентов.АдресДоставкиПеревозчикаЗначенияПолей, """") КАК ЗаказАдресДоставкиПеревозчикаЗначенияПолей,
			|	ЕСТЬNULL(ЗаказыКлиентов.ВремяДоставкиС, ДАТАВРЕМЯ(1, 1, 1)) КАК ЗаказВремяДоставкиС,
			|	ЕСТЬNULL(ЗаказыКлиентов.ВремяДоставкиПо, ДАТАВРЕМЯ(1, 1, 1)) КАК ЗаказВремяДоставкиПо,
			|
			|	ТИПЗНАЧЕНИЯ(ЗаказыТорговыхПлощадок.ДокументОтгрузки) КАК ТипДокументаОтгрузки,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.СпособДоставки, ЕСТЬNULL(РеализацииТоваровУслуг.СпособДоставки, ЗНАЧЕНИЕ(Перечисление.СпособыДоставки.ПустаяСсылка))) КАК ДокументОтгрузкиСпособДоставки,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.ЗонаДоставки, ЕСТЬNULL(РеализацииТоваровУслуг.ЗонаДоставки, ЗНАЧЕНИЕ(Справочник.ЗоныДоставки.ПустаяСсылка))) КАК ДокументОтгрузкиЗонаДоставки,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.ПеревозчикПартнер, ЕСТЬNULL(РеализацииТоваровУслуг.ПеревозчикПартнер, ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка))) КАК ДокументОтгрузкиПеревозчикПартнер,
			|	ЕСТЬNULL(РеализацииТоваровУслуг.Курьер, ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)) КАК ДокументОтгрузкиКурьер,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.АдресДоставкиПеревозчика, ЕСТЬNULL(РеализацииТоваровУслуг.АдресДоставкиПеревозчика, """")) КАК ДокументОтгрузкиАдресДоставкиПеревозчика,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.АдресДоставкиПеревозчикаЗначение, ЕСТЬNULL(РеализацииТоваровУслуг.АдресДоставкиПеревозчикаЗначение, """")) КАК ДокументОтгрузкиАдресДоставкиПеревозчикаЗначение,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.АдресДоставкиПеревозчикаЗначенияПолей, ЕСТЬNULL(РеализацииТоваровУслуг.АдресДоставкиПеревозчикаЗначенияПолей, """")) КАК ДокументОтгрузкиАдресДоставкиПеревозчикаЗначенияПолей,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.ВремяДоставкиС, ЕСТЬNULL(РеализацииТоваровУслуг.ВремяДоставкиС, ДАТАВРЕМЯ(1, 1, 1))) КАК ДокументОтгрузкиВремяДоставкиС,
			|	ЕСТЬNULL(ПередачиТоваровХранителю.ВремяДоставкиПо, ЕСТЬNULL(РеализацииТоваровУслуг.ВремяДоставкиПо, ДАТАВРЕМЯ(1, 1, 1))) КАК ДокументОтгрузкиВремяДоставкиПо,
			|";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&СведенияПоДоставке КАК СведенияПоДоставке,",
		ТекстСведенийПоДоставке);
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",          УчетнаяЗапись);
	Запрос.УстановитьПараметр("НомераОтправлений",      НомераОтправлений);
	
	Статусы = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе();
	Статусы.Добавить(Перечисления.СтатусыЗаказовТорговыхПлощадок.Доставлен);
	
	Запрос.УстановитьПараметр("СтатусыЗаказовРаботе", Статусы);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Итоги Тогда
		ВыборкаРезультатаЗапроса = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Иначе
		ВыборкаРезультатаЗапроса = РезультатЗапроса.Выбрать();
	КонецЕсли;
	
	Возврат ВыборкаРезультатаЗапроса;

КонецФункции

// Возвращает выборку транспортных накладных или заданий на перевозку по транспортным накладным.
//
// Параметры:
//   Организация           - СправочникСсылка.Организации - организация.
//   ИдентификаторОтгрузки - Строка - идентификатор отгрузки.
//   ЗаданиеНаПеревозку    - Булево - вариант ответа:
//                                       для получения данных по ТТН значение Ложь;
//                                       для получения данных по заданию на перевозку значение Истина.
//
// Возвращаемое значение:
//   ВыборкаИзРезультатаЗапроса - выборка результата запроса.
//
Функция ВыборкаДокументовПеревозки(Организация, ИдентификаторОтгрузки, ЗаданиеНаПеревозку = Ложь)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(ТранспортныеНакладные.Ссылка, НЕОПРЕДЕЛЕНО) КАК Ссылка,
		|	&Поля КАК Поля,
		|	ЕСТЬNULL(ТранспортныеНакладные.Проведен, ЛОЖЬ) КАК Проведен,
		|	ЕСТЬNULL(ТранспортныеНакладные.ПометкаУдаления, ИСТИНА) КАК ПометкаУдаления
		|ИЗ
		|	Документ.ТранспортнаяНакладная КАК ТранспортныеНакладные";
	
	ТекстЗаменыПолей = "";
	Если ЗаданиеНаПеревозку Тогда
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"ТранспортныеНакладные.",
			"ЗаданияНаПеревозку.");
		
		ТекстЗаменыПолей = "ЕСТЬNULL(ЗаданияНаПеревозку.Статус, ЗНАЧЕНИЕ(Перечисление.СтатусыЗаданийНаПеревозку.ПустаяСсылка)) КАК Статус,";
		
		ТекстЗапроса = ТекстЗапроса + "
			|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку КАК ЗаданияНаПеревозку
			|		ПО ТранспортныеНакладные.ЗаданиеНаПеревозку = ЗаданияНаПеревозку.Ссылка";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&Поля КАК Поля,",
		ТекстЗаменыПолей);
	
	ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ТранспортныеНакладные.Организация = &Организация
		|	И ТранспортныеНакладные.Идентификатор = &Идентификатор";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("Идентификатор", ИдентификаторОтгрузки);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат РезультатЗапроса.Выбрать();

КонецФункции

// Выполняет дополнительные действия, связанные с отгрузкой:
//   - обновление статусов заказов,
//   - запись связи отгрузки с отправлениями,
//   - проверка отправлений.
//
// Параметры:
//   ИнформацияОбОтгрузке     - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   МетодДоставки            - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//   ЗаполнитьСвязьСОтгрузкой - Булево - признак связки отправлений с отгрузкой.
//
Процедура ВыполнитьДополнительныеДействияСОтгрузкой(ИнформацияОбОтгрузке, МетодДоставки, ЗаполнитьСвязьСОтгрузкой = Ложь)

	Если ПустаяСтрока(ИнформацияОбОтгрузке.Ошибка.ОписаниеОшибки) Тогда
		Если ЗаполнитьСвязьСОтгрузкой Тогда
			ВыполнитьОбновлениеСтатусовЗаказов(ИнформацияОбОтгрузке);
			ЗаполнитьСвязьОтправленийСОтгрузкой(ИнформацияОбОтгрузке);
			ОбновитьНастройкиДоставкиВОтправлениях(ИнформацияОбОтгрузке, МетодДоставки);
			ОбновитьДанныеПоДоставке(ИнформацияОбОтгрузке);
		КонецЕсли;
		
		ВыполнитьПроверкуОтправленийВОтгрузке(ИнформацияОбОтгрузке);
		ЗаполнитьМассыИзНакладных(ИнформацияОбОтгрузке);
	КонецЕсли;

КонецПроцедуры

// Выполняет обновление статусов заказов по данным торговой площадки.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Процедура ВыполнитьОбновлениеСтатусовЗаказов(ИнформацияОбОтгрузке)

	Ошибка = ОбновитьСтатусыЗаказовТорговойПлощадки(ИнформацияОбОтгрузке.УчетнаяЗапись,, ИнформацияОбОтгрузке);
	
	ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);

КонецПроцедуры

// Вызывает заполнение связи отправлений с отгрузкой на торговой площадке.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Процедура ЗаполнитьСвязьОтправленийСОтгрузкой(ИнформацияОбОтгрузке)

	НомераОтправлений = ОбщегоНазначения.СкопироватьРекурсивно(ИнформацияОбОтгрузке.ОтправленияВОтгрузке);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(НомераОтправлений, ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки);
	
	Если ИнформацияОбОтгрузке.ОтгрузкаОтменена Тогда
		ИдентификаторОтгрузки = "";
	Иначе
		ИдентификаторОтгрузки = ИнформацияОбОтгрузке.ИдентификаторОтгрузки;
	КонецЕсли;
	
	Ошибка = ЗаполнитьИдентификаторОтгрузкиВОтправлениях(
		ИнформацияОбОтгрузке.УчетнаяЗапись,
		НомераОтправлений,
		ИдентификаторОтгрузки);
	
	ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);

КонецПроцедуры

// Заполняет связь отправлений с отгрузкой на торговой площадке.
//
// Параметры:
//   УчетнаяЗапись          - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   ОтправленияВОтгрузке   - Массив из Строка - список обрабатываемых отправлений.
//   ОтправленияНеВОтгрузке - Массив из Строка - список обрабатываемых отправлений.
//   ИдентификаторОтгрузки  - Строка - идентификатор отгрузки, в которую входят отправления.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ЗаполнитьИдентификаторОтгрузкиВОтправлениях(УчетнаяЗапись, ОтправленияВОтгрузке, ИдентификаторОтгрузки = "")

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ЗаполнитьСвязьСОтгрузкой");
	
	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись КАК УчетнаяЗапись,
		|	ЗаказыТорговыхПлощадок.Статус КАК Статус,
		|	ЗаказыТорговыхПлощадок.СрокОтгрузки КАК СрокОтгрузки,
		|	ЗаказыТорговыхПлощадок.СрокДоставки КАК СрокДоставки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторЗаказа КАК ИдентификаторЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа КАК НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки КАК ИдентификаторСкладаТорговойПлощадки,
		|	ЗаказыТорговыхПлощадок.ДатаУстановкиСтатуса КАК ДатаУстановкиСтатуса,
		|	ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа КАК ДатаСозданияЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления КАК НомерРодительскогоОтправления,
		|	ЗаказыТорговыхПлощадок.НаименованиеСкладаТорговойПлощадки КАК НаименованиеСкладаТорговойПлощадки,
		|	ЗаказыТорговыхПлощадок.ТребуетсяЗаполнениеСведенийПоТоварам КАК ТребуетсяЗаполнениеСведенийПоТоварам,
		|	ЗаказыТорговыхПлощадок.ТребуетсяПолучениеЭтикеток КАК ТребуетсяПолучениеЭтикеток,
		|	ЗаказыТорговыхПлощадок.НомерЗаданияПолученияЭтикеток КАК НомерЗаданияПолученияЭтикеток,
		|	ВЫБОР
		|		КОГДА ЗаказыТорговыхПлощадок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен)
		|			ТОГДА """"
		|		ИНАЧЕ &ИдентификаторОтгрузки
		|	КОНЕЦ КАК ИдентификаторОтгрузки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузкиТекущий,
		|	ЗаказыТорговыхПлощадок.ДополнительныеСведения КАК ДополнительныеСведения,
		|	ЗаказыТорговыхПлощадок.ОписаниеОшибки КАК ОписаниеОшибки
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.НомерОтправления В(&ОтправленияВОтгрузке)
		|	И ВЫБОР
		|			КОГДА ЗаказыТорговыхПлощадок.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен)
		|				ТОГДА """"
		|			ИНАЧЕ &ИдентификаторОтгрузки
		|		КОНЕЦ <> ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись,
		|	ЗаказыТорговыхПлощадок.Статус,
		|	ЗаказыТорговыхПлощадок.СрокОтгрузки,
		|	ЗаказыТорговыхПлощадок.СрокДоставки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерОтправления,
		|	ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки,
		|	ЗаказыТорговыхПлощадок.ДатаУстановкиСтатуса,
		|	ЗаказыТорговыхПлощадок.ДатаСозданияЗаказа,
		|	ЗаказыТорговыхПлощадок.НомерРодительскогоОтправления,
		|	ЗаказыТорговыхПлощадок.НаименованиеСкладаТорговойПлощадки,
		|	ЗаказыТорговыхПлощадок.ТребуетсяЗаполнениеСведенийПоТоварам,
		|	ЗаказыТорговыхПлощадок.ТребуетсяПолучениеЭтикеток,
		|	ЗаказыТорговыхПлощадок.НомерЗаданияПолученияЭтикеток,
		|	"""",
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки,
		|	ЗаказыТорговыхПлощадок.ДополнительныеСведения,
		|	ЗаказыТорговыхПлощадок.ОписаниеОшибки
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И НЕ ЗаказыТорговыхПлощадок.НомерОтправления В (&ОтправленияВОтгрузке)
		|	И ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки = &ИдентификаторОтгрузки
		|	И ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки <> """"";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",         УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИдентификаторОтгрузки", ИдентификаторОтгрузки);
	Запрос.УстановитьПараметр("ОтправленияВОтгрузке",  ОтправленияВОтгрузке);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса  = Запрос.Выполнить();
	КоличествоЗаписей = 0;
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ВыборкаДанных     = РезультатЗапроса.Выбрать();
		КоличествоЗаписей = ВыборкаДанных.Количество();
		
		НачатьТранзакцию();
		
		Попытка
			// Блокировка изменения заказов торговых площадок.
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыТорговыхПлощадок");
			ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапроса;
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Заказ", "Заказ");
			ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ДокументОтгрузки", "ДокументОтгрузки");
			БлокировкаДанных.Заблокировать();
			
			Пока ВыборкаДанных.Следующий() Цикл
				Если ВыборкаДанных.ИдентификаторОтгрузки <> ВыборкаДанных.ИдентификаторОтгрузкиТекущий Тогда
					НаборЗаписей = РегистрыСведений.ЗаказыТорговыхПлощадок.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.Заказ.Установить(ВыборкаДанных.Заказ);
					НаборЗаписей.Отбор.ДокументОтгрузки.Установить(ВыборкаДанных.ДокументОтгрузки);
					
					Запись = НаборЗаписей.Добавить();
					ЗаполнитьЗначенияСвойств(Запись, ВыборкаДанных);
					НаборЗаписей.Записать(Истина);
				КонецЕсли;
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			
			ОписаниеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
			Ошибка.ОписаниеОшибки = ОписаниеОшибки;
			
			ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
			
			ЗаписьЖурналаРегистрации(
				ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОписаниеОшибки);
		КонецПопытки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		КоличествоЗаписей / 1000);
	
	Возврат Ошибка;

КонецФункции

// Проверяет данные по отправлениям отгрузки в учетной системе и дополняет описание ошибки для отгрузки.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
// Возвращаемое значение:
//   см. ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка.
//
Функция ПроверитьОтправления(ИнформацияОбОтгрузке)

	Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
	Ошибка.Детализация = Новый Массив;
	
	ТаблицаОтправлений = Новый ТаблицаЗначений;
	ТаблицаОтправлений.Колонки.Добавить("НомерОтправления", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(50)));
	
	Для Каждого НомерОтправления Из ИнформацияОбОтгрузке.ОтправленияВОтгрузке Цикл
		НоваяСтрокаТаблицы = ТаблицаОтправлений.Добавить();
		НоваяСтрокаТаблицы.НомерОтправления = НомерОтправления;
	КонецЦикла;
	
	Для Каждого НомерОтправления Из ИнформацияОбОтгрузке.ОтправленияВПланеОтгрузки Цикл
		НоваяСтрокаТаблицы = ТаблицаОтправлений.Добавить();
		НоваяСтрокаТаблицы.НомерОтправления = НомерОтправления;
	КонецЦикла;
	
	Для Каждого НомерОтправления Из ИнформацияОбОтгрузке.ОтправленияНеВОтгрузке Цикл
		НоваяСтрокаТаблицы = ТаблицаОтправлений.Добавить();
		НоваяСтрокаТаблицы.НомерОтправления = НомерОтправления;
	КонецЦикла;
	
	КоличествоОтправлений = ТаблицаОтправлений.Количество();
	
	Если КоличествоОтправлений = 0 Тогда
		Возврат Ошибка;
	КонецЕсли;
	
	ПустыеДокументовОтгрузки = Новый Массив;
	ПустыеДокументовОтгрузки.Добавить(Неопределено);
	ПустыеДокументовОтгрузки.Добавить(Документы.ПередачаТоваровХранителю.ПустаяСсылка());
	ПустыеДокументовОтгрузки.Добавить(Документы.РеализацияТоваровУслуг.ПустаяСсылка());
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ПроверитьОтправления");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТаблицаОтправлений.НомерОтправления КАК НомерОтправления
		|ПОМЕСТИТЬ ТаблицаОтправлений
		|ИЗ
		|	&ТаблицаОтправлений КАК ТаблицаОтправлений
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерОтправления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОтправлений.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Заказ КАК Заказ,
		|	ЗаказыТорговыхПлощадок.ДокументОтгрузки КАК ДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.Статус КАК Статус,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки
		|ПОМЕСТИТЬ ЗаказыТорговыхПлощадок
		|ИЗ
		|	ТаблицаОтправлений КАК ТаблицаОтправлений
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ПО (ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись)
		|			И ТаблицаОтправлений.НомерОтправления = ЗаказыТорговыхПлощадок.НомерОтправления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Заказ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТаблицаОтправлений
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	ЗаказыТорговыхПлощадок.Заказ ЕСТЬ NULL
		|		ИЛИ ЕСТЬNULL(Заказы.ПометкаУдаления, ЛОЖЬ) КАК НезагруженЗаказ,
		|	ЕСТЬNULL(Заказы.ПометкаУдаления, ЛОЖЬ) КАК ЗаказТребуетПовторнойЗагрузки,
		|	НЕ ЗаказыТорговыхПлощадок.ДокументОтгрузки ЕСТЬ NULL
		|		И ЗаказыТорговыхПлощадок.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен)
		|		И ЗаказыТорговыхПлощадок.ДокументОтгрузки В (&ПустыеДокументовОтгрузки) КАК НеоформленДокументОтгрузки,
		|	ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки КАК ИдентификаторОтгрузки,
		|	ЗаказыТорговыхПлощадок.Статус КАК Статус
		|ПОМЕСТИТЬ ДанныеПоОтправлениям
		|ИЗ
		|	ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК Заказы
		|		ПО ЗаказыТорговыхПлощадок.Заказ = Заказы.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Статус,
		|	ИдентификаторОтгрузки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЗаказыТорговыхПлощадок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоОтправлениям.НомерОтправления КАК НомерОтправления
		|ИЗ
		|	ДанныеПоОтправлениям КАК ДанныеПоОтправлениям
		|ГДЕ
		|	ДанныеПоОтправлениям.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен)
		|	И ДанныеПоОтправлениям.ИдентификаторОтгрузки = &ИдентификаторОтгрузки
		|	И &ИдентификаторОтгрузки <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.ИдентификаторОтгрузки = &ИдентификаторОтгрузки
		|	И &ИдентификаторОтгрузки <> """"
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоОтправлениям.НомерОтправления КАК НомерОтправления
		|ИЗ
		|	ДанныеПоОтправлениям КАК ДанныеПоОтправлениям
		|ГДЕ
		|	ДанныеПоОтправлениям.НеоформленДокументОтгрузки
		|	И НЕ ДанныеПоОтправлениям.НомерОтправления В (&ОтправленияНеВОтгрузке)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоОтправлениям.НомерОтправления КАК НомерОтправления,
		|	ДанныеПоОтправлениям.ЗаказТребуетПовторнойЗагрузки КАК ЗаказТребуетПовторнойЗагрузки
		|ИЗ
		|	ДанныеПоОтправлениям КАК ДанныеПоОтправлениям
		|ГДЕ
		|	ДанныеПоОтправлениям.НезагруженЗаказ
		|	И НЕ ДанныеПоОтправлениям.НомерОтправления В (&ОтправленияНеВОтгрузке)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерОтправления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДанныеПоОтправлениям";
	
	Запрос.УстановитьПараметр("УчетнаяЗапись",            ИнформацияОбОтгрузке.УчетнаяЗапись);
	Запрос.УстановитьПараметр("ИдентификаторОтгрузки",    ИнформацияОбОтгрузке.ИдентификаторОтгрузки);
	Запрос.УстановитьПараметр("ОтправленияНеВОтгрузке",   ИнформацияОбОтгрузке.ОтправленияНеВОтгрузке);
	Запрос.УстановитьПараметр("ТаблицаОтправлений",       ТаблицаОтправлений);
	Запрос.УстановитьПараметр("ПустыеДокументовОтгрузки", ПустыеДокументовОтгрузки);
	
	УстановитьПривилегированныйРежим(Истина);
	ПакетЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ИнформацияОбОтгрузке.ОтправленияНезагруженные.Очистить();
	ТребуетсяПовторнаяЗагрузкаЗаказов = Ложь;
	
	ВыборкаПоОтсутствующимОтправлениям = ПакетЗапроса[ПакетЗапроса.ВГраница()-1].Выбрать();
	Пока ВыборкаПоОтсутствующимОтправлениям.Следующий() Цикл
		ИнформацияОбОтгрузке.ОтправленияНезагруженные.Добавить(ВыборкаПоОтсутствующимОтправлениям.НомерОтправления);
		
		Если ВыборкаПоОтсутствующимОтправлениям.ЗаказТребуетПовторнойЗагрузки Тогда
			ТребуетсяПовторнаяЗагрузкаЗаказов = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ИнформацияОбОтгрузке.ОтгрузкаОтменена
			И Не ИнформацияОбОтгрузке.ПовторитьЗапрос
			И ИнформацияОбОтгрузке.ОтправленияНезагруженные.Количество() > 0 Тогда
		Если ТребуетсяПовторнаяЗагрузкаЗаказов Тогда
			Шаблон = НСтр("ru = 'В отгрузку включены отправления, которые не загружены в учетную систему или требуют повторной загрузки: %1.';
							|en = 'The shipment includes shipments that are not imported to the accounting system or require reimport: %1.'");
		Иначе
			Шаблон = НСтр("ru = 'В отгрузку включены отправления, которые не загружены в учетную систему: %1.';
							|en = 'The shipment includes shipments that are not imported to the accounting system: %1.'");
		КонецЕсли;
		
		Ошибка.Детализация.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон + " " + НСтр("ru = 'Выполните загрузку заказов (в подменю ""Действия"").';
								|en = 'Import orders (in the ""Actions"" submenu).'"),
			СтрСоединить(ИнформацияОбОтгрузке.ОтправленияНезагруженные, ", ")));
	КонецЕсли;
	
	Если Не ИнформацияОбОтгрузке.ОтгрузкаОтменена
			И Не ИнформацияОбОтгрузке.ПовторитьЗапрос
			И Не ПакетЗапроса[ПакетЗапроса.ВГраница()-2].Пустой() Тогда
		Ошибка.Детализация.Добавить(
			НСтр("ru = 'Для некоторых отправлений необходимо оформить документы отгрузки.';
				|en = 'For some shipments, it is necessary to issue shipping documents.'"));
	КонецЕсли;
	
	ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Очистить();
	
	ВыборкаПоПривязаннымОтправлениям = ПакетЗапроса[ПакетЗапроса.ВГраница()-3].Выбрать();
	Пока ВыборкаПоПривязаннымОтправлениям.Следующий() Цикл
		ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Добавить(ВыборкаПоПривязаннымОтправлениям.НомерОтправления);
	КонецЦикла;
	
	ИнформацияОбОтгрузке.ОтправленияОтмененные.Очистить();
	
	ВыборкаПоОтмененнымОтправлениям = ПакетЗапроса[ПакетЗапроса.ВГраница()-4].Выбрать();
	Пока ВыборкаПоОтмененнымОтправлениям.Следующий() Цикл
		ИнформацияОбОтгрузке.ОтправленияОтмененные.Добавить(ВыборкаПоОтмененнымОтправлениям.НомерОтправления);
	КонецЦикла;
	
	Если Ошибка.Детализация.Количество() > 0 Тогда
		Ошибка.КодОшибки = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		КоличествоОтправлений / 1000);
	
	Возврат Ошибка;

КонецФункции

// Выполняет запись ТТН в привилегированном режиме.
//
// Параметры:
//   ДокументОбъект - ДокументСсылка.ТранспортнаяНакладная - ТТН.
//   Провести       - Булево - признак проведения или записи.
//   ИмяСобытия     - Строка - имя события журнала регистрации.
//
// Возвращаемое значение:
//   Структура - результат записи документов:
//     * ЗаписьВыполнена - Булево - результат записи;
//     * ОписаниеОшибки  - Строка - описание ошибки при выполнении процедуры записи.
//
Функция ЗаписатьТТНСУчетомПрав(ДокументОбъект, Провести, ИмяСобытия)

	ДоступноДобавлениеТТН = ПравоДоступа("Добавление", Метаданные.Документы.ТранспортнаяНакладная);
	
	Если Не ДоступноДобавлениеТТН Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(
		ДокументОбъект,
		Провести,
		ИмяСобытия,
		,
		Ложь);
	
	Если Не ДоступноДобавлениеТТН Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

#КонецОбласти

#Область ЗаполнениеМассСлужебные

// Заполняет массы в объекте.
// Параметры:
//   Объект - ДокументОбъект.ТранспортнаяНакладная,
//            см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   Запрос - Запрос - запрос к базе данных.
//
Процедура ЗаполнитьМассыИзЗапроса(Объект, Запрос)

	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Объект.МассаБрутто = Объект.МассаБрутто + Выборка.МассаБрутто;
			Объект.МассаНетто  = Объект.МассаНетто + Выборка.МассаНетто;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Формирует текст запроса получения масс из накладных.
//
// Параметры:
//   Запрос                         - Запрос - запрос к базе данных.
//   РаспоряженияДокументовОтгрузок - Неопределено, Соответствие из КлючИЗначение - в ключе документ отгрузки, в значении заказ.
//
Процедура УстановитьТекстЗапросаМассИзНакладных(Запрос, РаспоряженияДокументовОтгрузок)

	МассивОснований = Новый Массив;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ПередачаТоваровХранителю"));
	МассивТипов.Добавить(Тип("ДокументСсылка.РеализацияТоваровУслуг"));
	
	ДокументыОснования = Новый ТаблицаЗначений;
	ДокументыОснования.Колонки.Добавить("Распоряжение", Новый ОписаниеТипов("ДокументСсылка.ЗаказКлиента"));
	ДокументыОснования.Колонки.Добавить("Накладная",    Новый ОписаниеТипов(МассивТипов));
	
	Для Каждого КлючИЗначение Из РаспоряженияДокументовОтгрузок Цикл
		МассивОснований.Добавить(КлючИЗначение.Ключ);
		
		НоваяСтрока = ДокументыОснования.Добавить();
		НоваяСтрока.Накладная = КлючИЗначение.Ключ;
		НоваяСтрока.Распоряжение = КлючИЗначение.Значение;
	КонецЦикла;
	
	ТипыОбъектов = ОбщегоНазначенияУТ.РазложитьМассивСсылокПоТипам(МассивОснований);
	
	Запрос.УстановитьПараметр("ДокументыОснования", ДокументыОснования);
	
	ТекстыПакетовЗапроса = Новый Массив;
	
	ТекстыПакетовЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ДокументыОснования.Распоряжение КАК Распоряжение,
		|	ДокументыОснования.Накладная КАК Накладная
		|ПОМЕСТИТЬ НакладныеПоЗаданиямНаПеревозку
		|ИЗ
		|	&ДокументыОснования КАК ДокументыОснования");
	
	ТекстыПакетовЗапроса.Добавить(
		СтрЗаменить(Документы.ТранспортнаяНакладная.ТекстВТМассыИзОснований(ТипыОбъектов, Запрос),
					"И ДокументТовары.Склад = НакладныеПоЗаданиямНаПеревозку.Склад",
					""));
	
	ТекстыПакетовЗапроса.Добавить(
		"УНИЧТОЖИТЬ НакладныеПоЗаданиямНаПеревозку");
	
	ТекстыПакетовЗапроса.Добавить(
		"ВЫБРАТЬ
		|	СУММА(МассыИзОснований.МассаБрутто) КАК МассаБрутто,
		|	СУММА(МассыИзОснований.МассаНетто) КАК МассаНетто
		|ИЗ
		|	МассыИзОснований КАК МассыИзОснований");
		
	Запрос.Текст = СтрСоединить(ТекстыПакетовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());

КонецПроцедуры

// Формирует текст запроса получения масс из сведений по товарам отправлений.
//
// Параметры:
//   Запрос - Запрос - запрос к базе данных.
//
Процедура УстановитьТекстЗапросаМассИзОтправлений(Запрос)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗаказКлиентаТовары.Номенклатура КАК Номенклатура,
		|	ЗаказКлиентаТовары.Упаковка КАК Упаковка,
		|	СУММА(&Количество) КАК Количество,
		|	СУММА(СведенияПоТоварам.Количество) КАК КоличествоУпаковок
		|ПОМЕСТИТЬ ТаблицаТоваров
		|ИЗ
		|	РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок КАК СведенияПоТоварам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
		|		ПО СведенияПоТоварам.Заказ = ЗаказКлиентаТовары.Ссылка
		|			И СведенияПоТоварам.КодСтроки = ЗаказКлиентаТовары.КодСтроки
		|ГДЕ
		|	СведенияПоТоварам.НомерОтправления В(&НомераОтправлений)
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаказКлиентаТовары.Номенклатура,
		|	ЗаказКлиентаТовары.Упаковка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
		|	ТаблицаТоваров.Упаковка КАК Упаковка,
		|	ЕСТЬNULL(ТаблицаТоваров.Количество * &ТекстЗапросаВесНоменклатуры, 0) КАК МассаНетто,
		|	ЕСТЬNULL(ВЫБОР
		|			КОГДА &ЗаполненаЕдиницаИзмеренияВеса
		|				ТОГДА ВЫБОР
		|						КОГДА ТаблицаТоваров.Упаковка.Вес ЕСТЬ NULL
		|							ТОГДА ТаблицаТоваров.Количество
		|						ИНАЧЕ ВЫБОР
		|								КОГДА ТаблицаТоваров.Упаковка.ТипИзмеряемойВеличины = ЗНАЧЕНИЕ(Перечисление.ТипыИзмеряемыхВеличин.Вес)
		|									ТОГДА 0
		|								ИНАЧЕ ТаблицаТоваров.КоличествоУпаковок
		|							КОНЕЦ
		|					КОНЕЦ * &ТекстЗапросаВесУпаковки
		|			ИНАЧЕ 0
		|		КОНЕЦ, 0) КАК МассаБрутто
		|ИЗ
		|	ТаблицаТоваров КАК ТаблицаТоваров";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Количество",
		"ВЫБОР
		|	КОГДА &КоэффициентУпаковки = 0
		|		ТОГДА СведенияПоТоварам.Количество
		|	ИНАЧЕ ВЫРАЗИТЬ(СведенияПоТоварам.Количество * &КоэффициентУпаковки КАК ЧИСЛО(31, 2))
		|КОНЕЦ");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА ЗаказКлиентаТовары.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА ЗаказКлиентаТовары.Номенклатура.ЕдиницаИзмерения
			|	ИНАЧЕ ЗаказКлиентаТовары.Упаковка
			|КОНЕЦ)",
			"ЗаказКлиентаТовары.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесНоменклатуры",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Номенклатура.ЕдиницаИзмерения",
			"ТаблицаТоваров.Номенклатура"));
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстЗапросаВесУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки(
			"ТаблицаТоваров.Упаковка",
			"ТаблицаТоваров.Номенклатура"));
	
	Запрос.УстановитьПараметр("ЗаполненаЕдиницаИзмеренияВеса", ЗначениеЗаполнено(Константы.ЕдиницаИзмеренияВеса.Получить()));
	
	Запрос.Текст = ТекстЗапроса;

КонецПроцедуры

#КонецОбласти

#Область УправлениеДоставкойСлужебные

// Обновляет настройки доставки для документов отправлений, вошедших в отгрузку на торговой площадке.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   МетодДоставки        - см. ИнтеграцияСМаркетплейсамиСервер.НовыйМетодДоставкиСклада.
//
Процедура ОбновитьНастройкиДоставкиВОтправлениях(ИнформацияОбОтгрузке, МетодДоставки)

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ОбновитьНастройкиДоставкиВОтправлениях");
	
	НастройкиСклада = Неопределено;
	
	ПерезаполняемыеОтправления =
		ПолучитьОтправленияДляПерезаполненияНастроекДоставки(ИнформацияОбОтгрузке, НастройкиСклада);
	
	ИмяСобытия = ИнтеграцияСМаркетплейсамиСервер.СобытиеЖурналаРегистрации(ИнформацияОбОтгрузке.УчетнаяЗапись);
	
	ДетализацияОшибок = Новый Массив;
	ШаблонОшибки = НСтр("ru = 'Для отправления ""%1"" не удалось перезаполнить настройки доставки в документах: %2.';
						|en = 'For the ""%1"" shipment, cannot refill the delivery settings in the documents: %2. '");
	
	Для Каждого ДанныеОтправления Из ПерезаполняемыеОтправления Цикл
		ДокументыСОшибками = Новый Массив;
		
		Для Каждого ДанныеДокумента Из ДанныеОтправления.Значение Цикл
			ДокументОбъект = ДанныеДокумента.Документ.ПолучитьОбъект();
			Ошибка = ИнтеграцияСМаркетплейсамиСервер.ЗаблокироватьДокумент(ДокументОбъект, ИмяСобытия);
			Если Не ПустаяСтрока(Ошибка) Тогда
				ДокументыСОшибками.Добавить(ДанныеДокумента.Документ);
				Продолжить;
			КонецЕсли;
			
			ДокументОбъект.ОбменДанными.Загрузка = Истина; // Настройки доставки меняются в режиме загрузки.
			ЗаполнитьЗначенияСвойств(ДокументОбъект, НастройкиСклада);
			
			НачатьТранзакцию();
			Попытка
				Результат = ИнтеграцияСМаркетплейсамиСервер.ЗаписатьПровестиДокумент(
					ДокументОбъект,
					ДанныеДокумента.Проведен,
					ИмяСобытия);
				
				Если Результат.ЗаписьВыполнена Тогда
					Отказ = Ложь;
					ДоставкаТоваров.ОтразитьСостояниеДоставки(ДанныеДокумента.Документ, Отказ, Не ДанныеДокумента.Проведен);
					
					Если Отказ Тогда
						ОтменитьТранзакцию();
						
						ДокументыСОшибками.Добавить(ДанныеДокумента.Документ);
						
						Продолжить;
					КонецЕсли;
				Иначе
					ДокументыСОшибками.Добавить(ДанныеДокумента.Документ);
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				ОтменитьТранзакцию();
				
				ДокументыСОшибками.Добавить(ДанныеДокумента.Документ);
				
				ЗаписьЖурналаРегистрации(ИмяСобытия,
					УровеньЖурналаРегистрации.Ошибка,
					ДокументОбъект.Метаданные(),
					ДокументОбъект.Ссылка,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
		КонецЦикла;
		
		Если ДокументыСОшибками.Количество() > 0 Тогда
			ДетализацияОшибок.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонОшибки,
				ДанныеОтправления.Ключ,
				СтрСоединить(ДокументыСОшибками, ", ")));
		КонецЕсли;
	КонецЦикла;
	
	Если ДетализацияОшибок.Количество() > 0 Тогда
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		Ошибка.КодОшибки   = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.Детализация = ДетализацияОшибок;
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		ПерезаполняемыеОтправления.Количество());
	
	Если ПерезаполняемыеОтправления.Количество() > 0
			И Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		ОбновитьДанныеПоДоставке(ИнформацияОбОтгрузке,, Истина)
	КонецЕсли;

КонецПроцедуры

// Добавляет отправления в исключения из доставки до формирования отгрузки на торговой площадке.
//
// Параметры:
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//   Заказы               - Неопределено, Массив из ДокументСсылка.ЗаказКлиента - загруженные заказы.
//   Принудительно        - Булево - без проверки доступности управления доставкой.
//
Процедура ОбновитьДанныеПоДоставке(ИнформацияОбОтгрузке, Заказы = Неопределено, Принудительно = Ложь)

	Если Не Принудительно И Не ПолучитьФункциональнуюОпцию("ИспользоватьУправлениеДоставкой") Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСкладов = Новый Массив;
	
	Если ПустаяСтрока(ИнформацияОбОтгрузке.ИдентификаторСклада) Тогда
		ТаблицаСкладов =
			ИнтеграцияСМаркетплейсамиСервер.ПолучитьСопоставленныеСклады(ИнформацияОбОтгрузке.УчетнаяЗапись, Ложь, Истина);
		
		Для Каждого СтрокаСклада Из ТаблицаСкладов Цикл
			Если (СтрокаСклада.ИспользуетсяДляСхемыРаботыFBS
						Или СтрокаСклада.ИспользуетсяДляСхемыРаботыDBS)
					И СтрокаСклада.НастройкиСклада.ИсключатьОтправленияИзДоставки Тогда
				ИдентификаторыСкладов.Добавить(СтрокаСклада.ИдентификаторСклада);
			КонецЕсли;
		КонецЦикла;
		
		ТаблицаСкладов = Неопределено;
	Иначе
		Если ИнформацияОбОтгрузке.ОтправленияВОтгрузке.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		НастройкиСклада =
			ИнтеграцияСМаркетплейсамиСервер.НастройкиСклада(
				ИнформацияОбОтгрузке.УчетнаяЗапись,
				ИнформацияОбОтгрузке.ИдентификаторСклада);
		
		Если НастройкиСклада.ИсключатьОтправленияИзДоставки Тогда
			ИдентификаторыСкладов.Добавить(ИнформацияОбОтгрузке.ИдентификаторСклада);
		КонецЕсли;
	КонецЕсли;
	
	Если ИдентификаторыСкладов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ИсключаемыеТипыДляКолонок = Новый Соответствие;
	ИсключаемыеТипыДляКолонок.Вставить("ПолучательОтправитель", Тип("Строка"));
	
	ТоварыКИсключениюИзДоставки =
		ИнтеграцияСМаркетплейсамиСервер.ПустаяТаблицаОбъектаМетаданных(
			"РегистрСведений.ТоварыКДоставке",
			Истина,
			ИсключаемыеТипыДляКолонок);
	
	ВыборкаСкладов = ВыборкаДанныхПоДоставке(
		ИнформацияОбОтгрузке.УчетнаяЗапись,
		Ложь,
		Заказы,
		ИнформацияОбОтгрузке.ОтправленияВОтгрузке,
		ИдентификаторыСкладов);
	
	Если ВыборкаСкладов = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Пока ВыборкаСкладов.Следующий() Цикл
		ВыборкаРаспоряжений = ВыборкаСкладов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаРаспоряжений.Следующий() Цикл
			Если Не ВыборкаРаспоряжений.ТребуетсяЗаписьИсключенийИзДоставки Тогда
				Продолжить;
			КонецЕсли;
			
			// Добавление строки для обновления исключений,
			// у которой не заполнены НХУ и ВсеТовары = Ложь.
			СтрокаТаблицы = ТоварыКИсключениюИзДоставки.Добавить();
			СтрокаТаблицы.Распоряжение = ВыборкаРаспоряжений.Распоряжение;
			СтрокаТаблицы.Склад = ВыборкаРаспоряжений.Склад;
			СтрокаТаблицы.ВсеТовары = Ложь;
			
			Если Не ВыборкаРаспоряжений.ИспользуетсяДоставка Тогда
				Продолжить;
			КонецЕсли;
			
			// Добавление строк обновления исключения.
			Если ВыборкаРаспоряжений.ВсеТоварыКИсключениюИзДоставки Тогда
				СтрокаТаблицы = ТоварыКИсключениюИзДоставки.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаРаспоряжений);
				СтрокаТаблицы.ВсеТовары = Истина;
			Иначе
				ВыборкаТоваровКДоставке = ВыборкаРаспоряжений.Выбрать();
				Пока ВыборкаТоваровКДоставке.Следующий() Цикл
					Количество = ВыборкаТоваровКДоставке.КоличествоИсключитьИзДоставкиПоОтправлениям;
					
					Если Количество <= 0 Тогда
						Продолжить;
					КонецЕсли;
					
					СтрокаТаблицы = ТоварыКИсключениюИзДоставки.Добавить();
					ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ВыборкаТоваровКДоставке);
					СтрокаТаблицы.ВсеТовары = Ложь;
					СтрокаТаблицы.Количество = Количество;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗаписатьИзключенияИзДоставки(ТоварыКИсключениюИзДоставки, ИнформацияОбОтгрузке);

КонецПроцедуры

// Добавляет исключения из доставки.
//
// Параметры:
//   ТоварыКИсключениюИзДоставки - см. ИнтеграцияСМаркетплейсамиСервер.ПустаяТаблицаОбъектаМетаданных.
//   ИнформацияОбОтгрузке - см. НовыйСписокАтрибутовИнформацииОбОтгрузке.
//
Процедура ЗаписатьИзключенияИзДоставки(ТоварыКИсключениюИзДоставки, ИнформацияОбОтгрузке)

	Если ТоварыКИсключениюИзДоставки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		УстановитьПривилегированныйРежим(Истина);
		
		// Блокировка изменения данных по доставке.
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.ТоварыКДоставке");
		ЭлементБлокировкиДанных.УстановитьЗначение("ЗаданиеНаПеревозку", Документы.ЗаданиеНаПеревозку.ПустаяСсылка());
		ЭлементБлокировкиДанных.ИсточникДанных = ТоварыКИсключениюИзДоставки;
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Распоряжение", "Распоряжение");
		БлокировкаДанных.Заблокировать();
		
		НаборЗаписей = Неопределено;
		
		Для Каждого СтрокаТаблицы Из ТоварыКИсключениюИзДоставки Цикл
			Если Не СтрокаТаблицы.ВсеТовары
					И Не ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) Тогда
				Если НаборЗаписей <> Неопределено Тогда
					НаборЗаписей.Записать();
				КонецЕсли;
				
				НаборЗаписей = Неопределено;
				Продолжить;
			КонецЕсли;
			
			Если НаборЗаписей = Неопределено Тогда
				НаборЗаписей = РегистрыСведений.ТоварыКДоставке.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ЗаданиеНаПеревозку.Установить(СтрокаТаблицы.ЗаданиеНаПеревозку);
				НаборЗаписей.Отбор.Склад.Установить(СтрокаТаблицы.Склад);
				НаборЗаписей.Отбор.Распоряжение.Установить(СтрокаТаблицы.Распоряжение);
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СтрокаТаблицы);
		КонецЦикла;
		
		Если НаборЗаписей <> Неопределено Тогда
			НаборЗаписей.Записать();
		КонецЕсли;
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Ошибка = ИнтеграцияСМаркетплейсамиСервер.НоваяОшибка();
		Ошибка.КодОшибки      = ИнтеграцияСМаркетплейсамиСервер.КодыОшибок().ОшибкаПрочие;
		Ошибка.ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось исключить из доставки товары отправлений: %1.';
				|en = 'Cannot exclude the following items from delivery: %1.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			СтрСоединить(ИнформацияОбОтгрузке.ОтправленияВОтгрузке, ", "));
		
		ИнтеграцияСМаркетплейсамиСервер.ДополнитьОшибку(ИнформацияОбОтгрузке.Ошибка, Ошибка);
	КонецПопытки;

КонецПроцедуры

// Возвращает выборку товаров по документам отгрузки распоряжений.
//   Для доставки по торговым площадкам анализируются только товары заказов, по которым оформлены накладные.
//   Товары заказа могут быть исключены из доставки до формирования отгрузки на торговой площадке.
//   К доставке могут быть доступны товары из накладных заказов, но не больше товаров заказа за минусом исключений из доставки.
//
// Параметры:
//   УчетнаяЗапись           - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись торговой площадки.
//   УчитыватьДанныеДоставки - Булево - признак выборки данных для маршрута.
//   Заказы                  - Неопределено, Массив из ДокументСсылка.ЗаказКлиента - заказы.
//   НомераОтправлений       - Неопределено, Массив из Строка - номера отправлений.
//   ИдентификаторыСкладов   - Неопределено, Массив из Строка - идентификаторы складов торговых площадок.
//
// Возвращаемое значение:
//   Неопределено, ВыборкаИзРезультатаЗапроса - выборка результата запроса.
//
Функция ВыборкаДанныхПоДоставке(УчетнаяЗапись, УчитыватьДанныеДоставки, Заказы = Неопределено,
			НомераОтправлений = Неопределено, ИдентификаторыСкладов = Неопределено)

	Если Заказы = Неопределено
		Или ТипЗнч(Заказы) <> Тип("Массив") Тогда
		Заказы = Новый Массив;
	КонецЕсли;
	
	Если НомераОтправлений = Неопределено
		Или ТипЗнч(НомераОтправлений) <> Тип("Массив") Тогда
		НомераОтправлений = Новый Массив;
	КонецЕсли;
	
	Если ИдентификаторыСкладов = Неопределено
		Или ТипЗнч(ИдентификаторыСкладов) <> Тип("Массив") Тогда
		ИдентификаторыСкладов = Новый Массив;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("УчетнаяЗапись",         УчетнаяЗапись);
	Запрос.УстановитьПараметр("Заказы",                Заказы);
	Запрос.УстановитьПараметр("НомераОтправлений",     НомераОтправлений);
	Запрос.УстановитьПараметр("ИдентификаторыСкладов", ИдентификаторыСкладов);
	
	ПроверитьПараметрыЗапросаПоДоставке(Запрос);
	
	Если Не ЗначениеЗаполнено(Запрос.Параметры.Заказы) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.ЗаказыТорговыхПлощадок.ВыборкаДанныхПоДоставке");
	
	ГраницыОборотов = ОбщегоНазначенияУТ.ГраницыОборотовРегистра(
		"РаспоряженияНаОтгрузкуИВозврат",
		"Распоряжение В (&Заказы)",
		Запрос.Параметры);
	
	Запрос.УстановитьПараметр("НачПериодОборотов", ГраницыОборотов.МинимальнаяДата);
	Запрос.УстановитьПараметр("КонПериодОборотов", ГраницыОборотов.МаксимальнаяДата);
	
	СтатусыНеотгружаемыхЗаказовРаботе = Перечисления.СтатусыЗаказовТорговыхПлощадок.СтатусыЗаказовРаботе(Истина);
	Запрос.УстановитьПараметр("СтатусыНеотгружаемыхЗаказовРаботе", СтатусыНеотгружаемыхЗаказовРаботе);
	
	ИспользуемыеСпособыДоставки = ДоставкаТоваровКлиентСервер.ИспользуемыеСпособыДоставки(
		ПолучитьФункциональнуюОпцию("ИспользоватьЗаданияНаПеревозкуДляУчетаДоставкиПеревозчиками"));
	
	Запрос.УстановитьПараметр("ИспользуемыеСпособыДоставки", ИспользуемыеСпособыДоставки);
	
	ТекстыЗапроса = Новый Массив;
	
	ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоРаспоряжениям(ТекстыЗапроса);
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоОтправлениям(ТекстыЗапроса, ВидМаркетплейса);
	
	ДополнитьТекстыЗапросаПоДоставкеВыборкойИтоговыхПоказателейПоСкладамРаспоряжений(ТекстыЗапроса);
	
	ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоСостояниямИРеквизитамДоставки(ТекстыЗапроса, УчитыватьДанныеДоставки);
	
	ДополнитьТекстыЗапросаПоДоставкеИтоговойВыборкойДанных(ТекстыЗапроса, УчитыватьДанныеДоставки);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДанных = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	КоличествоЗаказов = ?(Запрос.Параметры.Заказы.Количество() = 0, 1, Запрос.Параметры.Заказы.Количество());
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(
		Замер,
		КоличествоЗаказов / 1000);
	
	Возврат ВыборкаДанных;

КонецФункции

// Проверяет и дополняет по необходимости параметры запроса.
//
// Параметры:
//   Запрос - Запрос - запрос выборки данных.
//
Процедура ПроверитьПараметрыЗапросаПоДоставке(Запрос)

	Если ЗначениеЗаполнено(Запрос.Параметры.Заказы)
			И Не ЗначениеЗаполнено(Запрос.Параметры.НомераОтправлений)
			И Не ЗначениеЗаполнено(Запрос.Параметры.ИдентификаторыСкладов) Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Распоряжение
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И &Условие";
		
	ТекстУсловия = "";
	
	Если ЗначениеЗаполнено(Запрос.Параметры.Заказы) Тогда
		ТекстУсловия = ТекстУсловия + "
			|	И ЗаказыТорговыхПлощадок.Заказ В(&Заказы)";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Параметры.НомераОтправлений) Тогда
		ТекстУсловия = ТекстУсловия + "
			|	И ЗаказыТорговыхПлощадок.НомерОтправления В(&НомераОтправлений)";
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запрос.Параметры.ИдентификаторыСкладов) Тогда
		ТекстУсловия = ТекстУсловия + "
			|	И ЗаказыТорговыхПлощадок.ИдентификаторСкладаТорговойПлощадки В(&ИдентификаторыСкладов)";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "И &Условие", ТекстУсловия);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапроса.Пустой() Тогда
		СписокРаспоряжений = Новый Массив;
	Иначе
		СписокРаспоряжений = РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Распоряжение");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Заказы", СписокРаспоряжений);

КонецПроцедуры

// Дополняет текст запроса выборкой данных по распоряжениям.
//
// Параметры:
//   ТекстыЗапроса - Массив из Строка - список частей текста запроса.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоРаспоряжениям(ТекстыЗапроса)

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
	|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
	|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
	|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
	|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
	|	РаспоряженияНаОтгрузкуОбороты.КодСтроки КАК КодСтроки,
	|	РаспоряженияНаОтгрузкуОбороты.ЗаказаноКоличествоОборот КАК КоличествоКОтгрузке
	|ПОМЕСТИТЬ ТоварыПоРаспоряжениямВыборкаОборотов
	|ИЗ
	|	#РаспоряженияНаОтгрузкуОборотыРазвернутая КАК РаспоряженияНаОтгрузкуОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РаспоряженияНаОтгрузкуОбороты.Распоряжение КАК Распоряжение,
	|	РаспоряженияНаОтгрузкуОбороты.Склад КАК Склад,
	|	РаспоряженияНаОтгрузкуОбороты.Номенклатура КАК Номенклатура,
	|	РаспоряженияНаОтгрузкуОбороты.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Обособленно
	|			ТОГДА ЗаказКлиента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ КАК Назначение,
	|	РаспоряженияНаОтгрузкуОбороты.Серия КАК Серия,
	|	РаспоряженияНаОтгрузкуОбороты.КодСтроки КАК КодСтроки,
	|	РаспоряженияНаОтгрузкуОбороты.КоличествоКОтгрузке КАК КоличествоКОтгрузке
	|ПОМЕСТИТЬ ТоварыПоРаспоряжениямВыборкаПоКодамСтрок
	|ИЗ
	|	ТоварыПоРаспоряжениямВыборкаОборотов КАК РаспоряженияНаОтгрузкуОбороты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ПО РаспоряженияНаОтгрузкуОбороты.Распоряжение = ЗаказКлиентаТовары.Ссылка
	|			И РаспоряженияНаОтгрузкуОбороты.КодСтроки = ЗаказКлиентаТовары.КодСтроки
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО РаспоряженияНаОтгрузкуОбороты.Распоряжение = ЗаказКлиента.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗаказКлиентаТовары.Ссылка,
	|	ЗаказКлиентаТовары.Склад,
	|	ЗаказКлиентаТовары.Номенклатура,
	|	ЗаказКлиентаТовары.Характеристика,
	|	ВЫБОР
	|		КОГДА ЗаказКлиентаТовары.Обособленно
	|			ТОГДА ЗаказКлиента.Назначение
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
	|	КОНЕЦ,
	|	ЗаказКлиентаТовары.Серия,
	|	ЗаказКлиентаТовары.КодСтроки,
	|	ЗаказКлиентаТовары.Количество
	|ИЗ
	|	Документ.ЗаказКлиента.Товары КАК ЗаказКлиентаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
	|		ПО ЗаказКлиентаТовары.Ссылка = ЗаказКлиента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПоРаспоряжениямВыборкаОборотов КАК РаспоряженияНаОтгрузкуОбороты
	|		ПО ЗаказКлиентаТовары.Ссылка = РаспоряженияНаОтгрузкуОбороты.Распоряжение
	|			И ЗаказКлиентаТовары.КодСтроки = РаспоряженияНаОтгрузкуОбороты.КодСтроки
	|ГДЕ
	|	ЗаказКлиентаТовары.Ссылка В(&Заказы)
	|	И РаспоряженияНаОтгрузкуОбороты.КодСтроки ЕСТЬ NULL
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Распоряжение,
	|	КодСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТоварыПоРаспоряжениямВыборкаОборотов";
	
	ПараметрыФормированияТаблицы = РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.НовыйПараметрыФормированияРазвернутойТаблицы();
	ПараметрыФормированияТаблицы.ТекстУсловия =
		"Распоряжение В (&Заказы)";
	ПараметрыФормированияТаблицы.НачалоПериода = "&НачПериодОборотов";
	ПараметрыФормированияТаблицы.КонецПериода = "&КонПериодОборотов";
	ПараметрыФормированияТаблицы.Группировка = "Распоряжение, Номенклатура, Характеристика, КодСтроки, Склад, Серия";
	ПараметрыФормированияТаблицы.Показатели = "Заказано";
	ПараметрыФормированияТаблицы.Ресурсы = "Количество";
	
	РаспоряженияНаОтгрузкуОборотыРазвернутая =
		РегистрыНакопления.РаспоряженияНаОтгрузкуИВозврат.ТекстЗапросаРазвернутаяТаблица(
			ПараметрыФормированияТаблицы);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"#РаспоряженияНаОтгрузкуОборотыРазвернутая",
		РаспоряженияНаОтгрузкуОборотыРазвернутая);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	СУММА(ТоварыПоРаспоряжениям.КоличествоКОтгрузке) КАК КоличествоКОтгрузке
		|ПОМЕСТИТЬ ТоварыПоРаспоряжениямВыборка
		|ИЗ
		|	ТоварыПоРаспоряжениямВыборкаПоКодамСтрок КАК ТоварыПоРаспоряжениям
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПоРаспоряжениям.Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение,
		|	ТоварыПоРаспоряжениям.Склад,
		|	ТоварыПоРаспоряжениям.Распоряжение,
		|	ТоварыПоРаспоряжениям.Номенклатура,
		|	ТоварыПоРаспоряжениям.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия");
	
	ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоДоставкеТоваровРаспоряжений(ТекстыЗапроса);
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ ТоварыПоРаспоряжениямВыборка");

КонецПроцедуры

// Дополняет текст запроса выборкой данных по доставке товаров распоряжений.
//
// Параметры:
//   ТекстыЗапроса - Массив из Строка - список частей текста запроса.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоДоставкеТоваровРаспоряжений(ТекстыЗапроса)

	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТоварыКДоставке.Распоряжение КАК Распоряжение,
		|	ТоварыКДоставке.Склад КАК Склад,
		|	ТоварыКДоставке.ВсеТовары КАК ВсеТовары,
		|	ТоварыКДоставке.Номенклатура КАК Номенклатура,
		|	ТоварыКДоставке.Характеристика КАК Характеристика,
		|	ТоварыКДоставке.Назначение КАК Назначение,
		|	ТоварыКДоставке.Серия КАК Серия,
		|	ТоварыКДоставке.Количество КАК Количество,
		|	ТоварыКДоставке.ЗаданиеНаПеревозку = ЗНАЧЕНИЕ(Документ.ЗаданиеНаПеревозку.ПустаяСсылка) КАК ИсключенИзДоставки
		|ПОМЕСТИТЬ ДанныеПоДоставкам
		|ИЗ
		|	РегистрСведений.ТоварыКДоставке КАК ТоварыКДоставке
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаданиеНаПеревозку КАК ЗаданияНаПеревозку
		|		ПО ТоварыКДоставке.ЗаданиеНаПеревозку = ЗаданияНаПеревозку.Ссылка
		|ГДЕ
		|	ТоварыКДоставке.Распоряжение В(&Заказы)
		|	И (ЗаданияНаПеревозку.Ссылка ЕСТЬ NULL
		|			ИЛИ ЗаданияНаПеревозку.Проведен)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоДоставкам.Распоряжение КАК Распоряжение,
		|	ДанныеПоДоставкам.Склад КАК Склад,
		|	ДанныеПоДоставкам.ИсключенИзДоставки КАК ИсключенИзДоставки
		|ПОМЕСТИТЬ РаспоряженияВДоставке
		|ИЗ
		|	ДанныеПоДоставкам КАК ДанныеПоДоставкам
		|ГДЕ
		|	ДанныеПоДоставкам.ВсеТовары
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеПоДоставкам.Распоряжение КАК Распоряжение,
		|	ДанныеПоДоставкам.Склад КАК Склад,
		|	ДанныеПоДоставкам.Номенклатура КАК Номенклатура,
		|	ДанныеПоДоставкам.Характеристика КАК Характеристика,
		|	ДанныеПоДоставкам.Назначение КАК Назначение,
		|	ДанныеПоДоставкам.Серия КАК Серия,
		|	СУММА(ВЫБОР
		|			КОГДА НЕ ДанныеПоДоставкам.ИсключенИзДоставки
		|				ТОГДА ДанныеПоДоставкам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоВДоставке,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеПоДоставкам.ИсключенИзДоставки
		|				ТОГДА ДанныеПоДоставкам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоИсключеноИзДоставки
		|ПОМЕСТИТЬ ТоварыВДоставке
		|ИЗ
		|	ДанныеПоДоставкам КАК ДанныеПоДоставкам
		|ГДЕ
		|	НЕ ДанныеПоДоставкам.ВсеТовары
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеПоДоставкам.Распоряжение,
		|	ДанныеПоДоставкам.Склад,
		|	ДанныеПоДоставкам.Номенклатура,
		|	ДанныеПоДоставкам.Характеристика,
		|	ДанныеПоДоставкам.Назначение,
		|	ДанныеПоДоставкам.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ДанныеПоДоставкам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	ТоварыПоРаспоряжениям.КоличествоКОтгрузке КАК КоличествоКОтгрузке,
		|	ВЫБОР
		|		КОГДА НЕ РаспоряженияВДоставке.ИсключенИзДоставки
		|			ТОГДА ТоварыПоРаспоряжениям.КоличествоКОтгрузке
		|		ИНАЧЕ ЕСТЬNULL(ТоварыВДоставке.КоличествоВДоставке, 0)
		|	КОНЕЦ КАК КоличествоВДоставке,
		|	ВЫБОР
		|		КОГДА РаспоряженияВДоставке.ИсключенИзДоставки
		|			ТОГДА ТоварыПоРаспоряжениям.КоличествоКОтгрузке
		|		ИНАЧЕ ЕСТЬNULL(ТоварыВДоставке.КоличествоИсключеноИзДоставки, 0)
		|	КОНЕЦ КАК КоличествоИсключеноИзДоставки
		|ПОМЕСТИТЬ ТоварыПоРаспоряжениям
		|ИЗ
		|	ТоварыПоРаспоряжениямВыборка КАК ТоварыПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ РаспоряженияВДоставке КАК РаспоряженияВДоставке
		|		ПО ТоварыПоРаспоряжениям.Распоряжение = РаспоряженияВДоставке.Распоряжение
		|			И ТоварыПоРаспоряжениям.Склад = РаспоряженияВДоставке.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыВДоставке КАК ТоварыВДоставке
		|		ПО ТоварыПоРаспоряжениям.Распоряжение = ТоварыВДоставке.Распоряжение
		|			И ТоварыПоРаспоряжениям.Склад = ТоварыВДоставке.Склад
		|			И ТоварыПоРаспоряжениям.Номенклатура = ТоварыВДоставке.Номенклатура
		|			И ТоварыПоРаспоряжениям.Характеристика = ТоварыВДоставке.Характеристика
		|			И ТоварыПоРаспоряжениям.Назначение = ТоварыВДоставке.Назначение
		|			И ТоварыПоРаспоряжениям.Серия = ТоварыВДоставке.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ РаспоряженияВДоставке
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТоварыВДоставке");

КонецПроцедуры

// Дополняет текст запроса выборкой данных по отправлениям.
//
// Параметры:
//   ТекстыЗапроса - Массив из Строка - список частей текста запроса.
//   ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоОтправлениям(ТекстыЗапроса, ВидМаркетплейса)

	// Используются обороты по кодам строк заказов из регистра накопления РаспоряженияНаОтгрузку,
	// помещенные в ВТ ТоварыПоРаспоряжениямВыборкаПоКодамСтрок,
	// а также свернутая информация (без кодов строк) с данными по доставке из ВТ ТоварыПоРаспоряжениям.
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ЗаказыТорговыхПлощадок.Заказ КАК Распоряжение,
		|	ЗаказыТорговыхПлощадок.НомерОтправления КАК НомерОтправления,
		|	НЕ ЗаказыТорговыхПлощадок.Статус В (&СтатусыНеотгружаемыхЗаказовРаботе) КАК ОтправлениеОтгружено
		|ПОМЕСТИТЬ ВсеОтправленияРаспоряжений
		|ИЗ
		|	РегистрСведений.ЗаказыТорговыхПлощадок КАК ЗаказыТорговыхПлощадок
		|ГДЕ
		|	ЗаказыТорговыхПлощадок.УчетнаяЗапись = &УчетнаяЗапись
		|	И ЗаказыТорговыхПлощадок.Заказ В(&Заказы)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	НомерОтправления
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СведенияПоТоварам.Заказ КАК Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	СУММА(ВЫБОР
		|			КОГДА НЕ ВсеОтправленияРаспоряжений.ОтправлениеОтгружено
		|				ТОГДА СведенияПоТоварам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ * ЕСТЬNULL(&Коэффициент, 1)) КАК КоличествоКОтгрузке,
		|	СУММА(ВЫБОР
		|			КОГДА ВсеОтправленияРаспоряжений.ОтправлениеОтгружено
		|				ТОГДА СведенияПоТоварам.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ * ЕСТЬNULL(&Коэффициент, 1)) КАК КоличествоОтгружено
		|ПОМЕСТИТЬ ТоварыПоОтправлениям
		|ИЗ
		|	РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок КАК СведенияПоТоварам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПоРаспоряжениямВыборкаПоКодамСтрок КАК ТоварыПоРаспоряжениям
		|		ПО СведенияПоТоварам.Заказ = ТоварыПоРаспоряжениям.Распоряжение
		|			И СведенияПоТоварам.КодСтроки = ТоварыПоРаспоряжениям.КодСтроки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеОтправленияРаспоряжений КАК ВсеОтправленияРаспоряжений
		|		ПО СведенияПоТоварам.Заказ = ВсеОтправленияРаспоряжений.Распоряжение
		|			И СведенияПоТоварам.НомерОтправления = ВсеОтправленияРаспоряжений.НомерОтправления
		|			И (&СоединениеСРегистромСтатусов)
		|
		|СГРУППИРОВАТЬ ПО
		|	СведенияПоТоварам.Заказ,
		|	ТоварыПоРаспоряжениям.Склад,
		|	ТоварыПоРаспоряжениям.Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение,
		|	ТоварыПоРаспоряжениям.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад,
		|	Номенклатура,
		|	Характеристика,
		|	Назначение,
		|	Серия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВсеОтправленияРаспоряжений";
	
	СоединениеСРегистромСтатусов = "";
	ТекстЗапросаКоэффициентаУпаковки = "1";
	
	Если ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		СоединениеСРегистромСтатусов = "
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации1
			|		ПО СведенияПоТоварам.ИдентификаторТовара = СтатусыПубликации1.ИдентификаторFBOSKU
			|			И (СтатусыПубликации1.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись)
			|			И (СтатусыПубликации1.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыПубликацииОбъектовМаркетплейсаOzon КАК СтатусыПубликации2
			|		ПО СведенияПоТоварам.ИдентификаторТовара = СтатусыПубликации2.ИдентификаторFBSSKU
			|			И (СтатусыПубликации2.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись)
			|			И (СтатусыПубликации2.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар))";
		
		ТекстЗапросаКоэффициентаУпаковки = Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ЕСТЬNULL(СтатусыПубликации1.Упаковка, СтатусыПубликации2.Упаковка))",
			"ТоварыПоРаспоряжениям.Номенклатура");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (&СоединениеСРегистромСтатусов)", СоединениеСРегистромСтатусов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Коэффициент", ТекстЗапросаКоэффициентаУпаковки);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ ТоварыПоРаспоряжениямВыборкаПоКодамСтрок");
	
	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	ТоварыПоРаспоряжениям.КоличествоКОтгрузке КАК КоличествоКОтгрузкеПоРаспоряжению,
		|	ТоварыПоРаспоряжениям.КоличествоВДоставке КАК КоличествоВДоставкеПоРаспоряжению,
		|	ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставки КАК КоличествоИсключеноИзДоставкиПоРаспоряжению,
		|	ЕСТЬNULL(ТоварыПоОтправлениям.КоличествоКОтгрузке, 0) КАК КоличествоКОтгрузкеПоОтправлениям,
		|	ЕСТЬNULL(ТоварыПоОтправлениям.КоличествоОтгружено, 0) КАК КоличествоОтгруженоПоОтправлениям,
		|	ТоварыПоРаспоряжениям.КоличествоКОтгрузке - ЕСТЬNULL(ТоварыПоОтправлениям.КоличествоКОтгрузке, 0) - ЕСТЬNULL(ТоварыПоОтправлениям.КоличествоОтгружено, 0) КАК КоличествоОтгруженоБезОтправлений
		|ПОМЕСТИТЬ ТоварыПоРаспоряжениямИтоговаяВыборка
		|ИЗ
		|	ТоварыПоРаспоряжениям КАК ТоварыПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТоварыПоОтправлениям КАК ТоварыПоОтправлениям
		|		ПО ТоварыПоРаспоряжениям.Распоряжение = ТоварыПоОтправлениям.Распоряжение
		|			И ТоварыПоРаспоряжениям.Склад = ТоварыПоОтправлениям.Склад
		|			И ТоварыПоРаспоряжениям.Номенклатура = ТоварыПоОтправлениям.Номенклатура
		|			И ТоварыПоРаспоряжениям.Характеристика = ТоварыПоОтправлениям.Характеристика
		|			И ТоварыПоРаспоряжениям.Назначение = ТоварыПоОтправлениям.Назначение
		|			И ТоварыПоРаспоряжениям.Серия = ТоварыПоОтправлениям.Серия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад");
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ ТоварыПоРаспоряжениям");
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ ТоварыПоОтправлениям");

КонецПроцедуры

// Дополняет текст запроса выборкой итоговых показателей в разрезе распоряжений и складов.
//
// Параметры:
//   ТекстыЗапроса - Массив из Строка - список частей текста запроса.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеВыборкойИтоговыхПоказателейПоСкладамРаспоряжений(ТекстыЗапроса)

	ТекстыЗапроса.Добавить(
		"ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	СУММА(ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставкиПоРаспоряжению) > 0 КАК ЕстьИсключенияИзДоставки,
		|	СУММА(ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставкиПоРаспоряжению) = 0
		|		И СУММА(ТоварыПоРаспоряжениям.КоличествоВДоставкеПоРаспоряжению) = 0
		|		И СУММА(ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоРаспоряжению) = СУММА(ТоварыПоРаспоряжениям.КоличествоОтгруженоПоОтправлениям) + СУММА(ТоварыПоРаспоряжениям.КоличествоОтгруженоБезОтправлений) КАК ВсеТоварыКДоставке,
		|	СУММА(ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставкиПоРаспоряжению) = 0
		|		И СУММА(ТоварыПоРаспоряжениям.КоличествоВДоставкеПоРаспоряжению) = 0
		|		И СУММА(ТоварыПоРаспоряжениям.КоличествоОтгруженоПоОтправлениям) = 0
		|		И СУММА(ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоРаспоряжению) = СУММА(ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоОтправлениям) КАК ВсеТоварыКИсключениюИзДоставки
		|ПОМЕСТИТЬ ИтоговыеПоказателиПоСкладамРаспоряжений
		|ИЗ
		|	ТоварыПоРаспоряжениямИтоговаяВыборка КАК ТоварыПоРаспоряжениям
		|
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПоРаспоряжениям.Распоряжение,
		|	ТоварыПоРаспоряжениям.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад");

КонецПроцедуры

// Дополняет текст запроса выборкой данных по по состояниям и реквизитам доставки.
//
// Параметры:
//   ТекстыЗапроса - Массив из Строка - список частей текста запроса.
//   УчитыватьДанныеДоставки - Булево - признак выборки данных для маршрута.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеВыборкойДанныхПоСостояниямИРеквизитамДоставки(ТекстыЗапроса, УчитыватьДанныеДоставки = Ложь)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ДанныеПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	ДанныеПоРаспоряжениям.Склад КАК Склад,
		|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ПолучательОтправитель, ЗаказКлиента.Партнер) КАК ПолучательОтправитель,
		|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.Перевозчик, ЗаказКлиента.ПеревозчикПартнер) КАК Перевозчик,
		|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.СпособДоставки, ЗаказКлиента.СпособДоставки) КАК СпособДоставки,
		|	&ДанныеДоставки КАК ДанныеДоставки,
		|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.СпособДоставки, ЗаказКлиента.СпособДоставки) В (&ИспользуемыеСпособыДоставки) КАК ИспользуетсяДоставка
		|ПОМЕСТИТЬ СостоянияИРеквизитыДоставки
		|ИЗ
		|	ИтоговыеПоказателиПоСкладамРаспоряжений КАК ДанныеПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента КАК ЗаказКлиента
		|		ПО ДанныеПоРаспоряжениям.Распоряжение = ЗаказКлиента.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияИРеквизитыДоставки КАК СостоянияИРеквизитыДоставки
		|		ПО ДанныеПоРаспоряжениям.Распоряжение = СостоянияИРеквизитыДоставки.Распоряжение
		|			И ДанныеПоРаспоряжениям.Склад = СостоянияИРеквизитыДоставки.Склад
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Распоряжение,
		|	Склад";
	
	ТекстЗамены = "";
	Если УчитыватьДанныеДоставки Тогда
		ТекстЗамены = "
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.Зона, ЗаказКлиента.ЗонаДоставки) КАК Зона,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.Адрес, ЗаказКлиента.АдресДоставкиПеревозчика) КАК Адрес,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.АдресЗначенияПолей, ЗаказКлиента.АдресДоставкиПеревозчикаЗначенияПолей) КАК АдресЗначенияПолей,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ВремяС, ЗаказКлиента.ВремяДоставкиС) КАК ВремяС,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ВремяПо, ЗаказКлиента.ВремяДоставкиПо) КАК ВремяПо,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ОсобыеУсловияПеревозки, ЗаказКлиента.ОсобыеУсловияПеревозки) КАК ОсобыеУсловияПеревозки,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ОсобыеУсловияПеревозкиОписание, ЗаказКлиента.ОсобыеУсловияПеревозкиОписание) КАК ОсобыеУсловияПеревозкиОписание,
			|	ЕСТЬNULL(СостоянияИРеквизитыДоставки.ДополнительнаяИнформация, ЗаказКлиента.ДополнительнаяИнформацияПоДоставке) КАК ДополнительнаяИнформация,";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДанныеДоставки КАК ДанныеДоставки,", ТекстЗамены);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);

КонецПроцедуры

// Дополняет текст запроса итоговой выборкой данных.
//
// Параметры:
//   ТекстыЗапроса           - Массив из Строка - список частей текста запроса.
//   УчитыватьДанныеДоставки - Булево - признак выборки данных для маршрута.
//
Процедура ДополнитьТекстыЗапросаПоДоставкеИтоговойВыборкойДанных(ТекстыЗапроса, УчитыватьДанныеДоставки = Ложь)

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТоварыПоРаспоряжениям.Склад КАК Склад,
		|	ТоварыПоРаспоряжениям.Распоряжение КАК Распоряжение,
		|	СостоянияИРеквизитыДоставки.ИспользуетсяДоставка КАК ИспользуетсяДоставка,
		|	СостоянияИРеквизитыДоставки.ПолучательОтправитель КАК ПолучательОтправитель,
		|	СостоянияИРеквизитыДоставки.Перевозчик КАК Перевозчик,
		|	СостоянияИРеквизитыДоставки.СпособДоставки КАК СпособДоставки,
		|	&ДанныеДоставки КАК ДанныеДоставки,
		|	ИтоговыеПоказатели.ЕстьИсключенияИзДоставки КАК ЕстьИсключенияИзДоставки,
		|	ИтоговыеПоказатели.ВсеТоварыКДоставке КАК ВсеТоварыКДоставке,
		|	ИтоговыеПоказатели.ВсеТоварыКИсключениюИзДоставки КАК ВсеТоварыКИсключениюИзДоставки,
		|	ТоварыПоРаспоряжениям.Номенклатура КАК Номенклатура,
		|	ТоварыПоРаспоряжениям.Характеристика КАК Характеристика,
		|	ТоварыПоРаспоряжениям.Назначение КАК Назначение,
		|	ТоварыПоРаспоряжениям.Серия КАК Серия,
		|	ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоРаспоряжению КАК КоличествоКОтгрузкеПоРаспоряжению,
		|	ТоварыПоРаспоряжениям.КоличествоВДоставкеПоРаспоряжению КАК КоличествоВДоставкеПоРаспоряжению,
		|	ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставкиПоРаспоряжению КАК КоличествоИсключеноИзДоставкиПоРаспоряжению,
		|	ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоОтправлениям КАК КоличествоИсключитьИзДоставкиПоОтправлениям,
		|	ТоварыПоРаспоряжениям.КоличествоОтгруженоПоОтправлениям КАК КоличествоОтгруженоПоОтправлениям,
		|	ТоварыПоРаспоряжениям.КоличествоОтгруженоБезОтправлений КАК КоличествоОтгруженоБезОтправлений,
		|	ТоварыПоРаспоряжениям.КоличествоИсключеноИзДоставкиПоРаспоряжению <> ТоварыПоРаспоряжениям.КоличествоКОтгрузкеПоОтправлениям КАК ТребуетсяЗаписьИсключенийИзДоставки,
		|	ТоварыПоРаспоряжениям.КоличествоОтгруженоПоОтправлениям + ТоварыПоРаспоряжениям.КоличествоОтгруженоБезОтправлений - ТоварыПоРаспоряжениям.КоличествоВДоставкеПоРаспоряжению КАК КоличествоДляДоставки
		|ИЗ
		|	ТоварыПоРаспоряжениямИтоговаяВыборка КАК ТоварыПоРаспоряжениям
		|		ЛЕВОЕ СОЕДИНЕНИЕ СостоянияИРеквизитыДоставки КАК СостоянияИРеквизитыДоставки
		|		ПО ТоварыПоРаспоряжениям.Распоряжение = СостоянияИРеквизитыДоставки.Распоряжение
		|			И ТоварыПоРаспоряжениям.Склад = СостоянияИРеквизитыДоставки.Склад
		|		ЛЕВОЕ СОЕДИНЕНИЕ ИтоговыеПоказателиПоСкладамРаспоряжений КАК ИтоговыеПоказатели
		|		ПО ТоварыПоРаспоряжениям.Распоряжение = ИтоговыеПоказатели.Распоряжение
		|			И ТоварыПоРаспоряжениям.Склад = ИтоговыеПоказатели.Склад
		|ИТОГИ
		|	МАКСИМУМ(ИспользуетсяДоставка),
		|	МАКСИМУМ(ПолучательОтправитель),
		|	МАКСИМУМ(ЕстьИсключенияИзДоставки),
		|	МИНИМУМ(ВсеТоварыКДоставке),
		|	МИНИМУМ(ВсеТоварыКИсключениюИзДоставки),
		|	МАКСИМУМ(ТребуетсяЗаписьИсключенийИзДоставки),
		|	СУММА(КоличествоДляДоставки)
		|ПО
		|	Склад,
		|	Распоряжение";
	
	ТекстЗамены = "";
	Если УчитыватьДанныеДоставки Тогда
		ТекстЗапроса = СтрЗаменить(
			ТекстЗапроса,
			"ИТОГИ",
			"ГДЕ
			|	ТоварыПоРаспоряжениям.КоличествоОтгруженоПоОтправлениям + ТоварыПоРаспоряжениям.КоличествоОтгруженоБезОтправлений - ТоварыПоРаспоряжениям.КоличествоВДоставкеПоРаспоряжению > 0
			|
			|ИТОГИ");
		
		ТекстЗамены = "
			|	СостоянияИРеквизитыДоставки.Зона КАК Зона,
			|	СостоянияИРеквизитыДоставки.Адрес КАК Адрес,
			|	СостоянияИРеквизитыДоставки.АдресЗначенияПолей КАК АдресЗначенияПолей,
			|	СостоянияИРеквизитыДоставки.ВремяС КАК ВремяС,
			|	СостоянияИРеквизитыДоставки.ВремяПо КАК ВремяПо,
			|	СостоянияИРеквизитыДоставки.ОсобыеУсловияПеревозки КАК ОсобыеУсловияПеревозки,
			|	СостоянияИРеквизитыДоставки.ОсобыеУсловияПеревозкиОписание КАК ОсобыеУсловияПеревозкиОписание,
			|	СостоянияИРеквизитыДоставки.ДополнительнаяИнформация КАК ДополнительнаяИнформация,";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДанныеДоставки КАК ДанныеДоставки,", ТекстЗамены);
	
	ТекстыЗапроса.Добавить(ТекстЗапроса);
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ ИтоговыеПоказателиПоСкладамРаспоряжений");
	
	ТекстыЗапроса.Добавить(
		"УНИЧТОЖИТЬ СостоянияИРеквизитыДоставки");

КонецПроцедуры

// Возвращает перезаполняемые настройки склада.
//
// Возвращаемое значение:
//   Массив Из Строка - список перезаполняемых настроек склада.
//
Функция ПерезаполняемыеНастройкиСклада()

	ПерезаполняемыеНастройкиСклада = Новый Массив;
	ПерезаполняемыеНастройкиСклада.Добавить("СпособДоставки");
	ПерезаполняемыеНастройкиСклада.Добавить("ЗонаДоставки");
	ПерезаполняемыеНастройкиСклада.Добавить("ПеревозчикПартнер");
	ПерезаполняемыеНастройкиСклада.Добавить("Курьер");
	ПерезаполняемыеНастройкиСклада.Добавить("АдресДоставкиПеревозчика");
	ПерезаполняемыеНастройкиСклада.Добавить("АдресДоставкиПеревозчикаЗначение");
	ПерезаполняемыеНастройкиСклада.Добавить("АдресДоставкиПеревозчикаЗначенияПолей");
	ПерезаполняемыеНастройкиСклада.Добавить("ВремяДоставкиС");
	ПерезаполняемыеНастройкиСклада.Добавить("ВремяДоставкиПо");
	
	Возврат ПерезаполняемыеНастройкиСклада;

КонецФункции

#КонецОбласти

#Область ПечатныеФормыСлужебные

// Записывает в присоединенные файлы переданный файл.
//
// Параметры:
//   ВидМаркетплейса     - ПеречислениеСсылка.ВидыМаркетплейсов - вид торговой площадки.
//   ВладелецФайла       - ОпределяемыйТип.ВладелецПрисоединенныхФайлов - ссылка объект, для которого есть
//                           справочник присоединенных файлов.
//   АдресДвоичныхДанных - Строка - адрес двоичных данных полученной печатной формы.
//   НаименованиеФайла   - Строка - наименование, под которым будет записан файл.
//   РасширениеФайла     - Строка - расширение файла.
//   ПреобразоватьВPDF   - Булево - преобразовать в PDF-файл.
//
Процедура ЗаписатьПрисоединенныйФайл(ВидМаркетплейса, ВладелецФайла, АдресДвоичныхДанных,
			НаименованиеФайла = "", РасширениеФайла = "", ПреобразоватьВPDF = Ложь)
	
	Если Не ПустаяСтрока(АдресДвоичныхДанных) Тогда
		Если Не ПустаяСтрока(АдресДвоичныхДанных) И ЗначениеЗаполнено(ВладелецФайла) Тогда
			ПредставлениеВидаТорговойПлощадки =
				ИнтеграцияСМаркетплейсамиПовтИсп.ПредставлениеВидаТорговойПлощадки(ВидМаркетплейса);
			
			ИнтеграцияСМаркетплейсамиСервер.ЗаписатьДвоичныеДанныеВПрисоединенныйФайл(
				ВладелецФайла,
				АдресДвоичныхДанных,
				ПредставлениеВидаТорговойПлощадки + " - " + НаименованиеФайла,
				РасширениеФайла);
		КонецЕсли;
		
		Если ПреобразоватьВPDF Тогда
			ИнтеграцияСМаркетплейсамиСервер.ПреобразоватьВPDF(АдресДвоичныхДанных, РасширениеФайла);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
