
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не предусмотрен возврат к отгрузке из этого источника.';
														|en = 'Cannot return to shipment from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма",         ПараметрыВыполненияКоманды.Источник);
	
	ПолучитьОтправленияДляВозвратаКОтгрузке(ПараметрыЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДоступныеСтатусы()
	
	ДоступныеСтатусы = Новый Массив;
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.Спор"));
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.НеПринятВДоставку"));
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПередаетсяВДоставку"));
	
	Возврат ДоступныеСтатусы;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьОтправленияДляВозвратаКОтгрузке(Знач Параметры)
	
	Форма = Параметры.Форма;
	
	ОтправленияДляВозвратаКОтгрузке = Новый Структура;
	ОтправленияДляВозвратаКОтгрузке.Вставить("Заказы",      Новый Массив);
	ОтправленияДляВозвратаКОтгрузке.Вставить("Отправления", Новый Массив);
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИмяТаблицы = "ТаблицаЗаказов";
		
		ИдентификаторыВыделенныхСтрок   = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ОтправленияДляВозвратаКОтгрузке = ПолучитьОтправленияДоступныеДляВозвратаКОтгрузке(ИдентификаторыВыделенныхСтрок,
			Форма.ТаблицаЗаказов);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ИмяТаблицы              = "";
		
		Если ДоступныеСтатусы().Найти(Форма.СтатусОтправления) <> Неопределено Тогда
			ОтправленияДляВозвратаКОтгрузке.Заказы.Добавить(Форма.ДокументЗаказа);
			ОтправленияДляВозвратаКОтгрузке.Отправления.Добавить(Форма.НомерОтправления);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Невозможно вернуть к отгрузке отправление %1 в статусе ""%2"".';
					|en = 'Cannot return the %1 shipment in the ""%2"" status to shipment.'"),
				Форма.НомерОтправления,
				Форма.СтатусОтправления);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	Иначе
		ИмяТаблицы = "Список";
		
		КлючиВыделенныхСтрок            = Форма.Элементы.Список.ВыделенныеСтроки;
		ОтправленияДляВозвратаКОтгрузке = ПолучитьОтправленияДоступныеДляВозвратаКОтгрузке(КлючиВыделенныхСтрок);
	КонецЕсли;
	
	Если ОтправленияДляВозвратаКОтгрузке.Отправления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИмяТаблицы",        ИмяТаблицы);
	Параметры.Вставить("Заказы",            ОтправленияДляВозвратаКОтгрузке.Заказы);
	Параметры.Вставить("НомераОтправлений", ОтправленияДляВозвратаКОтгрузке.Отправления);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВернутьКОтгрузке", ЭтотОбъект, Параметры);
	
	ТекстВопроса = НСтр("ru = 'Вернуть к отгрузке выделенные отправления?';
						|en = 'Do you want to return the selected shipments to shipment?'");
	ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОтправленияДоступныеДляВозвратаКОтгрузке(ВыделенныеСтроки, Таблица = Неопределено)

	НомерОтправления    = "";
	ИдентификаторЗаказа = "";
	СтатусОтправления   = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка");
	
	ДоступныеСтатусы = ДоступныеСтатусы();
	
	ЗаказыДляВозвратаКОтгрузке      = Новый Массив;
	ОтправленияДляВозвратаКОтгрузке = Новый Массив;
	ОтправленияПрочие               = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказ             = ДанныеСтроки.ДокументЗаказа;
			НомерОтправления  = ДанныеСтроки.НомерОтправления;
			СтатусОтправления = ДанныеСтроки.СтатусОтправления;
			
			Если ДоступныеСтатусы.Найти(СтатусОтправления) <> Неопределено Тогда
				ЗаказыДляВозвратаКОтгрузке.Добавить(Заказ);
				ОтправленияДляВозвратаКОтгрузке.Добавить(НомерОтправления);
			Иначе
				ОтправленияПрочие.Добавить(НомерОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтправленияДляВозвратаКОтгрузке.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет отправлений, доступных для возврата к отгрузке.';
								|en = 'There are no shipments available for return to shipment.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли ОтправленияПрочие.Количество() > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Невозможно вернуть к отгрузке отправления: %1.';
				|en = 'Cannot return shipments to shipment: %1.'"),
			СтрСоединить(ОтправленияПрочие, ", "));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Заказы",      ЗаказыДляВозвратаКОтгрузке);
	Результат.Вставить("Отправления", ОтправленияДляВозвратаКОтгрузке);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВернутьКОтгрузке(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма             = ДополнительныеПараметры.Форма;
	УчетнаяЗапись     = ДополнительныеПараметры.УчетнаяЗапись;
	НомераОтправлений = ДополнительныеПараметры.НомераОтправлений;
	
	Если Не ЗначениеЗаполнено(НомераОтправлений) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	
	ДлительнаяОперация = ВернутьКОтгрузкеНаСервере(
		ДанныеУчетнойЗаписи,
		Форма.УникальныйИдентификатор,
		НомераОтправлений);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания             = Истина;
	ПараметрыОжидания.ТекстСообщения                   = НСтр("ru = 'Выполняется возврат к отгрузке выделенных отправлений.';
																|en = 'Returning the selected shipments to shipment.'");
	ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст     = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершено фоновое задание возврата к отгрузке.';
																|en = 'The background job for returning to shipment is completed.'");
	ПараметрыОжидания.ОповещениеПользователя.Картинка  = ДанныеУчетнойЗаписи.Логотип;
	
	ДополнительныеПараметры.Удалить("НомераОтправлений");
	ДополнительныеПараметры.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	
	Обработчик = Новый ОписаниеОповещения("ВернутьКОтгрузкеЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ВернутьКОтгрузкеНаСервере(ДанныеУчетнойЗаписиь, Знач ИдентификаторФормы, Знач НомераОтправлений)
	
	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Возврат к отгрузке.';
			|en = '%1. Return to shipment.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ВернутьКОтгрузкеНаТорговойПлощадке";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		НомераОтправлений);
	
КонецФункции

// Обработка оповещения подтверждения сборки заказов.
//
// Параметры:
//   РезультатЗадания        - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
&НаКлиенте
Процедура ВернутьКОтгрузкеЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДополнительныеПараметры.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Если Не ПустаяСтрока(ДополнительныеПараметры.ИмяТаблицы) Тогда
				Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
				Форма.Элементы[ДополнительныеПараметры.ИмяТаблицы].Обновить();
			КонецЕсли;
			
			Оповестить("МП_ВыполненВозвратКОтгрузкеНаТорговойПлощадке", ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
