
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не предусмотрено оформление документов отгрузки из этого источника.';
														|en = 'Cannot register shipping documents from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма",         ПараметрыВыполненияКоманды.Источник);
	
	ОткрытьФормуПечатиЭтикеток(ПараметрыЗадания);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОткрытьФормуПечатиЭтикеток(Параметры)

	Форма = Параметры.Форма;
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ЭтоFBO = Форма.ЭтоFBO;
		ТаблицаЗаказов = Форма.ТаблицаЗаказов;
		ВыделенныеСтроки = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ЭтоFBO = Ложь;
		ТаблицаЗаказов = Неопределено;
		
		ДанныеОтправления = Новый Структура;
		ДанныеОтправления.Вставить("ДокументЗаказа", ПредопределенноеЗначение("Документ.ЗаказКлиента.ПустаяСсылка"));
		ДанныеОтправления.Вставить("НомерОтправления", "");
		ДанныеОтправления.Вставить("СтатусОтправления",
			ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка"));
		ЗаполнитьЗначенияСвойств(ДанныеОтправления, Форма);
		
		ВыделенныеСтроки = Новый Массив;
		ВыделенныеСтроки.Добавить(ДанныеОтправления);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ФормаСпискаЗаказов" Тогда
		ЭтоFBO = Ложь;
		ТаблицаЗаказов = Неопределено;
		ВыделенныеСтроки = Форма.Элементы.Список.ВыделенныеСтроки;
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки" Тогда
		ЭтоFBO = Ложь;
		ТаблицаЗаказов = Неопределено;
		ВыделенныеСтроки = Форма.Элементы.Список.ВыделенныеСтроки;
		
	Иначе
		Возврат;
	КонецЕсли;
	
	ОбъектыПечати = ПолучитьОбъектыПечати(Форма.УчетнаяЗапись, ВыделенныеСтроки, ТаблицаЗаказов);
	
	Если ОбъектыПечати.Количество() > 0 Тогда
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("ЭтоЗаказКлиента", Истина);
		ПараметрыОткрытия.Вставить("ЭтоFBO",          ЭтоFBO);
		ПараметрыОткрытия.Вставить("Объекты",         ОбъектыПечати);
		
		ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПечатьЭтикеток",
			ПараметрыОткрытия,
			ЭтотОбъект);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для выделенных строк печать этикеток не предусмотрена.';
														|en = 'Cannot print labels for the selected lines.'"));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьОбъектыПечати(УчетнаяЗапись, ВыделенныеСтроки, ТаблицаЗаказов)

	ОбъектыПечати = Новый Массив;
	
	СтатусыПечатиЭтикеток = СтатусыПечатиЭтикеток();
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, ТаблицаЗаказов);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.ДокументЗаказа) Тогда
				Если СтатусыПечатиЭтикеток.Найти(ДанныеСтроки.СтатусОтправления) <> Неопределено Тогда
					ОбъектыПечати.Добавить(ДанныеСтроки.ДокументЗаказа);
				ИначеЕсли ВыделенныеСтроки.Количество() = 1 Тогда
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Печать этикеток возможна только для заказов в статусах: ""%1"".';
							|en = 'You can only print labels for orders with the following statuses: %1.'"),
						СтрСоединить(СтатусыПечатиЭтикеток, """, """));
						
					ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
					Прервать;
				КонецЕсли;
			ИначеЕсли ВыделенныеСтроки.Количество() = 1 Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Для выделенной строки печать этикеток не предусмотрена. Загрузите заказ с торговой площадки.';
																|en = 'Cannot print labels for the selected line. Import the order from the marketplace.'"));
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбъектыПечати;

КонецФункции

&НаСервере
Функция СтатусыПечатиЭтикеток()

	Возврат РегистрыСведений.ЗаказыТорговыхПлощадок.СтатусыПечатиЭтикеток();

КонецФункции

#КонецОбласти
