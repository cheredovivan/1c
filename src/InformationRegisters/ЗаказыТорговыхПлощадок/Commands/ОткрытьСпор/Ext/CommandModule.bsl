
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не предусмотрено открытие спора из этого источника.';
														|en = 'Cannot open a dispute from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма",         ПараметрыВыполненияКоманды.Источник);
	
	ПолучитьОтправленияДляОткрытияСпора(ПараметрыЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ДоступныеСтатусы()
	
	ДоступныеСтатусы = Новый Массив;
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ГотовКОтгрузке"));
	ДоступныеСтатусы.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.НеПринятВДоставку"));
	
	Возврат ДоступныеСтатусы;
	
КонецФункции

&НаКлиенте
Процедура ПолучитьОтправленияДляОткрытияСпора(Знач Параметры)
	
	Форма = Параметры.Форма;
	
	ОтправленияДляОткрытияСпора = Новый Структура;
	ОтправленияДляОткрытияСпора.Вставить("Заказы",      Новый Массив);
	ОтправленияДляОткрытияСпора.Вставить("Отправления", Новый Массив);
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИмяТаблицы = "ТаблицаЗаказов";
		
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ОтправленияДляОткрытияСпора   = ПолучитьОтправленияДоступныеДляОткрытияСпора(ИдентификаторыВыделенныхСтрок,
			Форма.ТаблицаЗаказов);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ИмяТаблицы              = "";
		
		Если ДоступныеСтатусы().Найти(Форма.СтатусОтправления) <> Неопределено Тогда
			ОтправленияДляОткрытияСпора.Заказы.Добавить(Форма.ДокументЗаказа);
			ОтправленияДляОткрытияСпора.Отправления.Добавить(Форма.НомерОтправления);
		Иначе
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Невозможно открыть спор по отправлению %1 в статусе ""%2"".';
					|en = 'Cannot open a dispute on the %1 shipment in the ""%2"" status.'"),
				Форма.НомерОтправления,
				Форма.СтатусОтправления);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	Иначе
		ИмяТаблицы = "Список";
		
		КлючиВыделенныхСтрок        = Форма.Элементы.Список.ВыделенныеСтроки;
		ОтправленияДляОткрытияСпора = ПолучитьОтправленияДоступныеДляОткрытияСпора(КлючиВыделенныхСтрок);
	КонецЕсли;
	
	Если ОтправленияДляОткрытияСпора.Отправления.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИмяТаблицы",        ИмяТаблицы);
	Параметры.Вставить("Заказы",            ОтправленияДляОткрытияСпора.Заказы);
	Параметры.Вставить("НомераОтправлений", ОтправленияДляОткрытияСпора.Отправления);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьСпор", ЭтотОбъект, Параметры);
	
	ТекстВопроса = НСтр("ru = 'Открыть спор для выделенных отправлений?';
						|en = 'Do you want to open a dispute for the selected shipments?'");
	ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьОтправленияДоступныеДляОткрытияСпора(ВыделенныеСтроки, Таблица = Неопределено)

	НомерОтправления  = "";
	СтатусОтправления = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка");
	
	ДоступныеСтатусы = ДоступныеСтатусы();
	
	ЗаказыДляОткрытияСпора      = Новый Массив;
	ОтправленияДляОткрытияСпора = Новый Массив;
	ОтправленияПрочие           = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказ             = ДанныеСтроки.ДокументЗаказа;
			НомерОтправления  = ДанныеСтроки.НомерОтправления;
			СтатусОтправления = ДанныеСтроки.СтатусОтправления;
			
			Если ДоступныеСтатусы.Найти(СтатусОтправления) <> Неопределено Тогда
				ЗаказыДляОткрытияСпора.Добавить(Заказ);
				ОтправленияДляОткрытияСпора.Добавить(НомерОтправления);
			Иначе
				ОтправленияПрочие.Добавить(НомерОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтправленияДляОткрытияСпора.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет отправлений, доступных для открытия спора.';
								|en = 'No shipments to open a dispute.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли ОтправленияПрочие.Количество() > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Невозможно открыть спор по отправлениям: %1.';
				|en = 'Cannot open a dispute on shipments: %1.'"),
			СтрСоединить(ОтправленияПрочие, ", "));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Заказы",      ЗаказыДляОткрытияСпора);
	Результат.Вставить("Отправления", ОтправленияДляОткрытияСпора);
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОткрытьСпор(РезультатВопроса, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Форма             = ДополнительныеПараметры.Форма;
	УчетнаяЗапись     = ДополнительныеПараметры.УчетнаяЗапись;
	НомераОтправлений = ДополнительныеПараметры.НомераОтправлений;
	
	Если Не ЗначениеЗаполнено(НомераОтправлений) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	
	ДлительнаяОперация = ОткрытьСпорНаСервере(
		ДанныеУчетнойЗаписи,
		Форма.УникальныйИдентификатор,
		НомераОтправлений);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания             = Истина;
	ПараметрыОжидания.ТекстСообщения                   = НСтр("ru = 'Выполняется открытие спора по выделенным отправлениям.';
																|en = 'Opening a dispute on selected shipments.'");
	ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст     = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершено фоновое задание открытия спора.';
																|en = 'The background job of opening a dispute is completed.'");
	ПараметрыОжидания.ОповещениеПользователя.Картинка  = ДанныеУчетнойЗаписи.Логотип;
	
	ДополнительныеПараметры.Удалить("НомераОтправлений");
	ДополнительныеПараметры.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ВидМаркетплейса);
	
	Обработчик = Новый ОписаниеОповещения("ОткрытьСпорЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОткрытьСпорНаСервере(ДанныеУчетнойЗаписи, Знач ИдентификаторФормы, Знач НомераОтправлений)
	
	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Открытие спора.';
			|en = '%1. Open a dispute.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОткрытьСпорНаТорговойПлощадке";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		НомераОтправлений);
	
КонецФункции

// Обработка оповещения подтверждения сборки заказов.
//
// Параметры:
//   РезультатЗадания        - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
&НаКлиенте
Процедура ОткрытьСпорЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДополнительныеПараметры.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Если Не ПустаяСтрока(ДополнительныеПараметры.ИмяТаблицы) Тогда
				Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
				Форма.Элементы[ДополнительныеПараметры.ИмяТаблицы].Обновить();
			КонецЕсли;
			
			Оповестить("МП_ВыполненоОткрытиеСпораНаТорговойПлощадке", ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
