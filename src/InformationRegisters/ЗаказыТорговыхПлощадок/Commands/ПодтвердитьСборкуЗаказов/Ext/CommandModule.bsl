#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не предусмотрено подтверждение сборки заказов из этого источника.';
				|en = 'Cannot confirm order picking from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма", ПараметрыВыполненияКоманды.Источник);
	
	ПолучитьОтправленияДляСборки(ПараметрыЗадания);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьОтправленияДляСборки(Знач Параметры)
	
	Форма = Параметры.Форма; 
	ВидМаркетплейса = ИнтеграцияСМаркетплейсамиВызовСервера.ВидМаркетплейсаУчетнойЗаписи(Параметры.УчетнаяЗапись);
	
	ОжидающиеСборки = Новый Структура;
	ОжидающиеСборки.Вставить("Заказы",      Новый Массив);
	ОжидающиеСборки.Вставить("Отправления", Новый Массив);
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИмяТаблицы = "ТаблицаЗаказов";
		
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ОжидающиеСборки = ПолучитьОтправленияОжидающиеСборки(
			ВидМаркетплейса, ИдентификаторыВыделенныхСтрок, Форма.ТаблицаЗаказов);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ИмяТаблицы = "";
		
		СтатусОжидаетСборки =
			ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки");
		
		Если Форма.СтатусОтправления = СтатусОжидаетСборки Тогда
			ОжидающиеСборки.Заказы.Добавить(Форма.ДокументЗаказа);
			ОжидающиеСборки.Отправления.Добавить(Форма.НомерОтправления);
		Иначе
			
			Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
				ШаблонСообщения = НСтр("ru = 'Подтверждение сборки не требуется для отправления %1.';
										|en = 'The %1 shipment does not require picking confirmation.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'Подтверждение сборки не требуется для заказа %1.';
										|en = 'The %1 order does not require picking confirmation.'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				Форма.НомерОтправления);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	Иначе
		ИмяТаблицы = "Список";
		
		КлючиВыделенныхСтрок = Форма.Элементы.Список.ВыделенныеСтроки;
		ОжидающиеСборки = ПолучитьОтправленияОжидающиеСборки(ВидМаркетплейса, КлючиВыделенныхСтрок);
	КонецЕсли;  
	
	Если (ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon")
		 И ОжидающиеСборки.Отправления.Количество() = 0)
		 Или (ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет")
		 	  И ОжидающиеСборки.Заказы.Количество() = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИмяТаблицы",        ИмяТаблицы);
	Параметры.Вставить("Заказы",            ОжидающиеСборки.Заказы);
	Параметры.Вставить("НомераОтправлений", ОжидающиеСборки.Отправления);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПодтвердитьСборкуЗаказов", ЭтотОбъект, Параметры);
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ТекстВопроса = НСтр("ru = 'Подтвердить сборку для выделенных отправлений?';
							|en = 'Do you want to confirm picking of the selected shipments?'");
	Иначе
		ТекстВопроса = НСтр("ru = 'Подтвердить сборку для выделенных заказов?';
							|en = 'Do you want to confirm picking of the selected orders?'");
	КонецЕсли;
	ПоказатьВопрос(ОповещениеОЗавершении, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьОтправленияОжидающиеСборки(ВидМаркетплейса, ВыделенныеСтроки, Таблица = Неопределено)

	НомерОтправления  = "";
	СтатусОтправления = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка");
	
	СтатусОжидаетСборки =
		ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ОжидаетСборки");
		
	ЗаказыОжидающиеСборки      = Новый Массив;
	ОтправленияОжидающиеСборки = Новый Массив;
	ОтправленияПрочие          = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказ             = ДанныеСтроки.ДокументЗаказа;
			НомерОтправления  = ДанныеСтроки.НомерОтправления;
			СтатусОтправления = ДанныеСтроки.СтатусОтправления;
		
			Если СтатусОтправления = СтатусОжидаетСборки Тогда
				ЗаказыОжидающиеСборки.Добавить(Заказ);
				ОтправленияОжидающиеСборки.Добавить(НомерОтправления);
			Иначе
				ОтправленияПрочие.Добавить(НомерОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтправленияОжидающиеСборки.Количество() = 0 Тогда
		
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ТекстСообщения = НСтр("ru = 'Нет отправлений, требующих подтверждение сборки.';
									|en = 'No shipments that required picking confirmation.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Нет заказов, требующих подтверждение сборки.';
									|en = 'No orders that required picking confirmation.'");
		КонецЕсли;
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	ИначеЕсли ОтправленияПрочие.Количество() > 0 Тогда
		
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ШаблонСообщения = НСтр("ru = 'Подтверждение сборки не требуется для следующих отправлений: %1.';
									|en = 'The following shipments do not require picking confirmation: %1.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Подтверждение сборки не требуется для следующих заказов: %1.';
									|en = 'The following orders do not require picking confirmation: %1.'");
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			СтрСоединить(ОтправленияПрочие, ", "));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет")
			И ОтправленияОжидающиеСборки.Количество() > 0 Тогда
		// Заменяем номера заказов на номера отправлений
		ОтправленияОжидающиеСборки = ИнтеграцияСЯндексМаркетВызовСервера.ВсеОтправленияЗаказов(
			ОтправленияОжидающиеСборки);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Заказы",      ЗаказыОжидающиеСборки);
	Результат.Вставить("Отправления", ОтправленияОжидающиеСборки);
	
	Возврат Результат;

КонецФункции

// Обработка выбора ответа.
//
// Параметры:
//   Ответ - КодВозвратаДиалога - выбранный ответ.
//   Параметры - Структура из КлючИЗначение - параметры обработки ответа.
//
&НаКлиенте
Процедура ПодтвердитьСборкуЗаказов(Ответ, Параметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметры.НомераОтправлений) Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьПодтверждениеСборкиЗаказов(Параметры);

КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПодтверждениеСборкиЗаказов(Параметры)

	ПараметрыОперации = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры); // Структура
	ПараметрыОперации.Удалить("Форма");
	
	ДлительнаяОперация = ЗапуститьПодтверждениеСборкиЗаказовНаСервере(
		ПараметрыОперации,
		Параметры.Форма.УникальныйИдентификатор);
	
	Параметры.Вставить("ДанныеУчетнойЗаписи", ПараметрыОперации.ДанныеУчетнойЗаписи);
	Параметры.Вставить("НесопоставленныеПричиныОтмены", ПараметрыОперации.НесопоставленныеПричиныОтмены);
	
	Если ДлительнаяОперация = Неопределено Тогда
		СопоставитьПричиныОтмены(Новый Структура, Параметры);
	Иначе
		ПерейтиКПодтвердениюСборкиЗаказов(ДлительнаяОперация, Параметры);
	КонецЕсли;

КонецПроцедуры

// Запускает длительную операцию.
//
// Параметры:
//   Параметры - Структура из Строка - список входящих параметров.
//   ИдентификаторФормы - УникальныйИдентификатор - идентификатор формы.
//
// Возвращаемое значение:
//   Неопределено,
//   см. ДлительныеОперации.ВыполнитьФункцию.
//
&НаСервере
Функция ЗапуститьПодтверждениеСборкиЗаказовНаСервере(Параметры, Знач ИдентификаторФормы)

	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(УчетнаяЗапись);
	Параметры.Вставить("ДанныеУчетнойЗаписи", ДанныеУчетнойЗаписи);
	Заказы = Параметры.Заказы;    
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		НомераОтправлений = ИнтеграцияСЯндексМаркетСервер.ПолучитьОтправленияЗаказов(Заказы);  
	Иначе
		НомераОтправлений = Параметры.НомераОтправлений;
	КонецЕсли;
	
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		НесопоставленныеПричиныОтмены = ПроверитьПричиныОтмены(УчетнаяЗапись,, НомераОтправлений);
	Иначе
		НесопоставленныеПричиныОтмены = Новый Массив;
	КонецЕсли;
	
	Параметры.Вставить("НесопоставленныеПричиныОтмены", НесопоставленныеПричиныОтмены);
	
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon
		И НесопоставленныеПричиныОтмены.Количество() > 0 Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Подтверждение сборки заказов.';
			|en = '%1. Confirm order picking.'"),
		ДанныеУчетнойЗаписи.ВидМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ПодтвердитьСборкуЗаказовНаТорговойПлощадке";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
		УчетнаяЗапись,
		Заказы,
		НомераОтправлений);

КонецФункции

&НаСервере
Функция ПроверитьПричиныОтмены(УчетнаяЗапись, Заказы = Неопределено, НомераОтправлений = Неопределено)

	НесопоставленныеПричиныОтмены = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ТоварыЗаказов.ПричинаОтмены КАК ПричинаОтмены
		|ИЗ
		|	РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок КАК СведенияПоТоварам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаказКлиента.Товары КАК ТоварыЗаказов
		|		ПО СведенияПоТоварам.Заказ = ТоварыЗаказов.Ссылка
		|			И СведенияПоТоварам.КодСтроки = ТоварыЗаказов.КодСтроки
		|			И (ТоварыЗаказов.Отменено)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
		|		ПО (СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись)
		|			И (СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.ПричинаОтмены))
		|			И (СоответствияОбъектовМаркетплейсов.Объект1С = ТоварыЗаказов.ПричинаОтмены)
		|ГДЕ
		|	&Условие
		|	И СоответствияОбъектовМаркетплейсов.Объект1С ЕСТЬ NULL";
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	Если НомераОтправлений <> Неопределено Тогда
		Условие = "СведенияПоТоварам.НомерОтправления В(&НомераОтправлений)";
		Запрос.УстановитьПараметр("НомераОтправлений", НомераОтправлений);
	ИначеЕсли Заказы <> Неопределено Тогда
		Условие = "СведенияПоТоварам.Заказ В(&Заказы)";
		Запрос.УстановитьПараметр("Заказы", Заказы);
	Иначе
		Возврат НесопоставленныеПричиныОтмены;
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", Условие);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.Следующий() Цикл
		НесопоставленныеПричиныОтмены.Добавить(Выборка.ПричинаОтмены);
	КонецЦикла;
	
	Возврат НесопоставленныеПричиныОтмены;

КонецФункции

&НаКлиенте
Процедура СопоставитьПричиныОтмены(Результат, Параметры) Экспорт

	Если ТипЗнч(Результат) <> Тип("Структура") Тогда
		Возврат;
	ИначеЕсли ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Результат, "Ссылка") Тогда
		Параметры.НесопоставленныеПричиныОтмены.Удалить(0);
	КонецЕсли;
	
	Если Параметры.НесопоставленныеПричиныОтмены.Количество() = 0 Тогда
		ЗапуститьПодтверждениеСборкиЗаказов(Параметры);
	Иначе
		ПричинаОтмены = Параметры.НесопоставленныеПричиныОтмены[0];
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("УчетнаяЗапись", Параметры.УчетнаяЗапись);
		ПараметрыФормы.Вставить("ПричинаОтмены", ПричинаОтмены);
		Если ЗначениеЗаполнено(ПричинаОтмены) Тогда
			ПараметрыФормы.Вставить("Пояснение",
				НСтр("ru = 'Выберите причину отмены из списка торговой площадки для отмененных в заказе позиций.';
					|en = 'Select the cancellation reason from the marketplace list for the items cancelled in the order.'"));
		КонецЕсли;
		
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("СопоставитьПричиныОтмены",
			ЭтотОбъект,
			Параметры);
		
		ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ВыборПричиныОтмены",
			ПараметрыФормы,,,,,
			ОповещениеОЗакрытии);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКПодтвердениюСборкиЗаказов(ДлительнаяОперация, Знач Параметры)

	Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(Параметры.ДанныеУчетнойЗаписи.ВидМаркетплейса);
	
	ТекстОжидания = НСтр("ru = 'Выполняется подтверждение сборки выделенных заказов.';
						|en = 'Confirming picking of the selected orders.'");
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Параметры.Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = ТекстОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = Параметры.ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение =
		НСтр("ru = 'Завершено фоновое подтверждение сборки заказов.';
			|en = 'Background confirmation of order picking is completed.'");
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "";
	ПараметрыОжидания.ОповещениеПользователя.Картинка = Логотип;
	
	Параметры = ОбщегоНазначенияКлиент.СкопироватьРекурсивно(Параметры); // Структура
	Параметры.Удалить("НомераОтправлений");
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьПодтверждениеСборкиЗаказовЗавершение",
		ЭтотОбъект,
		Параметры);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);

КонецПроцедуры

&НаКлиенте
// Обработка оповещения подтверждения сборки заказов.
//
// Параметры:
//   РезультатЗадания - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
Процедура ВыполнитьПодтверждениеСборкиЗаказовЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт

	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(
				ДополнительныеПараметры.ДанныеУчетнойЗаписи.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Если Не ПустаяСтрока(ДополнительныеПараметры.ИмяТаблицы) Тогда
				Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
				Форма.Элементы[ДополнительныеПараметры.ИмяТаблицы].Обновить();
			КонецЕсли;
			Для Каждого Сообщение Из РезультатЗадания.Сообщения Цикл
				Сообщение.Сообщить();
			КонецЦикла;
			
			Оповестить("МП_ВыполненоПодтверждениеСборкиЗаказовНаТорговойПлощадке", ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
