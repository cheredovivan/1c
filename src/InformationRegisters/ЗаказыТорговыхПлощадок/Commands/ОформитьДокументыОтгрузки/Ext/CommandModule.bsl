
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не предусмотрено оформление документов отгрузки из этого источника.';
														|en = 'Cannot register shipping documents from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма",         ПараметрыВыполненияКоманды.Источник);
	
	ПолучитьЗаказыДляОформленияДокументовОтгрузки(ПараметрыЗадания);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьЗаказыДляОформленияДокументовОтгрузки(Знач Параметры)
	
	Форма  = Параметры.Форма;
	Заказы = Новый Массив;
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ФормаСпискаЗаказов"
			Или Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки" Тогда
		ИмяТаблицы           = "Список";
		КлючиВыделенныхСтрок = Форма.Элементы.Список.ВыделенныеСтроки;
		Заказы               = ЗаказыДляОформленияДокументовОтгрузки(КлючиВыделенныхСтрок);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ДополнительныеСведенияПоТоварамЗаказовТорговыхПлощадок.Форма.ЗаполнениеДанныхЭкземпляров" Тогда
		ИмяТаблицы = "";
		Заказы.Добавить(Форма.Заказ);
		
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Заказы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИмяТаблицы", ИмяТаблицы);
	Параметры.Вставить("Заказы",     Заказы);
	
	ОформитьДокументыОтгрузки(Параметры);
	
КонецПроцедуры

&НаКлиенте
Функция ЗаказыДляОформленияДокументовОтгрузки(ВыделенныеСтроки, Таблица = Неопределено)
	
	Заказы = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказы.Добавить(ДанныеСтроки.Заказ);
		КонецЕсли;
	КонецЦикла;
	
	Если Заказы.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Нет заказов, доступных для оформления документов отгрузки.';
								|en = 'There are no orders available for registering shipping documents.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Заказы;
	
КонецФункции

&НаКлиенте
Процедура ОформитьДокументыОтгрузки(Параметры)
	
	Форма         = Параметры.Форма;
	УчетнаяЗапись = Параметры.УчетнаяЗапись;
	Заказы        = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Параметры.Заказы);
	
	Если Не ЗначениеЗаполнено(Заказы) Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	
	ДлительнаяОперация = ОформитьДокументыОтгрузкиНаСервере(
		ДанныеУчетнойЗаписи,
		Форма.УникальныйИдентификатор,
		Заказы);
	
	Параметры.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ВидМаркетплейса);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания             = Истина;
	ПараметрыОжидания.ТекстСообщения                   = НСтр("ru = 'Выполняется оформление документов отгрузки.';
																|en = 'Registering shipping documents.'");
	ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст     = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = НСтр("ru = 'Завершено оформление документов отгрузки';
																|en = 'Shipping documents are registered'");
	ПараметрыОжидания.ОповещениеПользователя.Картинка  = ДанныеУчетнойЗаписи.Логотип;
	
	Обработчик = Новый ОписаниеОповещения("ОформитьДокументыОтгрузкиЗавершение", ЭтотОбъект, Параметры);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция ОформитьДокументыОтгрузкиНаСервере(ДанныеУчетнойЗаписи, Знач ИдентификаторФормы, Знач Заказы)
	
	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Оформление документов отгрузки.';
			|en = '%1. Register shipping documents.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение            = 0;
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОформитьДокументыОтгрузки";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		ИмяМетода,
		Заказы, 
		Пользователи.ТекущийПользователь());
	
КонецФункции

// Обработка оповещения подтверждения сборки заказов.
//
// Параметры:
//   РезультатЗадания        - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
&НаКлиенте
Процедура ОформитьДокументыОтгрузкиЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Ошибка оформления документов отгрузки. Подробности см. в журнале регистрации.';
					|en = 'An error occurred when registering the shipping documents. For details, see the event log.'"));
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			ИнформацияОбОшибке = Ошибка.ИнформацияОбОшибке;
			Если ТипЗнч(ИнформацияОбОшибке) = Тип("Структура")
					И Не ПустаяСтрока(ИнформацияОбОшибке.ОписаниеОшибки) Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(ИнформацияОбОшибке.ОписаниеОшибки);
			КонецЕсли;
			
			ОбработатьРезультатПроверкиЗаказов(ДополнительныеПараметры.ВидМаркетплейса, Ошибка.РезультатПроверкиЗаказов);
			
			Оповестить("МП_ОбновлениеСведенийПоЗаказамИОтправлениям",
				Ошибка.РезультатПроверкиЗаказов,
				ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатПроверкиЗаказов(ВидМаркетплейса, РезультатПроверкиЗаказов)
	
	Если Не ЗначениеЗаполнено(РезультатПроверкиЗаказов) Тогда
		Возврат;
	КонецЕсли;
	
	НеоформленныеОтправления = Новый Массив;
	ОтправленияОтмененные = Новый Массив;
	
	Для Каждого КлючИЗначение Из РезультатПроверкиЗаказов Цикл
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			НеоформленныеОтправления,
			КлючИЗначение.Значение.ОтправленияБезДокументовОтгрузки, Истина);
			
		Для Каждого ЭлементКоллекции Из КлючИЗначение.Значение.ИзмененныеДокументыОтгрузки Цикл
			НеоформленныеОтправления.Добавить(ЭлементКоллекции.Значение);
		КонецЦикла;
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
			ОтправленияОтмененные,
			КлючИЗначение.Значение.ОтправленияОтмененные, Истина);
	КонецЦикла;
	
	Если НеоформленныеОтправления.Количество() > 0 Тогда
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ШаблонСообщения = НСтр("ru = 'Не удалось оформить документы отгрузки для отправлений: %1.';
									|en = 'Cannot register shipping documents for shipments: %1.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Не удалось оформить документы отгрузки для заказов: %1.';
									|en = 'Cannot register shipping documents for orders: %1.'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, СтрСоединить(НеоформленныеОтправления, ", ")));
	КонецЕсли;
	
	Если ОтправленияОтмененные.Количество() > 0 Тогда
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ШаблонСообщения = НСтр("ru = 'Не оформляются документы отгрузки для отмененных отправлений: %1.';
									|en = 'Shipping documents are not registered for cancelled shipments: %1.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Не оформляются документы отгрузки для отмененных заказов: %1.';
									|en = 'Shipping documents are not registered for cancelled orders: %1.'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения, СтрСоединить(ОтправленияОтмененные, ", ")));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
