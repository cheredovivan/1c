#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не предусмотрена загрузка заказов из этого источника.';
				|en = 'Cannot import orders from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма", ПараметрыВыполненияКоманды.Источник);
	
	ЗапуститьЗагрузкуЗаказов(ПараметрыЗадания);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьЗагрузкуЗаказов(Параметры)

	Форма = Параметры.Форма;
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		Если ИдентификаторыВыделенныхСтрок.Количество() = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(
				НСтр("ru = 'Нет заказов для загрузки.';
					|en = 'No orders to import.'"));
			Возврат;
		КонецЕсли;
		
		ИмяТаблицы = "ТаблицаЗаказов";
		
		ВыделенныеСтроки = Новый Массив;
		Для Каждого ИдентификаторСтроки Из ИдентификаторыВыделенныхСтрок Цикл
			ДанныеСтроки = Форма[ИмяТаблицы].НайтиПоИдентификатору(ИдентификаторСтроки);
			Если ДанныеСтроки <> Неопределено Тогда
				ВыделенныеСтроки.Добавить(
					Новый Структура("ИдентификаторЗаказа, ДатаОтправления",
						ДанныеСтроки.ИдентификаторЗаказа,
						ДанныеСтроки.ДатаОтправления));
			Иначе
				Продолжить;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ИмяТаблицы = "";
		
		ВыделенныеСтроки = Новый Массив;
		ВыделенныеСтроки.Добавить(
			Новый Структура("ИдентификаторЗаказа, ДатаОтправления",
				Форма.ИдентификаторЗаказа,
				Форма.ДатаОтправления));
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки" Тогда
		ИмяТаблицы = "";
		ВыделенныеСтроки = Неопределено;
		
	Иначе
		ИмяТаблицы       = "Список";
		ВыделенныеСтроки = Неопределено;
	КонецЕсли;
	
	Если ВыделенныеСтроки = Неопределено Тогда
		ТекстОжидания = НСтр("ru = 'Выполняется загрузка новых заказов.';
							|en = 'Importing new orders.'");
	Иначе
		ТекстОжидания = НСтр("ru = 'Выполняется загрузка выделенных заказов.';
							|en = 'Importing selected orders.'");
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", Параметры.УчетнаяЗапись);
	
	ДлительнаяОперация = ВыполнитьЗагрузкуНовыхЗаказов(
		ДанныеУчетнойЗаписи,
		Форма.УникальныйИдентификатор,
		ВыделенныеСтроки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = ТекстОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение =
		НСтр("ru = 'Завершена фоновая загрузка заказов.';
			|en = 'Background order import is completed.'");
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "";
	ПараметрыОжидания.ОповещениеПользователя.Картинка = ДанныеУчетнойЗаписи.Логотип;
	
	ДопПараметрыОповещения = Новый Структура;
	ДопПараметрыОповещения.Вставить("Форма",           Форма);
	ДопПараметрыОповещения.Вставить("ИмяТаблицы",      ИмяТаблицы);
	ДопПараметрыОповещения.Вставить("УчетнаяЗапись",   ДанныеУчетнойЗаписи.УчетнаяЗапись);
	ДопПараметрыОповещения.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ВидМаркетплейса);
	
	Обработчик = Новый ОписаниеОповещения("ВыполнитьЗагрузкуНовыхЗаказовЗавершение",
		ЭтотОбъект,
		ДопПараметрыОповещения);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ВыполнитьЗагрузкуНовыхЗаказов(ДанныеУчетнойЗаписи, Знач ИдентификаторФормы, Знач ВыделенныеСтроки)

	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Загрузка заказов.';
			|en = '%1. Import orders.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ЗагрузитьЗаказыТорговойПлощадки";
	
	ОкончаниеПериода = КонецДня(ТекущаяДатаСеанса());
	НачалоПериода    = НачалоДня(ДобавитьМесяц(ОкончаниеПериода, -1));
	
	Если ВыделенныеСтроки = Неопределено Тогда
		ИдентификаторыЗаказов = Неопределено;
	Иначе
		ИдентификаторыЗаказов = Новый Массив;
		
		Для Каждого ДанныеВыделеннойСтроки Из ВыделенныеСтроки Цикл
			ИдентификаторыЗаказов.Добавить(ДанныеВыделеннойСтроки.ИдентификаторЗаказа);
			
			НачалоПериода    = Мин(НачалоПериода, ДанныеВыделеннойСтроки.ДатаОтправления);
			ОкончаниеПериода = Макс(ОкончаниеПериода, ДанныеВыделеннойСтроки.ДатаОтправления);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon Тогда
		ПараметрыФункции = ИнтеграцияСМаркетплейсомOzonСервер.НовыйПараметрыЗагрузкиЗаказов();
		ПараметрыФункции.НачалоПериода       = НачалоДня(НачалоПериода);
		ПараметрыФункции.ОкончаниеПериода    = КонецДня(ОкончаниеПериода);
		ПараметрыФункции.ИдентификаторЗаказа = ИдентификаторыЗаказов;
		Если ВыделенныеСтроки = Неопределено Тогда
			// Загрузка только новых заказов.
			СтатусыОтправлений = Новый Массив;
			СтатусыОтправлений.Добавить("awaiting_packaging");
			СтатусыОтправлений.Добавить("awaiting_deliver");
			ПараметрыФункции.СтатусыОтправлений = СтатусыОтправлений;
		КонецЕсли;
	ИначеЕсли ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ПараметрыФункции = ИнтеграцияСЯндексМаркетСервер.НовыйПараметрыЗагрузкиЗаказов();
		ПараметрыФункции.НачалоПериода       = НачалоДня(НачалоПериода);
		ПараметрыФункции.ОкончаниеПериода    = КонецДня(ОкончаниеПериода);
		ПараметрыФункции.ИдентификаторыЗаказов = ИдентификаторыЗаказов;
		Если ВыделенныеСтроки = Неопределено Тогда
			ПараметрыФункции.ТолькоНовые = Истина;
		КонецЕсли;
	Иначе
		ПараметрыФункции = Новый Структура;
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		ПараметрыФункции);

КонецФункции

&НаКлиенте
// Обработка оповещения загрузки заказов.
//
// Параметры:
//   РезультатЗадания - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
Процедура ВыполнитьЗагрузкуНовыхЗаказовЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт

	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДополнительныеПараметры.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Если Не ПустаяСтрока(ДополнительныеПараметры.ИмяТаблицы) Тогда
				Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
				Форма.Элементы[ДополнительныеПараметры.ИмяТаблицы].Обновить();
			КонецЕсли;
			
			Оповестить("МП_ВыполненаЗагрузкаЗаказовТорговойПлощадки", ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
