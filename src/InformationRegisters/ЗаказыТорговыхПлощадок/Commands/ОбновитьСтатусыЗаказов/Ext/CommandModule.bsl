#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не предусмотрено обновление статусов заказов из этого источника.';
				|en = 'Cannot update order statuses from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма", ПараметрыВыполненияКоманды.Источник);
	
	Если ПараметрыЗадания.Форма.Элементы.Список.ВыделенныеСтроки.Количество() = 0
			И ПараметрыВыполненияКоманды.Источник.ИмяФормы <> "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Нет заказов для обновления статусов.';
				|en = 'No orders for status updating.'"));
		Возврат;
	Иначе
		ТолькоВыделенные = Истина;
	КонецЕсли;
	
	ЗапуститьОбновлениеСтатусовЗаказов(ПараметрыЗадания, ТолькоВыделенные);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗапуститьОбновлениеСтатусовЗаказов(Параметры, ТолькоВыделенные = Ложь)

	ИдентификаторОтгрузки = "";
	
	Если ТолькоВыделенные И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Параметры.Форма.Элементы, "Список") Тогда
		Если Параметры.Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки" Тогда
			ИнформацияОбОтгрузке = Параметры.Форма.ИнформацияОбОтгрузке;
			ИдентификаторОтгрузки = ИнформацияОбОтгрузке.ИдентификаторОтгрузки;
			
			ВыделенныеСтроки = Новый СписокЗначений;
			ВыделенныеСтроки.ТипЗначения = Новый ОписаниеТипов("Строка");
			
			Если ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке.Количество() > 0 Тогда
				ВыделенныеСтроки.ЗагрузитьЗначения(ИнформацияОбОтгрузке.ОтправленияПривязанныеКОтгрузке);
			КонецЕсли;
			
			ОбщегоНазначенияКлиентСервер.ДополнитьСписок(
				ВыделенныеСтроки,
				Параметры.Форма.ОтправленияВОтгрузке);
			
			ОбщегоНазначенияКлиентСервер.ДополнитьСписок(
				ВыделенныеСтроки,
				Параметры.Форма.ОтправленияНеВОтгрузке);
			
			ОбщегоНазначенияКлиентСервер.ДополнитьСписок(
				ВыделенныеСтроки,
				Параметры.Форма.ОтправленияНеВОтгрузке);
			
			Если ВыделенныеСтроки.Количество() = 0 Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(
					НСтр("ru = 'Нет заказов для обновления статусов.';
						|en = 'No orders for status updating.'"));
				Возврат;
			КонецЕсли;
			
			ТекстОжидания = НСтр("ru = 'Выполняется обновление статусов заказов отгрузки.';
								|en = 'Updating shipment order statuses.'");
		Иначе
			ВыделенныеСтроки = Параметры.Форма.Элементы.Список.ВыделенныеСтроки;
			ТекстОжидания = НСтр("ru = 'Выполняется обновление статусов выделенных заказов.';
								|en = 'Updating the selected order statuses.'");
		КонецЕсли;
	Иначе
		ВыделенныеСтроки = Неопределено;
		ТекстОжидания = НСтр("ru = 'Выполняется обновление статусов незавершенных заказов.';
							|en = 'Updating open order statuses.'");
	КонецЕсли;
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", Параметры.УчетнаяЗапись);
	
	ДлительнаяОперация = ОбновитьСтатусыЗаказов(
		ДанныеУчетнойЗаписи,
		Параметры.Форма.УникальныйИдентификатор,
		ВыделенныеСтроки);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Параметры.Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ПараметрыОжидания.ТекстСообщения = ТекстОжидания;
	ПараметрыОжидания.ОповещениеПользователя.Показать = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение =
		НСтр("ru = 'Завершено фоновое обновление статусов заказов.';
			|en = 'Background update of order statuses is completed.'");
	ПараметрыОжидания.ОповещениеПользователя.НавигационнаяСсылка = "";
	ПараметрыОжидания.ОповещениеПользователя.Картинка = ДанныеУчетнойЗаписи.Логотип;
	
	ДопПараметрыОповещения = Новый Структура;
	ДопПараметрыОповещения.Вставить("Форма",           Параметры.Форма);
	ДопПараметрыОповещения.Вставить("УчетнаяЗапись",   ДанныеУчетнойЗаписи.УчетнаяЗапись);
	ДопПараметрыОповещения.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ВидМаркетплейса);
	ДопПараметрыОповещения.Вставить("ИдентификаторОтгрузки", ИдентификаторОтгрузки);
	
	Обработчик = Новый ОписаниеОповещения("ОбновитьСтатусыЗаказовЗавершение",
		ЭтотОбъект,
		ДопПараметрыОповещения);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Обработчик, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ОбновитьСтатусыЗаказов(ДанныеУчетнойЗаписи, Знач ИдентификаторФормы, Знач ВыделенныеСтроки)

	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Обновление статусов заказов.';
			|en = '%1. Update order statuses.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОбновитьСтатусыЗаказовТорговойПлощадки";
	
	Если ВыделенныеСтроки = Неопределено Тогда
		Заказы = Неопределено;
	ИначеЕсли ТипЗнч(ВыделенныеСтроки) = Тип("СписокЗначений") Тогда
		Заказы = РегистрыСведений.ЗаказыТорговыхПлощадок.ЗаказыОтправлений(ДанныеУчетнойЗаписи.УчетнаяЗапись, ВыделенныеСтроки.ВыгрузитьЗначения());
	Иначе
		Заказы = Новый Массив;
		Для Каждого ДанныеВыделеннойСтроки Из ВыделенныеСтроки Цикл
			Заказы.Добавить(ДанныеВыделеннойСтроки.Заказ);
		КонецЦикла;
	КонецЕсли;
	
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсOzon
		ИЛИ ДанныеУчетнойЗаписи.ВидМаркетплейса = Перечисления.ВидыМаркетплейсов.МаркетплейсЯндексМаркет Тогда
		ПараметрыФункции = Новый Структура;
		ПараметрыФункции.Вставить("НачалоПериода",       Неопределено);
		ПараметрыФункции.Вставить("ОкончаниеПериода",    Неопределено);
		ПараметрыФункции.Вставить("ИдентификаторЗаказа", Неопределено);
		ПараметрыФункции.Вставить("Заказ",               Заказы);
	Иначе
		ПараметрыФункции = Новый Структура;
	КонецЕсли;
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ИмяМетода,
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		ПараметрыФункции);

КонецФункции

&НаКлиенте
// Обработка оповещения обновления статусов.
//
// Параметры:
//   РезультатЗадания - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
Процедура ОбновитьСтатусыЗаказовЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт

	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДополнительныеПараметры.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Оповестить("МП_ОбновлениеСведенийПоЗаказамИОтправлениям", ДополнительныеПараметры.ИдентификаторОтгрузки);
		КонецЕсли;
	Иначе
		Оповестить("МП_ОбновлениеСведенийПоЗаказамИОтправлениям", ДополнительныеПараметры.ИдентификаторОтгрузки, "ОтменаЗадания");
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

