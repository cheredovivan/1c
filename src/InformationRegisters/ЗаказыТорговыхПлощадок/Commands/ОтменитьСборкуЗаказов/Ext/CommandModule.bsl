
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ЕстьУчетнаяЗапись =
		ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыВыполненияКоманды.Источник, "УчетнаяЗапись");
	Если Не ЕстьУчетнаяЗапись Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не предусмотрена отмена сборки заказов из этого источника.';
				|en = 'Cannot cancel order picking from this source.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("УчетнаяЗапись", ПараметрыВыполненияКоманды.Источник.УчетнаяЗапись);
	ПараметрыЗадания.Вставить("Форма",         ПараметрыВыполненияКоманды.Источник);
	
	ПолучитьОтправленияДляОтмены(ПараметрыЗадания);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьОтправленияДляОтмены(Знач Параметры)

	Форма = Параметры.Форма;  
	ВидМаркетплейса = ИнтеграцияСМаркетплейсамиВызовСервера.ВидМаркетплейсаУчетнойЗаписи(Параметры.УчетнаяЗапись);
	
	ОтправленияДляОтмены = Новый Структура;
	ОтправленияДляОтмены.Вставить("Заказы",      Новый Массив);
	ОтправленияДляОтмены.Вставить("Отправления", Новый Массив);
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИмяТаблицы = "ТаблицаЗаказов";
		
		ИдентификаторыВыделенныхСтрок = Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки;
		ОтправленияДляОтмены = ПолучитьОтправленияДоступныеДляОтмены(
			ВидМаркетплейса, ИдентификаторыВыделенныхСтрок, Форма.ТаблицаЗаказов);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.СоставЗаказа" Тогда
		ИмяТаблицы    = "";
		СтатусОтменен = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен");
		
		Если Форма.СтатусОтправления = СтатусОтменен Тогда
			
			Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
				ШаблонСообщения = НСтр("ru = 'Отправление %1 уже отменено.';
										|en = 'The %1 shipment is already canceled.'");
			Иначе
				ШаблонСообщения = НСтр("ru = 'Заказ %1 уже отменен.';
										|en = 'The %1 order is already cancelled.'");
			КонецЕсли;
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ШаблонСообщения,
				Форма.НомерОтправления);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Иначе
			ОтправленияДляОтмены.Заказы.Добавить(Форма.ДокументЗаказа);
			ОтправленияДляОтмены.Отправления.Добавить(Форма.НомерОтправления);
		КонецЕсли;
		
	Иначе
		ИмяТаблицы = "Список";
		
		КлючиВыделенныхСтрок = Форма.Элементы.Список.ВыделенныеСтроки;
		ОтправленияДляОтмены = ПолучитьОтправленияДоступныеДляОтмены(ВидМаркетплейса, КлючиВыделенныхСтрок);
	КонецЕсли;
	   
	Если (ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon")
		 И ОтправленияДляОтмены.Отправления.Количество() = 0)
		 Или (ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет")
		 	  И ОтправленияДляОтмены.Заказы.Количество() = 0) Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("ИмяТаблицы",        ИмяТаблицы);
	Параметры.Вставить("Заказы",            ОтправленияДляОтмены.Заказы);
	Параметры.Вставить("НомераОтправлений", ОтправленияДляОтмены.Отправления); 
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОтменитьСборкуЗаказов", ЭтотОбъект, Параметры);
		
		ПараметрыОткрытия = Новый Структура;
		ПараметрыОткрытия.Вставить("УчетнаяЗапись", Параметры.УчетнаяЗапись);
		
		ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ВыборПричиныОтмены",
			ПараметрыОткрытия,
			Параметры.Форма,
			,
			,
			,
			ОповещениеОЗакрытии,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	ИначеЕсли ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет") Тогда
		ПричинаОтмены = ИнтеграцияСЯндексМаркетВызовСервера.ПолучитьПричинуОтменыЗаказовПоУмолчанию();
		ОтменитьСборкуЗаказов(ПричинаОтмены, Параметры)	
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Функция ПолучитьОтправленияДоступныеДляОтмены(ВидМаркетплейса, ВыделенныеСтроки, Таблица = Неопределено)

	НомерОтправления  = "";
	СтатусОтправления = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.ПустаяСсылка");
	СтатусОтменен     = ПредопределенноеЗначение("Перечисление.СтатусыЗаказовТорговыхПлощадок.Отменен");
	
	ЗаказыДляОтмены      = Новый Массив;
	ОтправленияДляОтмены = Новый Массив;
	ОтправленияПрочие    = Новый Массив;
	
	Для Каждого ВыделеннаяСтрока Из ВыделенныеСтроки Цикл
		ДанныеСтроки = ИнтеграцияСМаркетплейсамиКлиент.ДанныеОтправленияИзВыделеннойСтрокиТаблицы(
			ВыделеннаяСтрока, Таблица);
		
		Если ДанныеСтроки <> Неопределено Тогда
			Заказ             = ДанныеСтроки.ДокументЗаказа;
			НомерОтправления  = ДанныеСтроки.НомерОтправления;
			СтатусОтправления = ДанныеСтроки.СтатусОтправления;
			
			Если СтатусОтправления = СтатусОтменен Тогда
				ОтправленияПрочие.Добавить(НомерОтправления);
			Иначе
				ЗаказыДляОтмены.Добавить(Заказ);
				ОтправленияДляОтмены.Добавить(НомерОтправления);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ОтправленияДляОтмены.Количество() = 0 Тогда
		
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ТекстСообщения = НСтр("ru = 'Нет отправлений, доступных для отмены.';
									|en = 'No shipments to cancel.'");
		Иначе
			ТекстСообщения = НСтр("ru = 'Нет заказов, доступных для отмены.';
									|en = 'There are no orders available for cancellation.'");
		КонецЕсли;
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		
	ИначеЕсли ОтправленияПрочие.Количество() > 0 Тогда
		
		Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
			ШаблонСообщения = НСтр("ru = 'Уже отменены следующие отправления: %1.';
									|en = 'Already canceled shipments: %1.'");
		Иначе
			ШаблонСообщения = НСтр("ru = 'Уже отменены следующие заказы: %1.';
									|en = 'Already canceled orders: %1.'");
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			СтрСоединить(ОтправленияПрочие, ", "));
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсЯндексМаркет")
			И ОтправленияДляОтмены.Количество() > 0 Тогда
		// Заменяем номера заказов на номера отправлений
		ОтправленияДляОтмены = ИнтеграцияСЯндексМаркетВызовСервера.ВсеОтправленияЗаказов(ОтправленияДляОтмены);
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("Заказы",      ЗаказыДляОтмены);
	Результат.Вставить("Отправления", ОтправленияДляОтмены);
	
	Возврат Результат;
	
КонецФункции

// Обработка выбора ответа.
//
// Параметры:
//   ПричинаОтмены           - Структура - выбранное значение:
//     * Идентификатор         - Число - идентификатор причины отмены отправления;
//     * Наименование          - Строка - наименование причины отмены;
//     * Ссылка                - СправочникСсылка.ПричиныОтменыЗаказовКлиентов - причина отмены 1С.
//   ДополнительныеПараметры - Структура из КлючИЗначение - параметры обработки ответа.
//
&НаКлиенте
Процедура ОтменитьСборкуЗаказов(ПричинаОтмены, ДополнительныеПараметры) Экспорт

	Если ПричинаОтмены = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ДополнительныеПараметры.НомераОтправлений) Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	ЕстьНезагруженныеЗаказы = Ложь;
	
	Для Каждого Заказ Из ДополнительныеПараметры.Заказы Цикл
		Если Не ЗначениеЗаполнено(Заказ) Тогда    
			ЕстьНезагруженныеЗаказы = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНезагруженныеЗаказы Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Нельзя отменить незагруженные заказы.';
														|en = 'Cannot cancel unimported orders.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыОтмены = Новый Структура;
	ПараметрыОтмены.Вставить("УчетнаяЗапись",      ДополнительныеПараметры.УчетнаяЗапись);
	ПараметрыОтмены.Вставить("ИдентификаторФормы", ДополнительныеПараметры.Форма.УникальныйИдентификатор);
	ПараметрыОтмены.Вставить("Заказы",             ДополнительныеПараметры.Заказы);
	ПараметрыОтмены.Вставить("НомераОтправлений",  ДополнительныеПараметры.НомераОтправлений);
	
	ДанныеУчетнойЗаписи = Новый Структура;
	ДанныеУчетнойЗаписи.Вставить("УчетнаяЗапись", ДополнительныеПараметры.УчетнаяЗапись);
	
	ДлительнаяОперация = ОтменитьСборкуЗаказовНаСервере(
		ДанныеУчетнойЗаписи,
		ПараметрыОтмены,
		ПричинаОтмены);
	
	ДополнительныеПараметры.Вставить("ВидМаркетплейса", ДанныеУчетнойЗаписи.ВидМаркетплейса);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОтменитьСборкуЗаказовЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ДанныеУчетнойЗаписи.ВидМаркетплейса = ПредопределенноеЗначение("Перечисление.ВидыМаркетплейсов.МаркетплейсOzon") Тогда
		ТекстСообщения  = НСтр("ru = 'Выполняется фоновая отмена сборки/доставки заказов.';
								|en = 'Background cancellation of order picking/delivery is in progress.'");
		ТекстОповещения = НСтр("ru = 'Завершена фоновая отмена сборки/доставки заказов.';
								|en = 'Background cancelation of order picking/delivery is completed.'");
	Иначе
		ТекстСообщения  = НСтр("ru = 'Выполняется фоновая отмена сборки заказов.';
								|en = 'Background cancellation of order picking is in progress.'");
		ТекстОповещения = НСтр("ru = 'Завершена фоновая отмена сборки заказов.';
								|en = 'Background cancellation of order picking is completed.'");
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ДополнительныеПараметры.Форма);
	ПараметрыОжидания.ВыводитьОкноОжидания             = Истина;
	ПараметрыОжидания.ТекстСообщения                   = ТекстСообщения;
	ПараметрыОжидания.ОповещениеПользователя.Показать  = Истина;
	ПараметрыОжидания.ОповещениеПользователя.Текст     = ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса;
	ПараметрыОжидания.ОповещениеПользователя.Пояснение = ТекстОповещения;
	ПараметрыОжидания.ОповещениеПользователя.Картинка  = ДанныеУчетнойЗаписи.Логотип;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);

КонецПроцедуры

&НаСервере
Функция ОтменитьСборкуЗаказовНаСервере(ДанныеУчетнойЗаписи, Знач ПараметрыОтмены, Знач ПричинаОтмены)

	ДанныеУчетнойЗаписи =
		Справочники.УчетныеЗаписиМаркетплейсов.ДанныеУчетнойЗаписиДляКоманд(ДанныеУчетнойЗаписи.УчетнаяЗапись);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ПараметрыОтмены.ИдентификаторФормы);
	ПараметрыВыполнения.ОжидатьЗавершение            = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1. Отмена сборки/доставки заказов.';
			|en = '%1. Cancel order picking/delivering.'"),
		ДанныеУчетнойЗаписи.ПредставлениеМаркетплейса);
	ПараметрыВыполнения.ЗапуститьВФоне               = Истина;
	ПараметрыВыполнения.ПрерватьВыполнениеЕслиОшибка = Истина;
	
	ИмяМетода = "РегистрыСведений.ЗаказыТорговыхПлощадок.ОтменитьЗаказыНаТорговойПлощадке";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, 
		ИмяМетода,
		ДанныеУчетнойЗаписи.УчетнаяЗапись,
		ПараметрыОтмены.Заказы,
		ПараметрыОтмены.НомераОтправлений,
		ПричинаОтмены);

КонецФункции

&НаКлиенте
// Обработка оповещения отмены сборки заказов.
//
// Параметры:
//   РезультатЗадания - см. ДлительныеОперации.ВыполнитьВФоне.
//   ДополнительныеПараметры - Произвольный - дополнительные параметры оповещения.
//
Процедура ОтменитьСборкуЗаказовЗавершение(РезультатЗадания, ДополнительныеПараметры) Экспорт

	Если РезультатЗадания <> Неопределено Тогда
		Если РезультатЗадания.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатЗадания.ПодробноеПредставлениеОшибки);
			
		ИначеЕсли РезультатЗадания.Статус = "Выполнено" Тогда
			Ошибка = ПолучитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			УдалитьИзВременногоХранилища(РезультатЗадания.АдресРезультата);
			
			Логотип = ИнтеграцияСМаркетплейсамиКлиентСервер.ЛоготипТорговойПлощадки(ДополнительныеПараметры.ВидМаркетплейса);
			ИнтеграцияСМаркетплейсамиКлиент.ВывестиСостояние(Ошибка, Неопределено, Истина, Логотип);
			
			Если Не ПустаяСтрока(ДополнительныеПараметры.ИмяТаблицы) Тогда
				Форма = ДополнительныеПараметры.Форма; // ФормаКлиентскогоПриложения
				Форма.Элементы[ДополнительныеПараметры.ИмяТаблицы].Обновить();
			КонецЕсли;
			
			Оповестить("МП_ВыполненаОтменаСборкиЗаказовНаТорговойПлощадке", ДополнительныеПараметры.УчетнаяЗапись);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
