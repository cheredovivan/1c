#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	ОчиститьСообщения();
	
	ПараметрыФормы = ПараметрыФормы(ПараметрыВыполненияКоманды.Источник);
	
	ОткрытьФорму("РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПодготовкаОтгрузки",
				  ПараметрыФормы,
				  ПараметрыВыполненияКоманды.Источник,
				  ПараметрыВыполненияКоманды.Уникальность,
				  ПараметрыВыполненияКоманды.Окно,
				  ПараметрыВыполненияКоманды.НавигационнаяСсылка);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПараметрыФормы(Форма)

	ПараметрыФормы = Новый Структура;
	
	Если Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ФормаСпискаЗаказов" Тогда
		ИдентификаторСклада = Форма.ОтборСкладТорговойПлощадки;
		Если Форма.Элементы.Список.ВыделенныеСтроки.Количество() = 1 Тогда
			ИдентификаторСклада = Форма.Элементы.Список.ТекущиеДанные.ИдентификаторСкладаТорговойПлощадки;
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УчетнаяЗапись",         Форма.УчетнаяЗапись);
		ПараметрыФормы.Вставить("ИдентификаторСклада",   ИдентификаторСклада);
		ПараметрыФормы.Вставить("СписокСкладов",         Форма.Элементы.ОтборСкладТорговойПлощадки.СписокВыбора);
		ПараметрыФормы.Вставить("СписокМетодовДоставки", Форма.СписокМетодовДоставки);
		
	ИначеЕсли Форма.ИмяФормы = "РегистрСведений.ЗаказыТорговыхПлощадок.Форма.ПросмотрИЗагрузкаЗаказов" Тогда
		ИдентификаторСклада = Форма.ФильтрПоСкладам;
		Если Форма.Элементы.ТаблицаЗаказов.ВыделенныеСтроки.Количество() = 1 Тогда
			ИдентификаторСклада = Форма.Элементы.ТаблицаЗаказов.ТекущиеДанные.ИдентификаторСклада;
		КонецЕсли;
		
		ПараметрыФормы.Вставить("УчетнаяЗапись",         Форма.УчетнаяЗапись);
		ПараметрыФормы.Вставить("ИдентификаторСклада",   ИдентификаторСклада);
		ПараметрыФормы.Вставить("СписокСкладов",         Форма.Элементы.ФильтрПоСкладам.СписокВыбора);
		ПараметрыФормы.Вставить("СписокМетодовДоставки", Форма.СписокМетодовДоставки);
		
	КонецЕсли;
	
	Возврат ПараметрыФормы

КонецФункции

#КонецОбласти
