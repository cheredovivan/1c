// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ИдентификаторЭДО - Строка
//  ЕстьОшибки - Булево
//  ДатаНачала - Дата
//  Остановлено - Неопределено,Булево
// 
// Возвращаемое значение:
//  См. НовоеСостояниеПолучения
Функция Записать(ИдентификаторЭДО, ЕстьОшибки, ДатаНачала, Остановлено = Неопределено) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
	
	Если ЕстьОшибки Тогда
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() Тогда
			ЗаписьНабора = НаборЗаписей[0];
		Иначе
			ЗаписьНабора = НаборЗаписей.Добавить();
		КонецЕсли;
		ЗаписьНабора.НомерПопытки = ЗаписьНабора.НомерПопытки + 1;
	Иначе
		ЗаписьНабора = НаборЗаписей.Добавить();
	КонецЕсли;
	
	ЗаписьНабора.ИдентификаторЭДО = ИдентификаторЭДО;
	ЗаписьНабора.ДатаНачала = ДатаНачала;
	ЗаписьНабора.ДатаОкончания = ТекущаяДатаСеанса();
	ЗаписьНабора.ЕстьОшибки = ЕстьОшибки;
	
	Если Остановлено <> Неопределено Тогда
		ЗаписьНабора.Остановлено = Остановлено;
	ИначеЕсли ЕстьОшибки И ЗаписьНабора.НомерПопытки >= 3 Тогда
		ЗаписьНабора.Остановлено = Истина;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	Состояние = НовоеСостояниеПолучения();
	ЗаполнитьЗначенияСвойств(Состояние, ЗаписьНабора);
	
	Возврат Состояние;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
//                   - Массив из Строка
Процедура Удалить(ИдентификаторЭДО) Экспорт
	
	Если ТипЗнч(ИдентификаторЭДО) = Тип("Массив") Тогда
		ИдентификаторыЭДО = ИдентификаторЭДО;
	Иначе
		ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО);
	КонецЕсли;
	
	Для Каждого УдаляемыйИдентификаторЭДО Из ИдентификаторыЭДО Цикл
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(УдаляемыйИдентификаторЭДО);
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура Очистить() Экспорт
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
КонецПроцедуры

// Параметры:
//  ИдентификаторыЭДО - Неопределено,Массив из Строка
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение:
//  * Ключ - Строка
//  * Значение - См. НовоеСостояниеПолучения
Функция СостояниеПоИдентификаторамЭДО(ИдентификаторыЭДО = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостоянияАвтоматическогоПолученияЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО,
		|	СостоянияАвтоматическогоПолученияЭДО.ЕстьОшибки КАК ЕстьОшибки,
		|	СостоянияАвтоматическогоПолученияЭДО.Остановлено КАК Остановлено
		|ИЗ
		|	РегистрСведений.СостоянияАвтоматическогоПолученияЭДО КАК СостоянияАвтоматическогоПолученияЭДО
		|ГДЕ
		|	&УсловиеПоИдентификаторамЭДО";
	
	Если ИдентификаторыЭДО = Неопределено Тогда
		Запрос.УстановитьПараметр("УсловиеПоИдентификаторамЭДО", Истина);
	Иначе
		УсловиеПоИдентификаторамЭДО = "СостоянияАвтоматическогоПолученияЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеПоИдентификаторамЭДО", УсловиеПоИдентификаторамЭДО);
		Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Состояние = НовоеСостояниеПолучения();
		Состояние.ЕстьОшибки = Выборка.ЕстьОшибки;
		Состояние.Остановлено = Выборка.Остановлено;
		Результат.Вставить(Выборка.ИдентификаторЭДО, Состояние);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  - Неопределено,Произвольный
//  - См. НовоеСостояниеПолучения
Функция СостояниеПоИдентификаторуЭДО(ИдентификаторЭДО) Экспорт
	
	ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО);
	СостояниеПоИдентификаторамЭДО = СостояниеПоИдентификаторамЭДО(ИдентификаторыЭДО);
	Возврат СостояниеПоИдентификаторамЭДО[ИдентификаторЭДО];
	
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
// 
// Возвращаемое значение:
//  Неопределено,Структура:
//  * ДатаНачала - Дата
//  * ДатаОкончания - Дата
Функция ПериодПолученияПоИдентификаторуЭДО(ИдентификаторЭДО) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	СостоянияАвтоматическогоПолученияЭДО.ДатаНачала,
		|	СостоянияАвтоматическогоПолученияЭДО.ДатаОкончания
		|ИЗ
		|	РегистрСведений.СостоянияАвтоматическогоПолученияЭДО КАК СостоянияАвтоматическогоПолученияЭДО
		|ГДЕ
		|	СостоянияАвтоматическогоПолученияЭДО.ИдентификаторЭДО = &ИдентификаторЭДО";
	Запрос.УстановитьПараметр("ИдентификаторЭДО", ИдентификаторЭДО);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Период = Новый Структура;
		Период.Вставить("ДатаНачала", Выборка.ДатаНачала);
		Период.Вставить("ДатаОкончания", Выборка.ДатаОкончания);
		Возврат Период;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
КонецФункции

// Возвращаемое значение:
//  Структура:
// * ЕстьОшибки - Булево
// * Остановлено - Булево
Функция НовоеСостояниеПолучения() Экспорт
	Состояние = Новый Структура;
	Состояние.Вставить("ЕстьОшибки", Ложь);
	Состояние.Вставить("Остановлено", Ложь);
	Возврат Состояние;
КонецФункции

// Параметры:
//  ИдентификаторЭДО - Строка
//  Значение - Булево
//  
// Возвращаемое значение:
//  См. НовоеСостояниеПолучения
Функция УстановитьПризнакОстановки(ИдентификаторЭДО, Значение = Ложь) Экспорт
	
	Состояние = НовоеСостояниеПолучения();
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторЭДО.Установить(ИдентификаторЭДО);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() Тогда
		ЗаписьНабора = НаборЗаписей[0];
		Если ЗаписьНабора.Остановлено = Значение Тогда
			ЗаполнитьЗначенияСвойств(Состояние, ЗаписьНабора);
			Возврат Состояние;
		КонецЕсли;
	Иначе
		ЗаписьНабора = НаборЗаписей.Добавить();
		ЗаписьНабора.ИдентификаторЭДО = ИдентификаторЭДО;
	КонецЕсли;
	
	ЗаписьНабора.Остановлено = Значение;
	
	Если Не ЗаписьНабора.Остановлено Тогда
		ЗаписьНабора.ЕстьОшибки = Ложь;
		ЗаписьНабора.НомерПопытки = 0;
	КонецЕсли;
	
	НаборЗаписей.Записать();
	
	ЗаполнитьЗначенияСвойств(Состояние, ЗаписьНабора);
	
	Возврат Состояние;
	
КонецФункции

// Параметры:
//  СостоянияДоПолучения - см. СостояниеПоИдентификаторамЭДО
//  СостоянияПослеПолучения - см. СостояниеПоИдентификаторамЭДО
// 
// Возвращаемое значение:
//  См. СостояниеПоИдентификаторамЭДО
Функция ИзмененныеСостоянияПоИдентификаторамЭДО(СостоянияДоПолучения, СостоянияПослеПолучения) Экспорт
	
	Результат = Новый Соответствие;
	
	Для Каждого СостояниеПоИдентификаторуЭДОПослеПолучения Из СостоянияПослеПолучения Цикл
		
		ИдентификаторЭДО = СостояниеПоИдентификаторуЭДОПослеПолучения.Ключ;
		СостояниеПослеПолучения = СостояниеПоИдентификаторуЭДОПослеПолучения.Значение;
		
		СостояниеДоПолучения = СостоянияДоПолучения[ИдентификаторЭДО];
		Если СостояниеДоПолучения = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЕстьИзменения = Ложь;
		Для Каждого Свойство Из СостояниеПослеПолучения Цикл
			Если СостояниеДоПолучения[Свойство.Ключ] <> Свойство.Значение Тогда
				ЕстьИзменения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			Результат.Вставить(ИдентификаторЭДО, СостояниеПослеПолучения);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьПредупреждения() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИСТИНА КАК ЕстьПредупреждения
		|ИЗ
		|	РегистрСведений.СостоянияАвтоматическогоПолученияЭДО КАК СостоянияАвтоматическогоПолученияЭДО
		|ГДЕ
		|	СостоянияАвтоматическогоПолученияЭДО.ЕстьОшибки
		|	ИЛИ СостоянияАвтоматическогоПолученияЭДО.Остановлено";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли
