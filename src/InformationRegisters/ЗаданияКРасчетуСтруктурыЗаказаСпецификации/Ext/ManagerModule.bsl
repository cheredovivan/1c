#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Временное хранилище
// - следует использовать для добавления заданий в обычных случаях

// Добавляет задания к расчету структуры заказа на производство и возвращает номер текущей очереди заданий
//
// Параметры:
//  Таблица	 - ТаблицаЗначений	 - задания к расчету.
// 
Процедура ДобавитьЗадания(Таблица) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Очередь = Константы.ГраницаРасчетаСтруктурыЗаказов.ТекущаяОчередь();
	
	КолонкиПоЗначению = Новый Структура();
	КолонкиПоЗначению.Вставить("ДатаЗаписи", УниверсальноеВремя(ТекущаяДатаСеанса()));
	
	НаборЗаписей = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Таблица);
	
	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, КолонкиПоЗначению); 
		Запись.Очередь = ?(Запись.Очередь > 0, 
			Запись.Очередь, Очередь);
		Запись.Разделитель = Новый УникальныйИдентификатор();
	КонецЦикла;

	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

// Добавляет задания к расчету структуры заказа на производство
//
// Параметры:
//  Таблица	 - ТаблицаЗначений	 - задания к расчету.
//
Процедура ДобавитьЗаданияПолныйРасчет(Таблица) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Очередь = Константы.ГраницаРасчетаСтруктурыЗаказов.ТекущаяОчередь();

	КолонкиПоЗначению = Новый Структура();
	КолонкиПоЗначению.Вставить("ДатаЗаписи", УниверсальноеВремя(ТекущаяДатаСеанса()));
	КолонкиПоЗначению.Вставить("ПолныйРасчет", Истина);
	
	НаборЗаписей = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Таблица);
	
	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, КолонкиПоЗначению); 
		Запись.Очередь = ?(Запись.Очередь > 0, 
			Запись.Очередь, Очередь);
		Запись.Разделитель = Новый УникальныйИдентификатор();
	КонецЦикла;

	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

// Добавляет задания к расчету структуры заказа на производство
// 
// Параметры:
// 	Спецификация		 - СправочникСсылка.РесурсныеСпецификации	- спецификация.
// 	ЗаказНаПроизводство	 - ДокументСсылка.ЗаказНаПроизводство2_2	- заказ на производство.
//
Процедура ДобавитьЗаданиеПолныйРасчет(Спецификация, ЗаказНаПроизводство) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	
	НоваяЗапись.Спецификация = Спецификация;
	НоваяЗапись.ЗаказНаПроизводство = ЗаказНаПроизводство;
	НоваяЗапись.ПолныйРасчет = Истина;
	
	НоваяЗапись.Очередь = Константы.ГраницаРасчетаСтруктурыЗаказов.ТекущаяОчередь();
	НоваяЗапись.Разделитель = Новый УникальныйИдентификатор();
	
	НаборЗаписей.Записать(Ложь);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////
// Итоговая очередь заданий к расчету
// - может использоваться для добавления заданий в процессе расчета

// Добавляет задания к расчету структуры заказа на производство в конец очереди (только для добавления заданий в процессе расчета)
//
// Параметры:
//  Таблица			 - ТаблицаЗначений	 - задания к расчету.
//  НомерОчереди	 - Число			 - номер очереди.
//
Процедура ДобавитьЗаданияВКонецОчереди(Таблица, НомерОчереди) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	КолонкиПоЗначению = Новый Структура();
	КолонкиПоЗначению.Вставить("Очередь", НомерОчереди);
	
	НаборЗаписей = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Таблица);
	
	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, КолонкиПоЗначению);
		Запись.Разделитель = Новый УникальныйИдентификатор();
	КонецЦикла;
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выбирает изменения из очереди по границу расчета (включая)
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
// 
// Возвращаемое значение:
//  Число - номер очереди
//
Функция ВыбратьИзменения(ГраницаРасчета) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Очередь                КАК Очередь,
		|	Таблица.Разделитель            КАК Разделитель,
		|	Таблица.ПолныйРасчет           КАК ПолныйРасчет,
		|	Таблица.Спецификация           КАК Спецификация,
		|	Таблица.ЗаказНаПроизводство    КАК ЗаказНаПроизводство
		|ИЗ
		|	РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации КАК Таблица
		|ГДЕ
		|	Таблица.Очередь <= &ГраницаРасчета
		|";
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации");
		ЭлементБлокировки.УстановитьЗначение("Очередь", Новый Диапазон(, ГраницаРасчета));
		Блокировка.Заблокировать();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Буфер = РезультатЗапроса.Выгрузить();
			
			Набор = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
			Набор.Загрузить(Буфер);
			Набор.Записать(РежимЗамещения.Удаление);
			
			Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
			Набор.Загрузить(Буфер);
			Набор.Записать(Ложь);
		
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(СтруктураЗаказа.ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации КАК Таблица
		|ГДЕ
		|	Таблица.Очередь <= &ГраницаРасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди;
	
КонецФункции

// Удаляет задания по номеру очереди
// 
// Параметры:
// 	Очередь - Число - номер очереди
Процедура УдалитьЗадания(Очередь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей();
	Набор.Отбор.Очередь.Установить(Очередь);
	Набор.Записать(Истина);
	
КонецПроцедуры

// Проверяет, есть ли задания к расчету
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
//  НомерОчереди	 - Число - минимальный номер очереди (возвращаемое значение)
// 
// Возвращаемое значение:
//  Булево - есть задания к расчету
//
Функция ЕстьЗаданияКРасчету(ГраницаРасчета = -1, НомерОчереди = -1) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации КАК Таблица
		|ГДЕ
		|	(&ГраницаРасчета = -1
		|			ИЛИ Таблица.Очередь <= &ГраницаРасчета)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди >= 0;
	
КонецФункции

// Возвращает новую таблицу заданий
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Описание
//
Функция ТаблицаЗаданий() Экспорт
	
	Таблица = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации.СоздатьНаборЗаписей().ВыгрузитьКолонки("Спецификация,ЗаказНаПроизводство");
	Возврат Таблица;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолноеИмя() Экспорт

	ПолноеИмя = Метаданные.РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаСпецификации.ПолноеИмя();

	Возврат ПолноеИмя;

КонецФункции

#КонецОбласти

#КонецЕсли