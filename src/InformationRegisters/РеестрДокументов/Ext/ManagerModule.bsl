#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область Проведение

// Формирует параметры для проведения документа по регистрам учетного механизма через общий механизм проведения.
//
// Параметры:
//  Документ - ДокументОбъект - записываемый документ
//  Свойства - См. ПроведениеДокументов.СвойстваДокумента
//
// Возвращаемое значение:
//  Структура - См. ПроведениеДокументов.ПараметрыУчетногоМеханизма
//
Функция ПараметрыДляПроведенияДокумента(Документ, Свойства) Экспорт
	
	Параметры = ПроведениеДокументов.ПараметрыУчетногоМеханизма();
	
	Параметры.НезависимыеРегистры.Добавить(Метаданные.РегистрыСведений.РеестрДокументов);
	
	Возврат Параметры;
	
КонецФункции

// Процедура формирования движений по подчиненным регистрам международного учета.
//
// Параметры:
//   ТаблицыДляДвижений - Структура - таблицы данных документа
//   Движения - КоллекцияДвижений - коллекция наборов записей движений документа
//   Отказ - Булево - признак отказа от проведения документа.
//
Процедура ОтразитьДвижения(ТаблицыДляДвижений, Движения, Отказ) Экспорт
	
КонецПроцедуры

// Возвращает тексты запросов для сторнирования движений при исправлении документов
// 
// Параметры:
// 	МетаданныеДокумента - ОбъектМетаданныхДокумент - Метаданные документа, который проводится.
// 
// Возвращаемое значение:
// 	Соответствие - Соответствие полного имени регистра тексту запроса сторнирования
//
Функция ТекстыЗапросовСторнирования(МетаданныеДокумента) Экспорт
	
	ТекстыЗапросов = Новый Соответствие();
	
	Возврат ТекстыЗапросов;
	
КонецФункции

// Дополняет текст запроса механизма проверки даты запрета по таблице изменений.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция ТекстыЗапросовКонтрольДатыЗапретаПоТаблицеИзменений(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие();
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Процедура формирования движений по регистру.
//
// Параметры:
//	ТаблицыДляДвижений - Структура - таблицы данных документа
//	Документ - ДокументСсылка - ссылка на документ
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер временных таблиц документа
//	Отказ - Булево - признак отказа от проведения документа.
//
Процедура ЗаписатьДанные(ТаблицыДляДвижений, Документ, МенеджерВременныхТаблиц, Отказ) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРеестрДокументов = Неопределено;
	Если НЕ ТаблицыДляДвижений.Свойство("Таблица" + "РеестрДокументов", ТаблицаРеестрДокументов) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.РеестрДокументов.СоздатьНаборЗаписей();
	
	Если МенеджерВременныхТаблиц <> Неопределено Тогда
		Набор.ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	КонецЕсли;
	
	Набор.Отбор.Ссылка.Установить(Документ);
	Набор.ЗагрузитьСОбработкой(ТаблицаРеестрДокументов);
	Набор.Записать();
	
	ОбновлениеИнформационнойБазыУТ.ОтметитьВыполнениеОбработкиИнтерактивно(Набор);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Записывает в регистр данные по переданному разделителю записи.
//
// Параметры:
//  ТаблицыДляДвижений		 - Структура - содержит по ключу ТаблицыДляДвижений структуру
//  	имеющую ключ ТаблицаРеестрДокументов (ТаблицаЗначений).
//  РазделительЗаписи		 - Строка - измерение, по которому необходимо выполнить запись
//  ЗамещатьЗаписи			 - Булево - определяет режим замещения существующих записей разделителя. Истина - перед записью существующие
//		записи будут удалены. Ложь - записи будут дописаны к уже существующим в информационной базе записям.
//
Процедура ЗаписатьДанныеРазделителя(ТаблицыДляДвижений, РазделительЗаписи, ЗамещатьЗаписи = Истина) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.РеестрДокументов.СоздатьНаборЗаписей();
	Набор.Отбор.РазделительЗаписи.Установить(РазделительЗаписи);
	Набор.ЗагрузитьСОбработкой(ТаблицыДляДвижений["Таблица" + "РеестрДокументов"]);
	Набор.Записать(ЗамещатьЗаписи);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

// Возвращает признак, того отражаются ли данные полученного объекта в реестре сведений или нет.
//
// Параметры:
//	ПроверяемыйОбъект - ДокументОбъект, ДокументСсылка, ДанныеФормыСтруктура, ОбъектМетаданных - 
//
// Возвращаемое значение:
//	Булево - Истина, если данные полученного объекта, отражаются в реестре сведений.
//
Функция ОбъектВключенВСоставДанныхРеестра(ПроверяемыйОбъект) Экспорт
	
	ТипЗначения = ТипЗнч(ПроверяемыйОбъект);
	Если ТипЗначения = Тип("ДанныеФормыСтруктура") Тогда
		Если Не ПроверяемыйОбъект.Свойство("Ссылка") Тогда
			Возврат Ложь;
		КонецЕсли;
		ТипСсылки = ТипЗнч(ПроверяемыйОбъект.Ссылка);
	Иначе	
		Если ТипЗначения = Тип("ОбъектМетаданных") Тогда
			МетаданныеЗначения = ПроверяемыйОбъект; // ОбъектМетаданныхДокумент
		Иначе
			МетаданныеЗначения = Метаданные.НайтиПоТипу(ТипЗначения); // ОбъектМетаданныхДокумент
			
			Если МетаданныеЗначения = Неопределено Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ОбщегоНазначения.ЭтоДокумент(МетаданныеЗначения) Тогда
			Возврат Ложь;
		КонецЕсли;

		ТипСсылки = ТипЗнч(МетаданныеЗначения.СтандартныеРеквизиты.Ссылка.Тип.ПривестиЗначение());
	КонецЕсли;

	Возврат Метаданные.РегистрыСведений.РеестрДокументов.Измерения.Ссылка.Тип.СодержитТип(ТипСсылки);
	
КонецФункции

// Инициализирует и записывает данные документов, полученных объектов метаданных, в регистр сведений.
//
// Параметры:
//	ОбъектыМетаданных - Соответствие - объекты метаданных:
//		* Ключ		- ОбъектМетаданныхДокумент	- объект метаданных документа.
//		* Значение	- Неопределено				- пустое значение.
//
Процедура ОтразитьДанныеДокументовВРеестре(ОбъектыМетаданных) Экспорт
	
	ИменаОбъектов		= Новый Массив;
	СсылкиДокументов	= Новый Массив;
	
	Для Каждого ЭлементДанных Из ОбъектыМетаданных Цикл
		ОбъектДанных		= ЭлементДанных.Ключ;
		ПолноеИмяОбъекта	= ОбъектДанных.ПолноеИмя();
		
		ИменаОбъектов.Добавить(ПолноеИмяОбъекта);
	КонецЦикла;
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("Дополнительно");
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Для Каждого ПолноеИмяОбъекта Из ИменаОбъектов Цикл
		
		ИмяДокумента = СтрРазделить(ПолноеИмяОбъекта, ".")[1];
		РезультатАдаптацииЗапроса = Документы[ИмяДокумента].АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов");
		Регистраторы = ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
							РезультатАдаптацииЗапроса,
							"РегистрСведений.РеестрДокументов",
							ПолноеИмяОбъекта,
							НеиспользуемыеПоля);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СсылкиДокументов, Регистраторы.ВыгрузитьКолонку("Ссылка"));
		
	КонецЦикла;
	
	Для Каждого Ссылка Из СсылкиДокументов Цикл
		ТаблицыДанных = ПроведениеДокументов.ДанныеДокументаДляПроведения(Ссылка, "РеестрДокументов");
		РегистрыСведений.РеестрДокументов.ЗаписатьДанные(ТаблицыДанных, Ссылка, Неопределено, Ложь);
	КонецЦикла;
	
КонецПроцедуры

// Возвращает коллекцию типов, которые включены в измерение 'Ссылка'.
//
// Возвращаемое значение:
//	Массив Из Тип - коллекция типов, которые включены в измерение 'Ссылка'.
//
Функция ТипыДокументовРеестра() Экспорт
	
	ТипыСсылки = Метаданные.РегистрыСведений.РеестрДокументов.Измерения.Ссылка.Тип.Типы();
	
	ИсключаемыеТипы = Новый Массив;
	ИсключаемыеТипы.Добавить(Тип("ДокументСсылка.ВводОстатков"));
	//++ Локализация
	ИсключаемыеТипы.Очистить();
	//-- Локализация
	
	Если ИсключаемыеТипы.Количество() Тогда
		ТипыДокументов = ОбщегоНазначенияКлиентСервер.РазностьМассивов(ТипыСсылки, ИсключаемыеТипы);
	Иначе
		ТипыДокументов = ТипыСсылки;
	КонецЕсли;
	
	Возврат ТипыДокументов;
	
КонецФункции

// Возвращает ключ записи реестра по переданной ссылке документа.
//
// Параметры:
//	Ссылка - ДокументСсылка - ссылка на документ для которого необходимо сформировать ключ записи реестра.
//
// Возвращаемое значение:
//	РегистрСведенийКлючЗаписи.РеестрДокументов - если для ссылки ключ не найден, то возвращается Неопределено.
//
Функция КлючЗаписиДокумента(Ссылка) Экспорт
	
	ПоляВыборки = Новый Массив;
	ИмяРегистра = Метаданные.РегистрыСведений.РеестрДокументов.Имя;
	Для Каждого Измерение Из Метаданные.РегистрыСведений.РеестрДокументов.Измерения Цикл
		ПоляВыборки.Добавить(Символы.Таб + ИмяРегистра + "." + Измерение.Имя);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|&ПоляВыборки
	|ИЗ
	|	РегистрСведений.РеестрДокументов КАК РеестрДокументов
	|ГДЕ
	|	РеестрДокументов.Ссылка = &Ссылка
	|	И НЕ РеестрДокументов.ДополнительнаяЗапись";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляВыборки", СтрСоединить(ПоляВыборки,"," + Символы.ПС));
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	РезультатЗароса = Запрос.Выполнить();
	Выборка = РезультатЗароса.Выбрать();
	КлючЗаписи = Неопределено;
	Если Выборка.Следующий() Тогда
		ДанныеКлюча = Новый Структура;
		Для Каждого Колонка Из РезультатЗароса.Колонки Цикл
			ДанныеКлюча.Вставить(Колонка.Имя, Выборка[Колонка.Имя]);
		КонецЦикла;
		КлючЗаписи = РегистрыСведений.РеестрДокументов.СоздатьКлючЗаписи(ДанныеКлюча);
	КонецЕсли;
	
	Возврат КлючЗаписи;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Ссылка)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ИзменениеОбъектаРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.РеестрДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.60";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a81d07e9-0d8b-4816-9c92-2fdc0f7aea6c");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.РеестрДокументов.ЗарегистрироватьДанныеКОбработке";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	
	ОписаниеОбработчика = Новый Массив;
	ОписаниеОбработчика.Добавить(
		НСтр("ru = 'Обновление записей регистра сведений ""Реестр документов"".';
			|en = 'Update ""Document registry"" information register records.'"));
	
	//++ НЕ УТКА
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - заполнение ""Места хранения"" в реестре документов по документам ""Этап производства"".';
			|en = ' - fill ""Storage locations"" in the document registry for the ""Production stage"" document.'"));
	//-- НЕ УТКА
	
	//++ НЕ УТ
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Акт выполненных внутренних работ"".';
			|en = ' - update ""Internal work acceptance"" document records.'"));
	
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Выработка сотрудников"".';
			|en = ' - update ""Timesheets charge"" document records.'"));
	
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Движения продукции и материалов"".';
			|en = ' - update ""Movements of products and materials"" document records.'"));
	
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Производство без заказа"".';
			|en = ' - update ""Backflush production"" document records.'"));
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Заказ материалов в производство"".';
			|en = ' - update ""Material order for shop floor stockroom"" document records.'"));
	//-- НЕ УТ
	
	ОписаниеОбработчика.Добавить(НСтр("ru = ' - обновление записей регистра по документу ""Списание безналичных ДС"".';
										|en = ' - Update register records of the ""Outgoing payments — Bank account"" document.'"));
	ОписаниеОбработчика.Добавить(НСтр("ru = ' - обновление записей регистра по документу ""Счет-фактура полученный (аванс)"".';
										|en = ' - update register records of the ""Received tax invoice (advance)"" document.'"));
	ОписаниеОбработчика.Добавить(НСтр("ru = ' - обновление записей регистра по документу ""Расходный кассовый ордер"".';
										|en = ' - Update register records of the ""Outgoing payment — Cash account"" document.'"));
	
	ОписаниеОбработчика.Добавить(
		НСтр("ru = ' - обновление записей по документам ""Заказ на перемещение"".';
			|en = ' - update ""Transfer order"" document records.'"));
	
	Обработчик.Комментарий = СтрСоединить(ОписаниеОбработчика, Символы.ПС);
	
	Читаемые = Новый Массив;
	//++ НЕ УТ
	Читаемые.Добавить(Метаданные.Документы.АктВыполненныхВнутреннихРабот.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ВыработкаСотрудников.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ДвижениеПродукцииИМатериалов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПроизводствоБезЗаказа.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЗаказМатериаловВПроизводство.ПолноеИмя());
	//-- НЕ УТ
	
	//++ НЕ УТКА
	Читаемые.Добавить(Метаданные.Документы.ЭтапПроизводства2_2.ПолноеИмя());
	//-- НЕ УТКА
	
	Читаемые.Добавить(Метаданные.Документы.ЗаказНаПеремещение.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СписаниеБезналичныхДенежныхСредств.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.СчетФактураПолученныйАванс.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.РасходныйКассовыйОрдер.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.РеестрДокументов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
КонецПроцедуры

// Регистрирует документы к обработке для изменения.
//
// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработке(Параметры) Экспорт 
	
	ПолноеИмяРегистра  = "РегистрСведений.РеестрДокументов";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("ДатаДокументаИБ УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Ссылка");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиРегистраторыРегистра();
	ПараметрыВыборки.ИмяИзмеренияДляОтбора = "Ссылка";
	
	ПолныеИменаОбъектов = Новый Массив;
	//++ НЕ УТ
	ПолныеИменаОбъектов.Добавить("Документ.АктВыполненныхВнутреннихРабот");
	ПолныеИменаОбъектов.Добавить("Документ.ВыработкаСотрудников");
	ПолныеИменаОбъектов.Добавить("Документ.ДвижениеПродукцииИМатериалов");
	ПолныеИменаОбъектов.Добавить("Документ.ПроизводствоБезЗаказа");
	ПолныеИменаОбъектов.Добавить("Документ.ЗаказМатериаловВПроизводство");
	//-- НЕ УТ
	
	//++ НЕ УТКА
	ПолныеИменаОбъектов.Добавить("Документ.ЭтапПроизводства2_2");
	//-- НЕ УТКА
	
	ПолныеИменаОбъектов.Добавить("Документ.ЗаказНаПеремещение");
	ПолныеИменаОбъектов.Добавить("Документ.СписаниеБезналичныхДенежныхСредств");
	ПолныеИменаОбъектов.Добавить("Документ.СчетФактураПолученныйАванс");
	ПолныеИменаОбъектов.Добавить("Документ.РасходныйКассовыйОрдер");

	ПараметрыВыборки.ПолныеИменаОбъектов  = СтрСоединить(ПолныеИменаОбъектов, ",");
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	//++ НЕ УТ
	
	#Область АктВыполненныхВнутреннихРабот
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.АктВыполненныхВнутреннихРабот.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.АктВыполненныхВнутреннихРабот",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	#Область ВыработкаСотрудников
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ВыработкаСотрудников.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ВыработкаСотрудников",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	#Область ДвижениеПродукцииИМатериалов
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	НеиспользуемыеПоля.Добавить("Дополнительно");
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ДвижениеПродукцииИМатериалов.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ДвижениеПродукцииИМатериалов",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	#Область ПроизводствоБезЗаказа
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("НомерПервичногоДокумента");
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ПроизводствоБезЗаказа.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ПроизводствоБезЗаказа",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	//-- НЕ УТ
	
	//++ НЕ УТКА
	
	#Область ЭтапПроизводства2_2
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ЭтапПроизводства2_2.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ЭтапПроизводства2_2",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	//-- НЕ УТКА
	
	#Область СписаниеБезналичныхДенежныхСредств
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.СписаниеБезналичныхДенежныхСредств.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.СписаниеБезналичныхДенежныхСредств",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти

	#Область СчетФактураПолученныйАванс
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.СчетФактураПолученныйАванс.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.СчетФактураПолученныйАванс",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти

	#Область ЗаказНаПеремещение
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	НеиспользуемыеПоля.Добавить("НаименованиеПервичногоДокумента");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ЗаказНаПеремещение.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ЗаказНаПеремещение",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	//++ НЕ УТ
	
	#Область ЗаказМатериаловВПроизводство
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	НеиспользуемыеПоля.Добавить("НаименованиеПервичногоДокумента");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.ЗаказМатериаловВПроизводство.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.ЗаказМатериаловВПроизводство",
			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти
	
	//-- НЕ УТ

	#Область РасходныйКассовыйОрдер
	
	НеиспользуемыеПоля = Новый Массив;
	НеиспользуемыеПоля.Добавить("ИдентификаторЗаписи");
	
	Регистраторы =
		ОбновлениеИнформационнойБазыУТ.ДанныеНезависимогоРегистраДляПерепроведения(
			Документы.РасходныйКассовыйОрдер.АдаптированныйТекстЗапросаДвиженийПоРегистру("РеестрДокументов"),
			ПолноеИмяРегистра,
			"Документ.РасходныйКассовыйОрдер",

			НеиспользуемыеПоля);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
	#КонецОбласти

КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ВсеКлючиОбработаны = Истина;
	
	ОбъектыМетаданных = Справочники.КлючиРеестраДокументов.ОбъектыМетаданныхЗакешированныеВКлючахРеестра();
	ОбъектыМетаданных.Вставить(Метаданные.Справочники.КлючиРеестраДокументов);
	
	Для Каждого ОбъектМетаданных Из ОбъектыМетаданных Цикл
		ЕстьЗаблокированные = ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(Параметры.Очередь,
			ОбъектМетаданных.Ключ.ПолноеИмя());
		Если ЕстьЗаблокированные Тогда
			ВсеКлючиОбработаны = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;

	Если Не ВсеКлючиОбработаны Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазыУТ.ДополнительныеПараметрыПерезаписиДвиженийИзОчереди();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ИмяИзмеренияДляОтбора = "Ссылка";
	ДополнительныеПараметры.ЗаписыватьВОднойТранзакции = Ложь;
	ДополнительныеПараметры.НужнаДополнительнаяОбработкаЗаписей = Истина;
	ДополнительныеПараметры.ОбновляемыеДанные = Параметры.ОбновляемыеДанные;
	
	ВсеСделано =
		ОбновлениеИнформационнойБазыУТ.ПерезаписатьДвиженияИзОчереди(
			Неопределено,
			"РегистрСведений.РеестрДокументов",
			Параметры.Очередь,
			ДополнительныеПараметры);
	
	Параметры.ОбработкаЗавершена = ВсеСделано;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
