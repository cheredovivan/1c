#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь;
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКРаспределениюРасчетов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.23.17";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("1ff77d60-ecd2-11e4-92f1-0050568b35ac");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияКРаспределениюРасчетов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий = НСтр("ru = 'Регистрация заданий к распределению взаиморасчетов';
									|en = 'Register jobs for allocating AR/AP'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюРасчетов.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ЗаданияКРаспределениюРасчетов";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ЗаданияКРаспределениюРасчетов");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаданияКРаспределениюРасчетов.ТипРасчетов КАК ТипРасчетов,
	|	ЗаданияКРаспределениюРасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюРасчетов.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗаданияКРаспределениюРасчетов.Валюта КАК Валюта
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюРасчетов КАК ЗаданияКРаспределениюРасчетов
	|ГДЕ
	|	&НоваяАрхитектураВзаиморасчетов";
	
	Кортежи = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Кортежи, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ЗаданияКРаспределениюРасчетов";
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	РаспределятьВзаиморасчетыФоновымЗаданием = Константы.РаспределятьВзаиморасчетыФоновымЗаданием.Получить();

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Данные.ТипРасчетов КАК ТипРасчетов,
	|	Данные.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	Данные.ОбъектРасчетов КАК ОбъектРасчетов,
	|	Данные.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВтДанныеДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК Данные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ТипРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтДанныеДляОбработки.ТипРасчетов КАК ТипРасчетов,
	|	ВтДанныеДляОбработки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтДанныеДляОбработки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтДанныеДляОбработки.Валюта КАК Валюта,
	|	МИНИМУМ(ЕСТЬNULL(Задания.ДатаПересчета, ДАТАВРЕМЯ(3999, 12, 31, 23, 59, 59))) КАК ДатаПересчета
	|ИЗ
	|	ВтДанныеДляОбработки КАК ВтДанныеДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюРасчетов КАК Задания
	|		ПО ВтДанныеДляОбработки.ТипРасчетов = Задания.ТипРасчетов
	|		И ВтДанныеДляОбработки.АналитикаУчетаПоПартнерам = Задания.АналитикаУчетаПоПартнерам
	|		И ВтДанныеДляОбработки.ОбъектРасчетов = Задания.ОбъектРасчетов
	|		И Задания.Валюта = ВтДанныеДляОбработки.Валюта
	|СГРУППИРОВАТЬ ПО
	|	ВтДанныеДляОбработки.ТипРасчетов,
	|	ВтДанныеДляОбработки.АналитикаУчетаПоПартнерам,
	|	ВтДанныеДляОбработки.ОбъектРасчетов,
	|	ВтДанныеДляОбработки.Валюта";
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	Выборка = Запрос.Выполнить().Выбрать();
		
	ЗапросРасчетыСПартнерами = Новый Запрос;
	ТекстЗапросаРасчетыСКлиентами = "
	|ВЫБРАТЬ
	|	МИНИМУМ(РасчетыСКлиентами.Период) КАК ПериодЗадания,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта КАК Валюта,
	|	РасчетыСКлиентами.Регистратор КАК Документ
	|ИЗ
	|	РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|ГДЕ
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|	И РасчетыСКлиентами.ОбъектРасчетов = &ОбъектРасчетов
	|	И РасчетыСКлиентами.Валюта = &Валюта
	|	И РасчетыСКлиентами.Активность
	|	И (РасчетыСКлиентами.Сумма <> 0
	|	ИЛИ РасчетыСКлиентами.КОплате <> 0
	|	ИЛИ РасчетыСКлиентами.КОтгрузке <> 0)
	|	И РасчетыСКлиентами.Период >= &Период
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСКлиентами.АналитикаУчетаПоПартнерам,
	|	РасчетыСКлиентами.ОбъектРасчетов,
	|	РасчетыСКлиентами.Валюта,
	|	РасчетыСКлиентами.Регистратор";
	
	ТекстЗапросаРасчетыСПоставщиками = "
	|ВЫБРАТЬ
	|	МИНИМУМ(РасчетыСПоставщиками.Период) КАК ПериодЗадания,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов КАК ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта КАК Валюта,
	|	РасчетыСПоставщиками.Регистратор КАК Документ
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|ГДЕ
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам = &АналитикаУчетаПоПартнерам
	|	И РасчетыСПоставщиками.ОбъектРасчетов = &ОбъектРасчетов
	|	И РасчетыСПоставщиками.Валюта = &Валюта
	|	И РасчетыСПоставщиками.Активность
	|	И (РасчетыСПоставщиками.Сумма <> 0
	|	ИЛИ РасчетыСПоставщиками.КОплате <> 0
	|	ИЛИ РасчетыСПоставщиками.КПоступлению <> 0)
	|	И РасчетыСПоставщиками.Период >= &Период
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация,
	|	РасчетыСПоставщиками.АналитикаУчетаПоПартнерам,
	|	РасчетыСПоставщиками.ОбъектРасчетов,
	|	РасчетыСПоставщиками.Валюта,
	|	РасчетыСПоставщиками.Регистратор";

	МаксимальныйПериод = Дата(3999, 12, 31, 23, 59, 59);
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;

	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр(
			"ru = 'Не удалось обработать задания по обработчику регистра сведений ""Задания к распределению расчетов"".';
			|en = 'Cannot process the jobs of the ""Jobs for allocating AR/AP"" information register handler:'"));
		
	Пока Выборка.Следующий() Цикл
		
		ПричинаИсключения = 0;
		Рекомендация = "";
			
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ТипРасчетов.Установить(Выборка.ТипРасчетов);
			НаборЗаписей.Отбор.АналитикаУчетаПоПартнерам.Установить(Выборка.АналитикаУчетаПоПартнерам);
			НаборЗаписей.Отбор.ОбъектРасчетов.Установить(Выборка.ОбъектРасчетов);
			НаборЗаписей.Отбор.Валюта.Установить(Выборка.Валюта);
			
			ПричинаИсключения = 1; // Блокировка
			
			Блокировка = Новый БлокировкаДанных;
			
			// Блокировка регистра "Задания к распределению расчетов".
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюРасчетов");
			ЭлементБлокировки.УстановитьЗначение("ТипРасчетов", Выборка.ТипРасчетов);
			ЭлементБлокировки.УстановитьЗначение("АналитикаУчетаПоПартнерам", Выборка.АналитикаУчетаПоПартнерам);
			ЭлементБлокировки.УстановитьЗначение("ОбъектРасчетов", Выборка.ОбъектРасчетов);
			ЭлементБлокировки.УстановитьЗначение("Валюта", Выборка.Валюта);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ПричинаИсключения = 2; // Обработка данных
			ШаблонРекомендации = НСтр(
				"ru = 'Начиная с периода %1 по объекту расчетов: %2 необходимо выполнить распределение взаиморасчетов.';
				|en = 'Allocate AR/AP for the ""%2"" AR/AP object starting from the period  of %1.'");
			Рекомендация =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРекомендации,
				Выборка.ДатаПересчета, Выборка.ОбъектРасчетов);

			Если РаспределятьВзаиморасчетыФоновымЗаданием Тогда
				
				Если Не Выборка.ДатаПересчета = МаксимальныйПериод Тогда

					Если Выборка.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом Тогда

						ЗапросРасчетыСПартнерами.Текст = ТекстЗапросаРасчетыСКлиентами;
						
					Иначе

						ЗапросРасчетыСПартнерами.Текст = ТекстЗапросаРасчетыСПоставщиками;

					КонецЕсли;

					ЗапросРасчетыСПартнерами.УстановитьПараметр("Период", Выборка.ДатаПересчета);
					ЗапросРасчетыСПартнерами.УстановитьПараметр("АналитикаУчетаПоПартнерам",
						Выборка.АналитикаУчетаПоПартнерам);
					ЗапросРасчетыСПартнерами.УстановитьПараметр("ОбъектРасчетов", Выборка.ОбъектРасчетов);
					ЗапросРасчетыСПартнерами.УстановитьПараметр("Валюта", Выборка.Валюта);
					ВыборкаРасчетыСПартнерами = ЗапросРасчетыСПартнерами.Выполнить().Выбрать();

					НаборЗаписейЗадания = РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьНаборЗаписей();

					Пока ВыборкаРасчетыСПартнерами.Следующий() Цикл
						СоздатьЗаданияПоДокументу(НаборЗаписейЗадания, ВыборкаРасчетыСПартнерами);
					КонецЦикла;

					НаборЗаписейЗадания.Записать(Ложь);

				КонецЕсли;
				
			КонецЕсли;
			
			ПричинаИсключения = 3; // Запись
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			
			НаборЗаписей.Записать();
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Выборка.ОбъектРасчетов);

			Если ПричинаИсключения = 2 Тогда

				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
						Выборка.ОбъектРасчетов, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);

			ИначеЕсли ПричинаИсключения = 3 Тогда

				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
						ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);

			КонецЕсли;
			
		КонецПопытки;

	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда

		СписокОписаний.Добавить(НСтр("ru = 'Всего пропущено: %1';
									|en = 'Total skipped: %1'"));
		ТекстСообщения = СтрШаблон(СтрСоединить(СписокОписаний, Символы.ПС), ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;

	Иначе

		ШаблонСообщения = НСтр("ru = 'Регистр сведений ""Задания к распределению расчетов"". Обработана порция заданий: %1';
								|en = 'The ""Jobs for allocating AR/AP"" information register. A batch of jobs is processed: %1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектовОбработано);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Информация, , ,
			ТекстСообщения);

	КонецЕсли;

	ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);

	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

Процедура СоздатьЗаданияПоДокументу(НаборЗаписейЗадания, ПараметрыЗадания)

	НовоеЗадание = НаборЗаписейЗадания.Добавить();
	НовоеЗадание.Организация = ПараметрыЗадания.Организация;
	НовоеЗадание.ПериодЗадания = ПараметрыЗадания.ПериодЗадания;
	НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
	НовоеЗадание.АналитикаУчетаПоПартнерам = ПараметрыЗадания.АналитикаУчетаПоПартнерам;
	НовоеЗадание.ОбъектРасчетов = ПараметрыЗадания.ОбъектРасчетов;
	НовоеЗадание.Валюта = ПараметрыЗадания.Валюта;
	НовоеЗадание.Документ = ПараметрыЗадания.Документ;
	НовоеЗадание.ДатаЗаписи = ТекущаяДатаСеанса();

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
