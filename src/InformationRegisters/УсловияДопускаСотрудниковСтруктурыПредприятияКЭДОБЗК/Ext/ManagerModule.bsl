#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция УсловияДопуска(СтруктурныеЕдиницы = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("СтруктурныеЕдиницыЗаданы", ЗначениеЗаполнено(СтруктурныеЕдиницы));
	Запрос.УстановитьПараметр("СтруктурныеЕдиницы", СтруктурныеЕдиницы);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Организации.Ссылка КАК Ссылка,
		|	НЕОПРЕДЕЛЕНО КАК Организация,
		|	НЕОПРЕДЕЛЕНО КАК Родитель,
		|	ЕСТЬNULL(УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.УсловиеДопуска, ЗНАЧЕНИЕ(Перечисление.УсловияДопускаСотрудниковКЭДОБЗК.ПриПроверкеСкановПодписанногоСогласияНаЭДОБЗК)) КАК УсловиеДопуска
		|ИЗ
		|	Справочник.Организации КАК Организации
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК КАК УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК
		|		ПО Организации.Ссылка = УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.СтруктурнаяЕдиница
		|ГДЕ
		|	(НЕ &СтруктурныеЕдиницыЗаданы
		|			ИЛИ Организации.Ссылка В (&СтруктурныеЕдиницы))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.Владелец,
		|	ПодразделенияОрганизаций.Родитель,
		|	ЕСТЬNULL(УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.УсловиеДопуска, НЕОПРЕДЕЛЕНО)
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК КАК УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК
		|		ПО ПодразделенияОрганизаций.Ссылка = УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.СтруктурнаяЕдиница
		|ГДЕ
		|	(НЕ &СтруктурныеЕдиницыЗаданы
		|			ИЛИ ПодразделенияОрганизаций.Ссылка В (&СтруктурныеЕдиницы))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ШтатноеРасписание.Ссылка,
		|	ШтатноеРасписание.Владелец,
		|	ШтатноеРасписание.Подразделение,
		|	ЕСТЬNULL(УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.УсловиеДопуска, НЕОПРЕДЕЛЕНО)
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК КАК УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК
		|		ПО ШтатноеРасписание.Ссылка = УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.СтруктурнаяЕдиница
		|ГДЕ
		|	(НЕ &СтруктурныеЕдиницыЗаданы
		|			ИЛИ ШтатноеРасписание.Ссылка В (&СтруктурныеЕдиницы))";
	
	ИзвестныеУсловияДопускаКЭДОБЗК = Новый Соответствие;
	УсловияДопускаКЭДОБЗК = Новый Соответствие;
	ВышестоящиеЕдиницы = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Сбор назначенных условий доступа к ЭДО и вышестоящих структурах
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.УсловиеДопуска) Тогда
			ИзвестныеУсловияДопускаКЭДОБЗК.Вставить(Выборка.Ссылка, Выборка.УсловиеДопуска);
		Иначе
			ИзвестныеУсловияДопускаКЭДОБЗК.Вставить(Выборка.Ссылка, Неопределено);
			Если Не ЗначениеЗаполнено(Выборка.УсловиеДопуска) Тогда
				Если ЗначениеЗаполнено(Выборка.Родитель) Тогда
					УсловияДопускаКЭДОБЗК.Вставить(Выборка.Ссылка, Выборка.Родитель);
				ИначеЕсли ЗначениеЗаполнено(Выборка.Организация) Тогда
					УсловияДопускаКЭДОБЗК.Вставить(Выборка.Ссылка, Выборка.Организация);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	// Сбор назначенных условий вышестоящих структур, если известен,
	// подготовка коллекции для рекурсивного получения условий доступа к ЭДО
	// вышестоящих структур
	ИзвестныеЗначенияУчтены = Ложь;
	Пока Не ИзвестныеЗначенияУчтены Цикл
		ВышестоящиеЕдиницы.Очистить();
		НайденныеЗначения = Новый Соответствие;
		Для Каждого ДанныеУсловия Из ИзвестныеУсловияДопускаКЭДОБЗК Цикл
			Если ЗначениеЗаполнено(ДанныеУсловия.Значение) Тогда
				Продолжить;
			КонецЕсли;
			УсловияДоступаВышестоящей = ИзвестныеУсловияДопускаКЭДОБЗК.Получить(УсловияДопускаКЭДОБЗК.Получить(ДанныеУсловия.Ключ));
			Если УсловияДоступаВышестоящей <> Неопределено Тогда
				НайденныеЗначения.Вставить(ДанныеУсловия.Ключ, УсловияДоступаВышестоящей);
			Иначе
				ВышестоящиеЕдиницы.Добавить(УсловияДопускаКЭДОБЗК.Получить(ДанныеУсловия.Ключ));
			КонецЕсли;
		КонецЦикла;
		Если НайденныеЗначения.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ИзвестныеУсловияДопускаКЭДОБЗК, НайденныеЗначения, Истина);
		Иначе
			ИзвестныеЗначенияУчтены = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Рекурсивное получение условий доступа вышестоящих структур
	Если ЗначениеЗаполнено(ВышестоящиеЕдиницы) Тогда
		ИзвестныеЗначенияВышестоящих = УсловияДопуска(ВышестоящиеЕдиницы);
		НайденныеЗначения = Новый Соответствие;
		Для Каждого ДанныеУсловия Из ИзвестныеУсловияДопускаКЭДОБЗК Цикл
			Если Не ЗначениеЗаполнено(ДанныеУсловия.Значение) Тогда
				УсловияДоступаВышестоящей = ИзвестныеЗначенияВышестоящих.Получить(УсловияДопускаКЭДОБЗК.Получить(ДанныеУсловия.Ключ));
				Если УсловияДоступаВышестоящей <> Неопределено Тогда
					НайденныеЗначения.Вставить(ДанныеУсловия.Ключ, УсловияДоступаВышестоящей);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если НайденныеЗначения.Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ИзвестныеУсловияДопускаКЭДОБЗК, НайденныеЗначения, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИзвестныеУсловияДопускаКЭДОБЗК;
	
КонецФункции

Процедура СохранитьУсловияДопуска(УсловияДопуска) Экспорт
	
	НаборЗаписей = РегистрыСведений.УсловияДопускаСотрудниковСтруктурыПредприятияКЭДОБЗК.СоздатьНаборЗаписей();
	Для Каждого ДанныеУсловияДопуска Из УсловияДопуска Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.СтруктурнаяЕдиница = ДанныеУсловияДопуска.Ключ;
		Запись.УсловиеДопуска = ДанныеУсловияДопуска.Значение;
	КонецЦикла;
	НаборЗаписей.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
