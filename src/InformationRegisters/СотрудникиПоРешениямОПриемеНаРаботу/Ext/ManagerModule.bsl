// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.СинхронизацияДанных") Тогда
		МодульСинхронизацияДанныхЗарплатаКадрыСервер = ОбщегоНазначения.ОбщийМодуль("СинхронизацияДанныхЗарплатаКадрыСервер");
		Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ПодборПерсонала") Тогда
			ИдентификаторРазделаДанных = МодульСинхронизацияДанныхЗарплатаКадрыСервер.ИдентификаторыРазделовДанных().ПодборПерсонала;
		Иначе
			ИдентификаторРазделаДанных = МодульСинхронизацияДанныхЗарплатаКадрыСервер.ИдентификаторыРазделовДанных().РегламентированныеДанные;
		КонецЕсли;
		ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, 
			Метаданные.РегистрыСведений.СотрудникиПоРешениямОПриемеНаРаботу.Измерения.Решение, 
			ИдентификаторРазделаДанных);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Решения о приеме на работу по сотрудникам.
// 
// Параметры:
//  Сотрудники - Массив из СправочникСсылка.Сотрудники
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//   * Ключ     - СправочникСсылка.Сотрудники
//   * Значение - ДокументСсылка.РешениеОПриемеНаРаботу
Функция РешенияОПриемеНаРаботуПоСотрудникам(Сотрудники) Экспорт
	
	РешенияПоСотрудникам = Новый Соответствие();
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СотрудникиПоРешениям.Сотрудник КАК Сотрудник,
		|	СотрудникиПоРешениям.Решение КАК Решение
		|ИЗ
		|	РегистрСведений.СотрудникиПоРешениямОПриемеНаРаботу КАК СотрудникиПоРешениям
		|ГДЕ
		|	СотрудникиПоРешениям.Сотрудник В(&Сотрудники)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		РешенияПоСотрудникам.Вставить(Выборка.Сотрудник, Выборка.Решение);
	КонецЦикла;
	
	Возврат РешенияПоСотрудникам;
	
КонецФункции

// Сотрудники по решениям о приеме на работу.
// 
// Параметры:
//  Решения - Массив Из ДокументСсылка.РешениеОПриемеНаРаботу
// 
// Возвращаемое значение:
//  Соответствие Из КлючИЗначение:
//   * Ключ     - ДокументСсылка.РешениеОПриемеНаРаботу
//   * Значение - СправочникСсылка.Сотрудники
Функция СотрудникиПоРешениямОПриемеНаРаботу(Решения) Экспорт
	
	СотрудникиПоРешениям = Новый Соответствие();

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Решения", Решения);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СотрудникиПоРешениям.Сотрудник КАК Сотрудник,
		|	СотрудникиПоРешениям.Решение КАК Решение
		|ИЗ
		|	РегистрСведений.СотрудникиПоРешениямОПриемеНаРаботу КАК СотрудникиПоРешениям
		|ГДЕ
		|	СотрудникиПоРешениям.Решение В(&Решения)";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СотрудникиПоРешениям.Вставить(Выборка.Решение, Выборка.Сотрудник);
	КонецЦикла;
	
	Возврат СотрудникиПоРешениям;
	
КонецФункции

// Связать сотрудников с решениями о приеме на работу.
// 
// Параметры:
//  СотрудникиПоРешениям - Соответствие Из КлючИЗначение:
//   * Ключ     - ДокументСсылка.РешениеОПриемеНаРаботу
//   * Значение - СправочникСсылка.Сотрудники
Процедура СвязатьСотрудниковСРешениямиОПриемеНаРаботу(СотрудникиПоРешениям) Экспорт
	
	Для Каждого КлючИЗначение Из СотрудникиПоРешениям Цикл
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Решение.Установить(КлючИЗначение.Ключ);
		Если ЗначениеЗаполнено(КлючИЗначение.Значение) Тогда
			СтрокаНабора = НаборЗаписей.Добавить();
			СтрокаНабора.Решение = КлючИЗначение.Ключ;
			СтрокаНабора.Сотрудник = КлючИЗначение.Значение;
		КонецЕсли;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
