#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	НепроверяемыеРеквизиты = Новый Массив;
	
	Для Каждого ЗаписьНабора Из ЭтотОбъект Цикл
		
		Если ЗначениеЗаполнено(ЗаписьНабора.Организация) Тогда
			
			ТаблицаОрганизаций = ИнтеграцияСАТУРН.НоваяТаблицаОрганизацияКонтрагент();
			НоваяСтрока                       = ТаблицаОрганизаций.Добавить();
			НоваяСтрока.ОрганизацияКонтрагент = ЗаписьНабора.Организация;
			ИнтеграцияСАТУРНВызовСервера.РеквизитыОрганизацийКонтрагентов(ТаблицаОрганизаций);
			
			ИННОрганизации = НоваяСтрока.ИНН;
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗаписьНабора.КлючАвторизации) Тогда
			
			РезультатДекодирования = ИнтеграцияСАТУРНСлужебный.ДекодироватьТокенJWT(ЗаписьНабора.КлючАвторизации);
			ОшибкаДекодирования    = РезультатДекодирования.ОшибкаДекодирования;
			
			Если ОшибкаДекодирования Тогда
				ОбщегоНазначения.СообщитьПользователю(
						СтрШаблон(НСтр("ru = 'Ошибка разбора ключа авторизации: %1';
										|en = 'Ошибка разбора ключа авторизации: %1'"), РезультатДекодирования.Результат),,
						"Запись.КлючАвторизации",,
						Отказ);
			ИначеЕсли ЗначениеЗаполнено(РезультатДекодирования.ИНН)
				И Не ПолучитьФункциональнуюОпцию("РежимРаботыСТестовымКонтуромСАТУРН") Тогда
				
				Если РезультатДекодирования.ИНН <> ИННОрганизации Тогда
					ОбщегоНазначения.СообщитьПользователю(
						НСтр("ru = 'ИНН в токене авторизации не соответствует ИНН организации';
							|en = 'ИНН в токене авторизации не соответствует ИНН организации'"),,
						"Запись.КлючАвторизации",,
						Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
			НепроверяемыеРеквизиты.Добавить("КлючОбновления");
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ЗаписьНабора.КлючОбновления) Тогда
			
			РезультатДекодирования = ИнтеграцияСАТУРНСлужебный.ДекодироватьТокенJWT(ЗаписьНабора.КлючОбновления);
			ОшибкаДекодирования    = РезультатДекодирования.ОшибкаДекодирования;
			
			Если ОшибкаДекодирования Тогда
				ОбщегоНазначения.СообщитьПользователю(
						СтрШаблон(НСтр("ru = 'Ошибка разбора ключа авторизации: %1';
										|en = 'Ошибка разбора ключа авторизации: %1'"), РезультатДекодирования.Результат),,
						"Запись.КлючОбновления",,
						Отказ);
			ИначеЕсли ЗначениеЗаполнено(РезультатДекодирования.ИНН)
				И Не ПолучитьФункциональнуюОпцию("РежимРаботыСТестовымКонтуромСАТУРН") Тогда
				
				Если РезультатДекодирования.ИНН <> ИННОрганизации Тогда
					ОбщегоНазначения.СообщитьПользователю(
						НСтр("ru = 'ИНН в токене обновления не соответствует ИНН организации';
							|en = 'ИНН в токене обновления не соответствует ИНН организации'"),,
						"Запись.КлючОбновления",,
						Отказ);
				КонецЕсли;
				
			КонецЕсли;
			
			НепроверяемыеРеквизиты.Добавить("КлючАвторизации");
			
		КонецЕсли;
		
	КонецЦикла;
	
	ИнтеграцияСАТУРНПереопределяемый.ПриОпределенииОбработкиПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, НепроверяемыеРеквизиты);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
