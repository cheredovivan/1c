
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	ОбщегоНазначенияИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, Элементы.КлючОбновления.Имя);
	ОбщегоНазначенияИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, Элементы.КлючАвторизации.Имя);
	УправлениеЭлементамиФормы();
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЗаполнитьПредставлениеДанныхКлючаАвторизации();
	ЗаполнитьПредставлениеДанныхКлючаОбновления();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если ОшибкаДекодированияКлючаАвторизации Тогда
		ОбщегоНазначения.СообщитьПользователю(
			ДанныеДекодированияКлючаАвторизации,,, "ДанныеДекодированияКлючаАвторизации", Отказ);
	КонецЕсли;
	
	Если ОшибкаДекодированияКлючаОбновления Тогда
		ОбщегоНазначения.СообщитьПользователю(
			ДанныеДекодированияКлючаОбновления,,, "ДанныеДекодированияКлючаОбновления", Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КлючАвторизацииОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючАвторизацииПриИзменении(Элемент)
	КлючАвторизацииПриИзмененииНаСервере();
	Элементы.СтраницыДанныеКлючей.ТекущаяСтраница = Элементы.СтраницаДанныеКлючаАвторизации;
КонецПроцедуры

&НаКлиенте
Процедура КлючОбновленияОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Элемент.РежимПароля = Не Элемент.РежимПароля;
	
КонецПроцедуры

&НаКлиенте
Процедура КлючОбновленияПриИзменении(Элемент)
	КлючОбновленияПриИзмененииНаСервере();
	Элементы.СтраницыДанныеКлючей.ТекущаяСтраница = Элементы.СтраницаДанныеКлючаОбновления;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитьКлючАвторизации(Команда)
	
	ОчиститьСообщения();
	
	Если Модифицированность Тогда
		
		ОписаниеОповещенияВопрос = Новый ОписаниеОповещения("ВопросЗаписатьНастройкуПодключенияЗавершение",
		                                                    ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Перед обновлением ключа авторизации необходимо записать настройку подключения. Записать?';
							|en = 'Перед обновлением ключа авторизации необходимо записать настройку подключения. Записать?'");
		ПоказатьВопрос(ОписаниеОповещенияВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ВыполнитьОбновлениеКлючаАвторизации();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВФормуАвторизации(Команда)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://api.fgis-saturn.ru/login");
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВФормуАвторизацииТестовыйКонтур(Команда)
	ФайловаяСистемаКлиент.ОткрытьНавигационнуюСсылку("https://demo-hs.fgis-saturn.ru/login");
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВопросЗаписатьНастройкуПодключенияЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВопроса = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Если ПроверитьЗаполнение() Тогда
		Если Записать() Тогда
			ОбновитьКлючАвторизацииНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбновлениеКлючаАвторизации()
	
	ОбновитьКлючАвторизацииНаСервере();
	Прочитать();
	
КонецПроцедуры

&НаСервере
Процедура КлючАвторизацииПриИзмененииНаСервере()
	ЗаполнитьПредставлениеДанныхКлючаАвторизации(Истина);
	ОбщегоНазначенияИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, Элементы.КлючОбновления.Имя);
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаСервере
Процедура КлючОбновленияПриИзмененииНаСервере()
	ЗаполнитьПредставлениеДанныхКлючаОбновления(Истина);
	ОбщегоНазначенияИС.ПрименитьУсловноеОформлениеКПолю(ЭтотОбъект, Элементы.КлючАвторизации.Имя);
	УправлениеЭлементамиФормы();
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// ДанныеДекодированияКлючаАвторизации
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеДекодированияКлючаАвторизации.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОшибкаДекодированияКлючаАвторизации");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС);
	
	// ДанныеДекодированияКлючаОбновления
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДанныеДекодированияКлючаОбновления.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("ОшибкаДекодированияКлючаОбновления");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КлючОбновления.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Запись.КлючАвторизации");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.КлючАвторизации.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Запись.КлючОбновления");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.Заполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ОбновитьКлючАвторизации.Доступность = ЗначениеЗаполнено(Запись.КлючОбновления)
		И ПравоДоступа("Изменение", Метаданные.РегистрыСведений.НастройкиПодключенияСАТУРН);
	
	Элементы.ПерейтиВФормуАвторизации.Видимость = Не ПолучитьФункциональнуюОпцию("РежимРаботыСТестовымКонтуромСАТУРН");
	Элементы.ПерейтиВФормуАвторизацииТестовыйКонтур.Видимость = ПолучитьФункциональнуюОпцию("РежимРаботыСТестовымКонтуромСАТУРН");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеДанныхКлючаАвторизации(ЭтоИзменениеТокена = Ложь)
	
	Если ЗначениеЗаполнено(Запись.КлючАвторизации) Тогда
		РезультатДекодирования              = ИнтеграцияСАТУРНСлужебный.ДекодироватьТокенJWT(Запись.КлючАвторизации);
		ОшибкаДекодированияКлючаАвторизации = РезультатДекодирования.ОшибкаДекодирования;
		Если Не ОшибкаДекодированияКлючаАвторизации
			И НРег(РезультатДекодирования.Тип) <> НРег("Bearer") Тогда
			ОшибкаДекодированияКлючаАвторизации = Истина;
			СрокДействияКлючаАвторизации        = Неопределено;
			Запись.Логин                        = Неопределено;
			ДанныеДекодированияКлючаАвторизации = СтрШаблон(
				НСтр("ru = 'Токен %1 не является ключем авторизации Bearer';
					|en = 'Токен %1 не является ключем авторизации Bearer'"),
				РезультатДекодирования.Тип);
		Иначе
			ДанныеДекодированияКлючаАвторизации = РезультатДекодирования.Результат;
			СрокДействияКлючаАвторизации        = РезультатДекодирования.ДействуетДо;
			Если ЭтоИзменениеТокена Тогда
				Запись.Логин = РезультатДекодирования.Логин;
			КонецЕсли;
		КонецЕсли;
	Иначе
		ОшибкаДекодированияКлючаАвторизации = Ложь;
		ДанныеДекодированияКлючаАвторизации = "";
		СрокДействияКлючаАвторизации        = Неопределено;
		Если ЭтоИзменениеТокена И Не ЗначениеЗаполнено(Запись.КлючОбновления) Тогда
			Запись.Логин = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокДействияКлючаАвторизации) Тогда
		ДанныеПредставленияСрока = ОбщегоНазначенияИСКлиентСервер.ДанныеПредставленияСрокаГодности(
			СрокДействияКлючаАвторизации, ТекущаяДатаСеанса(), Истина);
		
		Если ДанныеПредставленияСрока.Истек Тогда
			Элементы.СрокДействияКлючаАвторизации.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПредставленияСрока.Представление,, ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС);
		ИначеЕсли ДанныеПредставленияСрока.ОсталосьДней = 0 Тогда
			Элементы.СрокДействияКлючаАвторизации.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПредставленияСрока.Представление,, ЦветаСтиля.ЦветТекстаПроблемаГосИС);
		Иначе
			Элементы.СрокДействияКлючаАвторизации.РасширеннаяПодсказка.Заголовок = ДанныеПредставленияСрока.Представление;
		КонецЕсли;
		
	Иначе
		Элементы.СрокДействияКлючаАвторизации.РасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПредставлениеДанныхКлючаОбновления(ЭтоИзменениеТокена = Ложь)
	
	Если ЗначениеЗаполнено(Запись.КлючОбновления) Тогда
		РезультатДекодирования             = ИнтеграцияСАТУРНСлужебный.ДекодироватьТокенJWT(Запись.КлючОбновления);
		ОшибкаДекодированияКлючаОбновления = РезультатДекодирования.ОшибкаДекодирования;
		
		Если Не ОшибкаДекодированияКлючаОбновления
			И НРег(РезультатДекодирования.Тип) <> НРег("Refresh") Тогда
			ОшибкаДекодированияКлючаОбновления = Истина;
			СрокДействияКлючаОбновления        = Неопределено;
			ДанныеДекодированияКлючаОбновления = СтрШаблон(
				НСтр("ru = 'Токен %1 не является ключем авторизации Refresh';
					|en = 'Токен %1 не является ключем авторизации Refresh'"),
				РезультатДекодирования.Тип);
		Иначе
			ДанныеДекодированияКлючаОбновления = РезультатДекодирования.Результат;
			СрокДействияКлючаОбновления        = РезультатДекодирования.ДействуетДо;
		КонецЕсли;
		
	Иначе
		ОшибкаДекодированияКлючаОбновления = Ложь;
		ДанныеДекодированияКлючаОбновления = "";
		СрокДействияКлючаОбновления        = Неопределено;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СрокДействияКлючаОбновления) Тогда
		ДанныеПредставленияСрока = ОбщегоНазначенияИСКлиентСервер.ДанныеПредставленияСрокаГодности(
			СрокДействияКлючаОбновления, ТекущаяДатаСеанса(), Истина);
		
		Если ДанныеПредставленияСрока.Истек < 0 Тогда
			Элементы.СрокДействияКлючаОбновления.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПредставленияСрока.Представление,, ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС);
		ИначеЕсли ДанныеПредставленияСрока.ОсталосьДней = 0 Тогда
			Элементы.СрокДействияКлючаОбновления.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ДанныеПредставленияСрока.Представление,, ЦветаСтиля.ЦветТекстаПроблемаГосИС);
		Иначе
			Элементы.СрокДействияКлючаОбновления.РасширеннаяПодсказка.Заголовок = ДанныеПредставленияСрока.Представление;
		КонецЕсли;
		
	Иначе
		Элементы.СрокДействияКлючаОбновления.РасширеннаяПодсказка.Заголовок = "";
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьКлючАвторизацииНаСервере()
	
	ТекущиеДанные = ИнтеграцияСАТУРН.РезультатПодбораКлючаАвторизации();
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Запись);
	ПараметрыОптимизации = ИнтеграцияСАТУРНСлужебный.ПараметрыОптимизации();
	РезультатОбновления  = ИнтеграцияСАТУРН.ОбновитьКлючСессии(ТекущиеДанные, ПараметрыОптимизации);
	
	Если Не РезультатОбновления.Успешно Тогда
		ОбщегоНазначения.СообщитьПользователю(РезультатОбновления.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти