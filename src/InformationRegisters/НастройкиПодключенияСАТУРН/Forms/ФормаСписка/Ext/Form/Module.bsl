
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Изменение_НастройкиПодключенияСАТУРН" Тогда
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	ЦветТребуетВнимания   = ЦветаСтиля.ЦветТекстаТребуетВниманияГосИС;
	ЦветНеТребуетВнимания = ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС;
	ЦветПроблема          = ЦветаСтиля.ЦветТекстаОтложенаПриПроверкеГосИС;
	ТекущаяДата           = ТекущаяДатаСеанса();
	
	Для Каждого Строка Из Строки Цикл
		
		Оформление = Строка.Значение.Оформление.Получить("ДействуетДо");
		Если Оформление <> Неопределено Тогда
			
			ДействуетДо   = Строка.Значение.Данные.ДействуетДо;
			ДанныеРазбора = ИнтеграцияСАТУРНСлужебный.ДекодироватьТокенJWT(Строка.Значение.Данные.КлючАвторизации);
			Если Не ДанныеРазбора.ОшибкаДекодирования Тогда
				ДействуетДо = ДанныеРазбора.ДействуетДо;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ДействуетДо) Тогда
				
				ДанныеПредставленияСрока = ОбщегоНазначенияИСКлиентСервер.ДанныеПредставленияСрокаГодности(
					ДействуетДо, ТекущаяДата, Истина);
				Оформление.УстановитьЗначениеПараметра("Текст", ДанныеПредставленияСрока.Представление);
				
				Если ДанныеПредставленияСрока.Истек Тогда
					Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветТребуетВнимания);
				ИначеЕсли ДанныеПредставленияСрока.ОсталосьДней = 0 Тогда
					Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветПроблема);
				КонецЕсли;
		
			Иначе
				
				Оформление.УстановитьЗначениеПараметра("Текст",      НСтр("ru = '<отсутствует>';
																			|en = '<отсутствует>'"));
				Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветНеТребуетВнимания);
				
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	//
	ЭлементУсловногоОформления = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.Пользователь.Имя);
	
	ОтборЭлемента = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ОтборЭлемента.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Список.Пользователь");
	ОтборЭлемента.ВидСравнения   = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<для всех пользователей>';
																					|en = '<для всех пользователей>'"));
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаНеТребуетВниманияГосИС);
	
КонецПроцедуры

#КонецОбласти

