
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи) Тогда
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Элементы.Организация.Видимость И Не ЗначениеЗаполнено(Запись.Организация)
		И Элементы.МестоУчета.Видимость И Не ЗначениеЗаполнено(Запись.МестоУчета) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""Организация"" или ""%1"" должно быть заполнено.';
										|en = 'The ""Company"" or ""%1"" field is required.'"), Элементы.МестоУчета.Заголовок);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
	ИначеЕсли Элементы.Организация.Видимость И Не ЗначениеЗаполнено(Запись.Организация) И Не Элементы.МестоУчета.Видимость Тогда
		ОбщегоНазначения.СообщитьПользователю(
			НСтр("ru = 'Поле ""Организация"" должна быть заполнено.';
				|en = 'The ""Company"" field is required.'"), ,  "Запись.Организация", , Отказ);
	ИначеЕсли Элементы.МестоУчета.Видимость И Не ЗначениеЗаполнено(Запись.МестоУчета) И Не Элементы.Организация.Видимость Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Поле ""%1"" должно быть заполнено.';
										|en = 'The ""%1"" field is required.'"), Элементы.МестоУчета.Заголовок);
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Запись.МестоУчета", , Отказ);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Запись_НастройкиСчетовМеждународногоУчетаПоОбъектам");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектУчетаПриИзменении(Элемент)
	ОбъектУчетаПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ТипНастройкиПриИзменении(Элемент)
	ТипНастройкиПриИзмененииСервер();
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНастройкиНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПолноеИмяФормы = ИсточникОбъектовНастройки + "." + "ФормаВыбора";
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ФиксированныеНастройки", КомпоновщикНастроекОбъектНастройки.Настройки);
	
	ОткрытьФорму(ПолноеИмяФормы, ПараметрыФормы, Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	ПолучитьОписаниеОбъектаУчета();
	УправлениеФормой()
КонецПроцедуры

&НаСервере
Процедура УправлениеФормой()
	
	НастройкаИсключения = (Запись.ТипНастройки = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ИсключениеПоОбъектуУчета);
	
	Элементы.Организация.Видимость = НастройкаИсключения;
	
	Элементы.ТипНастройки.ТолькоПросмотр = Параметры.НеИзменятьТипНастройки;
	Элементы.ОбъектУчета.ТолькоПросмотр = Параметры.НеИзменятьОбъектУчета;
	
	Если ОписаниеОбъектаУчета <> Неопределено Тогда
		ВыделениеДолгосрочныхАктивовОбязательств = ОписаниеОбъектаУчета.ВыделениеДолгосрочныхАктивовОбязательств;
		НесобственныеЦенности = ОписаниеОбъектаУчета.НесобственныеЦенности;
		
		Элементы.ОбъектНастройки.Видимость = ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки.Типы().Количество() > 0;
		Элементы.ОбъектНастройки.ОграничениеТипа = ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки;
		Элементы.ОбъектНастройки.Заголовок = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗаголовокОбъектаПоОписаниюТипов(
			ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки);
		
		Элементы.МестоУчета.Видимость = НастройкаИсключения И ОписаниеОбъектаУчета.ОписаниеТиповДетальнойНастройки.Типы().Количество() > 0;
		Элементы.МестоУчета.ОграничениеТипа = ОписаниеОбъектаУчета.ОписаниеТиповДетальнойНастройки;
		Элементы.МестоУчета.Заголовок = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ЗаголовокОбъектаПоОписаниюТипов(
			ОписаниеОбъектаУчета.ОписаниеТиповДетальнойНастройки);
		
		КомпоновщикНастроекОбъектНастройки.Инициализировать(Неопределено);
		КомпоновщикНастроекОбъектНастройки.Настройки.Отбор.Элементы.Очистить();
		
		Если ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки.Типы().Количество() > 0 Тогда
			ТипОбъектаНастройки = ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки.Типы()[0];
			МетаданныеОбъектНастройки = Метаданные.НайтиПоТипу(ТипОбъектаНастройки);
			ИсточникОбъектовНастройки = МетаданныеОбъектНастройки.ПолноеИмя();
			
			ПланыВидовХарактеристик.СтатьиАктивовПассивов.ИнициализироватьКомпоновщикНастроекОбъектомНастройки(
				КомпоновщикНастроекОбъектНастройки,
				ИсточникОбъектовНастройки,
				МетаданныеОбъектНастройки,
				УникальныйИдентификатор);
			
			МеждународныйУчетПоДаннымФинансовыхРегистров.СкопироватьОтборКомпоновкиДанных(
				КомпоновщикНастроекОбъектНастройки.Настройки.Отбор,
				ОписаниеОбъектаУчета.ОтборОбъектовНастройки);
		КонецЕсли;
	Иначе
		ВыделениеДолгосрочныхАктивовОбязательств = Ложь;
		Элементы.ОбъектНастройки.Видимость = Ложь;
		Элементы.МестоУчета.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Организация.АвтоОтметкаНезаполненного = НЕ Элементы.МестоУчета.Видимость;
	Элементы.Организация.ПодсказкаВвода = ?(Элементы.МестоУчета.Видимость, НСтр("ru = 'Все';
																				|en = 'All'"), "");
	
	Элементы.МестоУчета.АвтоОтметкаНезаполненного = НЕ Элементы.Организация.Видимость;
	Элементы.МестоУчета.ПодсказкаВвода = ?(Элементы.Организация.Видимость, НСтр("ru = 'Все';
																				|en = 'All'"), "");
	
	Если НЕ ВыделениеДолгосрочныхАктивовОбязательств Тогда
	ИначеЕсли НЕ ЗначениеЗаполнено(Запись.НастройкаФормированияПроводок) Тогда
		ВыделениеДолгосрочныхАктивовОбязательств = Ложь;
	Иначе
		ВыделениеДолгосрочныхАктивовОбязательств = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Запись.НастройкаФормированияПроводок, "ИспользоватьВыделениеДолгосрочныхАктивовОбязательств");
	КонецЕсли;
	
	Элементы.ГруппаСчетаУчета.ОтображатьЗаголовок = ВыделениеДолгосрочныхАктивовОбязательств;
	Элементы.СчетУчетаДолгосрочный.Видимость = ВыделениеДолгосрочныхАктивовОбязательств;
	Элементы.СчетУчета.Заголовок = ?(ВыделениеДолгосрочныхАктивовОбязательств, НСтр("ru = 'Краткосрочный';
																					|en = 'Short-term'"), "");
	
КонецПроцедуры

&НаСервере
Процедура ОбъектУчетаПриИзмененииСервер()
	
	ПолучитьОписаниеОбъектаУчета();
	
	Если ОписаниеОбъектаУчета <> Неопределено Тогда
		Запись.ОбъектНастройки = ОписаниеОбъектаУчета.ОписаниеТиповОбъектНастройки.ПривестиЗначение(Запись.ОбъектНастройки);
		
		Если Запись.ТипНастройки = Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ИсключениеПоОбъектуУчета Тогда
			Запись.МестоУчета = ОписаниеОбъектаУчета.ОписаниеТиповДетальнойНастройки.ПривестиЗначение(Запись.МестоУчета);
		КонецЕсли;
	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ТипНастройкиПриИзмененииСервер()
	Если Запись.ТипНастройки <> Перечисления.ТипыНастроекОтраженияВМеждународномУчете.ИсключениеПоОбъектуУчета Тогда
		Запись.Организация = Неопределено;
		Запись.МестоУчета = Неопределено;
	КонецЕсли;
	УправлениеФормой();
КонецПроцедуры

&НаСервере
Процедура ПолучитьОписаниеОбъектаУчета()
	
	Если ЗначениеЗаполнено(Запись.ОбъектУчета) Тогда
		ОписаниеОбъектаУчета = ПланыВидовХарактеристик.СтатьиАктивовПассивов.ОписаниеОбъектаФинансовогоУчета(
			Запись.ОбъектУчета);
	Иначе
		ОписаниеОбъектаУчета = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

