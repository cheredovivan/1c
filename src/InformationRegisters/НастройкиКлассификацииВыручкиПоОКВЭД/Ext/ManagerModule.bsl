#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет настройки заполнения данными из отчета РаспределениеВыручкиПоОКВЭД.
//
// Параметры:
//  ДополнительныеПараметры - Структура:
//				Организация - СправочникСсылка.Организации.
//  			ОбъектНаблюдения - СправочникСсылка.ОбъектыСтатистическогоНаблюдения.
//  			АдресРаспределениеВыручки - Строка.
//  ОбъектНаблюдения - СправочникСсылка.ОбъектыСтатистическогоНаблюдения - объект наблюдения.
//
Процедура ЗаполнитьРаспределениеВыручкиПоОКВЭД(ДополнительныеПараметры, ОбъектНаблюдения = Неопределено) Экспорт
	
	СохраненныеНастройкиПравил = РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.ПолучитьНастройкиОКВЭД(ДополнительныеПараметры.Организация, ДополнительныеПараметры.ДатаОкончания);
	Если СохраненныеНастройкиПравил = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаРаспределенияВыручки = СохраненныеНастройкиПравил.УстановленныеКоды; // ТаблицаЗначений
	ТаблицаРаспределенияВыручки.Колонки.Добавить("ЭтоИсключение");
	// Создаем новую таблицу, с темже набором колонк, но без определения типа.
	// Может потребоваться тип колонки СписокЗначений, если нужно будет сгруппировать условия Исключения.
	ТаблицаРаспределенияВыручки = ИнициализироватьТаблицу(ТаблицаРаспределенияВыручки);

	ПравилаУстановкиОКВЭД = Новый Соответствие();
	ТаблицаНастроек = ТаблицаРаспределенияВыручки.СкопироватьКолонки();
	ТаблицаНастроек.Индексы.Добавить("ЭтоИсключение");
	ТаблицаНастроек.Индексы.Добавить("Приоритет");
	ТаблицаНастроек.Индексы.Добавить("ОКВЭД");
	
	ТаблицаРаспределенияВыручки.Индексы.Добавить("ЭтоИсключение");
	ТаблицаРаспределенияВыручки.Индексы.Добавить("Приоритет");
	ТаблицаРаспределенияВыручки.Индексы.Добавить("ОКВЭД");
	
	СписокГруппировок = СтрРазделить(СохраненныеНастройкиПравил.СтрокаПравилНастройки, ",");
	МаксимальныйПриоритет = СписокГруппировок.Количество();
	
	// Определим наименование колонок в правилах в соответствии с группировками отчета
	СоответвиеПолейИГруппировок = Новый Соответствие();
	НомерПоля = 1;
	НомерКолонкиНоменклатура = 0;
	Для Каждого ПолеГруппировки Из СписокГруппировок Цикл
		
		СоответвиеПолейИГруппировок.Вставить("Поле"+НомерПоля,СтрЗаменить(Строка(ПолеГруппировки), ".", ""));
		Если Строка(ПолеГруппировки) = "Номенклатура" Тогда
			НомерКолонкиНоменклатура = НомерПоля;
		КонецЕсли;
		НомерПоля = НомерПоля + 1;
		
	КонецЦикла;
	
	// Если есть контролируемые сделки, то для услуг ОКВЭД заполняем из номенклатуры
	ИспользоватьУведомленияОКонтролируемыхСделках = Константы.ИспользоватьУведомленияОКонтролируемыхСделках.Получить();
	Если ИспользоватьУведомленияОКонтролируемыхСделках
		И НомерКолонкиНоменклатура > 0
		И ДополнительныеПараметры <> Неопределено Тогда
		
		ПравилаОКВЭДНоменклатуры = ОКВЭДНоменклатурыУслуг(ДополнительныеПараметры);
		ПравилаУстановкиОКВЭДНоменклатуры = ПравилаОКВЭДНоменклатуры.ПравилаУстановкиОКВЭД;
		
		Для Каждого ПравилоОКВЭД Из ПравилаУстановкиОКВЭДНоменклатуры Цикл
			
			Строка = ТаблицаРаспределенияВыручки.Добавить();
			Строка.Приоритет = 0;
			Строка["Поле"+НомерКолонкиНоменклатура] = ПравилоОКВЭД.Значение;
			Строка.ЭтоИсключение = Ложь;
			НайденнаяСтрока = ПравилаОКВЭДНоменклатуры.НаименованияОКВЭД.Найти(ПравилоОКВЭД.Ключ);
			Если НайденнаяСтрока <> Неопределено Тогда
				Строка.ОКВЭДНаименование = НайденнаяСтрока.Наименование;
			КонецЕсли;
			Строка.ОКВЭД = ПравилоОКВЭД.Ключ;
			
		КонецЦикла;
			
	КонецЕсли;
	
	ОКВЭДОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДополнительныеПараметры.Организация, "КодОКВЭД2, НаименованиеОКВЭД2");
	
	ТаблицаОКВЭД = ТаблицаРаспределенияВыручки.Скопировать();
	ТаблицаОКВЭД.Свернуть("ОКВЭД, ОКВЭДНаименование");
	Если ТаблицаОКВЭД.Найти(ОКВЭДОрганизации.КодОКВЭД2) = Неопределено Тогда
		
		Строка = ТаблицаОКВЭД.Добавить();
		Строка.ОКВЭД = ОКВЭДОрганизации.КодОКВЭД2;
		Строка.ОКВЭДНаименование = ОКВЭДОрганизации.НаименованиеОКВЭД2;
		
		ТекущееПравило = ПравилаУстановкиОКВЭД.Получить(ОКВЭДОрганизации.КодОКВЭД2);
		Если ТекущееПравило = Неопределено Тогда
			ПравилаУстановкиОКВЭД.Вставить(ОКВЭДОрганизации.КодОКВЭД2, ТаблицаНастроек.Скопировать());
		КонецЕсли;
		
	КонецЕсли;
	
	// Дополним правила исключениями
	ТаблицаРаспределенияВыручки.ЗаполнитьЗначения(Ложь, "ЭтоИсключение");
	Для Каждого ОКВЭДИсключения Из ТаблицаОКВЭД Цикл
		
		Отбор = Новый Структура();
		Отбор.Вставить("ОКВЭД", ОКВЭДИсключения.ОКВЭД);
		ОсновныеПравила = ТаблицаРаспределенияВыручки.НайтиСтроки(Отбор);
		
		Для Каждого ОКВЭДОтбора Из ТаблицаОКВЭД Цикл
			
			Если ОКВЭДИсключения.ОКВЭД <> ОКВЭДОтбора.ОКВЭД Тогда
				
				Отбор = Новый Структура();
				Отбор.Вставить("ОКВЭД", ОКВЭДОтбора.ОКВЭД);
				ПравилаОКВЭД = ТаблицаРаспределенияВыручки.СкопироватьКолонки();
				ОбщегоНазначенияУТ.ДобавитьСтрокиВТаблицу(ПравилаОКВЭД, ТаблицаРаспределенияВыручки.НайтиСтроки(Отбор));
				
				Для Каждого СтрокаОсновногоПравила Из ОсновныеПравила Цикл
					
					Если Не СтрокаОсновногоПравила.ЭтоИсключение Тогда
						
							ОтборПоГруппировкам = ОтборНастроек(СтрокаОсновногоПравила, СтрокаОсновногоПравила.Приоритет + 1, Ложь); 
							ОтборПоГруппировкам.Удалить("Приоритет");
							СтрокиОсновногоПравила = ПравилаОКВЭД.НайтиСтроки(ОтборПоГруппировкам);
							Если СтрокиОсновногоПравила.Количество() = 0 Или СтрокаОсновногоПравила.Приоритет = 0 Тогда
								Если СтрокаОсновногоПравила.Приоритет = 0 Тогда
									ИмяКолонки = "Поле"+НомерКолонкиНоменклатура;
								Иначе
									ИмяКолонки = "Поле" + СтрокаОсновногоПравила.Приоритет;
								КонецЕсли;
								ДобавитьСтрокуИсключения(ТаблицаРаспределенияВыручки, СтрокаОсновногоПравила, ОКВЭДОтбора, ИмяКолонки);
							КонецЕсли;
							
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Сгруппируем все условия
	Приоритет = 0;
	Пока Приоритет <= МаксимальныйПриоритет Цикл
		
		ОтборПоПриоритетам = Новый Структура();
		ОтборПоПриоритетам.Вставить("Приоритет", Приоритет);
		
		СтрокиПоПриоритету = ТаблицаРаспределенияВыручки.НайтиСтроки(ОтборПоПриоритетам);
		Если СтрокиПоПриоритету.Количество() = 0 Тогда
			Приоритет = Приоритет + 1;
			Продолжить;
		КонецЕСли;
		
		Для Каждого СтрокаПриоритета Из СтрокиПоПриоритету Цикл
			
			ТекущееПравило = ПравилаУстановкиОКВЭД.Получить(СтрокаПриоритета.ОКВЭД);
			Если ТекущееПравило = Неопределено Тогда
				ТаблицаНастроек.Очистить();
				ПравилаУстановкиОКВЭД.Вставить(СтрокаПриоритета.ОКВЭД, ТаблицаНастроек.Скопировать());
			КонецЕсли;
			
			ОтборПоГруппировкам = ОтборНастроек(СтрокаПриоритета, Приоритет, СтрокаПриоритета.ЭтоИсключение);
			ТаблицаНастроекОКВЭД = ПравилаУстановкиОКВЭД[СтрокаПриоритета.ОКВЭД];
			Правила = ТаблицаНастроекОКВЭД.НайтиСтроки(ОтборПоГруппировкам);
			
			Если Приоритет = 0 Тогда
				ИмяПоля = "Поле"+НомерКолонкиНоменклатура;
			Иначе
				ИмяПоля = "Поле"+Приоритет;
			КонецЕсли;
			
			Если Правила.Количество() > 0 Тогда
				СтрокаПравил = Правила[0];
				Если ТипЗнч(СтрокаПриоритета[ИмяПоля]) = Тип("СписокЗначений") Тогда
					Для Каждого Элемент Из СтрокаПриоритета[ИмяПоля] Цикл
						Если СтрокаПравил[ИмяПоля].НайтиПоЗначению(Элемент.Значение) = Неопределено Тогда
							СтрокаПравил[ИмяПоля].Добавить(Элемент.Значение);
						КонецЕсли;
					КонецЦикла;
				Иначе
					СтрокаПравил[ИмяПоля].Добавить(СтрокаПриоритета[ИмяПоля]);
				КонецЕсли;
			Иначе
				СтрокаПравил = ТаблицаНастроекОКВЭД.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПравил, СтрокаПриоритета);
				Если ТипЗнч(СтрокаПриоритета[ИмяПоля]) = Тип("СписокЗначений") Тогда
					ПоляЗначенийОтбора = СтрокаПриоритета[ИмяПоля].Скопировать();
				Иначе
					ПоляЗначенийОтбора = Новый СписокЗначений;
					ПоляЗначенийОтбора.Добавить(СтрокаПриоритета[ИмяПоля]);
				КонецЕсли;
				СтрокаПравил[ИмяПоля] = ПоляЗначенийОтбора;
			КонецЕсли;
			
		КонецЦикла;
		
		Приоритет = Приоритет + 1;
		
	КонецЦикла;
	
	ТаблицаЗаписей = ИнициализироватьТаблицуЗаписей();
	РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	Для Каждого ПравилоОКВЭД Из ПравилаУстановкиОКВЭД Цикл
		
		ТаблицаНастроекОКВЭД = ПравилоОКВЭД.Значение;
		
		Отбор = Новый Структура();
		Отбор.Вставить("Приоритет", 0); // Отбор только по Номенклатуре услуг контролируемых сделок
		Отбор.Вставить("ЭтоИсключение", Истина);
		Строки = ТаблицаНастроекОКВЭД.НайтиСтроки(Отбор);
		ЕстьИсключенияПоНоменклатуре = Строки.Количество() > 0;
		Если ЕстьИсключенияПоНоменклатуре Тогда
			СтрокаИсключенияПоНоменклатуре = Строки[0];
		КонецЕсли;
		
		Отбор = Новый Структура();
		Отбор.Вставить("Приоритет", 0); // Отбор только по Номенклатуре услуг контролируемых сделок
		Отбор.Вставить("ЭтоИсключение", Ложь);
		Строки = ТаблицаНастроекОКВЭД.НайтиСтроки(Отбор);
		ЕстьОсновнойОтборПоНоменклатуре = Строки.Количество() > 0;
		Если ЕстьОсновнойОтборПоНоменклатуре Тогда
			СтрокаПравилПоНоменклатуре = Строки[0];
		КонецЕсли;
		
		Отбор = Новый Структура();
		Отбор.Вставить("ЭтоИсключение", Ложь);
		Строки = ТаблицаНастроекОКВЭД.НайтиСтроки(Отбор);
		НесколькоОсновныхПравил = Строки.Количество() > 1; // Если несколько правил отбора, нужно добавить группу Или
		
		НастройкиКомпоновки = Новый КомпоновщикНастроекКомпоновкиДанных;
		ОтборКомпоновки = НастройкиКомпоновки.Настройки.Отбор;
		ГруппаИлиВерхегоУровня = Неопределено;
		Если НесколькоОсновныхПравил Тогда
			ГруппаИлиВерхегоУровня = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				ОтборКомпоновки, "ГруппаИли", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
			ГруппаИлиВерхегоУровня.РежимОтображения = РежимОтображения;
			ГруппаИлиВерхегоУровня.Представление = "";
		КонецЕсли;
			
		СправочникДетализации = Неопределено;
			
		Для Каждого СтрокаНастройки Из ТаблицаНастроекОКВЭД Цикл
				
			ГруппаИ = Неопределено;
			Если Не СтрокаНастройки.ЭтоИсключение
				И (СтрокаНастройки.Приоритет >1 Или ЕстьИсключенияПоНоменклатуре) Тогда
				
				Если НесколькоОсновныхПравил Тогда
					КоллекцияЭлементов = ГруппаИлиВерхегоУровня;
				Иначе
					КоллекцияЭлементов = ОтборКомпоновки;
				КонецЕсли;
				
				ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
					КоллекцияЭлементов, "ГруппаИ", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
				ГруппаИ.РежимОтображения = РежимОтображения;
				ГруппаИ.Представление = "";
				
			КонецЕсли;
			
			ГруппаНе = Неопределено;
			Если СтрокаНастройки.Приоритет >1 И СтрокаНастройки.ЭтоИсключение Тогда
				
				ГруппаНе = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
					ОтборКомпоновки, "ГруппаНе", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);
				ГруппаНе.РежимОтображения = РежимОтображения;
				ГруппаНе.Представление = "";
				
			КонецЕсли;
			
			Если СтрокаНастройки.Приоритет = 0 И СтрокаНастройки.ЭтоИсключение
				И ОКВЭДОрганизации.КодОКВЭД2 <> ПравилоОКВЭД.Ключ Тогда
				Продолжить; // Отбор по номенклатуре будет добавлен ниже в другие группировки основных отборов
			КонецЕсли;
			
			Если Не (ОКВЭДОрганизации.КодОКВЭД2 = ПравилоОКВЭД.Ключ И Не СтрокаНастройки.ЭтоИсключение) Тогда // Основные отборы для ОКВЭД организации не нужны
				
				Приоритет = 0;
				Пока Приоритет <= СтрокаНастройки.Приоритет Цикл
					
					Если Приоритет = 0 Тогда
						
						Если СтрокаНастройки.Приоритет = 0 И НомерКолонкиНоменклатура <> 0 Тогда
							ИмяКолонки = "Поле"+НомерКолонкиНоменклатура;
						Иначе
							Приоритет = Приоритет + 1;
							Продолжить;
						КонецЕсли;
						
					Иначе
						ИмяКолонки = "Поле" + Приоритет;
					КонецЕсли;
					
					ИмяПоля    = СоответвиеПолейИГруппировок[ИмяКолонки];
						
					ИзменитьВидСравнения = Ложь;
					Если СтрокаНастройки.Приоритет <= 1 И СтрокаНастройки.ЭтоИсключение Тогда
						ИзменитьВидСравнения = Истина;
					КонецЕсли;
					
					Если Не СтрокаНастройки.ЭтоИсключение И СтрокаНастройки.Приоритет <=1 Тогда
						Группа = ГруппаИлиВерхегоУровня;
					Иначе
						Группа = ?(СтрокаНастройки.ЭтоИсключение, ГруппаНе, ГруппаИ);
					КонецЕсли;
					
					ДобавитьОтбор(СтрокаНастройки, ИмяКолонки, ИмяПоля, ОтборКомпоновки, Группа, ИзменитьВидСравнения);
					
					Приоритет = Приоритет + 1;
					
				КонецЦикла;
				
			КонецЕсли;
			
			// Основные правила по ОКВЭД Организации должны быть добавлены как исключения
			// в вытестняющие правила ОКВЭД организации
			Если ОКВЭДОрганизации.КодОКВЭД2 = ПравилоОКВЭД.Ключ И СтрокаНастройки.ЭтоИсключение
				И СтрокаНастройки.Приоритет < МаксимальныйПриоритет И СтрокаНастройки.Приоритет <> 0 Тогда
				
				Отбор = Новый Структура();
				Отбор.Вставить("ЭтоИсключение", Ложь);
				СтрокиОсновногоОтбора = ТаблицаНастроекОКВЭД.НайтиСтроки(Отбор);
				ГруппаНеПоОрганизации = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				?(Группа = Неопределено, ОтборКомпоновки, Группа), "ГруппаНе", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе);
				ГруппаНеПоОрганизации.РежимОтображения = РежимОтображения;
				ГруппаНеПоОрганизации.Представление = "";

				Для Каждого Строка Из СтрокиОсновногоОтбора Цикл
					
					Если СтрокаНастройки.Приоритет < Строка.Приоритет Тогда
						
						Приоритет = 1;
						Пока Приоритет <= Строка.Приоритет Цикл

							ИмяКолонки = "Поле" + Приоритет;
							ИмяПоля    = СоответвиеПолейИГруппировок[ИмяКолонки];
							Группа = ?(СтрокаНастройки.ЭтоИсключение, ГруппаНе, ГруппаИ);
							ДобавитьОтбор(Строка, ИмяКолонки, ИмяПоля, ОтборКомпоновки, ГруппаНеПоОрганизации, Ложь);

							Приоритет = Приоритет + 1;

						КонецЦикла;

					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
			Если Не СтрокаНастройки.ЭтоИсключение
				И ЕстьИсключенияПоНоменклатуре
				И СтрокаНастройки.Приоритет <> 0
				И СтрокаНастройки.Приоритет <> НомерКолонкиНоменклатура Тогда  // Добавим отбор-исключение по номенклатуре в каждую группу основных правил
				
				ИмяКолонки = "Поле"+НомерКолонкиНоменклатура;
				ИмяПоля    = СоответвиеПолейИГруппировок[ИмяКолонки];
				ДобавитьОтбор(СтрокаИсключенияПоНоменклатуре, ИмяКолонки, ИмяПоля, ОтборКомпоновки, ГруппаИ, Истина);
				
			КонецЕсли;
				
			Если СтрокаНастройки.ЭтоИсключение
				И ЕстьОсновнойОтборПоНоменклатуре
				И СтрокаНастройки.Приоритет <> 0
				И СтрокаНастройки.Приоритет <> НомерКолонкиНоменклатура Тогда  // Добавим отбор по номенклатуре в каждую группу правил -исключения
				
				ИмяКолонки = "Поле"+НомерКолонкиНоменклатура;
				ИмяПоля    = СоответвиеПолейИГруппировок[ИмяКолонки];
				ДобавитьОтбор(СтрокаПравилПоНоменклатуре, ИмяКолонки, ИмяПоля, ОтборКомпоновки, ГруппаНе, Истина);
				
			КонецЕсли;
			
			Если СправочникДетализации = Неопределено Тогда
				Если ПравилоОКВЭД.Ключ = ОКВЭДОрганизации.КодОКВЭД2 И Не ЗначениеЗаполнено(СтрокаНастройки.ОКВЭДНаименование) Тогда
					НаименованиеОКВЭД2 = ОКВЭДОрганизации.НаименованиеОКВЭД2;
				Иначе
					НаименованиеОКВЭД2 = СтрокаНастройки.ОКВЭДНаименование;
				КонецЕсли;
				СправочникДетализации = СправочникДетализации(ПравилоОКВЭД.Ключ, НаименованиеОКВЭД2);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТаблицаНастроекОКВЭД.Количество() = 0 Тогда // Настройка по умолчанию
			СправочникДетализации = СправочникДетализации(ОКВЭДОрганизации.КодОКВЭД2, ОКВЭДОрганизации.НаименованиеОКВЭД2);
		КонецЕсли;
		
		Запись = ТаблицаЗаписей.Добавить();
		Запись.Детализация = СправочникДетализации;
		РезультатНастройки = ЗаполнениеФормСтатистики.СериализоватьРезультатНастройки(НастройкиКомпоновки);
		ЗаполнитьЗначенияСвойств(Запись, РезультатНастройки);
		
	КонецЦикла;
		
	РегистрыСведений.НастройкаЗаполненияСвободныхСтрокФормСтатистики.ЗаполнитьНастройкиПоОбъектамНаблюдения(
		ТаблицаЗаписей, ДополнительныеПараметры.Организация, ОбъектНаблюдения);
	
КонецПроцедуры

// Получить настройки ОКВЭД.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации.
//  Период  - Дата.
// 
// Возвращаемое значение:
//  Неопределено, Произвольный - Получить настройки ОКВЭД
Функция ПолучитьНастройкиОКВЭД(Организация, Период) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", КонецДня(Период));
	Запрос.Текст = "ВЫБРАТЬ
	               |	НастройкиКлассификацииВыручкиСрезПоследних.Настройка КАК Настройка
	               |ИЗ
	               |	РегистрСведений.НастройкиКлассификацииВыручкиПоОКВЭД.СрезПоследних(&Период, Организация = &Организация) КАК НастройкиКлассификацииВыручкиСрезПоследних";
	
	ПравилаОКВЭД = Запрос.Выполнить().Выбрать();
	
	Настройка = Неопределено;
	
	Если ПравилаОКВЭД.Следующий() Тогда
		Настройка = ПолучитьНастройки(ПравилаОКВЭД.Настройка);
	КонецЕсли;
	
	Возврат Настройка;
		
КонецФункции

// Добавить настройки по организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации.
//  Период - Дата.
//  НастройкаСтрокой - Строка.
//
Процедура ДобавитьНастройкиПоОрганизации(Организация, Период, НастройкаСтрокой) Экспорт
	
	ДобавитьПравилаЗаполненияОКВЭД(Организация, Период, НастройкаСтрокой);
	НастройкаЗаполненияСвободныхСтрокФормСтатистики(Организация, Период, НастройкаСтрокой);
	
КонецПроцедуры

// Добавить правила заполнения ОКВЭД.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация
//  Период - Дата - Период
//  НастройкаСтрокой - Строка - Настройка строкой
//  ПредставлениеНастройки - Строка - Представление настройки
Процедура ДобавитьПравилаЗаполненияОКВЭД(Организация, Период, НастройкаСтрокой, ПредставлениеНастройки = "Значение по умолчанию") Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиКлассификацииВыручкиПоОКВЭД");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.УстановитьЗначение("Период", Период);
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.НастройкиКлассификацииВыручкиПоОКВЭД.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(НачалоДня(Период));
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.Период       = НачалоДня(Период);
		НоваяЗапись.Организация  = Организация;
		НоваяЗапись.Настройка    = НастройкаСтрокой;
		НоваяЗапись.ПредставлениеНастройки    = ПредставлениеНастройки;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Организация);
		
	КонецПопытки;
	
КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОтбор(СтрокаНастройки, ИмяКолонки, ИмяПоля, ОтборКомпоновки, Группа, ИзменитьВидСравнения)
	
	ИмяОтбораКомпоновки = ИмяПоля;
	
	Если ИмяПоля = "ГруппаФинансовогоУчета" Тогда
		ИмяОтбораКомпоновки = "ВидДеятельности";
	ИначеЕсли СтрНайти(ИмяПоля, "ГруппаФинансовогоУчета") Тогда
		ИмяОтбораКомпоновки = СтрЗаменить(ИмяОтбораКомпоновки, "ГруппаФинансовогоУчета", "ВидДеятельности.");
	КонецЕсли;
	
	Если ИмяПоля = "Подразделение" Тогда
		ИмяОтбораКомпоновки = "ОбособленноеПодразделение";
	ИначеЕсли СтрНайти(ИмяПоля, "Подразделение") Тогда
		ИмяОтбораКомпоновки = СтрЗаменить(ИмяОтбораКомпоновки, "Подразделение", "ОбособленноеПодразделение.");
	КонецЕсли;
	
	Если СтрНайти(ИмяПоля, "НаправлениеДеятельности") и ИмяПоля <> "НаправлениеДеятельности" Тогда
		ИмяОтбораКомпоновки = СтрЗаменить(ИмяОтбораКомпоновки, "НаправлениеДеятельности", "НаправлениеДеятельности.");
	КонецЕсли;
	
	Если СтрНайти(ИмяПоля, "Номенклатура") и ИмяПоля <> "Номенклатура" Тогда
		ИмяОтбораКомпоновки = СтрЗаменить(ИмяОтбораКомпоновки, "Номенклатура", "Номенклатура.");
	КонецЕсли;
	
	УстановитьОтбор(СтрокаНастройки[ИмяКолонки], ИмяПоля, ИмяОтбораКомпоновки, ОтборКомпоновки,
					Группа , ИзменитьВидСравнения);
	
КонецПроцедуры

Процедура УстановитьОтбор(ЗначениеОтбора, ИмяПоляГруппировки, ИмяОтбораКомпоновки, ОтборКомпоновки, ГруппаИли,
	ИзменитьВидСравнения = Ложь)
	
	ПравоеЗначение = ЗначениеОтбора;
	ВидСравненияКомпоновки = ?(ИзменитьВидСравнения, ВидСравненияКомпоновкиДанных.НеРавно, ВидСравненияКомпоновкиДанных.Равно);
	Если ТипЗнч(ЗначениеОтбора) = Тип("СписокЗначений") Тогда
		Если ЗначениеОтбора.Количество() > 1 Тогда
			ВидСравненияКомпоновки = ?(ИзменитьВидСравнения, ВидСравненияКомпоновкиДанных.НеВСписке, ВидСравненияКомпоновкиДанных.ВСписке);
		ИначеЕсли ЗначениеОтбора.Количество() = 1 Тогда
			ЗначениеОтбора = ЗначениеОтбора[0].Значение;
		КонецЕсли;
	КонецЕсли;
	
	Если ГруппаИли <> Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаИли, ИмяОтбораКомпоновки, ВидСравненияКомпоновки, ПравоеЗначение,, Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			ОтборКомпоновки, ИмяОтбораКомпоновки, ПравоеЗначение, ВидСравненияКомпоновки,,Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьНастройки(Настройка)
	
	Если ПустаяСтрока(Настройка) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Попытка
		НастройкаПравил = ОбщегоНазначения.ЗначениеИзСтрокиXML(Настройка);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат НастройкаПравил;
	
КонецФункции

Функция ИнициализироватьТаблицуЗаписей()
	
	ТаблицаНастроек = Новый ТаблицаЗначений();
	ТаблицаНастроек.Колонки.Добавить("Детализация");
	ТаблицаНастроек.Колонки.Добавить("Настройка");
	ТаблицаНастроек.Колонки.Добавить("ПредставлениеНастройки");
	
	Возврат ТаблицаНастроек;
	
КонецФункции

Функция ОтборНастроек(СтрокаПриоритета, Приоритет, ЭтоИсключение)
	
	Отбор = Новый Структура();
	
	НомерПоля = 1;
	
	Пока НомерПоля < Приоритет Цикл
		ИмяПоля = "Поле"+НомерПоля;
		Отбор.Вставить(ИмяПоля, СтрокаПриоритета[ИмяПоля]);
		НомерПоля = НомерПоля + 1;
	КонецЦикла;
	
	Отбор.Вставить("Приоритет", Приоритет);
	Отбор.Вставить("ЭтоИсключение", ЭтоИсключение);
	
	Возврат Отбор;
	
КонецФункции

// Установка ОКВЭД организации в качестве значения по умолчанию
Процедура НастройкаЗаполненияСвободныхСтрокФормСтатистики(Организация, Период, НастройкаСтрокой)
	
	Настройка = ПолучитьНастройки(НастройкаСтрокой);
	
	Если Настройка = Неопределено Тогда
		Возврат ;
	КонецЕсли;
	
	ОКВЭДОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Организация, "КодОКВЭД2, НаименованиеОКВЭД2");
	Если ЗначениеЗаполнено(ОКВЭДОрганизации.КодОКВЭД2) Тогда
			
		ТаблицаЗаписей = ИнициализироватьТаблицуЗаписей();
		Запись = ТаблицаЗаписей.Добавить();
		СправочникДетализации = СправочникДетализации(ОКВЭДОрганизации.КодОКВЭД2, ОКВЭДОрганизации.НаименованиеОКВЭД2);
		Запись.Детализация = СправочникДетализации;
		РегистрыСведений.НастройкаЗаполненияСвободныхСтрокФормСтатистики.ЗаполнитьНастройкиПоОбъектамНаблюдения(
			ТаблицаЗаписей, Организация);
			
	КонецЕсли;
	
КонецПроцедуры

Функция ОКВЭДНоменклатурыУслуг(ДополнительныеПараметры)
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	                |	ХозрасчетныйОбороты.Субконто1   КАК Номенклатура,
	                |	Номенклатура.КодОКВЭД2          КАК ОКВЭД,
	                |	ХозрасчетныйОбороты.СуммаОборот КАК СуммаОборот
	                |ИЗ
	                |	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаВыручки), &СубконтоНоменклатура, Организация = &Организация, , ) КАК ХозрасчетныйОбороты
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	                |		ПО ХозрасчетныйОбороты.Субконто1 = Номенклатура.Ссылка
	                |ГДЕ
	                |	Номенклатура.ТипНоменклатуры В (&ТипыНоменклатурыУслуг)
	                |	И Номенклатура.КодОКВЭД2 <> """"
	                |ИТОГИ ПО
	                |	ОКВЭД";
	СчетаВыручки = БухгалтерскийУчетПовтИсп.СчетаВИерархии(ПланыСчетов.Хозрасчетный.Выручка);
	Запрос.УстановитьПараметр("СчетаВыручки",СчетаВыручки);
	
	ТипыНоменклатуры = Новый СписокЗначений();
	ТипыНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
	ТипыНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Работа);
	
	Запрос.УстановитьПараметр("ТипыНоменклатурыУслуг", ТипыНоменклатуры);
	Запрос.УстановитьПараметр("НачалоПериода",         ДополнительныеПараметры.ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода",          ДополнительныеПараметры.ДатаОкончания);
	Запрос.УстановитьПараметр("Организация",           ДополнительныеПараметры.Организация);
	Запрос.УстановитьПараметр("СубконтоНоменклатура",  ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
	
	ВыборкаКодОКВЭД = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	ПравилаУстановкиОКВЭД = Новый Соответствие();
	СписокКодовОКВЭД = Новый СписокЗначений();
	
	Пока ВыборкаКодОКВЭД.Следующий() Цикл
		
		ВыборкаНоменклатура = ВыборкаКодОКВЭД.Выбрать();
		СписокНоменклатуры = Новый СписокЗначений();
		Пока ВыборкаНоменклатура.Следующий() Цикл
			СписокНоменклатуры.Добавить(ВыборкаНоменклатура.Номенклатура);
		КонецЦикла;
		
		ПравилаУстановкиОКВЭД.Вставить(ВыборкаКодОКВЭД.ОКВЭД, СписокНоменклатуры);
		СписокКодовОКВЭД.Добавить(ВыборкаКодОКВЭД.ОКВЭД);
		
	КонецЦикла;
	
	НаименованияОКВЭД = РегистрыСведений.ДанныеОКВЭД.ПоискПоКодам(СписокКодовОКВЭД).Выгрузить();
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("НаименованияОКВЭД",     НаименованияОКВЭД);
	СтруктураДанных.Вставить("ПравилаУстановкиОКВЭД", ПравилаУстановкиОКВЭД);
	
	Возврат СтруктураДанных;
	
КонецФункции

Функция СправочникДетализации(ОКВЭД, ОКВЭДНаименование)
	
	СправочникДетализации = Справочники.КлассификаторВидовЭкономическойДеятельности.НайтиПоКоду(ОКВЭД);
	Если Не ЗначениеЗаполнено(СправочникДетализации) Тогда
		СправочникДетализации = Справочники.КлассификаторВидовЭкономическойДеятельности.СоздатьЭлемент();
		СправочникДетализации.Код = ОКВЭД;
		СправочникДетализации.Наименование = ОКВЭДНаименование;
		СправочникДетализации.НаименованиеПолное = ОКВЭДНаименование;
		СправочникДетализации.Записать();
	КонецЕсли;
	
	Возврат СправочникДетализации.Ссылка;
	
КонецФункции

Процедура ДобавитьСтрокуИсключения(Правила, СтрокаОсновногоПравила, ОКВЭДИсключения, ИмяКолонки)
	
	СтрокаИсключения = Правила.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаИсключения, СтрокаОсновногоПравила, , ИмяКолонки);
	Если ТипЗнч(СтрокаОсновногоПравила[ИмяКолонки]) = Тип("СписокЗначений") Тогда
		СтрокаИсключения[ИмяКолонки] = СтрокаОсновногоПравила[ИмяКолонки].Скопировать();
	Иначе
		СтрокаИсключения[ИмяКолонки] = СтрокаОсновногоПравила[ИмяКолонки];
	КонецЕсли;
	СтрокаИсключения.ОКВЭД =ОКВЭДИсключения.ОКВЭД;
	СтрокаИсключения.ОКВЭДНаименование = ОКВЭДИсключения.ОКВЭДНаименование;
	СтрокаИсключения.ЭтоИсключение = Истина;
	
КонецПроцедуры

Функция ИнициализироватьТаблицу(СтараяТаблица)
	
	НоваяТаблица = Новый ТаблицаЗначений();
	Для Каждого Колонка Из СтараяТаблица.Колонки Цикл
		НоваяТаблица.Колонки.Добавить(Колонка.Имя);
	КонецЦикла;
	
	Для Каждого СтараяСтрока Из СтараяТаблица Цикл
		НоваяСтрока = НоваяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтараяСтрока);
	КонецЦикла;
	
	Возврат НоваяТаблица;
	
КонецФункции

#КонецОбласти

#КонецЕсли