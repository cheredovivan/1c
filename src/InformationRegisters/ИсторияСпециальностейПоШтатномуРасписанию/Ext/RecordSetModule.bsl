#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИзменений = РегистрыБЗК.ИзменениеЗаписейНабора(РегистрыБЗК.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение), ЭтотОбъект, Замещение, Истина);
	МассивОбновляемыхПозиций = ТаблицаИзменений.ВыгрузитьКолонку("ПозицияШтатногоРасписания");
	Если ЗначениеЗаполнено(МассивОбновляемыхПозиций) Тогда
		ДополнительныеСвойства.Вставить("МассивОбновляемыхПозиций", МассивОбновляемыхПозиций);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("МассивОбновляемыхПозиций")
		Или ДополнительныеСвойства.МассивОбновляемыхПозиций.Количество() = 0 Тогда
		
		Возврат;
	КонецЕсли;
	
	ТаблицаПозицийРегистра = Новый ТаблицаЗначений;
	ТаблицаПозицийРегистра.Колонки.Добавить("ПозицияШтатногоРасписания", Новый ОписаниеТипов("СправочникСсылка.ШтатноеРасписание"));
	ТаблицаПозицийРегистра.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	
	ОтборыЗаписейНабора = РегистрыБЗК.ОтборыЗаписейНабора(ЭтотОбъект, Замещение);
	Если Не РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		ДатыПозиций = Новый Соответствие;
		РегистраторыУтверждения = Новый Массив;
		РегистраторыИзменения = Новый Массив;
		РеквизитыУтверждений = Новый Соответствие;
		РеквизитыИзменений = Новый Соответствие;
		Для Каждого ОтборЗаписейНабора Из ОтборыЗаписейНабора Цикл
			Если ОтборЗаписейНабора["Регистратор_Использование"] Тогда
				Если ТипЗнч(ОтборЗаписейНабора["Регистратор"]) = Тип("ДокументСсылка.УтверждениеШтатногоРасписания") Тогда
					РегистраторыУтверждения.Добавить(ОтборЗаписейНабора["Регистратор"]);
				ИначеЕсли ТипЗнч(ОтборЗаписейНабора["Регистратор"]) = Тип("ДокументСсылка.ИзменениеШтатногоРасписания")Тогда
					РегистраторыИзменения.Добавить(ОтборЗаписейНабора["Регистратор"]);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ЗначениеЗаполнено(РегистраторыУтверждения) Тогда
			РеквизитыУтверждений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(РегистраторыУтверждения, "МесяцВступленияВСилу");
		КонецЕсли;
		Если ЗначениеЗаполнено(РегистраторыИзменения) Тогда
			РеквизитыИзменений = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(РегистраторыИзменения, "ДатаВступленияВСилу");
		КонецЕсли;
		Для Каждого Запись Из ЭтотОбъект Цикл
			ДатаУтверждения = РеквизитыУтверждений.Получить(Запись.Регистратор);
			ДатаИзменения = РеквизитыИзменений.Получить(Запись.Регистратор);
			Если ЗначениеЗаполнено(ДатаИзменения) И Не ЗначениеЗаполнено(ДатаУтверждения) Тогда
				ДатаПозиции = ДатаИзменения;
			ИначеЕсли Не ЗначениеЗаполнено(ДатаИзменения) И ЗначениеЗаполнено(ДатаУтверждения) Тогда
				ДатаПозиции = ДатаУтверждения;
			ИначеЕсли ЗначениеЗаполнено(ДатаИзменения) И ЗначениеЗаполнено(ДатаУтверждения) Тогда
				ДатаПозиции = Макс(ДатаИзменения, ДатаУтверждения);
			Иначе
				Продолжить;
			КонецЕсли;
			ИзвестнаяДата = ДатыПозиций.Получить(Запись.ПозицияШтатногоРасписания);
			Если ИзвестнаяДата = Неопределено Или ИзвестнаяДата < ДатаПозиции Тогда
				ДатыПозиций.Вставить(Запись.ПозицияШтатногоРасписания, ДатаПозиции);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("ПозицииШтатногоРасписания", ДополнительныеСвойства.МассивОбновляемыхПозиций);
	Если ЗначениеЗаполнено(ТаблицаПозицийРегистра) Тогда
		Запрос.УстановитьПараметр("ТаблицаПозицийРегистра", ТаблицаПозицийРегистра);
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ТаблицаПозицийРегистра.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
			|	ТаблицаПозицийРегистра.Дата КАК Дата
			|ПОМЕСТИТЬ ВТТаблицаПозицийРегистра
			|ИЗ
			|	&ТаблицаПозицийРегистра КАК ТаблицаПозицийРегистра
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ТаблицаПозицийРегистра.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
			|	ТаблицаПозицийРегистра.Дата КАК Дата
			|ПОМЕСТИТЬ ВТОбщийНабор
			|ИЗ
			|	ВТТаблицаПозицийРегистра КАК ТаблицаПозицийРегистра
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ШтатноеРасписание.Ссылка,
			|	ЕСТЬNULL(МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата), ДАТАВРЕМЯ(1, 1, 1))
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|		ПО ШтатноеРасписание.Ссылка = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
			|ГДЕ
			|	ИсторияИспользованияШтатногоРасписания.Используется
			|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания В(&ПозицииШтатногоРасписания)
			|
			|СГРУППИРОВАТЬ ПО
			|	ШтатноеРасписание.Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ОбщийНабор.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
			|	МАКСИМУМ(ОбщийНабор.Дата) КАК Дата
			|ПОМЕСТИТЬ ВТПоследниеДатыИспользованияПозиций
			|ИЗ
			|	ВТОбщийНабор КАК ОбщийНабор
			|
			|СГРУППИРОВАТЬ ПО
			|	ОбщийНабор.ПозицияШтатногоРасписания";
	Иначе
		Запрос.Текст =
			"ВЫБРАТЬ
			|	ШтатноеРасписание.Ссылка КАК ПозицияШтатногоРасписания,
			|	ЕСТЬNULL(МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата), ДАТАВРЕМЯ(1, 1, 1)) КАК Дата
			|ПОМЕСТИТЬ ВТПоследниеДатыИспользованияПозиций
			|ИЗ
			|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
			|		ПО ШтатноеРасписание.Ссылка = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
			|ГДЕ
			|	ИсторияИспользованияШтатногоРасписания.Используется
			|	И ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания В(&ПозицииШтатногоРасписания)
			|
			|СГРУППИРОВАТЬ ПО
			|	ШтатноеРасписание.Ссылка";
	КонецЕсли;
	Запрос.Выполнить();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания КАК ПозицияШтатногоРасписания,
		|	ИсторияСпециальностейПоШтатномуРасписанию.Специальность КАК Специальность
		|ИЗ
		|	ВТПоследниеДатыИспользованияПозиций КАК ПоследниеДатыИспользованияПозиций
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияСпециальностейПоШтатномуРасписанию КАК ИсторияСпециальностейПоШтатномуРасписанию
		|		ПО ПоследниеДатыИспользованияПозиций.ПозицияШтатногоРасписания = ИсторияСпециальностейПоШтатномуРасписанию.ПозицияШтатногоРасписания
		|			И ПоследниеДатыИспользованияПозиций.Дата = ИсторияСпециальностейПоШтатномуРасписанию.Дата
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПозицияШтатногоРасписания,
		|	Специальность";
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПозицияШтатногоРасписания") Цикл
		ПозицияОбъект = Выборка.ПозицияШтатногоРасписания.ПолучитьОбъект();
		ПозицияОбъект.Специальности.Очистить();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(ПозицияОбъект.Специальности.Добавить(), Выборка);
		КонецЦикла;
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеСтруктурыШтатногоРасписания(ПозицияОбъект);
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиИспользованияСтраховыхВзносовПоКлассамУсловийТруда(ПозицияОбъект);
		УправлениеШтатнымРасписанием.ОтключитьОбновлениеНастройкиПубликации(ПозицияОбъект);
		ПозицияОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли