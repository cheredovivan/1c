#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Временное хранилище
// - следует использовать для добавления заданий в обычных случаях

// Добавляет задания к расчету структуры заказа на производство
//
// Параметры:
//  Таблица	 - ТаблицаЗначений	 - задания к расчету.
//
Процедура ДобавитьЗадания(Таблица) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Очередь = Константы.ГраницаРасчетаСтруктурыЗаказов.ТекущаяОчередь();
	
	КолонкиПоЗначению = Новый Структура();
	КолонкиПоЗначению.Вставить("ДатаЗаписи", УниверсальноеВремя(ТекущаяДатаСеанса()));
	
	НаборЗаписей = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Таблица);
	
	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, КолонкиПоЗначению); 
		Запись.Очередь = ?(Запись.Очередь > 0, 
			Запись.Очередь, Очередь);
		Запись.Разделитель = Новый УникальныйИдентификатор();
	КонецЦикла;

	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Итоговая очередь заданий к расчету
// - может использоваться для добавления заданий в процессе расчета

// Добавляет задания к расчету структуры заказа на производство в следующую очередь (только для добавления заданий в процессе расчета)
//
// Параметры:
//  Таблица		 - ТаблицаЗначений	 - задания к расчету.
//  НомерОчереди - Число			 - номер очереди.
//
Процедура ДобавитьЗаданияВСледующуюОчередь(Таблица, НомерОчереди) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	КолонкиПоЗначению = Новый Структура();
	КолонкиПоЗначению.Вставить("Очередь", НомерОчереди + 1);
	
	НаборЗаписей = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(Таблица);
	
	Для каждого Запись Из НаборЗаписей Цикл
		ЗаполнитьЗначенияСвойств(Запись, КолонкиПоЗначению);
		Запись.Разделитель = Новый УникальныйИдентификатор();
	КонецЦикла;

	НаборЗаписей.Записать(Ложь);
КонецПроцедуры

// Добавляет задания к расчету структуры заказа на производство в следующую очередь (только для добавления заданий в процессе расчета)
//
// Параметры:
//  Номенклатура	 - СправочникСсылка.Номенклатура			 - номенклатура
//  Характеристика	 - СправочникСсылка.ХарактеристикиНоменклатуры	 - характеристика номенклатуры
//  Склад			 - СправочникСсылка.Склады						 - склад
//  Назначение		 - СправочникСсылка.Назначения					 - назначение
//  НомерОчереди	 - Число										 - номер очереди.
//
Процедура ДобавитьЗаданиеВСледующуюОчередь(Номенклатура, Характеристика, Склад, Назначение, НомерОчереди) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	НаборЗаписей = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
	
	НоваяЗапись = НаборЗаписей.Добавить();
	
	НоваяЗапись.Очередь = НомерОчереди + 1;
	
	НоваяЗапись.Номенклатура   = Номенклатура;
	НоваяЗапись.Характеристика = Характеристика;
	НоваяЗапись.Склад          = Склад;
	НоваяЗапись.Назначение     = Назначение;
	
	НоваяЗапись.Разделитель = Новый УникальныйИдентификатор();
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выбирает изменения из очереди по границу расчета (включая)
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
// 
// Возвращаемое значение:
//  Число - номер очереди
//
Функция ВыбратьИзменения(ГраницаРасчета) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Очередь             КАК Очередь,
		|	Таблица.Разделитель         КАК Разделитель,
		|	Таблица.Номенклатура        КАК Номенклатура,
		|	Таблица.Характеристика      КАК Характеристика,
		|	Таблица.Склад               КАК Склад,
		|	Таблица.Назначение          КАК Назначение,
		|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство
		|ИЗ
		|	РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Таблица
		|ГДЕ
		|	Таблица.Очередь <= &ГраницаРасчета
		|	И НЕ(Таблица.ДатаЗаписи > &ГраницаОжиданияСвязанныхДанных
		|					И (ИСТИНА В
		|							(ВЫБРАТЬ ПЕРВЫЕ 1
		|								ИСТИНА
		|							ИЗ
		|								РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ЗаданияКРаспределениюЗапасов
		|							ГДЕ
		|								ЗаданияКРаспределениюЗапасов.Номенклатура = Таблица.Номенклатура
		|								И ЗаданияКРаспределениюЗапасов.Характеристика = Таблица.Характеристика)
		|						ИЛИ Таблица.НоменклатураПродукции <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|							И ИСТИНА В
		|								(ВЫБРАТЬ ПЕРВЫЕ 1
		|									ИСТИНА
		|								ИЗ
		|									РегистрСведений.ЗаданияКРаспределениюЗапасов КАК ЗаданияКРаспределениюЗапасов
		|								ГДЕ
		|									ЗаданияКРаспределениюЗапасов.Номенклатура = Таблица.НоменклатураПродукции))
		|				ИЛИ НЕ Таблица.НоменклатураПродукции В (ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка), Таблица.Номенклатура)
		|					И ИСТИНА В
		|						(ВЫБРАТЬ ПЕРВЫЕ 1
		|							ИСТИНА
		|						ИЗ
		|							РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК БуферЗаданийПродукция
		|						ГДЕ
		|							БуферЗаданийПродукция.Очередь <= &ГраницаРасчета
		|							И БуферЗаданийПродукция.Номенклатура = Таблица.НоменклатураПродукции))
		|";
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	Запрос.УстановитьПараметр("ГраницаОжиданияСвязанныхДанных", УниверсальноеВремя(ТекущаяДатаСеанса()) - 500);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов");
		ЭлементБлокировки.УстановитьЗначение("Очередь", Новый Диапазон(, ГраницаРасчета));
		Блокировка.Заблокировать();
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Буфер = РезультатЗапроса.Выгрузить();
			
			Набор = РегистрыСведений.БуферЗаданийКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
			Набор.Загрузить(Буфер);
			Набор.Записать(РежимЗамещения.Удаление);
			
			Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
			Набор.Загрузить(Буфер);
			Набор.Записать(Ложь);
		
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(СтруктураЗаказа.ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Таблица
		|ГДЕ
		|	Таблица.Очередь <= &ГраницаРасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди;
	
КонецФункции

// Удаляет задания по номеру очереди
// 
// Параметры:
// 	Очередь - Число - номер очереди
Процедура УдалитьЗадания(Очередь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов.СоздатьНаборЗаписей();
	Набор.Отбор.Очередь.Установить(Очередь);
	Набор.Записать(Истина);
	
КонецПроцедуры

// Проверяет, есть ли задания к расчету
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
//  НомерОчереди	 - Число - минимальный номер очереди (возвращаемое значение)
// 
// Возвращаемое значение:
//  Булево - есть задания к расчету
//
Функция ЕстьЗаданияКРасчету(ГраницаРасчета = -1, НомерОчереди = -1) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов КАК Таблица
		|ГДЕ
		|	(&ГраницаРасчета = -1
		|			ИЛИ Таблица.Очередь <= &ГраницаРасчета)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди >= 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолноеИмя() Экспорт

	ПолноеИмя = Метаданные.РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаРаспределениеЗапасов.ПолноеИмя();

	Возврат ПолноеИмя;

КонецФункции

#КонецОбласти

#КонецЕсли