
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Приложение = Параметры.Приложение;
	Если Не ЗначениеЗаполнено(Приложение) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Приложение", Приложение);
	ЗарплатаКадры.ПриСозданииНаСервереФормыСДинамическимСписком(ЭтотОбъект, "Список",,,,"Приложение");
	
	Если Приложение = Перечисления.ПриложенияДляИнтеграции.КабинетСотрудника Тогда
		ТекстЗаголовка = НСтр("ru = '1С:Кабинет сотрудника';
								|en = '1C:Employee Account'");
		ТекстОтветственный = НСтр("ru = 'Ответственный за работу с сервисом';
									|en = 'Person responsible for the service'");
	Иначе
		ТекстЗаголовка = НСтр("ru = '1С:Приложение';
								|en = '1C:Application'");
		ТекстОтветственный = НСтр("ru = 'Ответственный за работу с приложением';
									|en = 'Person responsible for the application'");
	КонецЕсли;
	ФорматнаяСтрока = СтрШаблон("БЛ=; БИ='%1'",ТекстОтветственный);
	Элементы.ЭтоОтветственный.Формат = ФорматнаяСтрока;
	Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиТаблицыСписок

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
		
	Если Элемент.Текущиеданные.ЭтоОтветственный Тогда
		Если Приложение = ПредопределенноеЗначение("Перечисление.ПриложенияДляИнтеграции.КабинетСотрудника") Тогда
			ТекстПредупреждения = СтрШаблон(
				НСтр("ru = '%1 назначен ответственным за работу с сервисом 1С:Кабинет сотрудника, удаление недоступно.';
					|en = '%1 is assigned as a person responsible for 1C:Employee account and cannot be deleted.'"),
				Элемент.Текущиеданные.ФизическоеЛицо);
		Иначе
			ТекстПредупреждения = СтрШаблон(
				НСтр("ru = '%1 назначен ответственным за работу с приложением 1С:Персонал, удаление недоступно.';
					|en = '%1 is assigned as a person responsible for 1C:Employee account and cannot be deleted.'"),
				Элемент.Текущиеданные.ФизическоеЛицо);
		КонецЕсли;
		ПоказатьПредупреждение(,ТекстПредупреждения);
	Иначе
		ТекстВопроса = СтрШаблон(
				НСтр("ru = 'Удалить сотрудника %1 из списка исключений?';
					|en = 'Do you want to delete the employee %1 from the exception list?'"),
				Элемент.Текущиеданные.ФизическоеЛицо);
		ДополнительныеПараметры = Новый Структура("ФизическоеЛицо", Элемент.Текущиеданные.ФизическоеЛицо);
		Оповещение = Новый ОписаниеОповещения("УдалениеСтрокиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РедактированиеЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("Приложение", Приложение);
	ОткрытьФорму("РегистрСведений.ИсключенияИзПравилПубликацииУправлениеПерсоналом.Форма.ФормаРедактирования",
		ПараметрыОткрытия, ЭтаФорма, ЭтаФорма,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("РедактированиеЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("Приложение,ФизическоеЛицо", Приложение, Элемент.ТекущиеДанные.ФизическоеЛицо);
	ОткрытьФорму("РегистрСведений.ИсключенияИзПравилПубликацииУправлениеПерсоналом.Форма.ФормаРедактирования",
		ПараметрыОткрытия, ЭтаФорма, ЭтаФорма,,, ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УдалениеСтрокиЗавершение(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	УдалитьСтроку(ДополнительныеПараметры.ФизическоеЛицо);
	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервере
Процедура УдалитьСтроку(ФизическоеЛицо)

	НачатьТранзакцию();
	Попытка
		
		Запись = РегистрыСведений.ИсключенияИзПравилПубликацииУправлениеПерсоналом.СоздатьМенеджерЗаписи();
		Запись.Приложение 		= Приложение;
		Запись.ФизическоеЛицо 	= ФизическоеЛицо;
		Запись.Удалить();
		
		ИнтеграцияУправлениеПерсоналом.ОбновитьСведенияОПубликацииФизическихЛицИсключений(Приложение, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ФизическоеЛицо));
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура РедактированиеЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Элементы.Список.Обновить();

КонецПроцедуры

#КонецОбласти
