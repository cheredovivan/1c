#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Записывает данные в регистр.
//
// Параметры:
//  Очередь	- ТаблицаЗначений - данные для записи.
//  Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - документ, которому принадлежит очередь.
//
Процедура ЗаписатьОчередь(Очередь, Калькуляция) Экспорт
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
	
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОчередьРасчетаПлановыхКалькуляций");
		ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Блокировка.Заблокировать();
		
		Разделитель = РазделительОчереди(Калькуляция);
		
		Очередь.Колонки.Добавить("Разделитель");
		Очередь.ЗаполнитьЗначения(Разделитель, "Разделитель");
		
		НаборЗаписей = РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
		НаборЗаписей.Отбор.Разделитель.Установить(Разделитель);
		НаборЗаписей.Загрузить(Очередь);
		
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей.Записать();
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		НаборЗаписей = РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.СоздатьНаборЗаписей();
		
		ТекстСообщения = НСтр("ru = 'Не удалось записать очередь расчета плана: %Ссылка% по причине: %Причина%';
								|en = 'Cannot save plan calculation queue: %Ссылка%. Reason: %Причина%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Калькуляция);
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.ПодробноеПредставлениеОшибки(
			ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,
			НаборЗаписей.Метаданные(),
			,
			ТекстСообщения);
		
		ВызватьИсключение;
			
	КонецПопытки;
	
КонецПроцедуры

// Очищает данные регистра по заданным измерениям.
//
// Параметры:
//  Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - документ, которому принадлежит очередь.
//  Очередь	- ТаблицаЗначений - данные измерений для очистки, в случае если необходимо очистить всю очередь можно передать Неопределено.
//
Процедура ОчиститьОчередь(Калькуляция, Очередь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Если Очередь = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Очередь.Калькуляция КАК Калькуляция,
			|	Очередь.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
			|	Очередь.Продукция КАК Продукция,
			|	Очередь.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
			|	Очередь.Назначение КАК Назначение,
			|	Очередь.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
			|	Очередь.КлючПартия КАК КлючПартия,
			|	Очередь.Полуфабрикат КАК Полуфабрикат,
			|	Очередь.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
			|	Очередь.Разделитель КАК Разделитель,
			|	Очередь.ДатаПотребности КАК ДатаПотребности,
			|	Очередь.Порядок КАК Порядок
			|ИЗ
			|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
			|ГДЕ
			|	Очередь.Калькуляция = &Калькуляция";
		
		Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
		Очередь = Запрос.Выполнить().Выгрузить();
	КонецЕсли;
	
	Для каждого Строка Из Очередь Цикл
		
		НаборЗаписей = РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.СоздатьНаборЗаписей();
		
		Для каждого Колонка Из Очередь.Колонки Цикл
			
			Измерение = НаборЗаписей.Отбор[Колонка.Имя]; // ЭлементОтбора
			Измерение.Установить(Строка[Колонка.Имя]);
			
		КонецЦикла;
		
		Попытка
			НаборЗаписей.Записать();
		Исключение
			
			НаборЗаписей = РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.СоздатьНаборЗаписей();
			
			ТекстСообщения = НСтр("ru = 'Не удалось очистить очередь расчета плана: %Ссылка% по причине: %Причина%';
									|en = 'Cannot clear plan calculation queue: %Ссылка%. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Калькуляция);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ОбработкаОшибок.ПодробноеПредставлениеОшибки(
				ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				НаборЗаписей.Метаданные(),
				,
				ТекстСообщения);
			
			ВызватьИсключение;
			
		КонецПопытки;
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#Область Прочее

Функция РазделительОчереди(Калькуляция)
	
	Разделитель = 1;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Калькуляция);
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЕСТЬNULL(МАКСИМУМ(Очередь.Разделитель), 0) КАК Разделитель
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Калькуляция = &Ссылка";
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Разделитель = ВыборкаДетальныеЗаписи.Разделитель + 1;
	КонецЕсли;
	
	Возврат Разделитель;
	
КонецФункции

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Запись очереди расчета калькуляции продукции';
				|en = 'Save product costing calculation queue'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#КонецЕсли
