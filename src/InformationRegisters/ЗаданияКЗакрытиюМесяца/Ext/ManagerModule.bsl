#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает имя константы, хранящей номер задания для данного регистра.
//
// Возвращаемое значение:
//	Строка - Строковое предствление имени константы НомерЗаданияКЗакрытиюМесяца.
//
Функция ИмяКонстантыНомераЗадания() Экспорт
	
	Возврат Метаданные.Константы.НомерЗаданияКЗакрытиюМесяца.Имя;
	
КонецФункции

// Увеличивает номер задания в константе.
//
// Возвращаемое значение:
//	Число - Предыдущий номер задания из константы "Номер задания к закрытию месяца".
//
Функция УвеличитьНомерЗадания() Экспорт
	
	Возврат ЗакрытиеМесяцаСервер.УвеличитьНомерЗадания(ИмяКонстантыНомераЗадания());
	
КонецФункции

// Возвращает номер задания из константы.
//
// Возвращаемое значение:
//	Число - Номер текущего задания из константы "Номер задания к закрытию месяца".
//
Функция ПолучитьНомерЗадания() Экспорт
	
	Возврат ЗакрытиеМесяцаСервер.ТекущийНомерЗадания(ИмяКонстантыНомераЗадания());
	
КонецФункции

// Метод создает запись регистра с заданными параметрами.
//
// Параметры:
//	ПериодЗадания   - Дата - Начало периода, для которого необходимо зарегистрировать задание к расчету
//	ДокументЗадания - ДокументСсылка - документ регистратор создавший движение в зависимых регистрах
//	Организация - СправочникСсылка.Организации - организация, по которой необходим перерасчет
//  Операция - СправочникСсылка.ОперацииЗакрытияМесяца - регламентная операция 
//  НомерЗадания - Число - номер задания; если не задано, то будет установлено значение из соответствующей константы.
//
Процедура СоздатьЗаписьРегистра(ПериодЗадания, ДокументЗадания = Неопределено, Организация = Неопределено,
				Операция = Неопределено, НомерЗадания = Неопределено) Экспорт
				
	УстановитьПривилегированныйРежим(Истина);
	
	Если НЕ ЗначениеЗаполнено(ДокументЗадания) И НЕ ЗначениеЗаполнено(Организация) Тогда
		
		// Создаем задание для каждой организации ИБ
		МассивОрганизаций = Справочники.Организации.ДоступныеОрганизации();
		
	Иначе
		
		МассивОрганизаций = Новый Массив;
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			// Попытаемся определить организацию по умолчанию.
			// Если не удастся, то будет вызвано платформенное исключение при записи.
			МассивОрганизаций.Добавить(Справочники.Организации.ОрганизацияПоУмолчанию());
		Иначе
			МассивОрганизаций.Добавить(Организация);
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Операция) Тогда
		
		// Создадим задание для каждой возможной операции
		МассивОпераций = Новый Массив;
	
		ВыборкаОпераций = Справочники.ОперацииЗакрытияМесяца.Выбрать();

		Пока ВыборкаОпераций.Следующий() Цикл
			МассивОпераций.Добавить(ВыборкаОпераций.Ссылка);
		КонецЦикла;
		
	Иначе
		
		МассивОпераций = ОбщегоНазначенияУТКлиентСервер.Массив(Операция)
		
	КонецЕсли;
	
	// Запишем задания.
	НачатьТранзакцию();
	
	Попытка
		
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ПолучитьНомерЗадания();
		КонецЕсли;
		
		Для Каждого ТекущаяОрганизация Из МассивОрганизаций Цикл
			
			Для Каждого ТекущаяОперация Из МассивОпераций Цикл
				Месяц = ЗакрытиеМесяцаСервер.ДатаЗаданияНаТекущийПериод(ТекущаяОперация, ТекущаяОрганизация, ДокументЗадания, ПериодЗадания);
				
				НаборЗаписей = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьМенеджерЗаписи();
				НаборЗаписей.Месяц        = Месяц;
				НаборЗаписей.Операция     = ТекущаяОперация;
				НаборЗаписей.Организация  = ТекущаяОрганизация;
				НаборЗаписей.Документ     = ДокументЗадания;
				НаборЗаписей.НомерЗадания = НомерЗадания;
				НаборЗаписей.Записать(Истина);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	
	Исключение
		ОтменитьТранзакцию();
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВызватьИсключение ТекстОшибки;
	КонецПопытки;
	
КонецПроцедуры

// Метод создает записи регистра с параметрами, полученными запросом.
//
// Параметры:
//	Выборка - ВыборкаИзРезультатаЗапроса - выборка, содержащая данные для формирования записей.
//  НомерЗадания - Число - номер задания; если не задано, то будет установлено значение из соответствующей константы.
//
Процедура СоздатьЗаписиРегистраПоДаннымВыборки(Выборка, НомерЗадания = Неопределено) Экспорт
	
	Если ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда
		// В РИБ данный регистр обрабатывается только в главном узле.
		Возврат;
	КонецЕсли;
	
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		СтруктураПолей = Новый Структура("Месяц, Организация, Операция, Документ");
		
		Если НомерЗадания = Неопределено Тогда
			НомерЗадания = ПолучитьНомерЗадания();
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			ЗаполнитьЗначенияСвойств(СтруктураПолей, Выборка);
			
			СоздатьЗаписьРегистра(СтруктураПолей.Месяц, СтруктураПолей.Документ, СтруктураПолей.Организация, СтруктураПолей.Операция, НомерЗадания);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки    = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось сформировать задание к закрытию месяца за %1 в организации %2 по причине: %3';
				|en = 'Cannot generate the job for month-end closing for %1 in company %2. Reason: %3'"),
			Выборка.Месяц,
			Выборка.Организация,
			ТекстОшибки);
			
		ЗаписьЖурналаРегистрации(НСтр("ru = 'Закрытие месяца';
										|en = 'Month-end closing'", ОбщегоНазначения.КодОсновногоЯзыка()), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		
	КонецПопытки;
	
КонецПроцедуры

// Метод очищает все задания по указанной операции.
//
// Параметры:
//	Операция - СправочникСсылка.ОперацииЗакрытияМесяца - операция для очистки заданий
//
Процедура УдалитьЗаписиПоОперации(Операция) Экспорт
	
	НаборЗаписей = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Операция.Установить(Операция);
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

// В РИБ закрытие месяца выполняется только в главном узле, при этом из подчиненных узлов
// записи регистра отправляются в главный узел. При получении в ГУ записей от ПУ,
// они регистрируются с текущим номером задания ГУ. Когда в ПУ приходит квитанция о получении
// данных в ГУ, в ПУ записи регистра снимаются с регистрации к обмену и удаляются, т.к. штатными
// средствами (после закрытия месяца, либо при получении удаления от ГУ) в ПУ они не могут быть удалены.
//
Процедура УдалитьЗаписиВПодчиненномУзлеРИБ() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаданияКЗакрытиюМесяца.Месяц КАК Месяц,
		|	ЗаданияКЗакрытиюМесяца.Операция КАК Операция,
		|	ЗаданияКЗакрытиюМесяца.Организация КАК Организация,
		|	ЗаданияКЗакрытиюМесяца.Документ КАК Документ,
		|	ЗаданияКЗакрытиюМесяца.НомерЗадания КАК НомерЗадания
		|ИЗ
		|	РегистрСведений.ЗаданияКЗакрытиюМесяца КАК ЗаданияКЗакрытиюМесяца
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКЗакрытиюМесяца.Изменения КАК ЗаданияКЗакрытиюМесяцаИзменения
		|		ПО ЗаданияКЗакрытиюМесяца.Месяц = ЗаданияКЗакрытиюМесяцаИзменения.Месяц
		|			И ЗаданияКЗакрытиюМесяца.Организация = ЗаданияКЗакрытиюМесяцаИзменения.Организация
		|			И ЗаданияКЗакрытиюМесяца.Операция = ЗаданияКЗакрытиюМесяцаИзменения.Операция
		|			И ЗаданияКЗакрытиюМесяца.Документ = ЗаданияКЗакрытиюМесяцаИзменения.Документ
		|			И ЗаданияКЗакрытиюМесяца.НомерЗадания = ЗаданияКЗакрытиюМесяцаИзменения.НомерЗадания
		|ГДЕ
		|	ЗаданияКЗакрытиюМесяцаИзменения.Месяц ЕСТЬ NULL";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();
	Для Сч = 1-ТаблицаЗаписей.Количество() По 0 Цикл
		Выборка = ТаблицаЗаписей[-Сч];
		НаборЗаписей = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);
		НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
		НаборЗаписей.Отбор.Операция.Установить(Выборка.Операция);
		НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
		НаборЗаписей.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
		НаборЗаписей.ДополнительныеСвойства.Вставить("ОтключитьМеханизмРегистрацииОбъектов");
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Формирует задания на следующий период.
//
// Параметры:
//  ВременныеТаблицы	- МенеджерВременныхТаблиц - Содержит таблицу КэшГраниц.
//  СледующийМесяц		- Дата - Дата, начиная с которой нужно сформировать задания.
//
Процедура СформироватьЗаданияНаСледующийПериод(ВременныеТаблицы, СледующийМесяц) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Задания.Операция,
	|	Задания.Организация,
	|	МАКСИМУМ(Задания.НомерЗадания) КАК НомерЗадания
	|ИЗ
	|	КэшГраниц КАК Задания
	|
	|СГРУППИРОВАТЬ ПО
	|	Задания.Операция,
	|	Задания.Организация";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.МенеджерВременныхТаблиц = ВременныеТаблицы;
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СледующийПериод = ЗакрытиеМесяцаСервер.ДатаЗаданияНаСледующийПериод(Выборка.Операция, Выборка.Организация, Неопределено, СледующийМесяц);
		
		Если ЗначениеЗаполнено(СледующийПериод) Тогда
			НоваяЗапись = РегистрыСведений.ЗаданияКЗакрытиюМесяца.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, Выборка);
			НоваяЗапись.Месяц = СледующийПериод;
			НоваяЗапись.Записать(Истина);
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

// Добавляет описания регистров для их подключения к механизму дат запрета изменения.
//
// Параметры:
//  ИсточникиДанных - ТаблицаЗначений - см. описание в механизме БСП.
//
Процедура ОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных) Экспорт

	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДвиженияДенежныеСредстваДоходыРасходы.ПолноеИмя(),
										"Период",
										"РегламентныеОперации",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДвиженияДоходыРасходыПрочиеАктивыПассивы.ПолноеИмя(),
										"Период",
										"РегламентныеОперации",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДенежныеСредстваБезналичные.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДенежныеСредстваВКассахККМ.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДенежныеСредстваВПути.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДенежныеСредстваНаличные.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ДенежныеСредстваУПодотчетныхЛиц.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ПартииПрочихРасходов.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.РасчетыПоФинансовымИнструментам.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных,
										Метаданные.РегистрыНакопления.ПрочаяВыручка.ПолноеИмя(),
										"Период",
										"ФинансовыйКонтур",
										"Организация");
	ЗакрытиеМесяцаЛокализация.ДополнитьОписаниеРегистровДляКонтроляДатЗапретаИзменения(ИсточникиДанных);
КонецПроцедуры

// Возвращает перечень объектов метаданных, на основании данных которых формируются записи в регистре.
// 
// Параметры:
//  Операция - СправочникСсылка.ОперацииЗакрытияМесяца - Операция
// 
// Возвращаемое значение:
//  Массив из ОбъектМетаданных - Входящие данные механизма
Функция ВходящиеДанныеМеханизма(Операция = Неопределено) Экспорт
	
	ВходящиеДанные = Новый Массив;
	
	Если Операция = Неопределено
	// ++ НЕ УТ
		ИЛИ Операция = Справочники.ОперацииЗакрытияМесяца.ЗакрытиеРасходовОтВыбытияОС
	// -- НЕ УТ
		Тогда
		
		ВходящиеДанные.Добавить(Метаданные.РегистрыНакопления.ПрочиеРасходы);
		ВходящиеДанные.Добавить(Метаданные.Документы.РеализацияУслугПрочихАктивов);
	КонецЕсли;
	
	Возврат ВходящиеДанные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияКЗакрытиюМесяца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.32";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("6fba64e6-493d-4268-8dfa-693e979e7a89");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияКЗакрытиюМесяца.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Заполняет измерение Операция значением с типом ""%1"".';
			|en = 'Fills the Operation dimension with a value of the ""%1"" type.'", ОбщегоНазначения.КодОсновногоЯзыка()),
		Метаданные.Справочники.ОперацииЗакрытияМесяца.ПолноеИмя());
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКЗакрытиюМесяца.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияКЗакрытиюМесяца.ПолноеИмя());
	
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПустойНабор = СоздатьНаборЗаписей();
	ПолноеИмяРегистра = ПустойНабор.Метаданные().ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Задания.Месяц,
	|	Задания.Организация,
	|	Задания.УдалитьОперация,
	|	Задания.Документ,
	|	Задания.НомерЗадания
	|ИЗ
	|	РегистрСведений.ЗаданияКЗакрытиюМесяца КАК Задания
	|ГДЕ
	|	Задания.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииЗакрытияМесяца.ПустаяСсылка)";
	
	Выборка= Запрос.Выполнить().Выбрать();
	
	Порция = ПустойНабор.Выгрузить(, "Месяц, Организация, УдалитьОперация, Документ, НомерЗадания");
	
	Счетчик = 0;
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Порция.Добавить(), Выборка);
		Счетчик = Счетчик + 1;

		Если Счетчик = 1000 Тогда
			ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Порция, ДополнительныеПараметры);
			Порция.Очистить();
			Счетчик = 0;
		КонецЕсли;
		
	КонецЦикла;
	
	Если Порция.Количество() > 0 Тогда
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Порция, ДополнительныеПараметры);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	
	ПолноеИмяОбъекта = МетаданныеРегистра.ПолноеИмя();
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОперацииЗакрытияМесяца.Ссылка,
	|	ОперацииЗакрытияМесяца.ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.ОперацииЗакрытияМесяца КАК ОперацииЗакрытияМесяца";
	
	ВыборкаОперацииЗакрытияМесяца = Запрос.Выполнить().Выбрать();
	
	СоответствиеОпераций = Новый Соответствие();
	
	Пока ВыборкаОперацииЗакрытияМесяца.Следующий() Цикл
		СоответствиеОпераций.Вставить(
			ВыборкаОперацииЗакрытияМесяца.ИмяПредопределенныхДанных,
			ВыборкаОперацииЗакрытияМесяца.Ссылка);
	КонецЦикла;

	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Не удалось обработать задания по обработчику регистра сведений  ""%1"".';
				|en = 'Cannot process the jobs of the ""%1"" information register handler.'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
					ПолноеИмяОбъекта));
		
	Для Каждого Выборка Из ОбновляемыеДанные Цикл
		
		ПричинаИсключения = "";
			
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Месяц.Установить(Выборка.Месяц);
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.УдалитьОперация.Установить(Выборка.УдалитьОперация);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.НомерЗадания.Установить(Выборка.НомерЗадания);
			
			ПричинаИсключения = "Блокировка";
			
			Блокировка = Новый БлокировкаДанных;
			
			// Блокировка регистра "Задания к распределению расчетов".
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Месяц", Выборка.Месяц);
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			ЭлементБлокировки.УстановитьЗначение("УдалитьОперация", Выборка.УдалитьОперация);
			ЭлементБлокировки.УстановитьЗначение("Документ", Выборка.Документ);
			ЭлементБлокировки.УстановитьЗначение("НомерЗадания", Выборка.НомерЗадания);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Блокировка.Заблокировать();
			
			ПричинаИсключения = "Обработка данных";

			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл

				Если Не ЗначениеЗаполнено(Выборка.Операция) Тогда
					Запись.Операция = СоответствиеОпераций.Получить(XMLСтрока(Запись.УдалитьОперация));
				КонецЕсли;

			КонецЦикла;
			
			ПричинаИсключения = "Запись";
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ОбъектовОбработано = ОбъектовОбработано + 1;
			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();
			
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			
			ОтборСтрокой = Строка(НаборЗаписей.Отбор);
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать набор записей %Набор% регистра 
										|""Задания к распределению расчетов"" по причине:  по причине: %Причина%';
										|en = 'Cannot process the %Набор% record set of the 
										|""Jobs for allocating AR/AP"" register. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Набор%", ОтборСтрокой);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", 
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									МетаданныеРегистра,
									ОтборСтрокой,
									ТекстСообщения);

			Если ПричинаИсключения = "Обработка данных" Тогда

				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);

			ИначеЕсли ПричинаИсключения = "Запись" Тогда

				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);

			КонецЕсли;
			
		КонецПопытки;

	КонецЦикла;
	
	Если ОбъектовОбработано = 0 И ПроблемныхОбъектов <> 0 Тогда

		СписокОписаний.Добавить(НСтр("ru = 'Всего пропущено: %1';
									|en = 'Total skipped: %1'"));
		ТекстСообщения = СтрШаблон(СтрСоединить(СписокОписаний, Символы.ПС), ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;

	КонецЕсли;

	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
