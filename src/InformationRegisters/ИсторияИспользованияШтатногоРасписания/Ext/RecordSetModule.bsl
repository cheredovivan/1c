#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИзменений = РегистрыБЗК.ИзменениеЗаписейНабора(РегистрыБЗК.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение), ЭтотОбъект, Замещение, Истина);
	МассивОбновляемыхПозиций = ТаблицаИзменений.ВыгрузитьКолонку("ПозицияШтатногоРасписания");
	Если ЗначениеЗаполнено(МассивОбновляемыхПозиций) Тогда
		ДополнительныеСвойства.Вставить("МассивОбновляемыхПозиций", МассивОбновляемыхПозиций);
	КонецЕсли;
	
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПередЗаписью(ЭтотОбъект, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("МассивОбновляемыхПозиций")
		И ДополнительныеСвойства.МассивОбновляемыхПозиций.Количество() > 0 Тогда
		
		СписокПозицийШтатногоРасписания = ДополнительныеСвойства.МассивОбновляемыхПозиций;
		
		// Обновление вторичных данных позиций штатного расписания.
		РегистрыСведений.ИсторияИспользованияШтатногоРасписания.ОбновитьТекущиеСведенияПозицииШтатногоРасписания(
			СписокПозицийШтатногоРасписания);
		
		ОбновитьСведенияДолжностей(СписокПозицийШтатногоРасписания);
		
		РегистрыСведений.ИсторияИспользованияШтатногоРасписания.ОбновитьСведенияПодразделенийПоСпискуПозиций(СписокПозицийШтатногоРасписания);
		
	КонецЕсли;
	
	ОтборыЗаписейНабора = РегистрыБЗК.ОтборыЗаписейНабора(ЭтотОбъект, Замещение);
	РегистраторыНабора = Новый Массив;
	Для Каждого ОтборЗаписейНабора Из ОтборыЗаписейНабора Цикл
		Если ОтборЗаписейНабора["Регистратор_Использование"] Тогда
			Если ТипЗнч(ОтборЗаписейНабора["Регистратор"]) = Тип("ДокументСсылка.ИзменениеШтатногоРасписания")
				Или ТипЗнч(ОтборЗаписейНабора["Регистратор"]) = Тип("ДокументСсылка.УтверждениеШтатногоРасписания") Тогда
				
				РегистраторыНабора.Добавить(ОтборЗаписейНабора["Регистратор"]);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ЗначениеЗаполнено(РегистраторыНабора) Тогда
		РеквизитыРегистраторов = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(РегистраторыНабора, "Организация,Подразделение");
		ТаблицаЗначенийРеквизитов = Новый ТаблицаЗначений;
		ТаблицаЗначенийРеквизитов.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
		ТаблицаЗначенийРеквизитов.Колонки.Добавить("Подразделение", Новый ОписаниеТипов("СправочникСсылка.ПодразделенияОрганизаций"));
		Для Каждого РеквизитыРегистратора Из РеквизитыРегистраторов Цикл
			ЗаполнитьЗначенияСвойств(ТаблицаЗначенийРеквизитов.Добавить(), РеквизитыРегистратора);
		КонецЦикла;
		ТаблицаЗначенийРеквизитов.Свернуть("Организация,Подразделение");
		Для Каждого СтрокаТаблицы Из ТаблицаЗначенийРеквизитов Цикл
			Подразделение = ?(ЗначениеЗаполнено(СтрокаТаблицы.Подразделение), СтрокаТаблицы.Подразделение, Неопределено);
			
			Справочники.ШтатноеРасписание.ОбновитьСтруктуруШтатногоРасписанияПоДаннымПодразделений(
				СтрокаТаблицы.Организация, Подразделение);
			Справочники.ШтатноеРасписание.ОбновитьСтруктуруШтатногоРасписанияПоДаннымПозиций(
				СтрокаТаблицы.Организация, Подразделение);
		КонецЦикла;
	КонецЕсли;
	
	ЗарплатаКадрыПериодическиеРегистры.КонтрольИзмененийПриЗаписи(ЭтотОбъект, Замещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает признак сформированности таблицы изменений
//
Функция ТаблицаИзменившихсяДанныхНабораСформирована() Экспорт
	Возврат ЗарплатаКадрыПериодическиеРегистры.ТаблицаИзменившихсяДанныхНабораСформирована(ЭтотОбъект);
КонецФункции

// Возвращает таблицу изменений регистра
//
Функция ТаблицаИзменившихсяДанныхНабора() Экспорт
	Возврат ЗарплатаКадрыПериодическиеРегистры.ТаблицаИзменившихсяДанныхНабора(ЭтотОбъект);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьСведенияДолжностей(СписокПозицийШтатногоРасписания)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокПозицийШтатногоРасписания", СписокПозицийШтатногоРасписания);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ШтатноеРасписание.Должность КАК Должность
		|ПОМЕСТИТЬ ВТОбновляемыеДолжности
		|ИЗ
		|	Справочник.ШтатноеРасписание КАК ШтатноеРасписание
		|ГДЕ
		|	ШтатноеРасписание.Ссылка В(&СписокПозицийШтатногоРасписания)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбновляемыеДолжности.Должность,
		|	МИНИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТМинимальныеДатыИспользованияДолжностей
		|ИЗ
		|	ВТОбновляемыеДолжности КАК ОбновляемыеДолжности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО ОбновляемыеДолжности.Должность = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания.Должность
		|			И (ИсторияИспользованияШтатногоРасписания.Используется)
		|
		|СГРУППИРОВАТЬ ПО
		|	ОбновляемыеДолжности.Должность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбновляемыеДолжности.Должность,
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания,
		|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТМаксимальныеДатыИзменений
		|ИЗ
		|	ВТОбновляемыеДолжности КАК ОбновляемыеДолжности
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО ОбновляемыеДолжности.Должность = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания.Должность
		|
		|СГРУППИРОВАТЬ ПО
		|	ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания,
		|	ОбновляемыеДолжности.Должность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МаксимальныеДатыИзменений.Должность,
		|	МАКСИМУМ(ИсторияИспользованияШтатногоРасписания.Используется) КАК Используется
		|ПОМЕСТИТЬ ВТПоследниеИспользованияДолжностей
		|ИЗ
		|	ВТМаксимальныеДатыИзменений КАК МаксимальныеДатыИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИсторияИспользованияШтатногоРасписания КАК ИсторияИспользованияШтатногоРасписания
		|		ПО МаксимальныеДатыИзменений.ПозицияШтатногоРасписания = ИсторияИспользованияШтатногоРасписания.ПозицияШтатногоРасписания
		|			И МаксимальныеДатыИзменений.Дата = ИсторияИспользованияШтатногоРасписания.Дата
		|
		|СГРУППИРОВАТЬ ПО
		|	МаксимальныеДатыИзменений.Должность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоследниеИспользованияДолжностей.Должность,
		|	МАКСИМУМ(МаксимальныеДатыИзменений.Дата) КАК Дата
		|ПОМЕСТИТЬ ВТДатыЗакрытияДолжностей
		|ИЗ
		|	ВТМаксимальныеДатыИзменений КАК МаксимальныеДатыИзменений
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПоследниеИспользованияДолжностей КАК ПоследниеИспользованияДолжностей
		|		ПО МаксимальныеДатыИзменений.Должность = ПоследниеИспользованияДолжностей.Должность
		|			И (НЕ ПоследниеИспользованияДолжностей.Используется)
		|
		|СГРУППИРОВАТЬ ПО
		|	ПоследниеИспользованияДолжностей.Должность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОбновляемыеДолжности.Должность,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(МинимальныеДатыИспользованияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ВведенаВШтатноеРасписание,
		|	ЕСТЬNULL(МинимальныеДатыИспользованияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаВвода,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(МинимальныеДатыИспользованияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ЕСТЬNULL(ДатыЗакрытияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ИсключенаИзШтатногоРасписания,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(МинимальныеДатыИспользованияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ДАТАВРЕМЯ(1, 1, 1)
		|		ИНАЧЕ ЕСТЬNULL(ДатыЗакрытияДолжностей.Дата, ДАТАВРЕМЯ(1, 1, 1))
		|	КОНЕЦ КАК ДатаИсключения
		|ПОМЕСТИТЬ ВТИспользуемостьДолжностей
		|ИЗ
		|	ВТОбновляемыеДолжности КАК ОбновляемыеДолжности
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТМинимальныеДатыИспользованияДолжностей КАК МинимальныеДатыИспользованияДолжностей
		|		ПО ОбновляемыеДолжности.Должность = МинимальныеДатыИспользованияДолжностей.Должность
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыЗакрытияДолжностей КАК ДатыЗакрытияДолжностей
		|		ПО ОбновляемыеДолжности.Должность = ДатыЗакрытияДолжностей.Должность
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИспользуемостьДолжностей.Должность,
		|	ИспользуемостьДолжностей.ВведенаВШтатноеРасписание,
		|	ИспользуемостьДолжностей.ДатаВвода,
		|	ИспользуемостьДолжностей.ИсключенаИзШтатногоРасписания,
		|	ИспользуемостьДолжностей.ДатаИсключения
		|ИЗ
		|	ВТИспользуемостьДолжностей КАК ИспользуемостьДолжностей
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Должности КАК Должности
		|		ПО ИспользуемостьДолжностей.Должность = Должности.Ссылка
		|ГДЕ
		|	(ИспользуемостьДолжностей.ВведенаВШтатноеРасписание <> Должности.ВведенаВШтатноеРасписание
		|			ИЛИ ИспользуемостьДолжностей.ДатаВвода <> Должности.ДатаВвода
		|			ИЛИ ИспользуемостьДолжностей.ИсключенаИзШтатногоРасписания <> Должности.ИсключенаИзШтатногоРасписания
		|			ИЛИ ИспользуемостьДолжностей.ДатаИсключения <> Должности.ДатаИсключения)";
		
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			ДолжностьОбъект = Выборка.Должность.ПолучитьОбъект();
			
			Попытка 
				ДолжностьОбъект.Заблокировать();
			Исключение
				
				ТекстИсключенияЗаписи = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Не удалось изменить должность ""%1"".
					|Возможно, должность редактируется другим пользователем';
					|en = 'Cannot change the ""%1"" position.
					|Maybe, the position is being edited by another user'"),
					ДолжностьОбъект.Наименование);
					
				ВызватьИсключение ТекстИсключенияЗаписи;
				
			КонецПопытки;	
			
			ЗаполнитьЗначенияСвойств(ДолжностьОбъект, Выборка);
			
			Если НЕ ДолжностьОбъект.ИсключенаИзШтатногоРасписания И ДолжностьОбъект.ПометкаУдаления Тогда
				ДолжностьОбъект.ПометкаУдаления = Ложь;
			КонецЕсли;
			
			ДолжностьОбъект.Записать();
			
		КонецЦикла;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли