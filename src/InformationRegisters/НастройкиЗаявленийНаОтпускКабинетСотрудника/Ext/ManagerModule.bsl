#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция НастройкиЗаявокНаОтпуск() Экспорт
	
	Настройки = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	СтруктураНастроек = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(
							Настройки, Метаданные.РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника);
	
	Возврат СтруктураНастроек;

КонецФункции

Процедура СохранитьНовыеНастройки(СохраняемыеНастройки) Экспорт

	НаборЗаписей = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СоздатьНаборЗаписей();
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() = 0 Тогда
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), СохраняемыеНастройки);
	Иначе
		ЗаполнитьЗначенияСвойств(НаборЗаписей[0], СохраняемыеНастройки);
	КонецЕсли;
	НаборЗаписей.Записать();

КонецПроцедуры

Процедура УстановитьТребуетсяОбновитьНастройки(НовоеЗначение) Экспорт
	
	Настройки = РегистрыСведений.НастройкиЗаявленийНаОтпускКабинетСотрудника.СоздатьМенеджерЗаписи();
	Настройки.Прочитать();
	
	Записывать = Ложь;
	Если Настройки.Выбран() Тогда
		Если НовоеЗначение <> Настройки.ОбновитьНастройкиВСервисе Тогда
			Настройки.ОбновитьНастройкиВСервисе = НовоеЗначение;
			Настройки.ВерсияИзменений = Строка(Новый УникальныйИдентификатор);
			Записывать = Истина;
		КонецЕсли;
	Иначе
		Настройки.ОбновитьНастройкиВСервисе = НовоеЗначение;
		Настройки.ВерсияИзменений = Строка(Новый УникальныйИдентификатор);
		Записывать = Истина;
	КонецЕсли;
	
	Если Записывать Тогда
		Настройки.Записать();
	КонецЕсли;

КонецПроцедуры


#КонецОбласти

#КонецЕсли
