#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
		
	// Очищаемся вторичный регистр при необходимости.
	ТаблицаЗапроса = ТаблицаИзмеренийУдаляемыхЗаписейЗаработанныхОтпусков(Замещение);
	ОстаткиОтпусков.УдалитьЗаписиЗаработанныхОтпусков(ТаблицаЗапроса);
	
	// Обновляем движения с возвратными показателями.
	ВозвратныеРесурсыПлановыеНачисления = "Используется, КоличествоДнейВГод";
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра = КадровыйУчетРасширенный.ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра();
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("СтрокаЗаполняемыхВозвратныхРесурсов", ВозвратныеРесурсыПлановыеНачисления);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ПередЗаписью", Истина);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ПервичныеРесурсыДляУвольнения", Ложь);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ОбновлятьУвольнение", Ложь);
	КадровыйУчетРасширенный.ОбновитьВторичныеДанныеВозвратногоРегистра(ЭтотОбъект,
		ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра,
		Замещение);
	
	// В случае наличия отпусков, зависящих от стажа, их необходимо обработать.
	Если ОстаткиОтпусков.ЕстьСтажевыеОтпуска() Тогда
		ОстаткиОтпусков.ОчиститьПредыдущиеДвиженияСтажевыхОтпусков(ЭтотОбъект, Отказ, Замещение);
	КонецЕсли;
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
		
	// Обновляем движения с возвратными показателями.
	ВозвратныеРесурсыПлановыеНачисления = "Используется, КоличествоДнейВГод";
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра = КадровыйУчетРасширенный.ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра();
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("СтрокаЗаполняемыхВозвратныхРесурсов", ВозвратныеРесурсыПлановыеНачисления);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ПередЗаписью", Ложь);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ПервичныеРесурсыДляУвольнения", Ложь);
	ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра.Вставить("ОбновлятьУвольнение", Ложь);
	КадровыйУчетРасширенный.ОбновитьВторичныеДанныеВозвратногоРегистра(ЭтотОбъект,
		ПараметрыОбновленияВторичныхДанныхВозвратногоРегистра,
		Замещение);
	
	// В случае наличия отпусков, зависящих от стажа, их необходимо обработать.
	Если ОстаткиОтпусков.ЕстьСтажевыеОтпуска() Тогда
		ОстаткиОтпусков.ДополнитьДвиженияСтажевыхОтпусков(ЭтотОбъект, Отказ, Замещение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Функция ТаблицаИзмеренийУдаляемыхЗаписейЗаработанныхОтпусков(Замещение)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.Регистратор КАК Регистратор,
		|	ВЫРАЗИТЬ(ТЗ.Сотрудник КАК Справочник.Сотрудники) КАК Сотрудник,
		|	ВЫРАЗИТЬ(ТЗ.ВидЕжегодногоОтпуска КАК Справочник.ВидыОтпусков) КАК ВидЕжегодногоОтпуска,
		|	ТЗ.КоличествоДнейВГод КАК КоличествоДнейВГод,
		|	НАЧАЛОПЕРИОДА(ТЗ.Период, ДЕНЬ) КАК Период
		|ПОМЕСТИТЬ НовыйНабор
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПоложенныеВидыЕжегодныхОтпусков.Регистратор КАК Регистратор,
		|	ПоложенныеВидыЕжегодныхОтпусков.Сотрудник КАК Сотрудник,
		|	ПоложенныеВидыЕжегодныхОтпусков.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	ПоложенныеВидыЕжегодныхОтпусков.КоличествоДнейВГод КАК КоличествоДнейВГод,
		|	НАЧАЛОПЕРИОДА(ПоложенныеВидыЕжегодныхОтпусков.Период, ДЕНЬ) КАК Период
		|ПОМЕСТИТЬ СтарыйНабор
		|ИЗ
		|	РегистрСведений.ПоложенныеВидыЕжегодныхОтпусков КАК ПоложенныеВидыЕжегодныхОтпусков
		|ГДЕ
		|	ПоложенныеВидыЕжегодныхОтпусков.Регистратор В(&Регистраторы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(НовыйНабор.Сотрудник, СтарыйНабор.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(НовыйНабор.ВидЕжегодногоОтпуска, СтарыйНабор.ВидЕжегодногоОтпуска) КАК ВидЕжегодногоОтпуска,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НовыйНабор.КоличествоДнейВГод, 0) <> ЕСТЬNULL(СтарыйНабор.КоличествоДнейВГод, 0)
		|			ТОГДА ИСТИНА
		|		КОГДА ЕСТЬNULL(НовыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1)) <> ЕСТЬNULL(СтарыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК БылиИзменения,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НовыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА СтарыйНабор.Период
		|		КОГДА ЕСТЬNULL(СтарыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1)) = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА НовыйНабор.Период
		|		ИНАЧЕ ВЫБОР
		|				КОГДА ЕСТЬNULL(СтарыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1)) > ЕСТЬNULL(НовыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1))
		|					ТОГДА ЕСТЬNULL(НовыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1))
		|				ИНАЧЕ ЕСТЬNULL(СтарыйНабор.Период, ДАТАВРЕМЯ(1, 1, 1))
		|			КОНЕЦ
		|	КОНЕЦ КАК РаннийПериод
		|ПОМЕСТИТЬ СравнительныйНабор
		|ИЗ
		|	НовыйНабор КАК НовыйНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ СтарыйНабор КАК СтарыйНабор
		|		ПО НовыйНабор.Сотрудник = СтарыйНабор.Сотрудник
		|			И НовыйНабор.Регистратор = СтарыйНабор.Регистратор
		|			И НовыйНабор.ВидЕжегодногоОтпуска = СтарыйНабор.ВидЕжегодногоОтпуска
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СравнительныйНабор.Сотрудник КАК Сотрудник,
		|	СравнительныйНабор.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	СравнительныйНабор.РаннийПериод КАК РаннийПериод
		|ПОМЕСТИТЬ ИзменившиесяЗаписи
		|ИЗ
		|	СравнительныйНабор КАК СравнительныйНабор
		|ГДЕ
		|	СравнительныйНабор.БылиИзменения = ИСТИНА
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИзменившиесяЗаписи.Сотрудник КАК Сотрудник,
		|	ИзменившиесяЗаписи.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	МИНИМУМ(ИзменившиесяЗаписи.РаннийПериод) КАК Период
		|ИЗ
		|	ИзменившиесяЗаписи КАК ИзменившиесяЗаписи
		|
		|СГРУППИРОВАТЬ ПО
		|	ИзменившиесяЗаписи.Сотрудник,
		|	ИзменившиесяЗаписи.ВидЕжегодногоОтпуска";
	
	ТаблицаНовыхДвижений = Выгрузить(,"Регистратор,Сотрудник,ВидЕжегодногоОтпуска,КоличествоДнейВГод,Период");
	Если Отбор.Найти("Регистратор") <> Неопределено И Отбор.Регистратор.Использование Тогда 
		ТаблицаНовыхДвижений.ЗаполнитьЗначения(Отбор.Регистратор.Значение, "Регистратор"); 
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ТЗ", ТаблицаНовыхДвижений);
	Запрос.УстановитьПараметр("Регистраторы", РегистрыБЗК.ЗначенияОтбораЗаписейНабора(ЭтотОбъект, Замещение, "Регистратор"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить();
	
КонецФункции

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли