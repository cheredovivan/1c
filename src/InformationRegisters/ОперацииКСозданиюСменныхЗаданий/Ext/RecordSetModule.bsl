
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОтключенКонтроль = РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.КлючНеКонтролироватьДанныеПриЗаписи();
	
	Если Количество() > 0
		И НЕ ДополнительныеСвойства.Свойство(ОтключенКонтроль) Тогда
		
		КонтрольНазначенияОпераций(Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КонтрольНазначенияОпераций(Отказ)
	
	ПараметрыТекстаЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ПараметрыТекстаЗапросаОчередьОпераций();
	ПараметрыТекстаЗапроса.ПоказателиВыполнения = Истина;
	ПараметрыТекстаЗапроса.РеквизитыИсполнителяОперации = Истина;
	ПараметрыТекстаЗапроса.ИмяВыходнойТаблицы = "ВТОчередь";
	ПараметрыТекстаЗапроса.Отборы.Добавить("(Очередь.Этап, Очередь.Операция, Очередь.ИдентификаторОперации) В (&ТекстОтбораОперацияЭтап)");
	
	ТекстЗапроса = РегистрыСведений.ОчередьПроизводственныхОпераций.ТекстЗапросаОчередьОпераций(ПараметрыТекстаЗапроса) + "
		|
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	NULL                     КАК Этап,
		|	NULL                     КАК Операция,
		|	NULL                     КАК ИдентификаторОперации,
		|	NULL                     КАК НаименованиеОперации,
		|	Операции.СменноеЗадание  КАК СменноеЗадание,
		|	NULL                     КАК Исполнитель,
		|	ВЫБОР
		|		КОГДА НЕ Задания.Проведен
		|			ТОГДА ""ЗаданиеНеПроведено""
		|		ИНАЧЕ ""СтатусЗаданияНеФормируется""
		|	КОНЕЦ                    КАК КодОшибки
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Операции
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СменноеЗадание КАК Задания
		|		ПО Операции.СменноеЗадание = Задания.Ссылка
		|ГДЕ
		|	Операции.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|	И &ЗаписиНабора
		|	И (НЕ Задания.Проведен
		|			ИЛИ Задания.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыСменныхЗаданий.Формируется))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	NULL                           КАК Этап,
		|	Операции.Операция              КАК Операция,
		|	Операции.ИдентификаторОперации КАК ИдентификаторОперации,
		|	Очередь.Наименование           КАК НаименованиеОперации,
		|	NULL                           КАК СменноеЗадание,
		|	NULL                           КАК Исполнитель,
		|	""НекорректныйВидРЦ""          КАК КодОшибки
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Операции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОчередь КАК Очередь
		|		ПО Операции.Этап = Очередь.Этап
		|			И Операции.Операция = Очередь.Операция
		|			И Операции.ИдентификаторОперации = Очередь.ИдентификаторОперации
		|ГДЕ
		|	Операции.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|	И &ЗаписиНабора
		|	И Операции.ВидРабочегоЦентра <> ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка)
		|	И Очередь.ВидРабочегоЦентра <> ЗНАЧЕНИЕ(Справочник.ВидыРабочихЦентров.ПустаяСсылка)
		|	И Операции.ВидРабочегоЦентра <> Очередь.ВидРабочегоЦентра
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Операции.Этап                  КАК Этап,
		|	Операции.Операция              КАК Операция,
		|	Операции.ИдентификаторОперации КАК ИдентификаторОперации,
		|	Очередь.Наименование           КАК НаименованиеОперации,
		|	NULL                           КАК СменноеЗадание,
		|	NULL                           КАК Исполнитель,
		|	""ПревышениеПланКоличества""   КАК КодОшибки
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Операции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОчередь КАК Очередь
		|		ПО Операции.Этап = Очередь.Этап
		|			И Операции.Операция = Очередь.Операция
		|			И Операции.ИдентификаторОперации = Очередь.ИдентификаторОперации
		|ГДЕ
		|	(Операции.Этап, Операции.Операция, Операции.ИдентификаторОперации) В (&ТекстОтбораОперацияЭтап)
		|	И Операции.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|
	 	|СГРУППИРОВАТЬ ПО
	 	|	Операции.Этап,
	 	|	Операции.Операция,
	 	|	Операции.ИдентификаторОперации,
	 	|	Очередь.Наименование
		|
		|ИМЕЮЩИЕ
		|	СУММА(Операции.Количество) > (МАКСИМУМ(Очередь.Запланировано) - МАКСИМУМ(Очередь.Создано))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	NULL                          КАК Этап,
		|	Операции.Операция             КАК Операция,
		|	NULL                          КАК ИдентификаторОперации,
		|	Очередь.Наименование          КАК НаименованиеОперации,
		|	NULL                          КАК СменноеЗадание,
		|	Бригады.Ссылка                КАК Исполнитель,
		|	""НесоответствиеОрганизации"" КАК КодОшибки
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Операции
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОчередь КАК Очередь
		|		ПО Операции.Этап = Очередь.Этап
		|			И Операции.Операция = Очередь.Операция
		|			И Операции.ИдентификаторОперации = Очередь.ИдентификаторОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Бригады КАК Бригады
		|		ПО Операции.Исполнитель = Бригады.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ЭтапПроизводства2_2
		|		ПО Операции.Этап = ЭтапПроизводства2_2.Ссылка
		|ГДЕ
		|	Операции.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|	И &ЗаписиНабора
		|	И Операции.Исполнитель ССЫЛКА Справочник.Бригады
		|	И Операции.Исполнитель <> ЗНАЧЕНИЕ(Справочник.Бригады.ПустаяСсылка)
		|	И Бригады.Организация <> ЭтапПроизводства2_2.Организация";
	
	Запрос = Новый Запрос;
	
	ТекстУсловия = "";
	Для каждого Элемент Из Отбор Цикл
		Если НЕ Элемент.Использование Тогда
			Продолжить;
		КонецЕсли;
		ТекстУсловия = ТекстУсловия	+ " И Операции." + Элемент.Имя + " = &" + Элемент.Имя;
		Запрос.УстановитьПараметр(Элемент.Имя, Элемент.Значение);
	КонецЦикла;
	
	ТекстОтбораОперацияЭтап = "
		|	ВЫБРАТЬ
		|		Операции.Этап,
		|		Операции.Операция,
		|		Операции.ИдентификаторОперации
		|	 ИЗ
		|		РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК Операции
		|	 ГДЕ
		|		Операции.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
		|		И &ЗаписиНабора";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтбораОперацияЭтап", ТекстОтбораОперацияЭтап);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ЗаписиНабора",          ТекстУсловия);
	
	Запрос.Текст = ТекстЗапроса;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = "";
		КлючДанных = Неопределено;
		
		Если Выборка.КодОшибки = "ЗаданиеНеПроведено" Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ %1 не проведен.';
											|en = 'Document %1 is not posted.'"), Выборка.СменноеЗадание);
			КлючДанных = Выборка.СменноеЗадание;
			
		ИначеЕсли Выборка.КодОшибки = "СтатусЗаданияНеФормируется" Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Документ %1 не находится в статусе ""Формируется"".';
											|en = 'Document %1 is not in the ""Generating"" status.'"), Выборка.СменноеЗадание);
			КлючДанных = Выборка.СменноеЗадание;
			
		ИначеЕсли Выборка.КодОшибки = "НекорректныйВидРЦ" Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Операция %1 не может быть назначена заданному РЦ (виду РЦ).';
											|en = 'The %1 operation cannot be assigned to the specified work center (work center type).'"), Выборка.НаименованиеОперации);
			
		ИначеЕсли Выборка.КодОшибки = "ПревышениеПланКоличества" Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Операция %1 %2 назначена в количестве, превышающем запланированное.';
											|en = 'The %1 %2 operation is assigned in the quantity exceeding the planned one.'"),
				Выборка.НаименованиеОперации, Выборка.Этап);
			
		ИначеЕсли Выборка.КодОшибки = "НесоответствиеОрганизации" Тогда
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Операция %1 и бригада %2 относятся к различным организациям.';
											|en = 'Operation %1 and work team %2 belong to different companies.'"),
				Выборка.НаименованиеОперации, Выборка.Исполнитель);
				
		КонецЕсли;
		
		ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				КлючДанных,
				,
				, 
				Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
