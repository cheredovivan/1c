#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получает ставку акциза по категории подакцизных товаров, действующую на указанную дату.
//
// Параметры:
//  Категория - СправочникСсылка.КатегорииПодакцизныхТоваров
//  Дата - Дата, Неопределено - Дата актуальности ставки. Если передано значение Неопределено,
//  							будет получена последняя ставка.
//
// Возвращаемое значение:
//  Структура:
//   * Период - Дата - дата начала применения ставки
//   * ДатаПо - Дата - дата окончания применения ставки
//   * Ставка - Число - значение записанной ставки
//   * КоличествоПериодов - Число - количество записанных периодов.
//
Функция СтавкаПоКатегорииНаДату(Категория, Дата = Неопределено) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Период", '00010101');
	Результат.Вставить("ДатаПо", '00010101');
	Результат.Вставить("Ставка", 0);
	Результат.Вставить("КоличествоПериодов", 0);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Категория", Категория);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаСтавок.Период КАК Период,
	|	ТаблицаСтавок.Ставка КАК Ставка
	|ПОМЕСТИТЬ ТаблицаТекущихСтавок
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров.СрезПоследних(&Дата, КатегорияПодакцизногоТовара = &Категория) КАК ТаблицаСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.Период КАК Период
	|ПОМЕСТИТЬ ТаблицаИсторииСтавок
	|ИЗ
	|	РегистрСведений.СтавкиКатегорийПодакцизныхТоваров КАК Таблица
	|ГДЕ
	|	Таблица.КатегорияПодакцизногоТовара = &Категория
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСтавок.Период КАК Период,
	|	ТаблицаСтавок.Ставка КАК Ставка
	|ИЗ
	|	ТаблицаТекущихСтавок КАК ТаблицаСтавок
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(Таблица.Период, ДЕНЬ), СЕКУНДА, -1) КАК ДатаПо
	|ИЗ
	|	ТаблицаИсторииСтавок КАК Таблица
	|ГДЕ
	|	Таблица.Период > &Дата
	|
	|УПОРЯДОЧИТЬ ПО
	|	Таблица.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(1) КАК КоличествоПериодов
	|ИЗ
	|	ТаблицаИсторииСтавок КАК ТаблицаИсторииСтавок";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Если Не РезультатЗапроса[2].Пустой() Тогда
		Выборка = РезультатЗапроса[2].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Если Не РезультатЗапроса[3].Пустой() Тогда
		Выборка = РезультатЗапроса[3].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Если Не РезультатЗапроса[4].Пустой() Тогда
		Выборка = РезультатЗапроса[4].Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Устанавливает ставку акциза по категории подакцизных товаров на указанную дату.
//
// Параметры:
//  Категория - СправочникСсылка.КатегорииПодакцизныхТоваров
//  Ставка - Число
//  Дата - Дата - Дата начала действия ставки.
//
Процедура УстановитьСтавкуПоКатегории(Категория, Ставка, Дата) Экспорт
	
	Запись = СоздатьМенеджерЗаписи();
	Запись.Период = Дата;
	Запись.КатегорияПодакцизногоТовара = Категория;
	Запись.Ставка = Ставка;
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
