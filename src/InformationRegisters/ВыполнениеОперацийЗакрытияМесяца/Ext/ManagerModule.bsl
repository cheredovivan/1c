#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ЗавершенныеРасчеты

Функция ПолучитьСведенияОВыполненииЭтапа(МассивОрганизаций, ПериодРасчета, Операция) Экспорт
	
	Результат = Новый Структура("ДатаНачала, ДатаОкончания, БылиОшибки");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МИНИМУМ(Статистика.ДатаНачала), НЕОПРЕДЕЛЕНО) 		КАК ДатаНачала,
	|	ЕСТЬNULL(МАКСИМУМ(Статистика.ДатаОкончания), НЕОПРЕДЕЛЕНО) 	КАК ДатаОкончания,
	|	ЕСТЬNULL(МАКСИМУМ(Статистика.БылиОшибки), ЛОЖЬ) 		   	КАК БылиОшибки
	|ИЗ
	|	РегистрСведений.ВыполнениеОперацийЗакрытияМесяца КАК Статистика
	|ГДЕ
	|	Статистика.Организация В(&МассивОрганизаций)
	|	И Статистика.ПериодРасчета = &ПериодРасчета
	|	И Статистика.Операция = &Операция
	|	И НЕ Статистика.РасчетВыполняется";
	
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииДляРасчета(МассивОрганизаций));
	Запрос.УстановитьПараметр("ПериодРасчета", 	   ПериодРасчета);
	Запрос.УстановитьПараметр("Операция", 		   Операция);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(Результат, Выборка);
	
	Возврат Результат;
	
КонецФункции

Процедура СохранитьСведенияОВыполненииЭтапа(МассивОрганизаций, ПериодРасчета, ОписаниеВыполненияЭтапа, БылиОшибки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если ЗакрытиеМесяцаСервер.УпрощенноеАвтотестирование() Тогда
		
		// Выполняется по всем организациям
		НаборЗаписей = РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.ПериодРасчета.Установить(ПериодРасчета);
		НаборЗаписей.Отбор.Операция.Установить(ОписаниеВыполненияЭтапа.Операция);
		НаборЗаписей.Отбор.РасчетВыполняется.Установить(Ложь);
			
		Для Каждого ТекущаяОрганизация Из ОрганизацииДляРасчета(МассивОрганизаций) Цикл
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ОписаниеВыполненияЭтапа);
			
			Запись.Организация 	     = ТекущаяОрганизация;
			Запись.ПериодРасчета     = ПериодРасчета;
			Запись.БылиОшибки 	     = БылиОшибки;
			Запись.РасчетВыполняется = Ложь;
			
		КонецЦикла;
		
		НаборЗаписей.Записать(Истина);
		
	Иначе
		
		Для Каждого ТекущаяОрганизация Из ОрганизацииДляРасчета(МассивОрганизаций) Цикл
			
			НаборЗаписей = РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.СоздатьНаборЗаписей();
			
			НаборЗаписей.Отбор.Организация.Установить(ТекущаяОрганизация);
			НаборЗаписей.Отбор.ПериодРасчета.Установить(ПериодРасчета);
			НаборЗаписей.Отбор.Операция.Установить(ОписаниеВыполненияЭтапа.Операция);
			НаборЗаписей.Отбор.РасчетВыполняется.Установить(Ложь);
			
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, ОписаниеВыполненияЭтапа);
			
			Запись.Организация 	     = ТекущаяОрганизация;
			Запись.ПериодРасчета     = ПериодРасчета;
			Запись.БылиОшибки 	     = БылиОшибки;
			Запись.РасчетВыполняется = Ложь;
			
			НаборЗаписей.Записать(Истина);
			
		КонецЦикла;
		
	КонецЕсли;
	
	ДанныеЗамера = Новый Структура("ДатаНачала, ДатаОкончания, Длительность");
	ЗаполнитьЗначенияСвойств(ДанныеЗамера, ОписаниеВыполненияЭтапа);
	
	ТекстДляЖурнала = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Общее время: %1
			|Дата начала: %2
			|Дата окончания: %3
			|Организации: %4';
			|en = 'Total time: %1
			|Start date: %2
			|End date: %3
			|Companies: %4'", ОбщегоНазначения.КодОсновногоЯзыка()),
		РасчетСебестоимостиПротоколРасчета.ПредставлениеВремени(ДанныеЗамера.Длительность/1000),
		ДанныеЗамера.ДатаНачала,
		ДанныеЗамера.ДатаОкончания,
		РасчетСебестоимостиПрикладныеАлгоритмы.ПредставлениеОрганизаций(ОрганизацииДляРасчета(МассивОрганизаций), ", "));
	
	ЗаписьЖурналаРегистрации(
		ЗакрытиеМесяцаСервер.ИмяСобытияЖурналаРегистрации(
			НСтр("ru = 'Расчет этапа';
				|en = 'Step calculation'", ОбщегоНазначения.КодОсновногоЯзыка())
				+ "." + Справочники.ОперацииЗакрытияМесяца.ПолучитьИмяПредопределенного(ОписаниеВыполненияЭтапа.Операция)),
		УровеньЖурналаРегистрации.Информация,
		,
		,
		ТекстДляЖурнала);
	
КонецПроцедуры

#КонецОбласти

#Область ВыполняемыеРасчеты

Функция ПроверитьНаличиеАктивныхРасчетов(МассивОрганизаций = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Организации.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ВТОрганизации
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка В(&МассивОрганизаций)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыполнениеРасчета.Организация КАК Организация,
	|	ВыполнениеРасчета.ПериодРасчета КАК ПериодРасчета,
	|	ВыполнениеРасчета.Операция КАК Операция,
	|	ВыполнениеРасчета.ДатаНачала КАК ДатаНачала,
	|	ВыполнениеРасчета.ИнформацияОЗапускеРасчета КАК ИнформацияОЗапускеРасчета,
	|	ВыполнениеРасчета.ИдентификаторРасчета КАК ИдентификаторРасчета
	|ПОМЕСТИТЬ ВТВыполнениеРасчета
	|ИЗ
	|	РегистрСведений.ВыполнениеОперацийЗакрытияМесяца КАК ВыполнениеРасчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТОрганизации КАК Организации
	|		ПО ВыполнениеРасчета.Организация = Организации.Ссылка
	|ГДЕ
	|	ВыполнениеРасчета.РасчетВыполняется
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Организации.Ссылка КАК Ссылка
	|ИЗ
	|	ВТОрганизации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВыполнениеРасчета КАК ВыполнениеРасчета
	|		ПО Организации.Ссылка = ВыполнениеРасчета.Организация
	|ГДЕ
	|	ВыполнениеРасчета.Организация ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыполнениеРасчета.Организация КАК Организация,
	|	ВыполнениеРасчета.ПериодРасчета КАК ПериодРасчета,
	|	ВыполнениеРасчета.Операция КАК Операция,
	|	ВыполнениеРасчета.ДатаНачала КАК ДатаНачала,
	|	ВыполнениеРасчета.ИнформацияОЗапускеРасчета КАК ИнформацияОЗапускеРасчета,
	|	ВыполнениеРасчета.ИдентификаторРасчета КАК ИдентификаторРасчета
	|ИЗ
	|	ВТВыполнениеРасчета КАК ВыполнениеРасчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыполнениеРасчета.ИнформацияОЗапускеРасчета КАК ИнформацияОЗапускеРасчета,
	|	ВыполнениеРасчета.ИдентификаторРасчета КАК ИдентификаторРасчета
	|ИЗ
	|	ВТВыполнениеРасчета КАК ВыполнениеРасчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИнформацияОЗапускеРасчета";
	
	Запрос.УстановитьПараметр("МассивОрганизаций", ОрганизацииДляРасчета(МассивОрганизаций));
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	РасчетПоВсемОрганизациям = РезультатЗапроса[2].Пустой();
	Расчеты 				 = РезультатЗапроса[3].Выгрузить();
	РасчетыПоИдентификаторам = РезультатЗапроса[4].Выгрузить();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОписаниеРасчетов = "";
	
	Для Каждого ТекСтр Из РасчетыПоИдентификаторам Цикл
		ОписаниеРасчетов = ОписаниеРасчетов + Символы.ПС + "  - " + СокрЛП(ТекСтр.ИнформацияОЗапускеРасчета);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ЕстьАктивныеРасчеты", 		(Расчеты.Количество() > 0));
	Результат.Вставить("ТекстОшибки", 				"");
	Результат.Вставить("Расчеты", 		      		Расчеты);
	Результат.Вставить("РасчетПоВсемОрганизациям", 	СокрЛП(РасчетПоВсемОрганизациям));
	
	Если Результат.ЕстьАктивныеРасчеты Тогда
		
		ОдноЗадание = (РасчетыПоИдентификаторам.Количество() < 2);
		
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '%1 закрытия месяца:
				|%2
				|Выполнение операций закрытия месяца остановлено.';
				|en = '%1 month-end closing:
				|%2
				|Execution of month-end closing operations is stopped.'"),
			?(ОдноЗадание, НСтр("ru = 'Обнаружено активное задание';
								|en = 'Active job is found'"), НСтр("ru = 'Обнаружены активные задания';
																			|en = 'Active jobs are found'")),
			СокрЛП(ОписаниеРасчетов));
		
		Если ОбщегоНазначения.ИнформационнаяБазаФайловая()
		 И ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца) Тогда
			ТекстОшибки = ТекстОшибки + Символы.ПС
				+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Возможно %1 аварийно, в этом случае информацию об %2 необходимо актуализировать вручную.';
						|en = '%1 was aborted. Update information on %2 manually.'"),
					?(ОдноЗадание, НСтр("ru = 'задание завершено';
										|en = 'job is completed'"), НСтр("ru = 'задания завершены';
																		|en = 'jobs are completed'")),
					?(ОдноЗадание, НСтр("ru = 'активном задании';
										|en = 'active job'"), НСтр("ru = 'активных заданиях';
																		|en = 'active jobs'")));
		КонецЕсли;
		
		ТекстОшибки = ТекстОшибки + Символы.ПС
			+ СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Подробнее см. в форме ""%1"", ""Еще"" - ""Активные сеансы закрытия месяца""';
					|en = 'For more information, see the ""%1"" form, ""More"" - ""Active sessions of month-end closing""'"),
				Метаданные.Обработки.ОперацииЗакрытияМесяца.Формы.ЗакрытиеМесяца.Синоним);
		
		Результат.ТекстОшибки = ТекстОшибки;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ОбновитьСостояниеАктивныхРасчетов() Экспорт
	
	ЭтоФайловаяИБ = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	ЭтоПолноправныйПользователь = Пользователи.ЭтоПолноправныйПользователь(,, Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВыполнениеРасчета.ИдентификаторРасчета КАК ИдентификаторРасчета
	|ИЗ
	|	РегистрСведений.ВыполнениеОперацийЗакрытияМесяца КАК ВыполнениеРасчета
	|ГДЕ
	|	ВыполнениеРасчета.РасчетВыполняется";
	
	Расчеты = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекСтр Из Расчеты Цикл
		
		СостояниеЗадания = ЗакрытиеМесяцаСервер.ТекущееСостояниеФоновогоЗадания(
			ЗакрытиеМесяцаСервер.ИмяФоновогоЗадания(ТекСтр.ИдентификаторРасчета));
		
		Если НЕ СостояниеЗадания.Активно Тогда
			
			// Если задание расчета не найдено по GUID, то его нельзя считать завершенным:
			// - в файловой ИБ, т.к. платформа "не видит" фоновые задания другого сеанса
			// - если текущий пользователь не администратор, т.к. менеджер фоновых заданий не показывает такому пользователю
			// задания других пользователей.
			Если НЕ (СостояниеЗадания.НеНайдено И (ЭтоФайловаяИБ ИЛИ НЕ ЭтоПолноправныйПользователь))
			 ИЛИ СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации() Тогда
				УстановитьПризнакОкончанияРасчета(ТекСтр.ИдентификаторРасчета);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Расчеты.Количество();
	
КонецФункции


Функция УстановитьПризнакЗапускаРасчета(МассивОрганизаций, ИдентификаторРасчета,
			ИнформацияОЗапускеРасчета = "", ПериодРасчета = Неопределено, Операция = Неопределено) Экспорт
	
	Организации = ОрганизацииДляРасчета(МассивОрганизаций);
	
	ИсточникБлокировки = Новый ТаблицаЗначений;
	ИсточникБлокировки.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	
	Для Каждого ТекущаяОрганизация Из Организации Цикл
		ИсточникБлокировки.Добавить().Организация = ТекущаяОрганизация;
	КонецЦикла;
	
	ДатаНачала = ТекущаяДатаСеанса();
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыполнениеОперацийЗакрытияМесяца");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ИсточникБлокировки;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
		Блокировка.Заблокировать();		
		
		// Установить признак расчета (и запустить расчет) можно только если нет других активных расчетов по указанным организациям.
		АктивныеРасчеты = ПроверитьНаличиеАктивныхРасчетов(Организации);
		
		Если НЕ АктивныеРасчеты.ЕстьАктивныеРасчеты Тогда
			
			НаборЗаписей = РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.СоздатьНаборЗаписей();
			
			Для Каждого ТекущаяОрганизация Из Организации Цикл
				
				Запись = НаборЗаписей.Добавить();
				
				Запись.Организация 	        	 = ТекущаяОрганизация;
				Запись.РасчетВыполняется    	 = Истина;
				Запись.ИдентификаторРасчета 	 = ИдентификаторРасчета;
				Запись.ДатаНачала		    	 = ДатаНачала;
				Запись.ИнформацияОЗапускеРасчета = Формат(ДатаНачала, "ДЛФ=DT") + "; " + СокрЛП(ИнформацияОЗапускеРасчета);
				Запись.ПериодРасчета 	         = ПериодРасчета;
				Запись.Операция 	        	 = Операция;
				
			КонецЦикла;
			
			НаборЗаписей.Записать(Ложь);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ЗакрытиеМесяцаСервер.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Установка признака выполнения расчета';
					|en = 'Set the accounting performing indicator'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ИнформацияОбОшибке);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
	Возврат АктивныеРасчеты;
	
КонецФункции

Функция УстановитьПризнакОкончанияРасчета(ИдентификаторРасчета = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		// Сохраним информацию о всех остальных выполняемых на настоящее время расчетах кроме текущего (оконченного).
		// Это будет быстрее, чем по одной очищать записи по каждой рассчитанной организации,
		// тем более, что в подавляющем большинстве случаев остальных расчетов не будет - выполнится только одна запись пустого набора.
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ВыполнениеОперацийЗакрытияМесяца");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("РасчетВыполняется", Истина);
		Блокировка.Заблокировать();		
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ВыполнениеРасчета.Организация КАК Организация,
		|	ВыполнениеРасчета.ПериодРасчета КАК ПериодРасчета,
		|	ВыполнениеРасчета.Операция КАК Операция,
		|	ВыполнениеРасчета.РасчетВыполняется КАК РасчетВыполняется,
		|	ВыполнениеРасчета.ДатаНачала КАК ДатаНачала,
		|	ВыполнениеРасчета.ДатаОкончания КАК ДатаОкончания,
		|	ВыполнениеРасчета.БылиОшибки КАК БылиОшибки,
		|	ВыполнениеРасчета.Длительность КАК Длительность,
		|	ВыполнениеРасчета.ИдентификаторРасчета КАК ИдентификаторРасчета,
		|	ВыполнениеРасчета.ИнформацияОЗапускеРасчета КАК ИнформацияОЗапускеРасчета
		|ИЗ
		|	РегистрСведений.ВыполнениеОперацийЗакрытияМесяца КАК ВыполнениеРасчета
		|ГДЕ
		|	ВыполнениеРасчета.РасчетВыполняется
		|	И ВыполнениеРасчета.ИдентификаторРасчета <> &ИдентификаторРасчета
		|	И НЕ &ОкончаниеВсехРасчетов";
		
		Запрос.УстановитьПараметр("ИдентификаторРасчета",  ?(ИдентификаторРасчета = Неопределено, Новый УникальныйИдентификатор, ИдентификаторРасчета));
		Запрос.УстановитьПараметр("ОкончаниеВсехРасчетов", ИдентификаторРасчета = Неопределено);
		
		Запрос.Выполнить().Выгрузить();
		
		НаборЗаписей = РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.РасчетВыполняется.Установить(Истина);
		
		НаборЗаписей.Загрузить(Запрос.Выполнить().Выгрузить());
		
		НаборЗаписей.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ЗаписьЖурналаРегистрации(
			ЗакрытиеМесяцаСервер.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Установка признака выполнения расчета';
					|en = 'Set the accounting performing indicator'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			ИнформацияОбОшибке);
		
		ВызватьИсключение ИнформацияОбОшибке;
		
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

Функция ОбновитьПризнакЗапускаРасчета(МассивОрганизаций, ИдентификаторРасчета,
			ИнформацияОЗапускеРасчета = "", ПериодРасчета = Неопределено, Операция = Неопределено) Экспорт
	
	УстановитьПризнакОкончанияРасчета(ИдентификаторРасчета);
	
	АктивныеРасчеты = УстановитьПризнакЗапускаРасчета(
		МассивОрганизаций,
		ИдентификаторРасчета,
		ИнформацияОЗапускеРасчета,
		ПериодРасчета,
		Операция);
	
	Если АктивныеРасчеты.ЕстьАктивныеРасчеты Тогда
		ВызватьИсключение АктивныеРасчеты.ТекстОшибки;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.25.62";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("7716a37d-637b-4cf0-9b3e-33816ccc95c2");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления =
		"РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий =
		НСтр("ru = 'Заполнение измерения ""Операция"" регистра сведений ""Выполнение операций закрытия месяца"".';
			|en = 'Fill in the ""Operation"" dimension of the ""Perform month-end closing operations"" information register.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.ПолноеИмя());
	
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПустойНабор = СоздатьНаборЗаписей();
	ПолноеИмяРегистра = ПустойНабор.Метаданные().ПолноеИмя();
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Регистр.УдалитьОперация КАК УдалитьОперация,
	|	Регистр.Организация КАК Организация,
	|	Регистр.ПериодРасчета КАК ПериодРасчета,
	|	Регистр.РасчетВыполняется КАК РасчетВыполняется
	|ИЗ
	|	РегистрСведений.ВыполнениеОперацийЗакрытияМесяца КАК Регистр
	|ГДЕ
	|	УдалитьОперация <> ЗНАЧЕНИЕ(Перечисление.УдалитьОперацииЗакрытияМесяца.ПустаяСсылка)
	|	И Регистр.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииЗакрытияМесяца.ПустаяСсылка)";
	
	Результат = Запрос.Выполнить().Выбрать();

	Порция = ПустойНабор.Выгрузить(, "УдалитьОперация, Организация, ПериодРасчета, РасчетВыполняется");
	Счетчик = 0;
	
	Выборка = Результат;
	Пока Выборка.Следующий() Цикл
		Если Счетчик = 1000 Тогда
			ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Порция, ДополнительныеПараметры);
			Порция.Очистить();
			Счетчик = 0;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(Порция.Добавить(), Выборка);
		Счетчик = Счетчик + 1;
	КонецЦикла;

	Если Порция.Количество() > 0 Тогда
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Порция, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	МетаданныеРегистра = СоздатьНаборЗаписей().Метаданные();
	ПолноеИмяОбъекта = МетаданныеРегистра.ПолноеИмя();
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;

	Для каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
	
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ВыполнениеОперацийЗакрытияМесяца");
			ЭлементБлокировки.УстановитьЗначение("УдалитьОперация", Выборка.УдалитьОперация);
			ЭлементБлокировки.УстановитьЗначение("Организация", Выборка.Организация);
			ЭлементБлокировки.УстановитьЗначение("ПериодРасчета", Выборка.ПериодРасчета);
			ЭлементБлокировки.УстановитьЗначение("РасчетВыполняется", Выборка.РасчетВыполняется);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ВыполнениеОперацийЗакрытияМесяца.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.УдалитьОперация.Установить(Выборка.УдалитьОперация);
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.ПериодРасчета.Установить(Выборка.ПериодРасчета);
			НаборЗаписей.Отбор.РасчетВыполняется.Установить(Выборка.РасчетВыполняется);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Если НЕ ЗначениеЗаполнено(Запись.Операция) Тогда
					Запись.Операция = ОбщегоНазначения.ПредопределенныйЭлемент(
						"Справочник.ОперацииЗакрытияМесяца." + XMLСтрока(Запись.УдалитьОперация));
			КонецЕсли;
			КонецЦикла;
			
			Если НаборЗаписей.Модифицированность() Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Не удалось обработать данные в регистре ""Выполнение операций закрытия месяца""
					| по набору записей ""%1"", по причине: %2';
					|en = 'Cannot process the data in the ""Perform month-end closing operations""
					| register for the ""%1"" record set. Reason: %2'"),
					Строка(НаборЗаписей.Отбор),
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				МетаданныеРегистра,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти


#Область Прочие

Функция ОрганизацииДляРасчета(МассивОрганизаций)
	
	Организации = ?(НЕ ЗначениеЗаполнено(МассивОрганизаций),
		Справочники.Организации.ДоступныеОрганизации(),
		ОбщегоНазначенияУТКлиентСервер.Массив(МассивОрганизаций));
	
	Возврат Организации;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
