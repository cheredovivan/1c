#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Добавляет задания к расчету структуры заказа на производство
// 
// Параметры:
// 	Таблица - ТаблицаЗначений - задания к расчету
// 	СвернутьТаблицу - Булево - признак, необходимо свернуть таблицу перед записью заданий.
// 
Процедура ДобавитьЗадания(Таблица, СвернутьТаблицу = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СвернутьТаблицу Тогда
		
		Колонки = "ЗаказНаПроизводство";
		Если Таблица.Колонки.Найти("КлючПартия") <> Неопределено Тогда
			Колонки = Колонки + ", КлючПартия";
		КонецЕсли;
		Если Таблица.Колонки.Найти("ЭтапПроизводства") <> Неопределено Тогда
			Колонки = Колонки + ", ЭтапПроизводства";
		КонецЕсли;
		Если Таблица.Колонки.Найти("Очередь") <> Неопределено Тогда
			Колонки = Колонки + ", Очередь";
		КонецЕсли;
		Таблица.Свернуть(Колонки);
		
	КонецЕсли;
	
	СтруктураЗаказа.ЗаполнитьИЗаписатьНаборЗаписей(
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей(),
		Таблица,
		Ложь,
		Истина);
	
КонецПроцедуры

Процедура ДобавитьЗадание(ЗаказНаПроизводство, ЭтапПроизводства = Неопределено, КлючПартия = Неопределено, ЗапуститьРасчет = Ложь) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("ЗаказНаПроизводство", Новый ОписаниеТипов("ДокументСсылка.ЗаказНаПроизводство2_2"));
	
	НоваяСтрока = Таблица.Добавить();
	НоваяСтрока.ЗаказНаПроизводство = ЗаказНаПроизводство;
	
	Если ЭтапПроизводства <> Неопределено Тогда
		Таблица.Колонки.Добавить("ЭтапПроизводства", Новый ОписаниеТипов("ДокументСсылка.ЭтапПроизводства2_2"));
		НоваяСтрока.ЭтапПроизводства = ЭтапПроизводства;
	КонецЕсли;
	
	Если КлючПартия <> Неопределено Тогда
		Таблица.Колонки.Добавить("КлючПартия", Новый ОписаниеТипов("УникальныйИдентификатор"));
		НоваяСтрока.КлючПартия = КлючПартия;
	КонецЕсли;
	
	СтруктураЗаказа.ЗаполнитьИЗаписатьНаборЗаписей(
		РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей(),
		Таблица,
		Ложь,
		Истина);
	
	Если ЗапуститьРасчет Тогда
		РегистрыСведений.НормативныйГрафикСтруктурыЗаказа.ЗапуститьРасчет();
	КонецЕсли;
	
КонецПроцедуры 

Процедура ДобавитьЗаданияИзМенеджераВременныхТаблиц(МенеджерВременныхТаблиц, ИмяВременнойТаблицы, ЗаказНаПроизводство, ЭтапПроизводства, КлючПартия, ТекстОтбора = "") Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|
	|	&ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|	&ЭтапПроизводства      КАК ЭтапПроизводства,
	|	&КлючПартия            КАК КлючПартия
	|ИЗ
	|	ИмяВременнойТаблицы КАК ИмяВременнойТаблицы
	|ГДЕ
	|	&ТекстОтбора
	|";
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИмяВременнойТаблицы",  ИмяВременнойТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЗаказНаПроизводство", ЗаказНаПроизводство);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ЭтапПроизводства",    ЭтапПроизводства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КлючПартия",          КлючПартия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстОтбора", ?(ПустаяСтрока(ТекстОтбора), "ИСТИНА", ТекстОтбора));
	
	Запрос.Текст = ТекстЗапроса;

	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		СтруктураЗаказа.ЗаполнитьИЗаписатьНаборЗаписей(
			РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей(),
			РезультатЗапроса.Выгрузить(),
			Ложь,
			Истина);
		
	КонецЕсли;
	
КонецПроцедуры

// Возвращает факт наличия заданий на расчет графика по данному этапу производства.
//
// Параметры:
//  ЭтапПроизводства - ДокументСсылка.ЭтапПроизводства2_2.
// 
// Возвращаемое значение:
//  Булево.
//
Функция ЕстьЗадания(ЭтапПроизводства) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Задания
		|ГДЕ
		|	Задания.ЭтапПроизводства = &ЭтапПроизводства");
	Запрос.УстановитьПараметр("ЭтапПроизводства", ЭтапПроизводства);
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

// Удаляет задания по этапу.
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2.
//  ЭтапПроизводства - ДокументСсылка.ЭтапПроизводства2_2.
//
Процедура УдалитьЗаданияПоЭтапу(ЗаказНаПроизводство, ЭтапПроизводства) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
	Набор.Отбор.ЭтапПроизводства.Установить(ЭтапПроизводства);
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
