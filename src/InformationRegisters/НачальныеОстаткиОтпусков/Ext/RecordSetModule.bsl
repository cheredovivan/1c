#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	УдалитьЗаписиЗаработанныхОтпусков(ЭтотОбъект, Замещение);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти 

#Область ВспомогательныеПроцедурыИФункции

Процедура УдалитьЗаписиЗаработанныхОтпусков(НаборЗаписей, Замещение)
	
	// Подготовка таблицы изменившихся записей.
	// В отличие от других источников вторичного регистра - изменение ввода остатков требует полного пересчета прав.
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЗ.Регистратор КАК Регистратор,
		|	ВЫРАЗИТЬ(ТЗ.Сотрудник КАК Справочник.Сотрудники) КАК Сотрудник,
		|	ВЫРАЗИТЬ(ТЗ.ВидЕжегодногоОтпуска КАК Справочник.ВидыОтпусков) КАК ВидЕжегодногоОтпуска,
		|	НАЧАЛОПЕРИОДА(ТЗ.ДатаОстатка, ДЕНЬ) КАК ДатаОстатка,
		|	НАЧАЛОПЕРИОДА(ТЗ.РабочийГодНачало, ДЕНЬ) КАК РабочийГодНачало,
		|	НАЧАЛОПЕРИОДА(ТЗ.РабочийГодОкончание, ДЕНЬ) КАК РабочийГодОкончание,
		|	ТЗ.КоличествоДней КАК КоличествоДней
		|ПОМЕСТИТЬ НовыйНабор
		|ИЗ
		|	&ТЗ КАК ТЗ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НачальныеОстаткиОтпусков.Регистратор КАК Регистратор,
		|	НачальныеОстаткиОтпусков.Сотрудник КАК Сотрудник,
		|	НачальныеОстаткиОтпусков.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	НАЧАЛОПЕРИОДА(НачальныеОстаткиОтпусков.ДатаОстатка, ДЕНЬ) КАК ДатаОстатка,
		|	НАЧАЛОПЕРИОДА(НачальныеОстаткиОтпусков.РабочийГодНачало, ДЕНЬ) КАК РабочийГодНачало,
		|	НАЧАЛОПЕРИОДА(НачальныеОстаткиОтпусков.РабочийГодОкончание, ДЕНЬ) КАК РабочийГодОкончание,
		|	НачальныеОстаткиОтпусков.КоличествоДней КАК КоличествоДней
		|ПОМЕСТИТЬ СтарыйНабор
		|ИЗ
		|	РегистрСведений.НачальныеОстаткиОтпусков КАК НачальныеОстаткиОтпусков
		|ГДЕ
		|	НачальныеОстаткиОтпусков.Регистратор В(&Регистраторы)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(НовыйНабор.Сотрудник, СтарыйНабор.Сотрудник) КАК Сотрудник,
		|	ЕСТЬNULL(НовыйНабор.ВидЕжегодногоОтпуска, СтарыйНабор.ВидЕжегодногоОтпуска) КАК ВидЕжегодногоОтпуска,
		|	ЕСТЬNULL(НовыйНабор.ДатаОстатка, СтарыйНабор.ДатаОстатка) КАК ДатаОстатка,
		|	ВЫБОР
		|		КОГДА ЕСТЬNULL(НовыйНабор.КоличествоДней, 0) <> ЕСТЬNULL(СтарыйНабор.КоличествоДней, 0)
		|			ТОГДА ИСТИНА
		|		КОГДА ЕСТЬNULL(НовыйНабор.РабочийГодОкончание, ДАТАВРЕМЯ(1, 1, 1)) <> ЕСТЬNULL(СтарыйНабор.РабочийГодОкончание, ДАТАВРЕМЯ(1, 1, 1))
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК БылиИзменения
		|ПОМЕСТИТЬ СравнительныйНабор
		|ИЗ
		|	НовыйНабор КАК НовыйНабор
		|		ПОЛНОЕ СОЕДИНЕНИЕ СтарыйНабор КАК СтарыйНабор
		|		ПО НовыйНабор.Сотрудник = СтарыйНабор.Сотрудник
		|			И НовыйНабор.Регистратор = СтарыйНабор.Регистратор
		|			И НовыйНабор.ВидЕжегодногоОтпуска = СтарыйНабор.ВидЕжегодногоОтпуска
		|			И НовыйНабор.ДатаОстатка = СтарыйНабор.ДатаОстатка
		|			И НовыйНабор.РабочийГодНачало = СтарыйНабор.РабочийГодНачало
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	СравнительныйНабор.Сотрудник КАК Сотрудник,
		|	СравнительныйНабор.ВидЕжегодногоОтпуска КАК ВидЕжегодногоОтпуска,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК Период
		|ИЗ
		|	СравнительныйНабор КАК СравнительныйНабор
		|ГДЕ
		|	СравнительныйНабор.БылиИзменения = ИСТИНА";
	
	ТаблицаНовыхДвижений = НаборЗаписей.Выгрузить(,"Регистратор,Сотрудник,ВидЕжегодногоОтпуска,ДатаОстатка,РабочийГодНачало,РабочийГодОкончание,КоличествоДней");
	Если НаборЗаписей.Отбор.Найти("Регистратор") <> Неопределено И НаборЗаписей.Отбор.Регистратор.Использование Тогда 
		ТаблицаНовыхДвижений.ЗаполнитьЗначения(НаборЗаписей.Отбор.Регистратор.Значение, "Регистратор"); 
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Регистраторы", РегистрыБЗК.ЗначенияОтбораЗаписейНабора(НаборЗаписей, Замещение, "Регистратор"));
	Запрос.УстановитьПараметр("ТЗ", ТаблицаНовыхДвижений);
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	ОстаткиОтпусков.УдалитьЗаписиЗаработанныхОтпусков(ТаблицаЗапроса);

КонецПроцедуры

#КонецОбласти 

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли