#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Отражает изменения состояний заказов в регистре.
//
// Параметры:
//  Заказы - Массив из ДокументСсылка.ЗаказНаПроизводство2_2 - массив отражаемых заказов.
//  Ошибки - Неопределено, Соответствие из КлючИЗначение - со структурой:
//                          *Ключ     - ДокументСсылка -
//                          *Значение - Строка - Подробное представление ошибки.
//
Процедура ОтразитьСостоянияЗаказов(Заказы, Ошибки = Неопределено) Экспорт
	
	ЗаказыПоТипам = ОбщегоНазначенияУТ.СоответствиеМассивовПоТипамОбъектов(Заказы);
	
	Для каждого ЗаказыПоТипу Из ЗаказыПоТипам Цикл
		
		Если ЗаказыПоТипу.Ключ = "Документ.ЗаказНаПроизводство2_2" Тогда
			
			Выборка = ВыборкаИзменений(ЗаказыПоТипу.Значение);
			ЗаписатьСостояния(Выборка, Ошибки);
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Дополняет текст запроса механизма расчета состояний.
// 
// Параметры:
// 	Запрос - Запрос - используется для установки параметров запроса.
// 
// Возвращаемое значение:
//	Соответствие из Строка - соответствие имен таблиц изменения регистров и текстов запросов.
//	
Функция СоответствиеЗапросовКонтрольнымРегистрам(Запрос) Экспорт

	СоответствиеТекстовЗапросов = Новый Соответствие;
	
	СоответствиеТекстовЗапросов.Вставить(
		"ДвиженияЗаказыНаПроизводствоИзменения",
		ТекстЗапросаЗаказыНаПроизводство(Запрос));
	
	Возврат СоответствиеТекстовЗапросов;
	
КонецФункции

// Добавляет задания к отражению состояния заказов.
// 
// Параметры:
//  Источник       - см. СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов.Источник
//  Заказы         - см. СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов.ОтражаемыеДокументы
//  Действие       - см. СостоянияДокументов.ДобавитьЗаданияКОтражениюСостоянияЗаказов.Действие
//  ТаблицаЗаданий - см. ОтложенныеЗадания.ДобавитьЗаданияВОчередь.ТаблицаЗаданий
//
Процедура ДобавитьЗаданияКОтражениюСостоянияЗаказов(
			Источник,
			Заказы,
			Действие = "",
			ТаблицаЗаданий = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТипыЗаказов = ТипыЗаказовДляОтраженияСостояний();
	
	Если ТипЗнч(Заказы) = Тип("Массив") Тогда
		
		ИсточникСсылка = ?(Источник = Неопределено, Неопределено, Источник.Ссылка);
		
		Если ИсточникСсылка <> Неопределено
			И ТипыЗаказов.Найти(ТипЗнч(ИсточникСсылка)) <> Неопределено
			И Заказы.Найти(ИсточникСсылка) = Неопределено Тогда
				РегистрыСведений.ЗаданияКОтражениюСостоянияЗаказов.ДобавитьЗадания(
					ИсточникСсылка,
					Действие,
					ТаблицаЗаданий);
		КонецЕсли;
		
		Для каждого Заказ Из Заказы Цикл
			
			Если ТипыЗаказов.Найти(ТипЗнч(Заказ)) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
				
			РегистрыСведений.ЗаданияКОтражениюСостоянияЗаказов.ДобавитьЗадания(Заказ, Действие, ТаблицаЗаданий);
			
		КонецЦикла;
		
	ИначеЕсли ТипыЗаказов.Найти(ТипЗнч(Заказы)) <> Неопределено Тогда
		
		РегистрыСведений.ЗаданияКОтражениюСостоянияЗаказов.ДобавитьЗадания(Заказы, Действие, ТаблицаЗаданий);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Возвращает типы заказов по которым нужно отразить состояния.
//
// Возвращаемое значение:
//  Массив из Тип - Типы заказов отражения состояний
//
Функция ТипыЗаказовДляОтраженияСостояний() Экспорт
	
	ТипыЗаказов = Новый Массив;
	ТипыЗаказов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство2_2"));
	
	Возврат ТипыЗаказов;
	
КонецФункции

// Возвращает таблицу действий для отражения состояния заказов.
// Подробное описание в функции СостоянияДокументов.ИнициализироватьТаблицуДействийДляОтраженияСостоянияЗаказов.
// 
// Возвращаемое значение:
//  - Неопределено,
//  - см. СостоянияДокументов.ИнициализироватьТаблицуДействийДляОтраженияСостоянияЗаказов
//
Функция ДействияДляОтраженияСостоянияЗаказов() Экспорт
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ТекстыЗапросовПоКонтрольнымРегистрам

Функция ТекстЗапросаЗаказыНаПроизводство(Запрос)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство КАК ОтражаемыйДокумент
	|ИЗ
	|	ДвиженияЗаказыНаПроизводствоИзменения КАК Таблица
	|";
	
	СтруктураТекстовЗапросов = ЗакрытиеМесяцаСервер.ИнициализироватьСтруктуруТекстовЗапросов(ТекстЗапроса);
	
	Возврат СтруктураТекстовЗапросов;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ВыборкаИзменений(МассивСсылок)

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("МассивСсылок", МассивСсылок);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(Таблица.ДинамическаяСтруктура) КАК ЕстьЗаказыДинамическаяСтруктура,
		|	МАКСИМУМ(НЕ Таблица.ДинамическаяСтруктура) КАК ЕстьЗаказыСтатическаяСтруктура
		|ИЗ
		|	Документ.ЗаказНаПроизводство2_2 КАК Таблица
		|ГДЕ
		|	Таблица.Ссылка В(&МассивСсылок)";
	
	ВыборкаТипыЗаказов = Запрос.Выполнить().Выбрать();
	ВыборкаТипыЗаказов.Следующий();

	Запрос.Текст = Документы.ЗаказНаПроизводство2_2.ТекстЗапросаДляРасчетаСостоянийЗаказов(ВыборкаТипыЗаказов);
		
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	Запрос.УстановитьПараметр("ИспользуетсяГрафикПроизводства", УправлениеПроизводствомПовтИсп.ИспользуетсяГрафикПроизводства());
	Запрос.УстановитьПараметр("ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров", Константы.ДопустимоеОтклонениеОтгрузкиПриемкиМерныхТоваров.Получить());

	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция ЗаписатьСостояния(Выборка, Ошибки = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Пока Выборка.Следующий() Цикл
		
		Набор = РегистрыСведений.СостоянияЗаказовНаПроизводство.СоздатьНаборЗаписей();
		Набор.Отбор.Заказ.Установить(Выборка.Заказ);
		
		Если Выборка.АктивныйСтатус Тогда 
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЕсли;
		
		Попытка
			Набор.Записать(Истина);
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось отразить состояние заказа по причине: %Причина%';
									|en = 'Cannot record state of order. Reason: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
				Выборка.Заказ.Метаданные(), Выборка.Заказ, ТекстСообщения);
			
			Если Ошибки <> Неопределено Тогда
				Ошибки.Вставить(Выборка.Заказ, ТекстСообщения);
			КонецЕсли;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецФункции

Функция СобытиеЖурналаРегистрации()
	
	Возврат НСтр("ru = 'Межцеховое управление';
				|en = 'Production scheduling and controlling'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()) + "."
			+ НСтр("ru = 'Ошибка отражения состояния заказов в очереди';
					|en = 'Error recording order states in the queue'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли
