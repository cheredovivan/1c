// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет запись в регистр
// Параметры:
//  Подписант - СправочникСсылка.ПодписантыСервисаМобильнойПодписи - Подписант
//  Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - Сертификат
//
Процедура ДобавитьЗапись(Подписант, Сертификат) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СертификатыПодписантовМобильнойПодписиБЭД");
		ЭлементБлокировкиДанных.УстановитьЗначение("Подписант", Подписант);
		ЭлементБлокировкиДанных.УстановитьЗначение("Сертификат", Сертификат);
		ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
		БлокировкаДанных.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.Подписант = Подписант;
		Запись.Сертификат = Сертификат;
		Запись.Записать(Истина);

		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		Информация = ИнформацияОбОшибке();
		
		ЭлектронноеВзаимодействие.ОбработатьОшибку(НСтр("ru = 'Добавление сертификата подписанта';
														|en = 'Add the signer certificate'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(Информация),
			СтрШаблон(НСтр("ru = 'Не удалось записать сертификат подписанта: %1';
							|en = 'Cannot save the signer certificate: %1'"), Подписант));
			
	КонецПопытки;
	
КонецПроцедуры



#КонецОбласти

#КонецЕсли
