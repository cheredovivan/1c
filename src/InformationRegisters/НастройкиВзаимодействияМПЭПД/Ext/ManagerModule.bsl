#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Параметры записи регистра.
// 
// Параметры:
//  ИдентификаторЭДО - Строка - Идентификатор ЭДО.
// 
// Возвращаемое значение:
//  Структура - Параметры записи регистра:
// * ИдентификаторЭДО - Строка
// * Организация - Неопределено
// * ТокенДоступа - Неопределено
// * ТокенОбновления - Неопределено
// * СрокДействияТокена - Неопределено
// * ДатаОпросаДокументов - Неопределено
// * ТочкаОпросаСобытий - Неопределено
// * ТочкаОпросаУведомлений - Неопределено
// * НастройкиФорм - Неопределено
// * Найден - Булево
Функция ПараметрыЗаписиРегистра(ИдентификаторЭДО) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	Результат.Вставить("Организация", Неопределено);
	Результат.Вставить("ТокенДоступа", Неопределено);
	Результат.Вставить("ТокенОбновления", Неопределено);
	Результат.Вставить("СрокДействияТокена", Неопределено);
	Результат.Вставить("ДатаОпросаДокументов", Неопределено);
	Результат.Вставить("ТочкаОпросаСобытий", Неопределено);
	Результат.Вставить("ТочкаОпросаУведомлений", Неопределено);
	Результат.Вставить("НастройкиФорм", Неопределено);
	Результат.Вставить("Найден", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Регистрировать запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Текущая запись:
// * ИдентификаторЭДО - Строка 
// * Организация - Неопределено 
// * ТокенДоступа - Неопределено 
// * ТокенОбновления - Неопределено 
// * СрокДействияТокена - Неопределено 
// * ДатаОпросаДокументов - Неопределено 
// * ТочкаОпросаСобытий - Неопределено 
// * ТочкаОпросаУведомлений - Неопределено 
// * НастройкиФорм - Неопределено 
// * Найден - Булево 
Процедура РегистрироватьЗапись(ТекущаяЗапись) Экспорт
	
	НашлиЗапись = НайтиЗапись(ТекущаяЗапись);
	СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(НашлиЗапись, ТекущаяЗапись);

	ЗаписьРегистра = СоздатьМенеджерЗаписи();
	СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(ЗаписьРегистра, НашлиЗапись);
	
	Если ТипЗнч(НашлиЗапись.ТокенДоступа) = Тип("Строка") Тогда
		ЗаписьРегистра.ТокенДоступа = Новый ХранилищеЗначения(НашлиЗапись.ТокенДоступа);
	КонецЕсли;
	
	Если ТипЗнч(НашлиЗапись.ТокенОбновления) = Тип("Строка") Тогда
		ЗаписьРегистра.ТокенОбновления = Новый ХранилищеЗначения(НашлиЗапись.ТокенОбновления);
	КонецЕсли;

	ЗаписьРегистра.Записать(Истина);
	
КонецПроцедуры

// Удалить запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Структура с данными измерений регистра.
Процедура УдалитьЗапись(ТекущаяЗапись) Экспорт
	
	НашлиЗапись = НайтиЗапись(ТекущаяЗапись);
	Если НашлиЗапись.Найден Тогда
		ЗаписьРегистра = СоздатьМенеджерЗаписи();
		СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(ЗаписьРегистра, НашлиЗапись);
		ЗаписьРегистра.Удалить();
	КонецЕсли;
	
КонецПроцедуры

// Найти запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Структура с данными измерений регистра. 
// 
// Возвращаемое значение:
//  Структура - Найденная запись:
// * ИдентификаторЭДО - Строка 
// * Организация - Неопределено, ОпределяемыйТип.Организация 
// * ТокенДоступа - Неопределено, Строка 
// * ТокенОбновления - Неопределено, Строка 
// * СрокДействияТокена - Неопределено, Дата 
// * ДатаОпросаДокументов - Неопределено, Дата 
// * ТочкаОпросаСобытий - Неопределено, Число 
// * ТочкаОпросаУведомлений - Неопределено, Число 
// * НастройкиФорм - Неопределено, ХранилищеЗначения
// * Найден - Булево
//  
//@skip-check doc-comment-field-type-strict
//@skip-check doc-comment-field-type
//@skip-check doc-comment-description-ends-on-dot
Функция НайтиЗапись(ТекущаяЗапись) Экспорт
	
	Результат = ПараметрыЗаписиРегистра(ТекущаяЗапись.ИдентификаторЭДО);
	
	ЗаписьРегистра = СоздатьМенеджерЗаписи();
	ЗаписьРегистра.ИдентификаторЭДО = ТекущаяЗапись.ИдентификаторЭДО;
	ЗаписьРегистра.Прочитать();
	Если ЗаписьРегистра.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(Результат, ЗаписьРегистра);
		Результат.ТокенДоступа = Результат.ТокенДоступа.Получить();
		Результат.ТокенОбновления = Результат.ТокенОбновления.Получить();
		Результат.Найден = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращает настройки форм для идентификатора ЭДО.
//
// Параметры:
//  ИдентификаторыЭДО - Строка, Массив из Строка - 
// 
// Возвращаемое значение:
//  Соответствие из Строка, Структура - Структура с настройками форм, 
//  									см. СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию().
//										Соответствие если передан массив в ИдентификаторыЭДО:
//  * ЭлектроннаяТранспортнаяНакладная - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число
//  * ЭлектронныйПутевойЛист - Структура:
//  ** ИндексыРаботОтображаемыеМП - Соответствие Из Число 
//
//@skip-check doc-comment-description-ends-on-dot
Функция НастройкиФорм(ИдентификаторыЭДО) Экспорт
	
	ВернутьСоответствие = ТипЗнч(ИдентификаторыЭДО) = Тип("Массив");
	
	Если (ВернутьСоответствие И ИдентификаторыЭДО.Количество() = 0)
		Или (Не ВернутьСоответствие И ПустаяСтрока(ИдентификаторыЭДО)) Тогда
		Возврат НастройкиФормПоУмолчаниюПоИдентификаторам(ИдентификаторыЭДО);
	КонецЕсли;
	
	Если ВернутьСоответствие Тогда
		Результат = Новый Соответствие;
		МассивИдентфикаторов = ИдентификаторыЭДО;
	Иначе
		Результат = Неопределено;
		МассивИдентфикаторов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторыЭДО);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НастройкиВзаимодействияМПЭПД.НастройкиФорм КАК НастройкиФорм,
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО КАК ИдентификаторЭДО
	|ИЗ
	|	РегистрСведений.НастройкиВзаимодействияМПЭПД КАК НастройкиВзаимодействияМПЭПД
	|ГДЕ
	|	НастройкиВзаимодействияМПЭПД.ИдентификаторЭДО В(&МассивИдентфикаторов)";
	Запрос.УстановитьПараметр("МассивИдентфикаторов", МассивИдентфикаторов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Возврат НастройкиФормПоУмолчаниюПоИдентификаторам(ИдентификаторыЭДО);
	КонецЕсли;
	
	СтруктураПоиска = Новый Структура("ИдентификаторЭДО");
	Для Каждого ИдентификаторЭДО Из МассивИдентфикаторов Цикл
		СтруктураПоиска.ИдентификаторЭДО = ИдентификаторЭДО;
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(СтруктураПоиска) Тогда
			НастройкиФорм = Выборка.НастройкиФорм.Получить();
		Иначе
			НастройкиФорм = Неопределено;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(НастройкиФорм) Тогда
			НастройкиФорм = СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию();
		КонецЕсли;
		
		Если ВернутьСоответствие Тогда
			Результат.Вставить(ИдентификаторЭДО, НастройкиФорм);
		Иначе
			Возврат НастройкиФорм;
		КонецЕсли;
	КонецЦикла;
		
	Возврат Результат;
	
КонецФункции

// Сохраняетт настройки форм для идентифкатора ЭДО.
//
// Параметры:
//  ИдентификаторЭДО		 - Строка	 - 
//  СтруктураНастроекФорм	 - Структура - Структура с настройками форм, см. НастройкиФормПоУмолчанию().
//
Процедура СохранитьНастройкиФорм(ИдентификаторЭДО, СтруктураНастроекФорм) Экспорт
	
	НоваяЗапись = ПараметрыЗаписиРегистра(ИдентификаторЭДО);
	НоваяЗапись.НастройкиФорм = Новый ХранилищеЗначения(СтруктураНастроекФорм);
	РегистрироватьЗапись(НоваяЗапись);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиФормПоУмолчаниюПоИдентификаторам(ИдентификаторыЭДО)
	
	ВернутьСоответствие = ТипЗнч(ИдентификаторыЭДО) = Тип("Массив");
	
	Если ВернутьСоответствие Тогда
		Соответствие = Новый Соответствие;
		Для Каждого Идентификатор Из ИдентификаторыЭДО Цикл
			НастройкиФормПоУмолчанию = СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию();
			Соответствие.Вставить(Идентификатор, НастройкиФормПоУмолчанию);
		КонецЦикла;
		
		Возврат Соответствие;
	Иначе
		Возврат СервисВзаимодействияМПЭПДКлиентСервер.НастройкиФормПоУмолчанию();
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецЕсли