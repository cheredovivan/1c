#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область СтатусГрафикаКонстанты

// Возвращает код статуса рабочего графика.
// 
// 
// Возвращаемое значение:
//  Число - статус графика.
//
Функция СтатусРабочийГрафик() Экспорт
	
	Возврат 0;
	
КонецФункции

// Возвращает код статуса предварительного графика. Предварительный график - это рассчитанный,
// но еще не записанный пользователем график производства заказа. После записи график переходит
// в статус "Рабочий" (см. СтатусРабочийГрафик).
// 
// 
// Возвращаемое значение:
//  Число - статус графика.
//
Функция СтатусПредварительныйГрафик() Экспорт
	
	Возврат 1;
	
КонецФункции

// Возвращает код статуса модели графика. Модель графика рассчитывается без учета одного или нескольких
// ограничений и не может быть сохранена как рабочий график.
// 
// 
// Возвращаемое значение:
//  Число - статус графика.
//
Функция СтатусМодельГрафика() Экспорт
	
	Возврат 2;
	
КонецФункции

#КонецОбласти

#Область Запись

// Записывает результаты планирования графика распоряжения в регистр.
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - заказ, график производства которого необходимо записать.
//  СтатусГрафика - Число - статус графика.
//  ГрафикПроизводства - ТаблицаЗначений - данные графика для записи. Состав колонок соответствует
//		составу измерений/ресурсов/реквизитов регистра.
//
Процедура ЗаписатьРезультатыПланирования(ЗаказНаПроизводство, СтатусГрафика, ГрафикПроизводства) Экспорт
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
	Набор.Отбор.СтатусГрафика.Установить(СтатусГрафика);
	
	Если СтатусГрафика = СтатусРабочийГрафик() Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ГрафикЭтаповПроизводства2_2.*
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
		|ГДЕ
		|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусГрафика
		|	И НЕ ГрафикЭтаповПроизводства2_2.Этап В (&Этапы)");
		Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
		Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
		Запрос.УстановитьПараметр("Этапы", ГрафикПроизводства.ВыгрузитьКолонку("Этап"));
		
		Набор.Загрузить(Запрос.Выполнить().Выгрузить());
		
	КонецЕсли;
	
	Для каждого Строка Из ГрафикПроизводства Цикл
		
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		Запись.ЗаказНаПроизводство = ЗаказНаПроизводство;
		Запись.СтатусГрафика = СтатусГрафика;
		Запись.ВидСтроки = Перечисления.ВидыСтрокГрафикаПроизводства.Этап;
		
	КонецЦикла;
	
	Набор.Записать();
	
КонецПроцедуры

// Изменяет статус предварительного графика на рабочий. Вместе с записью рабочего графика
// происходит очистка данных предварительного графика и модели.
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - распоряжение, график которого необходимо записать.
//
Процедура ЗаписатьПредварительныйГрафикВРабочий(ЗаказНаПроизводство) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство,
	|	ГрафикЭтаповПроизводства2_2.Этап,
	|	ГрафикЭтаповПроизводства2_2.Начало,
	|	ГрафикЭтаповПроизводства2_2.Окончание,
	|	ГрафикЭтаповПроизводства2_2.ОкончаниеПредварительногоБуфера,
	|	ГрафикЭтаповПроизводства2_2.НачалоЗавершающегоБуфера,
	|	ГрафикЭтаповПроизводства2_2.НачалоСледующегоЭтапа,
	|	ГрафикЭтаповПроизводства2_2.НаКритическомПути,
	|	ГрафикЭтаповПроизводства2_2.ОграничиваетСрокВыпуска,
	|	ГрафикЭтаповПроизводства2_2.ОграниченПоМатериалам,
	|	ГрафикЭтаповПроизводства2_2.ОграниченПоОборудованию,
	|	ГрафикЭтаповПроизводства2_2.РазмещениеВыпуска,
	|	ГрафикЭтаповПроизводства2_2.ВидСтроки
	|ПОМЕСТИТЬ ПредварительныйГрафик
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|ГДЕ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусПредварительныйГрафик
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредварительныйГрафик.ЗаказНаПроизводство,
	|	ПредварительныйГрафик.Этап,
	|	&СтатусРабочийГрафик КАК СтатусГрафика,
	|	ПредварительныйГрафик.Начало,
	|	ПредварительныйГрафик.Окончание,
	|	ПредварительныйГрафик.ОкончаниеПредварительногоБуфера,
	|	ПредварительныйГрафик.НачалоЗавершающегоБуфера,
	|	ПредварительныйГрафик.НачалоСледующегоЭтапа,
	|	ПредварительныйГрафик.НаКритическомПути,
	|	ПредварительныйГрафик.ОграничиваетСрокВыпуска,
	|	ПредварительныйГрафик.ОграниченПоМатериалам,
	|	ПредварительныйГрафик.ОграниченПоОборудованию,
	|	ПредварительныйГрафик.РазмещениеВыпуска,
	|	ПредварительныйГрафик.ВидСтроки
	|ИЗ
	|	ПредварительныйГрафик КАК ПредварительныйГрафик
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство,
	|	ГрафикЭтаповПроизводства2_2.Этап,
	|	ГрафикЭтаповПроизводства2_2.СтатусГрафика,
	|	ГрафикЭтаповПроизводства2_2.Начало,
	|	ГрафикЭтаповПроизводства2_2.Окончание,
	|	ГрафикЭтаповПроизводства2_2.ОкончаниеПредварительногоБуфера,
	|	ГрафикЭтаповПроизводства2_2.НачалоЗавершающегоБуфера,
	|	ГрафикЭтаповПроизводства2_2.НачалоСледующегоЭтапа,
	|	ГрафикЭтаповПроизводства2_2.НаКритическомПути,
	|	ГрафикЭтаповПроизводства2_2.ОграничиваетСрокВыпуска,
	|	ГрафикЭтаповПроизводства2_2.ОграниченПоМатериалам,
	|	ГрафикЭтаповПроизводства2_2.ОграниченПоОборудованию,
	|	ГрафикЭтаповПроизводства2_2.РазмещениеВыпуска,
	|	ГрафикЭтаповПроизводства2_2.ВидСтроки
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|ГДЕ
	|	ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|	И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусРабочийГрафик
	|	И НЕ ГрафикЭтаповПроизводства2_2.Этап В
	|				(ВЫБРАТЬ
	|					ПредварительныйГрафик.Этап
	|				ИЗ
	|					ПредварительныйГрафик)");
	
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусПредварительныйГрафик", СтатусПредварительныйГрафик());
	Запрос.УстановитьПараметр("СтатусРабочийГрафик", СтатусРабочийГрафик());
	
	ДанныеГрафика = Запрос.Выполнить().Выгрузить();
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
	Набор.Загрузить(ДанныеГрафика);
	Набор.Записать();
	
КонецПроцедуры

// Очищает предварительный график распоряжения.
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - распоряжение, график которого необходимо очистить.
//
Процедура ОчиститьПредварительныйГрафик(ЗаказНаПроизводство) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|		ГДЕ
	|			ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|			И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусПредварительныйГрафик) КАК ОчиститьПредварительныйГрафик,
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК ГрафикЭтаповПроизводства2_2
	|		ГДЕ
	|			ГрафикЭтаповПроизводства2_2.ЗаказНаПроизводство = &ЗаказНаПроизводство
	|			И ГрафикЭтаповПроизводства2_2.СтатусГрафика = &СтатусМодельГрафика) КАК ОчиститьМодель");
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	
	СтатусПредварительныйГрафик = СтатусПредварительныйГрафик();
	Запрос.УстановитьПараметр("СтатусПредварительныйГрафик", СтатусПредварительныйГрафик);
	
	СтатусМодельГрафика = СтатусМодельГрафика();
	Запрос.УстановитьПараметр("СтатусМодельГрафика", СтатусМодельГрафика);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.ОчиститьПредварительныйГрафик Тогда
		Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
		Набор.Отбор.СтатусГрафика.Установить(СтатусПредварительныйГрафик);
		Набор.Записать();
	КонецЕсли;
	
	Если Выборка.ОчиститьМодель Тогда
		Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
		Набор.Отбор.СтатусГрафика.Установить(СтатусМодельГрафика);
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры

// Записывает заданное вручную размещение этапа в графике
//
// Параметры:
//  Измерения - Структура.
//  ГрафикПроизводства - См. УправлениеПроизводствомКлиентСервер.СтруктураРазмещенияЭтапаВГрафике.
//  Реквизиты - Структура.
//
Процедура ЗаписатьРучноеРазмещениеЭтапаВГрафике(Измерения, ГрафикПроизводства, Реквизиты) Экспорт
	
	СтатусГрафика = СтатусРабочийГрафик();
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(Измерения.ЗаказНаПроизводство);
	Набор.Отбор.СтатусГрафика.Установить(СтатусГрафика);
	Набор.Отбор.Этап.Установить(Измерения.Этап);
	Набор.Отбор.КлючПартия.Установить(Измерения.КлючПартия);
	Набор.Прочитать();
	
	Если Набор.Количество() > 0 Тогда
		Запись = Набор[0];
	Иначе
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Измерения, "ЗаказНаПроизводство, Этап, КлючПартия");
		ЗаполнитьЗначенияСвойств(Запись, Реквизиты);
		Запись.СтатусГрафика = СтатусГрафика;
		Запись.ВидСтроки = Перечисления.ВидыСтрокГрафикаПроизводства.Этап;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Запись, ГрафикПроизводства,
		"Начало, Окончание, ОкончаниеПредварительногоБуфера, НачалоЗавершающегоБуфера");
	
	Набор.Записать();
	
	Если ЗначениеЗаполнено(Измерения.КлючПартия) Тогда
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("КлючПартия", Измерения.КлючПартия);
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	&КлючПартия КАК КлючПартия
			|ПОМЕСТИТЬ ВТКлючиПартий";
		Запрос.Выполнить();
		
		РассчитатьИтогиПартий(Измерения.ЗаказНаПроизводство, МенеджерВременныхТаблиц, "ВТКлючиПартий");
	КонецЕсли;
	
КонецПроцедуры

// Очищает график производства заданного этапа.
// 
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2
//  Этап - ДокументСсылка.ЭтапПроизводства2_2
//
Процедура ОчиститьГрафикЭтапа(ЗаказНаПроизводство, Этап) Экспорт
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(ЗаказНаПроизводство);
	Набор.Отбор.СтатусГрафика.Установить(СтатусРабочийГрафик());
	Набор.Отбор.Этап.Установить(Этап);
	Набор.Записать();
	
КонецПроцедуры

// Очищает график производства и загрузку видов РЦ по всем этапам заданной партии
//
// Параметры:
//  Партии	 - Структура, ТаблицаЗначений, МенеджерВременныхТаблиц - партия или список партий:
//  	*  ЗаказНаПроизводство - ЛюбаяСсылка - заказ на производство.
//  	*  КлючПартия - УникальныйИдентификатор - ключ партии.
//  	При передаче менеджера временных таблиц он должен содержать таблицу Партии.
//
Процедура ОчиститьГрафикИЗагрузкуПартии(Партии) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусРабочийГрафик());
	
	ТекстыЗапроса = Новый СписокЗначений;
	
	Если ТипЗнч(Партии) = Тип("ТаблицаЗначений") Тогда
		
		ТекстЗапроса = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Партии.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
			|	Партии.КлючПартия          КАК КлючПартия
			|ПОМЕСТИТЬ Партии
			|ИЗ
			|	&Партии КАК Партии
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ЗаказНаПроизводство,
			|	КлючПартия";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "Партии");
		
		Запрос.УстановитьПараметр("Партии", Партии);
		
	ИначеЕсли ТипЗнч(Партии) = Тип("МенеджерВременныхТаблиц") Тогда
		
		Запрос.МенеджерВременныхТаблиц = Партии;
		
	Иначе
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
				|	&ЗаказНаПроизводство КАК ЗаказНаПроизводство,
				|	&КлючПартия          КАК КлючПартия
				|ПОМЕСТИТЬ Партии";
		ТекстыЗапроса.Добавить(ТекстЗапроса, "Партии");
		
		Запрос.УстановитьПараметр("ЗаказНаПроизводство", Партии.ЗаказНаПроизводство);
		Запрос.УстановитьПараметр("КлючПартия", Партии.КлючПартия);
		
	КонецЕсли;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	График.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика КАК СтатусГрафика,
		|	График.Этап КАК Этап,
		|	График.КлючПартия КАК КлючПартия
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	(График.ЗаказНаПроизводство, График.СтатусГрафика, График.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ДД.ЗаказНаПроизводство,
		|				&СтатусГрафика,
		|				ДД.КлючПартия
		|			ИЗ
		|				Партии КАК ДД)";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "График");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Загрузка.Регистратор КАК Регистратор,
		|	Загрузка.Период КАК Период,
		|	Загрузка.ВидРабочегоЦентра КАК ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала КАК ДатаИнтервала,
		|	-СУММА(Загрузка.Занято) КАК Занято,
		|	Загрузка.Распоряжение КАК Распоряжение,
		|	Загрузка.Подразделение КАК Подразделение,
		|	Загрузка.Смена КАК Смена,
		|	Загрузка.КлючПартия КАК КлючПартия,
		|	Загрузка.Этап КАК Этап
		|ИЗ
		|	РегистрНакопления.ДоступностьВидовРабочихЦентров КАК Загрузка
		|ГДЕ
		|	(Загрузка.Распоряжение, Загрузка.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ДД.ЗаказНаПроизводство,
		|				ДД.КлючПартия
		|			ИЗ
		|				Партии КАК ДД)
		|
		|СГРУППИРОВАТЬ ПО
		|	Загрузка.Регистратор,
		|	Загрузка.Период,
		|	Загрузка.ВидРабочегоЦентра,
		|	Загрузка.ДатаИнтервала,
		|	Загрузка.Распоряжение,
		|	Загрузка.Подразделение,
		|	Загрузка.Смена,
		|	Загрузка.КлючПартия,
		|	Загрузка.Этап
		|
		|ИМЕЮЩИЕ
		|	СУММА(Загрузка.Занято) <> 0
		|ИТОГИ ПО
		|	Регистратор";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "Доступность");
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Задания.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	Задания.Разделитель         КАК Разделитель,
		|	Задания.ЭтапПроизводства    КАК ЭтапПроизводства,
		|	Задания.КлючПартия          КАК КлючПартия
		|ИЗ
		|	РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК Задания
		|ГДЕ
		|	(Задания.ЗаказНаПроизводство, Задания.КлючПартия) В
		|			(ВЫБРАТЬ
		|				ДД.ЗаказНаПроизводство,
		|				ДД.КлючПартия
		|			ИЗ
		|				Партии КАК ДД)";
	ТекстыЗапроса.Добавить(ТекстЗапроса, "Задания");
	
	РезультатыЗапроса = ОбщегоНазначенияУТ.ПолучитьРезультатыЗапроса(Запрос, ТекстыЗапроса, Истина);
	
	РезультатЗапросаГрафик = РезультатыЗапроса.График; // РезультатЗапроса
	Выборка = РезультатЗапросаГрафик.Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(Выборка.ЗаказНаПроизводство);
		Набор.Отбор.СтатусГрафика.Установить(Выборка.СтатусГрафика);
		Набор.Отбор.Этап.Установить(Выборка.Этап);
		Набор.Отбор.КлючПартия.Установить(Выборка.КлючПартия);
		Набор.Записать();
	КонецЦикла;
	
	РезультатЗапросаДоступность = РезультатыЗапроса.Доступность; // РезультатЗапроса
	ВыборкаРегистратор = РезультатЗапросаДоступность.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаРегистратор.Следующий() Цикл
		Набор = РегистрыНакопления.ДоступностьВидовРабочихЦентров.СоздатьНаборЗаписей();
		Набор.Отбор.Регистратор.Установить(ВыборкаРегистратор.Регистратор);
		
		Выборка = ВыборкаРегистратор.Выбрать();
		Пока Выборка.Следующий() Цикл
			ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		КонецЦикла;
		
		Набор.Записать(Ложь);
	КонецЦикла;
	
	РезультатЗапросаЗадания = РезультатыЗапроса.Задания; // РезультатЗапроса
	ВыборкаЗадания = РезультатЗапросаЗадания.Выбрать();
	Пока ВыборкаЗадания.Следующий() Цикл
		Набор = РегистрыСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(ВыборкаЗадания.ЗаказНаПроизводство);
		Набор.Отбор.Разделитель.Установить(ВыборкаЗадания.Разделитель);
		Набор.Отбор.ЭтапПроизводства.Установить(ВыборкаЗадания.ЭтапПроизводства);
		Набор.Отбор.КлючПартия.Установить(ВыборкаЗадания.КлючПартия);
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Снимает флаги "НаКритическомПути" и "ОграничиваетСрокВыпуска" в графике этапа производства.
//
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2 - Этап производства.
//
Процедура ОчиститьПризнакЭтапНаКритическомПути(Этап) Экспорт
	
	Менеджер = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьМенеджерЗаписи();
	Менеджер.ЗаказНаПроизводство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Этап, "Распоряжение");
	Менеджер.Этап = Этап;
	Менеджер.СтатусГрафика = СтатусРабочийГрафик();
	Менеджер.Прочитать();
	
	Если Менеджер.Выбран() И Менеджер.НаКритическомПути Тогда
		
		Менеджер.НаКритическомПути = Ложь;
		Менеджер.ОграничиваетСрокВыпуска = Ложь;
		Менеджер.Записать();
		
	КонецЕсли;
	
КонецПроцедуры

// Рассчитывает итоги заданных партий и при необходимости итоги заказа.
// 
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц
//  ИмяТаблицыПартий - Строка
//
Процедура РассчитатьИтогиПартий(ЗаказНаПроизводство, МенеджерВременныхТаблиц, ИмяТаблицыПартий) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказ", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", РегистрыСведений.ГрафикЭтаповПроизводства2_2.СтатусРабочийГрафик());
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	
	ТекстЗапроса = 
		"ВЫБРАТЬ
		|	График.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика            КАК СтатусГрафика,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)        КАК Этап,
		|	График.КлючПартия               КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Партия) КАК ВидСтроки,
		|	МИНИМУМ(График.Начало)          КАК Начало,
		|	МАКСИМУМ(График.Окончание)      КАК Окончание,
		|	МИНИМУМ(График.Порядок)         КАК Порядок
		|ПОМЕСТИТЬ ВТИтогиПартий
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	(График.ЗаказНаПроизводство, График.СтатусГрафика, График.КлючПартия) В (
		|		ВЫБРАТЬ
		|			&Заказ КАК ЗаказНаПроизводство,
		|			&СтатусГрафика КАК СтатусГрафика,
		|			Т.КлючПартия КАК КлючПартия
		|		ИЗ
		|			&ТаблицаПартий КАК Т)
		|	И График.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Партия)
		|
		|СГРУППИРОВАТЬ ПО
		|	График.ЗаказНаПроизводство,
		|	График.СтатусГрафика,
		|	График.КлючПартия
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство, СтатусГрафика, Этап, КлючПартия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИтогиПартий.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ИтогиПартий.СтатусГрафика       КАК СтатусГрафика,
		|	ИтогиПартий.Этап                КАК Этап,
		|	ИтогиПартий.КлючПартия          КАК КлючПартия,
		|	ИтогиПартий.ВидСтроки           КАК ВидСтроки,
		|	ИтогиПартий.Начало              КАК Начало,
		|	ИтогиПартий.Окончание           КАК Окончание,
		|	ИтогиПартий.Порядок             КАК Порядок
		|ИЗ
		|	ВТИтогиПартий КАК ИтогиПартий
		|ГДЕ
		|	НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|		ГДЕ
		|			График.ЗаказНаПроизводство = ИтогиПартий.ЗаказНаПроизводство
		|			И График.СтатусГрафика = ИтогиПартий.СтатусГрафика
		|			И График.Этап = ИтогиПартий.Этап
		|			И График.КлючПартия = ИтогиПартий.КлючПартия
		|			И График.Начало = ИтогиПартий.Начало
		|			И График.Окончание = ИтогиПартий.Окончание)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИтогиПартий";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаПартий", ИмяТаблицыПартий);
	Запрос.Текст = ТекстЗапроса;
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатыЗапроса[1].Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
		Набор.Отбор.ЗаказНаПроизводство.Установить(Выборка.ЗаказНаПроизводство);
		Набор.Отбор.СтатусГрафика.Установить(Выборка.СтатусГрафика);
		Набор.Отбор.Этап.Установить(Выборка.Этап);
		Набор.Отбор.КлючПартия.Установить(Выборка.КлючПартия);
		ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
		Набор.Записать();
	КонецЦикла;
	
	// Итоги заказа
	Запрос.Текст =
		"ВЫБРАТЬ
		|	График.ЗаказНаПроизводство      КАК ЗаказНаПроизводство,
		|	График.СтатусГрафика            КАК СтатусГрафика,
		|	ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)       КАК Этап,
		|	&ПустойКлючСвязи                КАК КлючПартия,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Заказ) КАК ВидСтроки,
		|	МИНИМУМ(График.Начало)          КАК Начало,
		|	МАКСИМУМ(График.Окончание)      КАК Окончание,
		|	0                               КАК Порядок
		|ПОМЕСТИТЬ ВТИтогиЗаказа
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	График.ЗаказНаПроизводство = &Заказ
		|	И График.СтатусГрафика = &СтатусГрафика
		|	И График.ВидСтроки <> ЗНАЧЕНИЕ(Перечисление.ВидыСтрокГрафикаПроизводства.Заказ)
		|
		|СГРУППИРОВАТЬ ПО
		|	График.ЗаказНаПроизводство,
		|	График.СтатусГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЗаказНаПроизводство, СтатусГрафика, Этап, КлючПартия
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ИтогиЗаказа.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
		|	ИтогиЗаказа.СтатусГрафика       КАК СтатусГрафика,
		|	ИтогиЗаказа.Этап                КАК Этап,
		|	ИтогиЗаказа.КлючПартия          КАК КлючПартия,
		|	ИтогиЗаказа.ВидСтроки           КАК ВидСтроки,
		|	ИтогиЗаказа.Начало              КАК Начало,
		|	ИтогиЗаказа.Окончание           КАК Окончание,
		|	ИтогиЗаказа.Порядок             КАК Порядок
		|ИЗ
		|	ВТИтогиЗаказа КАК ИтогиЗаказа
		|ГДЕ
		|	НЕ ИСТИНА В (
		|		ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|		ГДЕ
		|			График.ЗаказНаПроизводство = ИтогиЗаказа.ЗаказНаПроизводство
		|			И График.СтатусГрафика = ИтогиЗаказа.СтатусГрафика
		|			И График.Этап = ИтогиЗаказа.Этап
		|			И График.КлючПартия = ИтогиЗаказа.КлючПартия
		|			И График.Начало = ИтогиЗаказа.Начало
		|			И График.Окончание = ИтогиЗаказа.Окончание)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТИтогиЗаказа";
	
	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатыЗапроса[1].Выбрать();
	Если НЕ Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ГрафикЭтаповПроизводства2_2.СоздатьНаборЗаписей();
	Набор.Отбор.ЗаказНаПроизводство.Установить(Выборка.ЗаказНаПроизводство);
	Набор.Отбор.СтатусГрафика.Установить(Выборка.СтатусГрафика);
	Набор.Отбор.Этап.Установить(Выборка.Этап);
	Набор.Отбор.КлючПартия.Установить(Выборка.КлючПартия);
	ЗаполнитьЗначенияСвойств(Набор.Добавить(), Выборка);
	Набор.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область Чтение

// Возвращает график производства этапов
//
// Параметры:
//  Этапы - Массив - Этапы производства.
// 
// Возвращаемое значение:
//   - ТаблицаЗначений - график производства этапов.
//
Функция ГрафикПроизводства(Этапы) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Документ.Ссылка                                                    КАК Этап,
		|	НЕ График.Начало ЕСТЬ NULL                                         КАК Рассчитан,
		|	ЕСТЬNULL(График.Начало, ДАТАВРЕМЯ(1,1,1))                          КАК Начало,
		|	ЕСТЬNULL(График.Окончание, ДАТАВРЕМЯ(1,1,1))                       КАК Окончание,
		|	ЕСТЬNULL(График.ОкончаниеПредварительногоБуфера, ДАТАВРЕМЯ(1,1,1)) КАК ОкончаниеПредварительногоБуфера,
		|	ЕСТЬNULL(График.НачалоЗавершающегоБуфера, ДАТАВРЕМЯ(1,1,1))        КАК НачалоЗавершающегоБуфера,
		|	ЕСТЬNULL(График.НаКритическомПути, ЛОЖЬ)                           КАК НаКритическомПути,
		|	ЕСТЬNULL(График.ОграничиваетСрокВыпуска, ЛОЖЬ)                     КАК ОграничиваетСрокВыпуска
		|ИЗ
		|	Документ.ЭтапПроизводства2_2 КАК Документ
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|		ПО График.ЗаказНаПроизводство = Документ.Распоряжение
		|			И График.Этап = Документ.Ссылка
		|			И (График.СтатусГрафика = &СтатусГрафика)
		|ГДЕ
		|	Документ.Ссылка В(&Этапы)");
	
	Запрос.УстановитьПараметр("Этапы", Этапы);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусРабочийГрафик());
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

// Возвращает текст запроса, который получает даты начала и окончания этапов по данным графика
//  (для не начатых этапов) и данным самого этапа (если этап начат или завершен).
//  При использовании функции запрос должен содержать параметр "СтатусГрафика" для выборки
//  графика с заданным статусом.
//
// Параметры:
//  ТаблицаЭтапы	 - Строка	 - имя временной таблицы, содержащей этапы для отбора данных.
//  ПолеЭтап		 - Строка	 - имя поля таблицы.
//  ПолеКлючПартия	 - Строка	 - имя поля таблицы.
// 
// Возвращаемое значение:
//   - Строка - текст запроса создания временной таблицы ВТСрокиВыполненияЭтапов.
//
Функция ТекстЗапросаВТСрокиВыполненияЭтапов(ТаблицаЭтапы, ПолеЭтап, ПолеКлючПартия = "") Экспорт
	
	Результат =
	"ВЫБРАТЬ
	|	График.ЗаказНаПроизводство КАК ЗаказНаПроизводство,
	|	График.Этап КАК ЭтапПроизводства,
	|	График.КлючПартия КАК КлючПартия,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Документ.Статус В (ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат), ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен))
	|				ТОГДА Документ.ФактическоеНачалоЭтапа
	|		КОНЕЦ, График.Начало) КАК НачалоЭтапа,
	|	ЕСТЬNULL(ВЫБОР
	|			КОГДА Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
	|				ТОГДА ВЫБОР
	|						КОГДА Документ.ДатаПроизводства >= График.Окончание
	|								И Документ.ДатаПроизводства >= Документ.ФактическоеНачалоЭтапа
	|							ТОГДА КОНЕЦПЕРИОДА(Документ.ДатаПроизводства, ДЕНЬ)
	|						КОГДА Документ.ФактическоеНачалоЭтапа >= График.Окончание
	|								И Документ.ФактическоеНачалоЭтапа >= Документ.ДатаПроизводства
	|							ТОГДА КОНЕЦПЕРИОДА(Документ.ФактическоеНачалоЭтапа, ДЕНЬ)
	|						ИНАЧЕ График.Окончание
	|					КОНЕЦ
	|			КОГДА Документ.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Завершен)
	|				ТОГДА Документ.ФактическоеОкончаниеЭтапа
	|		КОНЕЦ, График.Окончание) КАК ОкончаниеЭтапа
	|ПОМЕСТИТЬ ВТСрокиВыполненияЭтапов
	|ИЗ
	|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Документ
	|		ПО График.Этап = Документ.Ссылка
	|ГДЕ
	|	(График.Этап, &ПолеГрафикКлючПартия) В
	|			(ВЫБРАТЬ
	|				&ПолеЭтап,
	|				&ПолеКлючПартия
	|			ИЗ
	|				&ТаблицаЭтапы КАК Т)
	|	И График.СтатусГрафика = &СтатусГрафика
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЭтапПроизводства,
	|	КлючПартия";
	
	Результат = СтрЗаменить(Результат, "&ТаблицаЭтапы", ТаблицаЭтапы);
	Результат = СтрЗаменить(Результат, "&ПолеЭтап", ПолеЭтап);
	Результат = СтрЗаменить(Результат, "&ПолеКлючПартия", ?(ПустаяСтрока(ПолеКлючПартия), "ИСТИНА", ПолеКлючПартия));
	Результат = СтрЗаменить(Результат, "&ПолеГрафикКлючПартия", ?(ПустаяСтрока(ПолеКлючПартия), "ИСТИНА", "График.КлючПартия"));
	
	Возврат Результат;
	
КонецФункции

// Функция - Полный интервал
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - Заказ на производство.
//  СтатусГрафика - См. УправлениеПроизводствомКлиентСервер.СтруктураРазмещенияЭтапаВГрафике
// 
// Возвращаемое значение:
//  Структура - границы заказа
//
Функция ПолныйИнтервал(ЗаказНаПроизводство, СтатусГрафика = 0) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойКлючСвязи", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусГрафика);
	
	Если ПолучитьФункциональнуюОпцию("ДинамическаяСтруктураЗаказовНаПроизводство")
		И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗаказНаПроизводство, "ДинамическаяСтруктура") = Истина Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	График.Начало КАК Начало,
		|	График.Окончание КАК Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	График.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И График.СтатусГрафика = &СтатусГрафика
		|	И График.КлючПартия = &ПустойКлючСвязи
		|	И График.Этап = ЗНАЧЕНИЕ(Справочник.ЭтапыПроизводства.ПустаяСсылка)
		|";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	МИНИМУМ(График.Начало) КАК Начало,
		|	МАКСИМУМ(График.Окончание) КАК Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК График
		|ГДЕ
		|	График.ЗаказНаПроизводство = &ЗаказНаПроизводство
		|	И График.СтатусГрафика = &СтатусГрафика
		|";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Границы = Новый Структура("Начало, Окончание", '00010101', '00010101');
	
	Если Выборка.Следующий() Тогда
		Границы.Начало = ?(Не ЗначениеЗаполнено(Выборка.Начало), '00010101', Выборка.Начало);
		Границы.Окончание = ?(Не ЗначениеЗаполнено(Выборка.Окончание), '00010101', Выборка.Окончание);
	КонецЕсли;
	
	Возврат Границы;
	
КонецФункции

// Функция возвращает признак необходимости расчета графика производства
//
// Параметры:
//  ЗаказНаПроизводство - ДокументСсылка.ЗаказНаПроизводство2_2 - Заказ на производство.
// 
// Возвращаемое значение:
//  Булево - Истина, если требуется рассчитать график.
//
Функция ТребуетсяРассчитатьГрафик(ЗаказНаПроизводство) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИСТИНА
		|ГДЕ
		|	ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ЗаданияКРасчетуСтруктурыЗаказаГрафикПроизводства КАК ДД
		|		ГДЕ
		|			ДД.ЗаказНаПроизводство = &ЗаказНаПроизводство)
		|	ИЛИ ИСТИНА В
		|		(ВЫБРАТЬ ПЕРВЫЕ 1
		|			ИСТИНА
		|		ИЗ
		|			РегистрСведений.ЗаданияКРасчетуГрафикаПроизводства КАК ДД
		|		ГДЕ
		|			ДД.Распоряжение = &ЗаказНаПроизводство)");
	Запрос.УстановитьПараметр("ЗаказНаПроизводство", ЗаказНаПроизводство);

	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти

#Область Прочее

// Проверяет содержит ли график производства изменения
//
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2 - Этап производства.
//  ГрафикПроизводства - См. УправлениеПроизводствомКлиентСервер.СтруктураРазмещенияЭтапаВГрафике
// 
// Возвращаемое значение:
//   - Булево - Истина, если график производства содержит изменения.
//
Функция ИзменилосьРазмещениеЭтапаВГрафике(Этап, ГрафикПроизводства) Экспорт
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ИСТИНА                                  КАК Рассчитан,
		|	Таблица.Начало                          КАК Начало,
		|	Таблица.ОкончаниеПредварительногоБуфера КАК ОкончаниеПредварительногоБуфера,
		|	Таблица.НачалоЗавершающегоБуфера        КАК НачалоЗавершающегоБуфера,
		|	Таблица.Окончание                       КАК Окончание
		|ИЗ
		|	РегистрСведений.ГрафикЭтаповПроизводства2_2 КАК Таблица
		|ГДЕ
		|	Таблица.Этап = &Этап
		|	И Таблица.СтатусГрафика  = &СтатусГрафика");
	
	СтатусГрафика = СтатусРабочийГрафик();
	
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("СтатусГрафика", СтатусРабочийГрафик());
	
	РазмещениеВГрафике = УправлениеПроизводствомКлиентСервер.СтруктураРазмещенияЭтапаВГрафике();

	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ЗаполнитьЗначенияСвойств(РазмещениеВГрафике, Выборка);
		
	КонецЕсли;
	
	Возврат НЕ ОбщегоНазначения.ДанныеСовпадают(РазмещениеВГрафике, ГрафикПроизводства);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли