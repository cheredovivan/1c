#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("НастройкаФормированияПроводок") Тогда
			ДанныеЗаполнения.Вставить("ПланСчетов", 
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения.НастройкаФормированияПроводок, "Владелец"));
		КонецЕсли;
	Иначе
		ДанныеЗаполнения = Новый Структура();
	КонецЕсли;
	
	Если Не ДанныеЗаполнения.Свойство("ПланСчетов") Тогда
		ДанныеЗаполнения.Вставить("ПланСчетов", Справочники.ПланыСчетовМеждународногоУчета.ПланСчетовПоУмолчанию());
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УправленческаяОрганизация = Справочники.Организации.УправленческаяОрганизация;
	
	Для Каждого ЗаписьРегистра Из ЭтотОбъект Цикл
		Если ЗаписьРегистра.Организация = УправленческаяОрганизация
			И НЕ ЗаписьРегистра.НастройкаФормированияПроводок.Пустая() Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Для организации ""%1"" использование международного финансового учета не предусмотрено.';
					|en = 'Use of financial accounting is not provided for ""%1"" company.'"),
				УправленческаяОрганизация);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Организация",, Отказ);
		КонецЕсли;
	КонецЦикла;
	
	Если Не МеждународныйУчетОбщегоНазначения.НастройкаПроводокПоОбъектамУчетаИлиРеглУчету() Тогда
		НастройкиФормированияПроводок = Выгрузить(, "НастройкаФормированияПроводок").ВыгрузитьКолонку("НастройкаФормированияПроводок");
		СпособыАннулированияПроводок = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(НастройкиФормированияПроводок, "СпособАннулированияПроводок");
		
		Для Каждого КлючИЗначение Из СпособыАннулированияПроводок Цикл
			Если КлючИЗначение.Значение = Перечисления.СпособыАннулированияПроводокМеждународногоУчета.РеверсивнымиПроводками Тогда
				ТекстСообщения = НСтр("ru = 'Нельзя использовать настройку формирования проводок со способом корректировки реверсивными проводками';
										|en = 'Cannot use the posting schema with correction by reversing entries'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "НастройкаФормированияПроводок",, Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);

КонецПроцедуры

#КонецОбласти

#КонецЕсли
