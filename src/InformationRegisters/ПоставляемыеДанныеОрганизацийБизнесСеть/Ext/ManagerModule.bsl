// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// См. ПоставляемыеДанныеПереопределяемый.ПолучитьОбработчикиПоставляемыхДанных
Процедура ПолучитьОбработчикиПоставляемыхДанных(Знач Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.ВидДанных = ИдентификаторВидаПоставляемыхДанных();
	Обработчик.КодОбработчика = "Организации1СБизнесСеть";
	Обработчик.Обработчик = БизнесСетьВМоделиСервиса;
	
КонецПроцедуры

// См. БизнесСетьВМоделиСервиса.ДоступныНовыеДанные
Процедура ДоступныНовыеДанные(Знач Дескриптор, Загружать) Экспорт
	
	Если Дескриптор.DataType = ИдентификаторВидаПоставляемыхДанных() Тогда
		Загружать = Истина;
	КонецЕсли;
	
КонецПроцедуры

// См. БизнесСетьВМоделиСервиса.ОбработатьНовыеДанные
Процедура ОбработатьНовыеДанные(Знач Дескриптор, Знач ПутьКФайлу) Экспорт
	
	Если Дескриптор.DataType = ИдентификаторВидаПоставляемыхДанных() Тогда
		ОбработатьНовыеДанныеОрганизации1СБизнесСеть(Дескриптор, ПутьКФайлу);
	КонецЕсли;
	
КонецПроцедуры

// См. БизнесСетьВМоделиСервиса.ОбработкаДанныхОтменена
Процедура ОбработкаДанныхОтменена(Знач Дескриптор) Экспорт
	// Реализация не требуется
КонецПроцедуры

// См. БизнесСетьСлужебный.ВыполнитьПоискКонтрагентов
Процедура ВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов) Экспорт
	
	Если ТаблицаПоискаКонтрагентов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаЗапроса = ВыборкаВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов);
	
	ТаблицаПоискаКонтрагентов.Индексы.Добавить("Контрагент");
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		Контрагент    = ВыборкаЗапроса.Контрагент;
		Идентификатор = ВыборкаЗапроса.Идентификатор;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Контрагент", Контрагент); 
		
		НайденныеСтроки = ТаблицаПоискаКонтрагентов.НайтиСтроки(Отбор);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка.Идентификатор = Идентификатор;
			Строка.Найден        = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Идентификатор вида поставляемых данных.
// 
// Возвращаемое значение:
//  Строка
Функция ИдентификаторВидаПоставляемыхДанных()
	Возврат "Организации1СБизнесСеть";
КонецФункции

// См. БизнесСетьВМоделиСервиса.ОбработатьНовыеДанные
Процедура ОбработатьНовыеДанныеОрганизации1СБизнесСеть(Знач Дескриптор, Знач ПутьКФайлу)
	
	ЧтениеZipФайла = Новый ЧтениеZipФайла(ПутьКФайлу);
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ЧтениеZipФайла.Элементы.Количество() = 1,
		СтрШаблон(
			НСтр(
				"ru = 'При чтении файла [%1] поставляемых данных [%2] выявлена ошибка:
				|Количество файлов в архиве (%3) не совпадает с ожидаемым (1)';
				|en = 'An error occurred when reading the [%1] file of the [%2] default master data:
				|The number of files in the archive (%3) does not match the expected number (1)'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ПутьКФайлу,
			ИдентификаторВидаПоставляемыхДанных(),
			ЧтениеZipФайла.Элементы.Количество()));
	
	ВременныйКаталог = ФайловаяСистема.СоздатьВременныйКаталог();
	
	ФайлАрхива = ЧтениеZipФайла.Элементы[0];
	ЧтениеZipФайла.Извлечь(ФайлАрхива, ВременныйКаталог);
	
	НайденныеФайлы = НайтиФайлы(ВременныйКаталог, "*.json");
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		НайденныеФайлы.Количество() = 1,
		СтрШаблон(
			НСтр(
				"ru = 'При чтении файла [%1] поставляемых данных [%2] выявлена ошибка:
				|Количество файлов в архиве (%3) не совпадает с ожидаемым (1)';
				|en = 'An error occurred when reading the [%1] file of the [%2] default master data:
				|The number of files in the archive (%3) does not match the expected number (1)'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ПутьКФайлу,
			ИдентификаторВидаПоставляемыхДанных(),
			НайденныеФайлы.Количество()));
	
	ЧтениеJSON = Новый ЧтениеJSON();
	ЧтениеJSON.ОткрытьФайл(НайденныеФайлы[0].ПолноеИмя);
	
	Данные = НоваяТаблицаДанных();
	
	РазмерПорции = 1000; // Записываем по 1000 записей за одну транзакцию
	
	Пока ЧтениеJSON.Прочитать() Цикл
		Если ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.НачалоОбъекта Тогда
			ДанныеОбъекта = Новый Структура;
		ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.ИмяСвойства Тогда
			ИмяСвойства = ЧтениеJSON.ТекущееЗначение;
		ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.Строка Тогда
			ДанныеОбъекта.Вставить(ИмяСвойства, СокрЛП(ЧтениеJSON.ТекущееЗначение));
		ИначеЕсли ЧтениеJSON.ТипТекущегоЗначения = ТипЗначенияJSON.КонецОбъекта Тогда
			ДобавитьДанные(Данные, ДанныеОбъекта);
			
			Если Данные.Количество() >= РазмерПорции Тогда
				//@skip-check query-in-loop - Порционная запись данных
				ЗаписатьДанные(Данные);
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	ЧтениеJSON.Закрыть();
	
	ЗаписатьДанные(Данные);
	Константы.ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть.Включить();
	
	ФайловаяСистема.УдалитьВременныйКаталог(ВременныйКаталог);
	
КонецПроцедуры

// Добавляет сведения об организации в таблицу для записи
// 
// Параметры:
//  Данные - См. НоваяТаблицаДанных
//  ДанныеОбъекта - Структура - Данные объекта, прочитанные из JSON
Процедура ДобавитьДанные(Данные, ДанныеОбъекта)
	
	Идентификатор = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОбъекта, "id"); // Строка
	
	Если Не СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(Идентификатор) Тогда
		Возврат; // Невалидный идентификатор 1С:Бизнес-сеть
	КонецЕсли;
	
	РегистрационныеДанныеХеш = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеОбъекта, "key"); // Строка
	
	Если РегистрационныеДанныеХеш = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Данные.Добавить();
	НоваяСтрока.Идентификатор = Идентификатор;
	НоваяСтрока.РегистрационныеДанныеХеш = РегистрационныеДанныеХеш;
	
КонецПроцедуры

// Таблица данных регистра для записи в ИБ
// 
// Возвращаемое значение:
//  ТаблицаЗначений:
// * Идентификатор - Строка - См. РегистрСведений.ПоставляемыеДанныеОрганизацийБизнесСеть.Идентификатор
// * РегистрационныеДанныеХеш - Строка - См. РегистрСведений.ПоставляемыеДанныеОрганизацийБизнесСеть.РегистрационныеДанныеХеш
Функция НоваяТаблицаДанных()
	
	Результат = Новый ТаблицаЗначений;
	МетаданныеРегистра = Метаданные.РегистрыСведений.ПоставляемыеДанныеОрганизацийБизнесСеть;
	
	Результат.Колонки.Добавить("Идентификатор", МетаданныеРегистра.Измерения.Идентификатор.Тип);
	Результат.Колонки.Добавить("РегистрационныеДанныеХеш", МетаданныеРегистра.Измерения.Идентификатор.Тип);
	
	Возврат Результат;
	
КонецФункции

// Получает только измененные записи
// 
// Параметры:
//  Данные - См. НоваяТаблицаДанных
// 
// Возвращаемое значение:
//  См. НоваяТаблицаДанных
Функция ИзмененныеЗаписи(Знач Данные)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Данные", Данные);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Данные.Идентификатор КАК Идентификатор,
		|	Данные.РегистрационныеДанныеХеш КАК РегистрационныеДанныеХеш
		|ПОМЕСТИТЬ Данные
		|ИЗ
		|	&Данные КАК Данные
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Идентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Данные.Идентификатор КАК Идентификатор,
		|	Данные.РегистрационныеДанныеХеш КАК РегистрационныеДанныеХеш
		|ИЗ
		|	Данные КАК Данные
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПоставляемыеДанныеОрганизацийБизнесСеть КАК ПоставляемыеДанные
		|		ПО Данные.Идентификатор = ПоставляемыеДанные.Идентификатор
		|ГДЕ
		|	ПоставляемыеДанные.Идентификатор ЕСТЬ NULL
		|	ИЛИ Данные.РегистрационныеДанныеХеш <> ПоставляемыеДанные.РегистрационныеДанныеХеш";
		
	Результат = НоваяТаблицаДанных();
	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаЗапроса.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Результат.Добавить(), ВыборкаЗапроса);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Записывает данные в регистр
// 
// Параметры:
//  Данные - См. НоваяТаблицаДанных
Процедура ЗаписатьДанные(Знач Данные)
	
	Если Данные.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИзмененныеЗаписи = ИзмененныеЗаписи(Данные);
	
	Если ИзмененныеЗаписи.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировкиДанных = БлокировкаДанных.Добавить(
		Метаданные.РегистрыСведений.ПоставляемыеДанныеОрганизацийБизнесСеть.ПолноеИмя());
	ЭлементБлокировкиДанных.ИсточникДанных = ИзмененныеЗаписи;
	ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Идентификатор", "Идентификатор");
	
	НачатьТранзакцию();
	Попытка
		БлокировкаДанных.Заблокировать();
		
		Для Каждого Строка Из ИзмененныеЗаписи Цикл
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Идентификатор.Установить(Строка.Идентификатор);
			
			Запись = НаборЗаписей.Добавить();
			Запись.Идентификатор = Строка.Идентификатор;
			Запись.РегистрационныеДанныеХеш = Строка.РегистрационныеДанныеХеш;
			
			НаборЗаписей.Записать(Истина);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
	// После записи очищаем набор
	Данные.Очистить();
	
КонецПроцедуры

// Метод-конструктор таблицы поиска
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица поиска:
// * Контрагент - См. РегистрСведений.КонтрагентыБизнесСеть.Контрагент
// * РегистрационныеДанныеХеш - См. РегистрСведений.ПоставляемыеДанныеОрганизацийБизнесСеть.РегистрационныеДанныеХеш
Функция НоваяТаблицаПоиска()
	
	Результат = Новый ТаблицаЗначений;
	
	Результат.Колонки.Добавить("Контрагент", 
		Метаданные.РегистрыСведений.КонтрагентыБизнесСеть.Измерения.Контрагент.Тип);
	Результат.Колонки.Добавить("РегистрационныеДанныеХеш", 
		Метаданные.РегистрыСведений.ПоставляемыеДанныеОрганизацийБизнесСеть.Ресурсы.РегистрационныеДанныеХеш.Тип);
	
	Возврат Результат;
	
КонецФункции

// Получает хеш регистрационных данных по ИНН и КПП.
// 
// Параметры:
//  ИНН - Строка - ИННорганизации
//  КПП - Строка - КПП организации
// 
// Возвращаемое значение:
//  Строка - Получает хеш регистрационных данных.
//  Неопределено - ИНН и/или КПП не валидны.
Функция РегистрационныеДанныеХеш(Знач ИНН, Знач КПП)
	
	Если БизнесСетьСлужебный.ЭтоИННКППЮЛ(ИНН, КПП) Тогда
		РегистрационныеДанные = СтрШаблон("%1:%2", ИНН, КПП);
	ИначеЕсли БизнесСетьСлужебный.ЭтоИННКППФЛ(ИНН, КПП) Тогда
		РегистрационныеДанные = ИНН
	Иначе
		Возврат Неопределено; // Данные невалидны
	КонецЕсли;
	
	Возврат НРег(ОбщегоНазначения.КонтрольнаяСуммаСтрокой(РегистрационныеДанные, ХешФункция.MD5));
	
КонецФункции

// Таблица поиска.
// 
// Параметры:
//  ТаблицаПоискаКонтрагентов - См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
// 
// Возвращаемое значение:
//  См. НоваяТаблицаПоиска
Функция ТаблицаПоиска(ТаблицаПоискаКонтрагентов)
	
	Результат = НоваяТаблицаПоиска();
	
	Для Каждого Строка Из ТаблицаПоискаКонтрагентов Цикл
		
		РегистрационныеДанные = РегистрационныеДанныеХеш(Строка.ИНН, Строка.КПП);
		
		Если РегистрационныеДанные = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = Результат.Добавить();
		НоваяСтрока.Контрагент            = Строка.Контрагент;
		НоваяСтрока.РегистрационныеДанныеХеш = РегистрационныеДанные;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает выборку идентификаторов контрагентов в сервисе 1С:Бизнес-сеть
// 
// Параметры:
//  ТаблицаПоискаКонтрагентов - См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса - Выборка выполнить поиск контрагентов:
// * Контрагент    - См. РегистрСведений.КонтрагентыБизнесСеть.Контрагент
// * Идентификатор - См. РегистрСведений.КонтрагентыБизнесСеть.Идентификатор
Функция ВыборкаВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов)
	
	ТаблицаПоиска = ТаблицаПоиска(ТаблицаПоискаКонтрагентов);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПоиска", ТаблицаПоиска);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаПоиска.Контрагент КАК Контрагент,
		|	ТаблицаПоиска.РегистрационныеДанныеХеш КАК РегистрационныеДанныеХеш
		|ПОМЕСТИТЬ ТаблицаПоиска
		|ИЗ
		|	&ТаблицаПоиска КАК ТаблицаПоиска
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	РегистрационныеДанныеХеш
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаПоиска.Контрагент КАК Контрагент,
		|	ПоставляемыеДанные.Идентификатор КАК Идентификатор
		|ИЗ
		|	ТаблицаПоиска КАК ТаблицаПоиска
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПоставляемыеДанныеОрганизацийБизнесСеть КАК ПоставляемыеДанные
		|		ПО ТаблицаПоиска.РегистрационныеДанныеХеш = ПоставляемыеДанные.РегистрационныеДанныеХеш";
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
