#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс
	
// Заполняет уточнения принадлежности видов запасов торговых точек
// Параметры:
//	Получатель - ПланОбменаСсылка.СинхронизацияДанныхЧерезУниверсальныйФормат
Процедура ЗаполнениеУточненияПринадлежностиЗапасовТорговыхТочек(Получатель) Экспорт
	
	// Тут происходит коррекция регистра УточненияПринадлежностиЗапасовТорговыхТочек по фактическим расчетным данным.
	Если ТипЗнч(Получатель) <> Тип("ПланОбменаСсылка.СинхронизацияДанныхЧерезУниверсальныйФормат") Тогда
		Возврат;
	КонецЕсли;
	
	// проверим принимает ли узел данные "УточненияПринадлежностиЗапасовТорговыхТочек"
	ЗаписьНастроек = РегистрыСведений.НастройкиОбменаДаннымиXDTO.СоздатьМенеджерЗаписи();
	ЗаписьНастроек.УзелИнформационнойБазы = Получатель;
	ЗаписьНастроек.Прочитать();
	ЗначениеХранилища = ЗаписьНастроек.НастройкиКорреспондента.Получить();
	СохраненныеДанные = Неопределено;
	Если НЕ ТипЗнч(ЗначениеХранилища) = Тип("Структура")
		ИЛИ НЕ  ЗначениеХранилища.Свойство("ПоддерживаемыеОбъекты", СохраненныеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	ТекВерсияФормата = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Получатель, "ВерсияФорматаОбмена");
	ОтборСтрок = Новый Структура();
	ОтборСтрок.Вставить("Версия", ТекВерсияФормата);
	ОтборСтрок.Вставить("Объект", "Справочник.УточненияПринадлежностиЗапасовТорговыхТочек");
	ОтборСтрок.Вставить("Получение", Истина);
	Если СохраненныеДанные.НайтиСтроки(ОтборСтрок).Количество() = 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ТекстЗапросаВтОперацииПоЗапасам());
	
	ТекстЗапросаВыборкиОперацийПоЗапасам = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОперацииПоЗапасам.Организация КАК Организация,
	|	ОперацииПоЗапасам.Склад КАК Склад,
	|	ИСТИНА КАК Очистить
	|ИЗ
	|	ОперацииПоЗапасам КАК ОперацииПоЗапасам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.УточненияПринадлежностиЗапасовТорговыхТочек КАК УточнениеЗапасов
	|		ПО ОперацииПоЗапасам.Склад = УточнениеЗапасов.Склад
	|			И ОперацииПоЗапасам.Организация = УточнениеЗапасов.ОрганизацияПродавец
	|ГДЕ
	|	НЕ ОперацииПоЗапасам.ЗаполнитьДанныеЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОперацииПоЗапасам.Организация КАК Организация,
	|	ОперацииПоЗапасам.Склад КАК Склад,
	|	ИСТИНА КАК Пересчитать
	|ИЗ
	|	ОперацииПоЗапасам КАК ОперацииПоЗапасам
	|ГДЕ
	|	ОперацииПоЗапасам.ЗаполнитьДанныеЗапасов";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаВыборкиОперацийПоЗапасам);
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	Запрос.Параметры.Вставить("УзелОбмена", Получатель);
	
	Результаты = Запрос.ВыполнитьПакет();
	ЗапросовПакета = Результаты.Количество();
	КортежиДляОчистки = Результаты[ЗапросовПакета - 2].Выгрузить();
	КортежиДляПересчета = Результаты[ЗапросовПакета - 1].Выгрузить();
	
	Для каждого Кортеж Из КортежиДляПересчета Цикл
		// Пересчитать данные комитентов по складам
		АктуализироватьУточненияПринадлежностиЗапасовТорговыхТочек(Кортеж.Организация, Кортеж.Склад); 
	КонецЦикла;
	
	УдаленоЗаписей = 0;
	Для каждого Кортеж Из КортежиДляОчистки Цикл
		
		// очистить данные по кортежам, которые перестали быть актуальными
		ЗапросОчистки = Новый Запрос;
		ЗапросОчистки.Параметры.Вставить("Склад", Кортеж.Склад);
		ЗапросОчистки.Параметры.Вставить("Организация", Кортеж.Организация);
		ЗапросОчистки.Текст = 
		"ВЫБРАТЬ
		|	УточнениеЗапасов.Склад КАК Склад,
		|	УточнениеЗапасов.Номенклатура КАК Номенклатура,
		|	УточнениеЗапасов.Характеристика КАК Характеристика,
		|	УточнениеЗапасов.Серия КАК Серия,
		|	УточнениеЗапасов.ОрганизацияПродавец КАК ОрганизацияПродавец
		|ИЗ
		|	РегистрСведений.УточненияПринадлежностиЗапасовТорговыхТочек КАК УточнениеЗапасов
		|ГДЕ
		|	УточнениеЗапасов.Склад = &Склад
		|	И УточнениеЗапасов.ОрганизацияПродавец = &Организация";
		РезультатЗапроса = ЗапросОчистки.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		Выборка = РезультатЗапроса.Выбрать();
		НаборЗаписей = РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек.СоздатьНаборЗаписей();
		НаборЗаписей.РасширенныеРежимыЗамещения = Истина;
		Пока Выборка.Следующий() Цикл
			НовСтрока = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрока, Выборка);
		КонецЦикла;
		УдаленоЗаписей = УдаленоЗаписей + Выборка.Количество();
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
	КонецЦикла;
	
	КлючиЗамера = Новый Структура;
	КлючиЗамера.Вставить("Узел", Строка(Получатель));
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ЗаполнениеУточненияПринадлежностиЗапасовТорговыхТочекКортежи",
		НачалоЗамера, КортежиДляПересчета.Количество()+КортежиДляОчистки.Количество(), ЗаписатьЗначениеJSON(КлючиЗамера),);
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ЗаполнениеУточненияПринадлежностиЗапасовТорговыхТочекУдаления",
		НачалоЗамера, УдаленоЗаписей, ЗаписатьЗначениеJSON(КлючиЗамера), );
		
КонецПроцедуры

// Выполняет удаление из регистра неиспользуемые данные о принадлежности видов запасов торговых точек
//
Процедура ВыполнитьУдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек() Экспорт
	
	УдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РозничныеПродажи

// Возвращает текст запроса для формирования временной таблицы ОперацииПоЗапасам, которая используется при заполнении
// уточнения принадлежности запасов торговых точек и при очистке неиспользуемых данных из регистра сведений.
//
// Параметры:
//  ИспользоватьОтборПоУзлу - Булево - Если параметр установлен в Истина, в запросе должен быть установлен параметр УзелОбмена.
//
// Возвращаемое значение:
//  Строка -
//
Функция ТекстЗапросаВтОперацииПоЗапасам(ИспользоватьОтборПоУзлу = Истина) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТЧ.ВидЦенНоменклатуры КАК ВидЦенНоменклатуры
	|ПОМЕСТИТЬ ВидыЦен
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК УзелОбмена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.ВидыЦенНоменклатуры КАК ТЧ
	|		ПО УзелОбмена.Ссылка = ТЧ.Ссылка
	|ГДЕ
	|	НЕ УзелОбмена.ЭтотУзел
	|	И УзелОбмена.ВыгружатьЦеныНоменклатуры
	|	И НЕ ТЧ.ВидЦенНоменклатуры.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УзелОбмена.Ссылка КАК Узел,
	|	ТЧ.Организация КАК Организация
	|ПОМЕСТИТЬ Организации
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК УзелОбмена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Организации КАК ТЧ
	|		ПО УзелОбмена.Ссылка = ТЧ.Ссылка
	|ГДЕ
	|	НЕ УзелОбмена.ЭтотУзел
	|	И УзелОбмена.ИспользоватьОтборПоОрганизациям
	|	И НЕ ТЧ.Организация.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УзелОбмена.Ссылка,
	|	СпрОрганизации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК СпрОрганизации,
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК УзелОбмена
	|ГДЕ
	|	НЕ УзелОбмена.ЭтотУзел
	|	И НЕ УзелОбмена.ИспользоватьОтборПоОрганизациям
	|	И (УзелОбмена.ВыгружатьУправленческуюОрганизацию ИЛИ 
	|		СпрОрганизации.Ссылка <> ЗНАЧЕНИЕ(Справочник.Организации.УправленческаяОрганизация))
	|	И НЕ СпрОрганизации.ПометкаУдаления
	|ИНДЕКСИРОВАТЬ ПО
	|	Узел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УзелОбмена.Ссылка КАК Узел,
	|	ТЧ.Склад КАК Склад,
	|	ТЧ.Склад.РозничныйВидЦены КАК РозничныйВидЦены,
	|	ТЧ.Склад.ТипСклада КАК СкладТипСклада
	|ПОМЕСТИТЬ Склады
	|ИЗ
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК УзелОбмена
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат.Склады КАК ТЧ
	|		ПО УзелОбмена.Ссылка = ТЧ.Ссылка
	|ГДЕ
	|	НЕ УзелОбмена.ЭтотУзел
	|	И УзелОбмена.ИспользоватьОтборПоСкладам
	|	И НЕ ТЧ.Склад.ПометкаУдаления
	|	И НЕ ТЧ.Склад.ЭтоГруппа
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	УзелОбмена.Ссылка,
	|	СпрОСклады.Ссылка,
	|	СпрОСклады.РозничныйВидЦены,
	|	СпрОСклады.ТипСклада
	|ИЗ
	|	Справочник.Склады КАК СпрОСклады,
	|	ПланОбмена.СинхронизацияДанныхЧерезУниверсальныйФормат КАК УзелОбмена
	|ГДЕ
	|	НЕ УзелОбмена.ЭтотУзел
	|	И НЕ УзелОбмена.ИспользоватьОтборПоСкладам
	|	И НЕ СпрОСклады.ПометкаУдаления
	|	И НЕ СпрОСклады.ЭтоГруппа
	|ИНДЕКСИРОВАТЬ ПО
	|	Узел
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.Организация КАК Организация,
	|	Склады.Склад КАК Склад,
	|	МАКСИМУМ(Склады.СкладТипСклада = ЗНАЧЕНИЕ(Перечисление.ТипыСкладов.РозничныйМагазин)
	|		И ВидыЦен.ВидЦенНоменклатуры ЕСТЬ НЕ NULL) КАК ЗаполнитьДанныеЗапасов
	|ПОМЕСТИТЬ ОперацииПоЗапасам
	|ИЗ
	|	Организации КАК Организации
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Склады КАК Склады
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВидыЦен КАК ВидыЦен
	|		ПО Склады.РозничныйВидЦены = ВидыЦен.ВидЦенНоменклатуры
	|	ПО Склады.Узел = Организации.Узел
	|ГДЕ &УсловиеОтбораПоУзлу
	|
	|СГРУППИРОВАТЬ ПО
	|	Организации.Организация,
	|	Склады.Склад
	|ИНДЕКСИРОВАТЬ ПО
	|	Склад,
	|	Организация
	|";
	
	УсловиеОтбораПоУзлу = "ИСТИНА";
	Если ИспользоватьОтборПоУзлу Тогда
		УсловиеОтбораПоУзлу = "Склады.Узел = &УзелОбмена";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоУзлу", УсловиеОтбораПоУзлу);
	
	Возврат ТекстЗапроса;
КонецФункции

Процедура УдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек()

	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегистрСведений.УточненияПринадлежностиЗапасовТорговыхТочек.УдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек");
	
	КоличествоОбработанныхДанных = 0;
	КомментарийЗамераДлительнойОперации = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	ТекстыЗапроса = Новый Массив();
	
	// Получение данных об актуальных кортежах Организация, Склад
	ТекстыЗапроса.Добавить(ТекстЗапросаВтОперацииПоЗапасам(Ложь));
	
	// Получение неиспользуемых кортежей
	ТекстЗапросаПолученияНеиспользуемыхКортежей = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	УточнениеЗапасов.ОрганизацияПродавец КАК ОрганизацияПродавец,
	|	УточнениеЗапасов.Склад КАК Склад
	|ПОМЕСТИТЬ КлючиУточненияПринадлежностиЗапасов
	|ИЗ
	|	РегистрСведений.УточненияПринадлежностиЗапасовТорговыхТочек КАК УточнениеЗапасов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КлючиУточненияПринадлежностиЗапасов.ОрганизацияПродавец КАК ОрганизацияПродавец,
	|	КлючиУточненияПринадлежностиЗапасов.Склад КАК Склад
	|ИЗ
	|	КлючиУточненияПринадлежностиЗапасов КАК КлючиУточненияПринадлежностиЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОперацииПоЗапасам КАК ОперацииПоЗапасам
	|		ПО ОперацииПоЗапасам.Склад = КлючиУточненияПринадлежностиЗапасов.Склад
	|		И ОперацииПоЗапасам.Организация = КлючиУточненияПринадлежностиЗапасов.ОрганизацияПродавец
	|ГДЕ
	|	ОперацииПоЗапасам.Склад ЕСТЬ NULL";
	
	ТекстыЗапроса.Добавить(ТекстЗапросаПолученияНеиспользуемыхКортежей);
	
	Запрос.Текст = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	РезультатЗапроса = Запрос.Выполнить();
	
	// Удаление неиспользуемых данных из регистра
	Если Не РезультатЗапроса.Пустой() Тогда
		ДопустимоеКоличествоОшибок = 3;
		Выборка = РезультатЗапроса.Выбрать();
		НаборЗаписей = РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
		
			КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + 1;
			НаборЗаписей.Отбор.Склад.Установить(Выборка.Склад);
			НаборЗаписей.Отбор.ОрганизацияПродавец.Установить(Выборка.ОрганизацияПродавец);
			
			Попытка
				НаборЗаписей.Записать(Истина);
			Исключение
				ПодробноеПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				СтрокиТекстаОшибки = Новый Массив;
				СтрокиТекстаОшибки.Добавить(НСтр("ru = 'Во время регламентного удаления неиспользуемых данных о принадлежности запасов торговых точек произошла ошибка.';
												|en = 'Error occurs during scheduled deletion of the unused inventory affiliation data of retail outlets.'", ОбщегоНазначения.КодОсновногоЯзыка()));
				ШаблонОписанияКортежа = НСтр("ru = 'Кортеж [Склад, Организация] = [%1, %2]';
											|en = 'Tuples [Warehouse, Company] = [%1, %2]'", ОбщегоНазначения.КодОсновногоЯзыка());
				СтрокиТекстаОшибки.Добавить(СтрШаблон(ШаблонОписанияКортежа, Выборка.Склад, Выборка.ОрганизацияПродавец));
				СтрокиТекстаОшибки.Добавить(ПодробноеПредставлениеОшибки);
				
				ЗаписьЖурналаРегистрации(НСтр("ru = 'Удаление неиспользуемых данных о принадлежности запасов торговых точек';
												|en = 'Delete unused inventory affiliation data of retail outlets'", ОбщегоНазначения.КодОсновногоЯзыка()),
				                         УровеньЖурналаРегистрации.Ошибка, , ,
				                         СтрСоединить(СтрокиТекстаОшибки,Символы.ПС));
			
				КомментарийЗамераДлительнойОперации = НСтр("ru = 'При выполнении замера возникли ошибки. Подробная информация в журнале регистрации.';
															|en = 'Errors occurred while sampling. For more information, see the event log.'", ОбщегоНазначения.КодОсновногоЯзыка());
				
				ДопустимоеКоличествоОшибок = ДопустимоеКоличествоОшибок - 1;
				Если ДопустимоеКоличествоОшибок = 0 Тогда
					Прервать;
				КонецЕсли;
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КоличествоОбработанныхДанных,, КомментарийЗамераДлительнойОперации);
	
КонецПроцедуры

Процедура АктуализироватьУточненияПринадлежностиЗапасовТорговыхТочек(Знач Организация, Знач Склад);
	
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	МодельЗапроса = РозничныеПродажиЛокализация.ЗапросДанныхКомитентовПоТоварам(Организация, Склад,, "_Обмен");
	ПакетЗапросов = МодельЗапроса.ПакетЗапросов;
	
	ТекстЗапросаФинальный = 
	"
	|ВЫБРАТЬ
	|	Таб.Номенклатура КАК Номенклатура, 
	|	Таб.Характеристика КАК Характеристика, 
	|	Таб.Серия КАК Серия, 
	|	ВЫБОР КОГДА Таб.Комитент ЕСТЬ NULL ИЛИ Таб.Комитент В (&ПустойКомитент) ТОГДА
	|		НЕОПРЕДЕЛЕНО
	|	ИНАЧЕ
	|		Таб.Комитент
	|	КОНЕЦ КАК Комитент 
	|ПОМЕСТИТЬ ДанныеПоКомитентам
	|ИЗ #ТаблицаРасчета КАК Таб
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|
	|;
	|ВЫБРАТЬ
	|	Рег.Номенклатура КАК Номенклатура, 
	|	Рег.Характеристика КАК Характеристика, 
	|	Рег.Серия КАК Серия, 
	|	Рег.Комитент КАК Комитент
	|ПОМЕСТИТЬ СохраненныеДанныеОбмена
	|ИЗ РегистрСведений.УточненияПринадлежностиЗапасовТорговыхТочек КАК Рег
	|ГДЕ
	|	Рег.Склад = &ОтборМестоХраненияПродавца_Обмен
	|	И Рег.ОрганизацияПродавец = &ОтборОрганизацияПродавец_Обмен
	|ИНДЕКСИРОВАТЬ ПО 
	|	Номенклатура,
	|	Характеристика,
	|	Серия
	|;
	|
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СохраненныеДанные.Номенклатура, ДанныеРасчета.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(СохраненныеДанные.Характеристика, ДанныеРасчета.Характеристика) КАК Характеристика,
	|	ЕСТЬNULL(СохраненныеДанные.Серия, ДанныеРасчета.Серия) КАК Серия,
	|	&ОтборМестоХраненияПродавца_Обмен КАК Склад,
	|	&ОтборОрганизацияПродавец_Обмен КАК ОрганизацияПродавец,
	|	ДанныеРасчета.Комитент КАК Комитент,
	|	ВЫБОР
	|		КОГДА ДанныеРасчета.Комитент ЕСТЬ NULL ТОГДА
	|			""Удалить""
	|		ИНАЧЕ
	|			""Слияние""
	|	КОНЕЦ КАК Действие
	|	ИЗ СохраненныеДанныеОбмена КАК СохраненныеДанные
	|	ПОЛНОЕ ВНЕШНЕЕ СОЕДИНЕНИЕ ДанныеПоКомитентам КАК ДанныеРасчета
	|
	|		ПО СохраненныеДанные.Номенклатура = ДанныеРасчета.Номенклатура
	|		И СохраненныеДанные.Характеристика = ДанныеРасчета.Характеристика
	|		И СохраненныеДанные.Серия = ДанныеРасчета.Серия
	|ГДЕ
	|	СохраненныеДанные.Комитент ЕСТЬ NULL
	|	ИЛИ ДанныеРасчета.Комитент ЕСТЬ NULL
	|	ИЛИ СохраненныеДанные.Комитент <> ДанныеРасчета.Комитент
	|ИТОГИ ПО 
	|	Действие
	|";
	ТекстЗапросаФинальный = СтрЗаменить(ТекстЗапросаФинальный, "#ТаблицаРасчета", ПакетЗапросов[ПакетЗапросов.Количество()-1].Представление);
	
	ПакетЗапросов.Добавить(ТекстЗапросаФинальный);
	
	ТекстЗапроса = СтрСоединить(ПакетЗапросов.ВыгрузитьЗначения(), Символы.ПС + ";" + Символы.ПС);
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Для каждого Элем Из МодельЗапроса.ПараметрыЗапроса Цикл
		Запрос.Параметры.Вставить(Элем.Ключ, Элем.Значение);
	КонецЦикла;
	ПустойКомитент = Новый Массив;
	ПустойКомитент.Добавить(Неопределено);
	ПустойКомитент.Добавить(Справочники.Контрагенты.ПустаяСсылка());
	ПустойКомитент.Добавить(Справочники.Организации.ПустаяСсылка());
	Запрос.Параметры.Вставить("ПустойКомитент", ПустойКомитент);
	Запрос.Параметры.Вставить("ОтборМестоХраненияПродавца_Обмен", Склад);
	Запрос.Параметры.Вставить("ОтборОрганизацияПродавец_Обмен", Организация);
	
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	КлючиЗамера = Новый Структура;
	КлючиЗамера.Вставить("Организация", Строка(Организация));
	КлючиЗамера.Вставить("Склад", Строка(Склад));
	
	Результаты = Запрос.ВыполнитьПакет();
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ЗаполнениеУточненияПринадлежностиЗапасовТорговыхТочекПакетКортежа",
		НачалоЗамера, , ЗаписатьЗначениеJSON(КлючиЗамера), );
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	
	ОбработаноСтрок = 0;
	ВыборкаДействие = Результаты[Результаты.Количество()-1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаДействие.Следующий() Цикл
		
		ТекущийНабор = РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек.СоздатьНаборЗаписей();
		ТекущийНабор.РасширенныеРежимыЗамещения = Истина;
		
		ВыборкаДетали = ВыборкаДействие.Выбрать();
		ОбработаноСтрок = ОбработаноСтрок + ВыборкаДетали.Количество();
		Пока ВыборкаДетали.Следующий() Цикл
			НовЗапись = ТекущийНабор.Добавить();
			ЗаполнитьЗначенияСвойств(НовЗапись, ВыборкаДетали);
		КонецЦикла;
		Если ВыборкаДействие.Действие = "Удалить" Тогда
			ТекРежимЗамещения = РежимЗамещения.Удаление;
		Иначе
			ТекРежимЗамещения = РежимЗамещения.Слияние;
		КонецЕсли;
		
		ТекущийНабор.Записать(ТекРежимЗамещения);
		
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ЗаполнениеУточненияПринадлежностиЗапасовТорговыхТочекАктуализация",
		НачалоЗамера, ОбработаноСтрок, ЗаписатьЗначениеJSON(КлючиЗамера), );
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
