
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьОписаниеНастраиваемыхРегламентныхЗаданий();
	УправлениеВидимостьюЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УдалитьНеиспользуемыеУточненияПринадлежностиЗапасовТорговыхТочек(Команда)
	ВыполнитьУдалениеНеиспользуемыхДанныхУточненияПринадлежностиЗапасовТорговыхТочек();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеУдаленияНеиспользуемыхУточненийПринадлежностиЗапасовТорговыхТочек(Команда)
	
	РозничныеПродажиКлиентЛокализация.ОткрытьФормуНастройкиРасписанияРегламентногоЗадания(ЭтотОбъект, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсеДанные(Команда)
	ОписаниеОповещения  = Новый ОписаниеОповещения("ОчиститьВесьРегистр", ЭтотОбъект);
	ТекстВопроса = НСтр("ru = 'Вы собираетесь удалить все данные регистра. Продолжить?';
						|en = 'You are going to delete all the register data. Continue?'");
	СтрЗаголовок = НСтр("ru = 'Удаление всех записей';
						|en = 'Delete all records'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена,20,, СтрЗаголовок);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеВидимостьюЭлементов();
	
	ИспользоватьРозничныеПродажи = ПолучитьФункциональнуюОпцию("ИспользоватьРозничныеПродажи");
	РазделениеВключено = ОбщегоНазначения.РазделениеВключено();
	
	Элементы.ФормаУдалитьНеиспользуемыеУточненияПринадлежностиЗапасовТорговыхТочек.Видимость = ИспользоватьРозничныеПродажи;
	Элементы.ФормаНастроитьРасписаниеУдаленияНеиспользуемыхУточненийПринадлежностиЗапасовТорговыхТочек.Видимость = РазделениеВключено И ИспользоватьРозничныеПродажи;
	Элементы.Список.ТолькоПросмотр = Истина;
	Элементы.Список.ИзменятьСоставСтрок = Ложь;
	Элементы.Список.ИзменятьПорядокСтрок = Ложь;
	Элементы.ФормаОчиститьВсеДанные.Видимость = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОписаниеНастраиваемыхРегламентныхЗаданий()
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОписаниеНастраиваемыхРегламентныхЗаданий = Новый Структура;
	ОписаниеНастраиваемыхРегламентныхЗаданий.Вставить("УдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек", 
					Новый Структура("ИмяМетаданныхРегламентногоЗадания, МетаданныеДляПроверкиПравДоступаНаИзменение",
									Метаданные.РегламентныеЗадания.УдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек.Имя, Новый СписокЗначений));
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьУдалениеНеиспользуемыхДанныхУточненияПринадлежностиЗапасовТорговыхТочек()
	
	РезультатОбработки = УдалитьНеиспользуемыеУточненияПринадлежностиЗапасовТорговыхТочекНаСервере();

	Если Не РезультатОбработки.Статус = "Выполнено" Тогда
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьРегламентноеЗаданиеОповещение", ЭтотОбъект);
		
		ДлительныеОперацииКлиент.ОжидатьЗавершение(
			РезультатОбработки, ОповещениеОЗавершении, ПараметрыОжидания);
		
	Иначе
		ОповеститьПользователяОРезультатахВыполненияЗадания(РезультатОбработки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция УдалитьНеиспользуемыеУточненияПринадлежностиЗапасовТорговыхТочекНаСервере()
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ЗапуститьВФоне = Истина;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = Команды.УдалитьНеиспользуемыеУточненияПринадлежностиЗапасовТорговыхТочек.Подсказка;
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	
	Метод = "РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек.ВыполнитьУдалениеНеиспользуемыхДанныхОПринадлежностиЗапасовТорговыхТочек";
	Результат = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, Метод);
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьРегламентноеЗаданиеОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	ОповеститьПользователяОРезультатахВыполненияЗадания(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьПользователяОРезультатахВыполненияЗадания(РезультатОбработки)
	
	Если РезультатОбработки = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если РезультатОбработки.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(РезультатОбработки.ИнформацияОбОшибке);
		Если СтандартныеПодсистемыКлиент.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая Тогда
			СообщениеОбОшибке = НСтр("ru = 'Удаление из регистра неиспользуемых данных завершено с ошибками.';
									|en = 'Deletion of unused data from the register was completed with errors.'");
		Иначе
			СообщениеОбОшибке = НСтр("ru = 'Удаление из регистра неиспользуемых данных завершено с ошибками. См. журнал регистрации.';
									|en = 'Deletion of unused data from the register completed with errors. See the event log.'");
		КонецЕсли;
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Удаление из регистра неиспользуемых данных завершено.';
											|en = 'Unused data is eleted from the register.'"),
				,
				СообщениеОбОшибке,
				БиблиотекаКартинок.Информация32);
		
		Возврат;
	ИначеЕсли РезультатОбработки.Статус = "Выполнено" Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Удаление из регистра неиспользуемых данных завершено.';
											|en = 'Unused data is eleted from the register.'"),
			,
			НСтр("ru = 'Удаление из регистра неиспользуемых данных успешно завершено.';
				|en = 'Unused data is successfully deleted from the register.'"),
			БиблиотекаКартинок.Информация32);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВесьРегистр(Знач РезультатВопроса, Знач ДопПараметры = Неопределено) Экспорт 
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		ОчиститьВесьРегистрсервер();
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьВесьРегистрСервер() 
	
	Если ПравоДоступа("Изменение", Метаданные.РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек) Тогда
		УстановитьПривилегированныйРежим(Истина);
		Набор = РегистрыСведений.УточненияПринадлежностиЗапасовТорговыхТочек.СоздатьНаборЗаписей();
		Набор.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти