
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия          = "3.1.32.56";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор   = Новый УникальныйИдентификатор("0be7457b-f8fa-11ef-814e-a842a16ef6f5");
	Обработчик.Процедура       = "РегистрыСведений.НастройкиУведомленийОбЭЛН.ЗаполнитьФлажокРассчитыватьБольничныеПоЭЛН";
	Обработчик.Комментарий     = НСтр("ru = 'СЭДО СФР: Заполнение флажка ""Рассчитывать больничные по ЭЛН"" в настройках учетной политики организаций.';
										|en = 'Social Insurance Fund EDI: Select the ""Calculate sick leaves by ESLR"" checkbox in the company''s accounting policy settings.'");
	
КонецПроцедуры

// Заполняет флажок "РассчитыватьБольничныеПоЭЛН" для тех записей, которые соответствовали настройкам "по умолчанию".
//   Удаляет из регистра настройки организации в случае, если они соответствовали настройкам "по умолчанию".
//   Регистрируется в модуле СЭДОФССРасширенный.
//
Процедура ЗаполнитьФлажокРассчитыватьБольничныеПоЭЛН(ПараметрыОбновления = Неопределено) Экспорт
	ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиУведомленийОбЭЛН.Организация КАК Организация
	|ИЗ
	|	РегистрСведений.НастройкиУведомленийОбЭЛН КАК НастройкиУведомленийОбЭЛН
	|ГДЕ
	|	НастройкиУведомленийОбЭЛН.СоздаватьБольничныеПоЭЛН
	|	И НастройкиУведомленийОбЭЛН.СоздаватьОтсутствияПоЭЛН
	|	И НастройкиУведомленийОбЭЛН.УтверждатьОтсутствияПоЭЛН";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.Организация.Установить(Выборка.Организация);
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор, Истина, Истина);
	КонецЦикла;
	
	ТаблицаКэш = СЭДОФССРасширенныйПовтИсп.НастройкиУведомленийОбЭЛН();
	ТаблицаКэш.Очистить();
	
	Если ПараметрыОбновления <> Неопределено Тогда
		ПараметрыОбновления.ОбработкаЗавершена = ОбработкаЗавершена;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "Организация",
		"РегламентированныеДанные");
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастройкиОрганизации(Организация) Экспорт
	Массив = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Организация);
	Возврат НастройкиОрганизаций(Массив)[0];
КонецФункции

Функция НастройкиОрганизаций(Организации) Экспорт
	ТаблицаКэш = СЭДОФССРасширенныйПовтИсп.НастройкиУведомленийОбЭЛН();
	
	Найденные = Новый Массив;
	ОрганизацииДляДобавленияВКэш = Новый Массив;
	Для Каждого Организация Из Организации Цикл
		СтрокаТаблицы = ТаблицаКэш.Найти(Организация, "Организация");
		Если СтрокаТаблицы = Неопределено Тогда
			ОрганизацииДляДобавленияВКэш.Добавить(Организация);
		Иначе
			Найденные.Добавить(СтрокаТаблицы);
		КонецЕсли;
	КонецЦикла;
	
	Если ОрганизацииДляДобавленияВКэш.Количество() > 0 Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Данные.Организация КАК Организация,
		|	Данные.СоздаватьБольничныеПоЭЛН КАК СоздаватьБольничныеПоЭЛН,
		|	Данные.СоздаватьОтсутствияПоЭЛН КАК СоздаватьОтсутствияПоЭЛН,
		|	Данные.РассчитыватьБольничныеПоЭЛН КАК РассчитыватьБольничныеПоЭЛН,
		|	Данные.УтверждатьОтсутствияПоЭЛН КАК УтверждатьОтсутствияПоЭЛН
		|ИЗ
		|	РегистрСведений.НастройкиУведомленийОбЭЛН КАК Данные
		|ГДЕ
		|	Данные.Организация В(&Организации)";
		Запрос.УстановитьПараметр("Организации", ОрганизацииДляДобавленияВКэш);
		
		УстановитьПривилегированныйРежим(Истина);
		ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
		УстановитьПривилегированныйРежим(Ложь);
		
		НастройкиПоУмолчанию = НастройкиПоУмолчанию();
		
		Для Каждого Организация Из ОрганизацииДляДобавленияВКэш Цикл
			СтрокаТаблицы = ТаблицаКэш.Добавить();
			СтрокаТаблицы.Организация = Организация;
			СтрокаТаблицыЗапроса = ТаблицаЗапроса.Найти(Организация, "Организация");
			Если СтрокаТаблицыЗапроса = Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, НастройкиПоУмолчанию);
			Иначе
				ЗаполнитьЗначенияСвойств(СтрокаТаблицы, СтрокаТаблицыЗапроса);
			КонецЕсли;
			Найденные.Добавить(СтрокаТаблицы);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаКэш.Скопировать(Найденные);
КонецФункции

Функция ПустаяТаблицаНастроек() Экспорт
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	МетаданныеРегистра = Метаданные.РегистрыСведений.НастройкиУведомленийОбЭЛН;
	Для Каждого Реквизит Из МетаданныеРегистра.Измерения Цикл
		ТаблицаЗначений.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	Для Каждого Реквизит Из МетаданныеРегистра.Ресурсы Цикл
		ТаблицаЗначений.Колонки.Добавить(Реквизит.Имя, Реквизит.Тип);
	КонецЦикла;
	
	Возврат ТаблицаЗначений;
КонецФункции

Процедура ЗаписатьНастройкиОрганизации(Организация, Настройки) Экспорт
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Организация.Установить(Организация);
	НастройкиДляЗаписи = НастройкиПоУмолчанию();
	НастройкиОтличаютсяОтНастроекПоУмолчанию = ЗаполнитьСтруктуру(НастройкиДляЗаписи, Настройки);
	Если НастройкиОтличаютсяОтНастроекПоУмолчанию Тогда
		Запись = Набор.Добавить();
		Запись.Организация = Организация;
		ЗаполнитьЗначенияСвойств(Запись, НастройкиДляЗаписи);
	КонецЕсли;
	Набор.Записать(Истина);
	ТаблицаКэш = СЭДОФССРасширенныйПовтИсп.НастройкиУведомленийОбЭЛН();
	СтрокаТаблицы = ТаблицаКэш.Найти(Организация, "Организация");
	Если СтрокаТаблицы <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, НастройкиДляЗаписи);
	КонецЕсли;
КонецПроцедуры

Функция НастройкиПоУмолчанию()
	НастройкиПоУмолчанию = Новый Структура;
	НастройкиПоУмолчанию.Вставить("СоздаватьБольничныеПоЭЛН", Истина);
	НастройкиПоУмолчанию.Вставить("СоздаватьОтсутствияПоЭЛН", Истина);
	НастройкиПоУмолчанию.Вставить("РассчитыватьБольничныеПоЭЛН", Истина);
	НастройкиПоУмолчанию.Вставить("УтверждатьОтсутствияПоЭЛН", Истина);
	Возврат НастройкиПоУмолчанию;
КонецФункции

Функция ЗаполнитьСтруктуру(Приемник, Источник)
	ДоИзменения = ОбщегоНазначения.ЗначениеВСтрокуXML(Приемник);
	ЗаполнитьЗначенияСвойств(Приемник, Источник);
	Возврат ДоИзменения <> ОбщегоНазначения.ЗначениеВСтрокуXML(Приемник);
КонецФункции

#КонецОбласти

#КонецЕсли
