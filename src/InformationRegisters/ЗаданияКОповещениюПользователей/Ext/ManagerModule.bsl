#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет задания в регистр сведений "Задания к оповещению пользователей".
// 
// Параметры:
//  ИсточникОповещения - ЛюбаяСсылка, Массив Из ЛюбаяСсылка - ссылка-источник задания или массив ссылок, которые надо отразить.
//  ВидыОповещений - Массив Из СправочникСсылка.ВидыОповещенийПользователей - массив отражаемых видов оповещения по источнику.
//  ИнициаторОповещения - СправочникСсылка.Пользователи, Неопределено - пользователь инициатор оповещения.
//  ТаблицаЗаданий - см. ОтложенныеЗадания.ДобавитьЗаданияВОчередь.ТаблицаЗаданий
//
Процедура ДобавитьЗаданияКОтражению(ИсточникОповещения, ВидыОповещений, ИнициаторОповещения = Неопределено, ТаблицаЗаданий = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаДанных = Новый ТаблицаЗначений;
	ТаблицаДанных.Колонки.Добавить("ИсточникОповещения", ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	ТаблицаДанных.Колонки.Добавить("ВидОповещения", Новый ОписаниеТипов("СправочникСсылка.ВидыОповещенийПользователей"));
	ТаблицаДанных.Колонки.Добавить("ИнициаторОповещения", Новый ОписаниеТипов("СправочникСсылка.Пользователи"));
	
	Если ТипЗнч(ИсточникОповещения) = Тип("Массив") Тогда
		МассивИсточников = ИсточникОповещения;
	Иначе
		МассивИсточников = Новый Массив;
		МассивИсточников.Добавить(ИсточникОповещения);
	КонецЕсли;
	
	Для каждого Источник Из МассивИсточников Цикл
		Для Каждого ВидОповещения Из ВидыОповещений Цикл
			НоваяСтрока = ТаблицаДанных.Добавить();
			НоваяСтрока.ИсточникОповещения = Источник;
			НоваяСтрока.ВидОповещения = ВидОповещения;
			НоваяСтрока.ИнициаторОповещения = ИнициаторОповещения;
		КонецЦикла;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ТаблицаДанных.ИсточникОповещения КАК ИсточникОповещения,
	|	ТаблицаДанных.ВидОповещения КАК ВидОповещения,
	|	ТаблицаДанных.ИнициаторОповещения КАК ИнициаторОповещения
	|ПОМЕСТИТЬ ТаблицаДанных
	|ИЗ
	|	&ТаблицаДанных КАК ТаблицаДанных
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДанных.ИсточникОповещения КАК ИсточникОповещения,
	|	ТаблицаДанных.ВидОповещения КАК ВидОповещения,
	|	ТаблицаДанных.ИнициаторОповещения КАК ИнициаторОповещения
	|ИЗ
	|	ТаблицаДанных КАК ТаблицаДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКОповещениюПользователей КАК ЗаданияКОповещениюПользователей
	|		ПО ТаблицаДанных.ИсточникОповещения = ЗаданияКОповещениюПользователей.ИсточникОповещения
	|			И ТаблицаДанных.ВидОповещения = ЗаданияКОповещениюПользователей.ВидОповещения
	|			И ТаблицаДанных.ИнициаторОповещения = ЗаданияКОповещениюПользователей.ИнициаторОповещения
	|ГДЕ
	|	ЗаданияКОповещениюПользователей.ИсточникОповещения ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТаблицаДанных", ТаблицаДанных);
	ТаблицаДанных = Запрос.Выполнить().Выгрузить();
	
	ОтложенныеЗадания.ДобавитьЗаданияВОчередь(ИмяОчередиЗаданий(), ТаблицаДанных,,ТаблицаЗаданий);
	
КонецПроцедуры

// Имя очереди заданий для оповещения пользователей.
//
// Возвращаемое значение:
//  Строка - Имя очереди заданий.
Функция ИмяОчередиЗаданий() Экспорт
	
	Возврат "ЗаданияКОповещениюПользователей";
	
КонецФункции

#КонецОбласти

#КонецЕсли