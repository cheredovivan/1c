#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Запись.ИсходныйКлючЗаписи.Пустой() Тогда
	
		ПриСозданииЧтенииНаСервере();
		
	КонецЕсли;
	
	ОбменНаСервере    = ?(Запись.ОбменНаСервере, 0, 1);
	ЦветВажногоТекста = ЦветаСтиля.ЦветТекстаПроблемаГосИС;
	
	Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаНеВыполнялась;
	
	ОбщегоНазначенияСобытияФормИСПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	МенеджерОборудованияКлиент.ОбновитьРабочееМестоКлиента();
	
	ТекущееРабочееМесто = ОбщегоНазначенияИСКлиентСервер.РабочееМестоПользователя();
	
	ПроверитьНастройкиПодключения();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПриСозданииЧтенииНаСервере();
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗащищенноеСоединениеПриИзменении(Элемент)
	
	ПроверитьНастройкиПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура СерверПодключенияПриИзменении(Элемент)
	
	ПроверитьНастройкиПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПортПодключенияПриИзменении(Элемент)
	
	ПроверитьНастройкиПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменНаСервереПриИзменении(Элемент)
	
	Если ОбменНаСервере = 1 Тогда
		Запись.ОбменНаСервере = Ложь;
	Иначе
		Запись.ОбменНаСервере = Истина;
	КонецЕсли;
	
	УправлениеВидимостьюДоступностью(ЭтотОбъект);
	ПроверитьНастройкиПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПроверкаНеВыполненаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНастройкаНеВыполненаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьПроверкаНеВыполняласьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ОбработатьНавигационнуюСсылку(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерФискальногоНакопителяПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(НомерФискальногоНакопителяПоДаннымТСПИоТ) Тогда
		Возврат;
	КонецЕсли;
	
	ОтобразитьНаФормеОтличияНастроекТСПИоТ();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ИННПоДаннымТСПИоТ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Запись.Организация) Тогда
		ПриИзмененииОрганизацииНаСервере();
	Иначе
		ИННОрганизации = "";
	КонецЕсли;
	
	ОтобразитьНаФормеОтличияНастроекТСПИоТ();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьНастройкиПоДаннымТСПИоТ(Команда)
	
	ПроверитьНастройкиПодключения(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаНастройкиПодключенияТСПИоТ

&НаКлиенте
Процедура ПроверитьНастройкиПодключения(ЗаполнитьНастройкиПринудительно = Ложь)
	
	ПерезаполнятьНастройки = Не ЗначениеЗаполнено(Запись.НомерФискальногоНакопителя)
		Или ЗаполнитьНастройкиПринудительно;
	
	Если ПроверитьКорректностьЗаполненияНастроек() Тогда
		ПодключитьОбработчикОжидания("ВыполнитьПроверкуПодключенияНачало", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьКорректностьЗаполненияНастроек()
	
	Возврат ЗначениеЗаполнено(Запись.Сервер)
		И ЗначениеЗаполнено(Запись.Порт);
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПроверкуПодключенияНачало() Экспорт
	
	Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаНеВыполнялась;
	
	Если Не Запись.ОбменНаСервере
		И ЗначениеЗаполнено(Запись.РабочееМесто)
		И Не Запись.РабочееМесто = ТекущееРабочееМесто Тогда
		Возврат;
	КонецЕсли;
	
	ЗапуститьПроверкуПодключения();
	
КонецПроцедуры

#КонецОбласти

#Область ИнтерфейсныеПроцедуры

&НаКлиенте
Процедура ОбработатьНавигационнуюСсылку(Знач НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ПроверитьПодключениеТСПИоТ" Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Если ПроверитьКорректностьЗаполненияНастроек() Тогда
			ВыполнитьПроверкуПодключенияНачало();
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Укажите настройки подключения и попробуйте снова.';
											|en = 'Укажите настройки подключения и попробуйте снова.'"));
		КонецЕсли;
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьНомерФискальногоНакопителя" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоместитьТекстВБуфераОбмена(НомерФискальногоНакопителяПоДаннымТСПИоТ);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СкопироватьИННОрганизации" Тогда
		
		СтандартнаяОбработка = Ложь;
		ПоместитьТекстВБуфераОбмена(ИННПоДаннымТСПИоТ);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ДлительныеПроцедуры

&НаКлиенте
Процедура ЗапуститьПроверкуПодключения()
	
	Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаВыполняется;
	ДополнительныеПараметры = Новый Структура("ЗаполнятьНастройки", ПерезаполнятьНастройки);
	
	ПерезаполнятьНастройки = Ложь;
	ПослеВыполненияОбновления = Новый ОписаниеОповещения("ПослеВыполненияПроверкиНастройкиТСПИоТ", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыПодключения = ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияТСПИоТ();
	ПараметрыПодключения.Сервер               = Запись.Сервер;
	ПараметрыПодключения.ЗащищенноеСоединение = Запись.ЗащищенноеСоединение;
	ПараметрыПодключения.Порт                 = Запись.Порт;
	
	ПараметрыОбработки = Новый Структура();
	ПараметрыОбработки.Вставить("ПараметрыПодключения",   ПараметрыПодключения);
	ПараметрыОбработки.Вставить("НеВыводитьОкноОжидания", Истина);
	
	ПараметрыЗапроса                         = ИнтерфейсИСМПОбщегоНазначенияКлиент.ПараметрыЗапросаДанных();
	ПараметрыЗапроса.ОповещениеОЗавершении   = ПослеВыполненияОбновления;
	ПараметрыЗапроса.УникальныйИдентификатор = УникальныйИдентификатор;
	ПараметрыЗапроса.Параметры               = ПараметрыОбработки;
	ПараметрыЗапроса.ВыполнениеНаКлиенте     = Не Запись.ОбменНаСервере;
	
	ИнтерфейсИСМПОбщегоНазначенияКлиент.ПолучитьДанныеНастройкиТСПИоТ(ПараметрыЗапроса);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыполненияПроверкиНастройкиТСПИоТ(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.ЕстьОшибки Тогда
		
		ШаблонСообщения = НСтр("ru = 'Не удалось подключиться к ТС ПИоТу.
						   |Описание ошибки: %1.';
						   |en = 'Не удалось подключиться к ТС ПИоТу.
						   |Описание ошибки: %1.'");
						   
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Результат.ТекстОшибки);
		ПоказатьПредупреждение(, ТекстСообщения);
		
		Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаНеВыполнена;
		
	Иначе
		
		Если Результат.Свойство("Результат") И ТипЗнч(Результат.Результат) = Тип("Структура") Тогда
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Результат.Результат, "НастройкиЭкземпляраТСПИоТ")
				И Результат.Результат.НастройкиЭкземпляраТСПИоТ <> Неопределено Тогда
				
				НастройкиЭкземпляраТСПИоТ                = Результат.Результат.НастройкиЭкземпляраТСПИоТ;
				НомерФискальногоНакопителяПоДаннымТСПИоТ = НастройкиЭкземпляраТСПИоТ.НомерФискальногоНакопителя;
				ИННПоДаннымТСПИоТ                        = НастройкиЭкземпляраТСПИоТ.ИНН;
				
				Если ДополнительныеПараметры <> Неопределено
					И ДополнительныеПараметры.Свойство("ЗаполнятьНастройки")
					И ДополнительныеПараметры.ЗаполнятьНастройки Тогда
					
					Запись.ЗаводскойНомерККТ             = НастройкиЭкземпляраТСПИоТ.ЗаводскойНомерККТ;
					Запись.ИдентификаторЭкземпляраТСПИоТ = НастройкиЭкземпляраТСПИоТ.ИдентификаторТСПИоТ;
					Запись.ИНН                           = НастройкиЭкземпляраТСПИоТ.ИНН;
					Запись.НомерФискальногоНакопителя    = НастройкиЭкземпляраТСПИоТ.НомерФискальногоНакопителя;
					Запись.ТаймаутПроверкиКодаМаркировки = НастройкиЭкземпляраТСПИоТ.ТаймаутПроверкиКодаМаркировки;
					
				КонецЕсли;
				
				ОтобразитьНаФормеОтличияНастроекТСПИоТ();
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСБуферомОбмена

&НаКлиенте
Асинх Процедура ПоместитьТекстВБуфераОбмена(ТекстДляКопирования)
	
	Если СредстваБуфераОбмена.ИспользованиеДоступно() Тогда
		
		ФорматДанных = СтандартныйФорматДанныхБуфераОбмена.Текст;
		
		Если Ждать СредстваБуфераОбмена.ПоддерживаетсяФорматДанных(ФорматДанных) Тогда
			
			ПомещаемыеДанные = Новый ЭлементБуфераОбмена(ФорматДанных, ТекстДляКопирования);
			ЗавершеноКопирование = Ждать СредстваБуфераОбмена.ПоместитьДанныеАсинх(ПомещаемыеДанные);
			
			Если ЗавершеноКопирование Тогда
				ПоказатьОповещениеПользователя(НСтр("ru = 'Текст скопирован в буфер обмена';
													|en = 'Текст скопирован в буфер обмена'"));
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеСлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииЧтенииНаСервере()
	
	УправлениеВидимостьюДоступностью(ЭтотОбъект);
	ПриИзмененииОрганизацииНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюДоступностью(Форма)
	
	Элементы = Форма.Элементы;
	Запись   = Форма.Запись;
	
	Элементы.РабочееМесто.Доступность = Не Запись.ОбменНаСервере;
	
	Если ЗначениеЗаполнено(Запись.РабочееМесто) И Запись.ОбменНаСервере Тогда
		Запись.РабочееМесто = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииОрганизацииНаСервере()
	
	Если Не ЗначениеЗаполнено(Запись.Организация) Тогда
		Возврат;
	КонецЕсли;
	
	ИННОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Запись.Организация).ИНН;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтобразитьНаФормеОтличияНастроекТСПИоТ()
	
	ОтличияНастроек = ПроверитьОтличияНастроекТСПИоТ();
	ОбновитьПодсказкуЗаполненияОрганизации();
	
	Если ОтличияНастроек.ЕстьОтличияНомераФН Тогда
		
		Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаВыполнена;
		Элементы.СтраницыРезультатНастройки.ТекущаяСтраница  = Элементы.СтраницаНомерФННеСовпадаетСНастройками;
		
		СформироватьНадписьОтличияФНОтНастроекТСПИоТ();
		
	ИначеЕсли ОтличияНастроек.ЕстьОтличияИНН Тогда
		
		Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаВыполнена;
		Элементы.СтраницыРезультатНастройки.ТекущаяСтраница  = Элементы.СтраницаИНННеСовпадаетСНастройками;
		
		СформироватьНадписьОтличияИННОтНастроекТСПИоТ();
		
	Иначе
		
		Элементы.СтраницыПроверкаПодключения.ТекущаяСтраница = Элементы.СтраницаПроверкаВыполнена;
		Элементы.СтраницыРезультатНастройки.ТекущаяСтраница  = Элементы.СтраницаНастройкаЗавершенаКорректно;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьОтличияНастроекТСПИоТ()
	
	СтруктураВозврата = Новый Структура();
	
	СтруктураВозврата.Вставить("ЕстьОтличияНомераФН", НомерФискальногоНакопителяПоДаннымТСПИоТ <> Запись.НомерФискальногоНакопителя);
	СтруктураВозврата.Вставить("ЕстьОтличияИНН",      ИННПоДаннымТСПИоТ <> ИННОрганизации И ЗначениеЗаполнено(ИННОрганизации));
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура СформироватьНадписьОтличияФНОтНастроекТСПИоТ()
	
	МассивСтрок = Новый Массив;
	
	СтрокаВажныйТекст = Новый ФорматированнаяСтрока(НСтр("ru = 'Номер фискального накопителя, указанного при настройке ТС ПИоТ, не совпадает с указанным';
														|en = 'Номер фискального накопителя, указанного при настройке ТС ПИоТ, не совпадает с указанным'"),, ЦветВажногоТекста);
	СтрокаНомерФН     = Новый ФорматированнаяСтрока(Формат(НомерФискальногоНакопителяПоДаннымТСПИоТ, "ЧРГ=; ЧГ=;"),,,, "СкопироватьНомерФискальногоНакопителя");
	
	МассивСтрок.Добавить(СтрокаВажныйТекст);
	МассивСтрок.Добавить(Символы.ПС);
	МассивСтрок.Добавить(НСтр("ru = 'Номер ФН по данным ТС ПИоТ -';
								|en = 'Номер ФН по данным ТС ПИоТ -'"));
	МассивСтрок.Добавить(" ");
	МассивСтрок.Добавить(СтрокаНомерФН);
	
	Элементы.НадписьПроверкаФННеВыполнена.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьНадписьОтличияИННОтНастроекТСПИоТ()
	
	МассивСтрок = Новый Массив;
	
	СтрокаВажныйТекст = Новый ФорматированнаяСтрока(НСтр("ru = 'ИНН владельца ККТ не совпадает с указанным в настройках ТС ПИоТ.';
														|en = 'ИНН владельца ККТ не совпадает с указанным в настройках ТС ПИоТ.'"),, ЦветВажногоТекста);
	СтрокаИНН         = Новый ФорматированнаяСтрока(Формат(ИННПоДаннымТСПИоТ, "ЧРГ=; ЧГ=;"),,,, "СкопироватьИННОрганизации");
	
	МассивСтрок.Добавить(СтрокаВажныйТекст);
	МассивСтрок.Добавить(Символы.ПС);
	МассивСтрок.Добавить(НСтр("ru = 'ИНН по данным ТС ПИоТ -';
								|en = 'ИНН по данным ТС ПИоТ -'"));
	МассивСтрок.Добавить(" ");
	МассивСтрок.Добавить(СтрокаИНН);
	
	Элементы.НадписьПроверкаИНННеВыполнена.Заголовок = Новый ФорматированнаяСтрока(МассивСтрок);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПодсказкуЗаполненияОрганизации()
	
	ПодключитьОбработчикОжидания("ВыполнитьПоискОрганизацииПоИНН", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПоискОрганизацииПоИНН() Экспорт
	
	Если Не ЗначениеЗаполнено(ИННПоДаннымТСПИоТ) Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПоискОрганизацииПоИНННаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПоискОрганизацииПоИНННаСервере()
	
	ОрганизацияПоИНН = Неопределено;
	
	ОбщегоНазначенияИСПереопределяемый.ЗаполнитьОрганизациюПоИННКПП(ОрганизацияПоИНН, ИННПоДаннымТСПИоТ, "");
	Элементы.Организация.ПодсказкаВвода = Строка(ОрганизацияПоИНН);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
