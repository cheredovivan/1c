#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Функция определяет требуется ли отразить факт в бюджетировании при формировании отчета.
//
// Параметры:
//  НачалоПериода  - Дата - Необязательный, начало периода.
//  КонецПериода  - Дата - Необязательный, окончание периода.
//  КоличествоНеактуальныхДокументов  - Число - В параметр сохраняется количество документов для отражения факта.
//  ЗапускатьФоновоеЗадание - Булево - Истина, если требуется запускать фоновое задание отражения в бюджетировании.								     
//
// Возвращаемое значение:
//   Булево - Признак, что требуется отразить факт в бюджетировании при формировании отчета. 
//
Функция ТребуетсяОтражениеВБюджетированииДляОтчетаЗаПериод(Знач НачалоПериода = Неопределено,
	Знач КонецПериода = Неопределено, КоличествоНеактуальныхДокументов = 0, ЗапускатьФоновоеЗадание = Ложь,
	ИменаВидовДокументов = Неопределено) Экспорт

	Если Не БюджетированиеПовтИсп.ИспользуетсяХранимыйФактПоСтатьямБюджетов() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ЗапускатьФоновоеЗадание = Константы.АктуализироватьФактБюджетированияПриФормированииОтчетов.Получить();
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Если Не ЗначениеЗаполнено(НачалоПериода) Тогда
		НачалоПериода = Дата(1,1,1);
	КонецЕсли;

	Если Не ЗначениеЗаполнено(КонецПериода) Тогда
		КонецПериода = Дата(3999, 12, 31, 23, 59, 59);
	КонецЕсли;
	
	ФактическиеДанныеБюджетированияСервер.ПоместитьДокументыДляОтраженияВоВременнуюТаблицу(НачалоПериода, КонецПериода, МенеджерВременныхТаблиц, ИменаВидовДокументов);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеДокументыКОтражению.Документ) КАК Количество
	|ИЗ
	|	ДокументыКОтражению КАК ВсеДокументыКОтражению";
		
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		КоличествоНеактуальныхДокументов = 0;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		КоличествоНеактуальныхДокументов = Выборка.Количество;
	КонецЕсли;
	
	Возврат КоличествоНеактуальныхДокументов > 0;
	
КонецФункции

#КонецОбласти

#КонецЕсли