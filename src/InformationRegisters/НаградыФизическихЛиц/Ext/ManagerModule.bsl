
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Награды физических лиц.
// 
// Параметры:
//  ФизическиеЛица	- Массив - список физических лиц.
//  НаДату 			- Дата	 - срез на дату (проверка по дате приказа).
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Награды физических лиц
Функция НаградыФизическихЛиц(ФизическиеЛица, НаДату = Неопределено) Экспорт
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ФизическиеЛица", ФизическиеЛица);
	Запрос.УстановитьПараметр("ДатаПриказа", НаДату);
	Запрос.УстановитьПараметр("ОтборПоДате", ТипЗнч(НаДату) = Тип("Дата"));
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НаградыФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
		|	НаградыФизическихЛиц.Награда КАК Награда,
		|	ЕСТЬNULL(КатегорииНаград.Ссылка, ЗНАЧЕНИЕ(Справочник.КатегорииНаград.ПустаяСсылка)) КАК КатегорияНаграды,
		|	НаградыФизическихЛиц.ДатаПриказа КАК ДатаПриказа,
		|	ПРЕДСТАВЛЕНИЕ(НаградыФизическихЛиц.Награда) КАК ПредставлениеНаграды
		|ИЗ
		|	РегистрСведений.НаградыФизическихЛиц КАК НаградыФизическихЛиц
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Награды КАК Награды
		|		ПО НаградыФизическихЛиц.Награда = Награды.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КатегорииНаград КАК КатегорииНаград
		|		ПО (Награды.КатегорияНаграды = КатегорииНаград.Ссылка)
		|ГДЕ
		|	НаградыФизическихЛиц.ФизическоеЛицо В(&ФизическиеЛица)
		|	И (НаградыФизическихЛиц.ДатаПриказа <= &ДатаПриказа
		|			ИЛИ НЕ &ОтборПоДате)";
	
	НаградыФизическихЛиц = Запрос.Выполнить().Выгрузить();
	
	Возврат НаградыФизическихЛиц;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ОбработчикиПравилРегистрации

// См. ЗарплатаКадрыРасширенныйСинхронизацияДанных.ШаблонОбработчика
Процедура ПриЗаполненииНастроекОбработчиковПравилРегистрации(Настройки) Экспорт
	ЗарплатаКадрыРасширенныйСинхронизацияДанных.ДляНезависимогоРегистра(Настройки, "ФизическоеЛицо",
		"РегламентированныеДанные");
КонецПроцедуры

#КонецОбласти

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьНаградыФизическихЛиц(КоллекцияЗаписей, НеПроверятьЗаписиДанныхФизическихЛиц = Ложь) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Загрузить(КоллекцияЗаписей);
	
	Если НеПроверятьЗаписиДанныхФизическихЛиц Тогда
		НаборЗаписей.ДополнительныеСвойства.Вставить("НеПроверятьЗаписиДанныхФизическихЛиц", НеПроверятьЗаписиДанныхФизическихЛиц);
	КонецЕсли;
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Процедура ОчиститьНаградыФизическихЛиц(ДокументРегистратор) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НаградыФизическихЛиц.ФизическоеЛицо КАК ФизическоеЛицо,
	|	НаградыФизическихЛиц.НомерПоПорядку КАК НомерПоПорядку
	|ИЗ
	|	РегистрСведений.НаградыФизическихЛиц КАК НаградыФизическихЛиц
	|ГДЕ
	|	НаградыФизическихЛиц.ДокументРегистратор = &ДокументРегистратор";
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументРегистратор);
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ФизическоеЛицо.Установить(Выборка.ФизическоеЛицо);
			НаборЗаписей.Отбор.НомерПоПорядку.Установить(Выборка.НомерПоПорядку);
			НаборЗаписей.Записать();
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
