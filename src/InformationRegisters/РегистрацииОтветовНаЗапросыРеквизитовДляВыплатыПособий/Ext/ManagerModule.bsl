///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(ИсходящийДокумент)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ИзменениеОбъектаРазрешено(ИсходящийДокумент)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти


#Область СлужебныйПрограммныйИнтерфейс

// АПК:581-выкл. Методы могут вызываться из расширений.
// АПК:299-выкл. Методы могут вызываться из расширений.
// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   ИсходящийДокумент - ДокументСсылка.ОтветНаЗапросРеквизитовДляВыплатыПособия - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(ИсходящийДокумент) Экспорт
	Если Не ЗначениеЗаполнено(ИсходящийДокумент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".';
				|en = 'Insufficient rights to change register ""%1"".'"),
			Метаданные.РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий");
		ЭлементБлокировки.УстановитьЗначение("ИсходящийДокумент", ИсходящийДокумент);
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИсходящийДокумент.Установить(ИсходящийДокумент);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить регистрацию сведений для расчета пособия ФСС %1 по причине: %2';
				|en = 'Cannot change registration of the information for SSF allowance calculation %1 due to: %2'"),
			ИсходящийДокумент,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий,
			ИсходящийДокумент,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей) Экспорт
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей) Экспорт
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.
// АПК:299-вкл.
// АПК:581-вкл.

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументу(ИсходящийДокументОбъект, РезультатОтправки = Неопределено) Экспорт
	// Регистрация является продолжением текущего документа.
	// Но если документ - новый, то поле "Ссылка" может быть не заполнено.
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = НачатьЗаписьНабора(ИсходящийДокументОбъект.Ссылка);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	
	Запись.ИсходящийДокумент   = ИсходящийДокументОбъект.Ссылка;
	Запись.ФизическоеЛицо      = ИсходящийДокументОбъект.ФизическоеЛицо;
	Запись.ГоловнаяОрганизация = ИсходящийДокументОбъект.ГоловнаяОрганизация;
	
	Если РезультатОтправки <> Неопределено Тогда
		Запись.Страхователь                = РезультатОтправки.Страхователь;
		Запись.ОтправленОператору          = РезультатОтправки.ОтправленоОператору;
		Запись.Доставлен                   = РезультатОтправки.ДоставленоФонду;
		Запись.ДатаОтправкиОператору       = РезультатОтправки.ДатаОтправкиОператору;
		Запись.ДатаОтправки                = РезультатОтправки.ДатаОтправкиФонду;
		Запись.ДоставкаТекстОшибки         = РезультатОтправки.ТекстОшибки;
		Запись.ДоставкаИдентификатор       = РезультатОтправки.ИдентификаторСообщения;
		Запись.ДоставкаИдентификаторПакета = РезультатОтправки.ИдентификаторПакета;
	КонецЕсли;
	
	ЗавершитьЗаписьНабора(Набор);
КонецПроцедуры

#Область СЭДО

Функция НайтиПоИдентификаторуСообщения(Страхователь, ИдентификаторСообщения, РезультатБРО) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистрация.ИсходящийДокумент КАК ИсходящийДокумент,
	|	Регистрация.Страхователь КАК Страхователь,
	|	Регистрация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК Регистрация
	|ГДЕ
	|	Регистрация.ДоставкаИдентификатор = &ИдентификаторСообщения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Регистрация.ИсходящийДокумент,
	|	Регистрация.Страхователь,
	|	Регистрация.ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий КАК Регистрация
	|ГДЕ
	|	Регистрация.РегистрацияИдентификатор = &ИдентификаторСообщения";
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
	Таблица = Запрос.Выполнить().Выгрузить();
	Найденные = Таблица.Скопировать(Новый Структура("Страхователь", Страхователь));
	Если Найденные.Количество() = 0 Тогда
		ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
		Найденные = Таблица.Скопировать(Новый Структура("ГоловнаяОрганизация", ГоловнаяОрганизация));
		Если Найденные.Количество() = 0 Тогда
			Найденные = Таблица;
		КонецЕсли;
	КонецЕсли;
	Если Найденные.Количество() = 0 Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'По страхователю ""%1"" не найдена %2 с идентификатором сообщения ""%3""';
				|en = 'No %2 with message ID ""%3"" was found for insurant ""%1""'"),
			Страхователь,
			Метаданные.РегистрыСведений.РегистрацииОтветовНаЗапросыРеквизитовДляВыплатыПособий.ПредставлениеЗаписи,
			ИдентификаторСообщения);
		СЭДОФСС.ОшибкаОбработки(РезультатБРО, ТекстОшибки);
	КонецЕсли;
	Возврат Найденные;
КонецФункции

Процедура ЗаполнитьПоРезультатуДоставки(ИсходящийДокумент, Сообщение) Экспорт
	Набор = НачатьЗаписьНабора(ИсходящийДокумент);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		Запись.ИсходящийДокумент = ИсходящийДокумент;
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	
	Запись.Доставлен           = Сообщение.ДоставленоФонду;
	Запись.ДатаОтправки        = Сообщение.ДатаОтправкиФонду;
	Запись.ДоставкаТекстОшибки = Сообщение.ТекстОшибки;
	
	ЗавершитьЗаписьНабора(Набор);
КонецПроцедуры

// Загружает ошибку логического контроля (тип 14) полученную в ответ на документ СЭДО 33.
Процедура ЗагрузитьОшибкуСообщения33(Страхователь, ИдентификаторСообщения, ТекстОшибки, Результат) Экспорт
	Таблица = НайтиПоИдентификаторуСообщения(Страхователь, ИдентификаторСообщения, Результат);
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Набор = НачатьЗаписьНабора(СтрокаТаблицы.ИсходящийДокумент);
		Если Набор.Количество() = 0 Тогда
			ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись = Набор[0];
		Запись.ОшибкиЛогическогоКонтроля = СокрЛП(ТекстОшибки);
		Запись.ЕстьОшибкиЛогическогоКонтроля = Истина;
		ЗавершитьЗаписьНабора(Набор);
		// Отметка для БРО и других потребителей что сообщение обработано.
		Результат.Обработано = Истина;
	КонецЦикла;
КонецПроцедуры

// Загружает результат регистрации документа.
Процедура ЗагрузитьСообщение34(Страхователь, РегистрацияИдентификатор, ТекстXML, Результат, Кэш) Экспорт
	// Пример:
	//<n1:noticeResponseResult xmlns:n1="urn:ru:ecp:integration:vnim:notice:responseResult:v3.0"
	//		specVersion="3.0.0"
	//		responseOn="a0930cd4-7626-464f-93c9-64e2b7be0777"
	//		messageDateTime="2024-04-20T09:31:47Z">
	//	<status>PROCESSED</status>
	//	<processedInfo/>
	//</n1:noticeResponseResult>
	
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'noticeResponseResult']");
	Если КореньDOM = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "noticeResponseResult");
		Возврат;
	КонецЕсли;
	
	АтрибутыКорня = СериализацияБЗК.АтрибутыЭлементаDOM(КореньDOM, "specVersion, responseOn, messageDateTime");
	Если АтрибутыКорня.responseOn = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаАтрибута(Результат, "responseOn");
		Возврат;
	КонецЕсли;
	ДоставкаИдентификатор = СериализацияБЗК.СтрокаИзXML(АтрибутыКорня.responseOn);
	Если Не ЗначениеЗаполнено(ДоставкаИдентификатор) Тогда
		СЭДОФСС.ЗаписатьОшибкуЗаполненностиАтрибута(Результат, "responseOn");
		Возврат;
	КонецЕсли;
	
	РегистрацияДата = СЭДОФСС.ДатаСообщения(РегистрацияИдентификатор, Кэш);
	РегистрацияДатаСФР = СериализацияБЗК.ДатаИзXML(АтрибутыКорня.messageDateTime);
	СЭДОФСС.ОбновитьДатуСообщения(РегистрацияДата, НачалоДня(РегистрацияДата), РегистрацияДатаСФР);
	
	РеквизитыКорня = СериализацияБЗК.УзлыЭлементаDOM(КореньDOM, "status, warnInfo, processedInfo, errorInfo");
	
	РегистрацияСтатус = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.status);
	СведенияОСтатусе  = СведенияОСтатусеРегистрации(РегистрацияСтатус);
	
	ТекстыПредупреждений = "";
	Если РеквизитыКорня.warnInfo <> Неопределено Тогда
		УзелОшибок = СериализацияБЗК.УзлыЭлементаDOM(РеквизитыКорня.warnInfo, "warnList");
		Если УзелОшибок.warnList <> Неопределено Тогда
			ТекстыПредупреждений = ПредставлениеСпискаОшибок(УзелОшибок.warnList, НСтр("ru = 'Предупреждения';
																						|en = 'Warnings'"));
		КонецЕсли;
	КонецЕсли;
	
	ТекстыОшибок = "";
	Если РеквизитыКорня.errorInfo <> Неопределено Тогда
		УзелОшибок = СериализацияБЗК.УзлыЭлементаDOM(РеквизитыКорня.errorInfo, "errorList");
		Если УзелОшибок.errorList <> Неопределено Тогда
			ТекстыОшибок = ПредставлениеСпискаОшибок(УзелОшибок.errorList, НСтр("ru = 'Ошибки';
																				|en = 'Errors'"));
		КонецЕсли;
	КонецЕсли;
	
	Если ТекстыПредупреждений = "" Тогда
		РегистрацияПротокол = ТекстыОшибок;
	ИначеЕсли ТекстыОшибок = "" Тогда
		РегистрацияПротокол = ТекстыПредупреждений;
	Иначе
		РегистрацияПротокол = ТекстыОшибок + Разделитель() + ТекстыПредупреждений;
	КонецЕсли;
	Если ЗначениеЗаполнено(РегистрацияСтатус) И Не ЗначениеЗаполнено(СведенияОСтатусе.Расшифровка) Тогда
		Текст = СтрШаблон(НСтр("ru = 'Неизвестный статус: ""%1"".';
								|en = 'Unknown status: ""%1"".'"), РегистрацияСтатус);
		РегистрацияПротокол = СокрЛП(Текст + Разделитель() + РегистрацияПротокол);
	КонецЕсли;
	
	Найденные = Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия.НайтиПоИдентификатору(ДоставкаИдентификатор, 1);
	Если Найденные.Количество() = 0 Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось найти %1 с идентификатором ""%2"".%3';
				|en = 'Cannot find %1 with ID %2.%3'"),
			Метаданные.Документы.ОтветНаЗапросРеквизитовДляВыплатыПособия.Представление(),
			ДоставкаИдентификатор,
			Разделитель() + РегистрацияПротокол);
		СЭДОФСС.ОшибкаОбработки(Результат, ТекстОшибки);
		Возврат;
	КонецЕсли;
	ОтветНаЗапрос = Найденные[0].Ссылка;
	
	СообщениеАктуальное = Истина;
	
	Набор = НачатьЗаписьНабора(ОтветНаЗапрос);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ОтветНаЗапрос);
		Запись.ИсходящийДокумент = ОтветНаЗапрос;
	Иначе
		Запись = Набор[0];
		Если ЗначениеЗаполнено(Запись.РегистрацияСтатус)
			И РегистрацияИдентификатор <> Запись.РегистрацияИдентификатор Тогда
			СуществующийСтатус = СведенияОСтатусеРегистрации(Запись.РегистрацияСтатус);
			Если СведенияОСтатусе.Вес < СуществующийСтатус.Вес Тогда
				СообщениеАктуальное = Ложь; // Неактуальный (старый) статус.
			ИначеЕсли СведенияОСтатусе.Вес = СуществующийСтатус.Вес
				И РегистрацияДата < Запись.РегистрацияДата Тогда
				СообщениеАктуальное = Ложь; // Более старая регистрация.
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если СообщениеАктуальное Тогда
		
		Запись.Зарегистрирован          = СведенияОСтатусе.Зарегистрирован;
		Запись.РегистрацияДата          = РегистрацияДата;
		Запись.РегистрацияСтатус        = РегистрацияСтатус;
		Запись.РегистрацияПротокол      = РегистрацияПротокол;
		Запись.РегистрацияИдентификатор = РегистрацияИдентификатор;
		
		ЗавершитьЗаписьНабора(Набор);
		
	Иначе
		
		ОтменитьЗаписьНабора(Набор);
		
	КонецЕсли;
	
	Результат.Обработано = Истина;
КонецПроцедуры

Функция ПредставлениеСпискаОшибок(СписокОшибок, ЗаголовокСписка = "")
	Результат = СЭДОФСС.ПредставлениеСпискаОшибокXML(СписокОшибок);
	Если ПустаяСтрока(Результат) Тогда
		Возврат "";
	КонецЕсли;
	Если ЗаголовокСписка = "" Тогда
		Возврат Результат;
	Иначе
		Возврат ЗаголовокСписка + " (" + СтрЧислоСтрок(Результат) + "): " + Результат;
	КонецЕсли;
КонецФункции

Функция СведенияОСтатусеРегистрации(Статус)
	Результат = Новый Структура("Вес, Зарегистрирован, Расшифровка");
	
	СтатусВРег = ВРег(СокрЛП(Статус));
	
	// Статусы из актуальной спецификации.
	Если СтатусВРег = "RECEIVED" Тогда
		Результат.Вес             = 1;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Документ получен и проверяется Фондом';
										|en = 'The document is received and sent for verification by the Social Security Fund'");
		
	ИначеЕсли СтатусВРег = "ERROR" Тогда
		Результат.Вес             = 4;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Выявлены ошибки, документ отклонен Фондом';
										|en = 'Errors are found. The document is rejected by the Social Security Fund'");
		
	ИначеЕсли СтатусВРег = "PROCESSED" Тогда
		Результат.Вес             = 3;
		Результат.Зарегистрирован = Истина;
		Результат.Расшифровка     = НСтр("ru = 'Документ принят Фондом';
										|en = 'The document is accepted by the Social Security Fund'");
		
	Иначе
		Результат.Вес             = 0;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = "";
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьСписокВыбораСтатусов(ПолеВвода, Статус) Экспорт
	СписокВыбора = ПолеВвода.СписокВыбора;
	Если СписокВыбора.Количество() = 0 Тогда
		Ключи = СтрРазделить("RECEIVED,ERROR,PROCESSED", ",");
		Для Каждого Ключ Из Ключи Цикл
			СведенияОСтатусе = СведенияОСтатусеРегистрации(Ключ);
			СписокВыбора.Добавить(Ключ, СведенияОСтатусе.Расшифровка + " (" + Ключ + ")");
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(Статус) И СписокВыбора.НайтиПоЗначению(Статус) = Неопределено Тогда
		СведенияОСтатусе = СведенияОСтатусеРегистрации(Статус);
		СписокВыбора.Добавить(Статус, СведенияОСтатусе.Расшифровка);
	КонецЕсли;
КонецПроцедуры

Функция Разделитель()
	Возврат Символы.ПС + "----" + Символы.ПС;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли