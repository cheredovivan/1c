
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.НастройкаОбмена) Тогда
		ТекущаяЗапись = РегистрыСведений.ПараметрыОбменСБанками.СоздатьМенеджерЗаписи();
		ТекущаяЗапись.НастройкаОбмена = Параметры.НастройкаОбмена;
		ТекущаяЗапись.Прочитать();
		
		// Для новой настройки записей в регистре еще нет
		Если Не ТекущаяЗапись.Выбран() Тогда
			ТекущаяЗапись.НастройкаОбмена = Параметры.НастройкаОбмена;
			КоличествоСообщенийВПакете = 1;
		КонецЕсли;
	
		ЗначениеВРеквизитФормы(ТекущаяЗапись, "Запись");
		
		РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Параметры.НастройкаОбмена, 
			"ПрограммаБанка, ИспользуетсяКриптография");
		ПрограммаБанка = РеквизитыНастройкиОбмена.ПрограммаБанка;
		ИспользуетсяКриптография = РеквизитыНастройкиОбмена.ИспользуетсяКриптография;
		
	КонецЕсли;
	
	Элементы.КаталогДляЖурналирования.Видимость = Запись.ИспользоватьЖурналирование
		И (ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК
			ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн И ИспользуетсяКриптография);

	Элементы.ОткрытьЖурнал.Видимость = НЕ Элементы.КаталогДляЖурналирования.Видимость
										И Запись.ИспользоватьЖурналирование;
										
	Элементы.ДатаПолученияЭД.Видимость = 
		ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен;
	Элементы.ИспользоватьАвтоматическоеОпределениеWindows7.Видимость = 
		ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн;
		
	Элементы.ГруппаПакетныйОбмен.Видимость = ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен;
	КоличествоСообщенийВПакете = Запись.КоличествоСообщенийВПакете;
	ИспользоватьПакетныйОбмен = КоличествоСообщенийВПакете > 1;
	ПоказатьПредупреждениеОКоличествеСообщений = КоличествоСообщенийВПакете <= 50;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.КаталогДляЖурналирования.КнопкаВыбора = Ложь;
		Оповещение = Новый ОписаниеОповещения("ПослеПодключенияРасширенияДляРаботыСФайламиПриОткрытии", ЭтотОбъект);
		НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
	#КонецЕсли
	
	СистемнаяИнформация = Новый СистемнаяИнформация;
	ИдентификаторКлиента = СистемнаяИнформация.ИдентификаторКлиента;
	
	ИспользоватьАвтоматическоеОпределениеWindows7 = ОбменСБанкамиСлужебныйВызовСервера.ПолучитьНастройкуПользователяДиректБанк(
		ИдентификаторКлиента,
		"VPNKeyTLS", 
		"ИспользоватьАвтоматическоеОпределениеWindows7"); 
		
	ИспользоватьПакетныйОбменОбработкаИзменения();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ИспользоватьЖурналирование И Не ЗначениеЗаполнено(Запись.КаталогДляЖурналирования)
		И (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
			ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
				И ИспользуетсяКриптография) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите каталог для журналирования.';
								|en = 'Specify a directory for logging.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , "Запись.КаталогДляЖурналирования", , Отказ);
		Возврат;
	КонецЕсли;
	
	Запись.ИдентификаторКлиента = ИдентификаторКлиента; 
	
	Запись.КоличествоСообщенийВПакете = ?(КоличествоСообщенийВПакете = 0,1,КоличествоСообщенийВПакете);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ОбменСБанкамиСлужебныйКлиент.СохранитьНастройкуПользователяДиректБанк(
		"VPNKeyTLS",
		"ИспользоватьАвтоматическоеОпределениеWindows7", ИспользоватьАвтоматическоеОпределениеWindows7);
	Если НЕ ИспользоватьАвтоматическоеОпределениеWindows7 Тогда
		ОбменСБанкамиСлужебныйКлиент.СохранитьНастройкуПользователяДиректБанк(
			"VPNKeyTLS",
			"ОчиститьВерсииWindowsКлиентскихПриложений",
			Неопределено);
		// Очистим кеш с версией компоненты 1.2.9.6 для Windows7
		ОбменСБанкамиСлужебныйКлиент.ЗакешироватьПараметрСбербанка("VPNKeyTLS_1_2_9_6", Неопределено);
	КонецЕсли;
	
	Оповестить("ИзмененаНастройкаОбменСБанками", Запись.НастройкаОбмена);
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьЖурналированиеПриИзменении(Элемент)
	
	Если Запись.ИспользоватьЖурналирование
		И Не ОбменСБанкамиСлужебныйВызовСервера.ЖурналРегистрацииПоддерживаетСобытияИнформация() Тогда
		
		Запись.ИспользоватьЖурналирование = Ложь;
		ПредупреждениеАсинх(НСтр(
			"ru = 'Журналирование обмена с банком недоступно, т.к. в журнале 
			|регистрации отключены события уровня Информация.
			|
			|Для настройки журнала регистрации:
			|1. Запустите конфигуратор.
			|2. Перейдите в меню ""Администрирование"".
			|3. Выберите пункт ""Настройка журнала регистрации"".
			|4. В окне ""Настройки журнала регистрации"" выберите один из вариантов:
			|- Регистрировать ошибки, предупреждения, информацию;
			|или
			|- Регистрировать ошибки, предупреждения, информацию, примечания.';
			|en = 'Bank exchange logging is unavailable as Information level
			|events are disabled in the event log.
			|
			|To configure the event log:
			|1. Run Designer.
			|2. Go to Administration.
			|3. Click Event log settings.
			|In the ""Event log settings"" window, select one of the following options:
			|- Register errors, warnings, and information
			|or
			|- Register errors, warnings, information, and notes.'"));
		Возврат;
	КонецЕсли;	

	Элементы.КаталогДляЖурналирования.Видимость = Запись.ИспользоватьЖурналирование
		И (ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
			ИЛИ ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн")
				И ИспользуетсяКриптография);

	Элементы.ОткрытьЖурнал.Видимость = НЕ Элементы.КаталогДляЖурналирования.Видимость И Запись.ИспользоватьЖурналирование;

КонецПроцедуры

&НаКлиенте
Процедура КаталогДляЖурналированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКаталога", ЭтотОбъект);
	ФайловаяСистемаКлиент.ВыбратьКаталог(Оповещение, НСтр("ru = 'Выберите каталог для сохранения файлов журнала.';
															|en = 'Select a directory for saving log files.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьЖурналНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура("НастройкаОбмена", Запись.НастройкаОбмена);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЖурналОбмена", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПакетныйОбменПриИзменении(Элемент)
	
	Если ИспользоватьПакетныйОбмен Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииОтветаНаВопросИспользоватьПакетныйОбмен",ЭтотОбъект);
		
		МассивТекста = Новый Массив;
		МассивТекста.Добавить(НСтр("ru = 'Перед изменением параметра необходимо проконсультироваться со службой технической поддержки банка.';
									|en = 'Before you change the parameter, consult the bank technical support.'"));
		МассивТекста.Добавить("");
		МассивТекста.Добавить(НСтр("ru = 'Продолжить?';
									|en = 'Continue?'"));
		ТекстВопроса = СтрСоединить(МассивТекста,Символы.ПС);
		
		КнопкиОтвета = Новый СписокЗначений;
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Да,НСтр("ru = 'Продолжить';
														|en = 'Continue'"));
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена';
																|en = 'Cancel'"));
		
		ПоказатьВопрос(ОписаниеОповещения,ТекстВопроса,КнопкиОтвета,,,НСтр("ru = 'Внимание!';
																			|en = 'Warning.'"));
	Иначе
		КоличествоСообщенийВПакете = 1;
		ИспользоватьПакетныйОбменОбработкаИзменения();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСообщенийВПакетеРегулирование(Элемент, Направление, СтандартнаяОбработка)
	                         
	КоличествоСообщенийВПакетеПриИзменении(Элемент);
		
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСообщенийВПакетеПриИзменении(Элемент)
	
	Если КоличествоСообщенийВПакете > 50 И ПоказатьПредупреждениеОКоличествеСообщений Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриПолученииОтветаНаВопросКоличествоСообщенийВПакете", ЭтотОбъект);
		
		МассивТекста = Новый Массив;
		МассивТекста.Добавить(НСтр("ru = 'При установке значения параметра более 50 необходимо проконсультироваться со службой  технической поддержки банка.';
									|en = 'If you want to set the parameter value to more than 50, consult the bank technical support.'"));
		МассивТекста.Добавить("");
		МассивТекста.Добавить(НСтр("ru = 'Продолжить?';
									|en = 'Continue?'"));
		ТекстВопроса = СтрСоединить(МассивТекста,Символы.ПС);
		
		КнопкиОтвета = Новый СписокЗначений;
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Да,НСтр("ru = 'Продолжить';
														|en = 'Continue'"));
		КнопкиОтвета.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена';
																|en = 'Cancel'"));
		
		ПоказатьВопрос(ОписаниеОповещения,ТекстВопроса,КнопкиОтвета,,,НСтр("ru = 'Внимание!';
																			|en = 'Warning.'"));
	ИначеЕсли КоличествоСообщенийВПакете < 1 Тогда
		КоличествоСообщенийВПакете = 1;
	КонецЕсли;
	
	ИспользоватьПакетныйОбмен = КоличествоСообщенийВПакете > 1;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеПодключенияРасширенияДляРаботыСФайламиПриОткрытии(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		Элементы.КаталогДляЖурналирования.КнопкаВыбора = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораКаталога(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = "" Тогда
		Возврат;
	КонецЕсли;
	
	Запись.КаталогДляЖурналирования = Результат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриПолученииОтветаНаВопросКоличествоСообщенийВПакете(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		КоличествоСообщенийВПакете = 50;
	Иначе
		ПоказатьПредупреждениеОКоличествеСообщений = Ложь;
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура ПриПолученииОтветаНаВопросИспользоватьПакетныйОбмен(Результат, ДополнительныеПараметры) Экспорт
	
	ИспользоватьПакетныйОбмен = Результат = КодВозвратаДиалога.Да;		

	Если ИспользоватьПакетныйОбмен Тогда
		КоличествоСообщенийВПакете = 50;		
	КонецЕсли;
	
	ИспользоватьПакетныйОбменОбработкаИзменения();
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьПакетныйОбменОбработкаИзменения()
	
	Элементы.ГруппаКоличествоСообщенийВПакете.Видимость = ИспользоватьПакетныйОбмен;	
	
КонецПроцедуры

#КонецОбласти

