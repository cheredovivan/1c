////////////////////////////////////////////////////////////////////////////////
// Модуль менеджера РегистрСведений.ПараметрыОбменСБанками
//  
////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Сохраняет параметры обмена с банками в регистр ПараметрыОбменСБанками
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком.
//	Параметры - Структура - сохраняемые параметры.
//
Процедура СохранитьЗначенияПараметровОбмена(НастройкаОбмена, Параметры) Экспорт

	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.НастройкаОбмена = НастройкаОбмена;
	МенеджерЗаписи.Прочитать();
	МенеджерЗаписи.НастройкаОбмена = НастройкаОбмена;
	
	Для Каждого Параметр Из Параметры Цикл
		
		МенеджерЗаписи[Параметр.Ключ] = Параметр.Значение;
		
	КонецЦикла;
	
	МенеджерЗаписи.Записать();

КонецПроцедуры

// Читает параметры обмена с банками из регистра ПараметрыОбменСБанками
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком.
//	Параметры - Структура - структура с именами ключей необходимых параметров.
//
Процедура ПрочитатьЗначенияПараметровОбмена(НастройкаОбмена, Параметры) Экспорт
	
	ТекстЗапросаПолей = "";
	
	Для Каждого Поле Из Параметры Цикл
		ТекстЗапросаПолей = 
			ТекстЗапросаПолей + ?(ПустаяСтрока(ТекстЗапросаПолей), "", ",") +
			"	" + Поле.Ключ + " КАК " + Поле.Ключ;
	КонецЦикла;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&ИменаПолей
	|ИЗ
	|	РегистрСведений.ПараметрыОбменСБанками КАК ПараметрыОбменСБанками
	|ГДЕ
	|	ПараметрыОбменСБанками.НастройкаОбмена = &НастройкаОбмена";
	
	Запрос = Новый Запрос(СтрЗаменить(ТекстЗапроса, "&ИменаПолей", ТекстЗапросаПолей));
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
		
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(Параметры, Выборка);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли