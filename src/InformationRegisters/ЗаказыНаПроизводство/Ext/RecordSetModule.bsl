#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Номенклатура          КАК Номенклатура,
	|	Таблица.Характеристика        КАК Характеристика,
	|	Таблица.Склад                 КАК Склад,
	|	Таблица.Назначение            КАК Назначение,
	|	Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|	Таблица.КлючНоменклатура      КАК КлючНоменклатура,
	|	Таблица.Спецификация          КАК Спецификация,
	|	Таблица.НачатьНеРанее         КАК НачатьНеРанее,
	|	Таблица.ДатаПотребности       КАК ДатаПотребности,
	|	Таблица.ДлительностьДоВыпуска КАК ДлительностьДоВыпуска,
	|	Таблица.Этап                  КАК Этап,
	|	Таблица.Партия                КАК Партия,
	|	Таблица.Подразделение         КАК Подразделение,
	|	Таблица.ДатаПоступления       КАК ДатаПоступления,
	|	Таблица.ЗаказНаПоступление    КАК ЗаказНаПоступление,
	|	Таблица.Заказано              КАК Заказано,
	|	Таблица.КПроизводству         КАК КПроизводству,
	|	Таблица.Запущено              КАК Запущено,
	|	Таблица.Готово                КАК Готово
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоПередЗаписью
	|ИЗ
	|	РегистрСведений.ЗаказыНаПроизводство КАК Таблица
	|ГДЕ
	|	Таблица.Регистратор = &Регистратор
	|;";
	Запрос.Выполнить();     

	УстановитьБлокировкуЗаказов(МенеджерВременныхТаблиц);
	
	ДополнительныеСвойства.Вставить("ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа", МенеджерВременныхТаблиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИзменений.Номенклатура         КАК Номенклатура,
	|	ТаблицаИзменений.Характеристика       КАК Характеристика,
	|	ТаблицаИзменений.Склад                КАК Склад,
	|	ТаблицаИзменений.Назначение           КАК Назначение,
	|	ТаблицаИзменений.ЗаказНаПроизводство  КАК ЗаказНаПроизводство,
	|	СУММА(ТаблицаИзменений.КПроизводству) КАК КПроизводству,
	|	СУММА(ТаблицаИзменений.КПроизводствуИзменение) КАК КПроизводствуИзменение
	|ПОМЕСТИТЬ ДвиженияЗаказыНаПроизводствоИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		Таблица.Номенклатура          КАК Номенклатура,
	|		Таблица.Характеристика        КАК Характеристика,
	|		Таблица.Склад                 КАК Склад,
	|		Таблица.Назначение            КАК Назначение,
	|		Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|		Таблица.КлючНоменклатура      КАК КлючНоменклатура,
	|		Таблица.Спецификация          КАК Спецификация,
	|		Таблица.ДлительностьДоВыпуска КАК ДлительностьДоВыпуска,
	|		Таблица.Этап                  КАК Этап,
	|		Таблица.Партия                КАК Партия,
	|		Таблица.Подразделение         КАК Подразделение,
	|		Таблица.ДатаПоступления       КАК ДатаПоступления,
	|		Таблица.ЗаказНаПоступление    КАК ЗаказНаПоступление,
	|		0                             КАК КПроизводству,
	|		-Таблица.Заказано             КАК ЗаказаноИзменение,
	|		-Таблица.КПроизводству        КАК КПроизводствуИзменение,
	|		-Таблица.Запущено             КАК ЗапущеноИзменение,
	|		-Таблица.Готово               КАК ГотовоИзменение
	|	ИЗ
	|		ДвиженияЗаказыНаПроизводствоПередЗаписью КАК Таблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Таблица.Номенклатура          КАК Номенклатура,
	|		Таблица.Характеристика        КАК Характеристика,
	|		Таблица.Склад                 КАК Склад,
	|		Таблица.Назначение            КАК Назначение,
	|		Таблица.ЗаказНаПроизводство   КАК ЗаказНаПроизводство,
	|		Таблица.КлючНоменклатура      КАК КлючНоменклатура,
	|		Таблица.Спецификация          КАК Спецификация,
	|		Таблица.ДлительностьДоВыпуска КАК ДлительностьДоВыпуска,
	|		Таблица.Этап                  КАК Этап,
	|		Таблица.Партия                КАК Партия,
	|		Таблица.Подразделение         КАК Подразделение,
	|		Таблица.ДатаПоступления       КАК ДатаПоступления,
	|		Таблица.ЗаказНаПоступление    КАК ЗаказНаПоступление,
	|		Таблица.КПроизводству         КАК КПроизводству,
	|		Таблица.Заказано              КАК ЗаказаноИзменение,
	|		Таблица.КПроизводству         КАК КПроизводствуИзменение,
	|		Таблица.Запущено              КАК ЗапущеноИзменение,
	|		Таблица.Готово                КАК ГотовоИзменение
	|	ИЗ
	|		РегистрСведений.ЗаказыНаПроизводство КАК Таблица
	|	ГДЕ
	|		Таблица.Регистратор = &Регистратор) КАК ТаблицаИзменений
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИзменений.Номенклатура,
	|	ТаблицаИзменений.Характеристика,
	|	ТаблицаИзменений.Склад,
	|	ТаблицаИзменений.Назначение,
	|	ТаблицаИзменений.ЗаказНаПроизводство,
	|	ТаблицаИзменений.КлючНоменклатура,
	|	ТаблицаИзменений.Спецификация,
	|	ТаблицаИзменений.ДлительностьДоВыпуска,
	|	ТаблицаИзменений.Этап,
	|	ТаблицаИзменений.Партия,
	|	ТаблицаИзменений.Подразделение,
	|	ТаблицаИзменений.ДатаПоступления,
	|	ТаблицаИзменений.ЗаказНаПоступление
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ТаблицаИзменений.ЗаказаноИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.КПроизводствуИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ЗапущеноИзменение) <> 0
	|		ИЛИ СУММА(ТаблицаИзменений.ГотовоИзменение) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Склад,
	|	Назначение,
	|	ЗаказНаПроизводство
	|;
	|УНИЧТОЖИТЬ ДвиженияЗаказыНаПроизводствоПередЗаписью
	|";
	
	МассивРезультатовЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = МассивРезультатовЗапроса[0].Выбрать();
	
	Если Выборка.Следующий() И Выборка.Количество > 0 Тогда
		СтруктураЗаказаПроведениеДокументов.РассчитатьСостояниеПродукцииЗаказа(ДополнительныеСвойства.ВременныеТаблицыРасчетаСостоянииПродукцииЗаказа,
			"ДвиженияЗаказыНаПроизводствоИзменения"); 
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьБлокировкуЗаказов(МенеджерВременныхТаблиц)
	
	Заказы = Выгрузить(, "ЗаказНаПроизводство");
	Заказы.Свернуть("ЗаказНаПроизводство");
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказы", Заказы);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Заказы.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ПОМЕСТИТЬ Заказы
	|ИЗ
	|	&Заказы КАК Заказы
	|;
	|ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство КАК ЗаказНаПроизводство
	|ИЗ
	|	Заказы КАК Таблица
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	Таблица.ЗаказНаПроизводство
	|ИЗ
	|	ДвиженияЗаказыНаПроизводствоПередЗаписью КАК Таблица
	|;
	|";
	РезультатЗапроса = Запрос.Выполнить();
	
	БлокировкаДанных = Новый БлокировкаДанных();
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ЗаказыНаПроизводство");
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ЗаказНаПроизводство", "ЗаказНаПроизводство");
	БлокировкаДанных.Заблокировать();
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
