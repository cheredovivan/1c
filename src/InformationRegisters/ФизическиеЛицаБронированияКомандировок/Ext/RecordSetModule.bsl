#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	ДополнительныеСвойства.Вставить("ПрежнийНабор", РегистрыБЗК.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение));
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если Не ДополнительныеСвойства.Свойство("ПрежнийНабор") Тогда
		Возврат;
	КонецЕсли;
	
	ПрежнийНабор = ДополнительныеСвойства.ПрежнийНабор;
	Изменения = РегистрыБЗК.ИзменениеЗаписейНабора(ПрежнийНабор, ЭтотОбъект, Замещение, Истина);
	Для Каждого СтрокаТаблицы Из Изменения Цикл
		Если СтрокаТаблицы.ВидИзмененияСтроки = -1 Тогда
			РегистрыСведений.ИзмененияФизическихЛицБронированияКомандировок.УдалитьСотрудниковИзСпискаОтправки(
				СтрокаТаблицы.СистемаБронирования, СтрокаТаблицы.ФизическоеЛицо);
		Иначе
			РегистрыСведений.ИзмененияФизическихЛицБронированияКомандировок.ЗарегистрироватьСотрудниковВСпискеОтправки(
				СтрокаТаблицы.СистемаБронирования, СтрокаТаблицы.ФизическоеЛицо);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли