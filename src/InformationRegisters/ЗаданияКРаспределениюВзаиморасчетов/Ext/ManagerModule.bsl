#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ПоискЗаданий

// Определяет актуальный период рассчитанных данных по взаиморасчетам
// 
// Параметры:
//  МассивАналитик - Неопределено, Массив из СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Список аналитик учета по партнерам
//  МассивОбъектовРасчетов - Неопределено, Массив Из СправочникСсылка.ОбъектыРасчетов - Список объектов расчетов
//  МаксимальнаяДата - Неопределено, Дата - Максимальная дата
// 
// Возвращаемое значение:
//  Дата, Неопределено - Период актуальности взаиморасчетов
Функция АктуальныйПериодРассчитанныхДанныхПоВзаиморасчетам(МассивАналитик = Неопределено, МассивОбъектовРасчетов = Неопределено, МаксимальнаяДата = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ 
	|	МИНИМУМ(Регистр.ДатаЗаписи) ДатаПервогоЗадания
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК Регистр
	|ГДЕ
	|	(Регистр.АналитикаУчетаПоПартнерам В (&МассивАналитик)
	|		ИЛИ &ВсяАналитика)
	|	И (Регистр.ОбъектРасчетов В (&МассивОбъектовРасчетов)
	|		ИЛИ &ВсеОбъектыРасчетов)
	|	И (Регистр.ПериодЗадания <= &МаксимальнаяДата
	|		ИЛИ &БезОграниченияПоПериоду)
	|ИМЕЮЩИЕ
	|	НЕ МИНИМУМ(Регистр.ДатаЗаписи) ЕСТЬ NULL";
	Запрос.УстановитьПараметр("МассивАналитик", МассивАналитик);
	Запрос.УстановитьПараметр("ВсяАналитика", МассивАналитик = Неопределено);
	Запрос.УстановитьПараметр("МассивОбъектовРасчетов", МассивОбъектовРасчетов);
	Запрос.УстановитьПараметр("ВсеОбъектыРасчетов", МассивОбъектовРасчетов = Неопределено);
	Запрос.УстановитьПараметр("МаксимальнаяДата", МаксимальнаяДата);
	Запрос.УстановитьПараметр("БезОграниченияПоПериоду", МаксимальнаяДата = Неопределено);
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	Если Выборка.Следующий() Тогда
		Если ЗначениеЗаполнено(Выборка.ДатаПервогоЗадания) Тогда
			Возврат Выборка.ДатаПервогоЗадания - 1;
		Иначе
			Возврат Выборка.ДатаПервогоЗадания;
		КонецЕсли;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область ФормированиеЗаданий

// Создание заданий неоперативной очереди.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  МенеджерРегистра - РегистрНакопленияМенеджер, РегистрСведенийМенеджер - Менеджер записываемого регистра
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗадания(МенеджерРегистра, ВидыЗаданий, НаборЗаписей, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт

	Если ВидыЗаданий.Количество() = 0 
		Или (НаборЗаписей.ДополнительныеСвойства.Свойство("НеСоздаватьЗаданияКРаспределениюВзаиморасчетов")
		И НаборЗаписей.ДополнительныеСвойства.НеСоздаватьЗаданияКРаспределениюВзаиморасчетов) Тогда
		Возврат;
	КонецЕсли;

	ДанныеКФормированиюЗаданий = МенеджерРегистра.ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(
			НаборЗаписей, ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
		Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам), МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);

	Если ДанныеКФормированиюЗаданий.Количество() > 0 Тогда

		СоздатьЗаданияПоДаннымТаблицыЗначений(ДанныеКФормированиюЗаданий);

	КонецЕсли;

КонецПроцедуры

// Создать задания при обмене данными РИБ.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗаданияПриОбменеДанными(НаборЗаписей, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт

	МетаданныеРегистра = НаборЗаписей.Метаданные();
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистра.ПолноеИмя());

	ВидыЗаданий = Новый Массив;
	ВидыЗаданий.Добавить(Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам);

	НаборЗаписей.ДополнительныеСвойства.Вставить("НеСоздаватьЗаданияКРаспределениюВзаиморасчетов", Ложь);
	СоздатьЗадания(МенеджерРегистра, ВидыЗаданий, НаборЗаписей, МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);
		
КонецПроцедуры

// Создание заданий неоперативной очереди при записи набора записей регистра расчетов по срокам.
// Данная процедура является частным случаем регистрации заданий и вызывается после заполнения регистров взаиморасчетов в новой архитектуре.
// см. ОперативныеВзаиморасчетыСервер.ЗаполнитьОперативныеВзаиморасчеты.
// 
// Параметры:
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Данные временных таблиц будут использованы при формировании новых заданий.
//  ЭтоРасчетыСКлиентами - Булево - Расчеты с клиентами
//
Процедура СоздатьЗаданияНеоперативнойОчередиПриИзмененииРасчетовПоСрокам(МенеджерВременныхТаблиц,
	ЭтоРасчетыСКлиентами = Истина) Экспорт

	Если ЭтоРасчетыСКлиентами Тогда
		МетаданныеРегистра = Метаданные.РегистрыНакопления.РасчетыСКлиентамиПоСрокам;
		ИмяТаблицы = "ТаблицаИзмененийРасчетыСКлиентамиПоСрокам";
	Иначе
		МетаданныеРегистра = Метаданные.РегистрыНакопления.РасчетыСПоставщикамиПоСрокам;
		ИмяТаблицы = "ТаблицаИзмененийРасчетыСПоставщикамиПоСрокам";
	КонецЕсли;

	Если МенеджерВременныхТаблиц.Таблицы.Найти(ИмяТаблицы) = Неопределено Тогда
		Возврат;
	КонецЕсли;

	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистра.ПолноеИмя());

	ВидыЗаданийКРегистрацииВОчереди = НеоперативнаяОчередьЗаданий.ВидыЗаданийКРегистрацииВОчереди(МетаданныеРегистра);
	Для Каждого ТекущиеДанные Из ВидыЗаданийКРегистрацииВОчереди Цикл

		МетаданныеРегистраЗаданий = ОбщегоНазначения.ОбъектМетаданныхПоИдентификатору(ТекущиеДанные.РегистрОчереди);
		МенеджерРегистраЗаданий = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистраЗаданий.ПолноеИмя());

		МенеджерРегистраЗаданий.СоздатьЗаданияПоДаннымВременнойТаблицы(МенеджерРегистра, ТекущиеДанные.ВидыЗаданий,
			МенеджерВременныхТаблиц, ИмяТаблицы);

	КонецЦикла;

КонецПроцедуры

// Создание заданий по данным переданной таблицы значений.
// 
// Параметры:
//  ТаблицаФормированияЗаданий - см. ТаблицаФормированияЗаданий
Процедура СоздатьЗаданияПоДаннымТаблицыЗначений(ТаблицаФормированияЗаданий) Экспорт

	Задания = СоздатьНаборЗаписей();

	Для Каждого ТекущиеДанные Из ТаблицаФормированияЗаданий Цикл
		
		Если Не ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
			ТекстИсключения = НСтр(
				"ru = 'У регистрируемого задания к распределению взаиморасчетов поле ""Документ"" не заполнено.';
				|en = 'У регистрируемого задания к распределению взаиморасчетов поле ""Документ"" не заполнено.'");
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;
		
		НовоеЗадание = Задания.Добавить();
		ЗаполнитьЗначенияСвойств(НовоеЗадание, ТекущиеДанные);
		НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;

	КонецЦикла;
	УстановитьПривилегированныйРежим(Истина);
	Задания.Записать(Ложь);
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

// Таблица данных для формирования заданий к распределению взаиморасчетов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица данных для формирования заданий к распределению взаиморасчетов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано задание
// * ПериодЗадания - Дата - Период задания
// * Документ - ДокументСсылка - Ссылка на документ, по которому формируется задание
// * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета по партнерам
// * ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Объект расчетов
// * Валюта - СправочникСсылка.Валюты - Валюта расчетов
// * ДатаЗаписи - Дата - Дата записи задания
// * ПериодКонтроляИзменений - Дата - Период, с которого необходимо проверять изменения
//
Функция ТаблицаФормированияЗаданий() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ПериодЗадания", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	Таблица.Колонки.Добавить("АналитикаУчетаПоПартнерам",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	Таблица.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	Таблица.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	Таблица.Колонки.Добавить("ДатаЗаписи", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("ПериодКонтроляИзменений", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));

	Возврат Таблица;

КонецФункции

#КонецОбласти

#Область ОбработкаДанных

// Получает данные гранул расчета для многопоточной фоновой обработки.
// 
// Параметры:
//  СтруктураФормированияПотока - см. НеоперативнаяОчередьЗаданий.СтруктураФормированияПорцийДанных
//  КоличествоСвободныхПотоков - Число - Количество свободных потоков
// 
// Возвращаемое значение:
//  Структура - Данные для многопоточной фоновой обработки:
// * КоличествоПорций - Число - Число порций к обработке
// * ГранулыРасчетаКОбработке - см. ТаблицаГранулРасчета
//
Функция ДанныеДляМногопоточнойФоновойОбработки(СтруктураФормированияПотока, КоличествоСвободныхПотоков) Экспорт
	
	ПорцииДанныхКРаспределениюСУчетомПриемников = ТаблицаГранулРасчета();
	ПорцииДанныхКРаспределениюСУчетомПриемников.Колонки.Добавить("Порция", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ПорцииДанныхКРаспределениюСУчетомПриемников.Индексы.Добавить(
		"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник");
	ПорцииДанныхКРаспределениюСУчетомПриемников.Индексы.Добавить("ГруппаОбработки");
	ПорцииДанныхКРаспределениюСУчетомПриемников.Индексы.Добавить("Порция");
	
	Если СтруктураФормированияПотока.ФоноваяОбработкаЗаданий Тогда
		
		ОтложенныеЗадания.Запустить(Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.Имя);
		
		СтруктураДанных = НеоперативнаяОчередьЗаданий.СтруктураДанныхОбработкиЗаданий();
		СтруктураДанных.ГранулыРасчетаКОбработке = ПорцииДанныхКРаспределениюСУчетомПриемников;
		
		Возврат СтруктураДанных;
		
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДокументыКОбработке.Документ КАК Документ
	|ПОМЕСТИТЬ ВтДокументыКОбработке
	|ИЗ
	|	&ТаблицаДокументов КАК ДокументыКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания) КАК ПериодЗадания,
	|	ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗаданияКРаспределениюВзаиморасчетов.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВтКортежиКОбработке
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДокументыКОбработке КАК ДокументыКОбработке
	|		ПО ЗаданияКРаспределениюВзаиморасчетов.Документ = ДокументыКОбработке.Документ
	|ГДЕ
	|	ЗаданияКРаспределениюВзаиморасчетов.ВыполненоСОшибкой = ЛОЖЬ
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов,
	|	ЗаданияКРаспределениюВзаиморасчетов.Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта КАК ВалютаРасчетов
	|ИЗ
	|	ВтКортежиКОбработке КАК ВтКортежиКОбработке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта КАК ВалютаРасчетов,
	|	ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) КАК ТипРасчетов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов,
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасчетыСКлиентами,
	|	ЕСТЬNULL(ОбъектыРасчетов.Объект, НЕОПРЕДЕЛЕНО) КАК Объект,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка) КАК ОбъектРасчетовПриемник,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаПриемник,
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ КАК Документ,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания) КАК ДатаПересчета,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания) КАК ДатаНачалаПересчета,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений,
	|	&ВидЗадания КАК ВидЗадания,
	|	&НомерОчереди КАК НомерОчереди
	|ИЗ
	|	ВтКортежиКОбработке КАК ВтКортежиКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|		ПО ВтКортежиКОбработке.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам
	|		И ВтКортежиКОбработке.ОбъектРасчетов = ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов
	|		И ВтКортежиКОбработке.Валюта = ЗаданияКРаспределениюВзаиморасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ВтКортежиКОбработке.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
	|ГДЕ
	|	ЗаданияКРаспределениюВзаиморасчетов.ВыполненоСОшибкой = ЛОЖЬ
	|СГРУППИРОВАТЬ ПО
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта,
	|	ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов,
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЕСТЬNULL(ОбъектыРасчетов.Объект, НЕОПРЕДЕЛЕНО),
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка),
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", СтруктураФормированияПотока.ТаблицаДокументов);
	Запрос.УстановитьПараметр("ВидЗадания", СтруктураФормированияПотока.ВидЗадания);
	Запрос.УстановитьПараметр("НомерОчереди", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
		СтруктураФормированияПотока.ВидЗадания, "НомерОчереди"));

	МаксимальноеКоличествоКортежей = КоличествоСвободныхПотоков * СтруктураФормированияПотока.МаксимальнаяПорция;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1000", Формат(МаксимальноеКоличествоКортежей, "ЧН=0; ЧГ=0"));
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоКортежам = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	КортежиКОбработке = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница() - 1].Выгрузить();
	ИсходныеДанныеКРаспределениюСУчетомПриемников = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница()].Выгрузить();
	ИсходныеДанныеКРаспределениюСУчетомПриемников.Индексы.Добавить(
		"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");

	ДанныеДляПолученияПриемников = ТаблицаГранулРасчета();
	ДанныеДляПолученияПриемников.Колонки.Добавить("Порция", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ДанныеДляПолученияПриемников.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");

	ИспользуемыеКортежиКОбработке = КортежиКОбработке.СкопироватьКолонки();
	ИспользуемыеКортежиКОбработке.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
	
	КоличествоКортежейКПолучениюПриемников = 0;
	РазмерПорцииКортежейКПолучениюПриемников = Мин(10, МаксимальноеКоличествоКортежей);
	СтруктураОтбораКортежей = Новый Структура;
	
	Для Каждого ТекущийКортеж Из КортежиКОбработке Цикл
		
		КоличествоКортежейКПолучениюПриемников = КоличествоКортежейКПолучениюПриемников + 1;
		
		СтруктураОтбораКортежей.Вставить("АналитикаУчетаПоПартнерам", ТекущийКортеж.АналитикаУчетаПоПартнерам);
		СтруктураОтбораКортежей.Вставить("ОбъектРасчетов", ТекущийКортеж.ОбъектРасчетов);
		СтруктураОтбораКортежей.Вставить("ВалютаРасчетов", ТекущийКортеж.ВалютаРасчетов);
		
		НайденныеКортежи = ИсходныеДанныеКРаспределениюСУчетомПриемников.НайтиСтроки(СтруктураОтбораКортежей);
		Для Каждого НайденныйКортеж Из НайденныеКортежи Цикл
			НоваяСтрока = ДанныеДляПолученияПриемников.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, НайденныйКортеж);
			НоваяСтрока.ПорядокОперации = ОперативныеВзаиморасчетыСервер.Порядок(
				НоваяСтрока.ДатаНачалаПересчета, "", Тип("ДокументСсылка.КорректировкаРегистров"), "1", "21");
		КонецЦикла;
		
		НайденныеКортежиВПорции = ИспользуемыеКортежиКОбработке.НайтиСтроки(СтруктураОтбораКортежей);
		Если НайденныеКортежиВПорции.Количество() = 0 Тогда
			ЗаполнитьЗначенияСвойств(ИспользуемыеКортежиКОбработке.Добавить(), СтруктураОтбораКортежей);
		КонецЕсли;

		Если КоличествоКортежейКПолучениюПриемников = РазмерПорцииКортежейКПолучениюПриемников Тогда
			
			ОбработатьТаблицуИзменений(ДанныеДляПолученияПриемников, ПорцииДанныхКРаспределениюСУчетомПриемников,
				ИспользуемыеКортежиКОбработке);
			
			ДанныеДляПолученияПриемников.Очистить();
			КоличествоКортежейКПолучениюПриемников = 0;

		КонецЕсли;
		
		Если ИспользуемыеКортежиКОбработке.Количество() >= МаксимальноеКоличествоКортежей Тогда
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Если ДанныеДляПолученияПриемников.Количество() > 0 Тогда
		ОбработатьТаблицуИзменений(ДанныеДляПолученияПриемников, ПорцииДанныхКРаспределениюСУчетомПриемников,
				ИспользуемыеКортежиКОбработке);
	КонецЕсли;
	
	// Заполнение групп обработки у таблицы кортежей
	ИспользуемыеКортежиКОбработке.Очистить();
	
	НомерГруппыОбработки = 0;
	
	ТаблицаГруппыОбработкиДанных = ПорцииДанныхКРаспределениюСУчетомПриемников.Скопировать( ,
		"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник, ГруппаОбработки");
	ТаблицаГруппыОбработкиДанных.Свернуть(
		"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник, ГруппаОбработки");
	
	ТаблицаГруппыОбработкиДанных.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
	ТаблицаГруппыОбработкиДанных.Индексы.Добавить("АналитикаУчетаПоПартнерамПриемник, ОбъектРасчетовПриемник, ВалютаПриемник");
	
	Для каждого Кортеж Из КортежиКОбработке Цикл
		
		СтруктураОтбораКортежей.Вставить("АналитикаУчетаПоПартнерам", Кортеж.АналитикаУчетаПоПартнерам);
		СтруктураОтбораКортежей.Вставить("ОбъектРасчетов", Кортеж.ОбъектРасчетов);
		СтруктураОтбораКортежей.Вставить("ВалютаРасчетов", Кортеж.ВалютаРасчетов);
		
		НомерГруппыОбработки = НомерГруппыОбработки + 1;
		
		ЗаполнитьГруппуОбработкиКортежей(ТаблицаГруппыОбработкиДанных, СтруктураОтбораКортежей, НомерГруппыОбработки);
		
	КонецЦикла;
	
	СтруктураОтбораКортежейСПриемниками = Новый Структура;
	Для каждого СтрокаТаблицы Из ТаблицаГруппыОбработкиДанных Цикл
		
		СтруктураОтбораКортежейСПриемниками.Вставить("АналитикаУчетаПоПартнерам", СтрокаТаблицы.АналитикаУчетаПоПартнерам);
		СтруктураОтбораКортежейСПриемниками.Вставить("ОбъектРасчетов", СтрокаТаблицы.ОбъектРасчетов);
		СтруктураОтбораКортежейСПриемниками.Вставить("ВалютаРасчетов", СтрокаТаблицы.ВалютаРасчетов);
		СтруктураОтбораКортежейСПриемниками.Вставить("АналитикаУчетаПоПартнерамПриемник", СтрокаТаблицы.АналитикаУчетаПоПартнерамПриемник);
		СтруктураОтбораКортежейСПриемниками.Вставить("ОбъектРасчетовПриемник", СтрокаТаблицы.ОбъектРасчетовПриемник);
		СтруктураОтбораКортежейСПриемниками.Вставить("ВалютаПриемник", СтрокаТаблицы.ВалютаПриемник);
		
		НайденныеСтроки = ПорцииДанныхКРаспределениюСУчетомПриемников.НайтиСтроки(СтруктураОтбораКортежейСПриемниками);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			ТекущаяСтрока.ГруппаОбработки = СтрокаТаблицы.ГруппаОбработки;
		КонецЦикла;
		
	КонецЦикла;
	
	НомерПорции = 1;
	
	СтруктураОтбораПоГруппе = Новый Структура;
	Для ИндексГруппы = 1 По НомерГруппыОбработки Цикл
		
		СтруктураОтбораПоГруппе.Вставить("ГруппаОбработки", ИндексГруппы);
		
		НайденныеСтроки = ПорцииДанныхКРаспределениюСУчетомПриемников.НайтиСтроки(СтруктураОтбораПоГруппе);
		
		Для Каждого ТекущаяСтрока Из НайденныеСтроки Цикл
			
			ТекущаяСтрока.Порция = НомерПорции;

			СтруктураОтбораКортежей.Вставить("АналитикаУчетаПоПартнерам", ТекущаяСтрока.АналитикаУчетаПоПартнерам);
			СтруктураОтбораКортежей.Вставить("ОбъектРасчетов", ТекущаяСтрока.ОбъектРасчетов);
			СтруктураОтбораКортежей.Вставить("ВалютаРасчетов", ТекущаяСтрока.ВалютаРасчетов);
			
			НайденныеКортежиВПорции = ИспользуемыеКортежиКОбработке.НайтиСтроки(СтруктураОтбораКортежей);
			Если НайденныеКортежиВПорции.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(ИспользуемыеКортежиКОбработке.Добавить(), СтруктураОтбораКортежей);
			КонецЕсли;

		КонецЦикла;
		
		Если НайденныеКортежиВПорции.Количество() >= СтруктураФормированияПотока.МаксимальнаяПорция Тогда
			
			Если НомерПорции < КоличествоСвободныхПотоков Тогда
				
				НайденныеКортежиВПорции.Очистить();
				НомерПорции = НомерПорции + 1;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДанных = НеоперативнаяОчередьЗаданий.СтруктураДанныхОбработкиЗаданий();
	СтруктураДанных.КоличествоПорций = НомерПорции;
	СтруктураДанных.ГранулыРасчетаКОбработке = ПорцииДанныхКРаспределениюСУчетомПриемников;

	Возврат СтруктураДанных;
	
КонецФункции

// Выполнить обработку порции гранул расчета.
// 
// Параметры:
//  ПараметрыДанных - см. НеоперативнаяОчередьЗаданий.ПараметрыОбработкиПорцииДанных
//
Процедура ВыполнитьОбработкуПорции(ПараметрыДанных) Экспорт
	
	Если ПараметрыДанных.ПорцияДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросДанныеПоКортежам = Новый Запрос;
	ЗапросДанныеПоКортежам.Текст = "
	|ВЫБРАТЬ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВтКортежиКОбработке.ДатаФакт КАК ДатаПересчета
	|ПОМЕСТИТЬ ВтКортежиКОбработке
	|ИЗ
	|	&ТаблицаКортежиКОбработке КАК ВтКортежиКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбъектРасчетов,
	|	АналитикаУчетаПоПартнерам,
	|	ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.ВалютаРасчетов КАК ВалютаРасчетов,
	|	МИНИМУМ(ВтКортежиКОбработке.ДатаПересчета) КАК ДатаПересчета
	|ПОМЕСТИТЬ ВтКортежиСДатойПересчета
	|ИЗ
	|	ВтКортежиКОбработке КАК ВтКортежиКОбработке
	|СГРУППИРОВАТЬ ПО
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов,
	|	ВтКортежиКОбработке.ВалютаРасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКортежиСДатойПересчета.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиСДатойПересчета.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиСДатойПересчета.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ЗаданияКРаспределениюВзаиморасчетов.ИдентификаторЗаписи КАК ИдентификаторЗадания,
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ КАК Документ,
	|	ЗаданияКРаспределениюВзаиморасчетов.ВыполненоСОшибкой КАК ВыполненоСОшибкой
	|ИЗ
	|	ВтКортежиСДатойПересчета КАК ВтКортежиСДатойПересчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|		ПО ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам = ВтКортежиСДатойПересчета.АналитикаУчетаПоПартнерам
	|		И ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов = ВтКортежиСДатойПересчета.ОбъектРасчетов
	|		И ЗаданияКРаспределениюВзаиморасчетов.Валюта = ВтКортежиСДатойПересчета.ВалютаРасчетов
	|		И ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания >= ВтКортежиСДатойПересчета.ДатаПересчета";
	
	ЗапросДанныеПоКортежам.УстановитьПараметр("ТаблицаКортежиКОбработке", ПараметрыДанных.ПорцияДанных);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоКортежам = ЗапросДанныеПоКортежам.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ИдентификаторыЗаданийКортежей = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница()].Выгрузить();
	ИдентификаторыЗаданийКортежей.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
	ГруппыОбработки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ПараметрыДанных.ПорцияДанных.Скопировать( ,
		"ГруппаОбработки").ВыгрузитьКолонку("ГруппаОбработки"));
		
	ПараметрыДанных.ПорцияДанных.Колонки.Добавить("ОписаниеОшибки", Новый ОписаниеТипов("Строка"));
	
	МетаданныеРегистраОчереди = Неопределено;
	
	СтруктураОтбораЗаданий = Новый Структура;
	СтруктураОтбораДанных = Новый Структура;
	ПараметрыОбработкиДанных = Новый Структура;
	ОтборЗаданийБезОшибок = Новый Структура("ВыполненоСОшибкой", Ложь);
	
	УспешноОбработанныеДанные = Новый Массив;

	НомерПопытки = 1;
	КоличествоПопыток = ?(ПараметрыДанных.ВыполнениеЗаданийВТранзакцииПроведения, 1, 3);
	Пока НомерПопытки <= КоличествоПопыток Цикл

		Если ПараметрыДанных.ПорцияДанных.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого ГруппаОбработки Из ГруппыОбработки Цикл
			
			ОбработкаГруппыКортежейВыполненаУспешно = Истина;
			
			СтруктураОтбораДанных.Вставить("ГруппаОбработки", ГруппаОбработки);

			ДанныеКОбработке = ПараметрыДанных.ПорцияДанных.Скопировать(СтруктураОтбораДанных);
			
			НачатьТранзакцию();

			Попытка
				
				ОбрабатываемыеКортежи = ДанныеКОбработке.Скопировать( ,
					"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
				ОбрабатываемыеКортежи.Свернуть("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
				ТаблицаИдентификаторовКортежей = ИдентификаторыЗаданийКортежей.СкопироватьКолонки();
				ТаблицаИдентификаторовКортежей.Индексы.Добавить("ВыполненоСОшибкой");
				
				Для Каждого Кортеж Из ОбрабатываемыеКортежи Цикл

					СтруктураОтбораЗаданий.Вставить("АналитикаУчетаПоПартнерам", Кортеж.АналитикаУчетаПоПартнерам);
					СтруктураОтбораЗаданий.Вставить("ОбъектРасчетов", Кортеж.ОбъектРасчетов);
					СтруктураОтбораЗаданий.Вставить("ВалютаРасчетов", Кортеж.ВалютаРасчетов);
					ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ИдентификаторыЗаданийКортежей.Скопировать(
						СтруктураОтбораЗаданий), ТаблицаИдентификаторовКортежей);

				КонецЦикла;
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных = ТаблицаИдентификаторовКортежей.Скопировать(ОтборЗаданийБезОшибок);
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторЗаписи", "ИдентификаторЗадания");
				Блокировка.Заблокировать();

				Для Каждого КортежКОбработке Из ДанныеКОбработке Цикл
					Если КортежКОбработке.ПорядокОперации = "" Тогда
						КортежКОбработке.ПорядокОперации = ОперативныеВзаиморасчетыСервер.Порядок(
							КортежКОбработке.ДатаНачалаПересчета, "", Тип("ДокументСсылка.КорректировкаРегистров"), "1", "21");
					КонецЕсли;
				КонецЦикла;
				
				ПараметрыОбработкиДанных.Вставить("ДанныеКОбработке", ДанныеКОбработке);
				ПараметрыОбработкиДанных.Вставить("ДополнитьТаблицуИзмененийПриемниками", Ложь);
				УстановитьПривилегированныйРежим(Истина);
				ОперативныеВзаиморасчетыСервер.ВыполнитьОтложенноеРаспределение(ПараметрыОбработкиДанных);
				УстановитьПривилегированныйРежим(Ложь);
				
				СписокИдентификаторовЗаданийКУдалению = ТаблицаИдентификаторовКортежей.ВыгрузитьКолонку(
					"ИдентификаторЗадания");
				СписокИдентификаторовЗаданийКУдалению = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
					СписокИдентификаторовЗаданийКУдалению);
			
				УстановитьПривилегированныйРежим(Истина);
				НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ПараметрыДанных.ИмяРегистраОчереди,
						СписокИдентификаторовЗаданийКУдалению, ПараметрыДанных.ОчередиЗаданий);
				УстановитьПривилегированныйРежим(Ложь);

				ЗафиксироватьТранзакцию();

			Исключение

				ОтменитьТранзакцию();
				
				Если МетаданныеРегистраОчереди = Неопределено Тогда
					МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов;
				КонецЕсли;
				
				ОбработкаГруппыКортежейВыполненаУспешно = Ложь;
				
				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				
				КортежиГруппыОбработки = ПараметрыДанных.ПорцияДанных.НайтиСтроки(СтруктураОтбораДанных);
				
				Для каждого Кортеж Из КортежиГруппыОбработки Цикл
					Кортеж.ОписаниеОшибки = ТекстОшибки;
				КонецЦикла;
				
				ДокументыГруппыОбработки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
					ДанныеКОбработке.ВыгрузитьКолонку("Документ"));
				
				Для Каждого ТекущийДокумент Из ДокументыГруппыОбработки Цикл
					НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди,
						Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ТекстОшибки,
						ТекущийДокумент);
				КонецЦикла;
				
			КонецПопытки;
			
			Если ОбработкаГруппыКортежейВыполненаУспешно Тогда
				
				ДокументыГруппыОбработки = Новый Массив;
				КортежиКУдалению = ПараметрыДанных.ПорцияДанных.НайтиСтроки(СтруктураОтбораДанных);
				Для Каждого КортежКУдалению Из КортежиКУдалению Цикл
					Если ДокументыГруппыОбработки.Найти(КортежКУдалению.Документ) = Неопределено Тогда
						ДокументыГруппыОбработки.Добавить(КортежКУдалению.Документ);
					КонецЕсли;
					УспешноОбработанныеДанные.Добавить(КортежКУдалению);
				КонецЦикла;
				
				УстановитьПривилегированныйРежим(Истина);
				НеоперативнаяОчередьЗаданий.СнятьПризнакВыполненоСОшибкойПоДокументам(ДокументыГруппыОбработки,
					Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ПараметрыДанных.ОчередиЗаданий);
				УстановитьПривилегированныйРежим(Ложь);
					
			КонецЕсли;

		КонецЦикла;

		Если УспешноОбработанныеДанные.Количество() > 0 Тогда

			Для Каждого ДанныеКУдалению Из УспешноОбработанныеДанные Цикл
				ПараметрыДанных.ПорцияДанных.Удалить(ДанныеКУдалению);
			КонецЦикла;
			
			УспешноОбработанныеДанные.Очистить();

		КонецЕсли;

		НомерПопытки = НомерПопытки + 1;

	КонецЦикла;
	
	Если Не ПараметрыДанных.ВыполнениеЗаданийВТранзакцииПроведения Тогда
		ЗарегистрироватьОшибкиОбработкиДанных(ПараметрыДанных, ИдентификаторыЗаданийКортежей, МетаданныеРегистраОчереди);
	Иначе
		СообщитьОбОшибкахОбработкиДанных(ПараметрыДанных);
	КонецЕсли;
	
КонецПроцедуры

#Область ОтложенныеЗадания

// Возвращает текст запроса выборки заданий.
//
// Параметры:
//  Запрос            - см. ОтложенныеЗадания.ТекстЗапросаВыборкиЗаданийПоУмолчанию.Запрос
//  ПараметрыОчереди  - см. ОтложенныеЗадания.ТекстЗапросаВыборкиЗаданийПоУмолчанию.ПараметрыОчереди
//  КоличествоЗаданий - см. ОтложенныеЗадания.ТекстЗапросаВыборкиЗаданийПоУмолчанию.Запрос
//
// Возвращаемое значение:
//  Строка - текст запроса выборки заданий
//
Функция ТекстЗапросаВыборкиЗаданий(Запрос, ПараметрыОчереди, КоличествоЗаданий) Экспорт
	
	ТекстЗапроса = ОтложенныеЗадания.ТекстЗапросаВыборкиЗаданийПоУмолчанию(
					Запрос,
					ПараметрыОчереди,
					КоличествоЗаданий,
					"Таблица.ВыполненоСОшибкой = ЛОЖЬ");
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Выполняет распределение расчетов по текущим заданиям к распределению в регистре ЗаданияКРаспределениюВзаиморасчетов.
// Распределение взаиморасчетов выполняется управляющим потоком оперативной очереди заданий.
//
// Параметры:
//  Задания - ТаблицаЗначений - колонки соответствуют измерениям, ресурсам и реквизитам регистра сведений
//                              "ЗаданияКРаспределениюВзаиморасчетов", есть и служебные колонки:
//             * Организация               - СправочникСсылка.Организации -
//             * ПериодЗадания             - Дата -
//             * ИдентификаторЗаписи       - УникальныйИдентификатор  -
//             * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам   -
//             * ОбъектРасчетов            - СправочникСсылка.ОбъектыРасчетов - 
//             * Валюта                    - СправочникСсылка.Валюты - 
//             * Документ                  - ДокументСсылка - 
//             * ДатаЗаписи                - Дата -
//             * ИдентификаторЗадания   - УникальныйИдентификатор - служебная
//             * ДатаЗадания            - Дата                    - служебная
//  ИдентификаторыНеОбработанныхЗаписей - Соответствие из УникальныйИдентификатор - ключ, это идентификатор 
//                                                                                   не выполненных (выдающих ошибку при
//                                                                                   выполнении) записей регистра
//                                                                                   сведений очереди заданий
//                                                                                   (измерение ИдентификаторЗаписи
//                                                                                   регистра сведений очереди заданий),
//                                                                                  значение, это представление ошибки.
//  ДополнительныеСвойства - Неопределено, Структура - дополнительные свойства выполнения заданий.
//
Процедура ВыполнитьОтложенноеРаспределениеВзаиморасчетов(
			Задания,
			ИдентификаторыНеОбработанныхЗаписей,
			ДополнительныеСвойства = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаЗаданий.Организация КАК Организация,
	|	ТаблицаЗаданий.ПериодЗадания КАК ПериодЗадания,
	|	ТаблицаЗаданий.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаЗаданий.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаЗаданий.Валюта КАК Валюта,
	|	ТаблицаЗаданий.Документ КАК Документ,
	|	ТаблицаЗаданий.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ПОМЕСТИТЬ ВтКортежиКОбработке
	|ИЗ
	|	&ТаблицаЗаданий КАК ТаблицаЗаданий
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта КАК ВалютаРасчетов
	|ИЗ
	|	ВтКортежиКОбработке КАК ВтКортежиКОбработке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта КАК ВалютаРасчетов,
	|	ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) КАК ТипРасчетов,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов,
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоРасчетыСКлиентами,
	|	ЕСТЬNULL(ОбъектыРасчетов.Объект, НЕОПРЕДЕЛЕНО) КАК Объект,
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка) КАК АналитикаУчетаПоПартнерамПриемник,
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка) КАК ОбъектРасчетовПриемник,
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка) КАК ВалютаПриемник,
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ КАК Документ,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания) КАК ДатаПересчета,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания) КАК ДатаНачалаПересчета,
	|	МИНИМУМ(ЗаданияКРаспределениюВзаиморасчетов.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений,
	|	1 КАК Приоритет,
	|	ЛОЖЬ КАК ПоДаннымОбъектаРасчетовИсточника
	|ИЗ
	|	ВтКортежиКОбработке КАК ВтКортежиКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|		ПО ВтКортежиКОбработке.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам
	|		И ВтКортежиКОбработке.ОбъектРасчетов = ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов
	|		И ВтКортежиКОбработке.Валюта = ЗаданияКРаспределениюВзаиморасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ОбъектыРасчетов КАК ОбъектыРасчетов
	|		ПО ВтКортежиКОбработке.ОбъектРасчетов = ОбъектыРасчетов.Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВтКортежиКОбработке.АналитикаУчетаПоПартнерам,
	|	ВтКортежиКОбработке.ОбъектРасчетов,
	|	ВтКортежиКОбработке.Валюта,
	|	ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов, ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)),
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ОбъектыРасчетов.ТипРасчетов,
	|			ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ЕСТЬNULL(ОбъектыРасчетов.Объект, НЕОПРЕДЕЛЕНО),
	|	ЗНАЧЕНИЕ(Справочник.КлючиАналитикиУчетаПоПартнерам.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.ОбъектыРасчетов.ПустаяСсылка),
	|	ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка),
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КортежиКОбработке.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	КортежиКОбработке.ОбъектРасчетов КАК ОбъектРасчетов,
	|	КортежиКОбработке.Валюта КАК ВалютаРасчетов,
	|	КортежиКОбработке.ИдентификаторЗаписи КАК ИдентификаторЗаписи
	|ИЗ
	|	ВтКортежиКОбработке КАК КортежиКОбработке";
	
	Запрос.УстановитьПараметр("ТаблицаЗаданий", Задания);
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеПоКортежам = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	КортежиКОбработке = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница() - 2].Выгрузить();
	ИсходныеДанныеКРаспределению = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница() - 1].Выгрузить(); // ТаблицаЗначений
	ИсходныеДанныеКРаспределению.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
	
	ИдентификаторыКортежей = ДанныеПоКортежам[ДанныеПоКортежам.ВГраница()].Выгрузить(); // ТаблицаЗначений
	ИдентификаторыКортежей.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");
	
	ПараметрыОбработкиДанных = Новый Структура;
	СтруктураОтбораКортежей = Новый Структура;
	
	МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов;
	ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();
	ДанныеОшибки = Новый Структура;
	
	Для Каждого ТекущийКортеж Из КортежиКОбработке Цикл
		
		УспешноОбработанныеДокументы = Новый Массив;
		СтруктураОтбораКортежей.Вставить("АналитикаУчетаПоПартнерам", ТекущийКортеж.АналитикаУчетаПоПартнерам);
		СтруктураОтбораКортежей.Вставить("ОбъектРасчетов", ТекущийКортеж.ОбъектРасчетов);
		СтруктураОтбораКортежей.Вставить("ВалютаРасчетов", ТекущийКортеж.ВалютаРасчетов);
		
		ДанныеКТекущемуРаспределению = ИсходныеДанныеКРаспределению.Скопировать(СтруктураОтбораКортежей, );

		НачатьТранзакцию();

		Попытка
			
			ТаблицаИдентификаторовКортежей = ИдентификаторыКортежей.Скопировать(СтруктураОтбораКортежей);

			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			ЭлементБлокировки.ИсточникДанных = ТаблицаИдентификаторовКортежей;
			ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторЗаписи", "ИдентификаторЗаписи");
			Блокировка.Заблокировать();

			ПараметрыОбработкиДанных.Вставить("ДанныеКОбработке", ДанныеКТекущемуРаспределению);
			ПараметрыОбработкиДанных.Вставить("ДополнитьТаблицуИзмененийПриемниками", Истина);
			УстановитьПривилегированныйРежим(Истина);
			ОперативныеВзаиморасчетыСервер.ВыполнитьОтложенноеРаспределение(ПараметрыОбработкиДанных);
			УстановитьПривилегированныйРежим(Ложь);
					
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(УспешноОбработанныеДокументы,
				ДанныеКТекущемуРаспределению.ВыгрузитьКолонку("Документ"), Истина);
					
			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();

			Задания.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");
			
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

			ДокументыГруппыОбработки = ОбщегоНазначенияКлиентСервер.СвернутьМассив(
					ДанныеКТекущемуРаспределению.ВыгрузитьКолонку("Документ"));

			Для Каждого ТекущийДокумент Из ДокументыГруппыОбработки Цикл
				НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди,
					Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ТекстОшибки,
					ТекущийДокумент);
			КонецЦикла;

			ДанныеОшибки.Вставить("АналитикаУчетаПоПартнерам", ТекущийКортеж.АналитикаУчетаПоПартнерам);
			ДанныеОшибки.Вставить("ОбъектРасчетов", ТекущийКортеж.ОбъектРасчетов);
			ДанныеОшибки.Вставить("Валюта", ТекущийКортеж.ВалютаРасчетов);
			НайденныеСтроки = Задания.НайтиСтроки(ДанныеОшибки);
			Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
				ИдентификаторыНеОбработанныхЗаписей.Вставить(НайденнаяСтрока.ИдентификаторЗаписи, ТекстОшибки);
			КонецЦикла;

		КонецПопытки;
		
		УстановитьПривилегированныйРежим(Истина);
		Если УспешноОбработанныеДокументы.Количество() > 0 Тогда
			НеоперативнаяОчередьЗаданий.СнятьПризнакВыполненоСОшибкойПоДокументам(УспешноОбработанныеДокументы,
				Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ОчередиЗаданий);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);

	КонецЦикла;
	
КонецПроцедуры

// Выполняет предварительную обработку ошибок до основной обработки в методе см. ОтложенныеЗадания.ВыполнитьЗадания.
//
// Параметры:
//  ПараметрыОчереди                    - СтрокаТаблицыЗначений из см. ОтложенныеЗадания.ОчередиЗаданий
//  Данные                              - ТаблицаЗначений - данные для выполнения заданий,
//  ИдентификаторыНеОбработанныхЗаписей - Соответствие из УникальныйИдентификатор - ключ, это идентификатор не
//                                                                                   выполненных (выдающих ошибку при
//                                                                                   выполнении) записей регистра
//                                                                                   сведений очереди заданий
//                                                                                   (измерение ИдентификаторЗаписи
//                                                                                   регистра сведений очереди заданий),
//                                                                                  значение, это представление ошибки.
//  ТекстИсключения                     - Строка - текст ошибки исключения,
//  ДополнительныеСвойства              - см. ДополнительныеСвойства
//
Процедура ПередОбработкойОшибок(
			ПараметрыОчереди,
			Данные,
			ИдентификаторыНеОбработанныхЗаписей,
			ТекстИсключения,
			ДополнительныеСвойства) Экспорт
	
	Если ИдентификаторыНеОбработанныхЗаписей.Количество() > 0 Тогда

		МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов;
		ИдентификаторыЗаданийКортежей = Новый Массив;
		ОчередиЗаданий = НеоперативнаяОчередьЗаданийПовтИсп.ОчередиЗаданий();

		Для Каждого Идентификатор Из ИдентификаторыНеОбработанныхЗаписей Цикл
			ИдентификаторыЗаданийКортежей.Добавить(Идентификатор.Ключ);
		КонецЦикла;

		ИмяРегистраОчереди = МетаданныеРегистраОчереди.Имя;
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Задания.Документ КАК Документ,
		|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Задания.Валюта КАК Валюта,
		|	Задания.Организация КАК Организация,
		|	Задания.ПериодЗадания КАК ПериодЗадания,
		|	Задания.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
		|	Задания.ДатаЗаписи КАК ДатаЗаписи,
		|	Задания.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений
		|ПОМЕСТИТЬ ВТ_Идентификаторы
		|ИЗ
		|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК Задания
		|ГДЕ
		|	Задания.ИдентификаторЗаписи В (&ИдентификаторыЗаданийКортежей)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Идентификаторы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВТ_Идентификаторы.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВТ_Идентификаторы.Валюта КАК Валюта,
		|	ВТ_Идентификаторы.ИдентификаторЗаписи КАК ИдентификаторЗаписи
		|ИЗ
		|	ВТ_Идентификаторы КАК ВТ_Идентификаторы
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Идентификаторы.Документ КАК Документ,
		|	ВТ_Идентификаторы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВТ_Идентификаторы.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВТ_Идентификаторы.Валюта КАК Валюта,
		|	ВТ_Идентификаторы.Организация КАК Организация,
		|	МИНИМУМ(ВТ_Идентификаторы.ПериодЗадания) КАК ПериодЗадания,
		|	МИНИМУМ(ВТ_Идентификаторы.ДатаЗаписи) КАК ДатаЗаписи,
		|	МИНИМУМ(ВТ_Идентификаторы.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений
		|ИЗ
		|	ВТ_Идентификаторы КАК ВТ_Идентификаторы
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Идентификаторы.Документ,
		|	ВТ_Идентификаторы.АналитикаУчетаПоПартнерам,
		|	ВТ_Идентификаторы.ОбъектРасчетов,
		|	ВТ_Идентификаторы.Валюта,
		|	ВТ_Идентификаторы.Организация
		|ИТОГИ
		|ПО
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта";

		Запрос.УстановитьПараметр("ИдентификаторыЗаданийКортежей", ИдентификаторыЗаданийКортежей);
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.ВыполнитьПакет();
		УстановитьПривилегированныйРежим(Ложь);

		ОшибкиКортежей = Результат[Результат.ВГраница() - 1].Выгрузить();
		ОшибкиКортежей.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, Валюта");

		ВыборкаПоАналитике = Результат[Результат.ВГраница()].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		СтруктураОтбораЗаданий = Новый Структура;

		Пока ВыборкаПоАналитике.Следующий() Цикл

			ВыборкаПоОбъектРасчетов = ВыборкаПоАналитике.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаПоОбъектРасчетов.Следующий() Цикл

				ВыборкаПоВалюте = ВыборкаПоОбъектРасчетов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаПоВалюте.Следующий() Цикл

					ИдентификаторОшибки = Новый УникальныйИдентификатор;

					СтруктураОтбораЗаданий.Вставить("АналитикаУчетаПоПартнерам",
						ВыборкаПоВалюте.АналитикаУчетаПоПартнерам);
					СтруктураОтбораЗаданий.Вставить("ОбъектРасчетов", ВыборкаПоВалюте.ОбъектРасчетов);
					СтруктураОтбораЗаданий.Вставить("Валюта", ВыборкаПоВалюте.Валюта);

					ТекстыОшибок = Новый Массив;
					ИдентификаторыКортежа = Новый Массив;
					ОписанияОшибок = ОшибкиКортежей.НайтиСтроки(СтруктураОтбораЗаданий);
					Для Каждого СтрокаОписанияОшибки Из ОписанияОшибок Цикл
						ТекстыОшибок.Добавить(ИдентификаторыНеОбработанныхЗаписей.Получить(
							СтрокаОписанияОшибки.ИдентификаторЗаписи));
						ИдентификаторыКортежа.Добавить(СтрокаОписанияОшибки.ИдентификаторЗаписи);
					КонецЦикла;
					ТекстыОшибок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТекстыОшибок);
					ОписаниеОшибки = СтрСоединить(ТекстыОшибок, Символы.ПС);

					НачатьТранзакцию();

					Попытка
						// Запись в регистр ошибок
						ОшибкиНаборЗаписей = РегистрыСведений.ОшибкиОчередейЗаданий.СоздатьНаборЗаписей();
						ЗаписьОшибки = ОшибкиНаборЗаписей.Добавить();
						ЗаписьОшибки.ИдентификаторОшибки = ИдентификаторОшибки;
						ЗаписьОшибки.ОписаниеОшибки = ОписаниеОшибки;
						ЗаписьОшибки.ДатаЗаписи = ТекущаяДатаСеанса();
						УстановитьПривилегированныйРежим(Истина);
						ОшибкиНаборЗаписей.Записать(Ложь);
						УстановитьПривилегированныйРежим(Ложь);

						ДетальныеЗаписи = ВыборкаПоВалюте.Выбрать();

						Задания = СоздатьНаборЗаписей();

						Пока ДетальныеЗаписи.Следующий() Цикл

							ЗаданиеСОшибкой = Задания.Добавить();
							ЗаполнитьЗначенияСвойств(ЗаданиеСОшибкой, ДетальныеЗаписи);
							ЗаданиеСОшибкой.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
							ЗаданиеСОшибкой.ВыполненоСОшибкой = Истина;
							ЗаданиеСОшибкой.ИдентификаторОшибки = ИдентификаторОшибки;

						КонецЦикла;
						
						УстановитьПривилегированныйРежим(Истина);
						Задания.Записать(Ложь);
						
						НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ИмяРегистраОчереди, ИдентификаторыКортежа,
							ОчередиЗаданий);
						УстановитьПривилегированныйРежим(Ложь);
							
						ЗафиксироватьТранзакцию();
						
					Исключение

						ОтменитьТранзакцию();

						ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

						НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди,
							Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ТекстОшибки);

					КонецПопытки;
					
				КонецЦикла;

			КонецЦикла;

		КонецЦикла;
		
		ИдентификаторыНеОбработанныхЗаписей.Очистить();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Переопределение параметров очереди заданий.
// 
// Параметры:
//  ИмяОчереди -Строка - Имя очереди заданий
// 
// Возвращаемое значение:
//  Структура - Переопределение параметров описания очереди заданий:
// * ПараметрЗапросаВидЗадания - Строка - Имя параметра, соответствующего полю "ВидЗадания" ,которое будет использовано при формировании текстов запросов.
// Переопределяется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания". 
// Имя параметра должно быть уникальным среди всех регистров заданий очередей.
// * ЗначениеВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Значение, которое будет подставлено в ПараметрЗапросаВидЗадания.
// Параметр заполняется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания".
// * ПараметрЗапросаНомерОчереди - Строка - Имя параметра, соответствующего полю "НомерОчереди" ,которое будет использовано при формировании текстов запросов.
// Переопределяется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания".
// Имя параметра должно быть уникальным среди всех регистров заданий очередей.
// * ЗначениеНомерОчереди - Число - Значение, которое будет подставлено в ПараметрЗапросаНомерОчереди.
// * ТекстЗапросаГранулыРасчетаВОбработке - Строка - Текст запроса, по которому будет формироваться временная таблица с гранулами расчета заданий.
// Выбираемые поля должны соответсвовать колонкам таблицы значений, получаемой в результате вызова функции ТаблицаГранулыРасчетаВОбработке модуля менеджера регистра заданий.
// Имя временной таблицы должно быть уникальным среди всех регистров заданий очередей.
// * ТекстЗапросаИсключаемыеДокументы - Строка - Текст запроса, результат выполнения которого получает список документов, по которым выполняется обработка гранул расчета.
// Текст запроса формируется на основании ТекстЗапросаГранулыРасчетаВОбработке.
// * ОбрабатываемыеГранулыИмяТаблицы - Строка - Имя параметра запроса, значение которого является Таблица значений с данными обрабатываемых гранул.
// Имя таблицы должно быть уникальным среди всех регистров заданий очередей.
//
Функция ОписаниеПараметровОчереди(ИмяОчереди) Экспорт
	
	ОписаниеОчереди = НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди();
	ОписаниеОчереди.ПараметрЗапросаВидЗадания = "ВидЗаданияРаспределениеВзаиморасчетовПоСрокам";
	ОписаниеОчереди.ЗначениеВидЗадания = Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам;
	ОписаниеОчереди.ПараметрЗапросаНомерОчереди = "НомерОчередиРаспределениеВзаиморасчетовПоСрокам";
	ОписаниеОчереди.ЗначениеНомерОчереди = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, "НомерОчереди");

	#Область ТекстЗапросаГранулыРасчетаВОбработке
	ТекстЗапросаГранулыРасчетаВОбработке = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбрабатываемыеГранулы.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ОбрабатываемыеГранулы.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ОбрабатываемыеГранулы.ВалютаРасчетов КАК Валюта,
	|	ОбрабатываемыеГранулы.ВидЗадания КАК ВидЗадания,
	|	ОбрабатываемыеГранулы.НомерОчереди КАК НомерОчереди
	|ПОМЕСТИТЬ ВтОбрабатываемыеГранулы
	|ИЗ
	|	&ОбрабатываемыеГранулы КАК ОбрабатываемыеГранулы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	АналитикаУчетаПоПартнерам,
	|	ОбъектРасчетов,
	|	Валюта";
	
	ТекстЗапросаГранулыРасчетаВОбработке = СтрЗаменить(ТекстЗапросаГранулыРасчетаВОбработке, "ВтОбрабатываемыеГранулы", "Вт" + ИмяОчереди);
	ТекстЗапросаГранулыРасчетаВОбработке = СтрЗаменить(ТекстЗапросаГранулыРасчетаВОбработке, "&ОбрабатываемыеГранулы", "&ОбрабатываемыеГранулы" + ИмяОчереди);
	ОписаниеОчереди.Вставить("ТекстЗапросаГранулыРасчетаВОбработке", ТекстЗапросаГранулыРасчетаВОбработке);
	ОписаниеОчереди.Вставить("ОбрабатываемыеГранулыИмяТаблицы", "ОбрабатываемыеГранулы" + ИмяОчереди);
	#КонецОбласти
	
	#Область ТекстЗапросаИсключаемыеДокументы
	ТекстЗапросаИсключаемыеДокументы = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЗаданияКРаспределениюВзаиморасчетов.Документ КАК Документ,
	|	ОбрабатываемыеКортежи.ВидЗадания КАК ВидЗадания,
	|	ОбрабатываемыеКортежи.НомерОчереди КАК НомерОчереди
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтОбрабатываемыеГранулы КАК ОбрабатываемыеКортежи
	|		ПО ОбрабатываемыеКортежи.АналитикаУчетаПоПартнерам = ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам
	|		И ОбрабатываемыеКортежи.ОбъектРасчетов = ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов
	|		И ОбрабатываемыеКортежи.Валюта = ЗаданияКРаспределениюВзаиморасчетов.Валюта";
	
	ТекстЗапросаИсключаемыеДокументы = СтрЗаменить(ТекстЗапросаИсключаемыеДокументы, "ВтОбрабатываемыеГранулы", "Вт" + ИмяОчереди);
	ОписаниеОчереди.Вставить("ТекстЗапросаИсключаемыеДокументы", ТекстЗапросаИсключаемыеДокументы);
	#КонецОбласти
	
	Возврат ОписаниеОчереди;
	
КонецФункции

// Таблица гранул расчета в обработке.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см.ТаблицаГранулРасчета:
// * ИдентификаторПотока - УникальныйИдентификатор - Идентификатор потока
//
Функция ТаблицаГранулыРасчетаВОбработке() Экспорт
	
	КортежиВОбработке = ТаблицаГранулРасчета();
	КортежиВОбработке.Колонки.Добавить("ИдентификаторПотока", Новый ОписаниеТипов("УникальныйИдентификатор"));
	КортежиВОбработке.Индексы.Добавить("ИдентификаторПотока");
	КортежиВОбработке.Индексы.Добавить("ГруппаОбработки");
	
	Возврат КортежиВОбработке;
	
КонецФункции

// Зарегистрировать проблему выполнения расчета.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Процедура ЗарегистрироватьПроблемуВыполненияРасчета(ПараметрыОбработчика, ВидЗадания = Неопределено) Экспорт
	
	Если Не ПараметрыОбработчика.ДанныеЭтапа.Код = Справочники.ОперацииЗакрытияМесяца.НеоперативноеДопроведениеДокументов Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыРасчета = ПараметрыОбработчика.ПараметрыРасчета;
	
	КонецПериода = ПараметрыРасчета.КонецПериода;
	
	ГруппаПроблем = НСтр("ru = 'При выполнении операции зарегистрированы ошибки';
						|en = 'Errors are registered while executing the operation'",
		ОбщегоНазначения.КодОсновногоЯзыка());
		
	ШаблонЗаданиеСОшибкой = НСтр("ru = 'Выполнение задания ""%1"" по
	|Аналитика учета по партнерам: %2, Объект расчетов: %3, валюта расчетов: %4 завершилось с ошибками.';
	|en = 'The ""%1"" job of
	|Accounting dimension by partners: %2, AR/AP object: %3, payment currency: %4 was completed with errors.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ШаблонЗаданиеНеВыполнено = НСтр("ru = 'Задание ""%1"" по
	|Аналитика учета по партнерам: %2, Объект расчетов: %3, валюта расчетов: %4 не было выполнено.';
	|en = 'The ""%1"" job of
	|Accounting dimension by partners: %2, AR/AP object: %3, payment currency: %4 was not completed.'", ОбщегоНазначения.КодОсновногоЯзыка());
		
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ЗаданияКРаспределениюВзаиморасчетов.Организация КАК Организация,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания, МЕСЯЦ)) КАК ПериодЗадания,
	|	ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ЗаданияКРаспределениюВзаиморасчетов.Валюта КАК Валюта,
	|	ЗаданияКРаспределениюВзаиморасчетов.ВыполненоСОшибкой
	|ПОМЕСТИТЬ ВТ_Ошибки
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ЗаданияКРаспределениюВзаиморасчетов
	|ГДЕ
	|	ЗаданияКРаспределениюВзаиморасчетов.Организация В (&МассивОрганизаций)
	|	И ЗаданияКРаспределениюВзаиморасчетов.ПериодЗадания <= &КонецПериода
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКРаспределениюВзаиморасчетов.Организация,
	|	ЗаданияКРаспределениюВзаиморасчетов.АналитикаУчетаПоПартнерам,
	|	ЗаданияКРаспределениюВзаиморасчетов.ОбъектРасчетов,
	|	ЗаданияКРаспределениюВзаиморасчетов.Валюта,
	|	ЗаданияКРаспределениюВзаиморасчетов.ВыполненоСОшибкой
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Ошибки.Организация КАК Организация,
	|	ВТ_Ошибки.ПериодЗадания КАК ПериодЗадания,
	|	ВТ_Ошибки.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ВТ_Ошибки.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ВТ_Ошибки.Валюта КАК Валюта,
	|	ВТ_Ошибки.ВыполненоСОшибкой КАК ВыполненоСОшибкой
	|ИЗ
	|	ВТ_Ошибки КАК ВТ_Ошибки
	|ИТОГИ
	|ПО
	|	Организация,
	|	ПериодЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Ошибки";
	
	УстановитьПривилегированныйРежим(Истина);
	ВыборкаПоОрганизации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	УстановитьПривилегированныйРежим(Ложь);

	Пока ВыборкаПоОрганизации.Следующий() Цикл

		ВыборкаПоПериоду = ВыборкаПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПоПериоду.Следующий() Цикл

			ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемыРасчета(
					ПараметрыОбработчика.ДанныеЭтапа.Код, ВыборкаПоОрганизации.Организация,
				?(ЗначениеЗаполнено(ВыборкаПоПериоду.ПериодЗадания), ВыборкаПоПериоду.ПериодЗадания, НачалоМесяца(КонецПериода)));

			ДетальныеЗаписи = ВыборкаПоПериоду.Выбрать();

			Пока ДетальныеЗаписи.Следующий() Цикл

				ПолныйТекстПроблемы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(?(
					ДетальныеЗаписи.ВыполненоСОшибкой, ШаблонЗаданиеСОшибкой, ШаблонЗаданиеНеВыполнено),
					Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам,
					ДетальныеЗаписи.АналитикаУчетаПоПартнерам, ДетальныеЗаписи.ОбъектРасчетов, ДетальныеЗаписи.Валюта);

				ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
							ПараметрыРегистрации, ГруппаПроблем, Перечисления.ВажностьПроблемыУчета.Ошибка,
					ПолныйТекстПроблемы, ДетальныеЗаписи.ОбъектРасчетов);

			КонецЦикла;

		КонецЦикла;

	КонецЦикла;
	
КонецПроцедуры

// Создается таблица кортежей, по которым необходимо выполнить распределение оперативных взаиморасчетов.
// Таблица с данной структурой в последствии передается в ОперативныеВзаиморасчетыСервер.ВыполнитьОтложенноеРаспределение в качестве параметра "ДанныеКОбработке".
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица кортежей регистра взаиморасчетов в обработке:
// * АналитикаУчетаПоПартнерам - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета по партнерам
// * АналитикаУчетаПоПартнерамПриемник - СправочникСсылка.КлючиАналитикиУчетаПоПартнерам - Аналитика учета по партнерам (приемник)
// * ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - Объект расчетов
// * ОбъектРасчетовПриемник - СправочникСсылка.ОбъектыРасчетов - Объект расчетов (приемник)
// * ВалютаРасчетов - СправочникСсылка.Валюты - Валюта расчетов
// * ВалютаПриемник - СправочникСсылка.Валюты - Валюта расчетов (приемник)
// * Документ - ДокументСсылка - Документ к обработке
// * ПорядокОперации - Строка - Порядок операции
// * ТипРасчетов - ПеречислениеСсылка.ТипыРасчетовСПартнерами - Тип расчетов с партнером
// * ЭтоРасчетыСКлиентами - Булево - Признак, что расчет с клиентом
// * ДатаПересчета - Строка, Неопределено - Дата начала пересчета, для совместимости с регистром заданий
// * ДатаНачалаПересчета - Строка, Неопределено - Дата начала пересчета
// * Объект - ЛюбаяСсылка - Ссылка на объект, по которому ведется расчет
// * ДатаФакт - Дата - Порядок обработки
// * ГруппаОбработки - Число - Номер группы обработки кортежей
// * ПоДаннымОбъектаРасчетовИсточника - Булево - Признак того, что кортеж считается по данным объекта расчетов - источника
//
Функция ТаблицаГранулРасчета() Экспорт

	ТаблицаКортежей = Новый ТаблицаЗначений;
	ТаблицаКортежей.Колонки.Добавить("АналитикаУчетаПоПартнерам",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	ТаблицаКортежей.Колонки.Добавить("АналитикаУчетаПоПартнерамПриемник",
		Новый ОписаниеТипов("СправочникСсылка.КлючиАналитикиУчетаПоПартнерам"));
	ТаблицаКортежей.Колонки.Добавить("ОбъектРасчетов", Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	ТаблицаКортежей.Колонки.Добавить("ОбъектРасчетовПриемник",
		Новый ОписаниеТипов("СправочникСсылка.ОбъектыРасчетов"));
	ТаблицаКортежей.Колонки.Добавить("ВалютаРасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаКортежей.Колонки.Добавить("ВалютаПриемник", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ТаблицаКортежей.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	ТаблицаКортежей.Колонки.Добавить("ПорядокОперации", ОбщегоНазначения.ОписаниеТипаСтрока(40));
	ТаблицаКортежей.Колонки.Добавить("ТипРасчетов", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыРасчетовСПартнерами"));
	ТаблицаКортежей.Колонки.Добавить("ЭтоРасчетыСКлиентами", Новый ОписаниеТипов("Булево"));
	ТаблицаКортежей.Колонки.Добавить("ДатаПересчета");
	ТаблицаКортежей.Колонки.Добавить("ДатаНачалаПересчета");
	ТаблицаКортежей.Колонки.Добавить("Объект");
	ТаблицаКортежей.Колонки.Добавить("ДатаФакт", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаКортежей.Колонки.Добавить("ГруппаОбработки", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаКортежей.Колонки.Добавить("ПоДаннымОбъектаРасчетовИсточника", Новый ОписаниеТипов("Булево"));
	ТаблицаКортежей.Колонки.Добавить("ПериодКонтроляИзменений", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	ТаблицаКортежей.Колонки.Добавить("ВидЗадания", Новый ОписаниеТипов("СправочникСсылка.ВидыНеоперативныхЗаданий"));
	ТаблицаКортежей.Колонки.Добавить("НомерОчереди", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаКортежей.Колонки.Добавить("НачальноеЗаполнение", Новый ОписаниеТипов("Булево"));
	
	Возврат ТаблицаКортежей;

КонецФункции

// По переданному списку документов и виду задания обрабатываются данные об ошибках.
//
// Параметры:
//  СписокДокументов - Неопределено, Массив Из ДокументСсылка - Обрабатываемые документы
//  ИдентификаторыОшибок - Неопределено, Массив Из УникальныйИдентификатор - Список обработанных идентифкаторов ошибок
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Процедура СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументов, ИдентификаторыОшибок, ВидЗадания = Неопределено) Экспорт
	
	Если ВидЗадания <> Неопределено 
		И Не ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаОчереди.ПериодЗадания КАК ПериодЗадания,
	|	ТаблицаОчереди.Организация КАК Организация,
	|	ТаблицаОчереди.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ТаблицаОчереди.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ТаблицаОчереди.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ТаблицаОчереди.Валюта КАК Валюта,
	|	ТаблицаОчереди.Документ КАК Документ,
	|	ТаблицаОчереди.ДатаЗаписи КАК ДатаЗаписи,
	|	ТаблицаОчереди.ИдентификаторОшибки КАК ИдентификаторОшибки,
	|	ТаблицаОчереди.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений
	|ПОМЕСТИТЬ ДанныеОбОшибках
	|ИЗ
	|	РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК ТаблицаОчереди
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
	|ГДЕ
	|	ТаблицаОчереди.ВыполненоСОшибкой
	|	И &ОтборПоДокументам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОбОшибках.ПериодЗадания КАК ПериодЗадания,
	|	ДанныеОбОшибках.Организация КАК Организация,
	|	ДанныеОбОшибках.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ДанныеОбОшибках.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ИЗ
	|	ДанныеОбОшибках КАК ДанныеОбОшибках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДанныеОбОшибках.ПериодЗадания) КАК ПериодЗадания,
	|	ДанныеОбОшибках.Организация КАК Организация,
	|	ДанныеОбОшибках.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
	|	ДанныеОбОшибках.ОбъектРасчетов КАК ОбъектРасчетов,
	|	ДанныеОбОшибках.Валюта КАК Валюта,
	|	ДанныеОбОшибках.Документ КАК Документ,
	|	МИНИМУМ(ДанныеОбОшибках.ДатаЗаписи) КАК ДатаЗаписи,
	|	МИНИМУМ(ДанныеОбОшибках.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений
	|ИЗ
	|	ДанныеОбОшибках КАК ДанныеОбОшибках
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОбОшибках.Организация,
	|	ДанныеОбОшибках.АналитикаУчетаПоПартнерам,
	|	ДанныеОбОшибках.ОбъектРасчетов,
	|	ДанныеОбОшибках.Валюта,
	|	ДанныеОбОшибках.Документ";
	
	Если СписокДокументов = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДокументам", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДокументам", "ТаблицаОчереди.Документ В (&СписокДокументов)");
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	ЗаданияСОшибками = Результат[Результат.ВГраница() - 1].Выгрузить();
	ЗаписиКФормированиюЗаданий = Результат[Результат.ВГраница()].Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);

	Если ЗаданияСОшибками.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ИдентификаторыОшибокОчереди = ЗаданияСОшибками.ВыгрузитьКолонку("ИдентификаторОшибки");

	НачатьТранзакцию();
	Попытка

		НаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ЗаданияСОшибками);
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
		УстановитьПривилегированныйРежим(Ложь);

		НовыйНаборЗаписей = РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов.СоздатьНаборЗаписей();
		Для Каждого ЗаписьЗадания Из ЗаписиКФормированиюЗаданий Цикл
			НовоеЗадание = НовыйНаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеЗадание, ЗаписьЗадания);
			НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
		КонецЦикла;
		УстановитьПривилегированныйРежим(Истина);
		НовыйНаборЗаписей.Записать(Ложь);
		УстановитьПривилегированныйРежим(Ложь);

		ЗафиксироватьТранзакцию();

	Исключение

		ОтменитьТранзакцию();
		
		ИдентификаторыОшибокОчереди = Новый Массив;
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

		Событие = НСтр("ru = 'Неоперативная очередь заданий: снятие признака ""Выполнено с ошибкой""';
						|en = 'Backdate job queue: clear the ""Failed"" flag'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);

	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИдентификаторыОшибок, ИдентификаторыОшибокОчереди, Истина);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДополнитьТаблицуИзмененийПриемниками(ТаблицаИзменений)
	
	ПараметрыРаспределения = ОперативныеВзаиморасчетыСервер.ПараметрыРаспределенияРасчетов();
	ПараметрыРаспределения.РаспределениеСУчетомПриемников = Истина;
	ПараметрыРаспределения.ТаблицаИзменений = ТаблицаИзменений;
	ОперативныеВзаиморасчетыСервер.ДополнитьТаблицуИзмененийПриемниками(ТаблицаИзменений, ПараметрыРаспределения);
	
КонецПроцедуры

Процедура ОбработатьТаблицуИзменений(ТаблицаИзменений, ТаблицаПорцийДанных, ИспользуемыеКортежи = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	ДополнитьТаблицуИзмененийПриемниками(ТаблицаИзменений);
	УстановитьПривилегированныйРежим(Ложь);
	
	СтруктураОтбораКортежей = Новый Структура;

	Для Каждого СтрокаИзменений Из ТаблицаИзменений Цикл

		СтрокаИзменений.ЭтоРасчетыСКлиентами = (СтрокаИзменений.ТипРасчетов
			= Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
		Если СтрокаИзменений.ПорядокОперации = "" Тогда
			СтрокаИзменений.ДатаФакт = Дата(1, 1, 1);
		Иначе
			СтрокаИзменений.ДатаФакт = Дата(Лев(СтрокаИзменений.ПорядокОперации, 8));
		КонецЕсли;

		СтруктураОтбораКортежей.Вставить("АналитикаУчетаПоПартнерам", СтрокаИзменений.АналитикаУчетаПоПартнерам);
		СтруктураОтбораКортежей.Вставить("ОбъектРасчетов", СтрокаИзменений.ОбъектРасчетов);
		СтруктураОтбораКортежей.Вставить("ВалютаРасчетов", СтрокаИзменений.ВалютаРасчетов);

		Если Не ИспользуемыеКортежи = Неопределено Тогда
			НайденныеКортежиВПорции = ИспользуемыеКортежи.НайтиСтроки(СтруктураОтбораКортежей);
			Если НайденныеКортежиВПорции.Количество() = 0 Тогда
				ЗаполнитьЗначенияСвойств(ИспользуемыеКортежи.Добавить(), СтруктураОтбораКортежей);
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ТаблицаИзменений, ТаблицаПорцийДанных);
	
КонецПроцедуры

Процедура ЗаполнитьГруппуОбработкиКортежей(ИсходныеДанные, ДанныеКортежа, НомерГруппы)
	
	НайденныеДанные = ИсходныеДанные.НайтиСтроки(ДанныеКортежа);
	
	АналитикаУчетаПоПартнерам = ДанныеКортежа.АналитикаУчетаПоПартнерам;
	ОбъектРасчетов = ДанныеКортежа.ОбъектРасчетов;
	ВалютаРасчетов = ДанныеКортежа.ВалютаРасчетов;
	
	Для Каждого ТекущиеДанные Из НайденныеДанные Цикл
		
		Если ТекущиеДанные.ГруппаОбработки <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущиеДанные.ГруппаОбработки = НомерГруппы;

		Если ЗначениеЗаполнено(ТекущиеДанные.АналитикаУчетаПоПартнерамПриемник) Или ЗначениеЗаполнено(
			ТекущиеДанные.ОбъектРасчетовПриемник) Или ЗначениеЗаполнено(ТекущиеДанные.ВалютаПриемник) Тогда

			ДанныеКортежа.Вставить("АналитикаУчетаПоПартнерам", ТекущиеДанные.АналитикаУчетаПоПартнерамПриемник);
			ДанныеКортежа.Вставить("ОбъектРасчетов", ТекущиеДанные.ОбъектРасчетовПриемник);
			ДанныеКортежа.Вставить("ВалютаРасчетов", ТекущиеДанные.ВалютаПриемник);

			ЗаполнитьГруппуОбработкиКортежей(ИсходныеДанные, ДанныеКортежа, НомерГруппы);

		КонецЕсли;

	КонецЦикла;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("АналитикаУчетаПоПартнерамПриемник", АналитикаУчетаПоПартнерам);
	СтруктураОтбора.Вставить("ОбъектРасчетовПриемник", ОбъектРасчетов);
	СтруктураОтбора.Вставить("ВалютаПриемник", ВалютаРасчетов);
	НайденныеДанные = ИсходныеДанные.НайтиСтроки(СтруктураОтбора);
	Для Каждого ТекущиеДанные Из НайденныеДанные Цикл
		
		Если ТекущиеДанные.ГруппаОбработки <> 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущиеДанные.ГруппаОбработки = НомерГруппы;
		
		ДанныеКортежа.Вставить("АналитикаУчетаПоПартнерам", ТекущиеДанные.АналитикаУчетаПоПартнерам);
		ДанныеКортежа.Вставить("ОбъектРасчетов", ТекущиеДанные.ОбъектРасчетов);
		ДанныеКортежа.Вставить("ВалютаРасчетов", ТекущиеДанные.ВалютаРасчетов);
		
		ЗаполнитьГруппуОбработкиКортежей(ИсходныеДанные, ДанныеКортежа, НомерГруппы);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗарегистрироватьОшибкиОбработкиДанных(ПараметрыДанных, ИдентификаторыЗаданийКортежей, МетаданныеРегистраОчереди)
	
	Если ПараметрыДанных.ПорцияДанных.Количество() > 0 Тогда

		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Задания.ИдентификаторЗадания КАК ИдентификаторЗадания
		|ПОМЕСТИТЬ ВтИдентификаторы
		|ИЗ
		|	&ИдентификаторыЗаданийКортежей КАК Задания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторЗадания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задания.Документ КАК Документ,
		|	Задания.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	Задания.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Задания.Валюта КАК Валюта,
		|	ВтИдентификаторы.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	Задания.Организация КАК Организация,
		|	Задания.ПериодЗадания КАК ПериодЗадания,
		|	Задания.ДатаЗаписи КАК ДатаЗаписи,
		|	Задания.ПериодКонтроляИзменений КАК ПериодКонтроляИзменений
		|ПОМЕСТИТЬ ВтЗаданияПоДокументам
		|ИЗ
		|	ВтИдентификаторы КАК ВтИдентификаторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияКРаспределениюВзаиморасчетов КАК Задания
		|		ПО ВтИдентификаторы.ИдентификаторЗадания = Задания.ИдентификаторЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтЗаданияПоДокументам.Документ КАК Документ,
		|	ВтЗаданияПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВтЗаданияПоДокументам.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВтЗаданияПоДокументам.Валюта КАК Валюта,
		|	ВтЗаданияПоДокументам.Организация КАК Организация,
		|	МИНИМУМ(ВтЗаданияПоДокументам.ПериодЗадания) КАК ПериодЗадания,
		|	МИНИМУМ(ВтЗаданияПоДокументам.ДатаЗаписи) КАК ДатаЗаписи,
		|	МИНИМУМ(ВтЗаданияПоДокументам.ПериодКонтроляИзменений) КАК ПериодКонтроляИзменений
		|ИЗ
		|	ВтЗаданияПоДокументам КАК ВтЗаданияПоДокументам
		|СГРУППИРОВАТЬ ПО
		|	ВтЗаданияПоДокументам.Документ,
		|	ВтЗаданияПоДокументам.АналитикаУчетаПоПартнерам,
		|	ВтЗаданияПоДокументам.ОбъектРасчетов,
		|	ВтЗаданияПоДокументам.Валюта,
		|	ВтЗаданияПоДокументам.Организация
		|ИТОГИ
		|ПО
		|	АналитикаУчетаПоПартнерам,
		|	ОбъектРасчетов,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтЗаданияПоДокументам.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	ВтЗаданияПоДокументам.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам,
		|	ВтЗаданияПоДокументам.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ВтЗаданияПоДокументам.Валюта КАК ВалютаРасчетов
		|ИЗ
		|	ВтЗаданияПоДокументам КАК ВтЗаданияПоДокументам";
		Запрос.УстановитьПараметр("ИдентификаторыЗаданийКортежей", ИдентификаторыЗаданийКортежей);
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.ВыполнитьПакет();
		УстановитьПривилегированныйРежим(Ложь);

		ОшибкиКортежей = ПараметрыДанных.ПорцияДанных.Скопировать( ,
			"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, ОписаниеОшибки"); // ТаблицаЗначений
		ОшибкиКортежей.Свернуть("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, ОписаниеОшибки");
		ОшибкиКортежей.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");

		ИдентификаторыЗаданий = Результат[Результат.ВГраница()].Выгрузить();
		ИдентификаторыЗаданий.Индексы.Добавить("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов");

		ВыборкаПоАналитике = Результат[Результат.ВГраница() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		СтруктураОтбораЗаданий = Новый Структура;

		Пока ВыборкаПоАналитике.Следующий() Цикл

			ВыборкаПоОбъектРасчетов = ВыборкаПоАналитике.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаПоОбъектРасчетов.Следующий() Цикл

				ВыборкаПоВалюте = ВыборкаПоОбъектРасчетов.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаПоВалюте.Следующий() Цикл

					ИдентификаторОшибки = Новый УникальныйИдентификатор;

					СтруктураОтбораЗаданий.Вставить("АналитикаУчетаПоПартнерам",
						ВыборкаПоВалюте.АналитикаУчетаПоПартнерам);
					СтруктураОтбораЗаданий.Вставить("ОбъектРасчетов", ВыборкаПоВалюте.ОбъектРасчетов);
					СтруктураОтбораЗаданий.Вставить("ВалютаРасчетов", ВыборкаПоВалюте.Валюта);

					ТекстыОшибок = ОшибкиКортежей.Скопировать(СтруктураОтбораЗаданий,
						"ОписаниеОшибки").ВыгрузитьКолонку("ОписаниеОшибки");
					ТекстыОшибок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТекстыОшибок);
					ОписаниеОшибки = СтрСоединить(ТекстыОшибок, Символы.ПС);

					НачатьТранзакцию();

					Попытка
						
						// Запись в регистр ошибок
						ОшибкиНаборЗаписей = РегистрыСведений.ОшибкиОчередейЗаданий.СоздатьНаборЗаписей();
						ЗаписьОшибки = ОшибкиНаборЗаписей.Добавить();
						ЗаписьОшибки.ИдентификаторОшибки = ИдентификаторОшибки;
						ЗаписьОшибки.ОписаниеОшибки = ОписаниеОшибки;
						ЗаписьОшибки.ДатаЗаписи = ТекущаяДатаСеанса();
						УстановитьПривилегированныйРежим(Истина);
						ОшибкиНаборЗаписей.Записать(Ложь);
						УстановитьПривилегированныйРежим(Ложь);

						ДетальныеЗаписи = ВыборкаПоВалюте.Выбрать();

						Задания = СоздатьНаборЗаписей();

						Пока ДетальныеЗаписи.Следующий() Цикл

							ЗаданиеСОшибкой = Задания.Добавить();
							ЗаполнитьЗначенияСвойств(ЗаданиеСОшибкой, ДетальныеЗаписи);
							ЗаданиеСОшибкой.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
							ЗаданиеСОшибкой.ВыполненоСОшибкой = Истина;
							ЗаданиеСОшибкой.ИдентификаторОшибки = ИдентификаторОшибки;

						КонецЦикла;
						
						УстановитьПривилегированныйРежим(Истина);
						Задания.Записать(Ложь);
						УстановитьПривилегированныйРежим(Ложь);

						ИдентификаторыКортежа = ИдентификаторыЗаданий.Скопировать(СтруктураОтбораЗаданий,
							"ИдентификаторЗадания").ВыгрузитьКолонку("ИдентификаторЗадания");
						
						УстановитьПривилегированныйРежим(Истина);
						НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ПараметрыДанных.ИмяРегистраОчереди,
							ИдентификаторыКортежа, ПараметрыДанных.ОчередиЗаданий);
						УстановитьПривилегированныйРежим(Ложь);
						
						ЗафиксироватьТранзакцию();

					Исключение

						ОтменитьТранзакцию();

						Если МетаданныеРегистраОчереди = Неопределено Тогда
							МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияКРаспределениюВзаиморасчетов;
						КонецЕсли;

						ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

						НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди,
							Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам, ТекстОшибки);

					КонецПопытки;

				КонецЦикла;

			КонецЦикла;

		КонецЦикла;

	КонецЕсли;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахОбработкиДанных(ПараметрыДанных)
	
	Если ПараметрыДанных.ПорцияДанных.Количество() > 0 Тогда
	
		ОшибкиКортежей = ПараметрыДанных.ПорцияДанных.Скопировать( ,
			"АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, ОписаниеОшибки"); // ТаблицаЗначений
		ОшибкиКортежей.Свернуть("АналитикаУчетаПоПартнерам, ОбъектРасчетов, ВалютаРасчетов, ОписаниеОшибки");
		
		ШаблонОшибки = НСтр("ru = 'Не удалось выполнить заполнение оперативных взаиморасчетов:
		|Аналитика учета по партнерам = %1
		|Объект расчетов = %2
		|Валюта = %3
		|по причине: %4';
		|en = 'Cannot fill in real-time AR/AP:
		|Accounting dimension by partners = %1
		|AR/AP object = %2
		|Currency = %3
		|Reason: %4'", ОбщегоНазначения.КодОсновногоЯзыка());
				
		МассивОшибок = Новый Массив;
		Для Каждого ОшибкаКортежа Из ОшибкиКортежей Цикл
			Если ЗначениеЗаполнено(ОшибкаКортежа.ОписаниеОшибки) Тогда
				ТекстОшибкиПоКортежу = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,
					ОшибкаКортежа.АналитикаУчетаПоПартнерам, ОшибкаКортежа.ОбъектРасчетов,
					ОшибкаКортежа.ВалютаРасчетов, ОшибкаКортежа.ОписаниеОшибки);
				МассивОшибок.Добавить(ТекстОшибкиПоКортежу);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивОшибок.Количество() > 0 Тогда
			ТекстИсключения = СтрСоединить(МассивОшибок, Символы.ПС);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли