#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьИдентификаторыПодписей(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ПустойИдентификатор", Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"));
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект КАК ПодписанныйОбъект
		|ПОМЕСТИТЬ ВТОбновляемыеОбъекты
		|ИЗ
		|	РегистрСведений.ЭлектронныеПодписиЭДОБЗК КАК ЭлектронныеПодписиЭДОБЗК
		|ГДЕ
		|	НЕ ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект В (&МассивОбновленных)
		|	И ЭлектронныеПодписиЭДОБЗК.ИдентификаторПодписи = &ПустойИдентификатор";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Запрос.Выполнить().Выбрать();
	Если ЗарплатаКадры.ВТСодержитСтроки(Запрос.МенеджерВременныхТаблиц, "ВТОбновляемыеОбъекты") = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбновляемыеОбъекты.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ЭлектронныеПодписиЭДОБЗК.УдалитьОтпечаток КАК УдалитьОтпечаток,
		|	ЭлектронныеПодписиЭДОБЗК.УдалитьДатаПодписи КАК УдалитьДатаПодписи,
		|	ЭлектронныеПодписиЭДОБЗК.РольПодписанта КАК РольПодписанта,
		|	ЭлектронныеПодписиЭДОБЗК.УдалитьМЧД КАК УдалитьМЧД,
		|	ЭлектронныеПодписи.ИдентификаторПодписи КАК ИдентификаторПодписи
		|ПОМЕСТИТЬ ВТЗаписиСИдентификатором
		|ИЗ
		|	ВТОбновляемыеОбъекты КАК ОбновляемыеОбъекты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписиЭДОБЗК КАК ЭлектронныеПодписиЭДОБЗК
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|			ПО ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект = ЭлектронныеПодписи.ПодписанныйОбъект
		|				И ЭлектронныеПодписиЭДОБЗК.УдалитьОтпечаток = ЭлектронныеПодписи.Отпечаток
		|				И ЭлектронныеПодписиЭДОБЗК.УдалитьДатаПодписи = ЭлектронныеПодписи.ДатаПодписи
		|		ПО ОбновляемыеОбъекты.ПодписанныйОбъект = ЭлектронныеПодписиЭДОБЗК.ПодписанныйОбъект
		|ГДЕ
		|	ЭлектронныеПодписиЭДОБЗК.ИдентификаторПодписи = &ПустойИдентификатор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗаписиСИдентификатором.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|	ЗаписиСИдентификатором.УдалитьОтпечаток КАК УдалитьОтпечаток,
		|	ЗаписиСИдентификатором.УдалитьДатаПодписи КАК УдалитьДатаПодписи,
		|	ЗаписиСИдентификатором.РольПодписанта КАК РольПодписанта,
		|	ЗаписиСИдентификатором.УдалитьМЧД КАК УдалитьМЧД,
		|	ЗаписиСИдентификатором.ИдентификаторПодписи КАК ИдентификаторПодписи,
		|	1 КАК КоличествоИдентификаторов
		|ИЗ
		|	ВТЗаписиСИдентификатором КАК ЗаписиСИдентификатором
		|ГДЕ
		|	НЕ ЗаписиСИдентификатором.ИдентификаторПодписи ЕСТЬ NULL
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗаписиСИдентификатором.ПодписанныйОбъект,
		|	ЗаписиСИдентификатором.УдалитьОтпечаток,
		|	ЗаписиСИдентификатором.УдалитьДатаПодписи,
		|	ЗаписиСИдентификатором.РольПодписанта,
		|	ЗаписиСИдентификатором.УдалитьМЧД,
		|	МАКСИМУМ(ЭлектронныеПодписи.ИдентификаторПодписи),
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭлектронныеПодписи.ИдентификаторПодписи)
		|ИЗ
		|	ВТЗаписиСИдентификатором КАК ЗаписиСИдентификатором
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|		ПО ЗаписиСИдентификатором.ПодписанныйОбъект = ЭлектронныеПодписи.ПодписанныйОбъект
		|			И ЗаписиСИдентификатором.УдалитьОтпечаток = ЭлектронныеПодписи.Отпечаток
		|ГДЕ
		|	ЗаписиСИдентификатором.ИдентификаторПодписи ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ЗаписиСИдентификатором.ПодписанныйОбъект,
		|	ЗаписиСИдентификатором.УдалитьОтпечаток,
		|	ЗаписиСИдентификатором.УдалитьДатаПодписи,
		|	ЗаписиСИдентификатором.РольПодписанта,
		|	ЗаписиСИдентификатором.УдалитьМЧД
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПодписанныйОбъект";

	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("ПодписанныйОбъект") Цикл
		
		МассивОбновленных.Добавить(Выборка.ПодписанныйОбъект);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ЭлектронныеПодписиЭДОБЗК", "ПодписанныйОбъект", Выборка.ПодписанныйОбъект) Тогда
			Продолжить;
		КонецЕсли;
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.КоличествоИдентификаторов = 1 Тогда
				НаборЗаписей = РегистрыСведений.ЭлектронныеПодписиЭДОБЗК.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(Выборка.ПодписанныйОбъект);
				НаборЗаписей.Отбор.УдалитьОтпечаток.Установить(Выборка.УдалитьОтпечаток);
				НаборЗаписей.Отбор.УдалитьДатаПодписи.Установить(Выборка.УдалитьДатаПодписи);
				НаборЗаписей.Отбор.РольПодписанта.Установить(Выборка.РольПодписанта);
				НаборЗаписей.Отбор.УдалитьМЧД.Установить(Выборка.УдалитьМЧД);
				
				НаборЗаписей.Прочитать();
				Для Каждого Запись Из НаборЗаписей Цикл
					Запись.ИдентификаторПодписи = Выборка.ИдентификаторПодписи;
				КонецЦикла;
				
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			КонецЕсли;
		КонецЦикла; 
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
