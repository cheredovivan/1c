#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ФормированиеЗаданийНеоперативнойОчереди

// Создание заданий неоперативной очереди при записи наборов данных регистров - источников.
// 
// Параметры:
//  МенеджерРегистра - РегистрНакопленияМенеджер, РегистрСведенийМенеджер - Менеджер регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                      По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - Данные временных таблиц используются при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗадания(МенеджерРегистра, ВидыЗаданий, НаборЗаписей, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт

	Если ВидыЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ТаблицаДокументовКОтражению = МенеджерРегистра.ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(
		НаборЗаписей, ВидыЗаданий, МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);

	МеждународныйУчетПроведениеСервер.ВернутьДокументыКОтражению(ТаблицаДокументовКОтражению);

КонецПроцедуры

// Таблица документов к отражению в международном учете.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица документов к отражению в международном учете:
// * Документ - ДокументСсылка - Документ к отражению
// * Организация - СправочникСсылка.Организации - Организация
// * ДатаОтражения - Дата - Дата отражения
// * Период - Дата - Период отражения
// 
Функция ТаблицаДокументовКОтражениюВМеждународномУчете() Экспорт
	
	ТаблицаДокументов = Новый ТаблицаЗначений();
	ТаблицаДокументов.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	ТаблицаДокументов.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаДокументов.Колонки.Добавить("ДатаОтражения", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	ТаблицаДокументов.Колонки.Добавить("Период", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	
	Возврат ТаблицаДокументов;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
