#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ЗаписатьДанные(ПараметрыМетода, ДанныеСервиса) Экспорт 
	
	Если ДанныеСервиса = Неопределено
		ИЛИ ТипЗнч(ДанныеСервиса) <> Тип("ТаблицаЗначений") Тогда
		УдалитьДанныеПоРегистру(ПараметрыМетода.Организация, ПараметрыМетода.ДатаНачала, ПараметрыМетода.ДатаОкончания);
		Возврат;
	КонецЕсли;
	
	Если СервисСверкиРасчетовВызовСервера.ОповещенияСверкиВключены() Тогда
		ЗаписатьИзменениеРасхождений(
			ДанныеСервиса, ПараметрыМетода.Организация, ПараметрыМетода.ДатаНачала, ПараметрыМетода.ДатаОкончания);
	КонецЕсли;
	УдалитьДанныеПоРегистру(ПараметрыМетода.Организация, ПараметрыМетода.ДатаНачала, ПараметрыМетода.ДатаОкончания);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Строка Из ДанныеСервиса Цикл
		
		Контрагент = Строка.Контрагент;
		Организация = ПараметрыМетода.Организация;
		
		НаборЗаписейРасхожденияДокументовПриСверке = РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.СоздатьНаборЗаписей();
		НаборЗаписейРасхожденияДокументовПриСверке.Отбор.Организация.Установить(Организация);
		НаборЗаписейРасхожденияДокументовПриСверке.Отбор.Контрагент.Установить(Контрагент);
		НаборЗаписейРасхожденияДокументовПриСверке.Отбор.ИдентификаторСверкиПоОрганизации.Установить(Строка.ИдентификаторСверкиПоОрганизации);
		НаборЗаписейРасхожденияДокументовПриСверке.Отбор.ИдентификаторСверкиКонтрагенту.Установить(Строка.ИдентификаторСверкиКонтрагенту);
		
		Запись = НаборЗаписейРасхожденияДокументовПриСверке.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		ДокументСсылка = Неопределено;
		
		РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.ЗаписатьДанные(Строка.МассивДокументовПоОрганизации, Организация, 
			Контрагент, Запись.ИдентификаторСверкиПоОрганизации, ДокументСсылка);
		РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.ЗаписатьДанные(Строка.МассивДокументовПоКонтрагенту, Организация, 
			Контрагент, Запись.ИдентификаторСверкиКонтрагенту);
		
		Если ЗначениеЗаполнено(ДокументСсылка) Тогда
			Если НЕ ОбщегоНазначения.СсылкаСуществует(ДокументСсылка) Тогда
				// Битые ссылки в регистр не записываем. Документ есть на сервисе, но не существует в базе.
				// Необходимо отправить документ к удаленю на сервис.
				СервисСверкиРасчетов.ЗарегистрироватьДокументКОтправке(Организация, Контрагент, ДокументСсылка);
				Продолжить;
			КонецЕсли;
			Запись.Документ = ДокументСсылка;
		КонецЕсли;
		
		НаборЗаписейРасхожденияДокументовПриСверке.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаписатьИзменениеРасхождений(ДанныеСервиса, Организация, ДатаНачала, ДатаОкончания)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеСервиса.ИдентификаторРасхождения КАК СТРОКА(38)) КАК ИдентификаторРасхождения
	|ПОМЕСТИТЬ ВТ_ИдентификаторыПолученногоСообщения
	|ИЗ
	|	&ДанныеСервиса КАК ДанныеСервиса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	ВЫБОР
	|		КОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации = 0
	|			ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымКонтрагента
	|		ИНАЧЕ СервисСверкиРасчетовОбнаруженныеРасхождения.СуммаПоДаннымОрганизации
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации = 0
	|			ТОГДА СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымКонтрагента
	|		ИНАЧЕ СервисСверкиРасчетовОбнаруженныеРасхождения.НДСПоДаннымОрганизации
	|	КОНЕЦ КАК НДСДокумента
	|ПОМЕСТИТЬ ТекущиеРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И НЕ СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку
	|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения КАК ИдентификаторРасхождения
	|ИЗ
	|	ВТ_ИдентификаторыПолученногоСообщения КАК ВТ_ИдентификаторыПолученногоСообщения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТекущиеРасхождения КАК ТекущиеРасхождения
	|		ПО ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения = ТекущиеРасхождения.ИдентификаторРасхождения
	|ГДЕ
	|	ТекущиеРасхождения.ИдентификаторРасхождения ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущиеРасхождения.Организация КАК Организация,
	|	ТекущиеРасхождения.Контрагент КАК Контрагент,
	|	ТекущиеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	1 КАК КоличествоДокументов,
	|	ТекущиеРасхождения.СуммаДокумента КАК СуммаДокумента,
	|	ТекущиеРасхождения.НДСДокумента КАК НДСДокумента
	|ИЗ
	|	ТекущиеРасхождения КАК ТекущиеРасхождения
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИдентификаторыПолученногоСообщения КАК ВТ_ИдентификаторыПолученногоСообщения
	|		ПО ТекущиеРасхождения.ИдентификаторРасхождения = ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения
	|ГДЕ
	|	ВТ_ИдентификаторыПолученногоСообщения.ИдентификаторРасхождения ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ДанныеСервиса", ДанныеСервиса);
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ДатаНачала",    ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	
	Результат = Запрос.ВыполнитьПакет();
	
	// Запишем новые расхождения.
	НовыеРасхождения = Результат[2].Выгрузить().ВыгрузитьКолонку("ИдентификаторРасхождения");
	РегистрыСведений.СервисСверкиРасчетовСтатусыРасхождений.ЗаписатьДанные(НовыеРасхождения);
	
	// Запишем устраненные расхождения.
	УстраненныеРасхожденияПоКонтрагенту = Результат[3].Выбрать();
	Пока УстраненныеРасхожденияПоКонтрагенту.Следующий() Цикл
		РегистрыСведений.СервисСверкиРасчетовУстраненныеРасхождения.ЗаписатьДанные(
			УстраненныеРасхожденияПоКонтрагенту.Организация,
			УстраненныеРасхожденияПоКонтрагенту.Контрагент,
			УстраненныеРасхожденияПоКонтрагенту.ИдентификаторРасхождения,
			УстраненныеРасхожденияПоКонтрагенту.СуммаДокумента,
			УстраненныеРасхожденияПоКонтрагенту.НДСДокумента);
	КонецЦикла;
	
КонецПроцедуры


#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьДанныеПоРегистру(Организация, ДатаНачала, ДатаОкончания)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаОкончания));
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	(СервисСверкиРасчетовОбнаруженныеРасхождения.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|			ИЛИ СервисСверкиРасчетовОбнаруженныеРасхождения.КонтрагентНеПроизводилСверку)
	|	И СервисСверкиРасчетовОбнаруженныеРасхождения.Организация = &Организация";
	
	ТаблицаСтрокКУдалению = Запрос.Выполнить().Выгрузить();
	
	Для Каждого ТекущаяСтрока Из ТаблицаСтрокКУдалению Цикл
		
		КлючиЗаписи = КлючиЗадачСтруктурой();
		ЗаполнитьЗначенияСвойств(КлючиЗаписи, ТекущаяСтрока);
		КлючЗаписи = СоздатьКлючЗаписи(КлючиЗаписи);
		
		
	Попытка
		
		ЗаблокироватьДанныеДляРедактирования(КлючЗаписи);
		
		// Обработаем задачи, созданные после регистрации действий
		МенеджерЗаписи = РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючиЗаписи);
		МенеджерЗаписи.Прочитать();
		Если Не МенеджерЗаписи.Выбран() Тогда
			// Обработаем задачи, созданные до регистрации действий
			ЗаполнитьЗначенияСвойств(МенеджерЗаписи, КлючиЗаписи);
			МенеджерЗаписи.Прочитать();
		КонецЕсли;
		Если МенеджерЗаписи.Выбран() Тогда
			РегистрыСведений.СервисСверкиРасчетовДанныеСервиса.УдалитьДанныеДокументовПоПараметрам(МенеджерЗаписи);
			МенеджерЗаписи.Удалить();
		КонецЕсли;
		
		РазблокироватьДанныеДляРедактирования(КлючЗаписи);
		
	Исключение
		
		ШаблонОшибки = НСтр("ru = 'Не удалось удалить ключ записи в регистре ""СервисСверкиРасчетовОбнаруженныеРасхождения"".
			|Подробное описание ошибки:
			|%1';
			|en = 'Cannot delete the record key in the ""AR/AP reconciliation service found discrepancies"" register.
			|Error details:
			|%1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СервисСверкиРасчетов.ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, ,, ТекстОшибки);
		
	КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Конструктор таблицы с колонками, соответствующими измерениям регистра.
// Используется часто, в том числе при определении статусов задач, поэтому коллекция конструируется без обращения к метаданным.
// При изменении структуры регистра, включая состав типов измерений, следует вносить изменения в эту функцию.
//
Функция КлючиЗадач()
	
	КлючиЗадач = Новый ТаблицаЗначений;
	
	КлючиЗадач.Колонки.Добавить("Организация",   Новый ОписаниеТипов("СправочникСсылка.Организации"));
	КлючиЗадач.Колонки.Добавить("Контрагент",    Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	КлючиЗадач.Колонки.Добавить("Документ",      ОбщегоНазначения.ОписаниеТипаВсеСсылки());
	КлючиЗадач.Колонки.Добавить("Дата",          ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.Дата));
	КлючиЗадач.Колонки.Добавить("ИдентификаторСверкиПоОрганизации", Новый ОписаниеТипов("УникальныйИдентификатор"));
	КлючиЗадач.Колонки.Добавить("ИдентификаторСверкиКонтрагенту",   Новый ОписаниеТипов("УникальныйИдентификатор"));
	КлючиЗадач.Колонки.Добавить("ИдентификаторРасхождения",         ОбщегоНазначения.ОписаниеТипаСтрока(38));
	
	Возврат КлючиЗадач;
	
КонецФункции

Функция КлючиЗадачСтруктурой()
	
	КлючиЗадач = КлючиЗадач();
	КлючиЗадач.Добавить();
	
	Возврат ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(КлючиЗадач[0]);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиОбновления

Процедура ЗаполнитьИдентификаторыРасхождений(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Организация КАК Организация,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Контрагент КАК Контрагент,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Документ КАК Документ,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.Дата КАК Дата,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиПоОрганизации КАК ИдентификаторСверкиПоОрганизации,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторСверкиКонтрагенту КАК ИдентификаторСверкиКонтрагенту,
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения КАК ИдентификаторРасхождения
	|ИЗ
	|	РегистрСведений.СервисСверкиРасчетовОбнаруженныеРасхождения КАК СервисСверкиРасчетовОбнаруженныеРасхождения
	|ГДЕ
	|	СервисСверкиРасчетовОбнаруженныеРасхождения.ИдентификаторРасхождения = """"
	|
	|УПОРЯДОЧИТЬ ПО
	|	Дата УБЫВ";
	
	Результат = Запрос.Выполнить();
	
	Параметры.ОбработкаЗавершена = Ложь;
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ИмяПроцедуры = "РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения.ЗаполнитьИдентификаторыРасхождений()";
	ОбработаноЗаписей = 0;
	ПроблемныхЗаписей = 0;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			ОбработаноЗаписей = ОбработаноЗаписей + 1;
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Организация.Установить(Выборка.Организация);
			НаборЗаписей.Отбор.Контрагент.Установить(Выборка.Контрагент);
			НаборЗаписей.Отбор.Документ.Установить(Выборка.Документ);
			НаборЗаписей.Отбор.Дата.Установить(Выборка.Дата);
			НаборЗаписей.Отбор.ИдентификаторСверкиПоОрганизации.Установить(Выборка.ИдентификаторСверкиПоОрганизации);
			НаборЗаписей.Отбор.ИдентификаторСверкиКонтрагенту.Установить(Выборка.ИдентификаторСверкиКонтрагенту);
			
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				
				// Добавим постфикс для того чтобы отличать расхождение по организации и по контрагенту,
				// когда в одной базе ведется учет по сверяемым между собой организациям.
				// ОК - есть ИД сверки у организации и контрагента
				// О_ - есть ИД сверки только по организации (у контрагента нет документа)
				// _К - есть ИД сверки только по контрагенту (у организации нет документа)
				ПредварительныйИдентификаторРасхождения = "";
				Постфикс = "ОК";
				Если Строка(Запись.ИдентификаторСверкиПоОрганизации)  =  "00000000-0000-0000-0000-000000000000"  Тогда
					Постфикс = "_К"; // у организации нет документа
					ПредварительныйИдентификаторРасхождения = Строка(Запись.ИдентификаторСверкиКонтрагенту);
				Иначе
					Если Строка(Запись.ИдентификаторСверкиКонтрагенту) = "00000000-0000-0000-0000-000000000000" Тогда
						Постфикс = "О_"; // у контрагента нет документа
					КонецЕсли;
					ПредварительныйИдентификаторРасхождения = Строка(Запись.ИдентификаторСверкиПоОрганизации);
				КонецЕсли;
				Запись.ИдентификаторРасхождения = ПредварительныйИдентификаторРасхождения + Постфикс;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		Исключение
			
			ПроблемныхЗаписей = ПроблемныхЗаписей + 1;
			
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить идентификатор расхождения по причине:
				|%2';
				|en = 'The %1 procedure failed to set a discrepancy ID due to:
				|%2'"), ИмяПроцедуры, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения,
				,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПроблемныхЗаписей > 0 Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Процедуре %1 не удалось установить идентификаторы расхождений: в %2 из %3 возникли ошибки';
										|en = 'The %1 procedure failed to set discrepancy IDs: errors occurred in %2 out of %3'"),
			ИмяПроцедуры, ПроблемныхЗаписей, ОбработаноЗаписей);
		ВызватьИсключение ТекстСообщения;
		
	Иначе
		
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.СервисСверкиРасчетовОбнаруженныеРасхождения,,
			СтрШаблон(НСтр("ru = 'Процедура %1 обработала очередную порцию идентификаторов: %2 записей';
							|en = 'The %1 procedure processed another batch of IDs: %2 records.'"),
				ИмяПроцедуры, ОбработаноЗаписей));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

