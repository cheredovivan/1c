#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	
	СтруктураЗначений = Новый Структура;
	
	СтруктураЗначений.Вставить("ПлательщикНДС", ПолучитьФункциональнуюОпцию("ИспользоватьУчетНДС"));
	СтруктураЗначений.Вставить("РаздельныйУчетТоваровПоНалогообложениюНДС", Ложь);
	СтруктураЗначений.Вставить("РаздельныйУчетВНАПоНалогообложениюНДС", Ложь);
	СтруктураЗначений.Вставить("РаздельныйУчетПостатейныхПроизводственныхЗатратПоНалогообложениюНДС", Ложь);
	СтруктураЗначений.Вставить("ПериодичностьРаспределенияНДС", Перечисления.Периодичность.Квартал);
	СтруктураЗначений.Вставить("БазаНДС", Перечисления.ВидыБазыНДС.ВыручкаТекущегоКвартала);
	СтруктураЗначений.Вставить("ПравилоОтбораАвансовДляРегистрацииСчетовФактур", Перечисления.ПорядокРегистрацииСчетовФактурНаАванс.КромеЗачтенныхВТечениеДня);
	СтруктураЗначений.Вставить("УчетнаяПолитикаСуществует", Ложь);
	СтруктураЗначений.Вставить("ПериодичностьФормированияВычетовИВосстановленийНДС", Перечисления.Периодичность.Месяц);
	СтруктураЗначений.Вставить("РаспределятьНДСВМесяцеОсуществленияКапВложений", Ложь);
	СтруктураЗначений.Вставить("ФормироватьНДСПредъявленныйПриВключенииВСтоимость", Ложь);
	СтруктураЗначений.Вставить("СписыватьНДСПоРасходамНеПринимаемымВНУ", Ложь);  
	
	УчетНДСЛокализация.ДополнитьОписаниеНастроекУчетаНДСПоОрганизации(СтруктураЗначений);
	
	Возврат СтруктураЗначений;
	
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА Таблица.Период ЕСТЬ NULL Тогда
	|		ЛОЖЬ
	|	ИНАЧЕ
	|		ИСТИНА
	|	КОНЕЦ КАК УчетнаяПолитикаСуществует,
	|	Таблица.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ВЫБОР КОГДА ЕСТЬNULL(УчетнаяПолитикаФинансовогоУчета.МетодОценкиСтоимостиТоваров, НЕОПРЕДЕЛЕНО) =
	|		ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая) ТОГДА
	|			ИСТИНА
	|	ИНАЧЕ
	|		ЕСТЬNULL(Таблица.ФормироватьНДСПредъявленныйПриВключенииВСтоимость, ЛОЖЬ)
	|	КОНЕЦ КАК ФормироватьНДСПредъявленныйПриВключенииВСтоимость
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиУчетаНДС.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК Таблица
	|		ПО ГоловныеОрганизации.Организация = Таблица.Организация
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаФинансовогоУчета.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК УчетнаяПолитикаФинансовогоУчета
	|		ПО ГоловныеОрганизации.Организация = УчетнаяПолитикаФинансовогоУчета.Организация
	|";
	
	УчетНДСЛокализация.ДополнитьТекстЗапросаДействующиеПараметрыНДС(ТекстЗапроса);
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	ВыбираемыеПоля = СхемаЗапроса.ПакетЗапросов[0].Операторы[0].ВыбираемыеПоля;
	ОписаниеНастроек = ЗначенияПоУмолчанию(); 
	ОписаниеНастроек.Удалить("УчетнаяПолитикаСуществует");
	ОписаниеНастроек.Удалить("ФормироватьНДСПредъявленныйПриВключенииВСтоимость");
	
	Для каждого Настройка Из ОписаниеНастроек Цикл
		ВыбираемыеПоля.Добавить(СтрШаблон("Таблица.%1", Настройка.Ключ));
	КонецЦикла;
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	
	УчетНДСЛокализация.ДополнитьТекстЗапросаДействующиеПараметрыНДС(ТекстЗапроса, Истина);
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// 	Все - Булево - если Истина, то возвращает описание всех настроек.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено, Все = Ложь) Экспорт
	
	СтрокаОписанияНастроек = "";
	
	УчетНДСЛокализация.ДополнитьОписаниеНастроекУчетаНДС(Организация, ДатаДействия,СтрокаОписанияНастроек);
	Если ЗначениеЗаполнено(СтрокаОписанияНастроек) Тогда
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"НастройкиУчетаНДС",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДействующиеНастройки) Тогда
		СтрокаОписанияНастроек = НСтр("ru = 'Не заданы параметры.';
										|en = 'Parameters are not specified.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;

	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	МассивСтрокОписанияПараметров = Новый Массив;  
	Если ДействующиеНастройки.ПлательщикНДС Или Все Тогда
		МассивСтрокОписанияПараметров.Добавить(
			СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Организация является плательщиком НДС';
					|en = 'Company is a VAT payer'")));
		МассивСтрокОписанияПараметров.Добавить(
			СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Правило отбора авансов';
					|en = 'Rule of advance filter'"),
				ДействующиеНастройки.ПравилоОтбораАвансовДляРегистрацииСчетовФактур));
		Если ДействующиеНастройки.РаздельныйУчетТоваровПоНалогообложениюНДС Тогда
			МассивСтрокОписанияПараметров.Добавить(СтрШаблон(СтрокаШаблонБулево, НСтр("ru = 'Используется раздельный учет НДС';
																						|en = 'Separate VAT accounting is used'")));
		КонецЕсли;
	Иначе
		МассивСтрокОписанияПараметров.Добавить(СтрШаблон(СтрокаШаблонБулево, НСтр("ru = 'Организация не является плательщиком НДС';
																					|en = 'Company is not a VAT payer'")));
	КонецЕсли;
	
	УчетНДСЛокализация.ДополнитьОписаниеДействующихНастроекУчетаНДС(МассивСтрокОписанияПараметров, ДействующиеНастройки, Все);
	
	СтрокаОписанияНастроек = СтрСоединить(МассивСтрокОписанияПараметров, Символы.ВК);
	
	Возврат СтрокаОписанияНастроек
	
КонецФункции

// Возвращает параметры выбора статей и аналитик.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация, для которой выполняется настройка
//  Дата - Дата - Период, на который выполняется настройка
//  ВариантУчетаНДСПриИзмененииВидаДеятельности - ПеречислениеСсылка.ВариантыУчетаНДСПриИзмененииВидаДеятельностиНаНеоблагаемую - Вариант учета НДС
//  
// Возвращаемое значение:
//  Массив Из См. ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики.
//
Функция ПараметрыВыбораСтатейИАналитик(Организация, Дата, ВариантУчетаНДСПриИзмененииВидаДеятельности) Экспорт
	
	ИспользоватьУчетПрочихДоходовРасходов = ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов");
	ЭтоНеБазовая = ПолучитьФункциональнуюОпцию("НеБазоваяВерсия");
	ПараметрыСистемыНалогообложения = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"НастройкиСистемыНалогообложения", Организация, Дата, Истина);
	
	МассивПараметров = Новый Массив;
	
	#Область СтатьяРасходовНеНДС
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Запись";
	ПараметрыВыбора.Статья      = "СтатьяРасходовНеНДС";
	
	ПараметрыВыбора.ДоступностьПоОперации = ИспользоватьУчетПрочихДоходовРасходов;
	
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Истина;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовНеНДС";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяЗатратНеНДС");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовНеНДС");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	
	#КонецОбласти
	
	#Область СтатьяРасходовЕНВД

	Если ПараметрыСистемыНалогообложения = Неопределено Тогда
		ПрименяетсяЕНВД = Ложь;
	Иначе
		ПрименяетсяЕНВД = ПараметрыСистемыНалогообложения.ПрименяетсяЕНВД;
	КонецЕсли;
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Запись";
	ПараметрыВыбора.Статья      = "СтатьяРасходовЕНВД";
	
	ПараметрыВыбора.ДоступностьПоОперации = ПрименяетсяЕНВД
											И ИспользоватьУчетПрочихДоходовРасходов;
	
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Истина;
	
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовЕНВД";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяЗатратЕНВД");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовЕНВД");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	
	#КонецОбласти
	
	#Область СтатьяРасходовСписаниеНДС
	
	ПараметрыВыбора = ДоходыИРасходыСервер.ПараметрыВыбораСтатьиИАналитики();
	ПараметрыВыбора.ПутьКДанным = "Запись";
	ПараметрыВыбора.Статья      = "СтатьяРасходовСписаниеНДС";
	
	ПараметрыВыбора.ДоступностьПоОперации = ИспользоватьУчетПрочихДоходовРасходов И ЭтоНеБазовая;
	
	ПараметрыВыбора.СкрыватьСтатьюНедоступнуюПоОперации = Истина;
	 
	ПараметрыВыбора.ВыборСтатьиРасходов = Истина;
	ПараметрыВыбора.АналитикаРасходов = "АналитикаРасходовСписаниеНДС";
	
	ПараметрыВыбора.ЭлементыФормы.Статья.Добавить("СтатьяРасходовСписаниеНДС");
	ПараметрыВыбора.ЭлементыФормы.АналитикаРасходов.Добавить("АналитикаРасходовСписаниеНДС");
	
	МассивПараметров.Добавить(ПараметрыВыбора);
	
	#КонецОбласти
	
	Возврат МассивПараметров;
	
КонецФункции

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаЗаписи" Тогда
		Если ПолучитьФункциональнуюОпцию("ЛокализацияРФ") Тогда
			ВыбраннаяФорма = "ФормаЗаписиРФ";
			СтандартнаяОбработка = Ложь; 
		Иначе
			ВыбраннаяФорма = "ФормаЗаписи";
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Используется среднескользящая в периоде действия настроек НДС.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации
//  ПериодЗаписи - Дата
// 
// Возвращаемое значение:
//  Булево - Используется среднескользящая в периоде действия настроек учета НДС
Функция ИспользуетсяСреднескользящаяВПериодеДействияНастроекНДС(Организация, ПериодЗаписи) Экспорт
	
	ИспользуетсяСреднескользящая = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МИНИМУМ(НастройкиУчетаНДС.Период), ДАТАВРЕМЯ(3999, 12, 31)) КАК ДатаОкончания
		|ПОМЕСТИТЬ втПериодДействияНастроекУчетаНДС
		|ИЗ
		|	РегистрСведений.НастройкиУчетаНДС КАК НастройкиУчетаНДС
		|ГДЕ
		|	НастройкиУчетаНДС.Организация = &Организация
		|	И НастройкиУчетаНДС.Период > &ПериодЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(УчетнаяПолитикаФинансовогоУчета.МетодОценкиСтоимостиТоваров =
		|		ЗНАЧЕНИЕ(Перечисление.МетодыОценкиСтоимостиТоваров.Среднескользящая)), ЛОЖЬ) КАК ИспользуетсяСреднескользящая
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаФинансовогоУчета КАК УчетнаяПолитикаФинансовогоУчета
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПериодДействияНастроекУчетаНДС КАК ПериодДействия
		|		ПО (ИСТИНА)
		|ГДЕ
		|	УчетнаяПолитикаФинансовогоУчета.Организация = &Организация
		|	И УчетнаяПолитикаФинансовогоУчета.Период МЕЖДУ &ПериодЗаписи И ПериодДействия.ДатаОкончания";
	
	Запрос.УстановитьПараметр("ПериодЗаписи", ПериодЗаписи);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ИспользуетсяСреднескользящая = Выборка.ИспользуетсяСреднескользящая;
	КонецЕсли;
	
	Возврат ИспользуетсяСреднескользящая;
	
КонецФункции

#КонецОбласти 

#КонецЕсли