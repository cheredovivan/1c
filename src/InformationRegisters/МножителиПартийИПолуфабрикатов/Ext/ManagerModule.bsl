#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Процедура выполняет фоновое проведение
//
// Параметры:
//	ПараметрыРасчета - Структура - набор параметров для фонового проведения:
//		*Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - Ссылка на документ.
//	КалькуляцииКПроверке - Массив, Неопределено - ссылки на плановые калькуляции для проверки расчета. 
//
Процедура ВыполнитьПроведение(ПараметрыРасчета, КалькуляцииКПроверке = Неопределено) Экспорт
	
	ОпределитьПараметрыРасчета(ПараметрыРасчета);
	
	Если Не ПараметрыРасчета.ЭтоПроведение Тогда
		ОтменитьПроведениеДокумента(ПараметрыРасчета.Калькуляция, ПараметрыРасчета);
		Возврат;
	КонецЕсли;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РегистрСведений.МножителиПартийИПолуфабрикатов.РазузловатьПродукцию");
	ПараметрыРасчета.Вставить("Замер", Замер);
		
	Если ПараметрыРасчета.РеквизитыПК.Дата = Неопределено Тогда
		ЗаписатьСостояниеКалькуляции(ПараметрыРасчета.Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета);
		РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(ПараметрыРасчета.Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(ПараметрыРасчета.Калькуляция, 
			НСтр("ru = 'Фоновое задания расчета запустилось раньше фиксации транзакции записи документа. Требуется перепровести документ.';
				|en = 'Background job for calculation started before the document posting. Repost the document.'"));
		Возврат;
	КонецЕсли;
	
	ПровестиДокумент(ПараметрыРасчета);
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных, "ПровестиДокумент");
	ПараметрыРасчета.КоличествоДанных = 0;
	
	РассчитатьПотребности(ПараметрыРасчета);
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных, "РассчитатьПотребности");
	ПараметрыРасчета.КоличествоДанных = 0;
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		ОтменитьПроведениеДокумента(ПараметрыРасчета.Калькуляция, ПараметрыРасчета);
		Возврат;
	КонецЕсли;
		
	ЗаполнитьСтоимости(ПараметрыРасчета);
	ОценкаПроизводительности.ЗафиксироватьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных, "ЗаполнитьСтоимости");
	ПараметрыРасчета.КоличествоДанных = 0;
	
	ДобавитьКалькуляциюКПроверке(ПараметрыРасчета.Калькуляция, КалькуляцииКПроверке);
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		ОтменитьПроведениеДокумента(ПараметрыРасчета.Калькуляция, ПараметрыРасчета);
		Возврат;
	КонецЕсли;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, ПараметрыРасчета.КоличествоДанных);
	
	ДокументКПроведению = ДокументВОчереди(ПараметрыРасчета.Калькуляция);
	Если Не ДокументКПроведению = Неопределено Тогда
		ВыполнитьПроведение(Новый Структура("Калькуляция", ДокументКПроведению), КалькуляцииКПроверке);
	Иначе
		
		Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации("РегистрСведений.МножителиПартийИПолуфабрикатов.ПроверитьСтоимость");
		ПроверитьСтоимость(КалькуляцииКПроверке);
		
		Если КалькуляцииКПроверке = Неопределено Тогда
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, 1);
		Иначе
			ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КалькуляцииКПроверке.Количество());
		КонецЕсли;
		
	КонецЕсли;
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		ОтменитьПроведениеДокумента(ПараметрыРасчета.Калькуляция, ПараметрыРасчета);
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

// Записать состояние калькуляции.
// 
// Параметры:
//  Калькуляция - Произвольный, Неопределено, ДокументСсылка.ПлановаяКалькуляция2_2 - Калькуляция
//  Состояние - ПеречислениеСсылка.СостоянияРасчетаПлановойКалькуляции - Состояние
Процедура ЗаписатьСостояниеКалькуляции(Калькуляция, Состояние) Экспорт
	
	РегистрыСведений.СостоянияПлановыхКалькуляций.УстановитьСостояние(Калькуляция, Состояние);
	
КонецПроцедуры

// Процедура сворачивает таблицу значений множители
//
// Параметры:
//	Множители - ТаблицаЗначений - Таблица множители которую необходимо свернуть
//	ПараметрыРасчета - Структура - Содержит ключи:
//		* Множители_КолонкиГруппировки - Строка - Колонки по которым необходимо сгруппировать таблицу значений
//		* Множители_КолонкиСуммирования - Строка - Колонки по которым необходимо проссумировать значения в рамках группируемых колонок.
Процедура СвернутьМножителиВРамкахТЗ(Множители, ПараметрыРасчета) Экспорт
	
	Если Множители.Колонки.Найти("Приоритет") = Неопределено Тогда
		КолонкиГруппировок = ПараметрыРасчета.Множители_КолонкиГруппировкиБезПриоритета;
	Иначе
		КолонкиГруппировок = ПараметрыРасчета.Множители_КолонкиГруппировки;
	КонецЕсли;
	
	Множители.Свернуть(КолонкиГруппировок,
		ПараметрыРасчета.Множители_КолонкиСуммирования);
		
КонецПроцедуры

// Процедура очищает РС МножителиПартийИПолуфабрикатов по переданной плановой калькуляции
//
// Параметры:
//		Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - плановая калькуляция по которой необходимо удалить движения.
Процедура ОчиститьМножители(Калькуляция) Экспорт
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

// Функция проверяет отмену проведения, в случае отмены проведения во время расчета, расчет должен быть завершен.
//
// Параметры:
//		Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - плановая калькуляция по которой необходимо проверить отмену проведения.
//
// Возвращаемое значение:
//		Булево - по документу была отмена проведения.
Функция БылаОтменаПроведения(Калькуляция) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Очередь.Калькуляция КАК Калькуляция
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.ОтменаПроведения
		|	И Очередь.Калькуляция = &Калькуляция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

// Очищает движения документа по регистрам.
// Параметры:
//	ДокументОбъект - ДокументОбъект.ПлановаяКалькуляция2_2 - калькуляция для которой необходимо очистить движения.
//	ПараметрыРасчета - см. ОпределитьПараметрыРасчета
//
Процедура ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета) Экспорт
	
	ДокументСсылка = ДокументОбъект.Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ВложенныйЗапрос.Количество) КАК Количество
		|ИЗ
		|	(ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*) КАК Количество
		|	ИЗ
		|		РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|	ГДЕ
		|		ПлановыеМатериальныеЗатраты.Регистратор = &Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*)
		|	ИЗ
		|		РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|	ГДЕ
		|		ПлановыеТрудозатраты.Регистратор = &Регистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КОЛИЧЕСТВО(*)
		|	ИЗ
		|		РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|	ГДЕ
		|		ПлановыеПрочиеЗатраты.Регистратор = &Регистратор) КАК ВложенныйЗапрос";
	
	Запрос.УстановитьПараметр("Регистратор", ДокументСсылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + Выборка.Количество;
	КонецЕсли;
	
	Для Каждого НаборЗаписей Из ДокументОбъект.Движения Цикл
		
		НаборЗаписей1 = НаборЗаписей; // РегистрНакопленияНаборЗаписей
		НаборЗаписей1.Записать();
		
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	ОчиститьМножители(ДокументСсылка);
	
КонецПроцедуры

// Функция возвращает таблицу спецификаций по переданной номенклатуре
//
// Параметры:
//	СпособыПолученияМатериалов - ТаблицаЗначений - таблица с номенклатурой и характеристиками по которым необходимо 
//	получить спецификации.
//
// Возвращаемое значение:
//	ТаблицаЗначений - содержащая спецификации по необходимой номенклатуре.
Функция ПолучитьСпецификацииНоменклатуры(СпособыПолученияМатериалов) Экспорт
	
	ПараметрыВыбораСпецификаций = УправлениеДаннымиОбИзделияхКлиентСервер.ПараметрыВыбораСпецификацийНаИзготовлениеСборку();
	
	ПараметрыЗапроса = УправлениеДаннымиОбИзделиях.ПараметрыТекстаЗапросаСпецификацийИзделий();
	ПараметрыЗапроса.ТолькоПриоритетные = Истина;
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	Т.ИндексДанных                                            КАК ИндексДанных,
		|	Т.Подразделение                                           КАК ПодразделениеДиспетчер,
		|	Т.Номенклатура                                            КАК Номенклатура,
		|	Т.Характеристика                                          КАК Характеристика,
		|	Т.НачалоПроизводства                                      КАК НачалоПроизводства
		|ПОМЕСТИТЬ Материалы
		|ИЗ
		|	&Материалы КАК Т
		|;
		|
		|ВЫБРАТЬ
		|	Т.*,
		|	Т.Номенклатура.ВидНоменклатуры                            КАК ВидНоменклатуры,
		|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка) КАК НаправлениеДеятельности
		|ПОМЕСТИТЬ ВтСписокНоменклатуры
		|ИЗ
		|	Материалы КАК Т
		|;
		|";
	
	ТекстЗапроса = ТекстЗапроса + УправлениеДаннымиОбИзделиях.ТекстЗапросаСпецификацийИзделий(
																	ПараметрыЗапроса,
																	ПараметрыВыбораСпецификаций);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Материалы", СпособыПолученияМатериалов);
	
	УправлениеДаннымиОбИзделиях.УстановитьПараметрыЗапросаСпецификацийИзделий(Запрос, ПараметрыВыбораСпецификаций);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Результат.Индексы.Добавить("ИндексДанных");
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РасчетПотребностей

Процедура РассчитатьПотребности(ПараметрыРасчета)
	
	Калькуляция = ПараметрыРасчета.Калькуляция;
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьОшибкиВРасчете = Ложь;
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(Калькуляция);
		
		МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		ЕстьЗаписиВОчереди = ОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц);
				
		Если ЕстьЗаписиВОчереди Тогда
						
			ОбработатьОчередьРасчета(МенеджерВременныхТаблиц, ПараметрыРасчета);
		
			ОчиститьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыРасчета.ОшибкаРазузлования) Тогда
			
			ЕстьОшибкиВРасчете = Истина;
			ЗаписатьСостояниеКалькуляции(Калькуляция,
				Перечисления.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета);
			ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ПараметрыРасчета.ОшибкаРазузлования);
			
		КонецЕсли;	
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЕстьОшибкиВРасчете = Истина;
		ЗаписатьСостояниеКалькуляции(Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета);
		РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЕстьОшибкиВРасчете И ЕстьЗаданияВОчереди(Калькуляция) Тогда
		РассчитатьПотребности(ПараметрыРасчета);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОчередьРасчета(МенеджерВременныхТаблиц, ПараметрыРасчета)
	
	Движения = ИнициализироватьТаблицыДвижений();
	
	РазузловатьПродукцию(ПараметрыРасчета.Калькуляция, МенеджерВременныхТаблиц, Движения, ПараметрыРасчета);
	
	ЗаписатьДвижения(Движения, МенеджерВременныхТаблиц, ПараметрыРасчета);
		
КонецПроцедуры

// Создает наборы записей для формирования движений.
// Возвращаемое значение:
//	Структура - содержит наборы записей регистров, для формирования движений:
//		* ПлановыеМатериальныеЗатраты - ТаблицаЗначений - набор колонок регистра ПлановыеМатериальныеЗатраты.
//		* ПлановыеТрудозатраты - ТаблицаЗначений - набор колонок регистра ПлановыеТрудозатраты.
//		* ПлановыеПрочиеЗатраты - ТаблицаЗначений - набор колонок регистра ПлановыеПрочиеЗатраты.
//		* МножителиПартийИПолуфабрикатов - ТаблицаЗначений - набор колонок регистра МножителиПартийИПолуфабрикатов.
//
Функция ИнициализироватьТаблицыДвижений() Экспорт
	
	Регистры = Новый Массив;
	Регистры.Добавить("ПлановыеМатериальныеЗатраты");
	Регистры.Добавить("ПлановыеТрудозатраты");
	Регистры.Добавить("ПлановыеПрочиеЗатраты");
	Регистры.Добавить("МножителиПартийИПолуфабрикатов");
	
	Результат = Новый Структура;
	
	Для каждого Регистр Из Регистры Цикл
		
		Если Регистр = "МножителиПартийИПолуфабрикатов" Тогда
			Набор = РегистрыСведений[Регистр].СоздатьНаборЗаписей();
		Иначе	
			Набор = РегистрыНакопления[Регистр].СоздатьНаборЗаписей();
		КонецЕсли;
		
		Таблица = Набор.ВыгрузитьКолонки();
		Результат.Вставить(Регистр, Таблица);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Проведение

Процедура ПровестиДокумент(ПараметрыРасчета)
	
	Калькуляция = ПараметрыРасчета.Калькуляция;
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(Калькуляция);
		
		Очередь = ОчередьПроведения(Калькуляция);
		
		Если Очередь.ЕстьЗаписиВОчереди Тогда
			
			ДокументОбъект = Калькуляция.ПолучитьОбъект();
			
			ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета);
			
			СформироватьДвижения(ДокументОбъект);
			
			РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Калькуляция, Очередь.Записи);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьСостояниеКалькуляции(Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета);
		РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(Калькуляция, ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

Функция ОчередьПроведения(Калькуляция)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ОчередьРасчетаПлановыхКалькуляций.Калькуляция КАК Калькуляция,
		|	ОчередьРасчетаПлановыхКалькуляций.Разделитель КАК Разделитель
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК ОчередьРасчетаПлановыхКалькуляций
		|ГДЕ
		|	ОчередьРасчетаПлановыхКалькуляций.Калькуляция = &Калькуляция";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Результат = Новый Структура("ЕстьЗаписиВОчереди, Записи");
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Результат.ЕстьЗаписиВОчереди = Истина;
		Результат.Записи = РезультатЗапроса.Выгрузить();
		
	Иначе
		Результат.ЕстьЗаписиВОчереди = Ложь;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ОтменитьПроведениеДокумента(Калькуляция, ПараметрыРасчета)
	
	ДокументОбъект = Калькуляция.ПолучитьОбъект();
	ОчиститьДвижения(ДокументОбъект, ПараметрыРасчета);
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Калькуляция);
	ЗаписатьСостояниеКалькуляции(Калькуляция, 
		Перечисления.СостоянияРасчетаПлановойКалькуляции.НеРассчитана);
	
КонецПроцедуры

#КонецОбласти

#Область Разузлование

// Управление разузлованием продукции.
// Параметры:
//	Калькуляция - ДокументСсылка.ПлановаяКалькуляция2_2 - ссылка на документ к разузлованию.
//	МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - менеджер со служебными таблицами.
//	Движения - См. РегистрыСведений.МножителиПартийИПолуфабрикатов.ИнициализироватьТаблицыДвижений
//	ПараметрыРасчета - См. ОпределитьПараметрыРасчета
//
Процедура РазузловатьПродукцию(Калькуляция, МенеджерВременныхТаблиц, Движения, ПараметрыРасчета)
	
	Запрос = Новый Запрос(ТекстЗапросаОчередьРазузлованияПродукции());
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	УстановитьПривилегированныйРежим(Истина);
	Множители = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("Множители", Множители);
	СтруктураДанных.Вставить("МенеджерВременныхТаблиц", МенеджерВременныхТаблиц);
	СтруктураДанных.Вставить("Движения", Движения);
	ПараметрыРасчета.Вставить("СтруктураДанных", СтруктураДанных);	
	
	РазузловкаДинСтруктуры = ТребуетсяРазузловатьЗаказДинСтруктура(СтруктураДанных.МенеджерВременныхТаблиц,
		ПараметрыРасчета);
	РазузловкаСтатСтруктуры = ТребуетсяРазузловатьЗаказ(СтруктураДанных.МенеджерВременныхТаблиц);
	Если РазузловкаДинСтруктуры Или РазузловкаСтатСтруктуры Тогда
		ПлановаяКалькуляцияСтруктурыЗаказа.ОбработатьПродукциюПоЗаказу(ПараметрыРасчета);
	КонецЕсли;
		
	Если СтруктураДанных.Множители.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		Возврат;
	КонецЕсли;
		
	#Область РазузловкаЧерезСпецификации
	СтруктураДанных.Множители.Индексы.Добавить("ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Полуфабрикат, РасчетЗавершен");
	СтруктураДанных.Множители.Индексы.Добавить("ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, РасчетЗавершен");

	ПроверкаЦикла = 0;
	Пока Истина Цикл
		// Во время разузлования ПФ по спецификациям, могут быть добавлены дополнительные ПФ требующие разузлования.
		ПотребностиКРазузлованию = ПотребностиДляПолученияДанныхСпецификаций(СтруктураДанных.Множители, ПараметрыРасчета);
		Если ПотребностиКРазузлованию.Количество() = 0 Тогда
			СтруктураДанных.Множители.Индексы.Очистить();
			Движения.МножителиПартийИПолуфабрикатов = СтруктураДанных.Множители;
			Прервать;
		КонецЕсли;
		РазузловатьСпецификациюИзделий(ПараметрыРасчета, ПотребностиКРазузлованию);
		СтруктураДанных.Множители.Колонки.Удалить("ИндексДанных");
		ПривестиМножителиКОбщемуЗнаменателю(СтруктураДанных.Множители);
		
		ПроверкаЦикла = ПроверкаЦикла + 1;
		Если ПроверкаЦикла > 10000 Тогда
			Прервать; // Зациклилось.
		КонецЕсли;
	КонецЦикла;
	#КонецОбласти
	
КонецПроцедуры

Функция ТекстЗапросаОчередьРазузлованияПродукции()
	
	Возврат
		"ВЫБРАТЬ
		|	ОчередьРасчета.Калькуляция КАК Калькуляция,
		|	ОчередьРасчета.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция КАК Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение КАК Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ОчередьРасчета.КлючПартия КАК КлючПартия,
		|	ОчередьРасчета.Полуфабрикат КАК Полуфабрикат,
		|	ОчередьРасчета.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ОчередьРасчета.Разделитель КАК Разделитель,
		|	ОчередьРасчета.ДатаПотребности КАК Период,
		|	ОчередьРасчета.Порядок КАК Порядок,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПартияВыпущена, ЛОЖЬ) КАК ПартияВыпущена,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.РасчетЗавершен, ЛОЖЬ) КАК РасчетЗавершен, 
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Калькуляция.ПодразделениеДиспетчер, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)) КАК Подразделение, 
		// Если знаменатель совпадает, то необходимо проссумировать ресурсные колонки и числитель
		// при использовании РАЗЛИЧНЫЕ и совпадении числителя и знаменателя, останется только 1 числитель. 
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Знаменатель, 0) КАК Знаменатель,	
		|	ВЫБОР
		|		КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Приоритет,
		|   СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Числитель, 0)) КАК Числитель,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката, 0)) КАК КоличествоПолуфабриката,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Стоимость, 0)) КАК Стоимость,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьБезНДС, 0)) КАК СтоимостьБезНДС,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьРегл, 0)) КАК СтоимостьРегл,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Трудозатраты, 0)) КАК Трудозатраты,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ТрудозатратыРегл, 0)) КАК ТрудозатратыРегл,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные, 0)) КАК ПостатейныеПостоянные,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС, 0)) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл, 0)) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременные, 0)) КАК ПостатейныеПеременные,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС, 0)) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл, 0)) КАК ПостатейныеПеременныеРегл,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая, 0)) КАК СтоимостьЗабалансовая,
		|	СУММА(ЕСТЬNULL(МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл, 0)) КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		// Отбор данных требующих расчета.
		// Условия соединения должны совпадать с условиями в первом запросе процедуры ЗаписатьМножители
		|		ПО ОчередьРасчета.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|			И ОчередьРасчета.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции
		|			И ОчередьРасчета.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|			И ОчередьРасчета.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			И ОчередьРасчета.Назначение = МножителиПартийИПолуфабрикатов.Назначение
		|			И ОчередьРасчета.ПартияСпецификацияПолуфабриката = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката
		|			И ОчередьРасчета.КлючПартия = МножителиПартийИПолуфабрикатов.КлючПартия
		|			И ОчередьРасчета.Полуфабрикат = МножителиПартийИПолуфабрикатов.Полуфабрикат
		|			И ОчередьРасчета.ХарактеристикаПолуфабриката = МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
		|			И (ОчередьРасчета.ДатаПотребности = НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, ДЕНЬ))
		|			И ОчередьРасчета.Порядок = МножителиПартийИПолуфабрикатов.Порядок
		|
		|СГРУППИРОВАТЬ ПО
		|	ОчередьРасчета.Калькуляция,
		|	ОчередьРасчета.ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПолуфабриката,
		|	ОчередьРасчета.КлючПартия,
		|	ОчередьРасчета.Полуфабрикат,
		|	ОчередьРасчета.ХарактеристикаПолуфабриката,
		|	ОчередьРасчета.Разделитель,
		|	ОчередьРасчета.ДатаПотребности,
		|	ОчередьРасчета.Порядок,
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.ПартияВыпущена, ЛОЖЬ),
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.РасчетЗавершен, ЛОЖЬ), 
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Калькуляция.ПодразделениеДиспетчер, ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка)), 
		|	ЕСТЬNULL(МножителиПартийИПолуфабрикатов.Знаменатель, 0),
		|	ВЫБОР
		|		КОГДА МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ	
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
	
КонецФункции

Процедура РазузловатьСпецификациюИзделий(ПараметрыРасчета, ПотребностиКРазузлованию)
	
	СтруктураДанных = ПараметрыРасчета.СтруктураДанных;
	ЕстьПриоритет = СтруктураДанных.Множители.Колонки.Найти("Приоритет") <> Неопределено;
	
	// Разузлование спецификаций.
	ПараметрыВыборкиДанных = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных();
	ПараметрыВыборкиДанных.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
	ПараметрыВыборкиДанных.ОкруглятьКоличествоШтучныхТоваров = Ложь;
	ПараметрыВыборкиДанных.РассчитыватьДолиСтоимостиВыходныхИзделий = Истина;
	
	// Для разузлования ПФ, у которых в спецификации СпособПолучения = Обеспечивать
	// В регистре сведений "ВариантыОбеспеченияТоварами" должна быть запись для этого ПФ с способом обеспечения потребностей = "Собственное производство"
	// В функции ниже смотри запрос формирования в #Область МатериалыИУслуги
	// Разузлование будет работать, если в данной таблице, для ПФ, колонка Запланировать = Истина
	Потребности = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоСпискуНоменклатуры(
		ПотребностиКРазузлованию,
		ПараметрыВыборкиДанных);
	
	// Заполнение дат производства в зависимости от длительности этапов.
	// Одновременное выполнение этапов не поддерживается.
	ПериодКалькуляции = ПараметрыРасчета.РеквизитыПК.Дата;
	ЗаполнитьДатыПроизводства(Потребности, ПериодКалькуляции);

	РазузлованныеОбъекты = Новый ТаблицаЗначений;
	РазузлованныеОбъекты.Колонки.Добавить("Спецификация", Новый ОписаниеТипов("СправочникСсылка.РесурсныеСпецификации"));
	РазузлованныеОбъекты.Колонки.Добавить("КлючПартия", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(36)));
	
	КоличествоПолуфабрикатов = Потребности.Количество();
	Индекс = 0;
	
	// Обработка результата разузлования.
	// Расчет множителей полуфабрикатов.
	Пока Индекс < КоличествоПолуфабрикатов Цикл
		
		ДанныеСпецификации = Потребности[Индекс];
		
		// Основное изделие это структура
		УидХарактеристики = Строка(ДанныеСпецификации.ОсновноеИзделие.Характеристика.УникальныйИдентификатор());
		
		РасчетПланаПроизводства.ПроброситьПеремещения(ДанныеСпецификации.МатериалыИУслуги);
		
		ДолиСтоимости = ДанныеСпецификации.ВыходныеИзделия.Итог("ДоляСтоимости");
		
		Если ДолиСтоимости = 0 Тогда
			ДолиСтоимости = 1;
		КонецЕсли;
		
		Для Каждого ВыходноеИзделие Из ДанныеСпецификации.ВыходныеИзделия Цикл
				
			ДоляСтоимости = ?(ВыходноеИзделие.ДоляСтоимости = 0, 1, ВыходноеИзделие.ДоляСтоимости);
			
			ОтборПродукции = Новый Структура;
			ОтборПродукции.Вставить("ПартияСпецификацияПродукции", ДанныеСпецификации.Спецификация);
			Если ЗначениеЗаполнено(ВыходноеИзделие.Номенклатура) Тогда
				
				ОтборПродукции.Вставить("Продукция", ВыходноеИзделие.Номенклатура);
				Если Не ВыходноеИзделие.ЛюбаяХарактеристика Тогда
					ОтборПродукции.Вставить("ХарактеристикаПродукции", ВыходноеИзделие.Характеристика);
				КонецЕсли;
				
			Иначе
				
				ОтборПродукции.Вставить("Продукция", 
					Справочники.Номенклатура.ПустаяСсылка());
				ОтборПродукции.Вставить("ХарактеристикаПродукции", 
					Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
				
			КонецЕсли;
			ОтборПродукции.Вставить("Полуфабрикат", Справочники.Номенклатура.ПустаяСсылка());
			ОтборПродукции.Вставить("РасчетЗавершен", Ложь);
				
			СтрокиМножителей = СтруктураДанных.Множители.НайтиСтроки(ОтборПродукции);
			
			Если СтрокиМножителей.Количество() = 0 Тогда
				
				// Выходное изделие является полуфабрикатом.
				ОтборПолуфабриката = Новый Структура;
				ОтборПолуфабриката.Вставить("ПартияСпецификацияПолуфабриката", 	ДанныеСпецификации.Спецификация);
				Если ЗначениеЗаполнено(ВыходноеИзделие.Номенклатура) Тогда
					
					ОтборПолуфабриката.Вставить("Полуфабрикат",					ВыходноеИзделие.Номенклатура);
					ОтборПолуфабриката.Вставить("ХарактеристикаПолуфабриката",	ВыходноеИзделие.Характеристика);
					
				Иначе
					
					ОтборПолуфабриката.Вставить("Полуфабрикат", 
						Справочники.Номенклатура.ПустаяСсылка());
					ОтборПолуфабриката.Вставить("ХарактеристикаПолуфабриката", 
						Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка());
						
				КонецЕсли;
				ОтборПолуфабриката.Вставить("РасчетЗавершен", Ложь);
				
				СтрокиМножителей = СтруктураДанных.Множители.НайтиСтроки(ОтборПолуфабриката);
				
			Иначе
				
				Для Каждого СтрокаПродукции Из СтрокиМножителей Цикл
					СтрокаПродукции.Период = ДанныеСпецификации.Окончание;
				КонецЦикла;
				
			КонецЕсли;
			
			Для Каждого Множитель Из СтрокиМножителей Цикл
				
				Множитель.Числитель = Множитель.КоличествоПолуфабриката * ДоляСтоимости;
				Множитель.Знаменатель = ДолиСтоимости * ВыходноеИзделие.Количество;
				// Можем сюда попасть после расчета дин. структуры,
				// поэтому КлючПартия может отличатся от УИД характеристики, обновляем значение.
				Множитель.КлючПартия = ?(ЗначениеЗаполнено(Множитель.Полуфабрикат),
					Строка(Множитель.ХарактеристикаПолуфабриката.УникальныйИдентификатор()),
					Строка(Множитель.ХарактеристикаПродукции.УникальныйИдентификатор()));
				Множитель.РасчетЗавершен = Истина;
				
				Для Каждого СтрокаМатериала Из ДанныеСпецификации.МатериалыИУслуги Цикл
					
					Если Не (СтрокаМатериала.Запланировать ИЛИ СтрокаМатериала.Производится) Тогда
						Продолжить;
					КонецЕсли;
					
					НовыйМножитель = СтруктураДанных.Множители.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйМножитель, Множитель, "ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, Калькуляция");
					
					Если СтрокаМатериала.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизвестиПоСпецификации Тогда
						НовыйМножитель.ПартияСпецификацияПолуфабриката = СтрокаМатериала.Спецификация;
					Иначе
						НовыйМножитель.ПартияСпецификацияПолуфабриката = Неопределено;
					КонецЕсли;
					НовыйМножитель.Подразделение = ПараметрыРасчета.РеквизитыПК.ПодразделениеДиспетчер;
					
					НовыйМножитель.Период = ДанныеСпецификации.Начало;
					НовыйМножитель.Полуфабрикат = СтрокаМатериала.Номенклатура;
					НовыйМножитель.ХарактеристикаПолуфабриката = СтрокаМатериала.Характеристика;
					НовыйМножитель.КлючПартия = Строка(СтрокаМатериала.Характеристика.УникальныйИдентификатор());
					НовыйМножитель.Порядок = Множитель.Порядок + 1;
					
					ОтборПозиций = Новый Структура("Период, ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение, ПартияСпецификацияПолуфабриката, Полуфабрикат, ХарактеристикаПолуфабриката, Порядок");	
					ЗаполнитьЗначенияСвойств(ОтборПозиций, НовыйМножитель);
					ОтборПозиций.Вставить("КлючПартия", НовыйМножитель.КлючПартия);
					ОтборПозиций.Вставить("РасчетЗавершен", Истина);
					РассчитанныеПозиции = СтруктураДанных.Множители.НайтиСтроки(ОтборПозиций);
					Для Каждого РассчитаннаяПозиция Из РассчитанныеПозиции Цикл
						
						Если НовыйМножитель.Порядок > РассчитаннаяПозиция.Порядок Тогда
							Продолжить;
						КонецЕсли;
						
						НовыйМножитель.Порядок = РассчитаннаяПозиция.Порядок + 1;
						
					КонецЦикла;
					
					НовыйМножитель.Числитель =  1;
					НовыйМножитель.Знаменатель = 1;
					
					НовыйМножитель.Стоимость = 0;
					НовыйМножитель.СтоимостьБезНДС = 0;
					НовыйМножитель.СтоимостьРегл = 0;
					НовыйМножитель.Трудозатраты = 0;
					НовыйМножитель.ТрудозатратыРегл = 0;
					НовыйМножитель.ПостатейныеПостоянные = 0;
					НовыйМножитель.ПостатейныеПостоянныеБезНДС = 0;
					НовыйМножитель.ПостатейныеПостоянныеРегл = 0;
					НовыйМножитель.ПостатейныеПеременные = 0;
					НовыйМножитель.ПостатейныеПеременныеБезНДС = 0;
					НовыйМножитель.ПостатейныеПеременныеРегл = 0;
					НовыйМножитель.СтоимостьЗабалансовая = 0;
					НовыйМножитель.СтоимостьЗабалансоваяРегл = 0;
					Если ЕстьПриоритет Тогда
						НовыйМножитель.Приоритет = ?(ЗначениеЗаполнено(НовыйМножитель.Полуфабрикат), 1, 0);
					КонецЕсли;
					
					НовыйМножитель.РасчетЗавершен = Ложь;
					НовыйМножитель.ПартияВыпущена = Ложь;
					
					НовыйМножитель.КоличествоПолуфабриката = Окр(Множитель.Числитель * СтрокаМатериала.Количество / Множитель.Знаменатель, 7);
					
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЦикла;
					
		СпецификацияСХарактеристикой = Новый Структура("Спецификация, КлючПартия",
			ДанныеСпецификации.Спецификация, УидХарактеристики);
		Если РазузлованныеОбъекты.НайтиСтроки(СпецификацияСХарактеристикой).Количество() > 0 Тогда
			
			Индекс = Индекс + 1;
			Продолжить;
			
		КонецЕсли;
		
		ПараметрыРасчетаПериода = ПараметрыРасчетаПериода();
		ПараметрыРасчетаПериода.Период = НачалоДня(ДанныеСпецификации.Начало);
		ПараметрыРасчетаПериода.Этапы = ДанныеСпецификации.Этапы;
		ПараметрыРасчетаПериода.ДлительностиЭтапов = ДанныеСпецификации.Этапы.ВыгрузитьКолонку("ДлительностьЭтапа");
		
		ЭтапыНаСтороне = ДанныеСпецификации.Этапы.НайтиСтроки(Новый Структура("ПроизводствоНаСтороне", Истина));
		Для Каждого ЭтапНаСтороне Из ЭтапыНаСтороне Цикл
			
			ПлановыеМатериальныеЗатраты = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты; // ТаблицаЗначений
			
			ПараметрыРасчетаПериода.ТекущийЭтап = ЭтапНаСтороне.Этап;
			ПериодЗатратЭтапа = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
			Для Каждого ТекУслуга Из ЭтапНаСтороне.УслугиПереработчика Цикл
				
				НоваяУслуга = ПлановыеМатериальныеЗатраты.Добавить();
				НоваяУслуга.Период 				= ПериодКалькуляции;
				НоваяУслуга.ПартияСпецификация 	= ДанныеСпецификации.Спецификация;
				НоваяУслуга.Подразделение 		= ЭтапНаСтороне.Подразделение;
				НоваяУслуга.СтатьяКалькуляции 	= ТекУслуга.СтатьяКалькуляции;
				НоваяУслуга.Номенклатура 		= ТекУслуга.Номенклатура;
				НоваяУслуга.Характеристика 		= ТекУслуга.Характеристика;
				НоваяУслуга.Количество 			= ТекУслуга.Количество;
				НоваяУслуга.ПериодЗатрат 		= ПериодЗатратЭтапа;
				НоваяУслуга.КлючПартия 			= УидХарактеристики;
				
			КонецЦикла;
			
		КонецЦикла;
		
		ПроизводимыеПолуфабрикаты = ДанныеСпецификации.МатериалыИУслуги.СкопироватьКолонки("Номенклатура,Характеристика,СтатьяКалькуляции,Этап");
		Для Каждого СтрокаМатериала Из ДанныеСпецификации.МатериалыИУслуги Цикл
			
			ПлановыеМатериальныеЗатраты = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты; // ТаблицаЗначений
			НовыйМатериал = ПлановыеМатериальныеЗатраты.Добавить();
			НовыйМатериал.КлючПартия = УидХарактеристики;
			НовыйМатериал.Период = ПериодКалькуляции;
			НовыйМатериал.ПартияСпецификация = ДанныеСпецификации.Спецификация;
			НовыйМатериал.Подразделение = СтрокаМатериала.ПодразделениеЭтапа;
			НовыйМатериал.СтатьяКалькуляции = СтрокаМатериала.СтатьяКалькуляции;
			НовыйМатериал.Номенклатура = СтрокаМатериала.Номенклатура;
			НовыйМатериал.Характеристика = СтрокаМатериала.Характеристика;
			НовыйМатериал.Количество = СтрокаМатериала.КоличествоУпаковок
											* СтрокаМатериала.ДанныеУпаковки.Числитель / СтрокаМатериала.ДанныеУпаковки.Знаменатель;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаМатериала.Этап;
			НовыйМатериал.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
			Если СтрокаМатериала.Запланировать ИЛИ СтрокаМатериала.Производится Тогда
				НовыйМатериал.ЭтоПолуфабрикат = Истина;
				Если СтрокаМатериала.СпособПолученияМатериала = Перечисления.СпособыПолученияМатериаловВСпецификации.ПроизводитсяНаЭтапе Тогда
					НовыйПолуфабрикат = ПроизводимыеПолуфабрикаты.Добавить();
					НовыйПолуфабрикат.Номенклатура = СтрокаМатериала.Номенклатура;
					НовыйПолуфабрикат.Характеристика = СтрокаМатериала.Характеристика;
					НовыйПолуфабрикат.СтатьяКалькуляции = СтрокаМатериала.СтатьяКалькуляции;
					НовыйПолуфабрикат.Этап = СтрокаМатериала.ИсточникПолученияПолуфабриката;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		ПроизводимыеПолуфабрикаты.Индексы.Добавить("Номенклатура,Характеристика,СтатьяКалькуляции,Этап");
		
		Для Каждого СтрокаТрудозатрат Из ДанныеСпецификации.Трудозатраты Цикл
			
			ПлановыеТрудозатраты = СтруктураДанных.Движения.ПлановыеТрудозатраты; // ТаблицаЗначений
			НоваяТрудозатрата = ПлановыеТрудозатраты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяТрудозатрата, СтрокаТрудозатрат);
			НоваяТрудозатрата.КлючПартия = УидХарактеристики;
			НоваяТрудозатрата.Период = ПериодКалькуляции;
			НоваяТрудозатрата.ПартияСпецификация = ДанныеСпецификации.Спецификация;
			НоваяТрудозатрата.Подразделение = СтрокаТрудозатрат.ПодразделениеЭтапа;
			НоваяТрудозатрата.Количество = СтрокаТрудозатрат.Количество;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаТрудозатрат.Этап;
			НоваяТрудозатрата.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
		КонецЦикла;
		
		Для Каждого СтрокаМатериала Из ДанныеСпецификации.ВозвратныеОтходы Цикл
			
			ПлановыеМатериальныеЗатраты = СтруктураДанных.Движения.ПлановыеМатериальныеЗатраты; // ТаблицаЗначений
			НоваяСтрока = ПлановыеМатериальныеЗатраты.Добавить();
			НоваяСтрока.КлючПартия = УидХарактеристики;
			НоваяСтрока.Период = ПериодКалькуляции;
			НоваяСтрока.ПартияСпецификация = ДанныеСпецификации.Спецификация;
			НоваяСтрока.Подразделение = СтрокаМатериала.ПодразделениеЭтапа;
			НоваяСтрока.СтатьяКалькуляции = СтрокаМатериала.СтатьяКалькуляции;
			НоваяСтрока.Номенклатура = СтрокаМатериала.Номенклатура;
			НоваяСтрока.Характеристика = СтрокаМатериала.Характеристика;
			НоваяСтрока.Количество = -СтрокаМатериала.КоличествоУпаковок
											* СтрокаМатериала.ДанныеУпаковки.Числитель / СтрокаМатериала.ДанныеУпаковки.Знаменатель;
			
			ПараметрыРасчетаПериода.ТекущийЭтап = СтрокаМатериала.Этап;
			НоваяСтрока.ПериодЗатрат = ПолучитьПериодЗатрат(ПараметрыРасчетаПериода);
			
			УсловияПоиска = Новый Структура("Номенклатура,Характеристика,СтатьяКалькуляции,Этап", 
				СтрокаМатериала.Номенклатура, СтрокаМатериала.Характеристика, СтрокаМатериала.СтатьяКалькуляции, СтрокаМатериала.Этап);
			Если ПроизводимыеПолуфабрикаты.НайтиСтроки(УсловияПоиска).Количество() > 0 Тогда
				НоваяСтрока.ЭтоПолуфабрикат = Истина;
			КонецЕсли;
			
		КонецЦикла;		
		
		НоваяСтрока = РазузлованныеОбъекты.Добавить();
		НоваяСтрока.Спецификация = СпецификацияСХарактеристикой.Спецификация; 
		НоваяСтрока.КлючПартия = УидХарактеристики;
		
		Индекс = Индекс + 1;
		
	КонецЦикла;
	
КонецПроцедуры

// Определяет потребности в разузловании полуфабрикатов.
// Параметры:
//	Изделия - ТаблицаЗначений - таблица полуфабрикатов к разузлованию.
//	ПараметрыРасчета - см. ОпределитьПараметрыРасчета
Функция ПотребностиДляПолученияДанныхСпецификаций(Изделия, ПараметрыРасчета)
	
	Результат = Справочники.РесурсныеСпецификации.СписокНоменклатуры();
	
	Изделия.Колонки.Добавить("ИндексДанных", Новый ОписаниеТипов("Число"));
	ОбщегоНазначенияУТ.ПронумероватьТаблицуЗначений(Изделия, "ИндексДанных");
	
	Полуфабрикаты = Изделия.Скопировать(
		Новый Структура("ПартияСпецификацияПолуфабриката", Неопределено), 
		"ИндексДанных, Полуфабрикат, ХарактеристикаПолуфабриката, Подразделение");
	КолонкаПолуфабрикат = Полуфабрикаты.Колонки.Полуфабрикат; // КолонкаТаблицыЗначений
	КолонкаПолуфабрикат.Имя = "Номенклатура";
	
	КолонкаХарактеристикаПолуфабриката = Полуфабрикаты.Колонки.ХарактеристикаПолуфабриката; // КолонкаТаблицыЗначений
	КолонкаХарактеристикаПолуфабриката.Имя = "Характеристика";
	
	Полуфабрикаты.Колонки.Добавить("НачалоПроизводства", Новый ОписаниеТипов("Дата"));
	Полуфабрикаты.ЗаполнитьЗначения(ПараметрыРасчета.РеквизитыПК.Дата, "НачалоПроизводства");
	
	Индекс = Полуфабрикаты.Количество() - 1;
	Пока Индекс > 0 Цикл
		
		Если Не ЗначениеЗаполнено(Полуфабрикаты[Индекс].Номенклатура) Тогда
			Полуфабрикаты.Удалить(Индекс);
		КонецЕсли;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
	СпецификацииИзделий = ПолучитьСпецификацииНоменклатуры(Полуфабрикаты);
	ОтборСпецификаций = Новый Структура("ИндексДанных");
	
	Изделия.Индексы.Добавить("Продукция, ХарактеристикаПродукции");
	
	Для Каждого Изделие Из Изделия Цикл
		
		Если Изделие.РасчетЗавершен Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяПотребность = Результат.Добавить();
		
		Если ЗначениеЗаполнено(Изделие.Полуфабрикат) Тогда
			
			НоваяПотребность.Номенклатура = Изделие.Полуфабрикат;
			НоваяПотребность.Характеристика = Изделие.ХарактеристикаПолуфабриката;
			НоваяПотребность.Спецификация = Изделие.ПартияСпецификацияПолуфабриката;
			
			Если Не ЗначениеЗаполнено(НоваяПотребность.Спецификация) Тогда
				
				ПФВИсходныхДанных = Изделия.НайтиСтроки(Новый Структура("Продукция, ХарактеристикаПродукции", Изделие.Полуфабрикат, Изделие.ХарактеристикаПолуфабриката));
				
				Если ПФВИсходныхДанных.Количество() > 0 Тогда
					НоваяПотребность.Спецификация = ПФВИсходныхДанных[0].ПартияСпецификацияПродукции;
				Иначе
					
					ОтборСпецификаций.ИндексДанных = Изделие.ИндексДанных;
					
					СпецификацииИзделия = СпецификацииИзделий.НайтиСтроки(ОтборСпецификаций);
					
					Если СпецификацииИзделия.Количество() > 0 Тогда
						НоваяПотребность.Спецификация = СпецификацииИзделия[0].Спецификация;
					КонецЕсли;
					
				КонецЕсли;
					
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(НоваяПотребность.Спецификация) Тогда
				
				Изделие.РасчетЗавершен = Истина;
				Результат.Удалить(НоваяПотребность);
				Продолжить;
				
			Иначе
				Изделие.ПартияСпецификацияПолуфабриката = НоваяПотребность.Спецификация;
			КонецЕсли;
			
		Иначе
			
			НоваяПотребность.Номенклатура = Изделие.Продукция;
			НоваяПотребность.Характеристика = Изделие.ХарактеристикаПродукции;
			НоваяПотребность.Спецификация = Изделие.ПартияСпецификацияПродукции;
			
		КонецЕсли;
		
		НоваяПотребность.НачалоПроизводства     = Изделие.Период;
		НоваяПотребность.ПодразделениеДиспетчер = Изделие.Подразделение;
		
		ДанныеОсновногоИзделия = Справочники.РесурсныеСпецификации.ДанныеОсновногоИзделияСпецификации(НоваяПотребность.Спецификация,
			НоваяПотребность.Номенклатура,
			НоваяПотребность.Характеристика);
			
		НоваяПотребность.Количество = ДанныеОсновногоИзделия.Количество;
		
	КонецЦикла;
	
	Возврат Результат;
	
Конецфункции

Процедура ЗаполнитьДатыПроизводства(Потребности, ПериодКалькуляции)
	
	Индекс = Потребности.Количество() - 1;
	
	ОбщаяДлительность = 0;
	Пока Индекс >= 0 Цикл
		
		ДанныеСпецификации = Потребности[Индекс]; // Структура
		ДанныеСпецификации.Вставить("ДатаВыпуска", ПериодКалькуляции);
		ДанныеСпецификации.Вставить("Начало");
		ДанныеСпецификации.Вставить("Окончание");
		ДанныеСпецификации.Вставить("РазмещениеВыпуска", Перечисления.СпособыПривязкиОперацийПроизводства.ПустаяСсылка());
		
		ТекущаяДлительность = 0;
		Для Каждого Этап Из ДанныеСпецификации.Этапы Цикл
			ТекущаяДлительность = ТекущаяДлительность + Этап.ДлительностьЭтапаВДнях;
		КонецЦикла;
		
		Если ДанныеСпецификации.РазмещениеВыпуска = Перечисления.СпособыПривязкиОперацийПроизводства.КНачалу Тогда
			
			ДанныеСпецификации.Начало = ДанныеСпецификации.ДатаВыпуска + ОбщаяДлительность;
			ДанныеСпецификации.Окончание = ДанныеСпецификации.Начало + ТекущаяДлительность * 86400;
			
		Иначе
			
			ДанныеСпецификации.Окончание = ПериодКалькуляции;
			ДанныеСпецификации.Начало = ПериодКалькуляции;
			
		КонецЕсли;
		
		ОбщаяДлительность = ОбщаяДлительность + ТекущаяДлительность * 86400;
		
		Индекс = Индекс - 1;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСДвижениями

Процедура СформироватьДвижения(ДокументОбъект)
	
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.ДополнительныеСвойства = ДокументОбъект.ДополнительныеСвойства;
	
	ТаблицыДляДвижений = ПроведениеДокументов.ДанныеДокументаДляПроведения(ДокументОбъект.Ссылка, ,
		ДопПараметры);
	
	ЗаписатьМножителиДокумента(ДокументОбъект.Ссылка, ТаблицыДляДвижений);
	
КонецПроцедуры

Процедура ЗаписатьМножители(ДополнительныеСвойства, ПараметрыРасчета, МенеджерВременныхТаблиц)
	
	Множители = Неопределено;
	
	Если Не ДополнительныеСвойства.Свойство("МножителиПартийИПолуфабрикатов", Множители)
		И Не ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("Таблица" + "МножителиПартийИПолуфабрикатов", Множители) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Стоимость КАК Стоимость,
		|	МножителиПартийИПолуфабрикатов.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	МножителиПартийИПолуфабрикатов.СтоимостьРегл КАК СтоимостьРегл,
		|	МножителиПартийИПолуфабрикатов.Трудозатраты КАК Трудозатраты,
		|	МножителиПартийИПолуфабрикатов.ТрудозатратыРегл КАК ТрудозатратыРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОчередьРасчета КАК ОчередьРасчета
		// Отбор ранее рассчитанных данных, для дозаполнения ТЗ Множители.
		// Условия соединения должны совпадать с запросом в функции ТекстЗапросаОчередьРазузлованияПродукции
		|		ПО ОчередьРасчета.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция
		|			И ОчередьРасчета.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции
		|			И ОчередьРасчета.Продукция = МножителиПартийИПолуфабрикатов.Продукция
		|			И ОчередьРасчета.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции
		|			И ОчередьРасчета.Назначение = МножителиПартийИПолуфабрикатов.Назначение
		|			И ОчередьРасчета.ПартияСпецификацияПолуфабриката = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката
		|			И ОчередьРасчета.КлючПартия = МножителиПартийИПолуфабрикатов.КлючПартия
		|			И ОчередьРасчета.Полуфабрикат = МножителиПартийИПолуфабрикатов.Полуфабрикат
		|			И ОчередьРасчета.ХарактеристикаПолуфабриката = МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката
		|			И (ОчередьРасчета.ДатаПотребности = НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, ДЕНЬ))
		|			И ОчередьРасчета.Порядок = МножителиПартийИПолуфабрикатов.Порядок
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|		И МножителиПартийИПолуфабрикатов.РасчетЗавершен
		|		И ОчередьРасчета.Калькуляция ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Множители.Индексы.Очистить();
	
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Множители.Добавить(), Выборка);
	КонецЦикла;
	
	СвернутьМножители(Множители);
	
	ПривестиМножителиКОбщемуЗнаменателю(Множители);
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(ПараметрыРасчета.Калькуляция);
	
	СвернутьМножителиВРамкахТЗ(Множители, ПараметрыРасчета);
	НаборЗаписей.Загрузить(Множители);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
	ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + Множители.Количество();
	
КонецПроцедуры

Процедура СвернутьМножители(Множители)
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Множители", Множители);
	Запрос.Текст = "ВЫБРАТЬ
	|	Множители.Период КАК Период,
	|	Множители.Калькуляция КАК Калькуляция,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Множители.Назначение КАК Назначение,
	|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	Множители.КлючПартия КАК КлючПартия,
	|	Множители.Полуфабрикат КАК Полуфабрикат,
	|	Множители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	Множители.Порядок КАК Порядок,
	|	Множители.Знаменатель КАК Знаменатель,
	|	Множители.РасчетЗавершен КАК РасчетЗавершен,
	|	Множители.ПартияВыпущена КАК ПартияВыпущена,
	|	Множители.Числитель КАК Числитель,
	|	Множители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
	|	Множители.Стоимость КАК Стоимость,
	|	Множители.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	Множители.СтоимостьРегл КАК СтоимостьРегл,
	|	Множители.Трудозатраты КАК Трудозатраты,
	|	Множители.ТрудозатратыРегл КАК ТрудозатратыРегл,
	|	Множители.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
	|	Множители.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
	|	Множители.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
	|	Множители.ПостатейныеПеременные КАК ПостатейныеПеременные,
	|	Множители.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
	|	Множители.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
	|	Множители.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	Множители.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
	|ПОМЕСТИТЬ Множители
	|ИЗ
	|	&Множители КАК Множители
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Калькуляция,
	|	ПартияСпецификацияПродукции,
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	Назначение,
	|	ПартияСпецификацияПолуфабриката,
	|	Полуфабрикат,
	|	ХарактеристикаПолуфабриката
	|;
	|
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(Множители.Период) КАК Период,
	|	Множители.Калькуляция КАК Калькуляция,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Множители.Назначение КАК Назначение,
	|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	Множители.КлючПартия КАК КлючПартия,
	|	Множители.Полуфабрикат КАК Полуфабрикат,
	|	Множители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	МАКСИМУМ(Множители.Порядок) КАК Порядок,
	|	Множители.ПартияВыпущена КАК ПартияВыпущена,  
	|	Множители.Знаменатель КАК Знаменатель,
	|	МАКСИМУМ(Множители.РасчетЗавершен) КАК РасчетЗавершен,
	|	СУММА(Множители.Числитель) КАК Числитель,
	|	СУММА(Множители.КоличествоПолуфабриката) КАК КоличествоПолуфабриката,
	|	СУММА(Множители.Стоимость) КАК Стоимость,
	|	СУММА(Множители.СтоимостьБезНДС) КАК СтоимостьБезНДС,
	|	СУММА(Множители.СтоимостьРегл) КАК СтоимостьРегл,
	|	СУММА(Множители.Трудозатраты) КАК Трудозатраты,
	|	СУММА(Множители.ТрудозатратыРегл) КАК ТрудозатратыРегл,
	|	СУММА(Множители.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
	|	СУММА(Множители.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДС,
	|	СУММА(Множители.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРегл,
	|	СУММА(Множители.ПостатейныеПеременные) КАК ПостатейныеПеременные,
	|	СУММА(Множители.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДС,
	|	СУММА(Множители.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРегл,
	|	СУММА(Множители.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
	|	СУММА(Множители.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл
	|	
	|ИЗ
	|	Множители КАК Множители
	|
	|СГРУППИРОВАТЬ ПО
	|	Множители.Калькуляция,
	|	Множители.ПартияСпецификацияПродукции,
	|	Множители.Продукция,
	|	Множители.ХарактеристикаПродукции,
	|	Множители.Назначение,
	|	Множители.ПартияСпецификацияПолуфабриката,
	|	Множители.Полуфабрикат,
	|	Множители.ХарактеристикаПолуфабриката,
	|	Множители.КлючПартия,
	|	Множители.Знаменатель,
	|	Множители.ПартияВыпущена";
	
	Множители = Запрос.Выполнить().Выгрузить();
КонецПроцедуры

Процедура ЗаписатьМножителиДокумента(Калькуляция, ТаблицыДляДвижений)
	
	Множители = Неопределено;
	
	Если Не ТаблицыДляДвижений.Свойство("Таблица" + "МножителиПартийИПолуфабрикатов", Множители) Тогда
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Калькуляция.Установить(Калькуляция);
	
	НаборЗаписей.Загрузить(Множители);
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ЗаписатьДвижения(Движения, МенеджерВременныхТаблиц, ПараметрыРасчета)
	
	АктуальныеДвижения = АктуальныеДвиженияОчереди(ПараметрыРасчета.Калькуляция, МенеджерВременныхТаблиц);
	
	ДокументОбъект = ПараметрыРасчета.Калькуляция.ПолучитьОбъект();
	
	Для Каждого КлючИЗначение Из Движения Цикл
			
		ИмяРегистра = КлючИЗначение.Ключ;
		
		Если ИмяРегистра = "МножителиПартийИПолуфабрикатов" Тогда
			Продолжить;
		КонецЕсли;
		
		ДвиженияПоРегистру = ДокументОбъект.Движения[ИмяРегистра];
		
		ТаблицаДвижений = АктуальныеДвижения[ИмяРегистра];
		ДвиженияПоРегистру.Загрузить(ТаблицаДвижений);
		
		ТаблицаДвижений.Свернуть("ПартияСпецификация");
		ПартииСпецификации = ТаблицаДвижений.ВыгрузитьКолонку("ПартияСпецификация");
		
		Для Каждого Строка Из КлючИЗначение.Значение Цикл
			
			Если Не ПартииСпецификации.Найти(Строка.ПартияСпецификация) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Запись = ДвиженияПоРегистру.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			Запись.Активность = Истина;
			Запись.Калькуляция = ПараметрыРасчета.Калькуляция;
			
		КонецЦикла;
		
		ДвиженияПоРегистру.Записывать = Истина;
		
	КонецЦикла;
	
	ЗаписатьМножители(Движения, ПараметрыРасчета, МенеджерВременныхТаблиц);
	
	УстановитьПривилегированныйРежим(Истина);
	ДокументОбъект.Движения.Записать();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция АктуальныеДвиженияОчереди(Калькуляция, МенеджерВременныхТаблиц)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Период,
		|	ПлановыеМатериальныеЗатраты.Регистратор,
		|	ПлановыеМатериальныеЗатраты.НомерСтроки,
		|	ПлановыеМатериальныеЗатраты.Активность,
		|	ПлановыеМатериальныеЗатраты.Калькуляция,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеМатериальныеЗатраты.Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции,
		|	ПлановыеМатериальныеЗатраты.Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат,
		|	ПлановыеМатериальныеЗатраты.ПериодЗатрат,
		|	ПлановыеМатериальныеЗатраты.Количество,
		|	ПлановыеМатериальныеЗатраты.Стоимость,
		|	ПлановыеМатериальныеЗатраты.СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьРегл		
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|ГДЕ
		|	ПлановыеМатериальныеЗатраты.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеТрудозатраты.Период,
		|	ПлановыеТрудозатраты.Регистратор,
		|	ПлановыеТрудозатраты.НомерСтроки,
		|	ПлановыеТрудозатраты.Активность,
		|	ПлановыеТрудозатраты.Калькуляция,
		|	ПлановыеТрудозатраты.ПартияСпецификация,
		|	ПлановыеТрудозатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеТрудозатраты.Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции,
		|	ПлановыеТрудозатраты.ВидРабот,
		|	ПлановыеТрудозатраты.ПериодЗатрат,
		|	ПлановыеТрудозатраты.Количество,
		|	ПлановыеТрудозатраты.Стоимость,
		|	ПлановыеТрудозатраты.СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|ГДЕ
		|	ПлановыеТрудозатраты.Регистратор = &Регистратор
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеПрочиеЗатраты.Период,
		|	ПлановыеПрочиеЗатраты.Регистратор,
		|	ПлановыеПрочиеЗатраты.НомерСтроки,
		|	ПлановыеПрочиеЗатраты.Активность,
		|	ПлановыеПрочиеЗатраты.Калькуляция,
		|	ПлановыеПрочиеЗатраты.ПартияСпецификация,
		|	ПлановыеПрочиеЗатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеПрочиеЗатраты.Подразделение,
		|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
		|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
		|	ПлановыеПрочиеЗатраты.АналитикаРасходов,
		|	ПлановыеПрочиеЗатраты.Стоимость,
		|	ПлановыеПрочиеЗатраты.СтоимостьБезНДС,
		|	ПлановыеПрочиеЗатраты.СтоимостьРегл
		|ИЗ
		|	РегистрНакопления.ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|ГДЕ
		|	ПлановыеПрочиеЗатраты.Регистратор = &Регистратор";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Регистратор", Калькуляция);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПлановыеМатериальныеЗатраты", МассивРезультатов[0].Выгрузить());
		
	Результат.Вставить("ПлановыеТрудозатраты", МассивРезультатов[1].Выгрузить());
	
	Результат.Вставить("ПлановыеПрочиеЗатраты", МассивРезультатов[2].Выгрузить());
		
	Возврат Результат;
	
КонецФункции

Процедура ОчиститьОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОчередьРасчета.Калькуляция,
		|	ОчередьРасчета.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ОчередьРасчета.Продукция,
		|	ОчередьРасчета.ХарактеристикаПродукции,
		|	ОчередьРасчета.Назначение КАК Назначение,
		|	ОчередьРасчета.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	ОчередьРасчета.КлючПартия КАК КлючПартия,
		|	ОчередьРасчета.Полуфабрикат,
		|	ОчередьРасчета.ХарактеристикаПолуфабриката,
		|	ОчередьРасчета.Разделитель,
		|	ОчередьРасчета.Порядок,
		|	ОчередьРасчета.ДатаПотребности
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК ОчередьРасчетаПлановыхКалькуляций
		|		ПО ОчередьРасчета.Калькуляция = ОчередьРасчетаПлановыхКалькуляций.Калькуляция
		|			И ОчередьРасчета.ПартияСпецификацияПродукции = ОчередьРасчетаПлановыхКалькуляций.ПартияСпецификацияПродукции
		|			И ОчередьРасчета.Продукция = ОчередьРасчетаПлановыхКалькуляций.Продукция
		|			И ОчередьРасчета.ХарактеристикаПродукции = ОчередьРасчетаПлановыхКалькуляций.ХарактеристикаПродукции
		|			И ОчередьРасчета.Назначение = ОчередьРасчетаПлановыхКалькуляций.Назначение
		|			И ОчередьРасчета.ПартияСпецификацияПолуфабриката = ОчередьРасчетаПлановыхКалькуляций.ПартияСпецификацияПолуфабриката
		|			И ОчередьРасчета.КлючПартия = ОчередьРасчетаПлановыхКалькуляций.КлючПартия
		|			И ОчередьРасчета.Полуфабрикат = ОчередьРасчетаПлановыхКалькуляций.Полуфабрикат
		|			И ОчередьРасчета.ХарактеристикаПолуфабриката = ОчередьРасчетаПлановыхКалькуляций.ХарактеристикаПолуфабриката
		|			И ОчередьРасчета.Разделитель = ОчередьРасчетаПлановыхКалькуляций.Разделитель
		|			И ОчередьРасчета.ДатаПотребности = ОчередьРасчетаПлановыхКалькуляций.ДатаПотребности
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ОчередьРасчета";
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	Очередь = МассивРезультатов[0].Выгрузить();
	
	РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(Калькуляция, Очередь);
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Процедура ЗаблокироватьДанные(Калькуляция)
	
	Блокировка = Новый БлокировкаДанных();
	
	ДобавитьБлокировкуРегистрМножителиПартийИПолуфабрикатов(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеМатериальныеЗатраты(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеТрудозатраты(Блокировка, Калькуляция);
	ДобавитьБлокировкуРегистрПлановыеПрочиеЗатраты(Блокировка, Калькуляция);
	
	Блокировка.Заблокировать();
	
КонецПроцедуры

Процедура СвернутьРегистрыЗатратВРамкахТЗ(СтруктураДанных)
	СтруктураДанных.ПлановыеМатериальныеЗатраты.Свернуть("Период,Калькуляция,ПартияСпецификация,Подразделение,
		|СтатьяКалькуляции,Номенклатура,Характеристика,КлючПартия,ЭтоПолуфабрикат,ПериодЗатрат",
		"Количество,Стоимость,СтоимостьБезНДС,СтоимостьРегл,СтоимостьЗабалансовая,СтоимостьЗабалансоваяРегл");

	СтруктураДанных.ПлановыеПрочиеЗатраты.Свернуть("Период,Калькуляция,ПартияСпецификация,Подразделение,СтатьяКалькуляции,
		|СтатьяРасходов,КлючПартия",
		"Стоимость,СтоимостьБезНДС,СтоимостьРегл");

	СтруктураДанных.ПлановыеТрудозатраты.Свернуть("Период,Калькуляция,ПартияСпецификация,Подразделение,СтатьяКалькуляции,
		|ВидРабот,КлючПартия,ПериодЗатрат",
		"Количество,Стоимость,СтоимостьРегл");
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрМножителиПартийИПолуфабрикатов(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.МножителиПартийИПолуфабрикатов");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеМатериальныеЗатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеМатериальныеЗатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеТрудозатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеТрудозатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Процедура ДобавитьБлокировкуРегистрПлановыеПрочиеЗатраты(Блокировка, Калькуляция)
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ПлановыеПрочиеЗатраты");
	ЭлементБлокировки.УстановитьЗначение("Калькуляция", Калькуляция);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
КонецПроцедуры

Функция ОчередьРасчета(Калькуляция, МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РС_Очередь.Калькуляция КАК Калькуляция,
		|	РС_Очередь.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	РС_Очередь.Продукция КАК Продукция,
		|	РС_Очередь.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	РС_Очередь.Назначение КАК Назначение,
		|	РС_Очередь.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	РС_Очередь.КлючПартия КАК КлючПартия,
		|	РС_Очередь.Полуфабрикат КАК Полуфабрикат,
		|	РС_Очередь.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	РС_Очередь.ДатаПотребности КАК ДатаПотребности,
		|	РС_Очередь.Порядок КАК Порядок,
		|	МАКСИМУМ(РС_Очередь.Разделитель) КАК Разделитель
		|ПОМЕСТИТЬ ОчередьРасчета
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК РС_Очередь
		|ГДЕ
		|	РС_Очередь.Калькуляция = &Калькуляция
		|	И НЕ РС_Очередь.ОтменаПроведения
		|
		|СГРУППИРОВАТЬ ПО
		|	РС_Очередь.ДатаПотребности,
		|	РС_Очередь.ХарактеристикаПолуфабриката,
		|	РС_Очередь.Полуфабрикат,
		|	РС_Очередь.ПартияСпецификацияПолуфабриката,
		|	РС_Очередь.КлючПартия,
		|	РС_Очередь.Назначение,
		|	РС_Очередь.ХарактеристикаПродукции,
		|	РС_Очередь.Продукция,
		|	РС_Очередь.ПартияСпецификацияПродукции,
		|	РС_Очередь.Порядок,
		|	РС_Очередь.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОчередьРасчета.Калькуляция КАК Калькуляция
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета";
		
	УстановитьПривилегированныйРежим(Истина);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЕстьЗаписиВОчереди = НЕ МассивРезультатов[1].Пустой();
	
	Возврат ЕстьЗаписиВОчереди;
	
КонецФункции

Функция ДокументВОчереди(Калькуляция)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Очередь.Калькуляция КАК Калькуляция,
		|	МАКСИМУМ(Очередь.ОтменаПроведения) КАК ОтменаПроведения
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Продукция = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	И Очередь.Калькуляция = &Калькуляция
		|
		|СГРУППИРОВАТЬ ПО
		|	Очередь.Калькуляция
		|
		|ИМЕЮЩИЕ
		|	МАКСИМУМ(Очередь.ОтменаПроведения) = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Калькуляция;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ЕстьЗаданияВОчереди(Калькуляция)
	
	Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Очередь.Разделитель КАК Разделитель
		|ИЗ
		|	РегистрСведений.ОчередьРасчетаПлановыхКалькуляций КАК Очередь
		|ГДЕ
		|	Очередь.Калькуляция = &Калькуляция
		|	И НЕ Очередь.ОтменаПроведения");
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат (НЕ Запрос.Выполнить().Пустой());
	
КонецФункции

Функция ТребуетсяРазузловатьЗаказДинСтруктура(МенеджерВременныхТаблиц, ПараметрыРасчета)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьЗаказыСДинСтруктурой
	|ИЗ
	|	Документ.ПлановаяКалькуляция2_2.ОбъектыКалькуляции КАК ДД
	|ГДЕ
	|	ДД.Ссылка = &Калькуляция
	|	И ИСТИНА В (ВЫБРАТЬ ПЕРВЫЕ 1 ИСТИНА 
	|					ИЗ Документ.ЗаказНаПроизводство2_2 КАК РеквизитыЗаказаНаПроизводство
	|					ГДЕ РеквизитыЗаказаНаПроизводство.Ссылка = ДД.Объект
	|					И РеквизитыЗаказаНаПроизводство.ДинамическаяСтруктура)";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Функция ТребуетсяРазузловатьЗаказ(МенеджерВременныхТаблиц)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ОчередьРасчета.Продукция
		|ИЗ
		|	ОчередьРасчета КАК ОчередьРасчета
		|ГДЕ
		|	ТИПЗНАЧЕНИЯ(ОчередьРасчета.ПартияСпецификацияПродукции) = ТИП(Документ.ЭтапПроизводства2_2)
		|	И ОчередьРасчета.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

Процедура АктуализироватьСостояниеКалькуляций(Калькуляции)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СостоянияПлановыхКалькуляций.Калькуляция КАК Калькуляция
		|ИЗ
		|	РегистрСведений.СостоянияПлановыхКалькуляций КАК СостоянияПлановыхКалькуляций
		|ГДЕ
		|	СостоянияПлановыхКалькуляций.Калькуляция В(&Калькуляции)
		|	И НЕ СостоянияПлановыхКалькуляций.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета)";
	
	Запрос.УстановитьПараметр("Калькуляции", Калькуляции);
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗаписатьСостояниеКалькуляции(Выборка.Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.РассчитанаСОшибками);
	КонецЦикла;
	
КонецПроцедуры

Функция ЗначениеВМассив(Значение)
	
	МассивЭлементов = Новый Массив();
	МассивЭлементов.Добавить(Значение);
	
	Возврат МассивЭлементов;
	
КонецФункции

Процедура ЗаписатьВЖурналОшибкуРассчета(Калькуляция, Причина)
	
	ТекстСообщения = НСтр("ru = 'Не удалось выполнить проведение документа %Ссылка%  по причине: %Причина%';
							|en = 'Cannot post document %Ссылка%. Reason: %Причина%'");
	
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Ссылка%", Калькуляция);
	ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", Причина);
		
	ЗаписьЖурналаРегистрации(
		СобытиеЖурналаРегистрацииОшибкаРасчета(),
		УровеньЖурналаРегистрации.Ошибка,
		Калькуляция.Метаданные(),
		Калькуляция,
		ТекстСообщения);
	
КонецПроцедуры

Функция СобытиеЖурналаРегистрацииОшибкаРасчета() Экспорт
	
	Возврат НСтр("ru = 'Фоновое проведение калькуляции продукции.';
				|en = 'Background posting of product costing.'", ОбщегоНазначения.КодОсновногоЯзыка());
	
КонецФункции

// Инициализирует параметры расчета.
//
// Параметры:
//  ПараметрыРасчета - Структура - первичная структура с параметрами расчета.
//  Замер - Соответствие - описание в ОценкаПроизводительности.НачатьЗамерДлительнойОперации.
//
// Возвращаемое значение:
//  Структура - содержит параметры расчета:
//   * РеквизитыПК - Соответствие - соответствие с значениями реквизитов плановой калькуляции.
//   * Замер - см. ОценкаПроизводительности.НачатьЗамерДлительнойОперации.
//   * КоличествоДанных - Число - показатель количества обработанных данных для замера.
//   * ОшибкаРазузлования - Строка - описание ошибок возникших в процессе разузлования.
//   * НеРассчитанныйПериодПоОрганизации - Дата - Дата не рассчитанного периода по организации плановой калькуляции.
//
Функция ОпределитьПараметрыРасчета(ПараметрыРасчета)
	
	СписокРеквизитовПК = "Дата,ПодразделениеДиспетчер,ВидЦены,Организация";
	
	РеквизитыПК = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыРасчета.Калькуляция, 
		СписокРеквизитовПК);
	
	// Запуск фонового задания расчета может произойти раньше фиксации транзакции записи документа плановой калькуляции,
	// чтобы избежать ошибки получения некорректных данных расчета реализован циклический повтор запроса данных
	// с секундной паузой между попытками и ограничением на количество попыток (60 раз)
	КоличествоПопытокЧтения = 60;
	Пока КоличествоПопытокЧтения > 0 И РеквизитыПК.Дата = Неопределено Цикл
		ОбщегоНазначенияБТС.Пауза(1);
		РеквизитыПК = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ПараметрыРасчета.Калькуляция,
			СписокРеквизитовПК);
		КоличествоПопытокЧтения = КоличествоПопытокЧтения - 1;
	КонецЦикла;
	
	РеквизитыПК.Вставить("ДатаКалькуляции", КонецДня(РеквизитыПК.Дата));
	РеквизитыПК.Вставить("НачалоМесяцаДатыКалькуляции", НачалоМесяца(РеквизитыПК.Дата));
	ПараметрыРасчета.Вставить("РеквизитыПК", РеквизитыПК);
	
	ВидЦеныПлановойСтоимостиТМЦ = Справочники.ВидыЦен.ВидЦеныПлановойСтоимостиТМЦ();
	ПараметрыРасчета.Вставить("ВидЦеныПлановойСтоимостиТМЦ", ВидЦеныПлановойСтоимостиТМЦ);
	ПараметрыРасчета.Вставить("ВалютаЦеныПлановойСтоимостиТМЦ",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВидЦеныПлановойСтоимостиТМЦ, "ВалютаЦены"));
		
	ПараметрыРасчета.Вставить("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	ПараметрыРасчета.Вставить("УчитыватьСебестоимостьТоваровПоНазначениям",
		РасчетСебестоимостиПовтИсп.СебестоимостьТоваровПоНазначениям());
	
	ПараметрыРасчета.Вставить("КоличествоДанных", 0);
	ПараметрыРасчета.Вставить("ОшибкаРазузлования", "");
	ПараметрыРасчета.Вставить("СобытиеЖурналаРегистрацииОшибкаРасчета", СобытиеЖурналаРегистрацииОшибкаРасчета());
	ПараметрыРасчета.Вставить("ОшибкаРазузлования", "");
	ПараметрыРасчета.Вставить("ПустойУИД", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	ПараметрыРасчета.Вставить("ПустойУИДСтрока", Строка(ПараметрыРасчета.ПустойУИД));
	ПараметрыРасчета.Вставить("НеРассчитанныйПериодПоОрганизации",
		РегистрыСведений.ЗаданияКРасчетуСебестоимости.ПолучитьНеРассчитанныйПериодПоОрганизации(РеквизитыПК.Организация));
	
	ПараметрыРасчета.Вставить("Множители_КолонкиГруппировкиБезПриоритета",
		"Период,Калькуляция,ПартияСпецификацияПродукции,Продукция,ХарактеристикаПродукции,
		|Назначение,ПартияСпецификацияПолуфабриката,КлючПартия,Полуфабрикат,ХарактеристикаПолуфабриката,
		|РасчетЗавершен,ПартияВыпущена,Знаменатель,Порядок");
	ПараметрыРасчета.Вставить("Множители_КолонкиГруппировки",
		ПараметрыРасчета.Множители_КолонкиГруппировкиБезПриоритета + ",Приоритет");
	ПараметрыРасчета.Вставить("Множители_КолонкиСуммирования",
		"Числитель,КоличествоПолуфабриката,Стоимость,СтоимостьБезНДС,СтоимостьРегл,Трудозатраты,
		|ТрудозатратыРегл,ПостатейныеПостоянные,ПостатейныеПостоянныеБезНДС,ПостатейныеПостоянныеРегл,
		|ПостатейныеПеременные,ПостатейныеПеременныеБезНДС,ПостатейныеПеременныеРегл,СтоимостьЗабалансовая,
		|СтоимостьЗабалансоваяРегл");
		
	Возврат ПараметрыРасчета;
	
КонецФункции

#КонецОбласти

#Область Стоимости

Процедура ЗаполнитьСтоимости(ПараметрыРасчета)
	
	Если БылаОтменаПроведения(ПараметрыРасчета.Калькуляция) Тогда
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	
	Попытка
		
		ЗаблокироватьДанные(ПараметрыРасчета.Калькуляция);
		
		МенеджерВТ = Новый МенеджерВременныхТаблиц;
		
		ПодготовитьСлужебныеВременныеТаблицы(МенеджерВТ, ПараметрыРасчета.Калькуляция);
		// Расчет стоимости материалов.
		ЗаполнитьСтоимостьМатериалов(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		// Расчет стоимости трудозатрат.
		ЗаполнитьСтоимостьТрудозатрат(ПараметрыРасчета.Калькуляция, МенеджерВТ);
		
		РассчитатьПредварительнуюСтоимость(ПараметрыРасчета, МенеджерВТ);
		
		// Расчет постатейных расходов.
		ЗаполнитьСтоимостьПостатейных(ПараметрыРасчета, МенеджерВТ);
		
		РассчитатьПредварительнуюСтоимость(ПараметрыРасчета, МенеджерВТ);

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	Материалы.Период,
			|	Материалы.ПериодЗатрат,
			|	Материалы.Калькуляция,
			|	Материалы.ПартияСпецификация,
			|	Материалы.Подразделение,
			|	Материалы.СтатьяКалькуляции,
			|	Материалы.КлючПартия,
			|	Материалы.Номенклатура,
			|	Материалы.Характеристика,
			|	Материалы.ЭтоПолуфабрикат,
			|	Материалы.Количество,
			|	Материалы.Стоимость
			|		+ Материалы.Трудозатраты
			|		+ Материалы.ПостатейныеПостоянные
			|		+ Материалы.ПостатейныеПеременные КАК Стоимость,
			|	Материалы.СтоимостьБезНДС
			|		+ Материалы.Трудозатраты
			|		+ Материалы.ПостатейныеПостоянныеБезНДС
			|		+ Материалы.ПостатейныеПеременныеБезНДС КАК СтоимостьБезНДС,
			|	Материалы.СтоимостьРегл
			|		+ Материалы.ТрудозатратыРегл
			|		+ Материалы.ПостатейныеПостоянныеРегл
			|		+ Материалы.ПостатейныеПеременныеРегл КАК СтоимостьРегл,
			|	Материалы.СтоимостьЗабалансовая,
			|	Материалы.СтоимостьЗабалансоваяРегл
			|ИЗ
			|	МатериальныеЗатратыВключаяПолуфабрикаты КАК Материалы
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПлановыеТрудозатраты.Период,
			|	ПлановыеТрудозатраты.ПериодЗатрат,
			|	ПлановыеТрудозатраты.Калькуляция,
			|	ПлановыеТрудозатраты.ПартияСпецификация,
			|	ПлановыеТрудозатраты.КлючПартия,
			|	ПлановыеТрудозатраты.Подразделение,
			|	ПлановыеТрудозатраты.СтатьяКалькуляции,
			|	ПлановыеТрудозатраты.ВидРабот,
			|	ПлановыеТрудозатраты.Количество,
			|	ПлановыеТрудозатраты.Стоимость,
			|	ПлановыеТрудозатраты.СтоимостьРегл
			|ИЗ
			|	ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПлановыеПрочиеЗатраты.Период,
			|	ПлановыеПрочиеЗатраты.Калькуляция,
			|	ПлановыеПрочиеЗатраты.ПартияСпецификация,
			|	ПлановыеПрочиеЗатраты.КлючПартия,
			|	ПлановыеПрочиеЗатраты.Подразделение,
			|	ПлановыеПрочиеЗатраты.СтатьяКалькуляции,
			|	ПлановыеПрочиеЗатраты.СтатьяРасходов,
			|	ПлановыеПрочиеЗатраты.Стоимость,
			|	ПлановыеПрочиеЗатраты.СтоимостьБезНДС,
			|	ПлановыеПрочиеЗатраты.СтоимостьРегл
			|ИЗ
			|	ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
			|ГДЕ
			|	ПлановыеПрочиеЗатраты.Стоимость <> 0
			|	ИЛИ ПлановыеПрочиеЗатраты.СтоимостьБезНДС <> 0 
			|	ИЛИ ПлановыеПрочиеЗатраты.СтоимостьРегл <> 0
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Множители.Период,
			|	Множители.Продукция,
			|	Множители.ПартияСпецификацияПродукции,
			|	Множители.Калькуляция,
			|	Множители.ХарактеристикаПродукции,
			|	Множители.Назначение,
			|	Множители.Стоимость,
			|	Множители.СтоимостьБезНДС,
			|	Множители.СтоимостьРегл,
			|	Множители.СтоимостьЗабалансовая,
			|	Множители.СтоимостьЗабалансоваяРегл,
			|	Множители.Трудозатраты,
			|	Множители.ТрудозатратыРегл,
			|	Множители.ПостатейныеПеременные,
			|	Множители.ПостатейныеПостоянныеБезНДС,
			|	Множители.ПостатейныеПостоянныеРегл,
			|	Множители.ПостатейныеПостоянные,
			|	Множители.ПостатейныеПеременныеБезНДС,
			|	Множители.ПостатейныеПеременныеРегл,
			|	Множители.ПартияСпецификацияПолуфабриката,
			|	Множители.КлючПартия,
			|	Множители.Полуфабрикат,
			|	Множители.ХарактеристикаПолуфабриката,
			|	Множители.Порядок,
			|	Множители.Числитель,
			|	Множители.Знаменатель,
			|	Множители.КоличествоПолуфабриката,
			|	Множители.РасчетЗавершен,
			|	Множители.ПартияВыпущена
			|ИЗ
			|	МножителиПартийИПолуфабрикатов КАК Множители";
		
		Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		ДокументОбъект = ПараметрыРасчета.Калькуляция.ПолучитьОбъект();
		
		СтруктураДанных = Новый Структура();
		СтруктураДанных.Вставить("МножителиПартийИПолуфабрикатов", МассивРезультатов[3].Выгрузить());
		СтруктураДанных.Вставить("ПлановыеМатериальныеЗатраты", МассивРезультатов[0].Выгрузить());
		СтруктураДанных.Вставить("ПлановыеТрудозатраты", МассивРезультатов[1].Выгрузить());
		СтруктураДанных.Вставить("ПлановыеПрочиеЗатраты", МассивРезультатов[2].Выгрузить());
		РассчитатьМножителиПоФактическимДанным(СтруктураДанных, ПараметрыРасчета);
		
		ДокументОбъект.Движения.ПлановыеМатериальныеЗатраты.Загрузить(СтруктураДанных.ПлановыеМатериальныеЗатраты);
		ДокументОбъект.Движения.ПлановыеТрудозатраты.Загрузить(СтруктураДанных.ПлановыеТрудозатраты);
		ДокументОбъект.Движения.ПлановыеПрочиеЗатраты.Загрузить(СтруктураДанных.ПлановыеПрочиеЗатраты);
		
		ДокументОбъект.Движения.ПлановыеМатериальныеЗатраты.Записывать = Истина;
		ДокументОбъект.Движения.ПлановыеТрудозатраты.Записывать = Истина;
		ДокументОбъект.Движения.ПлановыеПрочиеЗатраты.Записывать = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		ДокументОбъект.Движения.Записать();
		УстановитьПривилегированныйРежим(Ложь);
		
		ТаблицыДляДвижений = Новый Структура("Таблица" + "МножителиПартийИПолуфабрикатов",
			СтруктураДанных.МножителиПартийИПолуфабрикатов);
		ЗаписатьМножителиДокумента(ПараметрыРасчета.Калькуляция, ТаблицыДляДвижений);
		
		Для Каждого НаборЗаписей Из ДокументОбъект.Движения Цикл
			ПараметрыРасчета.КоличествоДанных = ПараметрыРасчета.КоличествоДанных + НаборЗаписей.Количество();
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ЗаписатьСостояниеКалькуляции(ПараметрыРасчета.Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета);
		РегистрыСведений.ОчередьРасчетаПлановыхКалькуляций.ОчиститьОчередь(ПараметрыРасчета.Калькуляция);
		ЗаписатьВЖурналОшибкуРассчета(ПараметрыРасчета.Калькуляция,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьСтоимостьМатериалов(Калькуляция, МенеджерВТ)
	
	ПолучитьЦеныНоменклатуры(Калькуляция, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Калькуляция, "ВидЦены, Дата, Организация");
		
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.УстановитьПараметр("ВидЦены", ЗначенияРеквизитов.ВидЦены);
	Запрос.УстановитьПараметр("Период", ЗначенияРеквизитов.Дата);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ЗначенияРеквизитов.Организация));
	Запрос.УстановитьПараметр("СтранаРегистрации", ЗначениеНастроекКлиентСерверПовтИсп.СтранаРегистрацииОрганизации(ЗначенияРеквизитов.Организация));
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ПОМЕСТИТЬ ВалютыНоменклатуры
		|ИЗ
		|	ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВалютыНоменклатуры.Валюта КАК Валюта,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.КурсЧислитель, 0) КАК КурсЧислитель,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.КурсЗнаменатель, 0) КАК КурсЗнаменатель
		|ПОМЕСТИТЬ КурсыВалютНоменклатуры
		|ИЗ
		|	ВалютыНоменклатуры КАК ВалютыНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Валюты.Валюта КАК Валюта
		|					ИЗ
		|						ВалютыНоменклатуры КАК Валюты)
		|					И БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютСрезПоследних
		|		ПО ВалютыНоменклатуры.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПлановыеМатериальныеЗатраты.Калькуляция КАК Калькуляция,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	ПлановыеМатериальныеЗатраты.Характеристика КАК Характеристика,
		|	ПлановыеМатериальныеЗатраты.Количество КАК Количество,
		|	ПлановыеМатериальныеЗатраты.Период КАК Период,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.Стоимость = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.Стоимость = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.Стоимость
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0) * ВЫБОР
		|				КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютУУ.Валюта
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютУУ.КурсЧислитель = 0
		|							ТОГДА 1
		|						ИНАЧЕ КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютУУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель)
		|					КОНЕЦ
		|			КОНЕЦ * ВЫБОР
		|				КОГДА Количество > 0
		|					И ПлановыеМатериальныеЗатраты.Стоимость = -1
		|					ТОГДА 1
		|				КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|					ТОГДА 1
		|				КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|					ТОГДА 1
		|				КОГДА ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА (ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))).Ставка > 0
		|							ТОГДА ((ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))).Ставка + 100) / 100
		|						ИНАЧЕ (ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))).Ставка
		|					КОНЕЦ
		|			КОНЕЦ
		|	КОНЕЦ КАК Стоимость,
		|	ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ПлановыеМатериальныеЗатраты.ПериодЗатрат КАК ПериодЗатрат,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.СтоимостьРегл = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.СтоимостьРегл = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.СтоимостьРегл
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ * ВЫБОР
		|		КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютБУ.Валюта
		|			ТОГДА 1
		|		КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютБУ.КурсЧислитель = 0
		|			ТОГДА 1
		|		ИНАЧЕ КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютБУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютБУ.КурсЧислитель)
		|	КОНЕЦ / ВЫБОР
		|		КОГДА Количество > 0
		|			И ПлановыеМатериальныеЗатраты.СтоимостьРегл = -1
		|			ТОГДА 1
		|		КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|			ТОГДА 1
		|		КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|			ТОГДА 1
		|		КОГДА НЕ ВидыЦен.ЦенаВключаетНДС
		|			ТОГДА 1
		|		ИНАЧЕ ((ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))).Ставка + 100) / 100
		|	КОНЕЦ КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА НЕ ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = 0
		|			И НЕ (ПлановыеМатериальныеЗатраты.Количество > 0 И ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = -1)
		|			ТОГДА ПлановыеМатериальныеЗатраты.СтоимостьБезНДС
		|		ИНАЧЕ ПлановыеМатериальныеЗатраты.Количество * ЕСТЬNULL(ЦеныНоменклатуры.Цена, 0)
		|	КОНЕЦ * ВЫБОР
		|		КОГДА ЦеныНоменклатуры.Валюта = КурсыВалютУУ.Валюта
		|			ТОГДА 1
		|		КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютУУ.КурсЧислитель = 0
		|			ТОГДА 1
		|		ИНАЧЕ КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютУУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель)
		|	КОНЕЦ / ВЫБОР
		|		КОГДА Количество > 0
		|			И ПлановыеМатериальныеЗатраты.СтоимостьБезНДС = -1
		|			ТОГДА 1
		|		КОГДА ПлановыеМатериальныеЗатраты.Количество < 0
		|			ТОГДА 1
		|		КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|			ТОГДА 1
		|		КОГДА НЕ ВидыЦен.ЦенаВключаетНДС
		|			ТОГДА 1
		|		ИНАЧЕ ((ЕСТЬNULL(СтавкиНДСНоменклатуры.СтавкаНДС, ЕСТЬNULL(ОсновныеСтавкиНДС.СтавкаНДС, ЗНАЧЕНИЕ(Справочник.СтавкиНДС.ПустаяСсылка)))).Ставка + 100) / 100
		|	КОНЕЦ КАК СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	ПлановыеМатериальныеЗатраты.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ МатериальныеЗатраты
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПлановыеМатериальныеЗатраты.Номенклатура = ЦеныНоменклатуры.Номенклатура
		|			И ПлановыеМатериальныеЗатраты.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И ПлановыеМатериальныеЗатраты.Период = ЦеныНоменклатуры.Период
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютНоменклатуры КАК КурсыВалютНоменклатуры
		|		ПО (ЦеныНоменклатуры.Валюта = КурсыВалютНоменклатуры.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО (ВидыЦен.Ссылка = &ВидЦены)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтавкиНДСНоменклатуры.СрезПоследних(&Период,
		|			Страна = &СтранаРегистрации ИЛИ Страна = ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтавкиНДСНоменклатуры
		|		ПО ПлановыеМатериальныеЗатраты.Номенклатура = СтавкиНДСНоменклатуры.Номенклатура
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОсновныеСтавкиНДС.СрезПоследних(&Период, Страна = &СтранаРегистрации) КАК ОсновныеСтавкиНДС
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ПлановыеМатериальныеЗатраты.Калькуляция = &Калькуляция
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Калькуляция,
		|	ПартияСпецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныНоменклатуры
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВалютыНоменклатуры";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

Процедура ПолучитьЦеныНоменклатуры(Калькуляции, МенеджерВТ) Экспорт
	
	СписокКалькуляций = Новый Массив;
	Если Не ТипЗнч(Калькуляции) = Тип("Массив") Тогда
		СписокКалькуляций = ОбщегоНазначенияУТКлиентСервер.Массив(Калькуляции);
	Иначе
		СписокКалькуляций = Калькуляции;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	Запрос.УстановитьПараметр("СписокКалькуляций", СписокКалькуляций);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.Калькуляция.ВидЦены КАК ВидЦены,
		|	Затраты.Номенклатура КАК Номенклатура,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.Период КАК Период
		|ПОМЕСТИТЬ НоменклатураДляЦен
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК Затраты
		|ГДЕ
		|	Затраты.Калькуляция В (&СписокКалькуляций)
		|
		|СГРУППИРОВАТЬ ПО
		|	Затраты.Калькуляция,
		|	Затраты.Номенклатура,
		|	Затраты.Характеристика,
		|	Затраты.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МАКСИМУМ(НоменклатураДляЦен.НомерСтроки) КАК НомерСтроки,
		|	НоменклатураДляЦен.Период,
		|	НоменклатураДляЦен.ВидЦены КАК ВидЦены,
		|	НоменклатураДляЦен.Номенклатура КАК Номенклатура,
		|	НоменклатураДляЦен.Характеристика КАК Характеристика,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
		|	0 КАК Цена
		|ИЗ
		|	НоменклатураДляЦен КАК НоменклатураДляЦен
		|
		|СГРУППИРОВАТЬ ПО
		|	НоменклатураДляЦен.Номенклатура,
		|	НоменклатураДляЦен.Характеристика,
		|	НоменклатураДляЦен.ВидЦены,
		|	НоменклатураДляЦен.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Период,
		|	Т.ВидЦены КАК ВидЦены,
		|	&БазоваяВалюта КАК ВалютаРеглУчета
		|ИЗ
		|	НоменклатураДляЦен КАК Т";
	
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	Материалы = Результат[Результат.Количество() - 2].Выгрузить();
	Выборка = Результат[Результат.Количество() - 1].Выбрать();
	
	МатериалыСЦенами = Материалы.СкопироватьКолонки("Номенклатура, Характеристика, ВидЦены");
	МатериалыСЦенами.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	МатериалыСЦенами.Колонки.Добавить("Валюта", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	МатериалыСЦенами.Колонки.Добавить("Цена", Метаданные.ОпределяемыеТипы.ДенежнаяСуммаНеотрицательная.Тип);
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыЗаполненияЦен = ЦеныПредприятияЗаполнениеСервер.НовыйПараметрыЗаполненияЗаполнитьЦены();
		ПараметрыЗаполненияЦен.Дата = Выборка.Период;
		ПараметрыЗаполненияЦен.Валюта = Выборка.ВалютаРеглУчета;
		ПараметрыЗаполненияЦен.ВидЦены = Выборка.ВидЦены;
		
		МатериалыДляПолученияЦены = Материалы.Скопировать(Новый Структура("Период, ВидЦены", Выборка.Период, Выборка.ВидЦены));
	    ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(МатериалыДляПолученияЦены,, ПараметрыЗаполненияЦен);
		
		Для Каждого ОбработанныйМатериал Из МатериалыДляПолученияЦены Цикл
			
			НоваяЗаписьСЦеной = МатериалыСЦенами.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗаписьСЦеной, ОбработанныйМатериал);
			
			НоваяЗаписьСЦеной.Период = Выборка.Период;
			НоваяЗаписьСЦеной.ВидЦены = Выборка.ВидЦены;
			НоваяЗаписьСЦеной.Валюта = Выборка.ВалютаРеглУчета;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Запрос.УстановитьПараметр("МатериалыСЦенами", МатериалыСЦенами);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Период КАК Период,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.ВидЦены КАК ВидЦены,
		|	Т.Валюта КАК Валюта,
		|	Т.Цена КАК Цена
		|ПОМЕСТИТЬ ПредварительнаяЦеныНоменклатуры
		|ИЗ
		|	&МатериалыСЦенами КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.Период КАК Период,
		|	Затраты.Номенклатура КАК Номенклатура,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.Регистратор.ВидЦены КАК ВидЦены
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	РегистрНакопления.ПлановыеМатериальныеЗатраты КАК Затраты
		|ГДЕ
		|	Затраты.Калькуляция В(&СписокКалькуляций)
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	ВидЦены
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Калькуляция КАК Калькуляция,
		|	ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка) КАК Упаковка,
		|	Цены.Период КАК Период,
		|	Цены.Номенклатура КАК Номенклатура,
		|	Цены.Характеристика КАК Характеристика,
		|	Цены.ВидЦены КАК ВидЦены,
		|	Цены.Валюта КАК Валюта,
		|	Цены.Цена КАК Цена
		|ПОМЕСТИТЬ ЦеныНоменклатуры
		|ИЗ
		|	Затраты КАК Затраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПредварительнаяЦеныНоменклатуры КАК Цены
		|		ПО Затраты.Номенклатура = Цены.Номенклатура
		|			И Затраты.Характеристика = Цены.Характеристика
		|			И Затраты.Период = Цены.Период
		|			И Затраты.ВидЦены = Цены.ВидЦены
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НоменклатураДляЦен
		|;
		|УНИЧТОЖИТЬ Затраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПредварительнаяЦеныНоменклатуры";
	
	Запрос.Выполнить();
	
КонецПроцедуры

Процедура ЗаполнитьСтоимостьТрудозатрат(Калькуляция, МенеджерВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	МассивТекстовЗапроса = Новый Массив();
	
	Запрос.УстановитьПараметр("ИспользоватьРазрядыРасценокРаботСотрудников",
		ПолучитьФункциональнуюОпцию("ИспользоватьРазрядыРасценокРаботСотрудников"));
	//++ Локализация
	Запрос.УстановитьПараметр("ДополнительныйРазрезТарифнойСетки",
		Константы.ДополнительныйРазрезТарифнойСетки.Получить());
	//-- Локализация
	МассивТекстовЗапроса.Добавить(РегистрыНакопления.ПлановыеТрудозатраты.ТекстЗапросаРасценкиТрудозатрат(Истина));
	
	ТекстЗапроса = "ВЫБРАТЬ
		|	Расценки.Организация КАК Организация,
		|	ПлановыеТрудозатраты.Калькуляция КАК Калькуляция,
		|	ПлановыеТрудозатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеТрудозатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеТрудозатраты.Подразделение КАК Подразделение,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ПлановыеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ПлановыеТрудозатраты.Период КАК Период,
		|	ПлановыеТрудозатраты.Количество КАК Количество,
		|	ПлановыеТрудозатраты.Количество * ЕСТЬNULL(Расценки.Расценка, 0)
		|		* ВЫБОР
		|			КОГДА КурсыВалютРасценок.Валюта = КурсыВалютУУ.Валюта
		|				ТОГДА 1
		|			ИНАЧЕ ВЫБОР
		|					КОГДА КурсыВалютРасценок.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель = 0
		|						ТОГДА 1
		|					ИНАЧЕ КурсыВалютРасценок.КурсЧислитель * КурсыВалютУУ.КурсЗнаменатель / (КурсыВалютРасценок.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель)
		|		КОНЕЦ
		|	КОНЕЦ КАК Стоимость,
		|	ПлановыеТрудозатраты.ПериодЗатрат КАК ПериодЗатрат,
		|	ПлановыеТрудозатраты.Количество * ЕСТЬNULL(Расценки.Расценка, 0) * ВЫБОР
		|		КОГДА КурсыВалютРасценок.Валюта = КурсыВалютБУ.Валюта
		|			ТОГДА 1
		|		ИНАЧЕ ВЫБОР
		|				КОГДА КурсыВалютРасценок.КурсЗнаменатель * КурсыВалютБУ.КурсЧислитель = 0
		|					ТОГДА 1
		|				ИНАЧЕ КурсыВалютРасценок.КурсЧислитель * КурсыВалютБУ.КурсЗнаменатель / (КурсыВалютРасценок.КурсЗнаменатель * КурсыВалютБУ.КурсЧислитель)
		|			КОНЕЦ
		|	КОНЕЦ КАК СтоимостьРегл
		|ПОМЕСТИТЬ ПлановыеТрудозатратыБезСтраховыхВзносов
		|ИЗ
		|	РегистрНакопления.ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Расценки КАК Расценки
		|		ПО (Расценки.ВидРабот = ПлановыеТрудозатраты.ВидРабот)
		|			И (Расценки.Период = ПлановыеТрудозатраты.Период)
		|			И Расценки.Подразделение = ПлановыеТрудозатраты.Подразделение
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРасценок
		|		ПО (КурсыВалютРасценок.ВидВалюты = ""ВалютаРасценокВидовРабот"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|ГДЕ
		|	ПлановыеТрудозатраты.Калькуляция = &Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация
		|ПОМЕСТИТЬ ПериодыЗатратПоОрганизации
		|ИЗ
		|	(ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, ГОД) КАК ГодЗатрат,
		|		ПлановыеТрудозатраты.Организация КАК Организация
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатраты
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, ГОД),
		|		ПлановыеТрудозатраты.Организация) КАК ВложенныйЗапрос
		|;
		|
		//++ Локализация
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ПрименяемыеТарифыСтраховыхВзносов.ВидТарифа КАК ВидТарифа
		|ПОМЕСТИТЬ ПрименяемыеТарифыПоПериодам
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПериодыЗатратПоОрганизации.ГодЗатрат КАК ГодЗатрат,
		|		ПериодыЗатратПоОрганизации.Организация КАК Организация,
		|		МАКСИМУМ(ПрименяемыеТарифыСтраховыхВзносов.Период) КАК Период,
		|		ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация КАК ГоловнаяОрганизация
		|	ИЗ
		|		РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыЗатратПоОрганизации КАК ПериодыЗатратПоОрганизации
		|			ПО (ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация = ПериодыЗатратПоОрганизации.Организация
		|					ИЛИ ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация = ПериодыЗатратПоОрганизации.Организация.ГоловнаяОрганизация)
		|				И ПрименяемыеТарифыСтраховыхВзносов.Период <= ПериодыЗатратПоОрганизации.ГодЗатрат
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыЗатратПоОрганизации.Организация,
		|		ПериодыЗатратПоОрганизации.ГодЗатрат,
		|		ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|		ПО ВложенныйЗапрос.Период = ПрименяемыеТарифыСтраховыхВзносов.Период
		|			И ВложенныйЗапрос.ГоловнаяОрганизация = ПрименяемыеТарифыСтраховыхВзносов.ГоловнаяОрганизация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.ГодЗатрат КАК ГодЗатрат,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ТарифыСтраховыхВзносов.СтавкаЕдиногоТарифа КАК СтавкаЕдиногоТарифа,
		|	ТарифыСтраховыхВзносов.ПФР КАК ПФР,
		|	ТарифыСтраховыхВзносов.ФСС КАК ФСС,
		|	ТарифыСтраховыхВзносов.ФФОМС + ТарифыСтраховыхВзносов.ТФОМС КАК ФОМС
		|ПОМЕСТИТЬ ПрименяемыеТарифыСтраховыхВзносов
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПрименяемыеТарифыПоПериодам.ГодЗатрат КАК ГодЗатрат,
		|		ПрименяемыеТарифыПоПериодам.ВидТарифа КАК ВидТарифа,
		|		МАКСИМУМ(ТарифыСтраховыхВзносов.Период) КАК Период,
		|		ПрименяемыеТарифыПоПериодам.Организация КАК Организация
		|	ИЗ
		|		ПрименяемыеТарифыПоПериодам КАК ПрименяемыеТарифыПоПериодам
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыСтраховыхВзносов КАК ТарифыСтраховыхВзносов
		|			ПО ПрименяемыеТарифыПоПериодам.ГодЗатрат >= ТарифыСтраховыхВзносов.Период
		|				И ПрименяемыеТарифыПоПериодам.ВидТарифа = ТарифыСтраховыхВзносов.ВидТарифа
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПрименяемыеТарифыПоПериодам.ГодЗатрат,
		|		ПрименяемыеТарифыПоПериодам.ВидТарифа,
		|		ПрименяемыеТарифыПоПериодам.Организация) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ТарифыСтраховыхВзносов КАК ТарифыСтраховыхВзносов
		|		ПО ВложенныйЗапрос.ВидТарифа = ТарифыСтраховыхВзносов.ВидТарифа
		|			И ВложенныйЗапрос.Период = ТарифыСтраховыхВзносов.Период
		|;
		|
		//-- Локализация
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	РасчетСтраховыхВзносов.Калькуляция КАК Калькуляция,
		|	РасчетСтраховыхВзносов.ПартияСпецификация КАК ПартияСпецификация,
		|	РасчетСтраховыхВзносов.КлючПартия КАК КлючПартия,
		|	РасчетСтраховыхВзносов.Подразделение КАК Подразделение,
		|	РасчетСтраховыхВзносов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	РасчетСтраховыхВзносов.ВидРабот КАК ВидРабот,
		|	РасчетСтраховыхВзносов.Период КАК Период,
		|	СУММА(РасчетСтраховыхВзносов.Стоимость) КАК Стоимость,
		|	РасчетСтраховыхВзносов.ПериодЗатрат КАК ПериодЗатрат,
		|	СУММА(РасчетСтраховыхВзносов.Количество) КАК Количество,
		|	СУММА(РасчетСтраховыхВзносов.СтоимостьРегл) КАК СтоимостьРегл
		|ПОМЕСТИТЬ ПлановыеТрудозатраты
		|ИЗ (
		//++ Локализация
		|	ВЫБРАТЬ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция КАК Калькуляция,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПартияСпецификация КАК ПартияСпецификация,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.КлючПартия КАК КлючПартия,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Подразделение КАК Подразделение,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ВидРабот КАК ВидРабот,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Период КАК Период,
		|		ВЫРАЗИТЬ((ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФОМС, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ПФР, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФСС, 0))/ 100 КАК ЧИСЛО(31,2)) КАК Стоимость,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат КАК ПериодЗатрат,
		|		0 КАК Количество,
		|		ВЫРАЗИТЬ((ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФОМС, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ПФР, 0)
		|					+ ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.ФСС, 0))/ 100 КАК ЧИСЛО(31,2)) КАК СтоимостьРегл
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатратыБезСтраховыхВзносов
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|			ПО (НАЧАЛОПЕРИОДА(ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат, ГОД) = ПрименяемыеТарифыСтраховыхВзносов.ГодЗатрат)
		|				И ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция.Организация = ПрименяемыеТарифыСтраховыхВзносов.Организация
		|	ГДЕ
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат, ГОД) < &ДатаОбъединенияВзносов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция КАК Калькуляция,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПартияСпецификация КАК ПартияСпецификация,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.КлючПартия КАК КлючПартия,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Подразделение КАК Подразделение,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ВидРабот КАК ВидРабот,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Период КАК Период,
		|		ВЫРАЗИТЬ(ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.СтавкаЕдиногоТарифа, 0)/ 100 КАК ЧИСЛО(31,2)) КАК Стоимость,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат КАК ПериодЗатрат,
		|		0 КАК Количество,
		|		ВЫРАЗИТЬ(ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл * ЕСТЬNULL(ПрименяемыеТарифыСтраховыхВзносов.СтавкаЕдиногоТарифа, 0)/ 100 КАК ЧИСЛО(31,2)) КАК СтоимостьРегл
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатратыБезСтраховыхВзносов
		|			ЛЕВОЕ СОЕДИНЕНИЕ ПрименяемыеТарифыСтраховыхВзносов КАК ПрименяемыеТарифыСтраховыхВзносов
		|			ПО (НАЧАЛОПЕРИОДА(ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат, ГОД) = ПрименяемыеТарифыСтраховыхВзносов.ГодЗатрат)
		|				И ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция.Организация = ПрименяемыеТарифыСтраховыхВзносов.Организация
		|	ГДЕ
		|		НАЧАЛОПЕРИОДА(ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат, ГОД) >= &ДатаОбъединенияВзносов
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		//-- Локализация
		|	
		|	ВЫБРАТЬ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Калькуляция,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПартияСпецификация,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.КлючПартия КАК КлючПартия,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Подразделение,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтатьяКалькуляции,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ВидРабот,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Период,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Стоимость,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.ПериодЗатрат,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.Количество,
		|		ПлановыеТрудозатратыБезСтраховыхВзносов.СтоимостьРегл
		|	ИЗ
		|		ПлановыеТрудозатратыБезСтраховыхВзносов КАК ПлановыеТрудозатратыБезСтраховыхВзносов) КАК РасчетСтраховыхВзносов
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетСтраховыхВзносов.Период,
		|	РасчетСтраховыхВзносов.Подразделение,
		|	РасчетСтраховыхВзносов.Калькуляция,
		|	РасчетСтраховыхВзносов.ВидРабот,
		|	РасчетСтраховыхВзносов.СтатьяКалькуляции,
		|	РасчетСтраховыхВзносов.ПериодЗатрат,
		|	РасчетСтраховыхВзносов.ПартияСпецификация,
		|	РасчетСтраховыхВзносов.КлючПартия
		|
		|ИМЕЮЩИЕ
		|	СУММА(РасчетСтраховыхВзносов.Количество) > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Калькуляция,
		|	ПартияСпецификация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВидыРабот
		|;
		|
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Расценки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПлановыеТрудозатратыБезСтраховыхВзносов
		|;
		//++ Локализация
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПрименяемыеТарифыПоПериодам
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПрименяемыеТарифыСтраховыхВзносов
		|;
		//-- Локализация
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПериодыЗатратПоОрганизации";
	
	МассивТекстовЗапроса.Добавить(ТекстЗапроса);
	Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);

	//++ Локализация
	Запрос.УстановитьПараметр("ДатаОбъединенияВзносов", УчетСтраховыхВзносов.ДатаОбъединенияВзносов());
	//-- Локализация
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры

Процедура ЗаполнитьСтоимостьПостатейных(ПараметрыРасчета, МенеджерВТ)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.УстановитьПараметр("ВидЦены", ПараметрыРасчета.РеквизитыПК.ВидЦены);
	Запрос.УстановитьПараметр("Организация", ПараметрыРасчета.РеквизитыПК.Организация);
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	Запрос.УстановитьПараметр("ИспользуетсяЦенообразование25",
		ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25());
	Запрос.УстановитьПараметр("ДатаПереходаНаЦенообразованиеВерсии25",
		ЦенообразованиеВызовСервера.ДатаПереходаНаЦенообразованиеВерсии25());
	
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	МАКСИМУМ(Т.Период),
		|	Т.Партия,
		|	Т.КлючПартия КАК КлючПартия,
		|	Т.Продукция,
		|	Т.Характеристика,
		|	СУММА(Т.КоличествоПолуфабриката) КАК КоличествоПолуфабриката
		|ПОМЕСТИТЬ ПродукцияПартий
		|ИЗ
		|(
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, МЕСЯЦ) КАК Период,
		|		МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК Партия,
		|		МножителиПартийИПолуфабрикатов.КлючПартия,
		|		МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|		МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК Характеристика,
		|		МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|		И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|		И МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НАЧАЛОПЕРИОДА(МножителиПартийИПолуфабрикатов.Период, МЕСЯЦ),
		|		МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|		МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|		МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|		МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката
		|	ИЗ
		|		РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|	ГДЕ
		|		МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|		И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена) КАК Т
		|СГРУППИРОВАТЬ ПО
		|	Т.Партия,
		|	Т.КлючПартия,
		|	Т.Продукция,
		|	Т.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Партия КАК Партия,
		|	Т.КлючПартия КАК КлючПартия,
		|	Т.Период КАК Период,
		|	Т.КоличествоПолуфабриката КАК КоличествоПолуфабриката
		|ПОМЕСТИТЬ ПартииИзделий
		|ИЗ
		|	ПродукцияПартий КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ВложенныйЗапрос.Период, МЕСЯЦ) КАК Период,
		|	&Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	""Продукция"" КАК ТипБазы,
		|	ВложенныйЗапрос.Номенклатура КАК Продукция,
		|	ВложенныйЗапрос.Характеристика КАК Характеристика,
		|	ВложенныйЗапрос.Ссылка КАК ПартияСпецификация,
		|	ВложенныйЗапрос.КлючПартия КАК КлючПартия,
		|	ВложенныйЗапрос.Количество КАК Количество
		|ПОМЕСТИТЬ ПродукцияПредварительная
		|ИЗ
		|	(ВЫБРАТЬ
		|		ПартииИзделий.Период КАК Период,
		|		ПартииИзделий.КлючПартия КАК КлючПартия,
		|		Реквизиты.Ссылка КАК Ссылка,
		|		ТЧВыходныеИзделия.Подразделение КАК Подразделение,
		|		ТЧВыходныеИзделия.Номенклатура КАК Номенклатура,
		|		ТЧВыходныеИзделия.Характеристика КАК Характеристика,
		|		ТЧВыходныеИзделия.Количество КАК Количество
		|	ИЗ
		|		ПартииИзделий КАК ПартииИзделий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК Реквизиты
		|			ПО ПартииИзделий.Партия = Реквизиты.Ссылка
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2.ВыходныеИзделия КАК ТЧВыходныеИзделия
		|			ПО ПартииИзделий.Партия = ТЧВыходныеИзделия.Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПартииИзделий.Период,
		|		ПродукцияПартий.КлючПартия КАК КлючПартия,
		|		ТаблицаВыходныеИзделия.Ссылка,
		|		ТаблицаВыходныеИзделия.Этап.Подразделение,
		|		ПродукцияПартий.Продукция,
		|		ПродукцияПартий.Характеристика,
		|		ПродукцияПартий.КоличествоПолуфабриката
		|	ИЗ
		|		ПартииИзделий КАК ПартииИзделий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПродукцияПартий КАК ПродукцияПартий
		|			ПО ПартииИзделий.Партия = ПродукцияПартий.Партия
		|				И ПартииИзделий.КлючПартия = ПродукцияПартий.КлючПартия
		|				И ПартииИзделий.Период = ПродукцияПартий.Период
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РеквизитыСпецификации
		|			ПО ПартииИзделий.Партия = РеквизитыСпецификации.Ссылка
		|				И НЕ РеквизитыСпецификации.ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.ВидНоменклатуры)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
		|			ПО ПартииИзделий.Партия = ТаблицаВыходныеИзделия.Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ПродукцияПартий.Период,
		|		ПродукцияПартий.КлючПартия КАК КлючПартия,
		|		ТаблицаВыходныеИзделия.Ссылка,
		|		ТаблицаВыходныеИзделия.Этап.Подразделение,
		|		ПродукцияПартий.Продукция,
		|		ПродукцияПартий.Характеристика,
		|		ПродукцияПартий.КоличествоПолуфабриката
		|	ИЗ
		|		ПродукцияПартий КАК ПродукцияПартий
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации КАК РеквизитыСпецификации
		|			ПО ПродукцияПартий.Партия = РеквизитыСпецификации.Ссылка
		|				И РеквизитыСпецификации.ВариантНазначения = ЗНАЧЕНИЕ(Перечисление.ВариантыНазначенияСпецификации.ВидНоменклатуры)
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ТаблицаВыходныеИзделия
		|			ПО ПродукцияПартий.Партия = ТаблицаВыходныеИзделия.Ссылка) КАК ВложенныйЗапрос
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|		ПродукцияПредварительная.Продукция КАК Продукция,
		|		ПродукцияПредварительная.Характеристика КАК Характеристика,
		|		ПродукцияПредварительная.Период КАК Период,
		|		МАКСИМУМ(ЦеныНоменклатуры25.Период) КАК ПериодЦены,
		|		&ВидЦены КАК ВидЦены,
		|		ЦеныНоменклатуры25.ХарактеристикаЦО КАК ХарактеристикаЦО
		|ПОМЕСТИТЬ АктуальныеПериодыУстановкиЦен25
		|ИЗ
		|	ПродукцияПредварительная КАК ПродукцияПредварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК СпрХарактеристикиНоменклатуры
		|		ПО СпрХарактеристикиНоменклатуры.Ссылка = ПродукцияПредварительная.Характеристика
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры25
		|		ПО	ЦеныНоменклатуры25.Период >= &ДатаПереходаНаЦенообразованиеВерсии25
		|			И ПродукцияПредварительная.Период >= ЦеныНоменклатуры25.Период
		|			И ПродукцияПредварительная.Продукция = ЦеныНоменклатуры25.Номенклатура
		|			И ЕСТЬNULL(СпрХарактеристикиНоменклатуры.ХарактеристикаНоменклатурыДляЦенообразования, ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка)) = ЦеныНоменклатуры25.ХарактеристикаЦО
		|			И (ЦеныНоменклатуры25.ВидЦены = &ВидЦены) 
		|ГДЕ
		|	&ИспользуетсяЦенообразование25
		|	И ЦеныНоменклатуры25.Период ЕСТЬ НЕ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродукцияПредварительная.Продукция,
		|	ПродукцияПредварительная.Характеристика,
		|	ПродукцияПредварительная.Период,
		|	ЦеныНоменклатуры25.ХарактеристикаЦО
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПродукцияПредварительная.Продукция КАК Продукция,
		|	ПродукцияПредварительная.Характеристика КАК Характеристика,
		|	ПродукцияПредварительная.Период КАК Период,
		|	МАКСИМУМ(ЕСТЬNULL(ЦеныНоменклатуры.Период, ДАТАВРЕМЯ(1, 1, 1))) КАК ПериодЦены,
		|	&ВидЦены КАК ВидЦены
		|ПОМЕСТИТЬ АктуальныеПериодыУстановкиЦен
		|ИЗ
		|	ПродукцияПредварительная КАК ПродукцияПредварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО ПродукцияПредварительная.Период >= ЦеныНоменклатуры.Период
		|			И ПродукцияПредварительная.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И ПродукцияПредварительная.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И (ЦеныНоменклатуры.ВидЦены = &ВидЦены)
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ АктуальныеПериодыУстановкиЦен25 КАК АктуальныеПериодыУстановкиЦен25
		|		ПО АктуальныеПериодыУстановкиЦен25.Продукция = ПродукцияПредварительная.Продукция
		|			И АктуальныеПериодыУстановкиЦен25.Характеристика = ПродукцияПредварительная.Характеристика
		|			И АктуальныеПериодыУстановкиЦен25.Период = ПродукцияПредварительная.Период
		|
		|ГДЕ
		|	АктуальныеПериодыУстановкиЦен25.ПериодЦены ЕСТЬ NULL
		|		ИЛИ НЕ &ИспользуетсяЦенообразование25
		|		ИЛИ ЕСТЬNULL(ЦеныНоменклатуры.Период, ДАТАВРЕМЯ(1, 1, 1)) < &ДатаПереходаНаЦенообразованиеВерсии25
		|
		|СГРУППИРОВАТЬ ПО
		|	ПродукцияПредварительная.Продукция,
		|	ПродукцияПредварительная.Характеристика,
		|	ПродукцияПредварительная.Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен25.Продукция КАК Номенклатура,
		|	АктуальныеПериодыУстановкиЦен25.Характеристика КАК Характеристика,
		|	АктуальныеПериодыУстановкиЦен25.Период КАК Период,
		|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен25 КАК АктуальныеПериодыУстановкиЦен25
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен25.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен25.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И АктуальныеПериодыУстановкиЦен25.ХарактеристикаЦО = ЦеныНоменклатуры.ХарактеристикаЦО
		|			И АктуальныеПериодыУстановкиЦен25.ВидЦены = ЦеныНоменклатуры.ВидЦены
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен25.Продукция,
		|	АктуальныеПериодыУстановкиЦен25.Характеристика,
		|	АктуальныеПериодыУстановкиЦен25.Период,
		|	ЦеныНоменклатуры.Упаковка,
		|	ЦеныНоменклатуры.Цена,
		|	ЦеныНоменклатуры.Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен25 КАК АктуальныеПериодыУстановкиЦен25
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры25 КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен25.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен25.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И (ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатурыДляЦенообразования.ПустаяСсылка) = ЦеныНоменклатуры.ХарактеристикаЦО)
		|			И АктуальныеПериодыУстановкиЦен25.ВидЦены = ЦеныНоменклатуры.ВидЦены
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен.Продукция КАК Номенклатура,
		|	АктуальныеПериодыУстановкиЦен.Характеристика КАК Характеристика,
		|	АктуальныеПериодыУстановкиЦен.Период КАК Период,
		|	ЦеныНоменклатуры.Упаковка КАК Упаковка,
		|	ЦеныНоменклатуры.Цена КАК Цена,
		|	ЦеныНоменклатуры.Валюта КАК Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен КАК АктуальныеПериодыУстановкиЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И АктуальныеПериодыУстановкиЦен.Характеристика = ЦеныНоменклатуры.Характеристика
		|			И АктуальныеПериодыУстановкиЦен.ВидЦены = ЦеныНоменклатуры.ВидЦены
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	АктуальныеПериодыУстановкиЦен.Продукция,
		|	АктуальныеПериодыУстановкиЦен.Характеристика,
		|	АктуальныеПериодыУстановкиЦен.Период,
		|	ЦеныНоменклатуры.Упаковка,
		|	ЦеныНоменклатуры.Цена,
		|	ЦеныНоменклатуры.Валюта
		|ИЗ
		|	АктуальныеПериодыУстановкиЦен КАК АктуальныеПериодыУстановкиЦен
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры КАК ЦеныНоменклатуры
		|		ПО АктуальныеПериодыУстановкиЦен.ПериодЦены = ЦеныНоменклатуры.Период
		|			И АктуальныеПериодыУстановкиЦен.Продукция = ЦеныНоменклатуры.Номенклатура
		|			И (ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) = ЦеныНоменклатуры.Характеристика)
		|			И АктуальныеПериодыУстановкиЦен.ВидЦены = ЦеныНоменклатуры.ВидЦены
		|";
		
	Запрос.Текст = ТекстЗапроса;
		
	УстановитьПривилегированныйРежим(Истина);
	Материалы = Запрос.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	
	КоэффициентыУпаковок = Справочники.УпаковкиЕдиницыИзмерения.КоэффициентыУпаковок(Материалы);
	
	Для Каждого Материал Из Материалы Цикл
		
		УпаковкиПоМатериалу = КоэффициентыУпаковок.Получить(Материал.Номенклатура);
		Если УпаковкиПоМатериалу = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		КоэффициентУпаковки = УпаковкиПоМатериалу.Получить(Материал.Упаковка);
		Если Не ЗначениеЗаполнено(КоэффициентУпаковки) Тогда
			Продолжить;
		КонецЕсли;
		
		Материал.Цена = Материал.Цена / КоэффициентУпаковки;
		
	КонецЦикла;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныНоменклатурыПредварительная
		|ИЗ
		|	&Материалы КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.Период КАК Период,
		|	Т.Упаковка КАК Упаковка,
		|	Т.Цена КАК Цена,
		|	Т.Валюта КАК Валюта
		|ПОМЕСТИТЬ ЦеныПродукции
		|ИЗ
		|	ЦеныНоменклатурыПредварительная КАК Т
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура,
		|	Характеристика,
		|	Период
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ПлановыеМатериальныеЗатраты.ПериодЗатрат, МЕСЯЦ) КАК Период,
		|	&Организация КАК Организация,
		|	ПлановыеМатериальныеЗатраты.Подразделение КАК Подразделение,
		|	ПлановыеМатериальныеЗатраты.Номенклатура КАК Материал,
		|	ПлановыеМатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ЛОЖЬ КАК ЭтоПолуфабрикатСобственный,
		|	ЛОЖЬ КАК ЭтоПолуфабрикатПереработчика,
		|	ВЫБОР
		|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции 
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ
		|			ЕСТЬNULL(РеквизитыПартии.ПартияПроизводства.ГруппаПродукции, 
		|				ЕСТЬNULL(ИзделияСпецификации.Номенклатура.ГруппаАналитическогоУчета, 
		|					ЕСТЬNULL(ПлановыеМатериальныеЗатраты.Номенклатура.ГруппаАналитическогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))))
		|	КОНЕЦ КАК ГруппаПродукции,
		|	ПлановыеМатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	ПлановыеМатериальныеЗатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеМатериальныеЗатраты.Количество КАК Количество,
		|	ПлановыеМатериальныеЗатраты.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	ПлановыеМатериальныеЗатраты.СтоимостьРегл КАК СтоимостьРегл,
		|	ПлановыеМатериальныеЗатраты.Стоимость КАК Стоимость,
		|	&Объем * ПлановыеМатериальныеЗатраты.Количество КАК Объем,
		|	&Вес * ПлановыеМатериальныеЗатраты.Количество КАК Вес
		|ПОМЕСТИТЬ ДанныеПоМатериальнымЗатраты
		|ИЗ
		|	МатериальныеЗатратыВключаяПолуфабрикаты КАК ПлановыеМатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО ПлановыеМатериальныеЗатраты.Номенклатура = СН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ИзделияСпецификации
		|		ПО ПлановыеМатериальныеЗатраты.ПартияСпецификация = ИзделияСпецификации.Ссылка
		|			И ИзделияСпецификации.НомерСтроки = 1
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыПартии
		|		ПО ПлановыеМатериальныеЗатраты.ПартияСпецификация = РеквизитыПартии.Ссылка
		|ГДЕ
		|	НЕ ПлановыеМатериальныеЗатраты.ЭтоПолуфабрикат
		|	И ПлановыеМатериальныеЗатраты.Количество > 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	Подразделение,
		|	Материал
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(ПлановыеТрудозатраты.ПериодЗатрат, МЕСЯЦ) КАК Период,
		|	&Организация КАК Организация,
		|	ПлановыеТрудозатраты.Подразделение КАК Подразделение,
		|	ПлановыеТрудозатраты.ВидРабот КАК ВидРабот,
		|	ПлановыеТрудозатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	ЛОЖЬ КАК ЭтоПолуфабрикатСобственный,
		|	ЛОЖЬ КАК ЭтоПолуфабрикатПереработчика,
		|	ВЫБОР
		|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции 
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ
		|			ЕСТЬNULL(РеквизитыПартии.ПартияПроизводства.ГруппаПродукции, 
		|				ЕСТЬNULL(ИзделияСпецификации.Номенклатура.ГруппаАналитическогоУчета, 
		|					ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)))
		|	КОНЕЦ КАК ГруппаПродукции,
		|	ПлановыеТрудозатраты.ПартияСпецификация,
		|	ПлановыеТрудозатраты.КлючПартия КАК КлючПартия,
		|	ПлановыеТрудозатраты.Количество,
		|	ПлановыеТрудозатраты.Стоимость,
		|	ПлановыеТрудозатраты.СтоимостьРегл,
		|	ПлановыеТрудозатраты.Стоимость КАК НормативнаяСтоимость
		|ПОМЕСТИТЬ ДанныеПоТрудозатратам
		|ИЗ
		|	ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ИзделияСпецификации
		|			ПО ПлановыеТрудозатраты.ПартияСпецификация = ИзделияСпецификации.Ссылка
		|			И ИзделияСпецификации.НомерСтроки = 1
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыПартии
		|			ПО ПлановыеТрудозатраты.ПартияСпецификация = РеквизитыПартии.Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	Подразделение,
		|	ВидРабот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(Предварительная.Период, МЕСЯЦ) КАК Период,
		|	Предварительная.Организация КАК Организация,
		|	Предварительная.Подразделение КАК Подразделение,
		|	Предварительная.Продукция КАК Продукция,
		|	ВЫБОР
		|		КОГДА НЕ &АналитическийУчетПоГруппамПродукции 
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка)
		|		ИНАЧЕ
		|			ЕСТЬNULL(РеквизитыПартии.ПартияПроизводства.ГруппаПродукции, 
		|				ЕСТЬNULL(ИзделияСпецификации.Номенклатура.ГруппаАналитическогоУчета, 
		|					ЕСТЬNULL(Предварительная.Продукция.ГруппаАналитическогоУчета, ЗНАЧЕНИЕ(Справочник.ГруппыАналитическогоУчетаНоменклатуры.ПустаяСсылка))))
		|	КОНЕЦ КАК ГруппаПродукции,
		|	Предварительная.ПартияСпецификация,
		|	Предварительная.КлючПартия КАК КлючПартия,
		|	Предварительная.Количество,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) * ВЫБОР
		|				КОГДА ЦеныПродукции.Валюта = КурсыВалютУУ.Валюта
		|					ТОГДА 1
		|				ИНАЧЕ ВЫБОР
		|						КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютУУ.КурсЧислитель = 0
		|							ТОГДА 1
		|						ИНАЧЕ КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютУУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель)
		|					КОНЕЦ
		|			КОНЕЦ * ВЫБОР
		|				КОГДА ВидыЦен.Ссылка ЕСТЬ NULL
		|					ТОГДА 1
		|				КОГДА ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ (Предварительная.Продукция.СтавкаНДС.Ставка + 100) / 100 КОНЕЦ КАК Стоимость,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) 
		|		* ВЫБОР
		|			КОГДА ЦеныПродукции.Валюта = КурсыВалютБУ.Валюта
		|				ТОГДА 1
		|			КОГДА ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютБУ.КурсЧислитель = 0
		|				ТОГДА 1
		|			ИНАЧЕ 
		|				КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютБУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютБУ.КурсЧислитель)
		|		КОНЕЦ / ВЫБОР
		|			КОГДА Предварительная.Количество < 0
		|				ИЛИ ВидыЦен.Ссылка ЕСТЬ NULL
		|				ИЛИ НЕ ВидыЦен.ЦенаВключаетНДС
		|				ТОГДА 1
		|			ИНАЧЕ (Предварительная.Продукция.СтавкаНДС.Ставка + 100) / 100 КОНЕЦ КАК СтоимостьРегл,
		|	Предварительная.Количество * ЕСТЬNULL(ЦеныПродукции.Цена, 0) 
		|		* ВЫБОР
		|			КОГДА ЦеныПродукции.Валюта = КурсыВалютУУ.Валюта
		|				ИЛИ ЕСТЬNULL(КурсыВалютНоменклатуры.КурсЗнаменатель, 0) * КурсыВалютУУ.КурсЧислитель = 0
		|				ТОГДА 1
		|			ИНАЧЕ КурсыВалютНоменклатуры.КурсЧислитель * КурсыВалютУУ.КурсЗнаменатель / (КурсыВалютНоменклатуры.КурсЗнаменатель * КурсыВалютУУ.КурсЧислитель)
		|		КОНЕЦ 
		|			/ ВЫБОР
		|				КОГДА Предварительная.Количество < 0
		|					ИЛИ ВидыЦен.Ссылка ЕСТЬ NULL
		|					ИЛИ НЕ ВидыЦен.ЦенаВключаетНДС
		|					ТОГДА 1
		|				ИНАЧЕ (Предварительная.Продукция.СтавкаНДС.Ставка + 100) / 100 КОНЕЦ КАК СтоимостьБезНДС,
		|	&Объем * Предварительная.Количество КАК Объем,
		|	&Вес * Предварительная.Количество КАК Вес
		|ПОМЕСТИТЬ ДанныеПоПродукции
		|ИЗ
		|	ПродукцияПредварительная КАК Предварительная
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныПродукции КАК ЦеныПродукции
		|		ПО Предварительная.Период = ЦеныПродукции.Период
		|			И Предварительная.Продукция = ЦеныПродукции.Номенклатура
		|			И Предварительная.Характеристика = ЦеныПродукции.Характеристика
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.РесурсныеСпецификации.ВыходныеИзделия КАК ИзделияСпецификации
		|			ПО Предварительная.ПартияСпецификация = ИзделияСпецификации.Ссылка
		|			И ИзделияСпецификации.НомерСтроки = 1
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК РеквизитыПартии
		|			ПО Предварительная.ПартияСпецификация = РеквизитыПартии.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СН
		|		ПО Предварительная.Продукция = СН.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютНоменклатуры КАК КурсыВалютНоменклатуры
		|		ПО (ЦеныПродукции.Валюта = КурсыВалютНоменклатуры.Валюта)
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютБУ
		|		ПО (КурсыВалютБУ.ВидВалюты = ""ВалютаРеглУчета"")
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыЦен КАК ВидыЦен
		|		ПО (ВидыЦен.Ссылка = &ВидЦены)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Период,
		|	Организация,
		|	Подразделение,
		|	Продукция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Т.Период КАК Период
		|ПОМЕСТИТЬ ПериодыПоМесяцам
		|ИЗ
		|	(ВЫБРАТЬ
		|		БазыРаспределения.Период КАК Период
		|	ИЗ
		|		ДанныеПоМатериальнымЗатраты КАК БазыРаспределения
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		БазыРаспределения.Период КАК Период
		|	ИЗ
		|		ДанныеПоТрудозатратам КАК БазыРаспределения
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		БазыРаспределения.Период КАК Период
		|	ИЗ
		|		ДанныеПоПродукции КАК БазыРаспределения) КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВложенныйЗапрос.Период КАК Период,
		|	ВложенныйЗапрос.Организация КАК Организация,
		|	ВложенныйЗапрос.Подразделение КАК Подразделение,
		|	МАКСИМУМ(Нормативы.Ссылка) КАК Ссылка
		|ПОМЕСТИТЬ АктуальныеДокументыПоПериодам
		|ИЗ
		|	(ВЫБРАТЬ
		|		МАКСИМУМ(Нормативы.Дата) КАК Дата,
		|		Нормативы.Организация КАК Организация,
		|		Нормативы.Подразделение КАК Подразделение,
		|		ПериодыПоМесяцам.Период КАК Период
		|	ИЗ
		|		ПериодыПоМесяцам КАК ПериодыПоМесяцам
		|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов КАК Нормативы
		|			ПО ПериодыПоМесяцам.Период >= Нормативы.ДатаНачалаДействия
		|				И (Нормативы.ДатаОкончанияДействия = ДАТАВРЕМЯ(1,1,1) ИЛИ ПериодыПоМесяцам.Период <= Нормативы.ДатаОкончанияДействия)
		|				И (НЕ Нормативы.ПометкаУдаления)
		|	
		|	СГРУППИРОВАТЬ ПО
		|		ПериодыПоМесяцам.Период,
		|		Нормативы.Организация,
		|		Нормативы.Подразделение) КАК ВложенныйЗапрос
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов КАК Нормативы
		|		ПО ВложенныйЗапрос.Дата = Нормативы.Дата
		|			И ВложенныйЗапрос.Организация = Нормативы.Организация
		|			И ВложенныйЗапрос.Подразделение = Нормативы.Подразделение
		|			И (НЕ Нормативы.ПометкаУдаления)
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Период,
		|	ВложенныйЗапрос.Организация,
		|	ВложенныйЗапрос.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	АктуальныеДокументыПоПериодам.Период КАК Период,
		|	АктуальныеДокументыПоПериодам.Организация КАК Организация,
		|	АктуальныеДокументыПоПериодам.Подразделение КАК Подразделение,
		|	НормыПостоянныхРасходов.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	НормыПостоянныхРасходов.СтатьяРасходов КАК СтатьяРасходов,
		|	ВЫРАЗИТЬ(НормыПостоянныхРасходов.Норматив КАК ЧИСЛО(31, 2)) КАК Норматив,
		|	НормыПостоянныхРасходов.Кратность КАК Кратность,
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаРегл, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаРегл,
		|			ЕСТЬNULL(НастройкиПоПодразделению.НастройкаРегл, 
		|				РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		|	))) КАК ПравилоРаспределенияРасходовРегл,
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаУпр, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаУпр,
		|			ЕСТЬNULL(НастройкиПоПодразделению.НастройкаУпр, 
		|				РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		|	))) КАК ПравилоРаспределенияРасходовУпр
		|ПОМЕСТИТЬ АктуальныеНормыПостоянныхИПеременныхРасходовПоПериодам
		|ИЗ
		|	АктуальныеДокументыПоПериодам КАК АктуальныеДокументыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов.НормыПостоянныхРасходов КАК НормыПостоянныхРасходов
		|		ПО АктуальныеДокументыПоПериодам.Ссылка = НормыПостоянныхРасходов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО НормыПостоянныхРасходов.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизации
		|		ПО НормыПостоянныхРасходов.СтатьяРасходов = НастройкиПоОрганизации.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Организация = НастройкиПоОрганизации.Организация
		|			И (АктуальныеДокументыПоПериодам.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоПодразделению
		|		ПО НормыПостоянныхРасходов.СтатьяРасходов = НастройкиПоПодразделению.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Подразделение = НастройкиПоПодразделению.Подразделение
		|			И (НастройкиПоПодразделению.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизацииИПодразделению
		|		ПО НормыПостоянныхРасходов.СтатьяРасходов = НастройкиПоОрганизацииИПодразделению.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Организация = НастройкиПоОрганизацииИПодразделению.Организация
		|			И АктуальныеДокументыПоПериодам.Подразделение = НастройкиПоОрганизацииИПодразделению.Подразделение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	АктуальныеДокументыПоПериодам.Период,
		|	АктуальныеДокументыПоПериодам.Организация,
		|	АктуальныеДокументыПоПериодам.Подразделение,
		|	НормыПеременныхРасходов.СтатьяКалькуляции,
		|	НормыПеременныхРасходов.СтатьяРасходов,
		|	ВЫРАЗИТЬ(НормыПеременныхРасходов.Норматив КАК Число(31, 2)),
		|	НормыПеременныхРасходов.Кратность,
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаРегл, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаРегл,
		|			ЕСТЬNULL(НастройкиПоПодразделению.НастройкаРегл, 
		|				РеквизитыСтатьи.ПравилоРаспределенияРасходовРегл
		|	))) КАК ПравилоРаспределенияРасходовРегл,
		|	ЕСТЬNULL(НастройкиПоОрганизацииИПодразделению.НастройкаУпр, 
		|		ЕСТЬNULL(НастройкиПоОрганизации.НастройкаУпр,
		|			ЕСТЬNULL(НастройкиПоПодразделению.НастройкаУпр, 
		|				РеквизитыСтатьи.ПравилоРаспределенияРасходовУпр
		|	))) КАК ПравилоРаспределенияРасходовУпр
		|ИЗ
		|	АктуальныеДокументыПоПериодам КАК АктуальныеДокументыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаНормативовПроизводственныхРасходов.НормыПеременныхРасходов КАК НормыПеременныхРасходов
		|		ПО АктуальныеДокументыПоПериодам.Ссылка = НормыПеременныхРасходов.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.СтатьиРасходов КАК РеквизитыСтатьи
		|		ПО НормыПеременныхРасходов.СтатьяРасходов = РеквизитыСтатьи.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизации
		|		ПО НормыПеременныхРасходов.СтатьяРасходов = НастройкиПоОрганизации.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Организация = НастройкиПоОрганизации.Организация
		|			И (АктуальныеДокументыПоПериодам.Подразделение = ЗНАЧЕНИЕ(Справочник.СтруктураПредприятия.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоПодразделению
		|		ПО НормыПеременныхРасходов.СтатьяРасходов = НастройкиПоПодразделению.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Подразделение = НастройкиПоПодразделению.Подразделение
		|			И (НастройкиПоПодразделению.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка))
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкиРаспределенияПостатейныхРасходов КАК НастройкиПоОрганизацииИПодразделению
		|		ПО НормыПеременныхРасходов.СтатьяРасходов = НастройкиПоОрганизацииИПодразделению.СтатьяРасходов
		|			И АктуальныеДокументыПоПериодам.Организация = НастройкиПоОрганизацииИПодразделению.Организация
		|			И АктуальныеДокументыПоПериодам.Подразделение = НастройкиПоОрганизацииИПодразделению.Подразделение
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РеглУчет,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.Норматив КАК Норматив,
		|	Т.Кратность КАК Кратность,
		|	Т.ПравилоРаспределенияРасходовУпр КАК ПравилоРаспределенияРасходов,
		|	ПравилаРаспределенияРасходов.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям
		|ПОМЕСТИТЬ АктуальныеНормативыПоПериодам
		|ИЗ
		|	АктуальныеНормыПостоянныхИПеременныхРасходовПоПериодам КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределенияРасходов
		|		ПО Т.ПравилоРаспределенияРасходовУпр = ПравилаРаспределенияРасходов.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЛОЖЬ КАК РеглУчет,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.Норматив КАК Норматив,
		|	Т.Кратность КАК Кратность,
		|	Т.ПравилоРаспределенияРасходовУпр КАК ПравилоРаспределенияРасходов,
		|	РаспределениеПрочихЗатрат.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям
		|ИЗ
		|	АктуальныеНормыПостоянныхИПеременныхРасходовПоПериодам КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
		|		ПО Т.ПравилоРаспределенияРасходовУпр = РаспределениеПрочихЗатрат.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИСТИНА КАК РеглУчет,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.Норматив КАК Норматив,
		|	Т.Кратность КАК Кратность,
		|	Т.ПравилоРаспределенияРасходовРегл КАК ПравилоРаспределенияРасходов,
		|	ПравилаРаспределенияРасходов.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям
		|ИЗ
		|	АктуальныеНормыПостоянныхИПеременныхРасходовПоПериодам КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК ПравилаРаспределенияРасходов
		|		ПО Т.ПравилоРаспределенияРасходовРегл = ПравилаРаспределенияРасходов.Ссылка
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ИСТИНА КАК РеглУчет,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	Т.СтатьяРасходов КАК СтатьяРасходов,
		|	Т.Норматив КАК Норматив,
		|	Т.Кратность КАК Кратность,
		|	Т.ПравилоРаспределенияРасходовРегл КАК ПравилоРаспределенияРасходов,
		|	РаспределениеПрочихЗатрат.БазаРаспределенияПоПартиям КАК БазаРаспределенияПоПартиям
		|ИЗ
		|	АктуальныеНормыПостоянныхИПеременныхРасходовПоПериодам КАК Т
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РаспределениеПрочихЗатрат
		|		ПО Т.ПравилоРаспределенияРасходовРегл = РаспределениеПрочихЗатрат.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	АВТОНОМЕРЗАПИСИ() КАК НомерЗаписи,
		|	Т.РеглУчет КАК РеглУчет,
		|	Т.Период КАК Период,
		|	Т.Организация КАК Организация,
		|	Т.Подразделение КАК Подразделение,
		|	Т.СтатьяРасходов,
		|	Т.СтатьяКалькуляции,
		|	Т.ПравилоРаспределенияРасходов,
		|	Т.БазаРаспределенияПоПартиям
		|ПОМЕСТИТЬ ВариантыРасходов
		|ИЗ
		|	АктуальныеНормативыПоПериодам КАК Т";
		
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Объем", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаОбъемУпаковки("СН.ЕдиницаИзмерения", "СН"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Вес", Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаВесУпаковки("СН.ЕдиницаИзмерения", "СН"));
	Запрос.УстановитьПараметр("АналитическийУчетПоГруппамПродукции", ПолучитьФункциональнуюОпцию("АналитическийУчетПоГруппамПродукции"));
	Запрос.УстановитьПараметр("Материалы", Материалы);
	
	Запрос.Выполнить();
	
	ПараметрыРасчета.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
	ПараметрыРасчета.Вставить("ДополнительныеПараметрыЗапросаЭтапа", Новый Структура);
	
	ИмяВременнойТаблицыГруппыОбъектов = "РегистрСведенийМножителиПартийИПолуфабрикатов";
	ТекстПолученияОбъектов = 
		"ВЫБРАТЬ
		|	Расходы.НомерЗаписи КАК Ссылка,
		|	ВЫБОР
		|		КОГДА РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат)
		|		ИНАЧЕ
		|			РеквизитыПравила.БазаРаспределенияПоПартиям
		|	КОНЕЦ КАК ТипБазыРаспределения,
		|	РеквизитыПравила.НастройкиБазыРаспределенияПоПартиям КАК НастройкиСхемы
		|ИЗ
		|	ВариантыРасходов КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК РеквизитыПравила
		|		ПО Расходы.ПравилоРаспределенияРасходов = РеквизитыПравила.Ссылка
		|			И НЕ РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Расходы.НомерЗаписи,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда) КАК ТипБазыРаспределения,
		|	РеквизитыПравила.НастройкиБазыРаспределенияПоПартиям КАК НастройкиСхемы
		|ИЗ
		|	ВариантыРасходов КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПравилаРаспределенияРасходов КАК РеквизитыПравила
		|		ПО Расходы.ПравилоРаспределенияРасходов = РеквизитыПравила.Ссылка
		|ГДЕ
		|	РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Расходы.НомерЗаписи КАК Ссылка,
		|	ВЫБОР
		|		КОГДА РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат)
		|		ИНАЧЕ
		|			РеквизитыПравила.БазаРаспределенияПоПартиям
		|	КОНЕЦ КАК ТипБазыРаспределения,
		|	РеквизитыПравила.НастройкиБазыРаспределенияПоПартиям КАК НастройкиСхемы
		|ИЗ
		|	ВариантыРасходов КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РеквизитыПравила
		|		ПО Расходы.ПравилоРаспределенияРасходов = РеквизитыПравила.Ссылка
		|			И НЕ РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Расходы.НомерЗаписи,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда) КАК ТипБазыРаспределения,
		|	РеквизитыПравила.НастройкиБазыРаспределенияПоПартиям КАК НастройкиСхемы
		|ИЗ
		|	ВариантыРасходов КАК Расходы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РаспределениеПрочихЗатрат КАК РеквизитыПравила
		|		ПО Расходы.ПравилоРаспределенияРасходов = РеквизитыПравила.Ссылка
		|ГДЕ
		|	РеквизитыПравила.БазаРаспределенияПоПартиям = ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)";

	НедоступныеПоляОтбора = Новый Массив;
	НедоступныеПоляОтбора.Добавить("НаправлениеДеятельности");
	ОписаниеГрупп = РасчетСебестоимостиПостатейныеЗатраты.ПолучитьОписаниеГруппОбъектовПоНастройкамОтбора(
		ТекстПолученияОбъектов, ПараметрыРасчета, ИмяВременнойТаблицыГруппыОбъектов, НедоступныеПоляОтбора);
	
	ТекстЗапросаОписаниеДанных = 
		"ВЫБРАТЬ
		|	ЛОЖЬ КАК РеглУчет,
		|	0 КАК НомерЗаписи,
		|	НЕОПРЕДЕЛЕНО КАК ПартияСпецификация,
		|	&ПустойУИДСтрока КАК КлючПартия,
		|	0 КАК Стоимость,
		|	0 КАК СтоимостьБезНДС,
		|	0 КАК СтоимостьРегл
		|ПОМЕСТИТЬ ОписаниеДанных
		|ГДЕ
		|	ЛОЖЬ";
	ГотовыеТекстыЗапросов = Новый Массив;
	ГотовыеТекстыЗапросов.Добавить(РасчетСебестоимостиПостатейныеЗатраты.ТекстПомещенияГруппыОбъектовВоВременнуюТаблицу(ИмяВременнойТаблицыГруппыОбъектов));
	ГотовыеТекстыЗапросов.Добавить(ТекстЗапросаОписаниеДанных);
	РасчетСебестоимостиПостатейныеЗатраты.ДополнитьТекстамиПоОписаниюГрупп(ГотовыеТекстыЗапросов, ОписаниеГрупп,
		"РегистрСведений.МножителиПартийИПолуфабрикатов");
	
	ТекстыФормированияИтоговойТаблицы = Новый Массив;
	ТекстБазаРаспределения = 
		"ВЫБРАТЬ
		|	Т.РеглУчет КАК РеглУчет,
		|	Т.НомерЗаписи,
		|	Т.ПартияСпецификация,
		|	Т.КлючПартия,
		|	Т.Стоимость,
		|	Т.СтоимостьБезНДС,
		|	Т.СтоимостьРегл
		|ПОМЕСТИТЬ БазаРаспределения
		|ИЗ
		|	ОписаниеДанных КАК Т";
	ТекстыФормированияИтоговойТаблицы.Добавить(ТекстБазаРаспределения);
	
	ТекстБазаРаспределения = СтрЗаменить(ТекстБазаРаспределения, "ПОМЕСТИТЬ БазаРаспределения", "");
	Для Каждого ОписаниеГруппы Из ОписаниеГрупп Цикл
		ТекстыФормированияИтоговойТаблицы.Добавить(
			СтрЗаменить(ТекстБазаРаспределения, 
				"ОписаниеДанных", СтрШаблон("ТаблицаДанных%1",
				Формат(ОписаниеГруппы.НомерГруппы, "ЧН=0; ЧГ=0"))));
	КонецЦикла;
	
	ГотовыеТекстыЗапросов.Добавить(СтрСоединить(ТекстыФормированияИтоговойТаблицы, ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()));
		
	ТекстПлановыеПрочиеЗатраты = 
		"ВЫБРАТЬ
		|	БазаРаспределения.ПартияСпецификация,
		|	БазаРаспределения.КлючПартия КАК КлючПартия,
		|	&Калькуляция КАК Калькуляция,
		|	АктуальныеНормативыПоПериодам.Подразделение,
		|	АктуальныеНормативыПоПериодам.СтатьяКалькуляции,
		|	АктуальныеНормативыПоПериодам.СтатьяРасходов,
		|	АктуальныеНормативыПоПериодам.Период,
		|	СУММА(ВЫБОР КОГДА АктуальныеНормативыПоПериодам.РеглУчет
		|		ТОГДА 0
		|		ИНАЧЕ ВЫРАЗИТЬ(БазаРаспределения.Стоимость
		|				* АктуальныеНормативыПоПериодам.Норматив / АктуальныеНормативыПоПериодам.Кратность КАК ЧИСЛО(31, 2))
		|	КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР КОГДА АктуальныеНормативыПоПериодам.РеглУчет
		|		ТОГДА 0
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(БазаРаспределения.СтоимостьБезНДС
		|				* АктуальныеНормативыПоПериодам.Норматив / АктуальныеНормативыПоПериодам.Кратность КАК ЧИСЛО(31, 2))
		|	КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(ВЫБОР 
		|		КОГДА НЕ АктуальныеНормативыПоПериодам.РеглУчет ИЛИ КурсыВалютУУ.КурсЗнаменатель = 0
		|			ТОГДА 0
		|		ИНАЧЕ
		|			ВЫРАЗИТЬ(БазаРаспределения.СтоимостьБезНДС
		|				* АктуальныеНормативыПоПериодам.Норматив / АктуальныеНормативыПоПериодам.Кратность
		|				* КурсыВалютУУ.КурсЧислитель / КурсыВалютУУ.КурсЗнаменатель КАК ЧИСЛО(31, 2))
		|	КОНЕЦ) КАК СтоимостьРегл
		|ПОМЕСТИТЬ ПлановыеПрочиеЗатраты
		|ИЗ
		|	АктуальныеНормативыПоПериодам КАК АктуальныеНормативыПоПериодам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВариантыРасходов КАК ВариантыРасходов
		|		ПО АктуальныеНормативыПоПериодам.Период = ВариантыРасходов.Период
		|			И АктуальныеНормативыПоПериодам.Организация = ВариантыРасходов.Организация
		|			И АктуальныеНормативыПоПериодам.Подразделение = ВариантыРасходов.Подразделение
		|			И АктуальныеНормативыПоПериодам.СтатьяРасходов = ВариантыРасходов.СтатьяРасходов
		|			И АктуальныеНормативыПоПериодам.СтатьяКалькуляции = ВариантыРасходов.СтатьяКалькуляции
		|			И АктуальныеНормативыПоПериодам.РеглУчет = ВариантыРасходов.РеглУчет
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БазаРаспределения КАК БазаРаспределения
		|		ПО ВариантыРасходов.НомерЗаписи = БазаРаспределения.НомерЗаписи
		|			И ВариантыРасходов.РеглУчет = БазаРаспределения.РеглУчет
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУУ
		|		ПО (КурсыВалютУУ.ВидВалюты = ""ВалютаУпрУчета"")
		|
		|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютРегл
		|		ПО (КурсыВалютРегл.ВидВалюты = ""ВалютаРеглУчета"")
		|
		|СГРУППИРОВАТЬ ПО
		|	БазаРаспределения.КлючПартия,
		|	БазаРаспределения.ПартияСпецификация,
		|	АктуальныеНормативыПоПериодам.Подразделение,
		|	АктуальныеНормативыПоПериодам.СтатьяКалькуляции,
		|	АктуальныеНормативыПоПериодам.СтатьяРасходов,
		|	АктуальныеНормативыПоПериодам.Период
		|";
		
	ГотовыеТекстыЗапросов.Добавить(ТекстПлановыеПрочиеЗатраты);
	
	Запрос.Текст = СтрСоединить(ГотовыеТекстыЗапросов, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Для Каждого ДопПараметр Из ПараметрыРасчета.ДополнительныеПараметрыЗапросаЭтапа Цикл
		Запрос.УстановитьПараметр(ДопПараметр.Ключ, ДопПараметр.Значение);
	КонецЦикла;
	Запрос.УстановитьПараметр("ПустойУИДСтрока", ПараметрыРасчета.ПустойУИДСтрока);
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	РасчетСебестоимостиПрикладныеАлгоритмы.УничтожитьВременныеТаблицы(МенеджерВТ,
		"МножителиПартийИПолуфабрикатов,МатериальныеЗатратыВключаяПолуфабрикаты,БазаРаспределения,ДанныеПоПродукции,ДанныеПоТрудозатратам,
		|ПериодыПоМесяцам,АктуальныеДокументыПоПериодам,АктуальныеНормативыПоПериодам,ПартииИзделий, ВариантыРасходов,ДанныеПоМатериальнымЗатраты,
		|ПродукцияПредварительная, АктуальныеПериодыУстановкиЦен, ЦеныНоменклатурыПредварительная, ЦеныПродукции, " + ИмяВременнойТаблицыГруппыОбъектов);
	
КонецПроцедуры

Функция ШаблонТекстаБазыРаспределения(ПоказательБазыРаспределения) Экспорт
	
	ПараметрыБазы = ПараметрыБазыРаспределения(ПоказательБазыРаспределения);
	
	Шаблон = 
		"ВЫБРАТЬ
		|	ДД.РеглУчет				КАК РеглУчет,
		|	ДД.НомерЗаписи,
		|	ИсточникБазы.ПартияСпецификация,
		|	ИсточникБазы.КлючПартия КАК КлючПартия,
		|	СУММА(&ЗначениеСНДС)	КАК Стоимость,
		|	СУММА(&ЗначениеБезНДС)	КАК СтоимостьБезНДС,
		|	СУММА(&ЗначениеРегл)	КАК СтоимостьРегл
		|ПОМЕСТИТЬ ТаблицаДанных
		|ИЗ
		|	ВариантыРасходов КАК ДД
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведенийМножителиПартийИПолуфабрикатов КАК Группа
		|		ПО ДД.НомерЗаписи = Группа.Объект
		|			И Группа.НомерГруппы = &НомерГруппы
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИмяТаблицыБазыРаспределения КАК ИсточникБазы
		|		ПО ДД.Период = ИсточникБазы.Период
		|			И ДД.Организация = ИсточникБазы.Организация
		|			И ДД.Подразделение = ИсточникБазы.Подразделение
		|			И &УсловиеСоединения
		|
		|СГРУППИРОВАТЬ ПО
		|	ДД.РеглУчет,
		|	ДД.НомерЗаписи,
		|	ИсточникБазы.ПартияСпецификация,
		|	ИсточникБазы.КлючПартия";

	Шаблон = СтрЗаменить(Шаблон, "ИмяТаблицыБазыРаспределения", ПараметрыБазы.ИмяТаблицыБазыРаспределения);
	Шаблон = СтрЗаменить(Шаблон, "&ЗначениеСНДС", ПараметрыБазы.ЗначениеСНДС);
	Шаблон = СтрЗаменить(Шаблон,
		"&ЗначениеБезНДС",
		?(ПараметрыБазы.Свойство("ЗначениеБезНДС"), ПараметрыБазы.ЗначениеБезНДС, ПараметрыБазы.ЗначениеСНДС));
	Шаблон = СтрЗаменить(Шаблон,
		"&ЗначениеРегл",
		?(ПараметрыБазы.Свойство("ЗначениеРегл"), ПараметрыБазы.ЗначениеРегл, ПараметрыБазы.ЗначениеСНДС));
	
	Возврат Шаблон;
	
КонецФункции

Функция ПараметрыБазыРаспределения(ПоказательБазыРаспределения)
	
	Параметры = Новый Структура;
	
	Если ПоказательБазыРаспределения = "МатериальныеЗатраты" Тогда
		
		Параметры.Вставить("ИмяТаблицыБазыРаспределения", "ДанныеПоМатериальнымЗатраты");
		Параметры.Вставить("ЗначениеСНДС", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат)
											|		ТОГДА ИсточникБазы.Стоимость
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
											|		ТОГДА ИсточникБазы.Стоимость
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Вес
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Объем
											|КОНЕЦ");
		Параметры.Вставить("ЗначениеБезНДС", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат)
											|		ТОГДА ИсточникБазы.СтоимостьБезНДС
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
											|		ТОГДА ИсточникБазы.СтоимостьБезНДС
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Вес
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Объем
											|КОНЕЦ");
		Параметры.Вставить("ЗначениеРегл", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхЗатрат)
											|		ТОГДА ИсточникБазы.СтоимостьРегл
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
											|		ТОГДА ИсточникБазы.СтоимостьРегл
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Вес
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемУказанныхМатериалов)
											|		ТОГДА ИсточникБазы.Объем
											|КОНЕЦ");
		
	ИначеЕсли ПоказательБазыРаспределения = "Трудозатраты" Тогда
		
		Параметры.Вставить("ИмяТаблицыБазыРаспределения", "ДанныеПоТрудозатратам");
		Параметры.Вставить("ЗначениеСНДС", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда)
											|		ТОГДА ИсточникБазы.Стоимость
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
											|		ТОГДА ИсточникБазы.Стоимость
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
											|		ТОГДА ИсточникБазы.НормативнаяСтоимость
											|КОНЕЦ");
		Параметры.Вставить("ЗначениеРегл", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаРасходовНаОплатуТруда)
											|		ТОГДА ИсточникБазы.СтоимостьРегл
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.СуммаМатериальныхИТрудозатрат)
											|		ТОГДА ИсточникБазы.СтоимостьРегл
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоРаботУказанныхВидов)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.НормативыОплатыТруда)
											|		ТОГДА ИсточникБазы.НормативнаяСтоимость
											|КОНЕЦ");
		
	ИначеЕсли ПоказательБазыРаспределения = "Продукция" Тогда
		
		
		Параметры.Вставить("ИмяТаблицыБазыРаспределения", "ДанныеПоПродукции");
		Параметры.Вставить("ЗначениеСНДС", "ВЫБОР ДД.БазаРаспределенияПоПартиям
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукции)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукции)
											|		ТОГДА ИсточникБазы.Объем
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукции)
											|		ТОГДА ИсточникБазы.Вес
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукции)
											|		ТОГДА ИсточникБазы.Стоимость
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.КоличествоПродукцииСУчетомБудущихВыпусков)
											|		ТОГДА ИсточникБазы.Количество
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ОбъемПродукцииСУчетомБудущихВыпусков)
											|		ТОГДА ИсточникБазы.Объем
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ВесПродукцииСУчетомБудущихВыпусков)
											|		ТОГДА ИсточникБазы.Вес
											|	КОГДА ЗНАЧЕНИЕ(Перечисление.ТипыБазыРаспределенияРасходов.ПлановаяСтоимостьПродукцииСУчетомБудущихВыпусков)
											|		ТОГДА ИсточникБазы.Стоимость
											|КОНЕЦ");

	КонецЕсли;
	
	Возврат Параметры;
	
КонецФункции


Процедура РассчитатьПредварительнуюСтоимость(ПараметрыРасчета, МенеджерВТ)
	
	УдалитьТаблицуПрочихЗатрат = Ложь;
	
	Если МенеджерВТ.Таблицы.Найти("ПлановыеПрочиеЗатраты") = Неопределено Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
		
		Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 0
			|	*
			|ПОМЕСТИТЬ ПлановыеПрочиеЗатраты
			|ИЗ
			|	РегистрНакопления.ПлановыеПрочиеЗатраты";
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		УдалитьТаблицуПрочихЗатрат = Истина;
		
	КонецЕсли;
	
	РассчитатьЦеныМатериалов(ПараметрыРасчета, МенеджерВТ);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	МатериальныеЗатраты.Калькуляция КАК Калькуляция,
		|	МатериальныеЗатраты.ПартияСпецификация КАК ПартияСпецификация,
		|	МатериальныеЗатраты.КлючПартия КАК КлючПартия,
		|	МатериальныеЗатраты.Номенклатура КАК Номенклатура,
		|	МатериальныеЗатраты.Характеристика КАК Характеристика,
		|	МатериальныеЗатраты.Подразделение КАК Подразделение,
		|	МатериальныеЗатраты.Период КАК Период,
		|	МатериальныеЗатраты.СтатьяКалькуляции КАК СтатьяКалькуляции,
		|	МатериальныеЗатраты.Количество КАК Количество,
		|	МатериальныеЗатраты.ЭтоПолуфабрикат КАК ЭтоПолуфабрикат,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаСтоимость
		|		ИНАЧЕ МатериальныеЗатраты.Стоимость
		|	КОНЕЦ КАК Стоимость,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаСтоимостьБезНДС
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьБезНДС
		|	КОНЕЦ КАК СтоимостьБезНДС,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаСтоимостьРегл
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьРегл
		|	КОНЕЦ КАК СтоимостьРегл,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаТрудозатраты
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Трудозатраты,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаТрудозатратыРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ТрудозатратыРегл,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПостоянные
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПостоянные,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПостоянныеБезНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПостоянныеБезНДС,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПостоянныеРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПостоянныеРегл,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПеременные
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПеременные,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПеременныеБезНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПеременныеБезНДС,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаПостатейныеПеременныеРегл
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК ПостатейныеПеременныеРегл,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаСтоимостьЗабалансовая
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьЗабалансовая
		|	КОНЕЦ КАК СтоимостьЗабалансовая,
		|	ВЫБОР
		|		КОГДА МатериальныеЗатраты.ЭтоПолуфабрикат
		|			ТОГДА МатериальныеЗатраты.Количество * ЦеныМатериалов.ЦенаСтоимостьЗабалансоваяРегл
		|		ИНАЧЕ МатериальныеЗатраты.СтоимостьЗабалансоваяРегл
		|	КОНЕЦ КАК СтоимостьЗабалансоваяРегл,
		|	МатериальныеЗатраты.ПериодЗатрат КАК ПериодЗатрат
		|ПОМЕСТИТЬ МатериальныеЗатратыВключаяПолуфабрикаты
		|ИЗ
		|	МатериальныеЗатраты КАК МатериальныеЗатраты
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныМатериалов КАК ЦеныМатериалов
		|		ПО МатериальныеЗатраты.Калькуляция = ЦеныМатериалов.Калькуляция
		|			И МатериальныеЗатраты.Номенклатура = ЦеныМатериалов.Номенклатура
		|			И МатериальныеЗатраты.Характеристика = ЦеныМатериалов.Характеристика
		|			И (МатериальныеЗатраты.ЭтоПолуфабрикат)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	МатериальныеЗатраты.ПартияСпецификация,
		|	МатериальныеЗатраты.Калькуляция
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена
		|ПОМЕСТИТЬ ВТПродукция
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		// Затраты по выпущенным партиям считаются при формировании отчета ПлановаяСебестоимостьПродукции
		|	И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период КАК Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция КАК Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение КАК Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат КАК Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок КАК Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен КАК РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК Материальные,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеБезНДС,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеРегл,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Трудозатраты * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК Трудозатраты,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ТрудозатратыРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ТрудозатратыРегл,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянные * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПостоянные,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянныеБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПостоянныеБезНДС,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянныеРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПостоянныеРегл,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременные * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПеременные,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременныеБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПеременныеБезНДС,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременныеРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК ПостатейныеПеременныеРегл,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансовая * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеЗабаланс,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансоваяРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель) КАК МатериальныеЗабалансРегл,
		|	МножителиПартийИПолуфабрикатов.Числитель КАК Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель КАК Знаменатель
		|ПОМЕСТИТЬ Затраты
		|ИЗ
		|	МатериальныеЗатратыВключаяПолуфабрикаты КАК МатериальныеЗатратыВключаяПолуфабрикаты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = МатериальныеЗатратыВключаяПолуфабрикаты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = МатериальныеЗатратыВключаяПолуфабрикаты.ПартияСпецификация
		|			И МножителиПартийИПолуфабрикатов.КлючПартия = МатериальныеЗатратыВключаяПолуфабрикаты.КлючПартия
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		// Затраты по выпущенным партиям считаются при формировании отчета ПлановаяСебестоимостьПродукции
		|	И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена,
		|	0,
		|	0,
		|	0,
		|	ПлановыеТрудозатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель),
		|	ПлановыеТрудозатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель),
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель
		|ИЗ
		|	ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеТрудозатраты.ПартияСпецификация
		|			И МножителиПартийИПолуфабрикатов.КлючПартия = ПлановыеТрудозатраты.КлючПартия
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		// Затраты по выпущенным партиям считаются при формировании отчета ПлановаяСебестоимостьПродукции
		|	И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена КАК ПартияВыпущена,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.Стоимость * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл * (МножителиПартийИПолуфабрикатов.Числитель / МножителиПартийИПолуфабрикатов.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	0,
		|	0,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель
		|ИЗ
		|	ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ПО МножителиПартийИПолуфабрикатов.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|			И МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|			И МножителиПартийИПолуфабрикатов.КлючПартия = ПлановыеПрочиеЗатраты.КлючПартия
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И НЕ МножителиПартийИПолуфабрикатов.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		// Затраты по выпущенным партиям считаются при формировании отчета ПлановаяСебестоимостьПродукции
		|	И НЕ МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.КлючПартия КАК КлючПартия,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	ВТПродукция.ПартияВыпущена КАК ПартияВыпущена,
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.Трудозатраты * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ТрудозатратыРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянные * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянныеБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПостоянныеРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременные * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременныеБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.ПостатейныеПеременныеРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансовая * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	МатериальныеЗатратыВключаяПолуфабрикаты.СтоимостьЗабалансоваяРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериальныеЗатратыВключаяПолуфабрикаты КАК МатериальныеЗатратыВключаяПолуфабрикаты
		|		ПО ВТПродукция.Калькуляция = МатериальныеЗатратыВключаяПолуфабрикаты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = МатериальныеЗатратыВключаяПолуфабрикаты.ПартияСпецификация
		|			И ВТПродукция.КлючПартия = МатериальныеЗатратыВключаяПолуфабрикаты.КлючПартия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.КлючПартия КАК КлючПартия,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	ВТПродукция.ПартияВыпущена КАК ПартияВыпущена,
		|	0,
		|	0,
		|	0,
		|	ПлановыеТрудозатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	ПлановыеТрудозатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель),
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|		ПО ВТПродукция.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = ПлановыеТрудозатраты.ПартияСпецификация
		|			И ВТПродукция.КлючПартия = ПлановыеТрудозатраты.КлючПартия
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПродукция.Период,
		|	ВТПродукция.Калькуляция,
		|	ВТПродукция.ПартияСпецификацияПродукции,
		|	ВТПродукция.Продукция,
		|	ВТПродукция.ХарактеристикаПродукции,
		|	ВТПродукция.Назначение,
		|	ВТПродукция.ПартияСпецификацияПолуфабриката,
		|	ВТПродукция.КлючПартия КАК КлючПартия,
		|	ВТПродукция.Полуфабрикат,
		|	ВТПродукция.ХарактеристикаПолуфабриката,
		|	ВТПродукция.Порядок,
		|	ВТПродукция.КоличествоПолуфабриката,
		|	ВТПродукция.РасчетЗавершен,
		|	ВТПродукция.ПартияВыпущена КАК ПартияВыпущена,
		|	0,
		|	0,
		|	0,
		|	0,
		|	0,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА ПлановыеПрочиеЗатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|		ИНАЧЕ 0
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.Стоимость * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|			ТОГДА 0
		|		ИНАЧЕ ПлановыеПрочиеЗатраты.СтоимостьРегл * (ВТПродукция.Числитель / ВТПродукция.Знаменатель)
		|	КОНЕЦ,
		|	0,
		|	0,
		|	ВТПродукция.Числитель,
		|	ВТПродукция.Знаменатель
		|ИЗ
		|	ВТПродукция КАК ВТПродукция
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|		ПО ВТПродукция.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|			И ВТПродукция.ПартияСпецификацияПродукции = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|			И ВТПродукция.КлючПартия = ПлановыеПрочиеЗатраты.КлючПартия
		|;
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Затраты.Продукция КАК Продукция,
		|	Затраты.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Затраты.Назначение КАК Назначение,
		|	Затраты.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Затраты.КлючПартия КАК КлючПартия,
		|	Затраты.Полуфабрикат КАК Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Затраты.Порядок КАК Порядок
		|ПОМЕСТИТЬ СводныеМножители
		|ИЗ
		|	Затраты КАК Затраты
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Продукция,
		|	ПартияСпецификацияПродукции,
		|	Калькуляция,
		|	ХарактеристикаПродукции,
		|	Назначение,
		|	ПартияСпецификацияПолуфабриката,
		|	КлючПартия,
		|	Полуфабрикат,
		|	ХарактеристикаПолуфабриката,
		|	Порядок
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Период КАК Период,
		|	Затраты.Продукция КАК Продукция,
		|	Затраты.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	Затраты.Назначение КАК Назначение,
		|	СУММА(Затраты.Трудозатраты) КАК Трудозатраты,
		|	СУММА(Затраты.ТрудозатратыРегл) КАК ТрудозатратыРегл,
		|	СУММА(Затраты.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(Затраты.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРегл,
		|	СУММА(Затраты.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(Затраты.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(Затраты.Материальные + Затраты.Трудозатраты + Затраты.ПостатейныеПеременные + Затраты.ПостатейныеПостоянные) КАК Стоимость,
		|	СУММА(Затраты.МатериальныеБезНДС + Затраты.Трудозатраты + Затраты.ПостатейныеПеременныеБезНДС + Затраты.ПостатейныеПостоянныеБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Затраты.МатериальныеРегл + Затраты.ТрудозатратыРегл + Затраты.ПостатейныеПеременныеРегл + Затраты.ПостатейныеПостоянныеРегл) КАК СтоимостьРегл,
		|	СУММА(Затраты.МатериальныеЗабаланс) КАК СтоимостьЗабалансовая,
		|	СУММА(Затраты.МатериальныеЗабалансРегл) КАК СтоимостьЗабалансоваяРегл,
		|	Затраты.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Затраты.КлючПартия КАК КлючПартия,
		|	Затраты.Полуфабрикат КАК Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	Затраты.Порядок КАК Порядок,
		|	Затраты.Числитель КАК Числитель,
		|	Затраты.Знаменатель КАК Знаменатель,
		|	Затраты.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	Затраты.РасчетЗавершен КАК РасчетЗавершен,
		|	Затраты.ПартияВыпущена КАК ПартияВыпущена
		|ПОМЕСТИТЬ МножителиПартийИПолуфабрикатов
		|ИЗ
		|	Затраты КАК Затраты
		|
		|СГРУППИРОВАТЬ ПО
		|	Затраты.Период,
		|	Затраты.Продукция,
		|	Затраты.ПартияСпецификацияПродукции,
		|	Затраты.Калькуляция,
		|	Затраты.ХарактеристикаПродукции,
		|	Затраты.Назначение,
		|	Затраты.ПартияСпецификацияПолуфабриката,
		|	Затраты.КлючПартия,
		|	Затраты.Полуфабрикат,
		|	Затраты.ХарактеристикаПолуфабриката,
		|	Затраты.Порядок,
		|	Затраты.Знаменатель,
		|	Затраты.Числитель,
		|	Затраты.КоличествоПолуфабриката,
		|	Затраты.РасчетЗавершен,
		|	Затраты.ПартияВыпущена
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.Трудозатраты,
		|	МножителиПартийИПолуфабрикатов.ТрудозатратыРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл,
		|	МножителиПартийИПолуфабрикатов.Стоимость,
		|	МножителиПартийИПолуфабрикатов.СтоимостьБезНДС,
		|	МножителиПартийИПолуфабрикатов.СтоимостьРегл,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|	И МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	МножителиПартийИПолуфабрикатов.Период,
		|	МножителиПартийИПолуфабрикатов.Продукция,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции,
		|	МножителиПартийИПолуфабрикатов.Калькуляция,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции,
		|	МножителиПартийИПолуфабрикатов.Назначение,
		|	МножителиПартийИПолуфабрикатов.Трудозатраты,
		|	МножителиПартийИПолуфабрикатов.ТрудозатратыРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПеременныеРегл,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянные,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеБезНДС,
		|	МножителиПартийИПолуфабрикатов.ПостатейныеПостоянныеРегл,
		|	МножителиПартийИПолуфабрикатов.Стоимость,
		|	МножителиПартийИПолуфабрикатов.СтоимостьБезНДС,
		|	МножителиПартийИПолуфабрикатов.СтоимостьРегл,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансовая,
		|	МножителиПартийИПолуфабрикатов.СтоимостьЗабалансоваяРегл,
		|	МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	МножителиПартийИПолуфабрикатов.Полуфабрикат,
		|	МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.Порядок,
		|	МножителиПартийИПолуфабрикатов.Числитель,
		|	МножителиПартийИПолуфабрикатов.Знаменатель,
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката,
		|	МножителиПартийИПолуфабрикатов.РасчетЗавершен,
		|	МножителиПартийИПолуфабрикатов.ПартияВыпущена
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
		|		ЛЕВОЕ СОЕДИНЕНИЕ СводныеМножители КАК СводныеМножители
		|		ПО СводныеМножители.Калькуляция = МножителиПартийИПолуфабрикатов.Калькуляция 
		|			И СводныеМножители.ПартияСпецификацияПродукции = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПродукции 
		|			И СводныеМножители.Продукция = МножителиПартийИПолуфабрикатов.Продукция 
		|			И СводныеМножители.ХарактеристикаПродукции = МножителиПартийИПолуфабрикатов.ХарактеристикаПродукции 
		|			И СводныеМножители.Назначение = МножителиПартийИПолуфабрикатов.Назначение 
		|			И СводныеМножители.ПартияСпецификацияПолуфабриката = МножителиПартийИПолуфабрикатов.ПартияСпецификацияПолуфабриката 
		|			И СводныеМножители.КлючПартия = МножителиПартийИПолуфабрикатов.КлючПартия
		|			И СводныеМножители.Полуфабрикат = МножителиПартийИПолуфабрикатов.Полуфабрикат 
		|			И СводныеМножители.ХарактеристикаПолуфабриката = МножителиПартийИПолуфабрикатов.ХарактеристикаПолуфабриката 
		|			И СводныеМножители.Порядок = МножителиПартийИПолуфабрикатов.Порядок 
		|ГДЕ
		|	МножителиПартийИПолуфабрикатов.КоличествоПолуфабриката = 0
		|	И МножителиПартийИПолуфабрикатов.Калькуляция = &Калькуляция
		|	И СводныеМножители.Калькуляция ЕСТЬ NULL
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СводныеМножители
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ЦеныМатериалов
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ Затраты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВТПродукция";
	
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	
	Запрос.ВыполнитьПакет();
	
	Если УдалитьТаблицуПрочихЗатрат Тогда
		
		Запрос.Текст =
			"УНИЧТОЖИТЬ ПлановыеПрочиеЗатраты";
		
		УстановитьПривилегированныйРежим(Истина);
		Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьЦеныМатериалов(ПараметрыРасчета, МенеджерВТ)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	       
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Множители.Калькуляция,
		|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	Множители.КлючПартия КАК КлючПартия,
		|	Множители.Полуфабрикат КАК Номенклатура,
		|	Множители.ХарактеристикаПолуфабриката КАК Характеристика,
		|	Множители.Знаменатель КАК Знаменатель,
		|	МАКСИМУМ(Множители.Порядок) КАК Порядок,
		|	СУММА(Множители.КоличествоПолуфабриката) КАК КоличествоПолуфабриката,
		|	СУММА(Множители.Числитель) КАК Числитель,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.Стоимость / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Стоимость,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьБезНДС / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьБезНДС,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьРегл,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.Трудозатраты / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК Трудозатраты,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ТрудозатратыРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ТрудозатратыРегл,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПостоянные / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПостоянные,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПостоянныеБезНДС / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПостоянныеРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПеременные / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПеременные,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПеременныеБезНДС / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.ПостатейныеПеременныеРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК ПостатейныеПеременныеРегл,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьЗабалансовая / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьЗабалансовая,
		|	СУММА(ВЫБОР
		|			КОГДА Множители.ПартияВыпущена
		|				ТОГДА ВЫРАЗИТЬ(Множители.СтоимостьЗабалансоваяРегл / (Множители.Числитель / Множители.Знаменатель) КАК ЧИСЛО(31,2))
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ НаборПолуфабрикатов
		|ИЗ
		|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК Множители
		|ГДЕ
		|	Множители.Калькуляция = &Калькуляция
		|	И НЕ Множители.Полуфабрикат = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
		|
		|СГРУППИРОВАТЬ ПО
		|	Множители.ХарактеристикаПолуфабриката,
		|	Множители.ПартияСпецификацияПолуфабриката,
		|	Множители.КлючПартия,
		|	Множители.Калькуляция,
		|	Множители.Полуфабрикат,
		|	Множители.Знаменатель
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|	НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|	НаборПолуфабрикатов.Знаменатель КАК Знаменатель,
		|	НаборПолуфабрикатов.Порядок КАК Порядок,
		|	НаборПолуфабрикатов.КоличествоПолуфабриката КАК Количество,
		|	НаборПолуфабрикатов.Числитель КАК Числитель,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияПолуфабриката,
		|	НаборПолуфабрикатов.КлючПартия КАК КлючПартия
		|ИЗ
		|	НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|
		|УПОРЯДОЧИТЬ ПО
		|	НаборПолуфабрикатов.Порядок УБЫВ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|	НаборПолуфабрикатов.КлючПартия КАК КлючПартия,
		|	НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|	НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|	СУММА(НаборПолуфабрикатов.Стоимость) КАК Стоимость,
		|	СУММА(НаборПолуфабрикатов.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(НаборПолуфабрикатов.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(НаборПолуфабрикатов.Трудозатраты) КАК Трудозатраты,
		|	СУММА(НаборПолуфабрикатов.ТрудозатратыРегл) КАК ТрудозатратыРегл,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(НаборПолуфабрикатов.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРегл,
		|	СУММА(НаборПолуфабрикатов.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
		|	СУММА(НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ПолуфабрикатыСводно
		|ИЗ
		|	НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|
		|СГРУППИРОВАТЬ ПО
		|	НаборПолуфабрикатов.Характеристика,
		|	НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|	НаборПолуфабрикатов.КлючПартия,
		|	НаборПолуфабрикатов.Калькуляция,
		|	НаборПолуфабрикатов.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Затраты.Калькуляция КАК Калькуляция,
		|	Затраты.Номенклатура КАК Номенклатура,
		|	Затраты.Характеристика КАК Характеристика,
		|	Затраты.ПартияСпецификацияПолуфабриката КАК ПартияПолуфабриката,
		|	Затраты.КлючПартия,
		|	Затраты.Материал КАК Материал,
		|	Затраты.ХарактеристикаМатериала КАК ХарактеристикаМатериала,
		|	СУММА(Затраты.КоличествоМатериала) КАК КоличествоМатериала,
		|	СУММА(Затраты.Стоимость) КАК Стоимость,
		|	СУММА(Затраты.СтоимостьБезНДС) КАК СтоимостьБезНДС,
		|	СУММА(Затраты.СтоимостьРегл) КАК СтоимостьРегл,
		|	СУММА(Затраты.Трудозатраты) КАК Трудозатраты,
		|	СУММА(Затраты.ТрудозатратыРегл) КАК ТрудозатратыРегл,
		|	СУММА(Затраты.ПостатейныеПостоянные) КАК ПостатейныеПостоянные,
		|	СУММА(Затраты.ПостатейныеПостоянныеБезНДС) КАК ПостатейныеПостоянныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПостоянныеРегл) КАК ПостатейныеПостоянныеРегл,
		|	СУММА(Затраты.ПостатейныеПеременные) КАК ПостатейныеПеременные,
		|	СУММА(Затраты.ПостатейныеПеременныеБезНДС) КАК ПостатейныеПеременныеБезНДС,
		|	СУММА(Затраты.ПостатейныеПеременныеРегл) КАК ПостатейныеПеременныеРегл,
		|	СУММА(Затраты.СтоимостьЗабалансовая) КАК СтоимостьЗабалансовая,
		|	СУММА(Затраты.СтоимостьЗабалансоваяРегл) КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	(ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция КАК Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура КАК Номенклатура,
		|		НаборПолуфабрикатов.Характеристика КАК Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
		|		НаборПолуфабрикатов.КлючПартия КАК КлючПартия,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Количество
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК КоличествоМатериала,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.Стоимость
		|		КОНЕЦ КАК Стоимость,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.СтоимостьБезНДС
		|		КОНЕЦ КАК СтоимостьБезНДС,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА 0
		|			ИНАЧЕ ПМЗ.СтоимостьРегл
		|		КОНЕЦ КАК СтоимостьРегл,
		|		0 КАК Трудозатраты,
		|		0 КАК ТрудозатратыРегл,
		|		0 КАК ПостатейныеПостоянные,
		|		0 КАК ПостатейныеПостоянныеБезНДС,
		|		0 КАК ПостатейныеПостоянныеРегл,
		|		0 КАК ПостатейныеПеременные,
		|		0 КАК ПостатейныеПеременныеБезНДС,
		|		0 КАК ПостатейныеПеременныеРегл,
		|		0 КАК СтоимостьЗабалансовая,
		|		0 КАК СтоимостьЗабалансоваяРегл,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Номенклатура
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ КАК Материал,
		|		ВЫБОР
		|			КОГДА ПМЗ.ЭтоПолуфабрикат
		|				ТОГДА ПМЗ.Характеристика
		|			ИНАЧЕ НЕОПРЕДЕЛЕНО
		|		КОНЕЦ КАК ХарактеристикаМатериала
		|	ИЗ
		|		ПолуфабрикатыСводно КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ МатериальныеЗатраты КАК ПМЗ
		|			ПО НаборПолуфабрикатов.Калькуляция = ПМЗ.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПМЗ.ПартияСпецификация
		|				И НаборПолуфабрикатов.КлючПартия = ПМЗ.КлючПартия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		НаборПолуфабрикатов.КлючПартия КАК КлючПартия,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ПлановыеТрудозатраты.Стоимость,
		|		ПлановыеТрудозатраты.СтоимостьРегл,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		ПолуфабрикатыСводно КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеТрудозатраты КАК ПлановыеТрудозатраты
		|			ПО НаборПолуфабрикатов.Калькуляция = ПлановыеТрудозатраты.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеТрудозатраты.ПартияСпецификация
		|				И НаборПолуфабрикатов.КлючПартия = ПлановыеТрудозатраты.КлючПартия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		НаборПолуфабрикатов.КлючПартия КАК КлючПартия,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		0,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|				ТОГДА ПлановыеПрочиеЗатраты.Стоимость
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПостоянные,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|				ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПостоянныеБезНДС,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Постоянные)
		|				ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПостоянныеРегл,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|				ТОГДА ПлановыеПрочиеЗатраты.Стоимость
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПеременные,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|				ТОГДА ПлановыеПрочиеЗатраты.СтоимостьБезНДС
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПеременныеБезНДС,
		|		ВЫБОР
		|			КОГДА ПлановыеПрочиеЗатраты.СтатьяРасходов.ХарактерПроизводственныхЗатрат = ЗНАЧЕНИЕ(Перечисление.ХарактерПроизводственныхЗатрат.Переменные)
		|				ТОГДА ПлановыеПрочиеЗатраты.СтоимостьРегл
		|			ИНАЧЕ
		|				0
		|		КОНЕЦ КАК ПостатейныеПеременныеРегл,
		|		0,
		|		0,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПлановыеПрочиеЗатраты КАК ПлановыеПрочиеЗатраты
		|			ПО НаборПолуфабрикатов.Калькуляция = ПлановыеПрочиеЗатраты.Калькуляция
		|				И НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката = ПлановыеПрочиеЗатраты.ПартияСпецификация
		|				И НаборПолуфабрикатов.КлючПартия = ПлановыеПрочиеЗатраты.КлючПартия
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		НаборПолуфабрикатов.Калькуляция,
		|		НаборПолуфабрикатов.Номенклатура,
		|		НаборПолуфабрикатов.Характеристика,
		|		НаборПолуфабрикатов.ПартияСпецификацияПолуфабриката,
		|		НаборПолуфабрикатов.КлючПартия,
		|		0,
		|		НаборПолуфабрикатов.Стоимость,
		|		НаборПолуфабрикатов.СтоимостьБезНДС,
		|		НаборПолуфабрикатов.СтоимостьРегл,
		|		НаборПолуфабрикатов.Трудозатраты,
		|		НаборПолуфабрикатов.ТрудозатратыРегл,
		|		НаборПолуфабрикатов.ПостатейныеПостоянные,
		|		НаборПолуфабрикатов.ПостатейныеПостоянныеБезНДС,
		|		НаборПолуфабрикатов.ПостатейныеПостоянныеРегл,
		|		НаборПолуфабрикатов.ПостатейныеПеременные,
		|		НаборПолуфабрикатов.ПостатейныеПеременныеБезНДС,
		|		НаборПолуфабрикатов.ПостатейныеПеременныеРегл,
		|		НаборПолуфабрикатов.СтоимостьЗабалансовая,
		|		НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл,
		|		НЕОПРЕДЕЛЕНО,
		|		НЕОПРЕДЕЛЕНО
		|	ИЗ
		|		НаборПолуфабрикатов КАК НаборПолуфабрикатов
		|	ГДЕ
		|		НЕ(НаборПолуфабрикатов.Стоимость = 0
		|					И НаборПолуфабрикатов.СтоимостьБезНДС = 0
		|					И НаборПолуфабрикатов.СтоимостьРегл = 0
		|					И НаборПолуфабрикатов.Трудозатраты = 0
		|					И НаборПолуфабрикатов.ТрудозатратыРегл = 0
		|					И НаборПолуфабрикатов.ПостатейныеПостоянные = 0
		|					И НаборПолуфабрикатов.ПостатейныеПостоянныеБезНДС = 0
		|					И НаборПолуфабрикатов.ПостатейныеПостоянныеРегл = 0
		|					И НаборПолуфабрикатов.ПостатейныеПеременные = 0
		|					И НаборПолуфабрикатов.ПостатейныеПеременныеБезНДС = 0
		|					И НаборПолуфабрикатов.ПостатейныеПеременныеРегл = 0
		|					И НаборПолуфабрикатов.СтоимостьЗабалансовая = 0
		|					И НаборПолуфабрикатов.СтоимостьЗабалансоваяРегл = 0)) КАК Затраты
		|
		|СГРУППИРОВАТЬ ПО
		|	Затраты.Номенклатура,
		|	Затраты.ХарактеристикаМатериала,
		|	Затраты.ПартияСпецификацияПолуфабриката,
		|	Затраты.КлючПартия,
		|	Затраты.Материал,
		|	Затраты.Калькуляция,
		|	Затраты.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПолуфабрикатыСводно
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ НаборПолуфабрикатов";
	
	Запрос.УстановитьПараметр("Калькуляция", ПараметрыРасчета.Калькуляция);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаПолуфабрикатов = МассивРезультатов[1].Выбрать();
	
	Затраты = МассивРезультатов[3].Выгрузить(); // ТаблицаЗначений
	Затраты.Индексы.Добавить("Калькуляция, Номенклатура, Характеристика, ПартияПолуфабриката, КлючПартия");
	
	ОтборЗатрат = Новый Структура("Калькуляция, Номенклатура, Характеристика, ПартияПолуфабриката, КлючПартия");
	
	РесурсыКРасчету = Новый Соответствие();
	РесурсыКРасчету.Вставить("Стоимость","ЦенаСтоимость");										//@Query-part
	РесурсыКРасчету.Вставить("СтоимостьБезНДС","ЦенаСтоимостьБезНДС");							//@Query-part
	РесурсыКРасчету.Вставить("СтоимостьРегл","ЦенаСтоимостьРегл");								//@Query-part
	РесурсыКРасчету.Вставить("Трудозатраты","ЦенаТрудозатраты");								//@Query-part
	РесурсыКРасчету.Вставить("ТрудозатратыРегл","ЦенаТрудозатратыРегл");						//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПостоянные","ЦенаПостатейныеПостоянные");				//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПостоянныеБезНДС","ЦенаПостатейныеПостоянныеБезНДС");	//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПостоянныеРегл","ЦенаПостатейныеПостоянныеРегл");		//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПеременные","ЦенаПостатейныеПеременные");				//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПеременныеБезНДС","ЦенаПостатейныеПеременныеБезНДС");	//@Query-part
	РесурсыКРасчету.Вставить("ПостатейныеПеременныеРегл","ЦенаПостатейныеПеременныеРегл");		//@Query-part
	РесурсыКРасчету.Вставить("СтоимостьЗабалансовая","ЦенаСтоимостьЗабалансовая");				//@Query-part
	РесурсыКРасчету.Вставить("СтоимостьЗабалансоваяРегл","ЦенаСтоимостьЗабалансоваяРегл");		//@Query-part
	
	ЦеныМатериалов = Новый ТаблицаЗначений;
	ЦеныМатериалов.Колонки.Добавить("Калькуляция", Новый ОписаниеТипов("ДокументСсылка.ПлановаяКалькуляция2_2"));
	ЦеныМатериалов.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ЦеныМатериалов.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	
	Для Каждого Ресурс Из РесурсыКРасчету Цикл
		ЦеныМатериалов.Колонки.Добавить(Ресурс.Значение, Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(23, 10)));
	КонецЦикла;
	
	ЦеныМатериалов.Индексы.Добавить("Калькуляция, Номенклатура, Характеристика");
	
	ОтборЦен = Новый Структура("Калькуляция, Номенклатура, Характеристика");
	
	Пока ВыборкаПолуфабрикатов.Следующий() Цикл
		
		ЗаполнитьЗначенияСвойств(ОтборЗатрат, ВыборкаПолуфабрикатов);
		
		ПоказателиРесурсов = Новый Структура;
		Для Каждого Ресурс Из РесурсыКРасчету Цикл
			ПоказателиРесурсов.Вставить(Ресурс.Ключ, 0);
		КонецЦикла;
		
		ЗатратыПолуфабриката = Затраты.НайтиСтроки(ОтборЗатрат);
		Для Каждого Затрата Из ЗатратыПолуфабриката Цикл
			
			Если ЗначениеЗаполнено(Затрата.Материал) Тогда
				
				ЗаполнитьЗначенияСвойств(ОтборЦен, Затрата);
				ОтборЦен.Номенклатура = Затрата.Материал;
				ОтборЦен.Характеристика = Затрата.ХарактеристикаМатериала;
				
				СредннееРесурсов = Новый Структура;
				Для Каждого Ресурс Из РесурсыКРасчету Цикл
					СредннееРесурсов.Вставить(Ресурс.Ключ, 0);
				КонецЦикла;
				
				СтрокиСЦенами = ЦеныМатериалов.НайтиСтроки(ОтборЦен);
				Для Каждого СтрокаЦены Из СтрокиСЦенами Цикл
					Для Каждого Ресурс Из РесурсыКРасчету Цикл
						СредннееРесурсов[Ресурс.Ключ] = СредннееРесурсов[Ресурс.Ключ] + СтрокаЦены[Ресурс.Значение];
					КонецЦикла;
				КонецЦикла;
				
				Для Каждого Ресурс Из РесурсыКРасчету Цикл
					ПоказателиРесурсов[Ресурс.Ключ] = ПоказателиРесурсов[Ресурс.Ключ] + СредннееРесурсов[Ресурс.Ключ] * Затрата.КоличествоМатериала;
				КонецЦикла;
				
			Иначе
				Для Каждого Ресурс Из РесурсыКРасчету Цикл
					ПоказателиРесурсов[Ресурс.Ключ] = ПоказателиРесурсов[Ресурс.Ключ] + Затрата[Ресурс.Ключ];
				КонецЦикла;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаполнитьЗначенияСвойств(ОтборЦен, ВыборкаПолуфабрикатов);
		Если ЦеныМатериалов.НайтиСтроки(ОтборЦен).Количество() > 0 Тогда
			Продолжить;
		КонецЕсли;

		ЦенаМатериала = ЦеныМатериалов.Добавить();
		ЗаполнитьЗначенияСвойств(ЦенаМатериала, ВыборкаПолуфабрикатов);
		
		Если Не ВыборкаПолуфабрикатов.Количество = 0 Тогда
			Для Каждого Ресурс Из РесурсыКРасчету Цикл
				ЦенаМатериала[Ресурс.Значение] = ПоказателиРесурсов[Ресурс.Ключ] 
					* ВыборкаПолуфабрикатов.Числитель / ВыборкаПолуфабрикатов.Знаменатель / ВыборкаПолуфабрикатов.Количество;
			КонецЦикла;
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Т.Калькуляция КАК Калькуляция,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	Т.ЦенаСтоимость,
		|	Т.ЦенаСтоимостьБезНДС,
		|	Т.ЦенаСтоимостьРегл,
		|	Т.ЦенаТрудозатраты,
		|	Т.ЦенаТрудозатратыРегл,
		|	Т.ЦенаПостатейныеПостоянные,
		|	Т.ЦенаПостатейныеПостоянныеБезНДС,
		|	Т.ЦенаПостатейныеПостоянныеРегл,
		|	Т.ЦенаПостатейныеПеременные,
		|	Т.ЦенаПостатейныеПеременныеБезНДС,
		|	Т.ЦенаПостатейныеПеременныеРегл,
		|	Т.ЦенаСтоимостьЗабалансовая,
		|	Т.ЦенаСтоимостьЗабалансоваяРегл
		|ПОМЕСТИТЬ ПредварительныеЦеныМатериалов
		|ИЗ
		|	&ЦеныМатериалов КАК Т
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Т.Калькуляция КАК Калькуляция,
		|	Т.Номенклатура КАК Номенклатура,
		|	Т.Характеристика КАК Характеристика,
		|	СРЕДНЕЕ(Т.ЦенаСтоимость),
		|	СРЕДНЕЕ(Т.ЦенаСтоимостьБезНДС),
		|	СРЕДНЕЕ(Т.ЦенаСтоимостьРегл),
		|	СРЕДНЕЕ(Т.ЦенаТрудозатраты),
		|	СРЕДНЕЕ(Т.ЦенаТрудозатратыРегл),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПостоянные),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПостоянныеБезНДС),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПостоянныеРегл),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПеременные),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПеременныеБезНДС),
		|	СРЕДНЕЕ(Т.ЦенаПостатейныеПеременныеРегл),
		|	СРЕДНЕЕ(Т.ЦенаСтоимостьЗабалансовая),
		|	СРЕДНЕЕ(Т.ЦенаСтоимостьЗабалансоваяРегл)
		|ПОМЕСТИТЬ ЦеныМатериалов
		|ИЗ
		|	ПредварительныеЦеныМатериалов КАК Т
		|
		|СГРУППИРОВАТЬ ПО
		|	Т.Калькуляция,
		|	Т.Номенклатура,
		|	Т.Характеристика
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПредварительныеЦеныМатериалов";
	
	Запрос.УстановитьПараметр("ЦеныМатериалов", ЦеныМатериалов);

	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ПривестиМножителиКОбщемуЗнаменателю(Множители)
	
	КолонкиУникальности = "Период, ПартияСпецификацияПродукции, Продукция, ХарактеристикаПродукции, Назначение,
	| ПартияСпецификацияПолуфабриката, КлючПартия, Полуфабрикат, ХарактеристикаПолуфабриката,
	| Порядок, РасчетЗавершен, ПартияВыпущена";	
	КопияМножителей = Множители.Скопировать(, КолонкиУникальности);
	
	КопияМножителей.Колонки.Добавить("Дубль");
	КопияМножителей.ЗаполнитьЗначения(1, "Дубль");
	
	КопияМножителей.Свернуть(КолонкиУникальности, "Дубль");
	
	Множители.Индексы.Добавить(КолонкиУникальности);
	ОтборМножителей = Новый Структура(КолонкиУникальности);
	
	Для Каждого Множитель Из КопияМножителей Цикл
		
		Если Множитель.Дубль <= 1 Или Не Множитель.РасчетЗавершен Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(ОтборМножителей, Множитель);
		МножителиДляПриведения = Множители.НайтиСтроки(ОтборМножителей);
		
		УникальныеЗнаменатели = Новый Соответствие();
		МассивЗнаменателей = Новый Массив();
		Для Каждого МножительДляПриведения Из МножителиДляПриведения Цикл
			Если УникальныеЗнаменатели[МножительДляПриведения.Знаменатель] = Неопределено Тогда
				УникальныеЗнаменатели.Вставить(МножительДляПриведения.Знаменатель, Истина);
				МассивЗнаменателей.Добавить(МножительДляПриведения.Знаменатель);
			КонецЕсли;
		КонецЦикла;
		
		НОК = ПроизводствоСервер.НаименьшееОбщееКратное(МассивЗнаменателей);
		
		Для Каждого МножительДляПриведения Из МножителиДляПриведения Цикл
			МножительДляПриведения.Числитель = НОК / МножительДляПриведения.Знаменатель * МножительДляПриведения.Числитель;
			МножительДляПриведения.Знаменатель = НОК;
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Процедура ПодготовитьСлужебныеВременныеТаблицы(МенеджерВТ, Калькуляция)
	
	Период = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Калькуляция, "Дата");
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВалютаУправленческогоУчета.Значение КАК Валюта,
		|	""ВалютаУпрУчета"" КАК ВидВалюты
		|ПОМЕСТИТЬ Валюты
		|ИЗ
		|	Константа.ВалютаУправленческогоУчета КАК ВалютаУправленческогоУчета
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПлановаяКалькуляция2_2.Организация.ВалютаРегламентированногоУчета КАК Значение,
		|	""ВалютаРеглУчета""
		|ИЗ
		|	Документ.ПлановаяКалькуляция2_2 КАК ПлановаяКалькуляция2_2
		|ГДЕ
		|	ПлановаяКалькуляция2_2.Ссылка = &Калькуляция
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВалютаРасценокВидовРабот.Значение,
		|	""ВалютаРасценокВидовРабот""
		|ИЗ
		|	Константа.ВалютаРасценокВидовРабот КАК ВалютаРасценокВидовРабот
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидВалюты,
		|	Валюта
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Валюты.Валюта КАК Валюта,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.КурсЧислитель, 0) КАК КурсЧислитель,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.КурсЗнаменатель, 0) КАК КурсЗнаменатель,
		|	Валюты.ВидВалюты КАК ВидВалюты
		|ПОМЕСТИТЬ КурсыВалют
		|ИЗ
		|	Валюты КАК Валюты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(
		|				&Период,
		|				Валюта В
		|					(ВЫБРАТЬ
		|						Валюты.Валюта КАК Валюта
		|					ИЗ
		|						Валюты КАК Валюты)
		|					И БазоваяВалюта = &БазоваяВалюта) КАК КурсыВалютСрезПоследних
		|		ПО Валюты.Валюта = КурсыВалютСрезПоследних.Валюта
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ВидВалюты,
		|	Валюта";
	
	Запрос.УстановитьПараметр("Период", Период);
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(
					ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Калькуляция, "Организация")));
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ФактическиеДанные

Процедура ДобавитьФактическиеМатериальныеЗатраты(СтруктураДанных, ПараметрыРасчета, СтрокаДерева, МножительРодитель)
	НовыйМатериал = СтруктураДанных.ПлановыеМатериальныеЗатраты.Добавить();
	НовыйМатериал.Калькуляция = ПараметрыРасчета.Калькуляция;
	НовыйМатериал.КлючПартия = МножительРодитель.КлючПартия;
	НовыйМатериал.Период = МножительРодитель.Период;
	НовыйМатериал.ПериодЗатрат = МножительРодитель.Период;
	НовыйМатериал.ПартияСпецификация = ?(ЗначениеЗаполнено(МножительРодитель.ПартияСпецификацияПолуфабриката),
		МножительРодитель.ПартияСпецификацияПолуфабриката, МножительРодитель.ПартияСпецификацияПродукции);
	НовыйМатериал.Подразделение = СтрокаДерева.Подразделение;
	НовыйМатериал.СтатьяКалькуляции = СтрокаДерева.СтатьяКалькуляции;
	НовыйМатериал.Номенклатура = СтрокаДерева.Номенклатура;
	НовыйМатериал.Характеристика = СтрокаДерева.Характеристика;
	НовыйМатериал.Количество = СтрокаДерева.Количество;
	НовыйМатериал.Стоимость = СтрокаДерева.Сумма;
	НовыйМатериал.СтоимостьБезНДС = СтрокаДерева.ПостатейныеПостоянныеБезНДС + СтрокаДерева.ПостатейныеПеременныеБезНДС
		+ СтрокаДерева.МатериальныеБезНДС + СтрокаДерева.ДопРасходыБезНДС + СтрокаДерева.Трудозатраты;
	НовыйМатериал.СтоимостьРегл = СтрокаДерева.ПостатейныеПостоянныеРегл + СтрокаДерева.ПостатейныеПеременныеРегл
		+ СтрокаДерева.МатериальныеРегл + СтрокаДерева.ДопРасходыРегл + СтрокаДерева.ТрудозатратыРегл;
	НовыйМатериал.СтоимостьЗабалансовая = СтрокаДерева.СуммаЗабалансовая;
	НовыйМатериал.СтоимостьЗабалансоваяРегл = СтрокаДерева.СтоимостьЗабалансоваяРегл;
	НовыйМатериал.ЭтоПолуфабрикат = СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката;
КонецПроцедуры

Процедура ОбработатьСтрокуДереваСебестоимости(СтруктураДанных, ПараметрыРасчета, СтрокаРазузлования, СтрокаДерева, МножительРодитель)
	
	Если СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПродукции
		Или СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката Тогда
		
		НовыйМножитель = ?(СтрокаРазузлования = Неопределено,
			СтруктураДанных.МножителиПартийИПолуфабрикатов.Добавить(),
			СтрокаРазузлования);
		
		НовыйМножитель.Период = ПараметрыРасчета.РеквизитыПК.ДатаКалькуляции;
		НовыйМножитель.Калькуляция = ПараметрыРасчета.Калькуляция;
		
		Если СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПродукции Тогда
			ДанныеРодителя = ?(МножительРодитель = Неопределено, СтрокаРазузлования, МножительРодитель);
			
			НовыйМножитель.Продукция					= ДанныеРодителя.Продукция;
			НовыйМножитель.ХарактеристикаПродукции		= ДанныеРодителя.ХарактеристикаПродукции;
			НовыйМножитель.КлючПартия					= Строка(НовыйМножитель.ХарактеристикаПродукции.УникальныйИдентификатор());
			НовыйМножитель.Назначение					= ДанныеРодителя.Назначение;
			НовыйМножитель.ПартияСпецификацияПродукции	= ДанныеРодителя.ПартияСпецификацияПродукции;
			
			НовыйМножитель.КоличествоПолуфабриката	= СтрокаДерева.Количество * СтрокаДерева.Числитель / СтрокаДерева.Знаменатель;
			НовыйМножитель.Числитель				= СтрокаДерева.Числитель;
			НовыйМножитель.Знаменатель				= СтрокаДерева.Знаменатель;

		ИначеЕсли СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката Тогда
			НовыйМножитель.ПартияСпецификацияПродукции	= МножительРодитель.ПартияСпецификацияПродукции;
			НовыйМножитель.Продукция					= МножительРодитель.Продукция;
			НовыйМножитель.ХарактеристикаПродукции		= МножительРодитель.ХарактеристикаПродукции;
			НовыйМножитель.Назначение					= МножительРодитель.Назначение;
			
			НовыйМножитель.ПартияСпецификацияПолуфабриката	= СтрокаДерева.Партия;
			НовыйМножитель.Полуфабрикат						= СтрокаДерева.Номенклатура;
			НовыйМножитель.ХарактеристикаПолуфабриката		= СтрокаДерева.Характеристика;
			НовыйМножитель.КлючПартия						= Строка(НовыйМножитель.ХарактеристикаПолуфабриката.УникальныйИдентификатор());
			
			НовыйМножитель.КоличествоПолуфабриката	= СтрокаДерева.Количество;
			НовыйМножитель.Числитель				= СтрокаДерева.Числитель;
			НовыйМножитель.Знаменатель				= СтрокаДерева.Знаменатель;
			НовыйМножитель.Порядок					= МножительРодитель.Порядок + 1;
		КонецЕсли;
		
		НовыйМножитель.Стоимость					= СтрокаДерева.Сумма;
		НовыйМножитель.СтоимостьБезНДС				= СтрокаДерева.ПостатейныеПостоянныеБезНДС
			+ СтрокаДерева.ПостатейныеПеременныеБезНДС + СтрокаДерева.МатериальныеБезНДС
			+ СтрокаДерева.ДопРасходыБезНДС + СтрокаДерева.Трудозатраты;
		НовыйМножитель.СтоимостьРегл				= СтрокаДерева.ПостатейныеПостоянныеРегл
			+ СтрокаДерева.ПостатейныеПеременныеРегл + СтрокаДерева.МатериальныеРегл
			+ СтрокаДерева.ДопРасходыРегл + СтрокаДерева.ТрудозатратыРегл;
			
		НовыйМножитель.Трудозатраты					= СтрокаДерева.Трудозатраты;
		НовыйМножитель.ТрудозатратыРегл				= СтрокаДерева.ТрудозатратыРегл;
		НовыйМножитель.ПостатейныеПостоянные		= СтрокаДерева.ПостатейныеПостоянные;
		НовыйМножитель.ПостатейныеПостоянныеБезНДС	= СтрокаДерева.ПостатейныеПостоянныеБезНДС;
		НовыйМножитель.ПостатейныеПостоянныеРегл	= СтрокаДерева.ПостатейныеПостоянныеРегл;
		НовыйМножитель.ПостатейныеПеременные		= СтрокаДерева.ПостатейныеПеременные;
		НовыйМножитель.ПостатейныеПеременныеБезНДС	= СтрокаДерева.ПостатейныеПеременныеБезНДС;
		НовыйМножитель.ПостатейныеПеременныеРегл	= СтрокаДерева.ПостатейныеПеременныеРегл;
		НовыйМножитель.СтоимостьЗабалансовая		= СтрокаДерева.СуммаЗабалансовая;
		НовыйМножитель.СтоимостьЗабалансоваяРегл	= СтрокаДерева.СтоимостьЗабалансоваяРегл;
		
		НовыйМножитель.ПартияВыпущена = Истина;
		НовыйМножитель.РасчетЗавершен = Истина;
		
		Если СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияПолуфабриката
			И МножительРодитель <> Неопределено Тогда
			// Для связи текущего ПФ с ПФ / продукцией уровнем выше добавляем тек. ПФ как мат. затрату.
			ДобавитьФактическиеМатериальныеЗатраты(СтруктураДанных, ПараметрыРасчета, СтрокаДерева,
				МножительРодитель);
		КонецЕсли;
		
		Если СтрокаДерева.ТребуетсяРазузлование И СтрокаДерева.Строки.Количество() > 0 Тогда
			Для Каждого Стр Из СтрокаДерева.Строки Цикл
				ОбработатьСтрокуДереваСебестоимости(СтруктураДанных, ПараметрыРасчета, Неопределено,
					Стр, НовыйМножитель);
			КонецЦикла;
		КонецЕсли;
		
	ИначеЕсли СтрокаДерева.ВидСтроки = Перечисления.ВидыСтрокДереваСебестоимости.ПартияЗатраты Тогда
		Если СтрокаДерева.ТипЗатрат = ПараметрыРасчета.ТипыЗатратДереваСебестоимости.ТипЗатратМатериальные Тогда
			ДобавитьФактическиеМатериальныеЗатраты(СтруктураДанных, ПараметрыРасчета, СтрокаДерева,
				МножительРодитель);
			
		ИначеЕсли СтрокаДерева.ТипЗатрат = ПараметрыРасчета.ТипыЗатратДереваСебестоимости.ТипЗатратПостатейные Тогда
			НоваяПрочаяЗатрата = СтруктураДанных.ПлановыеПрочиеЗатраты.Добавить();
			НоваяПрочаяЗатрата.Калькуляция = ПараметрыРасчета.Калькуляция;
			НоваяПрочаяЗатрата.Период = ПараметрыРасчета.РеквизитыПК.ДатаКалькуляции;
			НоваяПрочаяЗатрата.ПартияСпецификация = ?(ЗначениеЗаполнено(МножительРодитель.ПартияСпецификацияПолуфабриката),
				МножительРодитель.ПартияСпецификацияПолуфабриката,
				МножительРодитель.ПартияСпецификацияПродукции);
			НоваяПрочаяЗатрата.Подразделение = СтрокаДерева.Подразделение;
			НоваяПрочаяЗатрата.СтатьяКалькуляции = СтрокаДерева.СтатьяКалькуляции;
			НоваяПрочаяЗатрата.СтатьяРасходов = СтрокаДерева.Номенклатура;
			НоваяПрочаяЗатрата.КлючПартия = МножительРодитель.КлючПартия;
			
			НоваяПрочаяЗатрата.Стоимость = СтрокаДерева.Сумма;
			НоваяПрочаяЗатрата.СтоимостьБезНДС = СтрокаДерева.ПостатейныеПостоянныеБезНДС
				+ СтрокаДерева.ПостатейныеПеременныеБезНДС;
			НоваяПрочаяЗатрата.СтоимостьРегл = СтрокаДерева.ПостатейныеПостоянныеРегл
				+ СтрокаДерева.ПостатейныеПеременныеРегл;
			
		ИначеЕсли СтрокаДерева.ТипЗатрат = ПараметрыРасчета.ТипыЗатратДереваСебестоимости.ТипЗатратТрудозатраты Тогда
			НоваяТрудозатрата = СтруктураДанных.ПлановыеТрудозатраты.Добавить();
			НоваяТрудозатрата.Калькуляция = ПараметрыРасчета.Калькуляция;
			НоваяТрудозатрата.Период = МножительРодитель.Период;
			НоваяТрудозатрата.ПериодЗатрат = МножительРодитель.Период;
			НоваяТрудозатрата.ПартияСпецификация = ?(ЗначениеЗаполнено(МножительРодитель.ПартияСпецификацияПолуфабриката),
				МножительРодитель.ПартияСпецификацияПолуфабриката,
				МножительРодитель.ПартияСпецификацияПродукции);
			НоваяТрудозатрата.Подразделение = СтрокаДерева.Подразделение;
			НоваяТрудозатрата.СтатьяКалькуляции = СтрокаДерева.СтатьяКалькуляции;
			НоваяТрудозатрата.ВидРабот = СтрокаДерева.Номенклатура;
			НоваяТрудозатрата.КлючПартия = МножительРодитель.КлючПартия;
			НоваяТрудозатрата.Количество = СтрокаДерева.Количество;
			НоваяТрудозатрата.Стоимость = СтрокаДерева.Сумма;
			НоваяТрудозатрата.СтоимостьРегл = СтрокаДерева.ТрудозатратыРегл;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПерерасчитатьЗнаменатели(ПараметрыРасчета, СтруктураДанных)
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"Множители", СтруктураДанных.МножителиПартийИПолуфабрикатов,
		ПараметрыРасчета.Множители_КолонкиГруппировкиБезПриоритета + ", "
			+ ПараметрыРасчета.Множители_КолонкиСуммирования,
		"ПартияСпецификацияПолуфабриката,КлючПартия"
	);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВЫБОР КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
	//		Комментарий 5205
	//		Если пересчитываем знаменатель по основному ГП заказа то далее соединяемся по партии спецификации основного ГП
	|		ТОГДА ДД.ПартияСпецификацияПродукции
	//		Если пересчитываем знаменатель по ПФ, необходимо учитывать, что в рамках 1 заказа на производство,
	//		1 ПФ может быть использован для разных ГП.
	//		1 ПФ может выпускается в рамках одного этапа на весь заказ, исходя из этого знаменатель необходимо пересчитывать в рамках потребления всего заказа.
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ПартияСпецификацияПродукции,
	|	ДД.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	// Пересчитываем значение знаменателя, как сумму числителей
	|	СУММА(ДД.Числитель) КАК Знаменатель,
	|	МАКСИМУМ(ДД.Порядок) КАК Порядок,
	|	ДД.КлючПартия
	|ПОМЕСТИТЬ ОбщиеЗнаменатели
	|ИЗ
	|	Множители КАК ДД
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
	|			ТОГДА ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПродукции) <> ТИП(Справочник.РесурсныеСпецификации)
	|		ИНАЧЕ
	|			ТИПЗНАЧЕНИЯ(ДД.ПартияСпецификацияПолуфабриката) <> ТИП(Справочник.РесурсныеСпецификации)
	|		КОНЕЦ
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР КОГДА ДД.ПартияСпецификацияПолуфабриката = НЕОПРЕДЕЛЕНО
	|		ТОГДА ДД.ПартияСпецификацияПродукции
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ДД.ПартияСпецификацияПолуфабриката,
	|	ДД.КлючПартия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПартияСпецификацияПродукции,
	|	ПартияСпецификацияПолуфабриката,
	|	КлючПартия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Множители.Период КАК Период,
	|	Множители.Калькуляция КАК Калькуляция,
	|	Множители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
	|	Множители.Продукция КАК Продукция,
	|	Множители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	Множители.Назначение КАК Назначение,
	|	Множители.ПартияСпецификацияПолуфабриката КАК ПартияСпецификацияПолуфабриката,
	|	Множители.КлючПартия КАК КлючПартия,
	|	Множители.Полуфабрикат КАК Полуфабрикат,
	|	Множители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
	|	ISNULL(ОбщиеЗнаменатели.Порядок, Множители.Порядок) КАК Порядок,
	|	ISNULL(ОбщиеЗнаменатели.Знаменатель, Множители.Знаменатель) КАК Знаменатель,
	|	Множители.РасчетЗавершен КАК РасчетЗавершен,
	// Это финальный запрос по разузловке ГП и ПФ
	// Далее данный флаг влияет только на старый функционал - будет ли происходить разузловка первого уровня в отчетах по ПК.
	// Партия в данном случае выпущена, но разузловка в данном случае происходит по дереву себестоимости и в отчете разузловка больше не нужна.
	// В запросах отчетов поддерживается только для обратной совместимости со старыми рассчитанными ПК.
	|	ЛОЖЬ КАК ПартияВыпущена,
	|	Множители.Числитель КАК Числитель,
	|	Множители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
	|	Множители.Стоимость КАК Стоимость,
	|	Множители.СтоимостьБезНДС КАК СтоимостьБезНДС,
	|	Множители.СтоимостьРегл КАК СтоимостьРегл,
	|	Множители.Трудозатраты КАК Трудозатраты,
	|	Множители.ТрудозатратыРегл КАК ТрудозатратыРегл,
	|	Множители.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
	|	Множители.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
	|	Множители.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
	|	Множители.ПостатейныеПеременные КАК ПостатейныеПеременные,
	|	Множители.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
	|	Множители.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
	|	Множители.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
	|	Множители.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
	|ИЗ
	|	Множители КАК Множители
	|		ЛЕВОЕ СОЕДИНЕНИЕ ОбщиеЗнаменатели КАК ОбщиеЗнаменатели
	|		ПО Множители.ПартияСпецификацияПолуфабриката = ОбщиеЗнаменатели.ПартияСпецификацияПолуфабриката
	|			И Множители.КлючПартия = ОбщиеЗнаменатели.КлючПартия
	// см. Комментарий 5205
	|			И (ОбщиеЗнаменатели.ПартияСпецификацияПродукции = НЕОПРЕДЕЛЕНО
	|				ИЛИ Множители.ПартияСпецификацияПродукции = ОбщиеЗнаменатели.ПартияСпецификацияПродукции)";
	
	СтруктураДанных.МножителиПартийИПолуфабрикатов = Запрос.Выполнить().Выгрузить();

КонецПроцедуры

Процедура РассчитатьМножителиПоФактическимДанным(СтруктураДанных, ПараметрыРасчета)

	Отбор = Новый Структура("ПартияВыпущена", Истина);
	СтрокиДляРазузлования = СтруктураДанных.МножителиПартийИПолуфабрикатов.НайтиСтроки(Отбор);
	Если СтрокиДляРазузлования.Количество() = 0 Тогда
		// Комментарий 5263
		// Во время формирования множителей по дереву себестоимости, неизвестно сколько из каждой партии итого будет взято ПФ.
		// Для корректного расчета затрат необходимо перерассчитать общие знаменатели.
		// Так как разузловка до этого момента работает под этот перерасчет,
		// пере рассчитываем вне зависимости от того есть необходимость разузлования по дереву или нет.
		// Есть условие - если есть разузлование по дереву, перерасчет должен работать после разузлования по дереву.
		ПерерасчитатьЗнаменатели(ПараметрыРасчета, СтруктураДанных);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();

	КолонкиГруппировок = ПараметрыРасчета.Множители_КолонкиГруппировкиБезПриоритета;
	РасчетСебестоимостиПрикладныеАлгоритмы.ПоместитьТаблицуЗначенийВоВременнуюТаблицу(Запрос.МенеджерВременныхТаблиц,
		"ВТМножители", СтруктураДанных.МножителиПартийИПолуфабрикатов.Скопировать(СтрокиДляРазузлования),
		КолонкиГруппировок + ", "+ ПараметрыРасчета.Множители_КолонкиСуммирования,
		"РасчетЗавершен,ПартияВыпущена");
		
	Отбор = Новый Структура("ПартияВыпущена", Ложь);
	СтрокиНеТребующиеРазузлования = СтруктураДанных.МножителиПартийИПолуфабрикатов.НайтиСтроки(Отбор);
	СтруктураДанных.МножителиПартийИПолуфабрикатов = СтруктураДанных.МножителиПартийИПолуфабрикатов.Скопировать(
		СтрокиНеТребующиеРазузлования);
		
	Запрос.Текст = "
		|ВЫБРАТЬ
		|	ВТМножители.Период КАК Период,
		|	ВТМножители.Калькуляция КАК Калькуляция,
		|	ВТМножители.ПартияСпецификацияПродукции КАК ПартияСпецификацияПродукции,
		|	ВТМножители.Продукция КАК Продукция,
		|	ВТМножители.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
		|	ВТМножители.Назначение КАК Назначение,
		|	ЕСТЬNULL(ОтчетыПереработчика2_5.Ссылка, ВТМножители.ПартияСпецификацияПолуфабриката) КАК ПартияСпецификацияПолуфабриката,
		|	ВТМножители.КлючПартия КАК КлючПартия,
		|	ВТМножители.Полуфабрикат КАК Полуфабрикат,
		|	ВТМножители.ХарактеристикаПолуфабриката КАК ХарактеристикаПолуфабриката,
		|	ВТМножители.Порядок КАК Порядок,
		|	ВТМножители.Знаменатель КАК Знаменатель,
		|	ВТМножители.РасчетЗавершен КАК РасчетЗавершен,
		|	ВТМножители.ПартияВыпущена КАК ПартияВыпущена,
		|	ВТМножители.Числитель КАК Числитель,
		|	ВТМножители.КоличествоПолуфабриката КАК КоличествоПолуфабриката,
		|	ВТМножители.Стоимость КАК Стоимость,
		|	ВТМножители.СтоимостьБезНДС КАК СтоимостьБезНДС,
		|	ВТМножители.СтоимостьРегл КАК СтоимостьРегл,
		|	ВТМножители.Трудозатраты КАК Трудозатраты,
		|	ВТМножители.ТрудозатратыРегл КАК ТрудозатратыРегл,
		|	ВТМножители.ПостатейныеПостоянные КАК ПостатейныеПостоянные,
		|	ВТМножители.ПостатейныеПостоянныеБезНДС КАК ПостатейныеПостоянныеБезНДС,
		|	ВТМножители.ПостатейныеПостоянныеРегл КАК ПостатейныеПостоянныеРегл,
		|	ВТМножители.ПостатейныеПеременные КАК ПостатейныеПеременные,
		|	ВТМножители.ПостатейныеПеременныеБезНДС КАК ПостатейныеПеременныеБезНДС,
		|	ВТМножители.ПостатейныеПеременныеРегл КАК ПостатейныеПеременныеРегл,
		|	ВТМножители.СтоимостьЗабалансовая КАК СтоимостьЗабалансовая,
		|	ВТМножители.СтоимостьЗабалансоваяРегл КАК СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ВТМножители КАК ВТМножители
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДокЭтапПроизводства
		|		ПО (ВТМножители.ПартияСпецификацияПолуфабриката = ДокЭтапПроизводства.Ссылка)
		|			И (ДокЭтапПроизводства.ПроизводствоНаСтороне2_5)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика2_5.ВыходныеИзделия КАК ОтчетыПереработчика2_5
		|		ПО (ОтчетыПереработчика2_5.ЭтапПроизводства = ДокЭтапПроизводства.Ссылка)
		|			И (НЕ ОтчетыПереработчика2_5.СписатьНаРасходы)
		|
		//++ Устарело_Переработка24
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТМножители.Период,
		|	ВТМножители.Калькуляция,
		|	ВТМножители.ПартияСпецификацияПродукции,
		|	ВТМножители.Продукция,
		|	ВТМножители.ХарактеристикаПродукции,
		|	ВТМножители.Назначение,
		|	ЕСТЬNULL(ОтчетПереработчика.Ссылка, ВТМножители.ПартияСпецификацияПолуфабриката),
		|	ВТМножители.КлючПартия,
		|	ВТМножители.Полуфабрикат,
		|	ВТМножители.ХарактеристикаПолуфабриката,
		|	ВТМножители.Порядок,
		|	ВТМножители.Знаменатель,
		|	ВТМножители.РасчетЗавершен,
		|	ВТМножители.ПартияВыпущена,
		|	ВТМножители.Числитель,
		|	ВТМножители.КоличествоПолуфабриката,
		|	ВТМножители.Стоимость,
		|	ВТМножители.СтоимостьБезНДС,
		|	ВТМножители.СтоимостьРегл,
		|	ВТМножители.Трудозатраты,
		|	ВТМножители.ТрудозатратыРегл,
		|	ВТМножители.ПостатейныеПостоянные,
		|	ВТМножители.ПостатейныеПостоянныеБезНДС,
		|	ВТМножители.ПостатейныеПостоянныеРегл,
		|	ВТМножители.ПостатейныеПеременные,
		|	ВТМножители.ПостатейныеПеременныеБезНДС,
		|	ВТМножители.ПостатейныеПеременныеРегл,
		|	ВТМножители.СтоимостьЗабалансовая,
		|	ВТМножители.СтоимостьЗабалансоваяРегл
		|ИЗ
		|	ВТМножители КАК ВТМножители
		|
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЭтапПроизводства2_2 КАК ДокЭтапПроизводства
		|		ПО ВТМножители.ПартияСпецификацияПолуфабриката = ДокЭтапПроизводства.Ссылка
		|			И (ДокЭтапПроизводства.ПроизводствоНаСтороне)
		|			И (НЕ ДокЭтапПроизводства.ПроизводствоНаСтороне2_5)
		|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетПереработчика.Продукция КАК ОтчетПереработчика
		|		ПО (ОтчетПереработчика.ЭтапПроизводства = ДокЭтапПроизводства.Ссылка)
		|			И (НЕ ОтчетПереработчика.СписатьНаРасходы)
		|
		//-- Устарело_Переработка24
		|";
	
	ОтборПолуфабрикатов = Новый Структура("ПартияПродукции, Продукция, ХарактеристикаПродукции, ТребуетсяРазузлование");
	ОтборПолуфабрикатов.ТребуетсяРазузлование = Истина;
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	ПараметрыРасчета.Вставить("ТипыЗатратДереваСебестоимости", СтруктураСебестоимости.ТипыЗатратДереваСебестоимости());
	
	Пока РезультатЗапроса.Следующий() Цикл
		
		ПараметрыДереваСебестоимости = СтруктураСебестоимости.ПараметрыДереваСебестоимости();
		ПараметрыДереваСебестоимости.ДинамическоеСчитывание = Ложь;
		ПараметрыДереваСебестоимости.ТипРезультата = "ДеревоЗначений";		
		ПараметрыУзлаДереваСебестоимости = СтруктураСебестоимости.ПараметрыУзлаДереваСебестоимости();
		Отборы = ПараметрыУзлаДереваСебестоимости.Отборы;
		
		Если ЗначениеЗаполнено(РезультатЗапроса.Полуфабрикат) Тогда
			Отборы.Продукция = ЗначениеВМассив(РезультатЗапроса.Полуфабрикат);
			Отборы.ХарактеристикиПродукции = ЗначениеВМассив(РезультатЗапроса.ХарактеристикаПолуфабриката);
			Отборы.ПартииПродукции = ЗначениеВМассив(РезультатЗапроса.ПартияСпецификацияПолуфабриката);
		Иначе
			Отборы.Продукция = ЗначениеВМассив(РезультатЗапроса.Продукция);
			Отборы.ХарактеристикиПродукции = ЗначениеВМассив(РезультатЗапроса.ХарактеристикаПродукции);
			Отборы.ПартииПродукции = ЗначениеВМассив(РезультатЗапроса.ПартияСпецификацияПродукции);
			Отборы.НазначенияПродукции = ЗначениеВМассив(РезультатЗапроса.Назначение);
		КонецЕсли;
		
		Отборы.Числитель = РезультатЗапроса.Числитель;
		// Для верного расчета знаменатель должен быть посчитан только по строкам где ПартияВыпущена = Истина!
		Отборы.Знаменатель = РезультатЗапроса.Знаменатель;
		КоличествоПолуфабриката = РезультатЗапроса.КоличествоПолуфабриката;
		
		СтруктураСебестоимости.ПостроитьДеревоСебестоимости(ПараметрыДереваСебестоимости,
			ПараметрыУзлаДереваСебестоимости);
		
		Если ПараметрыДереваСебестоимости.Результат = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийМножитель = СтруктураДанных.МножителиПартийИПолуфабрикатов.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущийМножитель, РезультатЗапроса);
		Для Каждого СтрокаДерева Из ПараметрыДереваСебестоимости.Результат.Строки Цикл
			Если ТекущийМножитель = Неопределено Тогда
				// В рамках одного этапа необходимая продукция может быть выпущена несколькими строками
				// Для каждой строки выпуска в множителях должна быть собственная строка.
				ТекущийМножитель = СтруктураДанных.МножителиПартийИПолуфабрикатов.Добавить();
				ЗаполнитьЗначенияСвойств(ТекущийМножитель, РезультатЗапроса);
			КонецЕсли;
			ОбработатьСтрокуДереваСебестоимости(СтруктураДанных, ПараметрыРасчета, ТекущийМножитель, СтрокаДерева,
					Неопределено);
			КоличествоПолуфабриката = КоличествоПолуфабриката - ТекущийМножитель.КоличествоПолуфабриката;
			Если КоличествоПолуфабриката = 0 Тогда
				Прервать;
			КонецЕсли;
			ТекущийМножитель = Неопределено;
		КонецЦикла;
	КонецЦикла;
	
	// см. Комментарий 5263
	ПерерасчитатьЗнаменатели(ПараметрыРасчета, СтруктураДанных);
	
	ПривестиМножителиКОбщемуЗнаменателю(СтруктураДанных.МножителиПартийИПолуфабрикатов);
	СвернутьМножителиВРамкахТЗ(СтруктураДанных.МножителиПартийИПолуфабрикатов, ПараметрыРасчета);
	СвернутьРегистрыЗатратВРамкахТЗ(СтруктураДанных);
	
КонецПроцедуры


#КонецОбласти

#Область Проверка

Процедура ДобавитьКалькуляциюКПроверке(Калькуляция, МассивКалькуляций)
	
	Если МассивКалькуляций = Неопределено Тогда
		МассивКалькуляций = Новый Массив;
	КонецЕсли;
	
	Если БылаОтменаПроведения(Калькуляция) Тогда
		Возврат;
	КонецЕсли;
	
	#Область БылаОшибкаРасчетаПК
	Запрос = Новый Запрос();
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДД.Калькуляция
	|ИЗ РегистрСведений.СостоянияПлановыхКалькуляций КАК ДД
	|ГДЕ
	|	ДД.Калькуляция = &Калькуляция
	|	И ДД.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияРасчетаПлановойКалькуляции.ОшибкаРасчета)";
	Запрос.УстановитьПараметр("Калькуляция", Калькуляция);
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	#КонецОбласти
	
	Если МассивКалькуляций.Найти(Калькуляция) = Неопределено Тогда
		МассивКалькуляций.Добавить(Калькуляция);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьСтоимость(Калькуляции)
	
	Проверка = АудитСостоянияСистемы.НайтиПроверкуПоКлючевымПолям(Документы.ПлановаяКалькуляция2_2.ПустаяСсылка(), "ДвиженияСНулевойСтоимостью");
	
	Если НЕ ЗначениеЗаполнено(Проверка) Тогда
		ВызватьИсключение НСтр("ru = 'Не найдена служебная контекстная проверка для операции ""Калькуляция продукциии""';
								|en = 'Service context check for the ""Product costing"" operation was not found'");
	КонецЕсли;
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Калькуляции", Калькуляции);
	
	УстановитьПривилегированныйРежим(Истина);
	ПараметрыПроверки = АудитСостоянияСистемы.ВыполнитьПроверкуСостоянияСистемы(Проверка, ДопПараметры);
	УстановитьПривилегированныйРежим(Ложь);
	
	КалькуляцииСОшибкамиРасчета = ПараметрыПроверки.ДополнительныеПараметры.КалькуляцииСОшибкамиРасчета;
	
	Для Каждого Калькуляция Из Калькуляции Цикл
		
		Если Не КалькуляцииСОшибкамиРасчета.Найти(Калькуляция) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаписатьСостояниеКалькуляции(Калькуляция, 
			Перечисления.СостоянияРасчетаПлановойКалькуляции.Рассчитана);
		
	КонецЦикла;
	
	Если КалькуляцииСОшибкамиРасчета.Количество() > 0 Тогда
		АктуализироватьСостояниеКалькуляций(КалькуляцииСОшибкамиРасчета);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ПолучитьПериодЗатрат(ПараметрыРасчетаПериода)
	
	ПериодЗатрат = НачалоДня(ПараметрыРасчетаПериода.Период);
	ПозицияЭтапа = 0;
	Счетчик = 0;
	
	ТекущийЭтапСтрока = ПараметрыРасчетаПериода.Этапы.Найти(ПараметрыРасчетаПериода.ТекущийЭтап, "Этап");
	Если ТекущийЭтапСтрока <> Неопределено Тогда
		ПозицияЭтапа = ПараметрыРасчетаПериода.Этапы.Индекс(ТекущийЭтапСтрока);
	КонецЕсли;
	
	Пока Счетчик < ПозицияЭтапа Цикл
		
		ПериодЗатрат = ПериодЗатрат + ПараметрыРасчетаПериода.ДлительностиЭтапов[Счетчик] * 86400;
		Счетчик = Счетчик + 1;
		
	КонецЦикла;
	
	Возврат НачалоМесяца(ПериодЗатрат);
	
КонецФункции

Функция ПараметрыРасчетаПериода()
	
	Параметры = Новый Структура;
	Параметры.Вставить("Период", Дата(1, 1, 1));
	Параметры.Вставить("Этапы");
	Параметры.Вставить("ТекущийЭтап");
	Параметры.Вставить("ДлительностиЭтапов");
	
	Возврат Параметры;
	
КонецФункции

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.МножителиПартийИПолуфабрикатов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.12";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("941997c3-10d2-4efa-abdf-3e3df95f61c7");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.МножителиПартийИПолуфабрикатов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Конвертация типа колонки ""Ключ партия"" регистра сведений ""Множители партий и полуфабрикатов"".';
									|en = 'Conversion of the ""Key lot"" column type of the ""Multiples of lots and semi-finished products"" information register.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.МножителиПартийИПолуфабрикатов.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.МножителиПартийИПолуфабрикатов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.МножителиПартийИПолуфабрикатов";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Период УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Период УБЫВ");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	МножителиПартийИПолуфабрикатов.Калькуляция КАК Калькуляция
	|ИЗ
	|	РегистрСведений.МножителиПартийИПолуфабрикатов КАК МножителиПартийИПолуфабрикатов
	|
	|ГДЕ
	// Конвертировать значения колонки имеет смысл только для тех документов, у которых заполнено значение КлючПартия хотя бы в одной строке.
	|	МножителиПартийИПолуфабрикатов.УдалитьКлючПартия ЕСТЬ НЕ NULL
	|	И МножителиПартийИПолуфабрикатов.УдалитьКлючПартия <> &ПустойУИД";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("ПустойУИД", ОбщегоНазначенияКлиентСервер.ПустойУникальныйИдентификатор());
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры,
		Запрос.Выполнить().Выгрузить(),
		ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.МножителиПартийИПолуфабрикатов";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.МножителиПартийИПолуфабрикатов");
			ЭлементБлокировки.УстановитьЗначение("Калькуляция", Выборка.Калькуляция);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.МножителиПартийИПолуфабрикатов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Калькуляция.Установить(Выборка.Калькуляция);
			НаборЗаписей.Прочитать();
			
			Для Каждого Запись Из НаборЗаписей Цикл
				Запись.КлючПартия = Строка(Запись.УдалитьКлючПартия);
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Не удалось записать данные в регистр ""Множители партий и полуфабрикатов"" по плановой калькуляции ""%1"", по причине: %2';
						|en = 'Cannot write data to the ""Multiples of lots and semi-finished products"" information register based on the ""%1"" product standard cost. Reason: %2'"),
					Выборка.Калькуляция,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.МножителиПартийИПолуфабрикатов,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
