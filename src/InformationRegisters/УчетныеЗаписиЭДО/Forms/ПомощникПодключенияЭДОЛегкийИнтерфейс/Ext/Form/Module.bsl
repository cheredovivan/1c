

#Область ОписаниеПеременных

&НаКлиенте
Перем ПараметрыОбработчикаОжидания,
	ПаролиСертификатов,
	КонтекстДиагностики,
	ПрограммаКриптографии,
	СвойстваПароля;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не НастройкиЭДО.ЕстьПравоНастройкиОбмена(Истина) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если ОбщегоНазначения.ЭтоМобильныйКлиент() Тогда
		Отказ = Истина;
		ИнтерфейсМобильногоЭДОКлиентСервер.СообщитьФункциональностьПокаНеДоступна();
		Возврат;
	КонецЕсли;
	
	ПараметрыПомощникаПодключенияЭДО = УчетныеЗаписиЭДО.ПараметрыПомощникаПодключенияЭДО();
	ЗаполнятьРеквизитыПоИНН = ПараметрыПомощникаПодключенияЭДО.ЗаполнятьРеквизитыПоИНН;
	Элементы.ДекорацияШапкаИНН.Видимость = ЗаполнятьРеквизитыПоИНН;
	
	УстанавливатьРасширение = ОбщегоНазначения.ЭтоВебКлиент() Или ОбщегоНазначения.ЭтоМобильныйКлиент();
	Элементы.ГруппаПроверкиНаличияРасширения.Видимость = УстанавливатьРасширение;
	
	Параметры.Свойство("ЧерезОблачныйЭДО", ЭтоОблачныйЭДО);
	Параметры.Свойство("Организация", Организация);
	
	Элементы.ГруппаУчетнаяЗаписьОблачногоЭДО.Видимость = ЭтоОблачныйЭДО;
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Организация = ОбщегоНазначенияБЭД.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	ВБазеОднаОрганизация = ОбщегоНазначенияБЭД.КоличествоОрганизаций() = 1;
	
	ОрганизацияПриИзмененииНаСервере();
	
	УстановитьЗначенияПоУмолчанию();
	
	ПереключитьТекущуюСтраницуНаЭтапВводаДанных(ЭтотОбъект);
	
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	УстановитьВидимостьВидаПодключения(ЭтотОбъект);
	
	СкрытьВидимостьДекорацииПоПоискуПоИНН(ЭтотОбъект);
	
	УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СкрытьЗаголовкиУРасширеннойПодсказки(ЭтотОбъект);
	
	ЗаполнитьПоИНН();
	
	ЗапуститьОбновлениеКешОператоровИФорматов();
	ЗапуститьОжиданиеОбновленияКешОператоровИФорматов();
	
	НовыйСвойстваПароля();
	НачатьПроверкуКриптографии();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНавигационнойСсылки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "ИнструкцияПоНастройкеКриптопровайдера" Тогда
		Если УстановленВипНет Тогда
			СсылкаНаИнструкцию = "https://edo.1c.ru/handbook/22/3994/";
		Иначе
			СсылкаНаИнструкцию = "https://edo.1c.ru/handbook/22/4182/";
		КонецЕсли;
		НавигационнаяСсылкаФорматированнойСтроки = СсылкаНаИнструкцию;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ВыполняетсяРегистрация Тогда
		Отказ = Истина;
		ОбработчикВопроса = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Подключение к ЭДО еще не завершено.
							|Прервать подключение?';
							|en = 'Connecting to EDI is not completed yet.
							|Terminate connection?'");
		ПоказатьВопрос(ОбработчикВопроса, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбработатьЗаписьПользователяОблачногоЭДО(ИмяСобытия, Параметр);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИдентификаторНовыйСуществующийЭДОПриИзменении(Элемент)
	
	УстановитьВидимостьВидаПодключения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорЭДОПриИзменении(Элемент)
	
	УстановитьСпособОбменаПоОператору(СпособОбменаЭД, ОператорыЭДО, ИдентификаторОператораЭДО);
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	УстановитьВидимостьВидаПодключения(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОрганизацииНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)

	ТипыКонтактнойИнформацииАдрес = ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес");
		
	ВидКонтактнойИнформации = ПараметрыВидаКонтактнойИнформации(ТипыКонтактнойИнформацииАдрес);
	ВидКонтактнойИнформации.Вставить("ВидРедактирования", "ПолеВводаИДиалог");
	ВидКонтактнойИнформации.Вставить("Наименование" , ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ЮрАдресОрганизации"));
	
	ПараметрыОткрытия = УправлениеКонтактнойИнформациейКлиент.ПараметрыФормыКонтактнойИнформации(
		ВидКонтактнойИнформации, ЗначениеПолейАдреса, АдресОрганизации, , ТипыКонтактнойИнформацииАдрес);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьФормуКонтактнойИнформацииЗавершение", ЭтотОбъект);
	
	УправлениеКонтактнойИнформациейКлиент.ОткрытьФормуКонтактнойИнформации(ПараметрыОткрытия, , Оповещение);

КонецПроцедуры

&НаКлиенте
Процедура СертификатПриИзменении(Элемент)
	
	ОчиститьСертификат();
	Если ЗначениеЗаполнено(ОтпечатокСертификата) Тогда
		НайтиСоздатьСертификатКриптографии();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	ОчиститьСертификат();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	
	ЗаполнитьПоИНН();
	
	ЗапуститьОбновлениеКешОператоровИФорматов();
	ЗапуститьОжиданиеОбновленияКешОператоровИФорматов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИННОчистка(Элемент, СтандартнаяОбработка)
	
	ЗаполнитьПоИНН();
	
	ЗапуститьОбновлениеКешОператоровИФорматов();
	ЗапуститьОжиданиеОбновленияКешОператоровИФорматов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	СкрытьКоличествоСимволовУПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СкрытыйПароль = СкрытыйПароль();
	Если Пароль = СкрытыйПароль Тогда
		НачалоСкрытогоПароля = СтрНайти(Текст, СкрытыйПароль);
		Если НачалоСкрытогоПароля > 0 Тогда
			КонецСкрытогоПароля = НачалоСкрытогоПароля + СтрДлина(СкрытыйПароль);
			Текст = Лев(Текст, НачалоСкрытогоПароля - 1) + Сред(Текст, КонецСкрытогоПароля);
		Иначе
			Текст = "";
		КонецЕсли;
		Пароль = Текст;
	КонецЕсли;
	
	СвойстваПароля.ПарольВерен = Ложь;
	СвойстваПароля.ПарольПроверен = Ложь;
	СвойстваПароля.Пароль = Текст;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьПолеПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорЭДООчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияОчистка(Элемент, СтандартнаяОбработка)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	ОчиститьСообщения();
	ПроверитьДанныеОрганизации(ЭтотОбъект, Истина);
	ЗапуститьОжиданиеОбновленияКешОператоровИФорматов();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаОтправкаЗаявленияНаРегистрациюРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НажатиеНаГиперссылкуДиагностики();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаОжиданиеОтветаОтОператораРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НажатиеНаГиперссылкуДиагностики();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСозданиеУчетнойЗаписиЭДОРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НажатиеНаГиперссылкуДиагностики();
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НажатиеНаГиперссылкуДиагностики();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОрганизацииПриИзменении(Элемент)
	
	АдресОрганизацииПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОрганизацииАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)

	УправлениеКонтактнойИнформациейКлиент.АвтоПодборАдреса(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных,
		Ожидание, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура АдресОрганизацииОбработкаВыбора(Элемент, ВыбранноеЗначение, ДополнительныеДанные, ВыборДобавлением, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	АдресОрганизации = ВыбранноеЗначение.Представление;
	ЗначениеПолейАдреса = ВыбранноеЗначение.Адрес;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ОбщегоНазначенияБЭДКлиент.АвтоПодборНайтиИВыделитьОформлением(Элемент.СписокВыбора, Текст, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторЭДОПриИзменении(Элемент)
	
	ПолучитьСпособОбменаПоИдентификатору();
	
КонецПроцедуры

&НаКлиенте
Процедура ДоверенностьПриИзменении(Элемент)
	
	МашиночитаемыеДоверенностиВызовСервера.ТипПоддержанОператором(ИдентификаторОператораЭДО, ТипЗнч(Доверенность));

КонецПроцедуры

&НаКлиенте
Процедура УчетнаяЗаписьОблачногоЭДОСоздание(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Оповещение = Новый ОписаниеОповещения("УчетнаяЗаписьОблачногоЭДОСозданиеЗавершение", ЭтотОбъект);
	ИнтеграцияОблачногоЭДОКлиент.ПодключитьОблачныйЭДО(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ЗарегистрироватьЭДО(Команда)
	
	ОчиститьСообщения();
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаКоманды, Элементы.СтраницаВводДанных);
	
	УстановитьОтображениеЗарегистрироватьЭДОКартинкаИТекст(ЭтотОбъект);
	
	Если Не КлючевыеПоляДляРегистрацииЗаполнены() Тогда
		УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Сертификат) Тогда
		Обработчик = Новый ОписаниеОповещения("ЗарегистрироватьЭДОПослеСозданияСертификатаКриптографии", ЭтотОбъект);
		НайтиСоздатьСертификатКриптографии(Обработчик);
		Возврат;
	КонецЕсли;
	
	Если Не СвойстваПароля.ПарольПроверен Тогда
		ПроверитьПароль();
	Иначе
		НачатьРегистрациюВСервисе();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура Назад(Команда)
	
	ПереключитьТекущуюСтраницуНаЭтапВводаДанных(ЭтотОбъект);
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКриптопровайдер(Команда)
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("ПредлагатьУстановитьПрограмму", Истина);
	
	ЭлектроннаяПодписьКлиент.ПроверитьУстановкуПрограммКриптографии(
		ЭтотОбъект, ПараметрыПроверки, Новый ОписаниеОповещения("ПослеПроверкиПрограммКриптографии", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСертификаты(Команда)
	
	НачатьПроверкуКриптографии();
	
КонецПроцедуры

&НаКлиенте
Процедура ПовторитьДиагностику(Команда)
	
	НачатьПроверкуКриптографии();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПроверкаКриптографии

&НаКлиенте
Процедура ПроверитьУстановкуРасширенияКриптографии()
	
	ТребуетсяУстановкаРасширенияКриптографии = Истина;
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
	ВыполняетсяПроверкаНаличияРасширенияКриптографии = Истина;
	Оповещение = Новый ОписаниеОповещения("ПроверитьУстановкуРасширенияКриптографииПродолжение", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСКриптографией(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУстановкуРасширенияКриптографииПродолжение(Подключено, ДополнительныеПараметры) Экспорт
	
	ВыполняетсяПроверкаНаличияРасширенияКриптографии = Ложь;
	ТребуетсяУстановкаРасширенияКриптографии = НЕ Подключено;
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьПроверкуКриптографии()
	
	ВыполняетсяПроверкаКриптографии = Истина;
	ПроверкаНаличияСертификатовПровалена = Ложь;
	ПроверкаКриптографииПровалена = Ложь;
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
	ПараметрыПроверки = Новый Структура;
	ПараметрыПроверки.Вставить("УстанавливатьРасширение", УстанавливатьРасширение);
	ПараметрыПроверки.Вставить("УстанавливатьКомпоненту", Истина);
	ПараметрыПроверки.Вставить("ПредлагатьУстановитьПрограмму", Ложь);
	ПараметрыПроверки.Вставить("ПроверяемыеПрограммы", Истина);
	
	ЭлектроннаяПодписьКлиент.ПроверитьУстановкуПрограммКриптографии(
		ЭтотОбъект, ПараметрыПроверки, Новый ОписаниеОповещения("ПослеПроверкиПрограммКриптографии", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПрограммКриптографии(Результат, ДополнительныеПараметры) Экспорт
	
	ПроверитьУстановкуРасширенияКриптографии();

	Если Результат.Программы.Количество() = 0
			И Результат.ПрограммыНаСервере.Количество() = 0 Тогда
		ВыполняетсяПроверкаКриптографии = Ложь;
		ПроверкаКриптографииПровалена = Истина;
		ПереключитьТекущуюСтраницуНаЭтапДиагностики(ЭтотОбъект);
		УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ПрограммаКриптографии = Результат.Программы[0].Ссылка;
	УстановленВипНет = СтрНайти(Результат.Программы[0].Программа, "VipNet");
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьСвойстваСертификатовНаКлиентеПослеСозданияМенеджераКриптографии", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(ОписаниеОповещения, "");
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеСертификатов

&НаКлиенте
Асинх Процедура ПолучитьСвойстваСертификатовНаКлиентеПослеСозданияМенеджераКриптографии(
	МенеджерКриптографии, ДополнительныеПараметры) Экспорт
	
	ВыполняетсяПроверкаКриптографии = Ложь;
	
	Если ТипЗнч(МенеджерКриптографии) <> Тип("МенеджерКриптографии") Тогда
		ПроверкаКриптографииПровалена = Истина;
		ПереключитьТекущуюСтраницуНаЭтапДиагностики(ЭтотОбъект);
		УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	ВыполняетсяПроверкаНаличияСертификатов = Истина;
	
	ПереключитьТекущуюСтраницуНаЭтапВводаДанных(ЭтотОбъект);
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
	Контекст = Новый Структура;
	Контекст.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	
	ПолучениеХранилища = Контекст.МенеджерКриптографии.ПолучитьХранилищеСертификатовАсинх(ТипХранилищаСертификатовКриптографии.ПерсональныеСертификаты);
	Хранилище = Ждать ПолучениеХранилища;
	
	ПолучениеСертификатов = Хранилище.ПолучитьВсеАсинх();
	Сертификаты = Ждать ПолучениеСертификатов;
	
	ВыполняетсяПроверкаНаличияСертификатов = Ложь;
	
	ОбновитьСписокСертификатов(Сертификаты);
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ОбновитьСписокСертификатов(СвойстваСертификатовНаКлиенте)
	
	ОбработанныеСтроки  = Новый Соответствие;
	Индекс = 0;
	Отбор = Новый Структура("Отпечаток", "");
	
	РазницаСУниверсальнымВременем = ОбщегоНазначенияКлиент.ДатаСеанса() - ОбщегоНазначенияКлиент.ДатаУниверсальная();
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	ОтпечаткиУжеДобавленныхСертификатов = Новый Соответствие; // Для пропуска дублей.
	
	Для Каждого СвойствоСертификата Из СвойстваСертификатовНаКлиенте Цикл
		
		Отпечаток = Base64Строка(СвойствоСертификата.Отпечаток);
		ДатыСертификата = ДатыСертификата(СвойствоСертификата, РазницаСУниверсальнымВременем);
		
		Если ДатыСертификата.ДатаОкончания <= ТекущаяДатаСеанса Тогда
			Продолжить; // Пропуск просроченных сертификатов.
		КонецЕсли;
		
		Если ОтпечаткиУжеДобавленныхСертификатов.Получить(Отпечаток) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		ОтпечаткиУжеДобавленныхСертификатов.Вставить(Отпечаток, Истина);
		
		СвойстваСубъекта = ЭлектроннаяПодписьКлиент.СвойстваСубъектаСертификата(СвойствоСертификата);
		Если СвойстваСубъекта.ИННЮЛ = Неопределено
			И СвойстваСубъекта.ОГРНИП = Неопределено
			И Не ЭтоСертификатФизЛица(СвойстваСубъекта) Тогда
			Продолжить;
		КонецЕсли;
		
		ДвоичныеДанныеСертификата = Ждать СвойствоСертификата.ВыгрузитьАсинх();
		
		ДатаОкончанияЗакрытогоКлюча = КриптографияБЭДКлиентСервер.ДатаОкончанияЗакрытогоКлюча(
			СвойствоСертификата, ДвоичныеДанныеСертификата);
		
		Если ДатаОкончанияЗакрытогоКлюча = Дата(1, 1, 1) Тогда
			ДатаОкончанияЗакрытогоКлюча = ДатыСертификата.ДатаОкончания;
		КонецЕсли;
		
		Если Мин(ДатыСертификата.ДатаОкончания, ДатаОкончанияЗакрытогоКлюча) <= ТекущаяДатаСеанса Тогда
			Продолжить; // Пропуск просроченных сертификатов.
		КонецЕсли;
		
		Отбор.Отпечаток = СвойствоСертификата.Отпечаток;
		Строки = СписокСертификатов.НайтиСтроки(Отбор);
		Если Строки.Количество() = 0 Тогда
			Если СписокСертификатов.Количество() - 1 < Индекс Тогда
				Строка = СписокСертификатов.Добавить();
			Иначе
				Строка = СписокСертификатов.Вставить(Индекс);
			КонецЕсли;
		Иначе
			Строка = Строки[0];
			ИндексСтроки = СписокСертификатов.Индекс(Строка);
			Если ИндексСтроки <> Индекс Тогда
				СписокСертификатов.Сдвинуть(ИндексСтроки, Индекс - ИндексСтроки);
			КонецЕсли;
		КонецЕсли;
		
		СвойстваСертификата = ЭлектроннаяПодписьКлиент.СвойстваСертификата(СвойствоСертификата);
		
		ОбновитьЗначение(Строка.Отпечаток, СвойстваСертификата.Отпечаток);
		ОбновитьЗначение(Строка.Представление, СвойстваСертификата.Представление);
		ОбновитьЗначение(Строка.КемВыдан, СвойстваСертификата.КемВыдан);
		ОбработанныеСтроки.Вставить(Строка, Истина);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Индекс = СписокСертификатов.Количество() - 1;
	Пока Индекс >= 0 Цикл
		Строка = СписокСертификатов.Получить(Индекс);
		Если ОбработанныеСтроки.Получить(Строка) = Неопределено Тогда
			СписокСертификатов.Удалить(Индекс);
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
	
	Элементы.Сертификат.СписокВыбора.Очистить();
	
	СписокСертификатов.Сортировать("Представление Возр");
	Для Каждого Строка Из СписокСертификатов Цикл
		Элементы.Сертификат.СписокВыбора.Добавить(Строка.Отпечаток, Строка.Представление);
	КонецЦикла;
	
	ПроверкаНаличияСертификатовПровалена = Элементы.Сертификат.СписокВыбора.Количество() = 0;
	Если Не ПроверкаНаличияСертификатовПровалена Тогда
		СкрытьВидимостьДекорацияОжиданиеПоискаСертификатов(ЭтотОбъект);
	КонецЕсли;
	
	Если ПроверкаНаличияСертификатовПровалена Тогда
		ПереключитьТекущуюСтраницуНаЭтапДиагностики(ЭтотОбъект);
	Иначе
		ПереключитьТекущуюСтраницуНаЭтапВводаДанных(ЭтотОбъект);
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Функция ЭтоСертификатФизЛица(СвойстваСубъекта)
	
	Возврат Не ЗначениеЗаполнено(СвойстваСубъекта.Организация)
		И СтрДлина(СвойстваСубъекта.ИНН) = 12
		И СвойстваСубъекта.ОГРНИП = Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьЗначение(СтароеЗначение, НовоеЗначение, ПропускатьНеопределенныеЗначения = Ложь)
	
	Если НовоеЗначение = Неопределено И ПропускатьНеопределенныеЗначения Тогда
		Возврат;
	КонецЕсли;
	
	Если СтароеЗначение <> НовоеЗначение Тогда
		СтароеЗначение = НовоеЗначение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ДатыСертификата(Сертификат, ДобавкаВремени)
	
	ДатыСертификата = Новый Структура;
	ДатыСертификата.Вставить("ДатаНачала", Сертификат.ДатаНачала + ДобавкаВремени);
	ДатыСертификата.Вставить("ДатаОкончания", Сертификат.ДатаОкончания + ДобавкаВремени);
	
	Возврат ДатыСертификата;
	
КонецФункции

#КонецОбласти

#Область ЗаполнениеПоИНН

&НаКлиенте
Процедура ЗаполнитьПоИНН()
	
	ДлинаИНН = СтрДлина(ИНН);
	Если ДлинаИНН = 10 Тогда
		ЭтоЮридическоеЛицо = Истина;
	ИначеЕсли ДлинаИНН = 12 Тогда
		ЭтоЮридическоеЛицо = Ложь;
	Иначе
		Возврат;
	КонецЕсли;
	
	УстановитьВидимостьОГРНКПП(ЭтотОбъект);
	
	Если Не ЗаполнятьРеквизитыПоИНН Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = "";
	СоответствуетТребованиям = РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(
		ИНН, ЭтоЮридическоеЛицо, ТекстСообщения);
	
	Если Не СоответствуетТребованиям Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , "ИНН");
		Возврат;
	КонецЕсли;
	
	ЗапуститьПоискПоИНН();
	ЗапуститьОжиданиеПолученияСведенийОрганизацииПоИНН();
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьПоискПоИНН()
	
	УстановитьВидимостьДекорацииПоПоискуПоИНН(ЭтотОбъект);

	Если Не ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.РаботаСКонтрагентами") Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоЮридическоеЛицо = СтрДлина(ИНН) = 10;
	Если ЭтоЮридическоеЛицо Тогда
		ИмяФункции = "РаботаСКонтрагентами.СведенияОЮридическомЛицеПоИНН";
	Иначе
		ИмяФункции = "РаботаСКонтрагентами.РеквизитыПредпринимателяПоИНН";
	КонецЕсли;

	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение сведений организации по ИНН в сервисе 1С:Контрагент';
															|en = 'Get information about a company by TIN in the 1C:Counterparty service'");
	ДлительнаяОперацияПолученияСведенийПоИНН = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, ИмяФункции, ИНН);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеПолученияСведенийОрганизацииПоИНН()
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда
		СкрытьВидимостьДекорацииПоПоискуПоИНН(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Описание = Новый ОписаниеОповещения("ЗаполнитьОрганизациюПослеПолученияДанных", ЭтотОбъект);
	ЗапуститьОжиданиеВыполненияДлительнойОперации(Описание, ДлительнаяОперацияПолученияСведенийПоИНН);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОрганизациюПослеПолученияДанных(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперацияПолученияСведенийПоИНН = Неопределено;
	
	Если Результат.Статус = СостоянияПодключения().Выполнено Тогда
		ЗаполнитьСведенияДляРегистрацииПоАдресуРезультата(Результат.АдресРезультата);
		ОбщегоНазначенияКлиент.ОповеститьОбИзмененииОбъекта(Организация);
	ИначеЕсли Результат.Статус = СостоянияПодключения().Ошибка Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	СкрытьВидимостьДекорацииПоПоискуПоИНН(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияДляРегистрацииПоАдресуРезультата(АдресРезультата)
	
	РеквизитыОрганизации = ПолучитьИзВременногоХранилища(АдресРезультата);
	НайденыСведенияПоОрганизации = РеквизитыОрганизации <> Неопределено
		И Не ЗначениеЗаполнено(РеквизитыОрганизации.ОписаниеОшибки);
		
	Если ЭтоЮридическоеЛицо Тогда
		НайденыСведенияПоОрганизации = НайденыСведенияПоОрганизации И РеквизитыОрганизации.ЕГРЮЛ <> Неопределено;
		Если РеквизитыОрганизации.ЕГРЮЛ <> Неопределено Тогда
			РеквизитыОрганизации = РеквизитыОрганизации.ЕГРЮЛ;
		КонецЕсли;
	КонецЕсли;
	
	ИзвлечьКодНалоговогоОрганаИзИНН(КодНалоговогоОргана, ИНН);
	
	Если Не НайденыСведенияПоОрганизации Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоЮридическоеЛицо Тогда
		КПП = РеквизитыОрганизации.КПП;
		АдресОрганизации = РеквизитыОрганизации.ЮридическийАдрес.Представление;
		ЗначениеПолейАдреса = РеквизитыОрганизации.ЮридическийАдрес.КонтактнаяИнформация;
	КонецЕсли;
	
	Если РеквизитыОрганизации.РегистрацияВНалоговомОргане <> Неопределено Тогда
		КодНалоговогоОргана = РеквизитыОрганизации.РегистрацияВНалоговомОргане.Код;
	КонецЕсли;
	
	ОГРН = РеквизитыОрганизации.РегистрационныйНомер;
	НаименованиеОрганизации = РеквизитыОрганизации.НаименованиеСокращенное;
	
	УстановитьВидимостьВводаИнформацииПоОрганизации();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзвлечьКодНалоговогоОрганаИзИНН(КодНалоговогоОргана, ИНН)
	
	КодНалоговогоОргана = Сред(ИНН, 1, 4);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеИдентификаторов

&НаСервере
Процедура ЗапуститьОбновлениеКешОператоровИФорматов()
	
	КонтекстДиагностики = ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение идентификаторов в сервисе ЭДО';
															|en = 'Receive identifiers in the EDI service'");
	
	ДлительнаяОперацияОбновленияКешОператоровИФорматов = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения, "СинхронизацияЭДО.ОбновитьКешиОператоровЭДОИФорматов", КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеОбновленияКешОператоровИФорматов()
	
	Описание = Новый ОписаниеОповещения("ЗапуститьПолучениеИдентификаторов", ЭтотОбъект);
	ЗапуститьОжиданиеВыполненияДлительнойОперации(Описание, ДлительнаяОперацияОбновленияКешОператоровИФорматов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьПолучениеИдентификаторов(Результат, ДополнительныеПараметры) Экспорт
	
	ЗапуститьПолучениеИдентификаторовНаСервере();
	ЗапуститьОжиданиеПолученияИдентификаторовЭДО();
	
КонецПроцедуры

&НаСервере
Процедура ЗапуститьПолучениеИдентификаторовНаСервере()

	КонтекстДиагностики = ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики();
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение идентификаторов в сервисе ЭДО';
															|en = 'Receive identifiers in the EDI service'");
	
	ДлительнаяОперацияПолученияИдентификаторов = ДлительныеОперации.ВыполнитьФункцию(
		ПараметрыВыполнения,
		"РаботаСАбонентамиЭДО.ПолучитьИдентификаторыВСервисе1СЭДОПоРеквизитам",
		Новый Структура("ИНН, КПП", ИНН, КПП),
		КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеПолученияИдентификаторовЭДО()
	
	Описание = Новый ОписаниеОповещения("ЗаполнитьИдентификаторыДляРегистрацииПослеПолученияДанных", ЭтотОбъект);
	ЗапуститьОжиданиеВыполненияДлительнойОперации(Описание, ДлительнаяОперацияПолученияИдентификаторов);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИдентификаторыДляРегистрацииПослеПолученияДанных(Результат, ДополнительныеПараметры) Экспорт
	
	ДлительнаяОперацияПолученияИдентификаторов = Неопределено;
	
	Если Результат <> Неопределено
		И Результат.Статус = СостоянияПодключения().Выполнено Тогда
		ЗаполнитьИдентификаторыДляРегистрацииПоАдресуРезультата(Результат.АдресРезультата);
	ИначеЕсли Результат <> Неопределено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(Результат.КраткоеПредставлениеОшибки);
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИдентификаторыДляРегистрацииПоАдресуРезультата(Знач АдресРезультата)
	
	ИдентификаторыОрганизации = ПолучитьИзВременногоХранилища(АдресРезультата); // См. УчетныеЗаписиЭДО.ДанныеПоИдентификаторамДляРегистрацииВЭДО
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	ЗаполнитьСписокИдентификаторов(ИдентификаторыОрганизации);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокИдентификаторов(СписокИдентификаторов)
	
	Элементы.ИдентификаторЭДО.СписокВыбора.Очистить();
	
	Если Не ЗначениеЗаполнено(СписокИдентификаторов) Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаСписка Из СписокИдентификаторов Цикл
		Элементы.ИдентификаторЭДО.СписокВыбора.Добавить(СтрокаСписка.Значение);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаСохранениеПароля

&НаКлиенте
Процедура ПроверитьПароль()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПроверкиПароляПодписанием", ЭтотОбъект);
	КриптографияБЭДКлиент.ПроверитьПароль(Сертификат, СвойстваПароля.Пароль, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПроверкиПароляПодписанием(Результат, ДополнительныеПараметры) Экспорт
	
	ОчиститьСообщения();

	СвойстваПароля.ПарольВерен = Результат.Успех;
	СвойстваПароля.ПарольПроверен = Истина;
	
	Элементы.Пароль.РасширеннаяПодсказка.Заголовок = "";
	Если Не Результат.Успех Тогда
		КонтекстДиагностики = Результат.КонтекстДиагностики;
		ОбработатьОшибкиВКонтекстномРежиме(Элементы.Пароль.РасширеннаяПодсказка);
		ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(Сертификат, Неопределено);
	КонецЕсли;
	
	НачатьРегистрациюВСервисе();
	
КонецПроцедуры

#КонецОбласти

#Область ОператорыЭДО

&НаСервере
Процедура ПолучитьСпособОбменаПоИдентификатору()
	
	Для Каждого Оператор Из ОператорыЭДО Цикл
		
		ИдентификаторОператора = Оператор.Идентификатор;
		ДлинаИдентификатораОператора = СтрДлина(ИдентификаторОператора);
		
		Если Лев(ИдентификаторЭДО, ДлинаИдентификатораОператора) = ИдентификаторОператора Тогда
			СпособОбменаЭД = Оператор.СпособОбмена;
			ИдентификаторОператораЭДО = Элементы.ОператорЭДО.СписокВыбора.НайтиПоЗначению(ИдентификаторОператора).Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	СпособОбменаАбонента = ОбменСКонтрагентамиИнтеграция.СпособОбменаАбонентаЭДО(ИдентификаторЭДО);
	Если ЗначениеЗаполнено(СпособОбменаАбонента) Тогда
		СпособОбменаЭД = СпособОбменаАбонента;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПодключенныхОператоровЭДО(ТаблицаОператоров)
	
	Если Не ЗначениеЗаполнено(ТаблицаОператоров) Тогда
		Возврат;
	КонецЕсли;
	
	ОператорыЭДО.Загрузить(ТаблицаОператоров);
	
	Элементы.ОператорЭДО.СписокВыбора.Очистить();
	Для Каждого ДанныеОператораЭДО Из ОператорыЭДО Цикл
		Элементы.ОператорЭДО.СписокВыбора.Добавить(
			ДанныеОператораЭДО.Идентификатор, ДанныеОператораЭДО.Наименование);
	КонецЦикла;
	Если Не ЗначениеЗаполнено(ИдентификаторОператораЭДО) Тогда
		ИдентификаторОператораЭДО = СервисНастроекЭДОКлиентСервер.ИдентификаторКалуги();
		УстановитьСпособОбменаПоОператору(СпособОбменаЭД, ОператорыЭДО, ИдентификаторОператораЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСпособОбменаПоОператору(СпособОбменаЭД, ОператорыЭДО, ИдентификаторОператораЭДО)
	
	ОтборОператоров = Новый Структура("Идентификатор", ИдентификаторОператораЭДО);
	НайденныеОператоры = ОператорыЭДО.НайтиСтроки(ОтборОператоров);
	Если ЗначениеЗаполнено(НайденныеОператоры) Тогда
		СпособОбменаЭД = НайденныеОператоры[0].СпособОбмена;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СозданиеСертификата

&НаКлиенте
Процедура НайтиСоздатьСертификатКриптографии(ОбработчикУспешногоЗавершения = Неопределено)
	
	Контекст = Новый Структура("ОбработчикУспешногоЗавершения", ОбработчикУспешногоЗавершения);
	Оповещение = Новый ОписаниеОповещения("ПослеПоискаСертификата", ЭтотОбъект, Контекст);
	КриптографияБЭДКлиент.НайтиСоздатьСертификатКриптографии(ОтпечатокСертификата, Организация, Оповещение,
		ПрограммаКриптографии);
	УстановитьДоступностьКнопкиЗарегистрироватьЭДО(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ПослеПоискаСертификата(СертификатКриптографии, Контекст) Экспорт
	
	УстановитьДоступностьКнопкиЗарегистрироватьЭДО();
	
	СертификатСозданИлиНайден = ТипЗнч(СертификатКриптографии) <> Тип("СертификатКриптографии");
	Если СертификатСозданИлиНайден Тогда
		Сертификат = СертификатКриптографии;
		Руководитель = СведенияПоРуководителю(Сертификат, Организация);
		ПроверитьНеобходимостьДоверенности();
		УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	Иначе
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось записать сертификат в программе';
														|en = 'Cannot save the certificate in the application'"), , , "ОтпечатокСертификата");
		УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если СертификатСозданИлиНайден И ЗначениеЗаполнено(Контекст.ОбработчикУспешногоЗавершения) Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОбработчикУспешногоЗавершения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьЭДОПослеСозданияСертификатаКриптографии(Результат, Контекст) Экспорт
	
	Если Не КлючевыеПоляДляРегистрацииЗаполнены() Тогда
		УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
		Возврат;
	КонецЕсли;
	
	Если Не СвойстваПароля.ПарольПроверен Тогда
		ПроверитьПароль();
	Иначе
		НачатьРегистрациюВСервисе();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьНеобходимостьДоверенности()
	
	ТребуетсяДоверенность = УчетныеЗаписиЭДОСлужебный.ТребуетсяДоверенностьДляРегистрацииУчетнойЗаписиПоИНН(ИНН,
		Сертификат, СпособОбменаЭД, ИдентификаторОператораЭДО);
	
	ДоверенностьОбязательна = Ложь;
	
	Если ТребуетсяДоверенность И Не ПустаяСтрока(ИдентификаторОператораЭДО) Тогда
		Отбор = Новый Структура("Идентификатор", ИдентификаторОператораЭДО);
		НайденныеСтроки = ОператорыЭДО.НайтиСтроки(Отбор);
		Если НайденныеСтроки.Количество() Тогда
			ДанныеОператора = НайденныеСтроки[0];
			Если ДанныеОператора.ПриРегистрацииПредставителяТребуетсяМЧД = "Требуется" Тогда
				ДоверенностьОбязательна = Истина;
			ИначеЕсли ДанныеОператора.ПриРегистрацииПредставителяТребуетсяМЧД = "НеТребуется" Тогда
				ТребуетсяДоверенность = Ложь;
			ИначеЕсли ДанныеОператора.ПриРегистрацииПредставителяТребуетсяМЧД = "Опционально" Тогда
				ДоверенностьОбязательна = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ТребуетсяДоверенность Тогда
		УчетныеЗаписиЭДОСлужебный.ЗаполнитьСписокВыбораДоверенностей(Организация, Сертификат,
		Элементы.Доверенность);
		Если Элементы.Доверенность.СписокВыбора.Количество() = 1 Тогда
			Доверенность = Элементы.Доверенность.СписокВыбора[0].Значение;
			МашиночитаемыеДоверенностиВызовСервера.ТипПоддержанОператором(
				ИдентификаторОператораЭДО, ТипЗнч(Доверенность));
		ИначеЕсли Элементы.Доверенность.СписокВыбора.Количество() = 0 Тогда 
			Доверенность = Справочники.МЧД003.ПустаяСсылка();
			Элементы.Доверенность.РежимВыбораИзСписка = Ложь;
			Элементы.Доверенность.ВыбиратьТип = Ложь;
			Элементы.Доверенность.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.ОтображатьВВыпадающемСпискеИВПолеВвода;
		Иначе
			Элементы.Доверенность.РежимВыбораИзСписка = Истина;
			Элементы.Доверенность.ВыбиратьТип = Истина;
			Элементы.Доверенность.ОтображениеКнопкиВыбора = ОтображениеКнопкиВыбора.Авто;
		КонецЕсли;
	Иначе
		Доверенность = Справочники.МашиночитаемыеДоверенностиОрганизаций.ПустаяСсылка();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияВСервисе

&НаКлиенте
Процедура СформироватьТекстПодвала()
	
	Элементы.ДекорацияПодвалНаименованиеОрганизации.Заголовок = НаименованиеОрганизации;
	Если ЭтоЮридическоеЛицо Тогда
		Элементы.ДекорацияПодвалИННКПП.Заголовок = СтрШаблон(НСтр("ru = 'ИНН: %1 КПП: %2';
																	|en = 'TIN: %1 KPP: %2'"), ИНН, КПП);
	Иначе
		Элементы.ДекорацияПодвалИННКПП.Заголовок = СтрШаблон(НСтр("ru = 'ИНН: %1 ОГРНИП: %2';
																	|en = 'TIN: %1 OGRNIP: %2'"), ИНН, ОГРН);
	КонецЕсли;
	Элементы.ДекорацияПодвалРуководитель.Заголовок = СтрШаблон(НСтр("ru = 'Руководитель: %1';
																	|en = 'CEO: %1'"), Руководитель);
	Элементы.ДекорацияПодвалАдрес.Заголовок = СтрШаблон(НСтр("ru = 'Адрес: %1';
															|en = 'Address: %1'"), АдресОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура НачатьРегистрациюВСервисе() Экспорт
	
	УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
	
	ПарольВеренИПроверен = СвойстваПароля.ПарольВерен И СвойстваПароля.ПарольПроверен;
	Если Не ПарольВеренИПроверен Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось проверить пароль. Смотрите ""Подробнее""';
														|en = 'Cannot check the password. See ""Details""'"), , , "Пароль");
		Возврат;
	КонецЕсли;
	
	СформироватьТекстПодвала();
	СкрытьЗаголовкиУРасширеннойПодсказки(ЭтотОбъект);
	ПереключитьТекущуюСтраницуНаЭтапРегистрации(ЭтотОбъект);
	
	РегистрацияВыполнена = Ложь;
	ВыполняетсяРегистрация = Истина;
	УстановкаСостоянияПолученияИдентификатораЭД(Ложь);
	
	Если ЭтоОблачныйЭДО Тогда
		ЗарегистрироватьСертификатЧерезОблачныйЭДО();
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		ЗарегистрироватьСертификат1СЭДО();
	ИначеЕсли СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
		ТестСвязиСервисаЭДО();
	Иначе
		ВыполняетсяРегистрация = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#Область РегистрацияВСервисе1СЭДО

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДО()
	
	ПараметрыРегистрации = СервисЭДОКлиент.НовыеПараметрыРегистрацииВСервисе1СЭДО();
	ПараметрыРегистрации.Организация = Организация;
	ПараметрыРегистрации.ОператорЭДО = ИдентификаторОператораЭДО;
	ПараметрыРегистрации.АдресОрганизации = АдресОрганизации;
	ПараметрыРегистрации.АдресОрганизацииЗначение = ЗначениеПолейАдреса;
	ПараметрыРегистрации.КодНалоговогоОргана = КодНалоговогоОргана;
	ПараметрыРегистрации.СертификатыПодписейОрганизации.Добавить(Сертификат);
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ПараметрыРегистрации.Доверенности.Вставить(Сертификат, Доверенность);
	КонецЕсли;
	Если ВидПодключения = ВидыПодключения().Существующий Тогда
		ПараметрыРегистрации.ИдентификаторОрганизации = ИдентификаторЭДО;
	КонецЕсли;
	
	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	Оповещение = Новый ОписаниеОповещения("ЗарегистрироватьСертификат1СЭДОЗавершение", ЭтотОбъект);
	УчетныеЗаписиЭДОКлиент.ЗарегистрироватьСертификатВСервисе1СЭДО(
		ЭтотОбъект, Оповещение, ПараметрыРегистрации, КонтекстДиагностики);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.Успех Тогда
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Ложь);
		УникальныйИдентификаторЗаявки1СЭДО = Результат.УникальныйИдентификаторЗаявки1СЭДО;
		ПаролиСертификатов = Результат.ПаролиСертификатов;
		// Запустим обработчик ожидания результата регистрации организации у оператора.
		ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания(
			"Подключаемый_ПроверитьВыполнениеЗаданияДляЭД", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	Иначе
		ВыполняетсяРегистрация = Ложь;
		УстановкаСостоянияПолученияИдентификатораЭД(СостоянияПодключения().Ошибка);
		КонтекстДиагностики = Результат.КонтекстДиагностики;
		ОбработатьОшибкиВКонтекстномРежиме(Элементы.ГруппаОтправкаЗаявленияНаРегистрацию.РасширеннаяПодсказка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкиВКонтекстномРежиме(ЭлементДляОтображенияОшибок)
	
	ПараметрыОбработкиОшибок = ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок();
	ПараметрыОбработкиОшибок.КонтекстныйРежимОбработки = Истина;
	ПараметрыОбработкиОшибок.НадписьПредупреждение = ЭлементДляОтображенияОшибок;
	ПараметрыОбработкиОшибок.ТекстПредупреждения = ТекстПредупреждения();
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики, ПараметрыОбработкиОшибок);
	
КонецПроцедуры

&НаКлиенте
Функция ТекстПредупреждения()
	
	Текст = НСтр("ru = 'Не удалось выполнить операцию. <a style=""font: ЖирныйШрифтБЭД"" href=""Подробнее"">Подробнее</a>.';
				|en = 'Cannot perform the operation. <a style=""font: ЖирныйШрифтБЭД"" href=""Подробнее"">Details</a>.'");
	
	Возврат СтроковыеФункцииКлиент.ФорматированнаяСтрока(Текст); 
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияДляЭД()
	
	Оповещение = Новый ОписаниеОповещения("ПолучениеСостоянияРегистрацииСертификатаВ1СЭДОЗавершение", ЭтотОбъект);
	СервисЭДОКлиент.ПолучитьСостояниеРегистрацииВСервисе1СЭДО(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УникальныйИдентификаторЗаявки1СЭДО), Оповещение,
		УникальныйИдентификатор);
		
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеСостоянияРегистрацииСертификатаВ1СЭДОЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, СостоянияПодключения().Ошибка);
	КонецЕсли;
	
	Если Результат.Статус = СостоянияПодключения().Выполнено Тогда
		
		РезультатФункции = ПолучитьИзВременногоХранилища(Результат.АдресРезультата); // см. СервисЭДО.ПолучитьСостояниеРегистрацииВСервисе1СЭДО
		СостояниеЗаявки = РезультатФункции.СостояниеРегистрации[УникальныйИдентификаторЗаявки1СЭДО];
		ИдентификаторЭДО = СостояниеЗаявки.ИдентификаторОрганизации;
		
		Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
			
			ТестСвязиСервисаЭДО();
			
		Иначе
			КонтекстДиагностики = РезультатФункции.КонтекстДиагностики;
			ОбработатьОшибкиВКонтекстномРежиме(Элементы.ГруппаОжиданиеОтветаОтОператора.РасширеннаяПодсказка);
			
			Если Не СостояниеЗаявки.Результат Тогда
				УстановкаСостоянияПолученияИдентификатораЭД(Истина, СостоянияПодключения().Ошибка);
				Возврат;
			КонецЕсли;
			
			ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД",
					ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
		
	Иначе
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при регистрации учетной записи у оператора ЭДО.
			|Подробности см. в журнале регистрации.';
			|en = 'The error occurred when the account was registered with the EDI service provider.
			|See more details in the event log.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, СостоянияПодключения().Ошибка);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТестСвязиСервисаЭДО()
	
	УстановкаСостоянияПолученияИдентификатораЭД(Истина, Истина, Ложь);
	
	Если СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО")
		И ВидПодключения = ВидыПодключения().Существующий Тогда
		ПолучитьПараметрыУведомлений();
	Иначе
		ВыполнитьОтправкуДанныхВСервисНастроек();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПараметрыУведомлений()
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьПараметрыУведомленийЗавершение", ЭтотОбъект);
	
	Если ЭтоОблачныйЭДО Тогда
		ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"УчетныеЗаписиЭДОИнтеграцияОблакаКлиент");
		ПараметрыПолучения = ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НовыеПараметрыПолученияНастройкиУведомленийУчетнойЗаписиЭДО();
		ПараметрыПолучения.ОповещениеОЗавершении = Оповещение;
		ПараметрыПолучения.ВыбранныйСертификат = Сертификат;
		ПараметрыПолучения.ИдентификаторУчетнойЗаписиЭДО = ИдентификаторЭДО;
		ПараметрыПолучения.ПаролиСертификатов = ПаролиСертификатов;
		ПараметрыПолучения.ФормаВладелец = ЭтотОбъект;
		ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.ПолучитьНастройкиУведомленийУчетнойЗаписиЭДО(ПараметрыПолучения);
	Иначе
		КлючСинхронизации = СинхронизацияЭДОКлиентСервер.НовыйКлючСинхронизации();
		КлючСинхронизации.ИдентификаторУчетнойЗаписи = ИдентификаторЭДО;
		КлючСинхронизации.СпособОбмена = СпособОбменаЭД;
		КлючСинхронизации.ВыбранныйСертификат = Сертификат;
		
		СервисЭДОКлиент.ПолучитьПараметрыУведомлений(КлючСинхронизации, КонтекстДиагностики, Оповещение, ЭтотОбъект,
			ПаролиСертификатов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьПараметрыУведомленийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТекущиеНастройкиУведомлений = Результат.ПараметрыУведомлений;
	КонтекстДиагностики = Результат.КонтекстДиагностики;
	Если ТекущиеНастройкиУведомлений = Неопределено Тогда
		ОбработатьОшибкиВКонтекстномРежиме(Элементы.ГруппаСозданиеУчетнойЗаписиЭДО.РасширеннаяПодсказка);
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Истина, СостоянияПодключения().Ошибка);
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ТекущиеНастройкиУведомлений);
	
	ВыполнитьОтправкуДанныхВСервисНастроек();
	
КонецПроцедуры

#КонецОбласти

#Область РегистрацияЧерезОблачныйЭДО

&НаКлиенте
Процедура ЗарегистрироватьСертификатЧерезОблачныйЭДО()
	
	ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"УчетныеЗаписиЭДОИнтеграцияОблакаКлиент");
	
	ПараметрыРегистрации = ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НовыеПараметрыРегистрацииСертификатовВЭДО();
	ПараметрыРегистрации.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
	ПараметрыРегистрации.Организация = Организация;
	ПараметрыРегистрации.ОператорЭДО = ИдентификаторОператораЭДО;
	ПараметрыРегистрации.АдресОрганизации = АдресОрганизации;
	ПараметрыРегистрации.АдресОрганизацииЗначение = ЗначениеПолейАдреса;
	ПараметрыРегистрации.КодНалоговогоОргана = КодНалоговогоОргана;
	ПараметрыРегистрации.СертификатыДляПодписания.Добавить(Сертификат);
	ПараметрыРегистрации.СертификатыДляРегистрации.Добавить(Сертификат);
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ПараметрыРегистрации.Доверенности.Вставить(Сертификат, Доверенность);
	КонецЕсли;
	Если ВидПодключения = ВидыПодключения().Существующий Тогда
		ПараметрыРегистрации.ИдентификаторОрганизации = ИдентификаторЭДО;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ЗарегистрироватьСертификатЧерезОблачныйЭДОЗавершение", ЭтотОбъект);
	ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НачатьОтправкуСертификатовДляРегистрацииВЭДО(
		Оповещение, ПараметрыРегистрации, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификатЧерезОблачныйЭДОЗавершение(Результат, Контекст) Экспорт
	
	Если Не ВыполняетсяРегистрация Тогда 
		Возврат;
	КонецЕсли;
	
	Если Результат.Успех Тогда
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Ложь);
		УникальныйИдентификаторЗаявки1СЭДО = Результат.ИдентификаторЗаявки;
		ПаролиСертификатов = Результат.ПаролиСертификатов;
		ОжиданиеОперацийБЭДКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПроверитьРезультатРегистрацииСертификатЧерезОблачныйЭДО",
			ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			
	ИначеЕсли Результат.СертификатыУжеЗарегистрированы Тогда
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Ложь);
		УникальныйИдентификаторЗаявки1СЭДО = Результат.ИдентификаторЗаявки;
		ПаролиСертификатов = Результат.ПаролиСертификатов;
		РезультатРегистрации = УчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НовыйРезультатРегистрацииСертификатовВЭДО();
		РезультатРегистрации.ИдентификаторыЭДО.Вставить(Результат.ИдентификаторЗаявки, ИдентификаторЭДО);
		ОбработатьРезультатРегистрацииСертификатаЧерезОблачныйЭДО(РезультатРегистрации);
	Иначе
		ВыполняетсяРегистрация = Ложь;
		УстановкаСостоянияПолученияИдентификатораЭД(СостоянияПодключения().Ошибка);
		КонтекстДиагностики = Результат.КонтекстДиагностики;
		ОбработатьОшибкиВКонтекстномРежиме(Элементы.ГруппаОтправкаЗаявленияНаРегистрацию.РасширеннаяПодсказка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРезультатРегистрацииСертификатЧерезОблачныйЭДО()
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатРегистрацииСертификатаЧерезОблачныйЭДО", ЭтотОбъект);
	
	ИдентификаторыЗаявок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(УникальныйИдентификаторЗаявки1СЭДО);
	ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО = Новый Соответствие;
	ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО.Вставить(УчетнаяЗаписьОблачногоЭДО, ИдентификаторыЗаявок);
	
	ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
		"УчетныеЗаписиЭДОИнтеграцияОблакаКлиент");
	ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.НачатьПолучениеРезультатаРегистрацииСертификатовЭДО(
		Оповещение, ИдентификаторыЗаявокПоУчетнымЗаписямОблачногоЭДО, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьРезультатРегистрацииСертификатаЧерезОблачныйЭДО(РезультатРегистрации, Контекст = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(РезультатРегистрации.ЗаявкиВОбработке) Тогда
		ОжиданиеОперацийБЭДКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("ПроверитьРезультатРегистрацииСертификатЧерезОблачныйЭДО",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЭДО = РезультатРегистрации.ИдентификаторыЭДО[УникальныйИдентификаторЗаявки1СЭДО];
	
	Если ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		Элементы.ГруппаОжиданиеОтветаОтОператора.РасширеннаяПодсказка.Заголовок = "";
		ТестСвязиСервисаЭДО();
	Иначе
		ВыполняетсяРегистрация = Ложь;
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, СостоянияПодключения().Ошибка);
		КонтекстДиагностики = РезультатРегистрации.КонтекстДиагностики;
		ОбработатьОшибкиВКонтекстномРежиме(Элементы.ГруппаОжиданиеОтветаОтОператора.РасширеннаяПодсказка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ВыполнитьОтправкуДанныхВСервисНастроек()
	
	ДанныеСохранены = СохранитьУчетнуюЗапись();
	
	Если Не ДанныеСохранены Тогда 
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Истина, СостоянияПодключения().Ошибка);
		Возврат;
	КонецЕсли;
	
	ПереключитьТекущуюСтраницуНаЭтапРегистрации(ЭтотОбъект);
	
	Если ЭтоОблачныйЭДО Тогда
		Оповещение = Новый ОписаниеОповещения("ОтправкаДанныхВСервисНастроекЧерезОблачныйЭДОЗавершение", ЭтотОбъект, ИдентификаторЭДО);
		ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль(
			"УчетныеЗаписиЭДОИнтеграцияОблакаКлиент");
		ОбщийМодульУчетныеЗаписиЭДОИнтеграцияОблакаКлиент.ОбновитьИнформациюОбУчетнойЗаписиЭДО(
			ИдентификаторЭДО, Оповещение, ЭтотОбъект);
	Иначе
		Оповещение = Новый ОписаниеОповещения("ОтправкаДанныхВСервисНастроекЗавершение", ЭтотОбъект, ИдентификаторЭДО);
		СервисНастроекЭДОКлиент.ОтправитьДанныеАбонентаВСервисНастроек(
			ИдентификаторЭДО, Оповещение, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаДанныхВСервисНастроекЗавершение(Результат, ИдентификаторЭДО) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = СостоянияПодключения().Выполнено Тогда
		Оповещение = Новый ОписаниеОповещения("ЗавершитьРегистрациюПослеВключенияАвтоматическогоПолученияЭДО",
			ЭтотОбъект, ИдентификаторЭДО);
		ВключитьАвтоматическоеПолучениеЭДО(Оповещение);
	Иначе
		УстановкаСостоянияПолученияИдентификатораЭД(Истина, Истина, СостоянияПодключения().Ошибка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправкаДанныхВСервисНастроекЧерезОблачныйЭДОЗавершение(Результат, ИдентификаторЭДО) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	КонецЕсли;
	
	ВключитьАвтоматическоеПолучениеЭДО(Новый ОписаниеОповещения);
	
КонецПроцедуры

&НаСервере
Функция СохранитьУчетнуюЗапись()
	
	ШаблонНаименование = "%1, %2";
	НаименованиеУчетнойЗаписи = СтрШаблон(ШаблонНаименование, Организация, СпособОбменаЭД);
	
	ОписаниеУчетнойЗаписи = УчетныеЗаписиЭДОКлиентСервер.НовоеОписаниеУчетнойЗаписи();
	ОписаниеУчетнойЗаписи.Наименование = НаименованиеУчетнойЗаписи;
	ОписаниеУчетнойЗаписи.Организация = Организация;
	ОписаниеУчетнойЗаписи.АдресОрганизации = АдресОрганизации;
	ОписаниеУчетнойЗаписи.АдресОрганизацииЗначение = ЗначениеПолейАдреса;
	ОписаниеУчетнойЗаписи.СпособОбмена = СпособОбменаЭД;
	ОписаниеУчетнойЗаписи.Идентификатор = ИдентификаторЭДО;
	ОписаниеУчетнойЗаписи.Сертификаты = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Сертификат);
	ОписаниеУчетнойЗаписи.КодНалоговогоОргана = КодНалоговогоОргана;
	ОписаниеУчетнойЗаписи.Оператор = ИдентификаторОператораЭДО;
	ОписаниеУчетнойЗаписи.ПринятыУсловияИспользования = Истина;
	ОписаниеУчетнойЗаписи.ЭтоОблачныйЭДО = ЭтоОблачныйЭДО;
	ОписаниеУчетнойЗаписи.УчетнаяЗаписьОблачногоЭДО = УчетнаяЗаписьОблачногоЭДО;
	Если ЗначениеЗаполнено(Доверенность) Тогда
		ОписаниеУчетнойЗаписи.Доверенности.Вставить(Сертификат, Доверенность);
	КонецЕсли;
	
	ОтборОператоров = Новый Структура("Идентификатор", ИдентификаторОператораЭДО);
	НайденныеОператоры = ОператорыЭДО.НайтиСтроки(ОтборОператоров);
	Если ЗначениеЗаполнено(НайденныеОператоры) Тогда
		ОписаниеУчетнойЗаписи.ПредставлениеОператора = НайденныеОператоры[0].Наименование;
	КонецЕсли;
		
	Возврат УчетныеЗаписиЭДО.СоздатьУчетнуюЗапись(ОписаниеУчетнойЗаписи, Ложь);
	
КонецФункции

&НаКлиенте
Процедура ЗавершитьРегистрациюПослеВключенияАвтоматическогоПолученияЭДО(Результат, Контекст) Экспорт
	
	ВыполняетсяРегистрация = Ложь;
	РегистрацияВыполнена = Истина;
	Оповестить("ОбновленСписокУчетныхЗаписей1СЭДО");
	Оповестить(УчетныеЗаписиЭДОКлиент.ИмяСобытияСозданаУчетнаяЗаписьЭДО(), Организация);
	УстановкаСостоянияПолученияИдентификатораЭД(Истина, Истина, Истина);
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область АвтоматическоеПолучениеЭДО

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения
//  ИдентификаторЭДО - Строка
&НаКлиенте
Процедура ВключитьАвтоматическоеПолучениеЭДО(ОповещениеОЗавершении)
	
	Если Не ИспользоватьАвтоматическоеПолучениеЭДО
		Или СпособОбменаЭД <> ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", ОповещениеОЗавершении);
	Контекст.Вставить("ИдентификаторЭДО", ИдентификаторЭДО);
	
	Оповещение = Новый ОписаниеОповещения("ВключитьАвтоматическоеПолучениеЭДОЗавершение", ЭтотОбъект, Контекст);
	
	ПараметрыАвторизации = УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО();
	ПараметрыАвторизации.ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ИдентификаторЭДО);
	ПараметрыАвторизации.ФормаВладелец = ЭтотОбъект;
	ПараметрыАвторизации.ПаролиСертификатов = ПаролиСертификатов;
	АвтоматическоеПолучениеЭДОКлиент.ВключитьПоУчетнымЗаписямЭДО(Оповещение, ПараметрыАвторизации);
	
КонецПроцедуры

// Параметры:
//  Результат - Неопределено
//            - см. АвтоматическоеПолучениеЭДОКлиент.ВключитьПоУчетнымЗаписямЭДОЗавершение
//  Контекст - Структура:
//  * ОповещениеОЗавершении - см. ВключитьАвтоматическоеПолучениеЭДО.ОповещениеОЗавершении
//  * ИдентификаторЭДО - Строка
&НаКлиенте
Процедура ВключитьАвтоматическоеПолучениеЭДОЗавершение(Результат, Контекст) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении);
		Возврат;
	КонецЕсли;
	
	ИзменениеСостояния = Результат.ИзменениеСостояния;
	Подключен = ИзменениеСостояния <> Неопределено
		И ИзменениеСостояния.СостояниеПоИдентификаторамЭДО[Контекст.ИдентификаторЭДО] <> Неопределено;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
	ВыполнитьОбработкуОповещения(Контекст.ОповещениеОЗавершении, Подключен);
	
КонецПроцедуры

#КонецОбласти

#Область ИнтеграцияОблачногоЭДО

// Параметры:
//  Результат - см. ИнтеграцияОблачногоЭДОВызовСервера.ПодключитьОблачныйЭДО
//  Контекст - Неопределено
&НаКлиенте
Процедура УчетнаяЗаписьОблачногоЭДОСозданиеЗавершение(Результат, Контекст = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(Результат)
		И ЗначениеЗаполнено(Результат.УчетнаяЗапись) Тогда
		УчетнаяЗаписьОблачногоЭДО = Результат.УчетнаяЗапись;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ИмяСобытия - Строка
//  Параметр - Структура
&НаКлиенте
Процедура ОбработатьЗаписьПользователяОблачногоЭДО(ИмяСобытия, Параметр)
	
	Если Не ЭтоОблачныйЭДО Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ИмяСобытия = "Запись_ПользователиОблачногоЭДО" Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Параметр) Тогда
		Возврат;
	КонецЕсли;
	
	ПользовательЗаписи = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметр, "Пользователь");
	Если ПользовательЗаписи = ПользователиКлиент.ТекущийПользователь()
		И Не ЗначениеЗаполнено(ИдентификаторЭДО) Тогда
		
		ЗапуститьОжиданиеОбновленияКешОператоровИФорматов();
		ЗапуститьОжиданиеПолученияИдентификаторовЭДО();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыполняетсяРегистрация = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьОГРНКПП(Форма)
	
	Форма.Элементы.ДекорацияЗаголовокОГРН.Заголовок = ?(Форма.ЭтоЮридическоеЛицо, НСтр("ru = 'ОГРН:';
																						|en = 'Company registration number:'"), НСтр("ru = 'ОГРНИП:';
																											|en = 'Registration number of individual entrepreneur:'"));
	Форма.Элементы.ГруппаКПП.Видимость = Форма.ЭтоЮридическоеЛицо;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьСертификат()
	
	СвойстваПароля.Пароль = "";
	СвойстваПароля.ПарольВерен = Ложь;
	СвойстваПароля.ПарольПроверен = Ложь;
	Пароль = "";
	Сертификат = Неопределено;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформации)

	Возврат УправлениеКонтактнойИнформацией.ПараметрыВидаКонтактнойИнформации(ВидКонтактнойИнформации);

КонецФункции

&НаСервере
Процедура АдресОрганизацииПриИзмененииНаСервере()
	
	ЗначениеПолейАдреса = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияПоПредставлению(
		АдресОрганизации, ПредопределенноеЗначение("Перечисление.ТипыКонтактнойИнформации.Адрес"));
	
КонецПроцедуры

&НаСервере
Процедура ОчисткаСведенийПоОрганизации(ОчищатьИНН = Истина)
	
	Если ОчищатьИНН Тогда
		ИНН = "";
	КонецЕсли;
	
	КПП = "";
	ОГРН = "";
	НаименованиеОрганизации = "";
	АдресОрганизации = "";
	ЗначениеПолейАдреса = "";
	ИдентификаторЭДО = "";
	Элементы.ИдентификаторЭДО.СписокВыбора.Очистить();
	ОтпечатокСертификата = "";
	Сертификат = Неопределено;
	Доверенность = Неопределено;
	ТребуетсяДоверенность = Ложь;
	ДоверенностьОбязательна = Ложь;
	Пароль = "";
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьЗаголовкиУРасширеннойПодсказки(Форма)
	
	Форма.Элементы.Пароль.РасширеннаяПодсказка.Заголовок = " ";
	Форма.Элементы.ГруппаОтправкаЗаявленияНаРегистрацию.РасширеннаяПодсказка.Заголовок = " ";
	Форма.Элементы.ГруппаОжиданиеОтветаОтОператора.РасширеннаяПодсказка.Заголовок = " ";
	Форма.Элементы.ГруппаСозданиеУчетнойЗаписиЭДО.РасширеннаяПодсказка.Заголовок = " ";
	
КонецПроцедуры

&НаКлиенте
Процедура НажатиеНаГиперссылкуДиагностики()
	
	КонтекстДиагностики.ЗаголовокОперации = НСтр("ru = 'При подключении к ЭДО';
												|en = 'When connecting to EDI'");
	ПараметрыОбработкиОшибок = ОбработкаНеисправностейБЭДКлиент.НовыеПараметрыОбработкиОшибок();
	КонтекстДиагностики.ОшибкиОбработаны = Ложь;
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики, ПараметрыОбработкиОшибок);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	ОчисткаСведенийПоОрганизации();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		СтруктураДанныхОрганизации = ИнтеграцияЭДО.НоваяСтруктураДанныхОрганизации();
		ОбменСКонтрагентамиПереопределяемый.ЗаполнитьРегистрационныеДанныеОрганизации(Организация, СтруктураДанныхОрганизации);
		НаименованиеОрганизации = СтруктураДанныхОрганизации.Наименование;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДанныхОрганизации);
		ЭтоЮридическоеЛицо = ВРег(СтруктураДанныхОрганизации.ЮрФизЛицо) = ВРег("ЮрЛицо");
		ИзвлечьКодНалоговогоОрганаИзИНН(КодНалоговогоОргана, ИНН);
		
		ЮрАдресОрганизации = ИнтеграцияБСПБЭД.КонтактнаяИнформацияОбъекта(Организация, "ЮрАдресОрганизации");
		
		АдресОрганизации = ЮрАдресОрганизации.Представление;
		ЗначениеПолейАдреса = ЮрАдресОрганизации.Значение;
		
		ИННСуществующейОрганизации = СтруктураДанныхОрганизации.ИНН;
		КППСуществующейОрганизации = СтруктураДанныхОрганизации.КПП;
		
	КонецЕсли;
	
	УстановитьВидимостьВводаИнформацииПоОрганизации();
	ЗапуститьОбновлениеКешОператоровИФорматов();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПроверитьДанныеОрганизации(Форма, ВыводитьСообщения = Ложь)

	Ошибки = Неопределено;
	
	Если Не ЗначениеЗаполнено(Форма.ИНН) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнен ИНН организации';
								|en = 'Company TIN is required'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИНН", ТекстСообщения);
	КонецЕсли;
	Если ЗначениеЗаполнено(Форма.ИНН) И Не (СтрДлина(Форма.ИНН) = 10 Или СтрДлина(Форма.ИНН) = 12) Тогда
		ТекстСообщения = НСтр("ru = 'ИНН должен состоять из 10 цифр для организации или из 12 для ИП';
								|en = 'TIN must consist of 10 digits for a company or 12 for an individual entrepreneur'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИНН", ТекстСообщения);
	КонецЕсли;
	
	Если Форма.ЭтоЮридическоеЛицо И Не ЗначениеЗаполнено(Форма.КПП) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнен КПП организации';
								|en = 'Company KPP is required'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "КПП", ТекстСообщения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.ОГРН) Тогда
		ШаблонСообщения = НСтр("ru = 'Не заполнен %1 организации';
								|en = 'Company %1 is required'");
		Если Форма.ЭтоЮридическоеЛицо Тогда
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'ОГРН';
															|en = 'Registration number'"));
		Иначе
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НСтр("ru = 'ОГРНИП';
															|en = 'Registration number of IE'"));
		КонецЕсли;
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ОГРН", ТекстСообщения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.НаименованиеОрганизации) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнено название организации';
								|en = 'Company name is required'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Наименование", ТекстСообщения);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Форма.АдресОрганизации) Тогда
		ТекстСообщения = НСтр("ru = 'Не заполнен адрес организации';
								|en = 'Company address is required'");
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "АдресОрганизации", ТекстСообщения);
	КонецЕсли;
	
	Если ВыводитьСообщения Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	КонецЕсли;
	
	Возврат Ошибки = Неопределено; 
	
КонецФункции

&НаКлиенте
Функция КлючевыеПоляДляРегистрацииЗаполнены()
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрана организация для подключения к ЭДО.';
								|en = 'Company to connect to EDI is not selected.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , , "Организация");
		Возврат Ложь;
	КонецЕсли;
	
	ЕстьОшибки = Не ПроверитьДанныеОрганизации(ЭтотОбъект, Истина);
	
	ЕстьИдентификаторУчетнойЗаписи = ВидПодключения = ВидыПодключения().Новый Или ЗначениеЗаполнено(ИдентификаторЭДО);
	Если Не ЕстьИдентификаторУчетнойЗаписи Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен идентификатор ЭДО';
														|en = 'EDI ID is not filled in'"), , ,
		"ИдентификаторЭДО", ЕстьОшибки);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ОтпечатокСертификата) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнен сертификат';
														|en = 'Certificate is required'"), , ,
		"ОтпечатокСертификата", ЕстьОшибки);
	КонецЕсли;
	
	Если ЭтоОблачныйЭДО И Не ЗначениеЗаполнено(УчетнаяЗаписьОблачногоЭДО) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнена учетная запись облачного ЭДО';
														|en = 'Cloud EDI account is required'"), , ,
		"УчетнаяЗаписьОблачногоЭДО", ЕстьОшибки);
	КонецЕсли;
	
	Если ДоверенностьОбязательна И Не ЗначениеЗаполнено(Доверенность) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не заполнена доверенность';
														|en = 'Letter of authority is required'"), , ,
		"Доверенность", ЕстьОшибки);
	КонецЕсли;
	
	Возврат Не ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура ОбработатьПолеПароля()
	
	Элементы.Пароль.РежимПароля = Не Элементы.Пароль.РежимПароля;
	Если Элементы.Пароль.РежимПароля Тогда
		Элементы.Пароль.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыВидны;
	Иначе
		Элементы.Пароль.КартинкаКнопкиВыбора = БиблиотекаКартинок.ВводимыеСимволыСкрыты;
	КонецЕсли;
	
	СкрытьКоличествоСимволовУПароля();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьКоличествоСимволовУПароля()
	
	Если ЗначениеЗаполнено(Пароль) И Элементы.Пароль.РежимПароля Тогда
		Пароль = СкрытыйПароль();
	Иначе
		Пароль = СвойстваПароля.Пароль;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СкрытыйПароль()
	
	Возврат "****************";
	
КонецФункции

&НаКлиенте
Процедура НовыйСвойстваПароля()
	
	СвойстваПароля = Новый Структура("Пароль, ПарольВерен, ПарольПроверен", "", Ложь, Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьТекущуюСтраницуНаЭтапРегистрации(Форма)
	
	Форма.Элементы.СтраницаВводДанных.Видимость = Ложь;
	Форма.Элементы.СтраницаДиагностика.Видимость = Ложь;
	Форма.Элементы.СтраницаРезультатПодключения.Видимость = Истина;
	
	Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаРезультатПодключения;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьТекущуюСтраницуНаЭтапВводаДанных(Форма)
	
	Форма.Элементы.СтраницаВводДанных.Видимость = Истина;
	Форма.Элементы.СтраницаДиагностика.Видимость = Ложь;
	Форма.Элементы.СтраницаРезультатПодключения.Видимость = Ложь;
	
	Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаВводДанных;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ПереключитьТекущуюСтраницуНаЭтапДиагностики(Форма)
	
	Форма.Элементы.СтраницаВводДанных.Видимость = Ложь;
	Форма.Элементы.СтраницаДиагностика.Видимость= Истина;
	Форма.Элементы.СтраницаРезультатПодключения.Видимость = Ложь;
	
	Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиагностика;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьВидимостьДекорацияОжиданиеПоискаСертификатов(Форма)
	
	Форма.Элементы.ДекорацияОжиданиеПоискаСертификатов.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДекорацииПоПоискуПоИНН(Форма)
	
	Форма.Элементы.ДекорацияОжиданиеПоискаИНН.Видимость = Истина;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СкрытьВидимостьДекорацииПоПоискуПоИНН(Форма)
	
	Форма.Элементы.ДекорацияОжиданиеПоискаИНН.Видимость = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗначенияПоУмолчанию()
	
	ВыполняетсяПроверкаКриптографии = Истина;
	ВидПодключения = ВидыПодключения().Новый;
	ИспользоватьАвтоматическоеПолучениеЭДО = АвтоматическоеПолучениеЭДО.Использовать();
	
	ТаблицаОператоров = СервисНастроекЭДО.ОператорыЭлектронногоДокументооборота();
	ЗаполнитьПодключенныхОператоровЭДО(ТаблицаОператоров);
	
	Если ЭтоОблачныйЭДО Тогда
		ОбщийМодульИнтеграцияОблачногоЭДО = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияОблачногоЭДО");
		УчетнаяЗаписьОблачногоЭДО = ОбщийМодульИнтеграцияОблачногоЭДО.ЕдинственнаяУчетнаяЗаписьОблачногоЭДО();
		Если Не ЗначениеЗаполнено(УчетнаяЗаписьОблачногоЭДО) Тогда
			ТекущийЭлемент = Элементы.УчетнаяЗаписьОблачногоЭДО;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановкаСостоянияПолученияИдентификатораЭД(
		ОтправкаЗаявления, ОжиданиеОтвета = Неопределено, СозданиеУчетнойЗаписи = Неопределено)
	
	Элементы.ГруппаЗаявлениеВыполнение.Видимость = ОтправкаЗаявления = Ложь;
	Элементы.ГруппаЗаявлениеВыполнено.Видимость = ОтправкаЗаявления = Истина;
	Элементы.ГруппаЗаявлениеОшибка.Видимость = ОтправкаЗаявления = СостоянияПодключения().Ошибка;
	
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаЗаявлениеВыполнение, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаЗаявлениеВыполнено, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаЗаявлениеОшибка, Элементы.СтраницаРезультатПодключения);
	
	Элементы.ГруппаОжиданияОтветаОжидание.Видимость = ОжиданиеОтвета = Неопределено;
	Элементы.ГруппаОжиданияОтветаВыполнение.Видимость = ОжиданиеОтвета = Ложь;
	Элементы.ГруппаОжиданияОтветаВыполнено.Видимость = ОжиданиеОтвета = Истина;
	Элементы.ГруппаОжиданияОтветаОшибка.Видимость = ОжиданиеОтвета = СостоянияПодключения().Ошибка;
	
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаОжиданияОтветаОжидание, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаОжиданияОтветаВыполнение, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаОжиданияОтветаВыполнено, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаОжиданияОтветаОшибка, Элементы.СтраницаРезультатПодключения);
	
	Элементы.ГруппаСозданиеУчетнойЗаписиЭДООжидание.Видимость = СозданиеУчетнойЗаписи = Неопределено;
	Элементы.ГруппаСозданиеУчетнойЗаписиЭДОВыполнение.Видимость = СозданиеУчетнойЗаписи = Ложь;
	Элементы.ГруппаСозданиеУчетнойЗаписиЭДОВыполнено.Видимость = СозданиеУчетнойЗаписи = Истина;
	Элементы.ГруппаСозданиеУчетнойЗаписиЭДООшибка.Видимость = СозданиеУчетнойЗаписи = СостоянияПодключения().Ошибка;
	
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаСозданиеУчетнойЗаписиЭДООжидание, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаСозданиеУчетнойЗаписиЭДОВыполнение, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаСозданиеУчетнойЗаписиЭДОВыполнено, Элементы.СтраницаРезультатПодключения);
	УстановитьТекущийЭлементПоВидимостиГрупп(ЭтотОбъект, Элементы.ГруппаСозданиеУчетнойЗаписиЭДООшибка, Элементы.СтраницаРезультатПодключения);
	
	Если ОтправкаЗаявления = СостоянияПодключения().Ошибка
		Или ОжиданиеОтвета = СостоянияПодключения().Ошибка
		Или СозданиеУчетнойЗаписи = СостоянияПодключения().Ошибка Тогда
			ВыполняетсяРегистрация = Ложь;
	КонецЕсли;
	
	УстановитьВидимостьЭлементовФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьВидаПодключения(Форма)
	
	Форма.Элементы.ГруппаВидПодключения.Видимость =
		ЗначениеЗаполнено(Форма.ИдентификаторОператораЭДО)
		И Не (Форма.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
				И Форма.ИдентификаторОператораЭДО = СервисНастроекЭДОКлиентСервер.ИдентификаторТакскома());
	
	Форма.Элементы.ГруппаИдентификаторЭДО.Видимость =
		Форма.Элементы.ГруппаВидПодключения.Видимость
		И Форма.ВидПодключения = ВидыПодключения().Существующий;
	
	Форма.Элементы.ГруппаОператорЭДО.Доступность = Не Форма.Элементы.ГруппаИдентификаторЭДО.Видимость;
	Форма.Элементы.ДекорацияИдентификаторОтступ.Видимость = Не Форма.Элементы.ГруппаИдентификаторЭДО.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьВводаИнформацииПоОрганизации()
	
	ПоказыватьВыборОрганизации = Истина;
	ПоказыватьСведенияПоОрганизации = Не ПоказыватьВыборОрганизации Или Не ПроверитьДанныеОрганизации(ЭтотОбъект);
	
	Элементы.ГруппаОрганизация.Видимость = ПоказыватьВыборОрганизации;
	Элементы.ГруппаЗаполненияСведенийПоОрганизации.Видимость = ПоказыватьСведенияПоОрганизации;
	
	УстановитьВидимостьОГРНКПП(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЗарегистрироватьЭДОКартинкаИТекст(Форма)
	
	Форма.Элементы.ЗарегистрироватьЭДО.Отображение = ОтображениеКнопки.КартинкаИТекст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеЗарегистрироватьЭДОТекст(Форма)
	
	Форма.Элементы.ЗарегистрироватьЭДО.Отображение = ОтображениеКнопки.Текст;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьЭлементовФормы(Форма)
	
	Форма.Элементы.ГруппаСостояниеВыполнения.Видимость = Не Форма.РегистрацияВыполнена;
	Форма.Элементы.ГруппаШапкаИнформации.Видимость = Форма.РегистрацияВыполнена;
	
	Форма.Элементы.ЗарегистрироватьЭДО.Видимость =
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаВводДанных
		И Не Форма.РегистрацияВыполнена;
	Форма.Элементы.ЗарегистрироватьЭДО.КнопкаПоУмолчанию = Форма.Элементы.ЗарегистрироватьЭДО.Видимость;
	
	Форма.Элементы.Назад.Видимость =
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаРезультатПодключения
		И (Не Форма.ВыполняетсяРегистрация И Не Форма.РегистрацияВыполнена);
	
	Форма.Элементы.Закрыть.Видимость =
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаРезультатПодключения
		И Форма.РегистрацияВыполнена;
	Форма.Элементы.Закрыть.КнопкаПоУмолчанию = Форма.Элементы.Закрыть.Видимость;
	
	Форма.Элементы.ГруппаПодвалИнформации.Видимость =
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаРезультатПодключения;
	
	Форма.Элементы.ПовторитьДиагностику.Видимость =
		Форма.Элементы.Страницы.ТекущаяСтраница = Форма.Элементы.СтраницаДиагностика
		И (Форма.ВыполняетсяПроверкаКриптографии
			Или Форма.ПроверкаКриптографииПровалена
			Или Форма.ВыполняетсяПроверкаНаличияСертификатов
			Или Форма.ПроверкаНаличияСертификатовПровалена
			Или Форма.ТребуетсяУстановкаРасширенияКриптографии);
	Форма.Элементы.ПовторитьДиагностику.КнопкаПоУмолчанию = Форма.Элементы.ПовторитьДиагностику.Видимость;
	
	Форма.Элементы.ГруппаВыполнениеПроверкиНаличияРасширения.Видимость =
		Форма.ВыполняетсяПроверкаНаличияРасширенияКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаВыполнениеПроверкиНаличияРасширения, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиРасширенияНеУспешно.Видимость =
		Форма.ТребуетсяУстановкаРасширенияКриптографии
		И Не Форма.ВыполняетсяПроверкаНаличияРасширенияКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиРасширенияНеУспешно, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиРасширенияУспешно.Видимость =
		Не Форма.ТребуетсяУстановкаРасширенияКриптографии
		И Не Форма.ВыполняетсяПроверкаНаличияРасширенияКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиРасширенияУспешно, Форма.Элементы.СтраницаДиагностика);
	
	Форма.Элементы.ГруппаВыполнениеПроверкиКриптографии.Видимость =
		Форма.ВыполняетсяПроверкаКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаВыполнениеПроверкиКриптографии, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиКриптографииНеУспешно.Видимость =
		Форма.ПроверкаКриптографииПровалена
		И Не Форма.ВыполняетсяПроверкаКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиКриптографииНеУспешно, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиКриптографииУспешно.Видимость =
		Не Форма.ПроверкаКриптографииПровалена
		И Не Форма.ВыполняетсяПроверкаКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиКриптографииУспешно, Форма.Элементы.СтраницаДиагностика);
	
	Форма.Элементы.ГруппаВыполнениеПроверкиНаличияСертификатов.Видимость =
		Форма.ВыполняетсяПроверкаНаличияСертификатов
		Или Форма.ВыполняетсяПроверкаКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаВыполнениеПроверкиНаличияСертификатов, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиНаличияСертификатовНеУспешно.Видимость =
		(Форма.ПроверкаНаличияСертификатовПровалена
			Или Форма.ПроверкаКриптографииПровалена)
		И Не Форма.ВыполняетсяПроверкаНаличияСертификатов
		И Не Форма.ВыполняетсяПроверкаКриптографии;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиНаличияСертификатовНеУспешно, Форма.Элементы.СтраницаДиагностика);
	Форма.Элементы.ГруппаРезультатПроверкиНаличияСертификатовУспешно.Видимость =
		Не Форма.ПроверкаНаличияСертификатовПровалена
		И Не Форма.ВыполняетсяПроверкаНаличияСертификатов
		И Не Форма.ВыполняетсяПроверкаКриптографии
		И Не Форма.ПроверкаКриптографииПровалена;
	УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Форма.Элементы.ГруппаРезультатПроверкиНаличияСертификатовУспешно, Форма.Элементы.СтраницаДиагностика);
	
	Если ЗначениеЗаполнено(Форма.ИдентификаторОператораЭДО) Тогда
		Форма.ПредставлениеОператораЭДО =
			Форма.Элементы.ОператорЭДО.СписокВыбора.НайтиПоЗначению(Форма.ИдентификаторОператораЭДО).Представление;
	КонецЕсли;
	
	Форма.Элементы.ГруппаДоверенность.Видимость = Форма.ТребуетсяДоверенность;
	Форма.Элементы.Доверенность.АвтоОтметкаНезаполненного = Форма.ДоверенностьОбязательна;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекущийЭлементПоВидимостиГрупп(Форма, Группа, ТекущаяСтраница)
	
	Если Форма.Элементы.Страницы.ТекущаяСтраница = ТекущаяСтраница
		И Группа.Видимость Тогда
		Форма.ТекущийЭлемент = Группа;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопкиЗарегистрироватьЭДО(Доступна = Истина)
	
	Элементы.ЗарегистрироватьЭДО.Доступность = Доступна;
	Если Доступна Тогда
		УстановитьОтображениеЗарегистрироватьЭДОТекст(ЭтотОбъект);
	Иначе
		УстановитьОтображениеЗарегистрироватьЭДОКартинкаИТекст(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		АдресОрганизации = Результат.Представление;
		ЗначениеПолейАдреса = Результат.Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьОжиданиеВыполненияДлительнойОперации(Оповещение, ДлительнаяОперация)
	
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция СведенияПоРуководителю(Знач Сертификат, Знач Организация)
	
	Результат = "";
	
	СвойстваСубъекта = КриптографияБЭД.СвойстваСубъектаСертификатаПоСсылке(Сертификат);
	СвойстваИздателя = КриптографияБЭД.СвойстваИздателяСертификатаПоСсылке(Сертификат);
	ЭтоСертификатФизическогоЛица = КриптографияБЭД.ЭтоВидСертификатаФизическогоЛица(СвойстваСубъекта, СвойстваИздателя);
	
	Если ЭтоСертификатФизическогоЛица Тогда
		ДанныеОрганизации = ИнтеграцияЭДО.РегистрационныеДанныеОрганизации(Организация);
		Результат = СтрШаблон("%1 %2",
			ДанныеОрганизации.Фамилия,
			ДанныеОрганизации.Имя);
	Иначе
		ЗначенияРеквизитов = ИнтеграцияБСПБЭД.ЗначенияРеквизитовСертификатаПоФамилииИмениДолжности(Сертификат);
		Если ЗначениеЗаполнено(ЗначенияРеквизитов.Должность) Тогда
			Результат = СтрШаблон("%1 %2, %3",
				ЗначенияРеквизитов.Фамилия,
				ЗначенияРеквизитов.Имя,
				ЗначенияРеквизитов.Должность);
		Иначе
			Результат = СтрШаблон("%1 %2",
				ЗначенияРеквизитов.Фамилия,
				ЗначенияРеквизитов.Имя);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция СостоянияПодключения()
	
	СостоянияПодключения = Новый Структура();
	СостоянияПодключения.Вставить("Выполнено", "Выполнено");
	СостоянияПодключения.Вставить("Завершить", "Завершить");
	СостоянияПодключения.Вставить("Ошибка", "Ошибка");
	
	Возврат СостоянияПодключения;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ВидыПодключения()
	
	ВидыПодключения = Новый Структура();
	ВидыПодключения.Вставить("Новый", "Новый");
	ВидыПодключения.Вставить("Существующий", "Существующий");
	
	Возврат ВидыПодключения;
	
КонецФункции

#КонецОбласти
