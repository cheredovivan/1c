#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";
	
	Ограничение.ТекстДляВнешнихПользователей =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы <> "ФормаЗаписи"
		Или Не ИнтеграцияЭДО.ИспользоватьПрямойОбмен() Тогда
		Возврат;
	КонецЕсли;
	
	КлючЗаписи = Неопределено;
	Если Не Параметры.Свойство("Ключ", КлючЗаписи)
		ИЛИ Не ЗначениеЗаполнено(КлючЗаписи.ИдентификаторЭДО) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО = &ИдентификаторЭДО
		|	И УчетныеЗаписиЭДО.СпособОбменаЭД В (&СпособыОбмена)";
		
	Запрос.УстановитьПараметр("ИдентификаторЭДО", КлючЗаписи.ИдентификаторЭДО);
	Запрос.УстановитьПараметр("СпособыОбмена", СинхронизацияЭДО.СпособыПрямогоОбмена());
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ВыбраннаяФорма = "УчетнаяЗаписьПрямогоОбмена";
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//	Возвращает признак отсутствия учетных записей в программе
//
// Возвращаемое значение:
//  Булево
Функция УчетныеЗаписиОтсутствуют() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	УчетныеЗаписиЭДО.ИдентификаторЭДО
	|ИЗ
	|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
	|ГДЕ
	|	ВЫБОР
	|		КОГДА &ИспользоватьПрямойОбмен
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ
	|		НЕ УчетныеЗаписиЭДО.СпособОбменаЭД В (&СпособыПрямогоОбмена)
	|	КОНЕЦ";
	Запрос.УстановитьПараметр("ИспользоватьПрямойОбмен", НастройкиЭДО.ИспользуетсяПрямойОбменЭлектроннымиДокументами());
	Запрос.УстановитьПараметр("СпособыПрямогоОбмена", СинхронизацияЭДО.СпособыПрямогоОбмена());
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

// Параметры:
//  ИдентификаторыЭДО - Массив из см. РегистрСведений.УчетныеЗаписиЭДО.ИдентификаторЭДО
// 
// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//  * ИдентификаторЭДО - см. РегистрСведений.УчетныеЗаписиЭДО.ИдентификаторЭДО
//  * ЭтоОблачныйЭДО - см. РегистрСведений.УчетныеЗаписиЭДО.ЭтоОблачныйЭДО
Функция ВыборкаПризнакаОбменаЧерезОблачныйЭДО(ИдентификаторыЭДО) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО,
		|	УчетныеЗаписиЭДО.ЭтоОблачныйЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО В (&ИдентификаторыЭДО)";
	Запрос.УстановитьПараметр("ИдентификаторыЭДО", ИдентификаторыЭДО);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выбрать();
	
КонецФункции

// Параметры:
//  ЭтоОблачныйЭДО - Булево
// 
// Возвращаемое значение:
//  Булево - Есть учетные записи
Функция ЕстьУчетныеЗаписи(ЭтоОблачныйЭДО)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	УчетныеЗаписиЭДО.ИдентификаторЭДО КАК ИдентификаторЭДО
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ЭтоОблачныйЭДО = &ЭтоОблачныйЭДО";
	Запрос.УстановитьПараметр("ЭтоОблачныйЭДО", ЭтоОблачныйЭДО);
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатЗапроса.Пустой();
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиЧерезОблачныйЭДО() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.ЭтоОблачныйЭДО";
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатЗапроса.Пустой();
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиЧерезСервис1СЭДО() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.УчетныеЗаписиЭДО КАК УчетныеЗаписиЭДО
		|ГДЕ
		|	УчетныеЗаписиЭДО.СпособОбменаЭД = ЗНАЧЕНИЕ(Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО)
		|	И НЕ УчетныеЗаписиЭДО.ЭтоОблачныйЭДО";
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	Возврат Не РезультатЗапроса.Пустой();
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиОблачногоЭДО() Экспорт
	
	Возврат ЕстьУчетныеЗаписи(Истина);
	
КонецФункции

// Возвращаемое значение:
//  Булево
Функция ЕстьУчетныеЗаписиЛокальногоЭДО() Экспорт
	
	Возврат ЕстьУчетныеЗаписи(Ложь);
	
КонецФункции

#КонецОбласти

#КонецЕсли
