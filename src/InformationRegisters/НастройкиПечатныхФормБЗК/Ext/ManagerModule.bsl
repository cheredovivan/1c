#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция Настройки() Экспорт
	
	НастройкиФорм = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	НастройкиПечатныхФорм.ИдентификаторПечатнойФормы КАК ИдентификаторПечатнойФормы,
		|	НастройкиПечатныхФорм.СодержимоеДокумента КАК СодержимоеДокумента,
		|	НастройкиПечатныхФорм.БлокировкаСУсловием КАК БлокировкаСУсловием,
		|	НастройкиПечатныхФорм.НаСписокСотрудников КАК НаСписокСотрудников,
		|	НастройкиПечатныхФорм.ВидДокумента КАК ВидДокумента,
		|	НастройкиПечатныхФорм.ВариантПодписания КАК ВариантПодписания,
		|	НастройкиПечатныхФорм.Наименование КАК Наименование
		|ИЗ
		|	РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФорм";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	Пока Выборка.Следующий() Цикл
		НастрокиФормы = Новый Структура(
			"СодержимоеДокумента,
			|БлокировкаСУсловием,
			|НаСписокСотрудников,
			|ВидДокумента,
			|ВариантПодписания,
			|Наименование");
		ЗаполнитьЗначенияСвойств(НастрокиФормы, Выборка);
		НастройкиФорм.Вставить(Выборка.ИдентификаторПечатнойФормы, НастрокиФормы);
	КонецЦикла;
	
	Возврат НастройкиФорм;
	
КонецФункции

Процедура ЗаполнитьНастройки() Экспорт
	
	ОбновитьНастройкиПечатныхФорм();
	
КонецПроцедуры

Процедура ЗаполнитьВариантПодписания() Экспорт
	
	ОбновитьНастройкиПечатныхФорм();
	ЗаполнитьВариантПодписанияПользовательскихПечатныхФорм();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьНастройкиПечатныхФорм(ОбновитьЗаписи = Ложь)
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.НастройкиПечатныхФормБЗК");
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Прочитать();
		
		ТаблицаНабора = НаборЗаписей.Выгрузить();
		
		ЗаписатьНабор = Ложь;
		НастройкиПечатныхФормПоУмолчанию = ЭДОБЗК.ОписанияНастроекПечатныхФормПоУмолчанию();
		Для Каждого НастройкиПечатнойФормы Из НастройкиПечатныхФормПоУмолчанию Цикл
			
			ЗаписьИдентификатора = ТаблицаНабора.Найти(НастройкиПечатнойФормы.ИдентификаторПечатнойФормы, "ИдентификаторПечатнойФормы");
			Если ЗаписьИдентификатора = Неопределено Тогда
				ЗаписатьНабор = Истина;
				ЗаписьИдентификатора = ТаблицаНабора.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьИдентификатора, НастройкиПечатнойФормы);
			ИначеЕсли ОбновитьЗаписи Тогда
				ЗаписатьНабор = Истина;
				ЗаполнитьЗначенияСвойств(ЗаписьИдентификатора, НастройкиПечатнойФормы);
			Иначе
				Для Каждого Колонка Из НастройкиПечатныхФормПоУмолчанию.Колонки Цикл
					Если Колонка.Имя = "ВариантПодписания"
						И ЗаписьИдентификатора.ВариантПодписания <> НастройкиПечатнойФормы.ВариантПодписания Тогда
						
						ЗаписатьНабор = Истина;
						ЗаписьИдентификатора.ВариантПодписания = НастройкиПечатнойФормы.ВариантПодписания;
					ИначеЕсли Колонка.Имя = "Наименование"
						И Не ЗаписьИдентификатора.ПользовательскоеНаименование Тогда
						
						ЗаписатьНабор = Истина;
						ЗаписьИдентификатора.Наименование = НастройкиПечатнойФормы.Наименование;
					ИначеЕсли Не ЗначениеЗаполнено(ЗаписьИдентификатора[Колонка.Имя])
							И ЗначениеЗаполнено(НастройкиПечатнойФормы[Колонка.Имя]) Тогда
						
						ЗаписатьНабор = Истина;
						ЗаписьИдентификатора[Колонка.Имя] = НастройкиПечатнойФормы[Колонка.Имя];
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если Не ЗаписьИдентификатора.Предопределенный Тогда
				ЗаписатьНабор = Истина;
				ЗаписьИдентификатора.Предопределенный = Истина;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(ЗаписьИдентификатора.ВидДокумента) Тогда
				ЗаписатьНабор = Истина;
				ЗаписьИдентификатора.ВидДокумента =
					Справочники.ВидыДокументовЭДОБЗК.ВидПоИдентификаторуПечатнойФормы(НастройкиПечатнойФормы.ИдентификаторПечатнойФормы);
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЗаписатьНабор Тогда
			НаборЗаписей.Загрузить(ТаблицаНабора);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			ЭДОБЗК.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Ошибка заполнения настроек печатных форм';
					|en = 'An error occurred when filling print form settings'", ОбщегоНазначения.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.НастройкиПечатныхФормБЗК,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаполнитьВариантПодписанияПользовательскихПечатныхФорм()
	
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	НастройкиПечатныхФормБЗК.ИдентификаторПечатнойФормы КАК ИдентификаторПечатнойФормы
			|ИЗ
			|	РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФормБЗК
			|ГДЕ
			|	НастройкиПечатныхФормБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.ПустаяСсылка)";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить(Выборка.ИдентификаторПечатнойФормы);
			НаборЗаписей.Прочитать();
			НаборЗаписей[0].ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.Требуется;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(
			ЭДОБЗК.ИмяСобытияЖурналаРегистрации(
				НСтр("ru = 'Ошибка заполнения настроек печатных форм';
					|en = 'An error occurred when filling print form settings'", ОбщегоНазначения.КодОсновногоЯзыка())),
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.РегистрыСведений.НастройкиПечатныхФормБЗК,
			,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
КонецПроцедуры

Процедура УстановитьВариантПодписанияПечатнойФормы(ИдентификаторПечатнойФормы, ВариантПодписания) Экспорт
	
	НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить(ИдентификаторПечатнойФормы);
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		НаборЗаписей[0].ВариантПодписания = ВариантПодписания;
		НаборЗаписей.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьНедостающиеВидыДокументов(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	НастройкиПечатныхФормБЗК.Наименование КАК Наименование,
		|	НастройкиПечатныхФормБЗК.УдалитьКодДокументаКадровогоМероприятия КАК УдалитьКодДокументаКадровогоМероприятия,
		|	НастройкиПечатныхФормБЗК.ИдентификаторПечатнойФормы КАК ИдентификаторПечатнойФормы
		|ИЗ
		|	РегистрСведений.НастройкиПечатныхФормБЗК КАК НастройкиПечатныхФормБЗК
		|ГДЕ
		|	НастройкиПечатныхФормБЗК.ВидДокумента = ЗНАЧЕНИЕ(Справочник.ВидыДокументовЭДОБЗК.ПустаяСсылка)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Справочники.ШаблоныДокументов.ЗаполнитьВидыДокументовПользовательскихШаблонов(ПараметрыОбновления);
		Возврат;
	КонецЕсли;
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	Пока Выборка.Следующий() Цикл
		
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.НастройкиПечатныхФормБЗК", "ИдентификаторПечатнойФормы", Выборка.ИдентификаторПечатнойФормы) Тогда
			ОбработкаВыполнена = Ложь;
			Продолжить;
		КонецЕсли;
		
		Если Выборка.УдалитьКодДокументаКадровогоМероприятия = ЭДОБЗКПовтИсп.КодКадровогоДокументаПоУмолчанию() Тогда
			КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("ПрочиеКЭДО");
		ИначеЕсли Не ЗначениеЗаполнено(Выборка.УдалитьКодДокументаКадровогоМероприятия) Тогда
			КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ПредопределенныйВидДокумента("Прочие");
		Иначе
			КлассификаторСсылка = Справочники.ВидыДокументовЭДОБЗК.ВидПоКодуКадровогоДокумент(Выборка.УдалитьКодДокументаКадровогоМероприятия);
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить(Выборка.ИдентификаторПечатнойФормы);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Количество() > 0 Тогда
			НаборЗаписей[0].ВидДокумента = КлассификаторСсылка;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьИдентификаторПечатнойФормыСогласияНаПрисоединениеКЭДОБЗК(ПараметрыОбновления = Неопределено) Экспорт
	
	Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления,
		"РегистрСведений.НастройкиПечатныхФормБЗК", "ИдентификаторПечатнойФормы", "ПФ_MXL_СогласиеНаПрисоединениеККЭДО") Тогда
		
		Возврат;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить("ПФ_MXL_СогласиеНаПрисоединениеККЭДО");
	НаборЗаписей.Прочитать();
	Если НаборЗаписей.Количество() > 0 Тогда
		
		НовыйНаборЗаписей = РегистрыСведений.НастройкиПечатныхФормБЗК.СоздатьНаборЗаписей();
		НовыйНаборЗаписей.Отбор.ИдентификаторПечатнойФормы.Установить("ПФ_MXL_СогласиеНаПрисоединениеКЭДОБЗК");
		НоваяЗапись = НовыйНаборЗаписей.Добавить();
		НоваяЗапись.ИдентификаторПечатнойФормы = "ПФ_MXL_СогласиеНаПрисоединениеКЭДОБЗК";
		ЗаполнитьЗначенияСвойств(НоваяЗапись, НаборЗаписей[0], , "ИдентификаторПечатнойФормы");
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НовыйНаборЗаписей);
		НаборЗаписей.Очистить();
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
