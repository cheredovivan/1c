// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// См. БизнесСеть.АктуализироватьКэшДанныхКонтрагентов
Процедура РезультатАктуализацииКешаКонтрагентов() Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = НовыйЗапросКонтрагентыДляКэширования();
	ТаблицаПоискаКонтрагентов = БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов();
	Контрагент = Неопределено;
	
	Пока Истина Цикл
		
		Запрос.УстановитьПараметр("Контрагент", Контрагент);
		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Прервать;
		КонецЕсли;
		
		ВыборкаЗапроса = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаЗапроса.Следующий() Цикл
			
			//@skip-check property-return-type - Особенность строгой типизации
			Контрагент = ВыборкаЗапроса.Контрагент; // ОпределяемыйТип.ОрганизацияКонтрагентEDI
			//@skip-check property-return-type - Особенность строгой типизации
			ИНН = ВыборкаЗапроса.ИНН; // Строка
			//@skip-check property-return-type - Особенность строгой типизации
			КПП = ВыборкаЗапроса.КПП; // Строка
			
			// Нормализуем значение, полученные из прикладных данных
			ИНН = СокрЛП(ИНН);
			КПП = СокрЛП(КПП);
			
			Если Не БизнесСетьСлужебный.ЭтоИННКПП(ИНН, КПП) Тогда
				Продолжить;
			КонецЕсли;
			
			НоваяСтрока = ТаблицаПоискаКонтрагентов.Добавить();
			НоваяСтрока.Контрагент = Контрагент;
			НоваяСтрока.ИНН = ИНН;
			НоваяСтрока.КПП = КПП;
			
		КонецЦикла;
		
	КонецЦикла;
	
	БизнесСетьСлужебный.ВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов);
	
	ЗаписатьКонтрагентов(ТаблицаПоискаКонтрагентов);
	
КонецПроцедуры

// Возвращаемое значение:
//  Массив Из См. ОпределяемыйТип.ОрганизацияКонтрагентEDI
Функция КонтрагентыEDIПоОрганизации() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КонтрагентыБизнесСеть.Контрагент КАК Контрагент
	|ИЗ
	|	РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть";
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контрагент");
	
КонецФункции

Процедура ОбновитьКэшДанныхКонтрагентаБизнесСети(Знач ДанныеКонтрагентов, Отказ, ТекстОшибки = "") Экспорт
	
	Если ДанныеКонтрагентов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КонтрагентыБизнесСеть");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ДанныеКонтрагентов;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Контрагент", "Контрагент");
		Блокировка.Заблокировать();
		
		Для Каждого ДанныеКонтрагента Из ДанныеКонтрагентов Цикл
			Если Не ЗначениеЗаполнено(ДанныеКонтрагента.Контрагент) Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Контрагент.Установить(ДанныеКонтрагента.Контрагент);
			НоваяЗапись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяЗапись, ДанныеКонтрагента);
			НаборЗаписей.Записать();
			
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		
		Отказ = Истина;
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Метод-конструктор результата актуализации кэша
// 
// Возвращаемое значение:
//  Структура:
// * Отказ             - Булево - Признак ошибки при выполнении задания
// * КоличествоЗаписей - Число  - Количество обновленных записей кэша
// * ТекстОшибки       - Число  - Текст ошибки при выполнении задания
Функция НовыйРезультатАктуализацииКешаКонтрагентов() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("Отказ"             , Ложь);
	Результат.Вставить("КоличествоЗаписей" , 0);
	Результат.Вставить("ТекстОшибки"       , 0);
	
	Возврат Результат;
	
КонецФункции

// Новый запрос контрагенты для кэширования.
// 
// Возвращаемое значение:
//  Запрос - Запрос контрагентов, требующих актуализации
Функция НовыйЗапросКонтрагентыДляКэширования()
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	Контрагенты.Ссылка КАК Контрагент,
		|	&ПутьКонтрагентИНН КАК ИНН,
		|	&ПутьКонтрагентКПП КАК КПП
		|ИЗ
		|	&ТаблицаКонтрагенты КАК Контрагенты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтрагентыБизнесСеть КАК КонтрагентыБизнесСеть
		|		ПО Контрагенты.Ссылка = КонтрагентыБизнесСеть.Контрагент
		|ГДЕ
		|	КонтрагентыБизнесСеть.Идентификатор ЕСТЬ NULL
		|	И Контрагенты.Ссылка > &Контрагент
		|
		|УПОРЯДОЧИТЬ ПО
		|	Контрагент";
	
	ИмяСправочникаКонтрагенты = ОбщегоНазначенияБЭД.ИмяПрикладногоСправочника("Контрагенты");
	РеквизитИНН = ОбщегоНазначенияБЭДПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННКонтрагента");
	РеквизитКПП = ОбщегоНазначенияБЭДПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППКонтрагента");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаКонтрагенты", "Справочник." + ИмяСправочникаКонтрагенты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПутьКонтрагентИНН", "Контрагенты." + РеквизитИНН);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПутьКонтрагентКПП", "Контрагенты." + РеквизитКПП);

	Запрос.Текст = ТекстЗапроса;
	
	Возврат Запрос;
	
КонецФункции

// Записать контрагентов.
// 
// Параметры:
//  ТаблицаПоискаКонтрагентов - См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
Процедура ЗаписатьКонтрагентов(Знач ТаблицаПоискаКонтрагентов)
	
	Если ТаблицаПоискаКонтрагентов.Количество() = 0 Тогда
		Возврат; 
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Найден", Истина);
	
	НайденныеКонтрагенты = ТаблицаПоискаКонтрагентов.Скопировать(Отбор, "Контрагент, Идентификатор");
	
	Порции = БизнесСетьСлужебный.РазбитьТаблицуНаПорции(
		НайденныеКонтрагенты, 1000); // Массив Из См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
	
	Для Каждого Порция Из Порции Цикл
		НачатьТранзакцию();
		Попытка
			УстановитьБлокировку(Порция);
			
			Для Каждого Строка Из Порция Цикл
				НаборЗаписей = СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Контрагент.Установить(Строка.Контрагент);
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Контрагент = Строка.Контрагент;
				НоваяЗапись.Идентификатор = Строка.Идентификатор;
				
				НаборЗаписей.Записать(Истина);
			КонецЦикла;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки
	КонецЦикла;
	
КонецПроцедуры

// Устанавливает исключительную блокировку таблицы для записи
// 
// Параметры:
//  ТаблицаПоискаКонтрагентов - См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
Процедура УстановитьБлокировку(Знач ТаблицаПоискаКонтрагентов)
	
	Контекст = "РегистрыСведений.КонтрагентыБизнесСеть.УстановитьБлокировку";
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ТранзакцияАктивна(),
		НСтр(
			"ru = 'Вызов метода вне тракнзации не допускается';
			|en = 'Cannot call the method outside the transaction'",
			ОбщегоНазначения.КодОсновногоЯзыка()),
		Контекст);
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить(Метаданные.РегистрыСведений.КонтрагентыБизнесСеть.ПолноеИмя());
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = ТаблицаПоискаКонтрагентов;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Контрагент", "Контрагент");

	//@skip-check lock-out-of-try
	Блокировка.Заблокировать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
