#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Запись содержит реквизиты деления этапов.
// 
// Параметры:
//  Запись - РегистрСведенийЗапись.ТехнологическиеПроцессыЭтаповПроизводства
// 
// Возвращаемое значение:
//  Булево
Функция ЗаписьСодержитОграниченияТехпроцесса(Запись) Экспорт
	
	Возврат ЗначениеЗаполнено(Запись.ИдентификаторПервойОперации)
		ИЛИ ЗначениеЗаполнено(Запись.ИдентификаторПоследнейОперации)
		ИЛИ ЗначениеЗаполнено(Запись.ОтмененоПоследняяОперация);
	
КонецФункции

// Запись содержит параметры технологического процесса.
// 
// Параметры:
//  Запись - РегистрСведенийЗапись.ТехнологическиеПроцессыЭтаповПроизводства
// 
// Возвращаемое значение:
//  Булево
Функция ЗаписьСодержитПараметрыТехнологическогоПроцесса(Запись) Экспорт
	
	Возврат ЗначениеЗаполнено(Запись.ТипТехнологическогоПроцесса)
		ИЛИ ЗначениеЗаполнено(Запись.ТехнологическийПроцесс)
		ИЛИ ЗначениеЗаполнено(Запись.КоэффициентТехнологическогоПроцесса);
	
КонецФункции

// Выполняет запись параметров техпроцесса этапа
// 
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2
//  ПараметрыТехпроцесса - см. РедакторПроизводственногоПроцессаКлиентСервер.ПараметрыТехпроцессаКонструктор
//  СохранитьОграниченияТехпроцесса - Булево - Определяет необходимость сохранения ограничений, установленных делением партии
Процедура ЗаписатьПараметрыТехпроцесса(Этап, ПараметрыТехпроцесса, СохранитьОграниченияТехпроцесса = Истина) Экспорт
	
	НаличиеЗаписиОбязательно = НЕ ПараметрыТехпроцесса.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Стандартный
		ИЛИ ТипЗнч(ПараметрыТехпроцесса.ТехнологическийПроцесс) = Тип("СправочникСсылка.МаршрутныеКарты")
				И ЗначениеЗаполнено(ПараметрыТехпроцесса.ТехнологическийПроцесс);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.Этап.Установить(Этап);
	
	Если СохранитьОграниченияТехпроцесса Тогда
		Набор.Прочитать();
	КонецЕсли;
	
	Если НаличиеЗаписиОбязательно
		И Набор.Количество() = 0 Тогда
		Набор.Добавить();
	ИначеЕсли НЕ НаличиеЗаписиОбязательно
		И Набор.Количество() > 0
		И НЕ ЗаписьСодержитОграниченияТехпроцесса(Набор[0]) Тогда
		Набор.Очистить();
	КонецЕсли;
	
	Если Набор.Количество() > 0 Тогда
		Набор[0].Этап = Этап;
		ЗаполнитьЗначенияСвойств(Набор[0], ПараметрыТехпроцесса);
	КонецЕсли;
	
	Набор.Записать();
	
КонецПроцедуры

// Конструктор структуры ограничений техпроцесса.
// 
// Возвращаемое значение:
//  Структура - из:
// * ИдентификаторПервойОперации - Число - ограничение слева
// * ИдентификаторПоследнейОперации - Число - ограничение справа
// * ОтмененоПоследняяОперация - Число - сокращение последней операции
Функция ОграниченияТехпроцессаКонструктор() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ИдентификаторПервойОперации", 0);
	Результат.Вставить("ИдентификаторПоследнейОперации", 0);
	Результат.Вставить("ОтмененоПоследняяОперация", 0);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.23.7";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("833b3f93-3d12-5ef3-805a-fa4f9f10a11a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Заполняет реквизиты деления партии и технологические процессы этапов с маршрутными картами.';
									|en = 'Fills the attributes of lot division and the technological processes of the stages with route sheets.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.РеквизитыДеленияЭтаповПроизводства.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ОчередьПроизводственныхОпераций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Этап.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Этап");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Этап КАК Этап
	|ИЗ
	|(
	|	ВЫБРАТЬ
	|		РеквизитыДеления.Этап КАК Этап
	|	ИЗ
	|		РегистрСведений.РеквизитыДеленияЭтаповПроизводства КАК РеквизитыДеления
	|	ГДЕ
	|		РеквизитыДеления.Этап.Проведен
	|		И НЕ ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцессы
	|			ГДЕ
	|				РеквизитыДеления.Этап = Техпроцессы.Этап
	|			)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Таблица.Ссылка КАК Этап
	|	ИЗ
	|		Документ.ЭтапПроизводства2_2 КАК Таблица
	|	ГДЕ
	|		Таблица.Проведен
	|		И Таблица.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат)
	|		И НЕ Таблица.УдалитьМаршрутнаяКарта = ЗНАЧЕНИЕ(Справочник.МаршрутныеКарты.ПустаяСсылка)
	|		И (НЕ Таблица.УдалитьМаршрутнаяКарта = Таблица.Этап.МаршрутнаяКарта
	|			ИЛИ НЕ Таблица.УдалитьКоэффициентМаршрутнойКарты = Таблица.Этап.КоэффициентМаршрутнойКарты
	|						* Таблица.КоличествоУпаковокПлан
	|						* ЕСТЬNULL(&КоэффициентУпаковки, 1) / 
	|						ВЫБОР
	|							КОГДА Таблица.КоличествоНаЕдиницуПартииВыпуска > 0
	|								ТОГДА Таблица.КоличествоНаЕдиницуПартииВыпуска
	|							ИНАЧЕ 1
	|						КОНЕЦ)
	|		И НЕ ИСТИНА В (
	|			ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцессы
	|			ГДЕ
	|				Таблица.Ссылка = Техпроцессы.Этап
	|			)
	|
	|) КАК ВложенныйЗапрос
	|";
	
	ТекстЗапросаКоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"Таблица.УпаковкаПлан",
		"Таблица.ПартияПроизводства.ОсновноеИзделиеНоменклатура");
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&КоэффициентУпаковки",
		ТекстЗапросаКоэффициентУпаковки);
		
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры,
		Запрос.Выполнить().Выгрузить(),
		ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Этапы = Новый Массив;
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		Этапы.Добавить(Выборка.Этап);
	КонецЦикла;
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	РеквизитыДокумента.Ссылка                                 КАК Этап,
	|	РеквизитыДокумента.УдалитьМаршрутнаяКарта                 КАК МаршрутнаяКартаДокумента,
	|	РеквизитыДокумента.УдалитьКоэффициентМаршрутнойКарты      КАК КоэффициентМаршрутнойКартыДокумента,
	|	РеквизитыДеления.УдалитьИдентификаторПервойОперации       КАК ИдентификаторПервойОперации,
	|	РеквизитыДеления.УдалитьИдентификаторПоследнейОперации    КАК ИдентификаторПоследнейОперации,
	|	РеквизитыДеления.УдалитьОтмененоПоследняяОперация         КАК ОтмененоПоследняяОперация,
	|	РеквизитыДокумента.Этап.МаршрутнаяКарта                   КАК МаршрутнаяКартаЭтапаСпецификации,
	|	РеквизитыДокумента.Этап.КоэффициентМаршрутнойКарты
	|		* РеквизитыДокумента.КоличествоУпаковокПлан
	|		* ЕСТЬNULL(&КоэффициентУпаковки, 1) / 
	|			ВЫБОР
	|				КОГДА РеквизитыДокумента.КоличествоНаЕдиницуПартииВыпуска > 0
	|					ТОГДА РеквизитыДокумента.КоличествоНаЕдиницуПартииВыпуска
	|				ИНАЧЕ 1
	|			КОНЕЦ                                             КАК КоэффициентМаршрутнойКартыЭтапаСпецификации,
	|	&ПараметрыТехпроцесса
	|ИЗ
	|	Документ.ЭтапПроизводства2_2 КАК РеквизитыДокумента
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеквизитыДеленияЭтаповПроизводства КАК РеквизитыДеления
	|		ПО РеквизитыДокумента.Ссылка = РеквизитыДеления.Этап
	|
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
	|		ПО ЛОЖЬ
	|
	|ГДЕ
	|	РеквизитыДокумента.Ссылка В (&Этапы)
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ПараметрыТехпроцесса",
		РедакторПроизводственногоПроцесса.ТекстЗапросаПараметрыТехпроцессаЭтапа());
		
	ТекстЗапросаКоэффициентУпаковки = Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
		"РеквизитыДокумента.УпаковкаПлан",
		"РеквизитыДокумента.ПартияПроизводства.ОсновноеИзделиеНоменклатура");
		
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&КоэффициентУпаковки",
		ТекстЗапросаКоэффициентУпаковки);
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Этапы", Этапы);
	
	ЗначенияРеквизитов = Запрос.Выполнить().Выгрузить();
	ЗначенияРеквизитов.Индексы.Добавить("Этап");
	
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
			ЭлементБлокировки.УстановитьЗначение("Этап", Выборка.Этап);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Этап.Установить(Выборка.Этап);
			
			НаборИзменен = Ложь;
			
			СтрокаЗначенияРеквизитов = ЗначенияРеквизитов.Найти(Выборка.Этап, "Этап");
			
			Если ЗаписьСодержитОграниченияТехпроцесса(СтрокаЗначенияРеквизитов) Тогда
			
				ЗаписьРегистра = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(ЗаписьРегистра, СтрокаЗначенияРеквизитов);
				
				НаборИзменен = Истина;
			
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаЗначенияРеквизитов.МаршрутнаяКартаДокумента) Тогда
				
				ЗаписьРегистра = ?(НаборЗаписей.Количество() > 0, НаборЗаписей[0], НаборЗаписей.Добавить());
				ЗаписьРегистра.Этап = Выборка.Этап;
				ЗаписьРегистра.ТехнологическийПроцесс = СтрокаЗначенияРеквизитов.МаршрутнаяКартаДокумента;
				ЗаписьРегистра.КоэффициентТехнологическогоПроцесса = СтрокаЗначенияРеквизитов.КоэффициентМаршрутнойКартыДокумента;
				ЗаписьРегистра.ТипТехнологическогоПроцесса = ?(
					СтрокаЗначенияРеквизитов.МаршрутнаяКартаДокумента = СтрокаЗначенияРеквизитов.МаршрутнаяКартаЭтапаСпецификации
						И СтрокаЗначенияРеквизитов.КоэффициентМаршрутнойКартыДокумента = СтрокаЗначенияРеквизитов.КоэффициентМаршрутнойКартыЭтапаСпецификации,
					Перечисления.ТипыТехнологическихПроцессовЭтапа.Стандартный,
					Перечисления.ТипыТехнологическихПроцессовЭтапа.Альтернативный);
				
				НаборИзменен = Истина;
				
			КонецЕсли;
			
			Если НаборИзменен Тогда
				ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			Иначе
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = 
				СтрШаблон(
					НСтр("ru = 'Не удалось записать данные в регистр ""Технологические процессы этапов производства"" по этапу ""%1"", по причине: %2';
						|en = 'Cannot save data to the ""Technological processes of production stages"" register for the ""%1"" stage due to: %2'"),
					Выборка.Этап,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций, 
				, 
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли