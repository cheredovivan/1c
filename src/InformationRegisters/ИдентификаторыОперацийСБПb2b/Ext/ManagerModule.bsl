///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Производит поиск идентификатора заказа на оплату или возврата,
// если идентификатор еще не был создан, создает новый.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//  ПараметрыНастройкиПодключения - Структура - параметры выполнения операции
//   см. СистемаБыстрыхПлатежейСлужебный.ПараметрыНастройкиПодключения;
//  ИдентификаторОплаты - Строка - идентификатор оплаты в Системе быстрых платежей. Передается
//    если известен на момент проведения операции;
//  КонтролироватьСтатусОперации - Булево - признак контроля потребности в генерации нового
//    идентификатора оплаты;
//  ОтложенноеПолучениеСтатуса - Булево - признак загрузки статуса оплаты регламентным заданием.
//
// Возвращаемое значение:
//  Строка - идентификатор оплаты (внешний идентификатор 1С по отношению к Системе быстрых платежей).
//
Функция НовыйИдентификаторОперации(
		ДокументОперации,
		ПараметрыНастройкиПодключения,
		ИдентификаторОплаты = "",
		КонтролироватьСтатусОперации = Истина,
		ОтложенноеПолучениеСтатуса = Ложь) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийСБПb2b");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		ТребуетсяГенерация = НастройкиПлатежныхСистемСлужебный.ТребуетсяГенерацияНовогоИдентификатора(
			Запись.Идентификатор,
			Запись.СтатусОперации);
		
		Если Не КонтролироватьСтатусОперации Или ТребуетсяГенерация Тогда
			Запись.Идентификатор = Новый УникальныйИдентификатор;
			Запись.ДокументОперации = ДокументОперации;
			Запись.ИдентификаторМерчанта = ПараметрыНастройкиПодключения.ИдентификаторМерчанта;
			Запись.НастройкаПодключения = ПараметрыНастройкиПодключения.НастройкаПодключения;
			Запись.ИдентификаторУчастника = ПараметрыНастройкиПодключения.ИдентификаторУчастника;
			Запись.ИдентификаторОплаты = ИдентификаторОплаты;
			Запись.ИдентификаторОперации = "";
			Запись.СтатусОперации = "";
			Запись.ПлатежнаяСсылка = "";
			Запись.ОтложенноеПолучениеСтатуса = Ложь;
			Запись.КоличествоПопыток = 0;
			Запись.ДатаОперации = Неопределено;
			Запись.СуммаОперации = 0;
			Запись.ПериодИспользования = Неопределено;
			Запись.ДатаЗапросаСтатуса = Неопределено;
			Запись.ОтложенноеПолучениеСтатуса = ОтложенноеПолучениеСтатуса;
			Запись.Заполнить(Неопределено);
			Запись.Записать();
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При формировании идентификатора оплаты возникли ошибки:
				|%1';
				|en = 'Errors occurred when generating the payment ID:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СистемаБыстрыхПлатежейСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
	Возврат Запись.Идентификатор;
	
КонецФункции

// Получает данные для определения статуса выполнения операции в Системе быстрых платежей.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//  ПериодИспользования - Дата - срок действия QR-кода.
//
// Возвращаемое значение:
//  Структура - идентификаторы оплаты в Системе быстрых платежей.
//
Функция ПараметрыОпределенияСтатусаОперации(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса,
		|	ИдентификаторыОперацийСБПb2b.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОперацийСБПb2b.ПериодИспользования КАК ПериодИспользования,
		|	ИдентификаторыОперацийСБПb2b.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОперацийСБПb2b.СуммаОперации КАК СуммаОперации,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторПлатежа КАК ИдентификаторПлатежа
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
		|ГДЕ
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("ИдентификаторОплаты", ВыборкаДетальныеЗаписи.ИдентификаторОплаты);
		Результат.Вставить("ИдентификаторОперации", ВыборкаДетальныеЗаписи.ИдентификаторОперации);
		Результат.Вставить("ДатаЗапросаСтатуса", ВыборкаДетальныеЗаписи.ДатаЗапросаСтатуса);
		Результат.Вставить("СтатусОперации", ВыборкаДетальныеЗаписи.СтатусОперации);
		Результат.Вставить("ПериодИспользования", ВыборкаДетальныеЗаписи.ПериодИспользования);
		Результат.Вставить("ДатаОперации", ВыборкаДетальныеЗаписи.ДатаОперации);
		Результат.Вставить("СуммаОперации", ВыборкаДетальныеЗаписи.СуммаОперации);
		Результат.Вставить("ИдентификаторПлатежа", ВыборкаДетальныеЗаписи.ИдентификаторПлатежа);
		
		// Если не было успешного получения статуса, необходимо
		// запрашивать наличие callback.
		Если Не ЗначениеЗаполнено(Результат.ДатаЗапросаСтатуса) Тогда
			Результат.ДатаЗапросаСтатуса = ТекущаяДатаСеанса();
		КонецЕсли;
		
		Возврат Результат;
		
	КонецЕсли;
	
КонецФункции

// Выполняет поиск и информации об оплате в регистре и устанавливает новое значение
// идентификатора Системы быстрых платежей и периода действия.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//  ИдентификаторОплаты - Строка - идентификатор оплаты в Системе быстрых платежей;
//  ПериодИспользования - Дата - срок действия QR-кода;
//  ПлатежнаяСсылка - Строка - идентификатор, по которому выполняется оплата;
//  СтатусОперации - Строка - текущий статус операции;
//  ЗаказНаОплату - Структура - см. МодульПереводыСБПb2bСервис.ОписаниеЗаказаНаОплату.
//
Процедура ЗаписатьДанныеОплатыСБП(
		ДокументОперации,
		ИдентификаторОплаты,
		ПериодИспользования,
		ПлатежнаяСсылка,
		СтатусОперации,
		ЗаказНаОплату) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийСБПb2b");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.ИдентификаторыОперацийСБПb2b.СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		Если Не ЗначениеЗаполнено(Запись.ДокументОперации) Тогда
			ВызватьИсключение НСтр("ru = 'Информация о документе оплаты не обнаружена, не возможно записать идентификатор СБП.';
									|en = 'Information about the AR/AP transaction is not found. Cannot save the FPS ID.'");
		КонецЕсли;
		
		Запись.ИдентификаторОплаты = СтрЗаменить(ИдентификаторОплаты, Символы.НПП, "");
		Запись.СтатусОперации = СтатусОперации;
		Запись.ПлатежнаяСсылка = ПлатежнаяСсылка;
		Запись.НазначениеПлатежа = ЗаказНаОплату.НазначениеПлатежа;
		Запись.РасчетныйСчет = ЗаказНаОплату.РасчетныйСчет;
		Запись.СуммаОперации = ЗаказНаОплату.СуммаОплаты;
		Запись.СуммаНДС = ЗаказНаОплату.СуммаНДС;
		Запись.ОблагаетсяНДС = ЗаказНаОплату.ОблагаетсяНДС;
		Запись.ИдентификаторПлатежа = ЗаказНаОплату.ИдентификаторПлатежа;
		
		// Период использования должен фиксироваться только при первом
		// запросе QR-кода.
		Если Не ЗначениеЗаполнено(Запись.ПериодИспользования) Тогда
			Запись.ПериодИспользования = ПериодИспользования;
		КонецЕсли;
		Запись.Заполнить(Неопределено);
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи идентификатора СБП возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the FPS ID:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СистемаБыстрыхПлатежейСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
КонецПроцедуры

// Выполняет поиск и информации об оплате в регистре и устанавливает новое значение
// идентификатора операции, статуса операции, даты операции и даты определения статуса.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//  ИдентификаторОперации - Строка - идентификатор оплаты в Системе быстрых платежей;
//  ДатаОперации - Дата - дата операции в Системе быстрых платежей;
//  СтатусОперации - Строка - статус операции в Системе быстрых платежей;
//  ДатаЗапросаСтатуса - Дата - дата последней операции получения статуса.
//
Процедура ЗаписатьСтатусОперации(
		ДокументОперации,
		ИдентификаторОперации,
		ДатаОперации,
		СтатусОперации,
		СуммаОперации = Неопределено,
		ДатаЗапросаСтатуса = Неопределено) Экспорт
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ИдентификаторыОперацийСБПb2b");
		ЭлементБлокировки.УстановитьЗначение("ДокументОперации", ДокументОперации);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запись = РегистрыСведений.ИдентификаторыОперацийСБПb2b.СоздатьМенеджерЗаписи();
		Запись.ДокументОперации = ДокументОперации;
		Запись.Прочитать();
		
		Если Не ЗначениеЗаполнено(Запись.ДокументОперации) Тогда
			ВызватьИсключение НСтр("ru = 'Информация о документе оплаты не обнаружена, невозможно записать данные.';
									|en = 'Information about the AR/AP transaction is not found. Cannot save the data.'");
		КонецЕсли;
		
		Запись.ИдентификаторОперации = ИдентификаторОперации;
		Запись.ДатаОперации = ДатаОперации;
		Если ЗначениеЗаполнено(СуммаОперации) Тогда
			Запись.СуммаОперации = СуммаОперации;
		КонецЕсли;
		Запись.СтатусОперации = СтатусОперации;
		Запись.КоличествоПопыток = Запись.КоличествоПопыток + 1;
		Если ЗначениеЗаполнено(ДатаЗапросаСтатуса) Тогда
			Запись.ДатаЗапросаСтатуса = ДатаЗапросаСтатуса;
		КонецЕсли;
		
		Если Запись.КоличествоПопыток > 400 Тогда
			Запись.ОтложенноеПолучениеСтатуса = Ложь;
		КонецЕсли;
		
		Запись.Заполнить(Неопределено);
		Запись.Записать();
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		
		ТекстИсключения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'При записи данных возникли ошибки:
				|%1';
				|en = 'Errors occurred when saving the data:
				|%1'"),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		СистемаБыстрыхПлатежейСлужебный.ЗаписатьИнформациюВЖурналРегистрации(
			ТекстИсключения,
			Истина);
		
		ВызватьИсключение ТекстИсключения;
	КонецПопытки;
	
КонецПроцедуры

// Создает запись с новыми данными оплаты.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе;
//  СуммаОперации - Число - Содержит сумму выполненной оплаты.
//  НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей -
//    настройка выполнения оплаты.
//  ПараметрыОперации - Структура - содержит дополнительные данные операции.
//
Процедура ЗаписатьНовыеДанныеОплаты(
	ДокументОперации,
	СуммаОперации,
	ПараметрыНастройкиОплаты,
	ПараметрыОперации) Экспорт
	
	Запись = РегистрыСведений.ИдентификаторыОперацийСБПb2b.СоздатьМенеджерЗаписи();
	Запись.ДокументОперации = ДокументОперации;
	Запись.Идентификатор = ПараметрыОперации.Идентификатор;
	Запись.ИдентификаторОплаты = ПараметрыОперации.ИдентификаторОплаты;
	Запись.ИдентификаторОперации = ПараметрыОперации.ИдентификаторОперации;
	Запись.ДатаОперации = ПараметрыОперации.ДатаОперации;
	Запись.СуммаОперации = СуммаОперации;
	Запись.СуммаНДС = ПараметрыОперации.СуммаНДС;
	Запись.ОблагаетсяНДС = ПараметрыОперации.ОблагаетсяНДС;
	Запись.ПериодИспользования = Неопределено;
	Запись.ПлатежнаяСсылка = "";
	Запись.РасчетныйСчет = ПараметрыОперации.РасчетныйСчет;
	Запись.ИдентификаторПлатежа = ПараметрыОперации.ИдентификаторПлатежа;
	Запись.ИдентификаторМерчанта = ПараметрыОперации.ИдентификаторМерчанта;
	Запись.ДатаЗапросаСтатуса = Неопределено;
	Запись.СтатусОперации = НастройкиПлатежныхСистемСлужебный.ИдентификаторСтатусаВыполнена();
	Запись.НастройкаПодключения = ПараметрыНастройкиОплаты.НастройкаПодключения;
	Запись.ОтложенноеПолучениеСтатуса = Ложь;
	Запись.КоличествоПопыток = 0;
	Запись.ИдентификаторУчастника = ПараметрыНастройкиОплаты.ИдентификаторУчастника;
	Запись.НазначениеПлатежа = ПараметрыОперации.НазначениеПлатежа;
	Запись.Заполнить(Неопределено);
	Запись.Записать();
	
КонецПроцедуры

// Получает исторические данные заказа на оплату по идентификатору.
//
// Параметры
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий оплату
//    в информационной базе.
//
// Возвращаемое значение:
//  Структура - данные заказа на оплату:
//    * СуммаОперации - Число - сумма операции;
//    * НазначениеПлатежа - Строка - назначение для СБП;
//    * ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - объект оплаты в информационной базе;
//    * ИдентификаторМерчанта - Строка - идентификатор торговой точки для которой была сформирован заказ;
//    * НастройкаПодключения - СправочникСсылка.НастройкиПодключенияКСистемеБыстрыхПлатежей - настройка подключения;
//    * ИдентификаторУчастника - Строка - идентификатор участника СБП;
//    * РасчетныйСчет - Строка - расчетный счет получателя платежа
//    * ИдентификаторПлатежа - Строка - уникальный идентификатор Платежа, назначаемый Получателем
//    * СуммаНДС - Число - Сумма НДС операции;
//    * ОблагаетсяНДС - Булево - Признак обложения НДС операции;
//    * СтатусОперации - Строка - Идентификатор статуса операции;
//    * ИдентификаторОплаты Строка - Идентификатор оплаты;
//    * ИдентификаторОперации - Строка - Идентификатор операции;
//    * Оплата - Булево- признак того является ли операция оплатой или возвратом.
//
Функция ДанныеОперации(ДокументОперации) Экспорт
	
	ДокументыОпераций = Новый Массив;
	ДокументыОпераций.Добавить(ДокументОперации);
	
	ДанныеОпераций = ДанныеОпераций(ДокументыОпераций);
	
	Возврат ДанныеОпераций.Получить(ДокументОперации);
	
КонецФункции

// Получает данные по документам операций.
//
// Параметры:
//  ДокументОперации - Массив Из ОпределяемыйТип.ДокументОперацииСБП - документы отражающие оплату
//    в информационной базе;
//
// Возвращаемое значение:
//  Соответствие - данные операций.
//
Функция ДанныеОпераций(ДокументыОпераций) Экспорт
	
	Результат = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОперацийСБПb2b.СуммаОперации КАК СуммаОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОперацийСБПb2b.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации КАК ДокументОперации,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
		|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения КАК НастройкаПодключения,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника <> """"
		|			ТОГДА ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника
		|		ИНАЧЕ ИдентификаторыОперацийСБПb2b.НастройкаПодключения.ИдентификаторУчастника
		|	КОНЕЦ КАК ИдентификаторУчастника,
		|	ИдентификаторыОперацийСБПb2b.РасчетныйСчет КАК РасчетныйСчет,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
		|	ИдентификаторыОперацийСБПb2b.СуммаНДС КАК СуммаНДС,
		|	ИдентификаторыОперацийСБПb2b.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОперацийСБПb2b.ОблагаетсяНДС КАК ОблагаетсяНДС
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
		|ГДЕ
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации В (&ДокументыОпераций)";
	
	Запрос.УстановитьПараметр("ДокументыОпераций", ДокументыОпераций);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Для Каждого ДокументОперации Из ДокументыОпераций Цикл
		
		Отбор = Новый Структура("ДокументОперации", ДокументОперации);
		
		Если ВыборкаДетальныеЗаписи.НайтиСледующий(Отбор) Тогда
			ДанныеОперации = Новый Структура;
			ДанныеОперации.Вставить("СуммаОперации", ВыборкаДетальныеЗаписи.СуммаОперации);
			ДанныеОперации.Вставить("ДатаОперации", ВыборкаДетальныеЗаписи.ДатаОперации);
			ДанныеОперации.Вставить("НазначениеПлатежа", ВыборкаДетальныеЗаписи.НазначениеПлатежа);
			ДанныеОперации.Вставить("ДокументОперации", ВыборкаДетальныеЗаписи.ДокументОперации);
			ДанныеОперации.Вставить("ИдентификаторМерчанта", ВыборкаДетальныеЗаписи.ИдентификаторМерчанта);
			ДанныеОперации.Вставить("НастройкаПодключения", ВыборкаДетальныеЗаписи.НастройкаПодключения);
			ДанныеОперации.Вставить("ИдентификаторУчастника", ВыборкаДетальныеЗаписи.ИдентификаторУчастника);
			ДанныеОперации.Вставить("РасчетныйСчет", ВыборкаДетальныеЗаписи.РасчетныйСчет);
			ДанныеОперации.Вставить("ИдентификаторПлатежа", ВыборкаДетальныеЗаписи.ИдентификаторПлатежа);
			ДанныеОперации.Вставить("СуммаНДС", ВыборкаДетальныеЗаписи.СуммаНДС);
			ДанныеОперации.Вставить("ОблагаетсяНДС", ВыборкаДетальныеЗаписи.ОблагаетсяНДС);
			ДанныеОперации.Вставить("СтатусОперации", ВыборкаДетальныеЗаписи.СтатусОперации);
			ДанныеОперации.Вставить("ИдентификаторОплаты", ВыборкаДетальныеЗаписи.ИдентификаторОплаты);
			ДанныеОперации.Вставить("ИдентификаторОперации", ВыборкаДетальныеЗаписи.ИдентификаторОперации);
			ДанныеОперации.Вставить("Оплата", Истина);
		Иначе
			ДанныеОперации = Неопределено;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи.Сбросить();
		
		Результат.Вставить(ДокументОперации, ДанныеОперации);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Получает данные отложенных операций для дальнейшей обработки статусов.
//
// Возвращаемое значение:
//  Структура - отложенные операции для обработки.
//
Функция ОтложенныеОперации() Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("ОтложенныеОперации", Новый Соответствие);
	Результат.Вставить("СменаУчастника", Новый Соответствие);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации КАК ДокументОперации,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса,
		|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения КАК НастройкаПодключения,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторУчастника <> ИдентификаторыОперацийСБПb2b.НастройкаПодключения.ИдентификаторУчастника КАК СменаУчастника,
		|	ИдентификаторыОперацийСБПb2b.ПериодИспользования КАК ПериодИспользования,
		|	ИдентификаторыОперацийСБПb2b.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОперацийСБПb2b.КоличествоПопыток КАК КоличествоПопыток,
		|	ИдентификаторыОперацийСБПb2b.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОперацийСБПb2b.СуммаОперации КАК СуммаОперации,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОперацийСБПb2b.ДокументОперации ССЫЛКА Документ.ПлатежнаяСсылкаСБП
		|			ТОГДА ВЫРАЗИТЬ(ИдентификаторыОперацийСБПb2b.ДокументОперации КАК Документ.ПлатежнаяСсылкаСБП).ОснованиеПлатежа
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДокументОснование,
		|	ИдентификаторыОперацийСБПb2b.РасчетныйСчет КАК РасчетныйСчет,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
		|	ИдентификаторыОперацийСБПb2b.КоличествоПопыток КАК КоличествоПопыток1
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
		|ГДЕ
		|	ИдентификаторыОперацийСБПb2b.ОтложенноеПолучениеСтатуса
		|	И ИдентификаторыОперацийСБПb2b.НастройкаПодключения.Используется
		|	И ИдентификаторыОперацийСБПb2b.СтатусОперации <> """"
		|ИТОГИ ПО
		|	НастройкаПодключения";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаНастройкаПодключения = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаНастройкаПодключения.Следующий() Цикл
		
		Если Не ЗначениеЗаполнено(ВыборкаНастройкаПодключения.НастройкаПодключения) Тогда
			Продолжить;
		КонецЕсли;
		
		ВыборкаДетальныеЗаписи = ВыборкаНастройкаПодключения.Выбрать();
		
		ОтложенныеОперации = Новый Массив;
		СменаУчастника = Новый Массив;
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Операция = Новый Структура;
			Операция.Вставить("ДокументОперации", ВыборкаДетальныеЗаписи.ДокументОперации);
			Операция.Вставить("ИдентификаторОплаты", ВыборкаДетальныеЗаписи.ИдентификаторОплаты);
			Операция.Вставить("ИдентификаторОперации", ВыборкаДетальныеЗаписи.ИдентификаторОперации);
			Операция.Вставить("ДатаЗапросаСтатуса", ВыборкаДетальныеЗаписи.ДатаЗапросаСтатуса);
			Операция.Вставить("НастройкаПодключения", ВыборкаДетальныеЗаписи.НастройкаПодключения);
			Операция.Вставить("ПериодИспользования", ВыборкаДетальныеЗаписи.ПериодИспользования);
			Операция.Вставить("СтатусОперации", ВыборкаДетальныеЗаписи.СтатусОперации);
			Операция.Вставить("КоличествоПопыток", ВыборкаДетальныеЗаписи.КоличествоПопыток);
			Операция.Вставить("РасчетныйСчет", ВыборкаДетальныеЗаписи.РасчетныйСчет);
			Операция.Вставить("ДокументОснование", ВыборкаДетальныеЗаписи.ДокументОснование);
			Операция.Вставить("ИдентификаторПлатежа", ВыборкаДетальныеЗаписи.ИдентификаторПлатежа);
			Операция.Вставить("КоличествоПопыток", ВыборкаДетальныеЗаписи.КоличествоПопыток);
			
			ПараметрыОперации = ПереводыСБПb2bСлужебный.НовыйОписаниеПараметровОперации();
			ЗаполнитьЗначенияСвойств(
				ПараметрыОперации,
				ВыборкаДетальныеЗаписи);
			ПараметрыОперации.ИдентификаторОперации = ВыборкаДетальныеЗаписи.ИдентификаторОперации;
			Операция.Вставить("ПараметрыОперации", ПараметрыОперации);
			
			Если ВыборкаДетальныеЗаписи.СменаУчастника Тогда
				СменаУчастника.Добавить(Операция);
			Иначе
				ОтложенныеОперации.Добавить(Операция);
			КонецЕсли;
		КонецЦикла;
		
		Если ОтложенныеОперации.Количество() <> 0 Тогда
			Результат.ОтложенныеОперации.Вставить(
				ВыборкаНастройкаПодключения.НастройкаПодключения,
				ОтложенныеОперации);
		КонецЕсли;
		
		Если СменаУчастника.Количество() <> 0 Тогда
			Результат.СменаУчастника.Вставить(
				ВыборкаНастройкаПодключения.НастройкаПодключения,
				СменаУчастника);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Формирует структуру данных для формы платежной ссылки в Системе быстрых платежей.
//
// Параметры:
//  ДокументОперации - ОпределяемыйТип.ДокументОперацииСБП - документ отражающий операцию
//    в информационной базе;
//
// Возвращаемое значение:
//  Структура - данные для формы платежной ссылки в Системе быстрых платежей
//
Функция ДанныеПлатежнойСсылки(ДокументОперации) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыОперацийСБПb2b.Идентификатор КАК Идентификатор,
		|	ИдентификаторыОперацийСБПb2b.ПлатежнаяСсылка КАК ПлатежнаяСсылка,
		|	ИдентификаторыОперацийСБПb2b.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОперацийСБПb2b.СуммаОперации КАК СуммаОперации,
		|	ИдентификаторыОперацийСБПb2b.НазначениеПлатежа КАК НазначениеПлатежа,
		|	ИдентификаторыОперацийСБПb2b.НастройкаПодключения КАК НастройкаПодключения,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторМерчанта КАК ИдентификаторМерчанта,
		|	ВЫБОР
		|		КОГДА ИдентификаторыОперацийСБПb2b.ДокументОперации ССЫЛКА Документ.ПлатежнаяСсылкаСБП
		|			ТОГДА ВЫРАЗИТЬ(ИдентификаторыОперацийСБПb2b.ДокументОперации КАК Документ.ПлатежнаяСсылкаСБП).ОснованиеПлатежа
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК ДокументОснование
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
		|ГДЕ
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		
		Результат = Новый Структура;
		Результат.Вставить("Идентификатор", ВыборкаДетальныеЗаписи.Идентификатор);
		Результат.Вставить("СтатусОперации", ВыборкаДетальныеЗаписи.СтатусОперации);
		Результат.Вставить("ПлатежнаяСсылка", ВыборкаДетальныеЗаписи.ПлатежнаяСсылка);
		Результат.Вставить("ДатаОперации", ВыборкаДетальныеЗаписи.ДатаОперации);
		Результат.Вставить("СуммаОперации", ВыборкаДетальныеЗаписи.СуммаОперации);
		Результат.Вставить("НазначениеПлатежа", ВыборкаДетальныеЗаписи.НазначениеПлатежа);
		Результат.Вставить("НастройкаПодключения", ВыборкаДетальныеЗаписи.НастройкаПодключения);
		Результат.Вставить("ИдентификаторМерчанта", ВыборкаДетальныеЗаписи.ИдентификаторМерчанта);
		Результат.Вставить("ДокументОснование", ВыборкаДетальныеЗаписи.ДокументОснование);
		
		Возврат Результат;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// См. СистемаБыстрыхПлатежейСлужебный.ИнформацияДляТехническойПоддержки.
//
Функция ИнформацияДляТехническойПоддержки(ДокументОперации) Экспорт
	
	ТекстСообщения = НСтр("ru = 'Данные операции b2b:';
							|en = 'b2b transaction data:'")
		+ Символы.ПС
		+ Символы.ПС;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИдентификаторыОперацийСБПb2b.Идентификатор КАК Идентификатор,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОплаты КАК ИдентификаторОплаты,
		|	ИдентификаторыОперацийСБПb2b.ИдентификаторОперации КАК ИдентификаторОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаОперации КАК ДатаОперации,
		|	ИдентификаторыОперацийСБПb2b.ДатаЗапросаСтатуса КАК ДатаЗапросаСтатуса,
		|	ИдентификаторыОперацийСБПb2b.СтатусОперации КАК СтатусОперации,
		|	ИдентификаторыОперацийСБПb2b.ПлатежнаяСсылка КАК ПлатежнаяСсылка
		|ИЗ
		|	РегистрСведений.ИдентификаторыОперацийСБПb2b КАК ИдентификаторыОперацийСБПb2b
		|ГДЕ
		|	ИдентификаторыОперацийСБПb2b.ДокументОперации = &ДокументОперации";
	
	Запрос.УстановитьПараметр("ДокументОперации", ДокументОперации);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат ТекстСообщения + НСтр("ru = 'Информация о выполнении операции c2b не обнаружена в базе данных.';
										|en = 'Information on c2b transaction execution is not found in the database.'")
			+ Символы.ПС
			+ Символы.ПС;
	КонецЕсли;
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	ВыборкаДетальныеЗаписи.Следующий();
	
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Идентификатор:';
				|en = 'ID:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.Идентификатор)
		+ Символы.ПС;
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Идентификатор оплаты:';
				|en = 'Payment ID:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.ИдентификаторОплаты)
		+ Символы.ПС; 
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Идентификатор операции:';
				|en = 'Transaction ID:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.ИдентификаторОперации)
		+ Символы.ПС;
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Платежная ссылка:';
				|en = 'Payment link:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.ПлатежнаяСсылка)
		+ Символы.ПС;
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Дата операции:';
				|en = 'Transaction date:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.ДатаОперации)
		+ Символы.ПС;
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Дата запроса статуса:';
				|en = 'Status request date:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.ДатаЗапросаСтатуса)
		+ Символы.ПС;
	ТекстСообщения = ТекстСообщения
		+ НСтр("ru = 'Статус операции:';
				|en = 'Transaction status:'")
		+ СистемаБыстрыхПлатежейСлужебный.ОбработатьЗначениеДляТехническойПоддержки(
			ВыборкаДетальныеЗаписи.СтатусОперации)
		+ Символы.ПС
		+ Символы.ПС;
	
	Возврат ТекстСообщения;
	
КонецФункции
#КонецОбласти

#КонецЕсли
