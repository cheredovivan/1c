#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	Если Количество() = 0 Или РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Возврат;
	КонецЕсли;
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.Состояние = СостояниеРегистрации(Запись);
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СостояниеРегистрации(Запись)
	ЭтоДополнительноеСведение = ТипЗнч(Запись.ИсходящийДокумент) = Тип("ДокументСсылка.ДополнительныеСведенияДляОплатыОтпускаСФР");
	Если Не ЗначениеЗаполнено(Запись.ДатаОтправки) И Не ЗначениеЗаполнено(Запись.ДатаОтправкиОператору) Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ПустаяСсылка(); // Не отправлен.
	ИначеЕсли ЗначениеЗаполнено(Запись.ДоставкаТекстОшибки) Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОшибкаПриОтправке; // Ошибка при отправке.
	ИначеЕсли Запись.ЕстьОшибкиЛогическогоКонтроля Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОшибкаЛогическогоКонтроля; // Отправлен, получены ошибки.
	ИначеЕсли ВРег(Запись.РегистрацияСтатус) = "RECEIVED" Тогда
		Если ЭтоДополнительноеСведение Тогда
			Возврат Перечисления.СостоянияДокументаСЭДОФСС.Принят; // Дополнительные сведения приняты.
		Иначе
			Возврат Перечисления.СостоянияДокументаСЭДОФСС.Отправлен; // Отправлен на рассмотрении.
		КонецЕсли;
	ИначеЕсли ВРег(Запись.РегистрацияСтатус) = "ERROR" Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.НеПринят; // Отправлен, но не зарегистрирован.
	ИначеЕсли ВРег(Запись.РегистрацияСтатус) = "VACATIONNEEDDOC" Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ТребуетсяСоздать;
	ИначеЕсли ВРег(Запись.РегистрацияСтатус) = "VACATIONRETURN" Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.НеПринят;
	ИначеЕсли ВРег(Запись.РегистрацияСтатус) = "VACATIONPAY" Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.Принят;
	ИначеЕсли Запись.ОтправленОператору И Не Запись.Доставлен Тогда
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.ОтправленОператору; // Передан оператору для доставки.
	Иначе
		Возврат Перечисления.СостоянияДокументаСЭДОФСС.Отправлен; // Отправлен - заполнена дата отправка, нет информации о статусе.
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли