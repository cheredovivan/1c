///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(ИсходящийДокумент)
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|	ИзменениеОбъектаРазрешено(ИсходящийДокумент)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// АПК:581-выкл. Методы могут вызываться из расширений.
// АПК:299-выкл. Методы могут вызываться из расширений.
// АПК:326-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// АПК:325-выкл. Методы поставляются комплектом и предназначены для совместного последовательного использования.
// Транзакция открывается в методе НачатьЗаписьНабора, закрывается в ЗавершитьЗаписьНабора, отменяется в ОтменитьЗаписьНабора.

// Транзакционный вариант (с управляемой блокировкой) получения набора записей, соответствующего значениям измерений.
//
// Параметры:
//   ИсходящийДокумент - ДокументСсылка.СведенияДляОплатыОтпускаСФР - Значение отбора по соответствующему измерению.
//
// Возвращаемое значение:
//   РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия - Если удалось установить блокировку
//       и прочитать набор записей. При необходимости, открывает свою локальную транзакцию. Для закрытия транзакции
//       следует использовать одну из терминирующих процедур: ЗавершитьЗаписьНабора, либо ОтменитьЗаписьНабора.
//   Неопределено - Если не удалось установить блокировку и прочитать набор записей.
//       Вызывать процедуры ЗавершитьЗаписьНабора, ОтменитьЗаписьНабора не требуется,
//       поскольку если перед блокировкой функции потребовалось открыть локальную транзакцию,
//       то после неудачной блокировки локальная транзакция была отменена.
//
Функция НачатьЗаписьНабора(ИсходящийДокумент) Экспорт
	Если Не ЗначениеЗаполнено(ИсходящийДокумент) Тогда
		Возврат Неопределено;
	КонецЕсли;
	ПолныеПраваИлиПривилегированныйРежим = Пользователи.ЭтоПолноправныйПользователь();
	Если Не ПолныеПраваИлиПривилегированныйРежим
		И Не ПравоДоступа("Изменение", Метаданные.РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР) Тогда
		ВызватьИсключение СтрШаблон(
			НСтр("ru = 'Недостаточно прав для изменения регистра ""%1"".';
				|en = 'Insufficient rights to change register ""%1"".'"),
			Метаданные.РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.Представление());
	КонецЕсли;
	ЛокальнаяТранзакция = Не ТранзакцияАктивна();
	Если ЛокальнаяТранзакция Тогда
		НачатьТранзакцию();
	КонецЕсли;
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР");
		ЭлементБлокировки.УстановитьЗначение("ИсходящийДокумент", ИсходящийДокумент);
		Блокировка.Заблокировать();
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ИсходящийДокумент.Установить(ИсходящийДокумент);
		НаборЗаписей.Прочитать();
		НаборЗаписей.ДополнительныеСвойства.Вставить("ЛокальнаяТранзакция", ЛокальнаяТранзакция);
	Исключение
		Если ЛокальнаяТранзакция Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Не удалось изменить регистрацию сведений для расчета пособия ФСС %1 по причине: %2';
				|en = 'Cannot change registration of the information for SSF allowance calculation %1 due to: %2'"),
			ИсходящийДокумент,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР,
			ИсходящийДокумент,
			ТекстСообщения);
		НаборЗаписей = Неопределено;
		ВызватьИсключение;
	КонецПопытки;
	
	Возврат НаборЗаписей;
КонецФункции

// Записывает набор и фиксирует локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия
//
Процедура ЗавершитьЗаписьНабора(НаборЗаписей) Экспорт
	НаборЗаписей.Записать(Истина);
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ЗафиксироватьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// Отменяет запись набора и отменяет локальную транзакцию, если она была открыта в функции НачатьЗаписьНабора.
//
// Параметры:
//   НаборЗаписей - РегистрСведенийНаборЗаписей.РегистрацииОтветовНаЗапросыФССДляРасчетаПособия
//
Процедура ОтменитьЗаписьНабора(НаборЗаписей) Экспорт
	ЛокальнаяТранзакция = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(НаборЗаписей.ДополнительныеСвойства, "ЛокальнаяТранзакция");
	Если ЛокальнаяТранзакция = Истина Тогда
		ОтменитьТранзакцию();
	КонецЕсли;
КонецПроцедуры

// АПК:326-вкл.
// АПК:325-вкл.
// АПК:299-вкл.
// АПК:581-вкл.

#Область ОбновлениеИнформационнойБазы

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументу(ИсходящийДокументОбъект, РезультатОтправки = Неопределено) Экспорт
	// Регистрация является продолжением текущего документа.
	// Но если документ - новый, то поле "Ссылка" может быть не заполнено.
	УстановитьПривилегированныйРежим(Истина);	
	Набор = НачатьЗаписьНабора(ИсходящийДокументОбъект.Ссылка);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	
	Запись.ИсходящийДокумент = ИсходящийДокументОбъект.Ссылка;
	Запись.ФизическоеЛицо    = ИсходящийДокументОбъект.ФизическоеЛицо;
	
	Если РезультатОтправки <> Неопределено Тогда
		Запись.Страхователь                = РезультатОтправки.Страхователь;
		Запись.ГоловнаяОрганизация         = РезультатОтправки.ГоловнаяОрганизация;
		Запись.ОтправленОператору          = РезультатОтправки.ОтправленоОператору;
		Запись.Доставлен                   = РезультатОтправки.ДоставленоФонду;
		Запись.ДатаОтправкиОператору       = РезультатОтправки.ДатаОтправкиОператору;
		Запись.ДатаОтправки                = РезультатОтправки.ДатаОтправкиФонду;
		Запись.ДоставкаТекстОшибки         = РезультатОтправки.ТекстОшибки;
		Запись.ДоставкаИдентификатор       = РезультатОтправки.ИдентификаторСообщения;
		Запись.ДоставкаИдентификаторПакета = РезультатОтправки.ИдентификаторПакета;
	КонецЕсли;
	
	ЗавершитьЗаписьНабора(Набор);
КонецПроцедуры

#Область СЭДО

Функция НайтиПоИдентификаторуСообщения(Страхователь, ИдентификаторСообщения, РезультатБРО) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Регистрация.ИсходящийДокумент КАК ИсходящийДокумент,
	|	Регистрация.Страхователь КАК Страхователь,
	|	Регистрация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистрация
	|ГДЕ
	|	Регистрация.ДоставкаИдентификатор = &ИдентификаторСообщения
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Регистрация.ИсходящийДокумент,
	|	Регистрация.Страхователь,
	|	Регистрация.ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистрация
	|ГДЕ
	|	Регистрация.РегистрацияИдентификатор = &ИдентификаторСообщения";
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
	Таблица = Запрос.Выполнить().Выгрузить();
	Найденные = Таблица.Скопировать(Новый Структура("Страхователь", Страхователь));
	Если Найденные.Количество() = 0 Тогда
		ГоловнаяОрганизация = ЗарплатаКадры.ГоловнаяОрганизация(Страхователь);
		Найденные = Таблица.Скопировать(Новый Структура("ГоловнаяОрганизация", ГоловнаяОрганизация));
		Если Найденные.Количество() = 0 Тогда
			Найденные = Таблица;
		КонецЕсли;
	КонецЕсли;
	Если Найденные.Количество() = 0 Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'По страхователю ""%1"" не найдена %2 с идентификатором сообщения ""%3""';
				|en = 'No %2 with message ID ""%3"" was found for insurant ""%1""'"),
			Страхователь,
			Метаданные.РегистрыСведений.РегистрацииСведенийДляОплатыОтпускаСФР.ПредставлениеЗаписи,
			ИдентификаторСообщения);
		СЭДОФСС.ОшибкаОбработки(РезультатБРО, ТекстОшибки);
	КонецЕсли;
	Возврат Найденные;
КонецФункции

Процедура ЗаполнитьПоРезультатуДоставки(ИсходящийДокумент, Сообщение) Экспорт
	Набор = НачатьЗаписьНабора(ИсходящийДокумент);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		Запись.ИсходящийДокумент = ИсходящийДокумент;
	Иначе
		Запись = Набор[0];
	КонецЕсли;
	
	Запись.Доставлен           = Сообщение.ДоставленоФонду;
	Запись.ДатаОтправки        = Сообщение.ДатаОтправкиФонду;
	Запись.ДоставкаТекстОшибки = Сообщение.ТекстОшибки;
	
	ЗавершитьЗаписьНабора(Набор);
КонецПроцедуры

// Загружает результат обработки на 94 и 96 сообщение
Процедура ЗагрузитьСообщение95(Страхователь, РегистрацияИдентификатор, ТекстXML, Результат) Экспорт
#Область Примеры
	//Ошибка обработки входящего сообщения 
	//<additionalVacationStatementResponse xmlns="http://www.fss.ru/additionalVacationStatement">
	//	<statementID>2a2f6af7-57f4-4a06-86bd-d5c3b45fa7e4</statementID>
	//	<msgDate>2022-08-18</msgDate>
	//	<status>ERROR</status>
	//	<errors>
	//		<errorCode>E_VV_0005</errorCode>
	//		<errorMessage>Регистрационный номер страхователя в сведениях для оплаты отпуска не совпадает с регистрационным номером в сообщении-запросе</errorMessage>
	//	</errors>
	//</additionalVacationStatementResponse>

	//Успешная обработка входящего сообщения
	//<additionalVacationStatementResponse xmlns="http://www.fss.ru/additionalVacationStatement">
	//	<statementID>2a2f6af7-57f4-4a06-86bd-d5c3b65fa7e4</statementID>
	//	<msgDate>2022-08-18</msgDate>
	//	<status>RECEIVED</status>
	//</additionalVacationStatementResponse>
#КонецОбласти
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'additionalVacationStatementResponse']");
	Если КореньDOM = Неопределено Тогда
		КореньDOM = СтруктураDOM.ДокументDOM;
	КонецЕсли;
	Если КореньDOM = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "additionalVacationStatementResponse");
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	РегистрацияПротокол = "";
	РегистрацияИнформация = "";
	РеквизитыКорня = СериализацияБЗК.УзлыЭлементаDOM(КореньDOM);
	ДоставкаИдентификатор = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.statementID);
	Если ДоставкаИдентификатор = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаАтрибута(Результат, "statementID");
	ИначеЕсли Не ЗначениеЗаполнено(ДоставкаИдентификатор) Тогда
		СЭДОФСС.ЗаписатьОшибкуЗаполненностиАтрибута(Результат, "statementID");
	КонецЕсли;
	РегистрацияДата   = СериализацияБЗК.ДатаИзXML(РеквизитыКорня.msgDate);
	РегистрацияСтатус = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.status);
	СведенияОСтатусе  = СведенияОСтатусеРегистрации(РегистрацияСтатус);
	
	Если РегистрацияСтатус = "ERROR" Тогда
		РеквизитыОшибки = СериализацияБЗК.УзлыЭлементаDOM(РеквизитыКорня.errors);
		КодОшибки = СериализацияБЗК.СтрокаИзXML(РеквизитыОшибки.errorCode);
		РегистрацияИнформация = СериализацияБЗК.СтрокаИзXML(РеквизитыОшибки.errorMessage);
		РегистрацияПротокол = РегистрацияПротокол + РегистрацияДата + " " + КодОшибки + ":" + РегистрацияИнформация;
	Иначе
		РегистрацияПротокол = РегистрацияПротокол + РегистрацияДата + " " + СведенияОСтатусе.Расшифровка;
	КонецЕсли;
	
	СведенияДляОплаты = НайтиСведенияДляОплаты(Страхователь, ДоставкаИдентификатор);
	Если Не ЗначениеЗаполнено(СведенияДляОплаты) Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось найти %1 с идентификатором ""%2"" .%4';
				|en = 'Cannot find %1 with ID %2.%4'"),
			Метаданные.Документы.СведенияДляОплатыОтпускаСФР.Представление(),
			ДоставкаИдентификатор,
			Разделитель() + РегистрацияПротокол);
		СЭДОФСС.ОшибкаОбработки(Результат, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	СообщениеАктуальное = Истина;
	
	Набор = НачатьЗаписьНабора(СведенияДляОплаты);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СведенияДляОплаты);
		Запись.ИсходящийДокумент = СведенияДляОплаты;
	Иначе
		Запись = Набор[0];
		Если ЗначениеЗаполнено(Запись.РегистрацияСтатус)
			И РегистрацияИдентификатор <> Запись.РегистрацияИдентификатор Тогда
			СуществующийСтатус = СведенияОСтатусеРегистрации(Запись.РегистрацияСтатус);
			Если СведенияОСтатусе.Вес < СуществующийСтатус.Вес Тогда
				СообщениеАктуальное = Ложь; // Неактуальный (старый) статус.
			ИначеЕсли СведенияОСтатусе.Вес = СуществующийСтатус.Вес
				И РегистрацияДата < Запись.РегистрацияДата Тогда
				СообщениеАктуальное = Ложь; // Более старая регистрация.
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьИзменения = Ложь;
	Если СообщениеАктуальное Тогда
		ЕстьИзменения = Истина;
		Запись.Зарегистрирован          = СведенияОСтатусе.Зарегистрирован Или СведенияОСтатусе.Вес = 1;
		Запись.РегистрацияДата          = РегистрацияДата;
		Запись.РегистрацияСтатус        = РегистрацияСтатус;
		Запись.РегистрацияПротокол      = Запись.РегистрацияПротокол + ?(ПустаяСтрока(Запись.РегистрацияПротокол), "", Символы.ПС) + РегистрацияПротокол;
		Запись.РегистрацияИдентификатор = РегистрацияИдентификатор;
		УстановитьЗначениеЕслиЗаполнено(Запись.РегистрацияИнформация, РегистрацияИнформация);
	Иначе
		Если Не ЗначениеЗаполнено(Запись.РегистрацияИнформация) И ЗначениеЗаполнено(РегистрацияИнформация) Тогда
			ЕстьИзменения = Истина;
			Запись.РегистрацияИнформация = РегистрацияИнформация;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		ЗавершитьЗаписьНабора(Набор);
	Иначе
		ОтменитьЗаписьНабора(Набор);
	КонецЕсли;
	
	Результат.Обработано = Истина;
КонецПроцедуры

// Загружает ошибку логического контроля полученную в ответ на документ СЭДО 94 96.
Процедура ЗагрузитьОшибкуСообщения95_97(Страхователь, ИдентификаторСообщения, ТекстОшибки, Результат) Экспорт
	Таблица = НайтиПоИдентификаторуСообщения(Страхователь, ИдентификаторСообщения, Результат);
	Для Каждого СтрокаТаблицы Из Таблица Цикл
		Набор = НачатьЗаписьНабора(СтрокаТаблицы.ИсходящийДокумент);
		Если Набор.Количество() = 0 Тогда
			ОтменитьЗаписьНабора(Набор);
			Продолжить;
		КонецЕсли;
		Запись = Набор[0];
		Запись.ОшибкиЛогическогоКонтроля = СокрЛП(ТекстОшибки);
		Запись.ЕстьОшибкиЛогическогоКонтроля = Истина;
		ЗавершитьЗаписьНабора(Набор);
		// Отметка для БРО и других потребителей что сообщение обработано.
		Результат.Обработано = Истина;
	КонецЦикла;
КонецПроцедуры

// Загружает результат ответа на Сведения для оплаты отпуска СФР.
Процедура ЗагрузитьСообщение97(Страхователь, РегистрацияИдентификатор, ТекстXML, Результат) Экспорт
#Область Примеры
	//Подтверждение оплаты отпуска (VACATIONPAY)
	//<resultAddVacationStatement xmlns="http://www.fss.ru/additionalVacationStatement">
	//	<statementID>c88c7012-6ed5-42cf-9553-3a9421609f3c</statementID>
	//	<msgDate>2022-08-18</msgDate>
	//	<notification>
	//		<number>0000047401</number>
	//		<ntfCode>VACATIONPAY</ntfCode>
	//		<comment>ОСФР ПО РЕСПУБЛИКЕ БАШКОРТОСТАН рассмотрены и подтверждены сведения для оплаты отпуска 
	//			застрахованного лица ЩЕРБАКОВА СНЕЖАННА ДЕНИСОВНА</comment>
	//	</notification>
	//</resultAddVacationStatement>
	
	//Возврат сведений страхователю (VACATIONRETURN)
	//<resultAddVacationStatement xmlns="http://www.fss.ru/additionalVacationStatement">
	//	<statementID>c99c9012-6ed5-42cf-9553-3a9421609f3c</statementID>
	//	<msgDate>2022-08-18</msgDate>
	//	<notification>
	//		<number>0000047403</number>
	//		<ntfCode>VACATIONRETURN</ntfCode>
	//		<comment>Возврат страхователю сведений для оплаты дополнительного отпуска по следующей причине:
	//			Поступили обновленные сведения по отпуску застрахованного лица</comment>
	//	</notification>
	//</resultAddVacationStatement>
	
	//Запрос дополнительных документов по сведениям (VACATIONNEEDDOC)
	//<resultAddVacationStatement xmlns="http://www.fss.ru/additionalVacationStatement">
	//	<statementID>c88c9812-6ed5-42cf-9553-3a9421809f3c</statementID>
	//	<msgDate>2022-08-18</msgDate>
	//	<notification>
	//		<number>0000047404</number>
	//		<ntfCode>VACATIONNEEDDOC</ntfCode>
	//		<comment> Для принятия решения об оплате отпуска застрахованного лица(сверх ежегодного оплачиваемого отпуска,
	//			установленного законодательством Российской Федерации) на весь период лечения и проезда к месту лечения и
	//			обратно  необходимо в отделение Фонда пенсионного и социального страхования (ОСФР ПО РЕСПУБЛИКЕ БАШКОРТОСТАН)
	//			предоставить недостающие документы и/или оригиналы в соответствии с пунктом 32(2) Положения об оплате дополнительных
	//			расходов на медицинскую, социальную и профессиональную реабилитацию застрахованных лиц, получивших повреждение здоровья
	//			вследствие несчастных случаев на производстве и профессиональных заболеваний, утвержденного постановлением Правительства
	//			Российской Федерации от 15 мая 2006 г. № 286 в течение 5 рабочих дней со дня получения настоящего извещения. 
	//			Перечень запрашиваемых документов: Сведения о заработке за 2022 год </comment>
	//	</notification>
	//</resultAddVacationStatement>
#КонецОбласти
	УстановитьПривилегированныйРежим(Истина);
	
	СтруктураDOM = СериализацияБЗК.СтруктураDOM(ТекстXML);
	КореньDOM = КореньDOMСообщения97(СтруктураDOM);
	Если КореньDOM = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаУзла(Результат, "resultAddVacationStatement");
		Возврат;
	КонецЕсли;
	
	ТекстОшибки = "";
	РегистрацияПротокол = "";
	РеквизитыКорня = СериализацияБЗК.УзлыЭлементаDOM(КореньDOM, "statementID, msgDate, notification");
	ДоставкаИдентификатор = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.statementID);
	Если ДоставкаИдентификатор = Неопределено Тогда
		СЭДОФСС.ЗаписатьОшибкуПоискаАтрибута(Результат, "statementID");
	ИначеЕсли Не ЗначениеЗаполнено(ДоставкаИдентификатор) Тогда
		СЭДОФСС.ЗаписатьОшибкуЗаполненностиАтрибута(Результат, "statementID");
	КонецЕсли;
	РегистрацияДата          = СериализацияБЗК.ДатаИзXML(РеквизитыКорня.msgDate);;
	
	РеквизитыКорня = СериализацияБЗК.УзлыЭлементаDOM(РеквизитыКорня.notification, "number, ntfCode, comment");
	РегистрацияСтатус        = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.ntfCode);
	РегистрацияИнформация    = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.comment);
	НомерУведомления         = СериализацияБЗК.СтрокаИзXML(РеквизитыКорня.number);
	СведенияОСтатусе         = СведенияОСтатусеРегистрации(РегистрацияСтатус);
	РегистрацияПротокол      = РегистрацияПротокол + Разделитель() + РегистрацияДата + " "+ НомерУведомления + ":" + РегистрацияИнформация;
	
	СведенияДляОплаты = НайтиСведенияДляОплаты(Страхователь, ДоставкаИдентификатор);
	Если Не ЗначениеЗаполнено(СведенияДляОплаты) Тогда
		ТекстОшибки = СтрШаблон(
			НСтр("ru = 'Не удалось найти %1 с идентификатором ""%2"" .%4';
				|en = 'Cannot find %1 with ID %2.%4'"),
			Метаданные.Документы.СведенияДляОплатыОтпускаСФР.Представление(),
			ДоставкаИдентификатор,
			Разделитель() + РегистрацияПротокол);
		СЭДОФСС.ОшибкаОбработки(Результат, ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	СообщениеАктуальное = Истина;
	
	Набор = НачатьЗаписьНабора(СведенияДляОплаты);
	Если Набор.Количество() = 0 Тогда
		Запись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СведенияДляОплаты);
		Запись.ИсходящийДокумент = СведенияДляОплаты;
	Иначе
		Запись = Набор[0];
		Если ЗначениеЗаполнено(Запись.РегистрацияСтатус)
			И РегистрацияИдентификатор <> Запись.РегистрацияИдентификатор Тогда
			СуществующийСтатус = СведенияОСтатусеРегистрации(Запись.РегистрацияСтатус);
			Если СведенияОСтатусе.Вес < СуществующийСтатус.Вес Тогда
				СообщениеАктуальное = Ложь; // Неактуальный (старый) статус.
			ИначеЕсли СведенияОСтатусе.Вес = СуществующийСтатус.Вес
				И РегистрацияДата < Запись.РегистрацияДата Тогда
				СообщениеАктуальное = Ложь; // Более старая регистрация.
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ЕстьИзменения = Ложь;
	Если СообщениеАктуальное Тогда
		ЕстьИзменения = Истина;
		Запись.Зарегистрирован          = СведенияОСтатусе.Зарегистрирован;
		Запись.РегистрацияДата          = РегистрацияДата;
		Запись.РегистрацияСтатус        = РегистрацияСтатус;
		Запись.РегистрацияПротокол      = Запись.РегистрацияПротокол + РегистрацияПротокол;
		Запись.РегистрацияИдентификатор = РегистрацияИдентификатор;
		Запись.НомерУведомления         = НомерУведомления;
		УстановитьЗначениеЕслиЗаполнено(Запись.РегистрацияИнформация, РегистрацияИнформация);
	Иначе
		Если Не ЗначениеЗаполнено(Запись.РегистрацияИнформация) И ЗначениеЗаполнено(РегистрацияИнформация) Тогда
			ЕстьИзменения = Истина;
			Запись.РегистрацияИнформация = РегистрацияИнформация;
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьИзменения Тогда
		ЗавершитьЗаписьНабора(Набор);
	Иначе
		ОтменитьЗаписьНабора(Набор);
	КонецЕсли;
	
	Результат.Обработано = Истина;
КонецПроцедуры

Функция КореньDOMСообщения97(СтруктураDOM)
	КореньDOM = СериализацияБЗК.НайтиУзелDOM(СтруктураDOM, "//*[local-name() = 'resultAddVacationStatement']");
	Если КореньDOM <> Неопределено Тогда
		Возврат КореньDOM;
	КонецЕсли;
	Возврат СтруктураDOM.ДокументDOM;
КонецФункции

Процедура УстановитьЗначениеЕслиЗаполнено(ИзменяемоеЗначение, Значение)
	Если ЗначениеЗаполнено(Значение) Тогда
		ИзменяемоеЗначение = Значение;
	КонецЕсли;
КонецПроцедуры

Функция НайтиСведенияДляОплаты(Страхователь, ИдентификаторСообщения)
	// Формирование набора запросов.
	
	Тексты = Новый ТаблицаЗначений;
	Тексты.Колонки.Добавить("Приоритет");
	Тексты.Колонки.Добавить("Текст");
	
	Если ЗначениеЗаполнено(ИдентификаторСообщения) Тогда
		Если ЗначениеЗаполнено(Страхователь) Тогда
			ЭлементСписка = Тексты.Добавить();
			ЭлементСписка.Приоритет = 1;
			ЭлементСписка.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	НайденныйДокумент.Ссылка КАК Ссылка,
			|	1 КАК Приоритет
			|ИЗ
			|	Документ.СведенияДляОплатыОтпускаСФР КАК НайденныйДокумент
			|ГДЕ
			|	НайденныйДокумент.ИдентификаторСообщения = &ИдентификаторСообщения
			|	И НайденныйДокумент.Страхователь = &Страхователь
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ ПЕРВЫЕ 1
			|	НайденныйДокумент.Ссылка,
			|	1
			|ИЗ
			|	Документ.ДополнительныеСведенияДляОплатыОтпускаСФР КАК НайденныйДокумент
			|ГДЕ
			|	НайденныйДокумент.ИдентификаторСообщения = &ИдентификаторСообщения
			|	И НайденныйДокумент.Страхователь = &Страхователь";
		КонецЕсли;
		ЭлементСписка = Тексты.Добавить();
		ЭлементСписка.Приоритет = 3;
		ЭлементСписка.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	НайденныйДокумент.Ссылка КАК Ссылка,
		|	3 КАК Приоритет
		|ИЗ
		|	Документ.СведенияДляОплатыОтпускаСФР КАК НайденныйДокумент
		|ГДЕ
		|	НайденныйДокумент.ИдентификаторСообщения = &ИдентификаторСообщения
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НайденныйДокумент.Ссылка,
		|	3
		|ИЗ
		|	Документ.ДополнительныеСведенияДляОплатыОтпускаСФР КАК НайденныйДокумент
		|ГДЕ
		|	НайденныйДокумент.ИдентификаторСообщения = &ИдентификаторСообщения";
	КонецЕсли;
	
	Если Тексты.Количество() = 0 Тогда
		Если ЗначениеЗаполнено(Страхователь) Тогда
			ЭлементСписка = Тексты.Добавить();
			ЭлементСписка.Приоритет = 5;
			ЭлементСписка.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Регистр.ИсходящийДокумент КАК Ссылка,
			|	5 КАК Приоритет
			|ИЗ
			|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистр
			|ГДЕ
			|	Регистр.Доставлен = ИСТИНА
			|	И Регистр.РегистрацияИдентификатор = """"
			|	И Регистр.Страхователь = &Страхователь";
		КонецЕсли;
		ЭлементСписка = Тексты.Добавить();
		ЭлементСписка.Приоритет = 6;
		ЭлементСписка.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Регистр.ИсходящийДокумент КАК Ссылка,
		|	6 КАК Приоритет
		|ИЗ
		|	РегистрСведений.РегистрацииСведенийДляОплатыОтпускаСФР КАК Регистр
		|ГДЕ
		|	Регистр.Доставлен = ИСТИНА
		|	И Регистр.РегистрацияИдентификатор = """"";
	КонецЕсли;
	
	Тексты.Сортировать("Приоритет");
	
	Отступ = Символы.ПС + Символы.ПС;
	Разделитель = Отступ + "ОБЪЕДИНИТЬ ВСЕ" + Отступ;
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(Тексты.ВыгрузитьКолонку("Текст"), Разделитель) + Отступ + "УПОРЯДОЧИТЬ ПО Приоритет";
	Запрос.УстановитьПараметр("Страхователь", Страхователь);
	Запрос.УстановитьПараметр("ИдентификаторСообщения", ИдентификаторСообщения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Неопределено;
КонецФункции

Функция СведенияОСтатусеРегистрации(Статус)
	Результат = Новый Структура("Вес, Зарегистрирован, Расшифровка");
	
	СтатусВРег = ВРег(СокрЛП(Статус));
	
	// Статусы из актуальной спецификации.
	Если СтатусВРег = "RECEIVED" Тогда
		Результат.Вес             = 1;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Документ получен и проверяется Фондом';
										|en = 'The document is received and sent for verification by the Social Security Fund'");
	ИначеЕсли СтатусВРег = "VACATIONNEEDDOC" Тогда
		Результат.Вес             = 2;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Требуется предоставление недостающих документов';
										|en = 'Provide missing documents'");
	ИначеЕсли СтатусВРег = "VACATIONPAY" Тогда
		Результат.Вес             = 3;
		Результат.Зарегистрирован = Истина;
		Результат.Расшифровка     = НСтр("ru = 'Документ принят Фондом';
										|en = 'The document is accepted by the Social Security Fund'");	
	ИначеЕсли СтатусВРег = "VACATIONRETURN" Тогда
		Результат.Вес             = 4;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Выявлены ошибки, документ отклонен Фондом';
										|en = 'Errors are found. The document is rejected by the Social Security Fund'");
	ИначеЕсли СтатусВРег = "ERROR" Тогда
		Результат.Вес             = 5;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = НСтр("ru = 'Выявлены ошибки, документ отклонен Фондом';
										|en = 'Errors are found. The document is rejected by the Social Security Fund'");
	Иначе
		Результат.Вес             = 0;
		Результат.Зарегистрирован = Ложь;
		Результат.Расшифровка     = "";
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Процедура ЗаполнитьСписокВыбораСтатусов(ПолеВвода, Статус, ДополнительныеСведения = Ложь) Экспорт
	СписокВыбора = ПолеВвода.СписокВыбора;
	Если СписокВыбора.Количество() = 0 Тогда
		ИменаКлючей = "RECEIVED,ERROR" + ?(ДополнительныеСведения, "", ",VACATIONPAY,VACATIONRETURN,VACATIONNEEDDOC");
		Ключи = СтрРазделить(ИменаКлючей, ",");
		Для Каждого Ключ Из Ключи Цикл
			СведенияОСтатусе = СведенияОСтатусеРегистрации(Ключ);
			СписокВыбора.Добавить(Ключ, СведенияОСтатусе.Расшифровка + " (" + Ключ + ")");
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(Статус) И СписокВыбора.НайтиПоЗначению(Статус) = Неопределено Тогда
		СведенияОСтатусе = СведенияОСтатусеРегистрации(Статус);
		СписокВыбора.Добавить(Статус, СведенияОСтатусе.Расшифровка);
	КонецЕсли;
КонецПроцедуры

Функция Разделитель()
	Возврат Символы.ПС + "----" + Символы.ПС;
КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли