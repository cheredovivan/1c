#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Менеджер = "РегистрСведений.АрхивЛоговДрайверовОборудования";
	МенеджерОборудованияВызовСервераПереопределяемый.ПриЗаполненииОграниченияДоступа(Менеджер, Ограничение);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Записать состояние получения запроса
// 
// Параметры:
//  ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование
// 
Процедура ЗафиксироватьНачалоЗапросаАрхиваЛоговДрайвера(ПодключаемоеОборудование) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Запись = Набор.Добавить();
	Запись.ПодключаемоеОборудование = ПодключаемоеОборудование;
	Запись.ДатаЗапроса = ТекущаяДатаСеанса();
	Набор.Записать(Истина);
	
КонецПроцедуры

// Записать файла логирования в регистр
// 
// Параметры:
//  ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование
//  Данные - Массив Из ОписаниеПередаваемогоФайла - если файлы переданы для архивирования на сервер
//         - ДвоичныеДанные  - если фалы были заархивированы на клиенте
// 
Процедура ЗаписатьАрхивЛоговДрайвера(ПодключаемоеОборудование, Знач Данные) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	НачатьТранзакцию();
	Попытка
		Набор = СоздатьНаборЗаписей();
		Набор.Отбор.ПодключаемоеОборудование.Установить(ПодключаемоеОборудование); 
		Набор.Прочитать();
		Если Набор.Количество() > 0 Тогда
			Блокировка = Новый БлокировкаДанных();
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.АрхивЛоговДрайверовОборудования");
			ЭлементБлокировки.УстановитьЗначение("ПодключаемоеОборудование", ПодключаемоеОборудование);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			Запись = Набор[0];
		Иначе
			Запись = Набор.Добавить();
		КонецЕсли;
		
		Запись.ПодключаемоеОборудование = ПодключаемоеОборудование;
		Запись.ДатаАрхива = ТекущаяДатаСеанса();
		Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
			ДанныеАрхива = Данные;
			Запись.ЭтоАрхив = Истина;
		ИначеЕсли ТипЗнч(Данные) = Тип("Массив") Тогда
			ДанныеАрхива = Новый Массив;
			Для Каждого Файл Из Данные Цикл
				ДанныеФайла = ПолучитьИзВременногоХранилища(Файл.Хранение);
				
				ОписаниеФайла = Новый Структура;
				ОписаниеФайла.Вставить("Данные", ДанныеФайла);
				ОписаниеФайла.Вставить("Имя", Файл.Имя);
				
				ДанныеАрхива.Добавить(ОписаниеФайла);
			КонецЦикла;
			Запись.ЭтоАрхив = Ложь;
		Иначе
			ВызватьИсключение НСтр("ru = 'Некорректный тип данных';
									|en = 'Invalid data type'")
		КонецЕсли;
		
		Запись.ДанныеАрхива = Новый ХранилищеЗначения(ДанныеАрхива, Новый СжатиеДанных());
		Набор.Записать(Истина);
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначенияБПО.ЗаписатьОшибкуВЖурналРегистрации(
			ИмяСобытияВЖурналеРегистрации(),
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()),
			"Ошибка",
			Метаданные.РегистрыСведений.АрхивЛоговДрайверовОборудования);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Возвращает информацию о логах драйвера указанного подключаемого оборудования.
//
// Параметры:
//   ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - ссылка на элемент 
//   справочника подключаемого оборудования
//
// Возвращаемое значение:
//   Структура - содержит информацию о дате архива логов драйвера оборудования:
//     * ДатаАрхива - Дата - дата архива логов
//
Функция ИнформацияОЛогахДрайвера(ПодключаемоеОборудование) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		ВызватьИсключение НСтр("ru = 'Не указано подключаемое оборудование';
								|en = 'The peripherals are not specified'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаАрхива", Дата(1,1,1));
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АрхивЛоговДрайверовОборудования.ДатаАрхива КАК ДатаАрхива
		|ИЗ
		|	РегистрСведений.АрхивЛоговДрайверовОборудования КАК АрхивЛоговДрайверовОборудования
		|ГДЕ
		|	АрхивЛоговДрайверовОборудования.ПодключаемоеОборудование = &ПодключаемоеОборудование";
	
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Результат.ДатаАрхива = Выборка.ДатаАрхива;
	Возврат Результат;
	
КонецФункции

// Возвращает архив логов драйвера для указанного подключаемого оборудования.
//
// Параметры:
//   ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - 
//     ссылка на подключаемое оборудование
//
// Возвращаемое значение:
//   Структура - содержит дату архива, данные архива и признак того, что это архив:
//    * ДатаАрхива - Дата - дата архива
//    * ДанныеАрхива - ДвоичныеДанные, Неопределено - данные архива
//    * ЭтоАрхив - Булево - признак того, что это архив
//    
Функция АрхивЛоговДрайвера(ПодключаемоеОборудование) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		ВызватьИсключение НСтр("ru = 'При запросе архива логов, не указано подключаемое оборудование.';
								|en = 'The peripherals are not specified when requesting the log archive'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаАрхива", Дата(1,1,1));
	Результат.Вставить("ДанныеАрхива", Неопределено);
	Результат.Вставить("ЭтоАрхив", Ложь);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АрхивЛоговДрайверовОборудования.ДатаАрхива КАК ДатаАрхива,
		|	АрхивЛоговДрайверовОборудования.ДанныеАрхива КАК ДанныеАрхива,
		|	АрхивЛоговДрайверовОборудования.ЭтоАрхив КАК ЭтоАрхив
		|ИЗ
		|	РегистрСведений.АрхивЛоговДрайверовОборудования КАК АрхивЛоговДрайверовОборудования
		|ГДЕ
		|	АрхивЛоговДрайверовОборудования.ПодключаемоеОборудование = &ПодключаемоеОборудование";
	
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Результат;
	КонецЕсли;
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Результат.ДатаАрхива = Выборка.ДатаАрхива;
	Результат.ДанныеАрхива = Выборка.ДанныеАрхива.Получить();
	Результат.ЭтоАрхив = Выборка.ЭтоАрхив;
	Возврат Результат;
	
КонецФункции

// Очищает архив логов драйвера для указанного подключаемого оборудования.
//
// Параметры:
//   ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - 
//     ссылка на подключаемое оборудование, для которого необходимо очистить архив логов
//
Процедура ОчиститьАрхивЛоговДрайвера(ПодключаемоеОборудование) Экспорт
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		ВызватьИсключение НСтр("ru = 'При очистке архива логов, не указано подключаемое оборудование.';
								|en = 'The peripherals are not specified when clearing the log archive'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = СоздатьНаборЗаписей();
	Набор.Отбор.ПодключаемоеОборудование.Установить(ПодключаемоеОборудование); 
	Набор.Записать(Истина);
	
КонецПроцедуры

// Возвращает состояние запроса.
//
// Параметры:
//   ПодключаемоеОборудование - СправочникСсылка.ПодключаемоеОборудование - 
//     ссылка на подключаемое оборудование, для которого необходимо очистить архив логов
//
Функция СостояниеЗапроса(ПодключаемоеОборудование) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Состояние", "Отсутствует");
	Результат.Вставить("ДатаЗапроса", Дата(1,1,1));
	Результат.Вставить("ДатаАрхива", Дата(1,1,1));
	
	Если Не ЗначениеЗаполнено(ПодключаемоеОборудование) Тогда
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	АрхивЛоговДрайверовОборудования.ДатаЗапроса КАК ДатаЗапроса,
		|	АрхивЛоговДрайверовОборудования.ДатаАрхива КАК ДатаАрхива,
		|	ВЫБОР
		|		КОГДА АрхивЛоговДрайверовОборудования.ДатаАрхива = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ""Запрос""
		|		ИНАЧЕ ""Получен""
		|	КОНЕЦ КАК Состояние
		|ИЗ
		|	РегистрСведений.АрхивЛоговДрайверовОборудования КАК АрхивЛоговДрайверовОборудования
		|ГДЕ
		|	АрхивЛоговДрайверовОборудования.ПодключаемоеОборудование = &ПодключаемоеОборудование";
	
	Запрос.УстановитьПараметр("ПодключаемоеОборудование", ПодключаемоеОборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	Возврат Результат; 
	
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИмяСобытияВЖурналеРегистрации()
	Возврат НСтр("ru = 'Запись архива логов драйверов оборудования';
				|en = 'Record hardware driver log archive'");
КонецФункции

#КонецОбласти

#КонецЕсли