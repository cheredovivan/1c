#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ДокументыПоОС.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.27";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("690110a9-6ed8-4bd9-acb0-012cd670f3fa");
	Обработчик.Многопоточный = Истина;
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ДокументыПоОС.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр(
		"ru = 'Удаляет записи в регистре сведений ""Документы по ОС"" движения по документу ""Распределение НДС"".';
		|en = 'Deletes records in the ""Fixed asset documents"" information register for the ""VAT allocation"" document.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ДокументыПоОС.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ДокументыПоОС.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ДокументыПоОС";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ДокументыПоОС");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДокументыПоОС.ТипСсылки КАК ТипСсылки,
	|	ДокументыПоОС.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДокументыПоОС.НомерЗаписи КАК НомерЗаписи,
	|	ДокументыПоОС.Дата КАК Дата,
	|	ДокументыПоОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ДокументыПоОС.Организация КАК Организация,
	|	ДокументыПоОС.Подразделение КАК Подразделение,
	|	ДокументыПоОС.Ссылка КАК Ссылка,
	|	ДокументыПоОС.ЭтоИсправление КАК ЭтоИсправление,
	|	ДокументыПоОС.НачалоПериодаИсправления КАК НачалоПериодаИсправления,
	|	ДокументыПоОС.КонецПериодаИсправления КАК КонецПериодаИсправления,
	|	ДокументыПоОС.ДополнительнаяЗапись КАК ДополнительнаяЗапись
	|ИЗ
	|	РегистрСведений.ДокументыПоОС КАК ДокументыПоОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО ДокументыПоОС.ТипСсылки = ИдентификаторыОбъектовМетаданных.Ссылка
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Имя = ""РаспределениеНДС""";
	
	РегистрируемыеИзмерения = Запрос.Выполнить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, РегистрируемыеИзмерения, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ДокументыПоОС";
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	

	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);

	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр(
			"ru = 'Не удалось обработать задания по обработчику регистра сведений ""Документы по ОС"".';
			|en = 'Cannot process the jobs of the ""Fixed asset documents"" information register handler.'"));
		
	Для каждого Строка Из ОбновляемыеДанные Цикл
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДокументыПоОС");
		ЭлементБлокировки.УстановитьЗначение("ТипСсылки", Строка.ТипСсылки);
		ЭлементБлокировки.УстановитьЗначение("ХозяйственнаяОперация", Строка.ХозяйственнаяОперация);
		ЭлементБлокировки.УстановитьЗначение("НомерЗаписи", Строка.НомерЗаписи);
		ЭлементБлокировки.УстановитьЗначение("Дата", Строка.Дата);
		ЭлементБлокировки.УстановитьЗначение("ОсновноеСредство", Строка.ОсновноеСредство);
		ЭлементБлокировки.УстановитьЗначение("Организация", Строка.Организация);
		ЭлементБлокировки.УстановитьЗначение("Подразделение", Строка.Подразделение);
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Строка.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ЭтоИсправление", Строка.ЭтоИсправление);
		ЭлементБлокировки.УстановитьЗначение("НачалоПериодаИсправления", Строка.НачалоПериодаИсправления);
		ЭлементБлокировки.УстановитьЗначение("КонецПериодаИсправления", Строка.КонецПериодаИсправления);
		ЭлементБлокировки.УстановитьЗначение("ДополнительнаяЗапись", Строка.ДополнительнаяЗапись);
		
		Блокировка.Заблокировать();
				
		Набор = РегистрыСведений.ДокументыПоОС.СоздатьНаборЗаписей();
		Набор.Отбор.ТипСсылки.Установить(Строка.ТипСсылки);
		Набор.Отбор.ХозяйственнаяОперация.Установить(Строка.ХозяйственнаяОперация);
		Набор.Отбор.НомерЗаписи.Установить(Строка.НомерЗаписи);
		Набор.Отбор.Дата.Установить(Строка.Дата);
		Набор.Отбор.ОсновноеСредство.Установить(Строка.ОсновноеСредство);
		Набор.Отбор.Организация.Установить(Строка.Организация);
		Набор.Отбор.Подразделение.Установить(Строка.Подразделение);
		Набор.Отбор.Ссылка.Установить(Строка.Ссылка);
		Набор.Отбор.ЭтоИсправление.Установить(Строка.ЭтоИсправление);
		Набор.Отбор.НачалоПериодаИсправления.Установить(Строка.НачалоПериодаИсправления);
		Набор.Отбор.КонецПериодаИсправления.Установить(Строка.КонецПериодаИсправления);
		Набор.Отбор.ДополнительнаяЗапись.Установить(Строка.ДополнительнаяЗапись);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		
		ЗафиксироватьТранзакцию();

	Исключение
		
		ОтменитьТранзакцию();
		
		ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Набор); 
		
	КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = 
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
