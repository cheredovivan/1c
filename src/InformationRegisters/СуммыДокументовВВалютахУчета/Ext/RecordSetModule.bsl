#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчередиПриОбмене();
		Возврат;
	КонецЕсли;
	
	Если НЕ ДополнительныеСвойства.Свойство("СвойстваДокумента") Тогда
		ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(ЭтотОбъект);
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		// Текущее состояние набора помещается во временную таблицу,
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор,
		|	СуммыДокументовВВалютахУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДС КАК СуммаБезНДС,
		|	СуммыДокументовВВалютахУчета.СуммаНДС КАК СуммаНДС,
		|	СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.Период КАК Период,
		|	СуммыДокументовВВалютахУчета.СтавкаНДС КАК СтавкаНДС,
		|	СуммыДокументовВВалютахУчета.ОбъектРасчетов КАК ОбъектРасчетов,
		|	СуммыДокументовВВалютахУчета.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
		|	СуммыДокументовВВалютахУчета.БазаНДСРегл КАК БазаНДСРегл,
		|	СуммыДокументовВВалютахУчета.СуммаНДСРегл КАК СуммаНДСРегл,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
		|	СуммыДокументовВВалютахУчета.СуммаНДСУпр КАК СуммаНДСУпр,
		|	СуммыДокументовВВалютахУчета.СуммаПриведенная КАК СуммаПриведенная,
		|	СуммыДокументовВВалютахУчета.СуммаПриведеннаяРегл КАК СуммаПриведеннаяРегл,
		|	СуммыДокументовВВалютахУчета.СуммаПриведеннаяУпр КАК СуммаПриведеннаяУпр,
		|	СуммыДокументовВВалютахУчета.Валюта КАК Валюта,
		|	СуммыДокументовВВалютахУчета.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.ПериодБазыНДС КАК ПериодБазыНДС,
		|	СуммыДокументовВВалютахУчета.ОтражаетсяВРасчетах КАК ОтражаетсяВРасчетах,
		|	СуммыДокументовВВалютахУчета.ТипРасчетов КАК ТипРасчетов
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаПередЗаписью
		|ИЗ
		|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|ГДЕ
		|	СуммыДокументовВВалютахУчета.Регистратор = &Регистратор
		|	И НЕ &ЭтоНовый
		|	И НЕ &ОбменДанными
		|");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Если ДополнительныеСвойства.Свойство("СвойстваДокумента") Тогда
			Запрос.УстановитьПараметр("ЭтоНовый", ДополнительныеСвойства.СвойстваДокумента.ЭтоНовый);
		Иначе
			Запрос.УстановитьПараметр("ЭтоНовый", Ложь);
		КонецЕсли;
		Если ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
			Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Иначе
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
		КонецЕсли;
		Запрос.УстановитьПараметр("ОбменДанными", ОбменДанными.Загрузка);

		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	// Проверка ОбменДанными.Загрузка не требуется, т.к. данный объект в РИБ при записи должен создавать задания.
	НоваяАрхитектураВзаиморасчетов = ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов");
	
	СоздатьЗаданияНеоперативнойОчередиПриЗаписи();
	
	Если Не ПроведениеДокументов.КонтролироватьИзменения(ДополнительныеСвойства) Тогда
		Возврат;
	КонецЕсли;
	
	Если НоваяАрхитектураВзаиморасчетов Тогда
		
		Запрос = Новый Запрос;
		
		МассивТекстовЗапроса = Новый Массив;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	Таблица.Период КАК Период,
		|	Таблица.Регистратор КАК Регистратор,
		|	Таблица.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	Таблица.СтавкаНДС КАК СтавкаНДС,
		|	Таблица.ОбъектРасчетов КАК ОбъектРасчетов,
		|	Таблица.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов,
		|	&СоздаватьЗаданияКЗакрытиюСледующегоМесяца КАК СоздаватьЗаданияКЗакрытиюСледующегоМесяца
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаИзменения
		|ИЗ
		|	(ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаБезНДС КАК СуммаБезНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДС КАК СуммаНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Период КАК Период,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СтавкаНДС КАК СтавкаНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ОбъектРасчетов КАК ОбъектРасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов
		|	ИЗ
		|		СуммыДокументовВВалютахУчетаПередЗаписью КАК СуммыДокументовВВалютахУчетаПередЗаписью
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчета.Регистратор,
		|		СуммыДокументовВВалютахУчета.ИдентификаторСтроки,
		|		-СуммыДокументовВВалютахУчета.СуммаБезНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСВзаиморасчетов,
		|		СуммыДокументовВВалютахУчета.Период,
		|		СуммыДокументовВВалютахУчета.СтавкаНДС,
		|		СуммыДокументовВВалютахУчета.ОбъектРасчетов,
		|		СуммыДокументовВВалютахУчета.ПересчитыватьПоДаннымРасчетов
		|	ИЗ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|	ГДЕ
		|		СуммыДокументовВВалютахУчета.Регистратор = &Регистратор) КАК Таблица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
		|		ПО Таблица.Регистратор = ДанныеПервичныхДокументов.Документ
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.ИдентификаторСтроки,
		|	Таблица.Регистратор,
		|	Таблица.ОбъектРасчетов,
		|	Таблица.Период,
		|	Таблица.СтавкаНДС,
		|	Таблица.ПересчитыватьПоДаннымРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаБезНДС) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДС) <> 0
		|	ИЛИ СУММА(Таблица.СуммаВзаиморасчетов) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДСВзаиморасчетов) <> 0
		|";
		МассивТекстовЗапроса.Добавить(ТекстЗапроса);
		
		МассивТекстовЗапроса.Добавить("УНИЧТОЖИТЬ СуммыДокументовВВалютахУчетаПередЗаписью");
		
		Запрос.Текст = СтрСоединить(МассивТекстовЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("СоздаватьЗаданияКЗакрытиюСледующегоМесяца", ТипЗнч(Отбор.Регистратор.Значение) = Тип("ДокументСсылка.ВводОстатковВзаиморасчетов"));
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		
		Запрос.ВыполнитьПакет();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область НеоперативнаяОчередьЗаданий

Процедура ПодготовитьДанныеДляРегистрацииЗаданийНеоперативнойОчередиПриОбмене()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТипРегистратора = ТипЗнч(Отбор.Регистратор.Значение);
	Если ОперативныеВзаиморасчетыСервер.СписокТиповРегистраторовПланов().НайтиПоЗначению(ТипРегистратора) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ
		|	СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор,
		|	СуммыДокументовВВалютахУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДС КАК СуммаБезНДС,
		|	СуммыДокументовВВалютахУчета.СуммаНДС КАК СуммаНДС,
		|	СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.Период КАК Период,
		|	СуммыДокументовВВалютахУчета.СтавкаНДС КАК СтавкаНДС,
		|	СуммыДокументовВВалютахУчета.ОбъектРасчетов КАК ОбъектРасчетов,
		|	СуммыДокументовВВалютахУчета.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
		|	СуммыДокументовВВалютахУчета.БазаНДСРегл КАК БазаНДСРегл,
		|	СуммыДокументовВВалютахУчета.СуммаНДСРегл КАК СуммаНДСРегл,
		|	СуммыДокументовВВалютахУчета.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
		|	СуммыДокументовВВалютахУчета.СуммаНДСУпр КАК СуммаНДСУпр,
		|	СуммыДокументовВВалютахУчета.СуммаПриведенная КАК СуммаПриведенная,
		|	СуммыДокументовВВалютахУчета.СуммаПриведеннаяРегл КАК СуммаПриведеннаяРегл,
		|	СуммыДокументовВВалютахУчета.СуммаПриведеннаяУпр КАК СуммаПриведеннаяУпр,
		|	СуммыДокументовВВалютахУчета.Валюта КАК Валюта,
		|	СуммыДокументовВВалютахУчета.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|	СуммыДокументовВВалютахУчета.ПериодБазыНДС КАК ПериодБазыНДС,
		|	СуммыДокументовВВалютахУчета.ОтражаетсяВРасчетах КАК ОтражаетсяВРасчетах,
		|	СуммыДокументовВВалютахУчета.ТипРасчетов КАК ТипРасчетов
		|ПОМЕСТИТЬ СуммыДокументовВВалютахУчетаПередЗаписью
		|ИЗ
		|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|ГДЕ
		|	СуммыДокументовВВалютахУчета.Регистратор = &Регистратор
		|");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Если ДополнительныеСвойства.Свойство("МенеджерВременныхТаблиц") Тогда
			Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Иначе
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			ДополнительныеСвойства.Вставить("МенеджерВременныхТаблиц", Запрос.МенеджерВременныхТаблиц);
		КонецЕсли;

		Запрос.Выполнить();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияНеоперативнойОчередиПриЗаписи()
	
	Если Не НеоперативнаяОчередьЗаданий.ДоступноСозданиеЗаданийНеоперативнойОчереди(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ТипРегистратора = ТипЗнч(Отбор.Регистратор.Значение);
	Если ОперативныеВзаиморасчетыСервер.СписокТиповРегистраторовПланов().НайтиПоЗначению(ТипРегистратора) <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов") Тогда
		
		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = ДополнительныеСвойства.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Регистратор КАК Регистратор,
		|	Таблица.Период КАК Период
		|ПОМЕСТИТЬ ОчередьДанныеКДопроведениюДокументов
		|ИЗ
		|	(ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаБезНДС КАК СуммаБезНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДС КАК СуммаНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Период КАК Период,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СтавкаНДС КАК СтавкаНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ОбъектРасчетов КАК ОбъектРасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.БазаНДСРегл КАК БазаНДСРегл,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСРегл КАК СуммаНДСРегл,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаНДСУпр КАК СуммаНДСУпр,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаПриведенная КАК СуммаПриведенная,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаПриведеннаяРегл КАК СуммаПриведеннаяРегл,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.СуммаПриведеннаяУпр КАК СуммаПриведеннаяУпр,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.Валюта КАК Валюта,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ПериодБазыНДС КАК ПериодБазыНДС,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ОтражаетсяВРасчетах КАК ОтражаетсяВРасчетах,
		|		СуммыДокументовВВалютахУчетаПередЗаписью.ТипРасчетов КАК ТипРасчетов
		|		
		|	ИЗ
		|		СуммыДокументовВВалютахУчетаПередЗаписью КАК СуммыДокументовВВалютахУчетаПередЗаписью
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		СуммыДокументовВВалютахУчета.Регистратор КАК Регистратор,
		|		СуммыДокументовВВалютахУчета.ИдентификаторСтроки КАК ИдентификаторСтроки,
		|		-СуммыДокументовВВалютахУчета.СуммаБезНДС КАК СуммаБезНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаНДС КАК СуммаНДС,
		|		-СуммыДокументовВВалютахУчета.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|		СуммыДокументовВВалютахУчета.Период КАК Период,
		|		СуммыДокументовВВалютахУчета.СтавкаНДС КАК СтавкаНДС,
		|		СуммыДокументовВВалютахУчета.ОбъектРасчетов КАК ОбъектРасчетов,
		|		СуммыДокументовВВалютахУчета.ПересчитыватьПоДаннымРасчетов КАК ПересчитыватьПоДаннымРасчетов,
		|		-СуммыДокументовВВалютахУчета.СуммаБезНДСРегл КАК СуммаБезНДСРегл,
		|		-СуммыДокументовВВалютахУчета.БазаНДСРегл КАК БазаНДСРегл,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСРегл КАК СуммаНДСРегл,
		|		-СуммыДокументовВВалютахУчета.СуммаБезНДСУпр КАК СуммаБезНДСУпр,
		|		-СуммыДокументовВВалютахУчета.СуммаНДСУпр КАК СуммаНДСУпр,
		|		-СуммыДокументовВВалютахУчета.СуммаПриведенная КАК СуммаПриведенная,
		|		-СуммыДокументовВВалютахУчета.СуммаПриведеннаяРегл КАК СуммаПриведеннаяРегл,
		|		-СуммыДокументовВВалютахУчета.СуммаПриведеннаяУпр КАК СуммаПриведеннаяУпр,
		|		СуммыДокументовВВалютахУчета.Валюта КАК Валюта,
		|		СуммыДокументовВВалютахУчета.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
		|		СуммыДокументовВВалютахУчета.ПериодБазыНДС КАК ПериодБазыНДС,
		|		СуммыДокументовВВалютахУчета.ОтражаетсяВРасчетах КАК ОтражаетсяВРасчетах,
		|		СуммыДокументовВВалютахУчета.ТипРасчетов КАК ТипРасчетов
		|	ИЗ
		|		РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
		|	ГДЕ
		|		СуммыДокументовВВалютахУчета.Регистратор = &Регистратор) КАК Таблица
		|
		|СГРУППИРОВАТЬ ПО
		|	Таблица.Регистратор,
		|	Таблица.ИдентификаторСтроки,
		|	Таблица.Период,
		|	Таблица.СтавкаНДС,
		|	Таблица.ОбъектРасчетов,
		|	Таблица.ПересчитыватьПоДаннымРасчетов,
		|	Таблица.Валюта,
		|	Таблица.ВалютаВзаиморасчетов,
		|	Таблица.ПериодБазыНДС,
		|	Таблица.ОтражаетсяВРасчетах,
		|	Таблица.ТипРасчетов
		|
		|ИМЕЮЩИЕ
		|	СУММА(Таблица.СуммаБезНДС) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДС) <> 0
		|	ИЛИ СУММА(Таблица.СуммаБезНДСРегл) <> 0
		|	ИЛИ СУММА(Таблица.БазаНДСРегл) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДСРегл) <> 0
		|	ИЛИ СУММА(Таблица.СуммаБезНДСУпр) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДСУпр) <> 0
		|	ИЛИ СУММА(Таблица.СуммаВзаиморасчетов) <> 0
		|	ИЛИ СУММА(Таблица.СуммаНДСВзаиморасчетов) <> 0
		|	ИЛИ СУММА(Таблица.СуммаПриведенная) <> 0
		|	ИЛИ СУММА(Таблица.СуммаПриведеннаяРегл) <> 0
		|	ИЛИ СУММА(Таблица.СуммаПриведеннаяУпр) <> 0
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Регистратор
		|";

		Запрос.ВыполнитьПакет();

		НеоперативнаяОчередьЗаданий.СоздатьЗаданияНеоперативнойОчередиПриЗаписиНабораЗаписей(ЭтотОбъект,
				Запрос.МенеджерВременныхТаблиц);
		
		ОбщегоНазначенияУТ.УничтожитьВременныеТаблицы(Запрос.МенеджерВременныхТаблиц,
			"ОчередьДанныеКДопроведениюДокументов");

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
