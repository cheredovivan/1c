#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	ПравоИзменения = ПравоДоступа("Изменение", Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров);
	Элементы.ПодакцизныеКатегорииНоменклатурыДобавить.Видимость = ПравоИзменения;
	Элементы.ПодакцизныеКатегорииНоменклатурыУдалить.Видимость = ПравоИзменения;
	Элементы.ПодакцизныеКатегорииНоменклатурыРедактировать.Видимость = ПравоИзменения;
	
	Номенклатура = Параметры.Номенклатура;
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") Тогда
		Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
	КонецЕсли;
	
	АвтоЗаголовок = Ложь;
	ЕдиницаИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения");
	ТексЗаголовка = НСтр("ru = 'Категории подакцизного товара ""%1"" (%2)';
						|en = '""%1"" (%2) excisable goods categories'");
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТексЗаголовка,
		СокрЛП(Строка(Номенклатура)), СокрЛП(Строка(ЕдиницаИзмеренияНоменклатуры)));
	
	Если Параметры.ТекущийПериод Тогда
		Период = ТекущаяДатаСеанса();
		Элементы.Период.ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ПрочитатьКатегорииНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПодакцизнаяКатегорияНоменклатуры"
		И Параметр.Номенклатура = Номенклатура И Источник = ЭтотОбъект Тогда
		СтруктураПоиска = Новый Структура("Период, Страна");
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Параметр);
		ПрочитатьКатегорииНоменклатуры(СтруктураПоиска);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	ПрочитатьКатегорииНоменклатуры();
КонецПроцедуры

&НаКлиенте
Процедура СтранаПриИзменении(Элемент)
	ПрочитатьКатегорииНоменклатуры();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодакцизныеКатегорииНоменклатуры

&НаКлиенте
Процедура ПодакцизныеКатегорииНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные =  ПодакцизныеКатегорииНоменклатуры.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ОткрытьФормуРедактирования(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Удалить(Команда)
	
	ТекущаяСтрока = Элементы.ПодакцизныеКатегорииНоменклатуры.ТекущиеДанные;
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	Иначе
		ЗавершениеУдаления = Новый ОписаниеОповещения("ЗавершитьУдалениеЗаписи",
			ЭтотОбъект, ТекущаяСтрока);
			
		ПоказатьВопрос(ЗавершениеУдаления,
			НСтр("ru = 'Удалить выбранную запись?';
				|en = 'Delete the selected record?'"),
			РежимДиалогаВопрос.ДаНет,
			,
			КодВозвратаДиалога.Нет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	ПрочитатьКатегорииНоменклатуры();
КонецПроцедуры

&НаКлиенте
Процедура Добавить(Команда)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Номенклатура", Номенклатура);
	Если ЗначениеЗаполнено(Страна) Тогда
		Отбор.Вставить("Страна", Страна);
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Отбор);
	
	ОткрытьФорму("РегистрСведений.КатегорииПодакцизныхТоваров.Форма.ФормаЗаписи", 
				ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура Редактировать(Команда)
	
	Если Элементы.ПодакцизныеКатегорииНоменклатуры.ТекущиеДанные <> Неопределено Тогда
		ОткрытьФормуРедактирования(Элементы.ПодакцизныеКатегорииНоменклатуры.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыСтавка.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыЕдиницаИзмеренияКатегории.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыКоэффициентПересчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.ТипСтавки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Адвалорная");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не используется>';
																|en = '<not used>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыВалюта.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.ТипСтавки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Адвалорная");
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыКатегорияПодакцизногоТовара.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.ТипСтавки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.КатегорияПодакцизногоТовара");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не задана>';
																|en = '<not set>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыКоэффициентПересчета.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.ТипСтавки");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПредопределенноеЗначение("Перечисление.ТипыСтавокПодакцизнойКатегории.Твердая");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодакцизныеКатегорииНоменклатуры.КоэффициентПересчета");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не задан>';
																|en = '<not specified>'"));
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	//
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыСтавка.Имя);
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПодакцизныеКатегорииНоменклатурыВалюта.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Период");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьКатегорииНоменклатуры(ПараметрыОтбора = Неопределено)
	
	ТаблицаАкцизов = УчетПодакцизныхТоваров.ПолучитьИсториюКатегорийНоменклатуры(Номенклатура, Страна, Период);
	ПодакцизныеКатегорииНоменклатуры.Загрузить(ТаблицаАкцизов);
	Если ПараметрыОтбора <> Неопределено Тогда
		СтрокиТаблицы = ПодакцизныеКатегорииНоменклатуры.НайтиСтроки(ПараметрыОтбора);
		Если ЗначениеЗаполнено(СтрокиТаблицы) Тогда
			ИдентификаторСтроки = СтрокиТаблицы[0].ПолучитьИдентификатор();
			Элементы.ПодакцизныеКатегорииНоменклатуры.ТекущаяСтрока = ИдентификаторСтроки;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Завершает удаление записи.
//
// Параметры:
//	Результат - КодВозвратаДиалога - Возвращаемый результат ответа;
//	ТекущаяСтрока - ДанныеФормыЭлементКоллекции - Удаляемая строка записи.
//
&НаКлиенте
Процедура ЗавершитьУдалениеЗаписи(Результат, ТекущаяСтрока) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ИдентификаторЗаписи = ТекущаяСтрока.ПолучитьИдентификатор();
		УдалитьЗаписьСервер(ИдентификаторЗаписи);
		ПараметрыОповещения = Новый Структура("Номенклатура", Номенклатура);
		Оповестить("Запись_ПодакцизнаяКатегорияНоменклатуры", ПараметрыОповещения, ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УдалитьЗаписьСервер(ИдентификаторЗаписи)
	
	Запись = ПодакцизныеКатегорииНоменклатуры.НайтиПоИдентификатору(ИдентификаторЗаписи);
	РегистрыСведений.КатегорииПодакцизныхТоваров.УдалитьЗапись(Запись.Период, Номенклатура, Запись.Страна);
	
	ПрочитатьКатегорииНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактирования(ТекущиеДанные)
	
	Отбор = Новый Структура();
	Отбор.Вставить("Номенклатура", Номенклатура);
	Отбор.Вставить("Страна", ТекущиеДанные.Страна);
	Отбор.Вставить("Период", ТекущиеДанные.Период);
	
	ПараметрыФормы = Новый Структура;
	
	МассивОтбор = Новый Массив;
	МассивОтбор.Добавить(Отбор);
	КлючЗаписи = Новый("РегистрСведенийКлючЗаписи.КатегорииПодакцизныхТоваров", МассивОтбор);
	ПараметрыФормы.Вставить("Ключ", КлючЗаписи);
	
	ОткрытьФорму("РегистрСведений.КатегорииПодакцизныхТоваров.Форма.ФормаЗаписи", 
				ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры;

#КонецОбласти
