#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если Не ЭтоБазоваяВерсия Тогда
		Элементы.ТипКатегории.ОтображениеПредупрежденияПриРедактировании =
			ОтображениеПредупрежденияПриРедактировании.Отображать;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТипКатегории = ?(Запись.ПодакцизныйТовар, 1, 0);
	ИспользоватьМногострановойУчет = ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет");
	ЭтоБазоваяВерсия = ПолучитьФункциональнуюОпцию("БазоваяВерсия");
	УстановитьВидимостьИПредставлениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("Запись_ПодакцизнаяКатегорияНоменклатуры", Запись, ВладелецФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПодакцизнаяКатегория"
		И Источник = Запись.КатегорияПодакцизногоТовара Тогда
		УстановитьВидимостьИПредставлениеЭлементов();
		Оповестить("Запись_ПодакцизнаяКатегорияНоменклатуры", Запись, ВладелецФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Запись.ПодакцизныйТовар
	И ЭтоТвердаяСтавка
	И Не ЗначениеЗаполнено(Запись.КоэффициентПересчета) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не заполнено значение коэффициента пересчета';
				|en = 'Conversion ratio value is required'"),
			,
			"Запись.КоэффициентПересчета",
			,
			Отказ);
	КонецЕсли;
	
	Если ЕстьДругаяЗапись() Тогда
		Если ИспользоватьМногострановойУчет Тогда
			Сообщение = Нстр("ru = 'Для номенклатуры ""%1"" по стране ""%2"" на дату %3 уже заданы настройки подакцизной категории.';
							|en = 'The excise category settings are already set for the ""%1"" item for the ""%2"" country as of %3.'");
			Сообщение = СтрШаблон(Сообщение, Запись.Номенклатура, Запись.Страна, Формат(Запись.Период, "ДЛФ=D"));
		Иначе
			Сообщение = Нстр("ru = 'Для номенклатуры ""%1"" на дату %2 уже заданы настройки подакцизной категории.';
							|en = 'The excise category settings are already set for the ""%1"" item as of %2.'");
			Сообщение = СтрШаблон(Сообщение, Запись.Номенклатура, Формат(Запись.Период, "ДЛФ=D"));
		КонецЕсли;
		
		ПоказатьПредупреждение(,Сообщение);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КатегорияПодакцизногоТовараПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Запись.КатегорияПодакцизногоТовара) Тогда
		Запись.КоэффициентПересчета = 0;
	КонецЕсли;

	УстановитьВидимостьИПредставлениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПодакцизныйТоварПриИзменении(Элемент)
	
	Если Не Запись.ПодакцизныйТовар 
		И ЗначениеЗаполнено(Запись.КатегорияПодакцизногоТовара) Тогда
		Запись.КатегорияПодакцизногоТовара = Неопределено;
		Запись.КоэффициентПересчета = 0;
	КонецЕсли;
	
	УстановитьВидимостьИПредставлениеЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ТипКатегорииПриИзменении(Элемент)
	
	Если ТипКатегории = 0 Тогда
		Запись.ПодакцизныйТовар = Ложь;
	Иначе
		Запись.ПодакцизныйТовар = Истина;
	КонецЕсли;

	Если Не Запись.ПодакцизныйТовар 
		И ЗначениеЗаполнено(Запись.КатегорияПодакцизногоТовара) Тогда
		Запись.КатегорияПодакцизногоТовара = Неопределено;
		Запись.КоэффициентПересчета = 0;
	КонецЕсли;
	
	УстановитьВидимостьИПредставлениеЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции 

&НаСервере
Процедура УстановитьВидимостьИПредставлениеЭлементов()
	
	Если ЗначениеЗаполнено(Запись.Номенклатура) Тогда
		Элементы.Номенклатура.ТолькоПросмотр = Истина;
	КонецЕсли;

	Если Не ИспользоватьМногострановойУчет Тогда
		Элементы.Страна.Видимость = Ложь;
	КонецЕсли;

	ЭтоТвердаяСтавка = Ложь;
	ЕдиницыИзмеренияОтличаются = Ложь;
	
	Если Запись.ПодакцизныйТовар И Не ЭтоБазоваяВерсия Тогда
		Элементы.КатегорияПодакцизногоТовара.Доступность = Истина;
		
		РеквизитыКатегории = Неопределено;
		
		Если ЗначениеЗаполнено(Запись.КатегорияПодакцизногоТовара) Тогда
			
			РеквизитыКатегории = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Запись.КатегорияПодакцизногоТовара, "ТипСтавки, ЕдиницаИзмерения");
			ЭтоТвердаяСтавка = РеквизитыКатегории.ТипСтавки = Перечисления.ТипыСтавокПодакцизнойКатегории.Твердая;
			
			Если ЭтоТвердаяСтавка
				И ЗначениеЗаполнено(Запись.Номенклатура) Тогда
				Элементы.КатегорияПодакцизногоТовараЕдиницаИзмеренияСпр.Видимость = Истина;
				ЕдиницаИзмеренияНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.Номенклатура, "ЕдиницаИзмерения");
				
				Если ЕдиницаИзмеренияНоменклатуры <> РеквизитыКатегории.ЕдиницаИзмерения Тогда
					ЕдиницыИзмеренияОтличаются = Истина;
					ТекстЗаголовка = НСтр("ru = 'В 1 %1.';
											|en = 'In 1 %1.'");
					ТекстЗаголовка = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстЗаголовка, СокрЛП(Строка(ЕдиницаИзмеренияНоменклатуры)));
					Элементы.КоэффициентПересчета.Заголовок = ТекстЗаголовка;
				Иначе
					Запись.КоэффициентПересчета = 1;
				КонецЕсли;
			ИначеЕсли Запись.КоэффициентПересчета <> 0 Тогда
				Запись.КоэффициентПересчета = 0;
			КонецЕсли;
			
		КонецЕсли;
				
	Иначе
		Элементы.КатегорияПодакцизногоТовара.Доступность = Ложь;
	КонецЕсли;
	
	Если ЭтоБазоваяВерсия Тогда
		Элементы.КатегорияПодакцизногоТовара.Видимость = Ложь;
	КонецЕсли;

	Элементы.ГруппаЕдиницыИзмерения.Видимость = ЭтоТвердаяСтавка И ЕдиницыИзмеренияОтличаются;
	Элементы.КоэффициентПересчета.Доступность = ЭтоТвердаяСтавка;
	Элементы.КатегорияПодакцизногоТовараЕдиницаИзмеренияСпр.Видимость = ЭтоТвердаяСтавка;

КонецПроцедуры

&НаСервере
Функция ЕстьДругаяЗапись()
	
	Результат = Ложь;
	
	Если Не (Запись.Период = Запись.ИсходныйКлючЗаписи.Период
		И Запись.Номенклатура = Запись.ИсходныйКлючЗаписи.Номенклатура
		И Запись.Страна = Запись.Страна) Тогда
			
		ИскомаяЗапись = РегистрыСведений.КатегорииПодакцизныхТоваров.СоздатьМенеджерЗаписи();
		ИскомаяЗапись.Период = Запись.Период;
		ИскомаяЗапись.Номенклатура = Запись.Номенклатура;
		ИскомаяЗапись.Страна = Запись.Страна;
		ИскомаяЗапись.Прочитать();
		Результат = ИскомаяЗапись.Выбран();
		
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

#КонецОбласти
