#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Устанавливает подакцизную категорию для указанной номенклатуры на дату по стране.
// Если товар неподакцизный, то устанавливается признак подакцизного товара.
//
// Параметры:
//  ПараметрыЗаполнения - структура:
//     * СписокНоменклатуры - Массив из СправочникСсылка.Номенклатура - список номенклатуры или адрес временного хранилища содержащего номенклатуру;
//     * Период - Дата - дата начала действия указанной категории для номенклатуры;
//     * Страна - СправочникСсылка.СтраныМира - страна действия категории;
//     * КатегорияПодакцизногоТовара - СправочникСсылка.КатегорииПодакцизныхТоваров - подакцизная категория.
//
Процедура УстановитьПодакцизнуюКатегориюНоменклатуры(ПараметрыЗаполнения) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.СписокНоменклатуры)
		И ТипЗнч(ПараметрыЗаполнения.СписокНоменклатуры) = Тип("Массив") Тогда
		
		Для Каждого ТекущаяНоменклатура Из ПараметрыЗаполнения.СписокНоменклатуры Цикл
			ДобавитьЗаписьВРегистр(ПараметрыЗаполнения.Период,
				ТекущаяНоменклатура,
				ПараметрыЗаполнения.Страна,
				ПараметрыЗаполнения.КатегорияПодакцизногоТовара,
				0);
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает коэффициент пересчета из единицы хранения номенклатуру в единицу измерения подакцизной категории.
//
// Параметры:
//  ПараметрыЗаполнения - структура:
//     * СписокНоменклатуры - Массив из Структура - список номенклатуры или адрес временного хранилища содержащего номенклатуру:
//        * АвтоКоэффициентПересчета - Число - коэффициент автопересчета единиц измерения;
//        * Номенклатура - СправочникСсылка.Номенклатура - номенклатура, для которой необходимо установить коэффициент;
//     * АвтоЗаполнениеКоэффициентаПересчета - Булево - признак авторасчета коэффициента пересчета;
//     * Период - Дата - дата начала действия указанной категории для номенклатуры;
//     * Страна - СправочникСсылка.СтраныМира - страна действия категории;
//     * КоэффициентПересчета - Число - введенный пользователем коэффициент пересчета.
//
Процедура УстановитьКоэффициентПересчетаПоСписку(ПараметрыЗаполнения) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыЗаполнения.СписокНоменклатуры)
		И ТипЗнч(ПараметрыЗаполнения.СписокНоменклатуры) = Тип("Массив") Тогда
		
		Для Каждого ТекущаяСтрока Из ПараметрыЗаполнения.СписокНоменклатуры Цикл
			Если ПараметрыЗаполнения.АвтоЗаполнениеКоэффициентаПересчета Тогда
				КоэффициентПересчета = ТекущаяСтрока.АвтоКоэффициентПересчета;
			Иначе
				КоэффициентПересчета = ПараметрыЗаполнения.КоэффициентПересчета;
			КонецЕсли;
			ДобавитьЗаписьВРегистр(ПараметрыЗаполнения.Период,
				ТекущаяСтрока.Номенклатура,
				ПараметрыЗаполнения.Страна,
				ТекущаяСтрока.КатегорияПодакцизногоТовара,
				КоэффициентПересчета);
		КонецЦикла;
			
	КонецЕсли;
	
КонецПроцедуры

// Определяет по массиву номенклатуры для указанной страны количество подакцизных товаров.
// 
// Параметры:
//  Номенклатура - Массив из СправочникСсылка.Номенклатура - 
//  Страна - СправочникСсылка.СтраныМира - 
//  Период - Дата, Неопределено - Период, на который необходимо определить принадлежность номенклатуры
//  							к подакцизной категории.
// 
// Возвращаемое значение:
//  Число - 
Функция КоличествоПодакцизныхТоваров(Номенклатура, Страна, Период = Неопределено) Экспорт
	
	КоличествоПодакцизныхТоваров = 0;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
	
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
		Запрос.УстановитьПараметр("Страна", Страна);
		Запрос.УстановитьПараметр("Период", Период);
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ЕСТЬNULL(КОЛИЧЕСТВО(*), 0) КАК КоличествоПодакцизныхТоваров
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров.СрезПоследних(&Период, Номенклатура В (&Номенклатура)
		|	И Страна = &Страна) КАК КатегорииПодакцизныхТоваровСрезПоследних
		|ГДЕ
		|	КатегорииПодакцизныхТоваровСрезПоследних.ПодакцизныйТовар";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			Выборка = РезультатЗапроса.Выбрать();
			Выборка.Следующий();
			КоличествоПодакцизныхТоваров = Выборка.КоличествоПодакцизныхТоваров;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат КоличествоПодакцизныхТоваров;
	
КонецФункции

// Сбрасывает подакцизную категорию и признак подакцизного товара для указанной номенклатуры на дату по стране.
//
// Параметры:
//  ПараметрыЗаполнения - структура:
//     * СписокНоменклатуры - Массив из СправочникСсылка.Номенклатура - список номенклатуры или адрес временного хранилища содержащего номенклатуру;
//     * Период - Дата - дата, с которой товар становится неподакцизным;
//     * Страна - СправочникСсылка.СтраныМира - страна, для который товар перестает быть подакцизным.
//
Процедура ОчиститьПодакцизныйПризнакНоменклатуры(ПараметрыЗаполнения) Экспорт

	Если ЗначениеЗаполнено(ПараметрыЗаполнения.СписокНоменклатуры)
		И ТипЗнч(ПараметрыЗаполнения.СписокНоменклатуры) = Тип("Массив") Тогда
			
		Для Каждого ТекущаяНоменклатура Из ПараметрыЗаполнения.СписокНоменклатуры Цикл
			ДобавитьЗаписьВРегистр(ПараметрыЗаполнения.Период,
				ТекущаяНоменклатура,
				ПараметрыЗаполнения.Страна,
				Неопределено,
				0);
		КонецЦикла;
			
	КонецЕсли;

КонецПроцедуры

// Загружает признак подакцизного товара.
//
// Параметры:
//  Номенклатура - СправочникСсылка.Номенклатура - номенклатура;
//  Страна - СправочникСсылка.СтраныМира - страна действия подакцизного признака;
//  ПодакцизныйТовар - Булево - признак подакцизности товара.
//
Процедура ЗагрузитьПризнакПодакцизногоТовара(Номенклатура, Страна, ПодакцизныйТовар) Экспорт
	
	Период = ТекущаяДатаСеанса();
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров.ПолноеИмя();
	
	НачатьТранзакцию();
	
	Попытка
		
		ОтборПоПараметрам = Новый Структура();
		ОтборПоПараметрам.Вставить("Номенклатура", Номенклатура);
		ОтборПоПараметрам.Вставить("Страна", Страна);
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
		ЭлементБлокировки.УстановитьЗначение("Номенклатура", Номенклатура);
		ЭлементБлокировки.УстановитьЗначение("Страна", Страна);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		ПоследниеДанные = РегистрыСведений.КатегорииПодакцизныхТоваров.ПолучитьПоследнее(Период, ОтборПоПараметрам);
		
		Если ПоследниеДанные.ПодакцизныйТовар <> ПодакцизныйТовар Тогда
			МенеджерЗаписи = СоздатьМенеджерЗаписи();
			МенеджерЗаписи.Номенклатура = Номенклатура;
			МенеджерЗаписи.Период = Период;
			МенеджерЗаписи.Страна = Страна;
			МенеджерЗаписи.ПодакцизныйТовар = ПодакцизныйТовар;
			МенеджерЗаписи.Записать(Истина);
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонОшибки = НСтр("ru = 'Не удалось перенести признак %1 по номенклатуре ""%2"":
									|
									|по причине:
									|%4';
									|en = 'Cannot transfer the %1 flag for the ""%2"" item:
									|
									|Reason:
									|%4'");
		
		ТекстОшибки = СтрШаблон(ШаблонОшибки,
									Метаданные.Справочники.Номенклатура.Реквизиты.УдалитьПодакцизныйТовар.Представление(),
									Номенклатура,
									ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ОшибкиВыполненияОтложенныхЗаданий,,
			ТекстОшибки);
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьЗаписьВРегистр(Период, Номенклатура, Страна, КатегорияПодакцизногоТовара, КоэффициентПересчета)
	
	НаборЗаписей = РегистрыСведений.КатегорииПодакцизныхТоваров.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
	НаборЗаписей.Отбор.Страна.Установить(Страна);
	
	НоваяЗапись = НаборЗаписей.Добавить();
	
	Если ЗначениеЗаполнено(КатегорияПодакцизногоТовара) Тогда
		НоваяЗапись.ПодакцизныйТовар = Истина;
	Иначе
		НоваяЗапись.ПодакцизныйТовар = Ложь;
	КонецЕсли;
	
	НоваяЗапись.Период = Период;
	НоваяЗапись.Номенклатура = Номенклатура;
	НоваяЗапись.Страна = Страна;
	НоваяЗапись.КатегорияПодакцизногоТовара = КатегорияПодакцизногоТовара;
	НоваяЗапись.КоэффициентПересчета = КоэффициентПересчета;
	
	НаборЗаписей.Записать();
	
КонецПроцедуры

// Удаляет запись из регистра.
//
// Параметры:
//  Период - Дата
//  Номенклатура - СправочникСсылка.Номенклатура
//  Страна - СправочникСсылка.СтраныМира
//
Процедура УдалитьЗапись(Период, Номенклатура, Страна) Экспорт
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(Период);
	НаборЗаписей.Отбор.Номенклатура.Установить(Номенклатура);
	НаборЗаписей.Отбор.Страна.Установить(Страна);
	НаборЗаписей.Записать();
	
КонецПроцедуры

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.КатегорииПодакцизныхТоваров.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.24.19";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("de901df9-908e-4a93-9ed3-b8c1277e9a11");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.КатегорииПодакцизныхТоваров.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = СтрШаблон(НСтр("ru = 'Переносит признак ""%1"" из справочника ""%2"" в регистр ""%3"".';
											|en = 'Transfers the ""%1"" flag from the ""%2"" catalog to the ""%3"" register.'"),
	Метаданные.Справочники.Номенклатура.Реквизиты.УдалитьПодакцизныйТовар.Представление(),
	Метаданные.Справочники.Номенклатура.Представление(),
	Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров.Представление());
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.Справочники.Номенклатура.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Справочники.Организации.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ИспользоватьМногострановойУчет.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Константы.ОсновнаяСтрана.ПолноеИмя());	
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров.ПолноеИмя());
	Изменяемые.Добавить(Метаданные.Константы.ИспользоватьПодакцизныеТовары.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Константы.ИспользоватьПодакцизныеТовары.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");

КонецПроцедуры

// Процедура регистрации данных для обработчика обновления ОбработатьДанныеДляПереходаНаВерсию.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = Метаданные.Справочники.Номенклатура.ПолноеИмя();
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДанныеНоменклатуры.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура КАК ДанныеНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
	|		ПО (ДанныеНоменклатуры.Ссылка = КатегорииПодакцизныхТоваров.Номенклатура)
	|ГДЕ
	|	ДанныеНоменклатуры.УдалитьПодакцизныйТовар
	|	И НЕ ДанныеНоменклатуры.ПометкаУдаления
	|	И КатегорииПодакцизныхТоваров.Номенклатура ЕСТЬ NULL";
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка"));
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = Метаданные.Справочники.Номенклатура.ПолноеИмя();
	
	ВыбранныеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	ПараметрыОтметкиВыполнения = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	
	Если ВыбранныеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена =
			ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить(Метаданные.Константы.ИспользоватьПодакцизныеТовары.ПолноеИмя());
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Константы.ИспользоватьПодакцизныеТовары.Установить(Истина);
		
		ЗафиксироватьТранзакцию();

	Исключение
		
		ОтменитьТранзакцию();
		
		ШаблонОшибки = НСтр("ru = 'Не удалось установить константу ""%1"":
									|Очередь: ""%2""
									|
									|по причине:
									|%3';
									|en = 'Cannot set the ""%1"" constant:
									|Queue: ""%2""
									|
									|Reason:
									|%3'");
		
		ТекстОшибки = СтрШаблон(ШаблонОшибки,
									Метаданные.Константы.ИспользоватьПодакцизныеТовары.ПолноеИмя(),
									Параметры.Очередь,
									ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Предупреждение,
			Метаданные.РегистрыСведений.ОшибкиВыполненияОтложенныхЗаданий,,
			ТекстОшибки);
			
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
		
	КонецПопытки;
	
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.КатегорииПодакцизныхТоваров.ПолноеИмя();
	
	Если Константы.ИспользоватьМногострановойУчет.Получить() Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДанныеОрганизаций.СтранаРегистрации КАК СтранаРегистрации
		|ИЗ
		|	Справочник.Организации КАК ДанныеОрганизаций
		|ГДЕ
		|	НЕ ДанныеОрганизаций.ПометкаУдаления
		|	И ДанныеОрганизаций.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыОрганизацийПодразделений.Действует)";
		
		СтраныРегистрации = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СтранаРегистрации");
		
	Иначе
		
		СтраныРегистрации = Новый Массив();
		СтраныРегистрации.Добавить(Константы.ОсновнаяСтрана.Получить());
		
	КонецЕсли;
	
	ПустаяДата = Дата(1,1,1);
	
	Для Каждого СтрокаДанных Из ВыбранныеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаДанных.Ссылка);
			НаборЗаписей.Отбор.Период.Установить(ПустаяДата);
			
			Для Каждого СтранаРегистрации Из СтраныРегистрации Цикл
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
				ЭлементБлокировки.УстановитьЗначение("Номенклатура", СтрокаДанных.Ссылка);
				ЭлементБлокировки.УстановитьЗначение("Страна", СтранаРегистрации);
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				Блокировка.Заблокировать();
				
				НоваяЗапись = НаборЗаписей.Добавить();
				НоваяЗапись.Период = ПустаяДата;
				НоваяЗапись.Номенклатура = СтрокаДанных.Ссылка;
				НоваяЗапись.Страна = СтранаРегистрации;
				НоваяЗапись.ПодакцизныйТовар = Истина;
				
			КонецЦикла;
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(СтрокаДанных.Ссылка, ПараметрыОтметкиВыполнения);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ШаблонОшибки = НСтр("ru = 'Не удалось перенести признак %1 по номенклатуре ""%2"":
										|Очередь: ""%3""
										|
										|по причине:
										|%4';
										|en = 'Cannot transfer the %1 flag for the ""%2'' item:
										|Queue: ""%3""
										|
										|Reason:
										|%4'");
			
			ТекстОшибки = СтрШаблон(ШаблонОшибки,
										Метаданные.Справочники.Номенклатура.Реквизиты.УдалитьПодакцизныйТовар.Представление(),
										СтрокаДанных.Ссылка,
										Параметры.Очередь,
										ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ОшибкиВыполненияОтложенныхЗаданий,,
				ТекстОшибки);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
