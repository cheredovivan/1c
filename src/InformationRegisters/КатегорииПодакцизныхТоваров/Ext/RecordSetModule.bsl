#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") И ЭтоНоваяЗапись() Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Запись.Страна = ЗначениеНастроекКлиентСерверПовтИсп.ОсновнаяСтрана();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Если Запись.ПодакцизныйТовар
			И Не ЗначениеЗаполнено(Запись.КатегорияПодакцизногоТовара)
			И Не ПолучитьФункциональнуюОпцию("БазоваяВерсия") Тогда
				
				СтруктураЗаписи = Новый Структура("Период,Номенклатура,Страна");
				ЗаполнитьЗначенияСвойств(СтруктураЗаписи, Запись);
				
				КлючЗаписи = РегистрыСведений.КатегорииПодакцизныхТоваров.СоздатьКлючЗаписи(СтруктураЗаписи);
				
				ИмяПоля = НСтр("ru = 'Категория';
								|en = 'Category'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения( , , ИмяПоля);
				ОбщегоНазначения.СообщитьПользователю(
					ТекстСообщения, КлючЗаписи, "Запись.КатегорияПодакцизногоТовара", , Отказ);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьМногострановойУчет") И ЭтоНоваяЗапись() Тогда
		Константы.ОсновнаяСтрана.ПроверитьЗаполнение();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЭтоНоваяЗапись()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КатегорииПодакцизныхТоваров.Период КАК Период
		|ИЗ
		|	РегистрСведений.КатегорииПодакцизныхТоваров КАК КатегорииПодакцизныхТоваров
		|ГДЕ
		|	КатегорииПодакцизныхТоваров.Период В(&Период)
		|	И КатегорииПодакцизныхТоваров.Номенклатура В(&Номенклатура)
		|	И КатегорииПодакцизныхТоваров.Страна В(&Страна)";
	
	Запрос.УстановитьПараметр("Номенклатура", ВыгрузитьКолонку("Номенклатура"));
	Запрос.УстановитьПараметр("Период", ВыгрузитьКолонку("Период"));
	Запрос.УстановитьПараметр("Страна", ВыгрузитьКолонку("Страна"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти

#КонецЕсли