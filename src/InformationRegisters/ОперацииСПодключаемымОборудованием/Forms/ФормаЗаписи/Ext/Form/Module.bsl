#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОригиналВходящихПараметров = Истина;
	ОригиналИсходящихПараметров = Истина;
	ЗаполнитьПараметры();
	Если Запись.Результат Тогда
		Операция = Запись.Операция;
	Иначе
		Операция = СтрШаблон(НСтр("ru = '%1 (ошибка выполнения)';
									|en = '%1 (error)'"), Запись.Операция);
	Конецесли;
	
	Если Запись.Результат Тогда
		Элементы.ГруппаОшибка.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.ФормаСообщениеВТехПоддержку.Видимость = ОбщегоНазначенияБПО.ИспользуетсяСообщенияВСлужбуТехническойПоддержки()
			И ОбщегоНазначенияБПОСлужебныйВызовСервера.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОригиналВходящихПараметровПриИзменении(Элемент)
	Если ОригиналВходящихПараметров Тогда
		ВходящиеПараметры = ВходящиеПараметрыФормат;
	Иначе
		ВходящиеПараметры = ВходящиеПараметрыОригинал;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОригиналИсходящихПараметровПриИзменении(Элемент)
	Если ОригиналИсходящихПараметров Тогда
		ИсходящиеПараметры = ИсходящиеПараметрыФормат;
	Иначе
		ИсходящиеПараметры = ИсходящиеПараметрыОригинал;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьПараметры()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииСПодключаемымОборудованием.ВходящиеПараметры КАК ВходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.ИсходящиеПараметры КАК ИсходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.ТекстОшибки КАК ТекстОшибки
		|ИЗ
		|	РегистрСведений.ОперацииСПодключаемымОборудованием КАК ОперацииСПодключаемымОборудованием
		|ГДЕ
		|	ОперацииСПодключаемымОборудованием.Оборудование = &Оборудование
		|	И ОперацииСПодключаемымОборудованием.Дата = &Дата
		|	И ОперацииСПодключаемымОборудованием.НомерОперации = &НомерОперации
		|	И ОперацииСПодключаемымОборудованием.ГодМесяц = &ГодМесяц";
	
	Запрос.УстановитьПараметр("ГодМесяц", Запись.ГодМесяц);
	Запрос.УстановитьПараметр("Дата", Запись.Дата);
	Запрос.УстановитьПараметр("НомерОперации", Запись.НомерОперации);
	Запрос.УстановитьПараметр("Оборудование", Запись.Оборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если ЗначениеЗаполнено(Запись.ВходящиеПараметрыСтрока) Тогда
			ВходящиеПараметрыОригинал = Запись.ВходящиеПараметрыСтрока;
		Иначе
			ВходящиеПараметрыОригинал = Выборка.ВходящиеПараметры.Получить();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ВходящиеПараметрыОригинал) Тогда
			ВходящиеПараметрыФормат = ЛогированиеОперацийБПО.ФорматПараметраЛогирования(ВходящиеПараметрыОригинал);
			ВходящиеПараметры = ВходящиеПараметрыФормат;
		Иначе
			Элементы.ОригиналВходящихПараметров.Видимость = Ложь;
			ВходящиеПараметры = ВходящиеПараметрыОригинал;
		КонецЕсли;
		

		Если ЗначениеЗаполнено(Запись.ИсходящиеПараметрыСтрока) Тогда
			ИсходящиеПараметрыОригинал = Запись.ИсходящиеПараметрыСтрока;
		Иначе
			ИсходящиеПараметрыОригинал = Выборка.ИсходящиеПараметры.Получить();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ИсходящиеПараметрыОригинал) Тогда
			ИсходящиеПараметрыФормат = ЛогированиеОперацийБПО.ФорматПараметраЛогирования(ИсходящиеПараметрыОригинал);
			ИсходящиеПараметры = ИсходящиеПараметрыФормат;
		Иначе
			Элементы.ОригиналИсходящихПараметров.Видимость = Ложь;
			ИсходящиеПараметры = ИсходящиеПараметрыОригинал;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Запись.ТекстОшибкиСтрока) Тогда
			ТекстОшибки = Запись.ТекстОшибкиСтрока;
		Иначе
			ТекстОшибки = Выборка.ТекстОшибки.Получить();
		КонецЕсли;
		
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти

