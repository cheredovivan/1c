#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.УправлениеДоступом
// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
//
// Параметры:
//  Ограничение - Структура:
//    * Текст                             - Строка - ограничение доступа для пользователей.
//                                          Если пустая строка, значит, доступ разрешен.
//    * ТекстДляВнешнихПользователей      - Строка - ограничение доступа для внешних пользователей.
//                                          Если пустая строка, значит, доступ запрещен.
//    * ПоВладельцуБезЗаписиКлючейДоступа - Неопределено - определить автоматически.
//                                        - Булево - если Ложь, то всегда записывать ключи доступа,
//                                          если Истина, тогда не записывать ключи доступа,
//                                          а использовать ключи доступа владельца (требуется,
//                                          чтобы ограничение было строго по объекту-владельцу).
//   * ПоВладельцуБезЗаписиКлючейДоступаДляВнешнихПользователей - Неопределено, Булево - также
//                                          как у параметра ПоВладельцуБезЗаписиКлючейДоступа.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Менеджер = "РегистрСведений.ОперацииСПодключаемымОборудованием";
	МенеджерОборудованияВызовСервераПереопределяемый.ПриЗаполненииОграниченияДоступа(Менеджер, Ограничение);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выполняет удаление записей регистра за прошлые месяцы, для ускорения очистки
// 
// Параметры:
//   ДатаОчистки - Дата - Дата в месяце до которой будет выполнена очистка, 
//     удаление записей будет выполнено до конца месяца переданной даты.
Процедура ОчиститьРегистрЗаПрошлыеМесяцы(ДатаОчистки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ОперацииСПодключаемымОборудованием.ГодМесяц КАК ГодМесяц
		|ИЗ
		|	РегистрСведений.ОперацииСПодключаемымОборудованием КАК ОперацииСПодключаемымОборудованием
		|ГДЕ
		|	ОперацииСПодключаемымОборудованием.ГодМесяц < &ГодМесяцОчистки";
	
	ГодМесяцОчистки = Год(ДатаОчистки) * 100 + Месяц(ДатаОчистки);
	Запрос.УстановитьПараметр("ГодМесяцОчистки", ГодМесяцОчистки);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОперацииСПодключаемымОборудованием");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ГодМесяц","ГодМесяц");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		Выборка      = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			НаборЗаписей.Очистить();
			НаборЗаписей.Отбор.ГодМесяц.Установить(Выборка.ГодМесяц);
			НаборЗаписей.Записать(Истина);
		КонецЦикла;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначенияБПО.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Очистка операций с оборудованием.';
				|en = 'Clear peripheral operations.'", ОбщегоНазначенияБПО.КодОсновногоЯзыка()));
	КонецПопытки
	
КонецПроцедуры

// Выполняет удаление записей регистра в месяце переданной даты
// 
// Параметры:
//   ДатаОчистки - Дата - Дата до которой в месяце даты будет выполнена очистка
//     удаление записей будет выполнено с начала месяца, по переданную дату.
Процедура ОчиститьРегистрЗаТекущийМесяц(ДатаОчистки) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииСПодключаемымОборудованием.Дата КАК Дата,
		|	ОперацииСПодключаемымОборудованием.Оборудование КАК Оборудование,
		|	ОперацииСПодключаемымОборудованием.НомерОперации КАК НомерОперации,
		|	ОперацииСПодключаемымОборудованием.ГодМесяц КАК ГодМесяц
		|ИЗ
		|	РегистрСведений.ОперацииСПодключаемымОборудованием КАК ОперацииСПодключаемымОборудованием
		|ГДЕ
		|	ОперацииСПодключаемымОборудованием.ГодМесяц = &ГодМесяцОчистки
		|	И ОперацииСПодключаемымОборудованием.Дата < &ДатаОчистки";
	
	ГодМесяцОчистки = Год(ДатаОчистки) * 100 + Месяц(ДатаОчистки);
	Запрос.УстановитьПараметр("ГодМесяцОчистки", ГодМесяцОчистки);
	Запрос.УстановитьПараметр("ДатаОчистки",     ДатаОчистки);
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьТранзакцию();
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОперацииСПодключаемымОборудованием");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Оборудование","Оборудование");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Дата","Дата");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		НаборЗаписей = СоздатьНаборЗаписей();
		Выборка      = РезультатЗапроса.Выбрать();
		Если ОбщегоНазначенияБПОПовтИсп.РежимЗамещенияДоступен() Тогда
			Пока Выборка.Следующий() Цикл
				ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
			КонецЦикла;
			НаборЗаписей.Записать(ОбщегоНазначенияБПОПовтИсп.РежимЗамещенияУдаление());
		Иначе
			Пока Выборка.Следующий() Цикл
				НаборЗаписей.Очистить();
				НаборЗаписей.Отбор.Оборудование.Установить(Выборка.Оборудование);
				НаборЗаписей.Отбор.Дата.Установить(Выборка.Дата);
				НаборЗаписей.Записать(Истина);
			КонецЦикла;
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ОбщегоНазначенияБПО.ЗаписатьОшибкуВЖурналРегистрации(
			НСтр("ru = 'Очистка операций с оборудованием.';
				|en = 'Clear peripheral operations.'", ОбщегоНазначенияБПО.КодОсновногоЯзыка()));
	КонецПопытки
	
КонецПроцедуры

Процедура ЗаписатьЛогОперацииНаСервере(Записи) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СтепеньСжатия = 9;
	
	НаборЗаписей = РегистрыСведений.ОперацииСПодключаемымОборудованием.СоздатьНаборЗаписей();
	НомерОперации = 0;
	Для Каждого Операция Из Записи Цикл
		
		Запись = НаборЗаписей.Добавить();
		
		ЗаполнитьЗначенияСвойств(Запись, Операция, , "ВходящиеПараметры, ИсходящиеПараметры, ТекстОшибки");
		Запись.Милисекунда = Операция.Таймкод % 1000;
		Запись.ГодМесяц = Год(Запись.Дата)*100+Месяц(Запись.Дата);
		НомерОперации = НомерОперации + 1;
		Запись.НомерОперации = НомерОперации;
		Запись.РевизияИнтерфейса = ОбщегоНазначенияБПОКлиентСервер.ПредставлениеРевизияИнтерфейса(Операция.РевизияИнтерфейса);
		Если СтрНайти(Операция.ПредставлениеОперации, " ")=0 Тогда
			Запись.Операция = ОбщегоНазначенияБПОКлиентСервер.ПредставлениеИдентификатора(Операция.ПредставлениеОперации);
		Иначе
			Запись.Операция = Операция.ПредставлениеОперации;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Операция.ВходящиеПараметры) Тогда
			ЗаписьJSON = Новый ЗаписьJSON();
			ЗаписьJSON.УстановитьСтроку();
			ЗаписатьJSON(ЗаписьJSON, Операция.ВходящиеПараметры, , "СтрокуВJSON", ОбщегоНазначенияБПО);
			Запись.ВходящиеПараметры = Новый ХранилищеЗначения(ЗаписьJSON.Закрыть(), Новый СжатиеДанных(СтепеньСжатия));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Операция.ИсходящиеПараметры) Тогда
			ЗаписьJSON = Новый ЗаписьJSON();
			ЗаписьJSON.УстановитьСтроку();
			ЗаписатьJSON(ЗаписьJSON, Операция.ИсходящиеПараметры, , "СтрокуВJSON", ОбщегоНазначенияБПО);
			Запись.ИсходящиеПараметры = Новый ХранилищеЗначения(ЗаписьJSON.Закрыть(), Новый СжатиеДанных(СтепеньСжатия));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Операция.ТекстОшибки) Тогда
			Запись.ТекстОшибки = Новый ХранилищеЗначения(Операция.ТекстОшибки, Новый СжатиеДанных(СтепеньСжатия));
		КонецЕсли
	КонецЦикла;
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

Функция ДополнительныеСведенияОперации(Ключ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииСПодключаемымОборудованием.ВходящиеПараметрыСтрока КАК ВходящиеПараметрыСтрока,
		|	ОперацииСПодключаемымОборудованием.ВходящиеПараметры КАК ВходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.ИсходящиеПараметрыСтрока КАК ИсходящиеПараметрыСтрока,
		|	ОперацииСПодключаемымОборудованием.ИсходящиеПараметры КАК ИсходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.ТекстОшибкиСтрока КАК ТекстОшибкиСтрока,
		|	ОперацииСПодключаемымОборудованием.ТекстОшибки КАК ТекстОшибки
		|ИЗ
		|	РегистрСведений.ОперацииСПодключаемымОборудованием КАК ОперацииСПодключаемымОборудованием
		|ГДЕ
		|	ОперацииСПодключаемымОборудованием.Оборудование = &Оборудование
		|	И ОперацииСПодключаемымОборудованием.Дата = &Дата
		|	И ОперацииСПодключаемымОборудованием.НомерОперации = &НомерОперации
		|	И ОперацииСПодключаемымОборудованием.ГодМесяц = &ГодМесяц";
	
	Запрос.УстановитьПараметр("ГодМесяц", Ключ.ГодМесяц);
	Запрос.УстановитьПараметр("Дата", Ключ.Дата);
	Запрос.УстановитьПараметр("НомерОперации", Ключ.НомерОперации);
	Запрос.УстановитьПараметр("Оборудование", Ключ.Оборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Результат = Новый Структура;
	Результат.Вставить("ВходящиеПараметры");
	Результат.Вставить("ИсходящиеПараметры");
	Результат.Вставить("ТекстОшибки");
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.ВходящиеПараметрыСтрока) Тогда
			Результат.ВходящиеПараметры = Выборка.ВходящиеПараметрыСтрока;
		Иначе
			Результат.ВходящиеПараметры = Выборка.ВходящиеПараметры.Получить();
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Выборка.ИсходящиеПараметрыСтрока) Тогда
			Результат.ИсходящиеПараметры = Выборка.ИсходящиеПараметрыСтрока;
		Иначе
			Результат.ИсходящиеПараметры = Выборка.ИсходящиеПараметры.Получить();
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ТекстОшибкиСтрока) Тогда
			Результат.ТекстОшибки = Выборка.ТекстОшибкиСтрока;
		Иначе
			Результат.ТекстОшибки = Выборка.ТекстОшибки.Получить();
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция АрхивОперацийОборудования(ПодключаемоеОборудование = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииСПодключаемымОборудованием.Оборудование КАК Оборудование,
		|	ОперацииСПодключаемымОборудованием.Дата КАК Дата,
		|	ОперацииСПодключаемымОборудованием.НомерОперации КАК НомерОперации,
		|	ОперацииСПодключаемымОборудованием.ГодМесяц КАК ГодМесяц,
		|	ОперацииСПодключаемымОборудованием.ВходящиеПараметрыСтрока КАК ВходящиеПараметрыСтрока,
		|	ОперацииСПодключаемымОборудованием.ВходящиеПараметры КАК ВходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.ИсходящиеПараметрыСтрока КАК ИсходящиеПараметрыСтрока,
		|	ОперацииСПодключаемымОборудованием.ИсходящиеПараметры КАК ИсходящиеПараметры,
		|	ОперацииСПодключаемымОборудованием.Результат КАК Результат,
		|	ОперацииСПодключаемымОборудованием.Таймкод КАК Таймкод,
		|	ОперацииСПодключаемымОборудованием.ТипЗаписи КАК ТипЗаписи,
		|	ОперацииСПодключаемымОборудованием.ВерсияКомпонента КАК ВерсияКомпонента,
		|	ОперацииСПодключаемымОборудованием.ВерсияДрайвера КАК ВерсияДрайвера,
		|	ОперацииСПодключаемымОборудованием.Операция КАК Операция,
		|	ОперацииСПодключаемымОборудованием.ИДУстройства КАК ИДУстройства,
		|	ОперацииСПодключаемымОборудованием.ВремяВыполнения КАК ВремяВыполнения,
		|	ОперацииСПодключаемымОборудованием.РевизияИнтерфейса КАК РевизияИнтерфейса,
		|	ОперацииСПодключаемымОборудованием.Милисекунда КАК Милисекунда,
		|	ОперацииСПодключаемымОборудованием.КодОшибки КАК КодОшибки,
		|	ОперацииСПодключаемымОборудованием.ТекстОшибкиСтрока КАК ТекстОшибкиСтрока,
		|	ОперацииСПодключаемымОборудованием.ТекстОшибки КАК ТекстОшибки
		|ИЗ
		|	РегистрСведений.ОперацииСПодключаемымОборудованием КАК ОперацииСПодключаемымОборудованием
		|ГДЕ
		|	ОперацииСПодключаемымОборудованием.Оборудование = &Оборудование";
	
	Запрос.УстановитьПараметр("Оборудование", ПодключаемоеОборудование);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Данные = Новый Массив;
	Пока Выборка.Следующий() Цикл
		СтрокаДанных = ПолучитьДвоичныеДанныеИзСтроки(СтрокаЗаписиЛога(Выборка));
		Данные.Добавить(СтрокаДанных);
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("ДатаАрхива", ТекущаяДатаСеанса());
	Результат.Вставить("ДанныеАрхива", СоединитьДвоичныеДанные(Данные));
	Результат.Вставить("ЭтоАрхив", Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция СтрокаЗаписиЛога(Запись) Экспорт
	
	НачалоЗаписи = СтрШаблон("%1 : %2; (%3);%4; [%5:%5:%6]; ""%8"";",
		Запись.Дата,
		Запись.Милисекунда,
		Запись.ВремяВыполнения,
		Запись.ТипЗаписи,
		Запись.РевизияИнтерфейса,
		Запись.ВерсияДрайвера,
		Запись.ВерсияКомпонента,
		Запись.Операция);
		
	Если ЗначениеЗаполнено(Запись.ВходящиеПараметрыСтрока) Тогда
		ВходящиеПараметры = Запись.ВходящиеПараметрыСтрока;
	Иначе
		ВходящиеПараметры = Запись.ВходящиеПараметры.Получить();
	КонецЕсли;
	Если ЗначениеЗаполнено(Запись.ИсходящиеПараметрыСтрока) Тогда
		ИсходящиеПараметры = Запись.ИсходящиеПараметрыСтрока;
	Иначе
		ИсходящиеПараметры = Запись.ИсходящиеПараметры.Получить();
	КонецЕсли;
	Если ЗначениеЗаполнено(Запись.ТекстОшибкиСтрока) Тогда
		ТекстОшибки = Запись.ТекстОшибкиСтрока;
	Иначе
		ТекстОшибки = Запись.ТекстОшибки.Получить();
	КонецЕсли;
	ВходящиеТекст = ЛогированиеОперацийБПО.ФорматПараметраЛогирования(ВходящиеПараметры);
	ИсходящиеТекст = ЛогированиеОперацийБПО.ФорматПараметраЛогирования(ИсходящиеПараметры);
	РезультатИПараметрыЗаписи = СтрШаблон("Результат: %1; %2; %3; %4",
		Запись.Результат,
		?(ПустаяСтрока(ТекстОшибки), "", Символы.ПС + НСтр("ru = ' - Текст ошибки:';
															|en = ' - Error text:'") + Символы.ПС + СокрЛП(ТекстОшибки)),
		?(ПустаяСтрока(ВходящиеТекст), "", Символы.ПС + НСтр("ru = ' - Входящие:';
															|en = '- Входящие:'") + Символы.ПС + СокрЛП(ВходящиеТекст)),
		?(ПустаяСтрока(ИсходящиеТекст), "", Символы.ПС + НСтр("ru = ' - Исходящие:';
																|en = ' - Outgoing:'") + Символы.ПС + СокрЛП(ИсходящиеТекст)));
		
	Возврат СтрШаблон("%1 %2", НачалоЗаписи, РезультатИПараметрыЗаписи);
	
КонецФункции


#КонецОбласти

#КонецЕсли