#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Склад)";
	
	Ограничение.ТекстДляВнешнихПользователей = Ограничение.Текст;

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.РаспределениеЗапасов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.24.21";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("a14ede29-84e0-43a5-8a5a-e86839420447");
	Обработчик.Многопоточный = Истина;
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.РаспределениеЗапасов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр("ru = 'Обновляет в регистре сведений ""Распределение запасов"" данные об остатке и графике поступления товаров и работ
	| содержащихся в оформленных документах ""Акт о расхождениях после приемки"", при раздельном оформлении закупок,
	| ""Корректировка приобретения"", ""Приобретение товаров и услуг"" при раздельном оформлении закупок, либо содержащих работы.';
	|en = 'Updates data in the ""Inventory allocation"" information register on the balance and schedule of receipt of goods and works
	|contained in the issued ""Receipt discrepancy report"" documents with separate registration of purchases
	| and in the ""Purchase adjustment"" and ""Vendor invoice"" documents with separate registration of purchases or with works.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.РаспределениеЗапасов.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.ЗапасыИПотребности.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.РаспределениеЗапасов.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.ЗапасыИПотребности.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.РаспределениеЗапасов";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.РаспределениеЗапасов");
	
	ИмяРегистраЗапасыИПотребности = "ЗапасыИПотребности";
	ПолноеИмяРегистраЗапасыИПотребности = "РегистрНакопления.ЗапасыИПотребности";
	
	ДопПараметры = ПроведениеДокументов.ДопПараметрыИнициализироватьДанныеДокументаДляПроведения();
	ДопПараметры.ПолучитьТекстыЗапроса = Истина;
	
	ТекстыЗапроса = Документы.АктОРасхожденияхПослеПриемки.ДанныеДокументаДляПроведения(
		Неопределено,
		ИмяРегистраЗапасыИПотребности,
		ДопПараметры);
	
	РегистраторыАктыПослеПриемки = ПроведениеДокументов.РегистраторыДляПерепроведения(
		ТекстыЗапроса,
		ПолноеИмяРегистраЗапасыИПотребности,
		"Документ.АктОРасхожденияхПослеПриемки",
		Неопределено,
		"ЗакрытьГрафикПоступления,ЗакрытьГрафикОтгрузки",
		"ТаблицаРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейЗапасыИПотребности.ПервичнаяЗапись)");
	
	ТекстыЗапроса = Документы.КорректировкаПриобретения.ДанныеДокументаДляПроведения(
		Неопределено,
		ИмяРегистраЗапасыИПотребности,
		ДопПараметры);
	
	РегистраторыКорректировкиПриобретения = ПроведениеДокументов.РегистраторыДляПерепроведения(
		ТекстыЗапроса,
		ПолноеИмяРегистраЗапасыИПотребности,
		"Документ.КорректировкаПриобретения",
		Неопределено,
		"ЗакрытьГрафикПоступления,ЗакрытьГрафикОтгрузки",
		"ТаблицаРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейЗапасыИПотребности.ПервичнаяЗапись)");
	
	ТекстыЗапроса = Документы.ПриобретениеТоваровУслуг.ДанныеДокументаДляПроведения(
		Неопределено,
		ИмяРегистраЗапасыИПотребности,
		ДопПараметры);
	
	РегистраторыПриобретения = ПроведениеДокументов.РегистраторыДляПерепроведения(
		ТекстыЗапроса,
		ПолноеИмяРегистраЗапасыИПотребности,
		"Документ.ПриобретениеТоваровУслуг",
		Неопределено,
		"ЗакрытьГрафикПоступления,ЗакрытьГрафикОтгрузки",
		"ТаблицаРегистра.ТипЗаписи = ЗНАЧЕНИЕ(Перечисление.ТипыЗаписейЗапасыИПотребности.ПервичнаяЗапись)");
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("РегистраторыАктыПослеПриемки", РегистраторыАктыПослеПриемки);
	Запрос.УстановитьПараметр("РегистраторыКорректировкиПриобретения", РегистраторыКорректировкиПриобретения);
	Запрос.УстановитьПараметр("РегистраторыПриобретения", РегистраторыПриобретения);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Набор.Номенклатура КАК Номенклатура,
		|	Набор.Характеристика КАК Характеристика,
		|	Набор.Склад КАК Склад,
		|	Набор.Назначение КАК Назначение
		|ПОМЕСТИТЬ АналитикиДляОбновления
		|ИЗ(
			// Перенос движений по отражению расхождений в корректировку приобретения.
		|	ВЫБРАТЬ
		|		ТоварыДокумента.Номенклатура КАК Номенклатура,
		|		ТоварыДокумента.Характеристика КАК Характеристика,
		|		ТоварыДокумента.Склад КАК Склад,
		|		ТоварыДокумента.Назначение КАК Назначение
		|	ИЗ
		|		Документ.АктОРасхожденияхПослеПриемки КАК ТаблицаДокумента
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.АктОРасхожденияхПослеПриемки.Товары КАК ТоварыДокумента
		|			ПО ТоварыДокумента.Ссылка = ТаблицаДокумента.Ссылка
		|	ГДЕ
		|		ТаблицаДокумента.Проведен
		|		И ТаблицаДокумента.Ссылка В (&РегистраторыАктыПослеПриемки)
		|		И ТаблицаДокумента.ХозяйственнаяОперация В(
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаТоварыВПути),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСТоварыВПути),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуТоварыВПути),
		|			ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаПоИмпортуПоступлениеИзТоваровВПути))
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
			// Перенос движений по отражению расхождений в корректировку приобретения.
			// Также добавление движений по работам в двухходовке.
			// Удаление движений по наличию работ в одноходовке для варианта отражения Отразить на доходах/ Списать на расходы
		|	ВЫБРАТЬ
		|		ТоварыДокумента.Номенклатура КАК Номенклатура,
		|		ТоварыДокумента.Характеристика КАК Характеристика,
		|		ТоварыДокумента.Склад КАК Склад,
		|		ТоварыДокумента.Назначение КАК Назначение
		|	ИЗ
		|		Документ.КорректировкаПриобретения КАК ТаблицаДокумента
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.КорректировкаПриобретения.Расхождения КАК ТоварыДокумента
		|			ПО ТоварыДокумента.Ссылка = ТаблицаДокумента.Ссылка
		|	ГДЕ
		|		ТаблицаДокумента.Проведен
		|		И ТаблицаДокумента.Ссылка В (&РегистраторыКорректировкиПриобретения)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		// Движения по работам документов не поддерживающих двухходовку работ (по старым договорам)
		|ВЫБРАТЬ
		|	ТоварыДокумента.Номенклатура КАК Номенклатура,
		|	ТоварыДокумента.Характеристика КАК Характеристика,
		|	ТоварыДокумента.Склад КАК Склад,
		|	ТоварыДокумента.Назначение КАК Назначение
		|ИЗ
		|	Документ.ПриобретениеТоваровУслуг КАК ТаблицаДокумента
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриобретениеТоваровУслуг.Товары КАК ТоварыДокумента
		|		ПО ТоварыДокумента.Ссылка = ТаблицаДокумента.Ссылка
		|ГДЕ
		|	ТаблицаДокумента.Проведен
		|	И ТаблицаДокумента.Ссылка В (&РегистраторыПриобретения)
		|	И ТоварыДокумента.Номенклатура.ТипНоменклатуры = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Работа)
		|	И ТаблицаДокумента.ХозяйственнаяОперация В(
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаУПоставщикаФактуровкаПоставки),
		|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ЗакупкаВСтранахЕАЭСФактуровкаПоставки))) КАК Набор
		|;
		|
		|/////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	Таблица.Назначение КАК Назначение
		|ИЗ
		|	АналитикиДляОбновления КАК Таблица
		|ГДЕ
		|	Таблица.Назначение <> ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Номенклатура КАК Номенклатура,
		|	Таблица.Характеристика КАК Характеристика,
		|	Таблица.Склад КАК Склад,
		|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение
		|ИЗ
		|	АналитикиДляОбновления КАК Таблица";
	
	РегистрируемыеИзмерения = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, РегистрируемыеИзмерения, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.РаспределениеЗапасов";
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	Если Не ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Неопределено, "РегистрНакопления.ЗапасыИПотребности") Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;

	ТаблицаОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Для Каждого Строка Из ТаблицаОбновляемыеДанные Цикл
		
		Набор = Неопределено;
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных();
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.РаспределениеЗапасов");
			ЭлементБлокировки.УстановитьЗначение("Номенклатура", Строка.Номенклатура);
			ЭлементБлокировки.УстановитьЗначение("Характеристика", Строка.Характеристика);
			ЭлементБлокировки.УстановитьЗначение("Склад", Строка.Склад);
			ЭлементБлокировки.УстановитьЗначение("Назначение", Строка.Назначение);
			
			Блокировка.Заблокировать();
			
			Набор = РаспределениеЗапасов.ОбновлениеИБПоТовару(Строка);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			Если Набор = Неопределено Тогда
				Набор = СоздатьНаборЗаписей();
				Набор.Отбор.Номенклатура.Установить(Строка.Номенклатура);
				Набор.Отбор.Характеристика.Установить(Строка.Характеристика);
				Набор.Отбор.Склад.Установить(Строка.Склад);
				Набор.Отбор.Назначение.Установить(Строка.Назначение);
			КонецЕсли;
			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Набор);
		КонецПопытки;
		
	КонецЦикла;
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
