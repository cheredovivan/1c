#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Должность = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.ВидРабот, "Должность");
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Должность",
		Должность,
		ВидСравненияКомпоновкиДанных.Равно,
		,
		ЗначениеЗаполнено(Должность));
		
	Заголовок = СтрШаблон(НСтр("ru = 'Тарифные ставки должности %1';
								|en = '%1 position tariff rates'"), Должность);
	
	ДопРазрез = Константы.ДополнительныйРазрезТарифнойСетки.Получить();
	
	Элементы.Подразделение.Видимость = ДопРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Подразделение;
	Элементы.Организация.Видимость = ДопРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Организация;
	
	Если ДопРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Подразделение Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Организация",
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено,
			,
			Истина);
	ИначеЕсли ДопРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.Организация Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Подразделение",
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено,
			,
			Истина);
	ИначеЕсли ДопРазрез = Перечисления.ДополнительныеРазрезыТарифнойСетки.БезДетализации Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Подразделение",
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено,
			,
			Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список,
			"Организация",
			,
			ВидСравненияКомпоновкиДанных.НеЗаполнено,
			,
			Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Документ.РегистраторТарифнойСетки.ФормаОбъекта", Новый Структура("Ключ", Элемент.ТекущиеДанные.Регистратор));
	
КонецПроцедуры

#КонецОбласти