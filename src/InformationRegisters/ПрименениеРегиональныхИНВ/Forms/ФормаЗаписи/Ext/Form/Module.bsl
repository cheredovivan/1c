#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда
		Запись.Организация = Параметры.ЗначенияЗаполнения.Организация;
	КонецЕсли;
	
	Элементы.Организация.Видимость = НЕ ЗначениеЗаполнено(Запись.Организация);
	
	КодРегионаСтрока = Формат(Запись.КодРегиона, "ЧЦ=2; ЧН=; ЧВН=");
	
	ЗаполнитьСписокВыбораРегиона(
			Элементы.КодРегионаСтрока.СписокВыбора,
			Ложь,
			КодРегионаСтрока);
			
	УстановитьСвойстваЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЗначениеЗаполнено(Запись.КодРегиона) Тогда
		ТекстСообщения = НСтр("ru = 'Поле ""Регион"" не заполнено';
								|en = 'The ""Region"" field is required'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "КодРегионаСтрока", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КодРегионаСтрокаНачалоВыбора(Элемент, ДанныеВыбора, ВыборДобавлением, СтандартнаяОбработка)
	
	ЗаполнитьСписокРегионов(Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура КодРегионаСтрокаПриИзменении(Элемент)
	
	Запись.КодРегиона = КодРегионаСтрока;
	Если Элемент.СписокВыбора.НайтиПоЗначению(КодРегионаСтрока) <> Неопределено Тогда
		Запись.Регион = Элемент.СписокВыбора.НайтиПоЗначению(КодРегионаСтрока).Представление;
	Иначе
		Запись.Регион = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименяетсяРегиональныйИНВПриИзменении(Элемент)
	
	Если НЕ Запись.ПрименяетсяРегиональныйИНВ Тогда
		Запись.ДоляРасходовИНВ_РБ = 0;
		Запись.МинимальнаяСтавкаИНВ = 0;
		Запись.СрокПереносаВычета = 0;
		Запись.ПрименяетсяФедеральныйИНВ = Ложь;
		Запись.ДоляРасходовИНВ_ФБ = 0; 
		Запись.МинимальныйСрокФактическогоИспользования = 0;
	КонецЕсли;
	
	УстановитьСвойстваЭлементов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрименяетсяФедеральныйИНВПриИзменении(Элемент)
	
	Если НЕ Запись.ПрименяетсяФедеральныйИНВ Тогда
		Запись.ДоляРасходовИНВ_ФБ = 0; 
		Запись.МинимальныйСрокФактическогоИспользования = 0;
	КонецЕсли;
	
	УстановитьСвойстваЭлементов(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаполнитьСписокРегионов(Элемент)
	
	// В списке выбора региона может быть 1 элемент, который был заполнен при открытии формы
	// В этом случае требуется заполнить список выбора полностью
	// Если элементов более 1, то считаем, что список уже заполнен полностью
	Если Элемент.СписокВыбора.Количество() > 1 Тогда
		Возврат;
	КонецЕсли;
	
	СписокРегионов = Новый СписокЗначений;
	ЗаполнитьСписокВыбораРегиона(СписокРегионов);
	
	// Чтобы избежать дублирования элементов, очищаем список выбора в поле формы
	Элемент.СписокВыбора.Очистить();
	
	Для Каждого Регион Из СписокРегионов Цикл
		Элемент.СписокВыбора.Добавить(Регион.Значение, Регион.Представление);
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораРегиона(СписокРегионов, ТолькоИспользуемые = Ложь, КодРегиона = Неопределено) 
	
	СписокРегионов.Очистить();
	
	Если ЗначениеЗаполнено(КодРегиона) И КодРегиона <> "00" Тогда
		
		ШаблонПредставления = НСтр("ru = '%1 - %2';
									|en = '%1 - %2'"); //например: "77 - Москва г"
		
		НаименованиеРегиона = АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона);
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления, КодРегиона, НаименованиеРегиона);
		
		СписокРегионов.Добавить(КодРегиона, Представление);
		
		Возврат;
		
	КонецЕсли;
	
	КлассификаторСубъектовРФ = АдресныйКлассификатор.СубъектыРФ();
	
	Если НЕ ТолькоИспользуемые Тогда
		
		ТаблицаРегионов = КлассификаторСубъектовРФ;
		
	Иначе
		
		// Запрос выбирает все коды регионов, которые выбраны в справочнике регистраций в налоговом органе
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РегистрацииВНалоговомОргане.КодРегиона КАК КодРегиона
		|ИЗ
		|	Справочник.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
		|";

		РезультатЗапроса = Запрос.Выполнить();
		
		Если РезультатЗапроса.Пустой() Тогда
			Возврат;
		КонецЕсли;
		
		ВыборкаРегионов = РезультатЗапроса.Выбрать();
		
		ТаблицаРегионов = КлассификаторСубъектовРФ.СкопироватьКолонки();
		Пока ВыборкаРегионов.Следующий() Цикл
			
			СтрокаРегиона = ТаблицаРегионов.Добавить();
			СтрокаРегиона.КодСубъектаРФ = ВыборкаРегионов.КодРегиона;
			
			СтрокаКлассификатора = КлассификаторСубъектовРФ.Найти(СтрокаРегиона.КодСубъектаРФ, "КодСубъектаРФ");
			Если СтрокаКлассификатора <> Неопределено Тогда
				ЗаполнитьЗначенияСвойств(СтрокаРегиона, СтрокаКлассификатора);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	ШаблонПредставления = НСтр("ru = '%1 - %2 %3';
								|en = '%1 - %2 %3'");  //например: "77 - Москва г"
	Для Каждого Регион Из ТаблицаРегионов Цикл
		Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредставления,
			Регион.КодСубъектаРФ,
			Регион.Наименование,
			Регион.Сокращение);
		СписокРегионов.Добавить(Формат(Регион.КодСубъектаРФ, "ЧЦ=2; ЧН=; ЧВН="), Представление);
	КонецЦикла;
	
	// Сортируем по наименованию региона
	СписокРегионов.СортироватьПоЗначению();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьСвойстваЭлементов(Форма)
	
	Элементы = Форма.Элементы;
	Запись = Форма.Запись;
	
	Элементы.ДекорацияЗаконСубъекта.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.НомерЗаконаСубъекта.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДатаЗаконаСубъекта.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.СрокПереносаВычета.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДекорацияСрокПереносаВычета.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.МинимальныйСрокФактическогоИспользования.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДекорацияСрокФактическогоИспользования.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДоляРасходовИНВ_РБ.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДекорацияДоляРасходовИНВ_РБ.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.МинимальнаяСтавкаИНВ.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДекорацияМинимальнаяСтавкаИНВ.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ПрименяетсяФедеральныйИНВ.Доступность = Запись.ПрименяетсяРегиональныйИНВ;
	Элементы.ДоляРасходовИНВ_ФБ.Доступность = Запись.ПрименяетсяФедеральныйИНВ;
	Элементы.ДекорацияДоляРасходовИНВ_ФБ.Доступность = Запись.ПрименяетсяФедеральныйИНВ;

КонецПроцедуры

#КонецОбласти

