#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
 
// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра:
//		* ПрименяетсяРегиональныйИНВ - Булево
//		* ДоляРасходовИНВ_РБ - Число
//		* МинимальнаяСтавкаИНВ - Число
//		* СрокПереносаВычета - Число
//		* ПрименяетсяФедеральныйИНВ - Булево
//		* ДоляРасходовИНВ_ФБ - Число
//		* МинимальныйСрокФактическогоИспользования - Число
//		* ДатаЗаконаСубъекта - Дата
//		* НомерЗаконаСубъекта - Строка
//		* Регион - Строка
Функция ЗначенияПоУмолчанию() Экспорт
	
	СтруктураЗначений = Новый Структура;
	
	СтруктураЗначений.Вставить("ПрименяетсяРегиональныйИНВ", Ложь);
	СтруктураЗначений.Вставить("ДоляРасходовИНВ_РБ", 0);
	СтруктураЗначений.Вставить("МинимальнаяСтавкаИНВ", 0);
	СтруктураЗначений.Вставить("СрокПереносаВычета", 0);
	СтруктураЗначений.Вставить("ПрименяетсяФедеральныйИНВ", Ложь);
	СтруктураЗначений.Вставить("ДоляРасходовИНВ_ФБ", 0);
	СтруктураЗначений.Вставить("МинимальныйСрокФактическогоИспользования", 0);
	СтруктураЗначений.Вставить("ДатаЗаконаСубъекта", Дата('00010101'));
	СтруктураЗначений.Вставить("НомерЗаконаСубъекта", "");
	СтруктураЗначений.Вставить("Регион", "");
	
	Возврат СтруктураЗначений;
	
КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ТаблицаСрезПоследних.КодРегиона КАК КодРегиона,
	|	ТаблицаСрезПоследних.ПрименяетсяРегиональныйИНВ КАК ПрименяетсяРегиональныйИНВ,
	|	ТаблицаСрезПоследних.ДоляРасходовИНВ_РБ КАК ДоляРасходовИНВ_РБ,
	|	ТаблицаСрезПоследних.МинимальнаяСтавкаИНВ КАК МинимальнаяСтавкаИНВ,
	|	ТаблицаСрезПоследних.СрокПереносаВычета КАК СрокПереносаВычета,
	|	ТаблицаСрезПоследних.ПрименяетсяФедеральныйИНВ КАК ПрименяетсяФедеральныйИНВ,
	|	ТаблицаСрезПоследних.ДоляРасходовИНВ_ФБ КАК ДоляРасходовИНВ_ФБ,
	|	ТаблицаСрезПоследних.МинимальныйСрокФактическогоИспользования КАК МинимальныйСрокФактическогоИспользования,
	|	ТаблицаСрезПоследних.ДатаЗаконаСубъекта КАК ДатаЗаконаСубъекта,
	|	ТаблицаСрезПоследних.НомерЗаконаСубъекта КАК НомерЗаконаСубъекта,
	|	ТаблицаСрезПоследних.Регион КАК Регион
	|	
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименениеРегиональныхИНВ.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Действующие настройки применения ИНВ по организации.
// 
// Параметры:
//  Организация - СправочникСсылка.Организации - Организация
//  ДатаНастроек - Дата - Дата настроек
// 
// Возвращаемое значение:
//  Соответствие из КлючИЗначение - Действующие настройки применения ИНВ по организации:
//   * Ключ - Строка - 
//   * Значение - Структура - Настройки применения ИНВ:
//    ** КодРегиона - Строка
//    ** ПрименяетсяРегиональныйИНВ - Булево
//    ** ДоляРасходовИНВ_РБ - Число
//    ** МинимальнаяСтавкаИНВ - Число
//    ** СрокПереносаВычета - Число
//    ** ПрименяетсяФедеральныйИНВ - Булево
//    ** ДоляРасходовИНВ_ФБ - Число
//    ** МинимальныйСрокФактическогоИспользования - Число
Функция ДействующиеНастройкиПримененияИНВПоОрганизации(Знач Организация, Знач ДатаНастроек) Экспорт
	
	ДействующиеНастройки = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаНастроек", ДатаНастроек);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПрименениеРегиональныхИНВСрезПоследних.Организация КАК Организация,
	|	ПрименениеРегиональныхИНВСрезПоследних.КодРегиона КАК КодРегиона,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДоляРасходовИНВ_РБ КАК ДоляРасходовИНВ_РБ,
	|	ПрименениеРегиональныхИНВСрезПоследних.МинимальнаяСтавкаИНВ КАК МинимальнаяСтавкаИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.СрокПереносаВычета КАК СрокПереносаВычета,
	|	ПрименениеРегиональныхИНВСрезПоследних.ПрименяетсяФедеральныйИНВ КАК ПрименяетсяФедеральныйИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДоляРасходовИНВ_ФБ КАК ДоляРасходовИНВ_ФБ,
	|	ПрименениеРегиональныхИНВСрезПоследних.МинимальныйСрокФактическогоИспользования КАК
	|		МинимальныйСрокФактическогоИспользования,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДатаЗаконаСубъекта КАК ДатаЗаконаСубъекта,
	|	ПрименениеРегиональныхИНВСрезПоследних.НомерЗаконаСубъекта КАК НомерЗаконаСубъекта,
	|	ПрименениеРегиональныхИНВСрезПоследних.ПрименяетсяРегиональныйИНВ КАК ПрименяетсяРегиональныйИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.Регион КАК Регион
	|ИЗ
	|	РегистрСведений.ПрименениеРегиональныхИНВ.СрезПоследних(&ДатаНастроек, Организация = ВЫРАЗИТЬ(&Организация КАК
	|		Справочник.Организации).ГоловнаяОрганизация) КАК ПрименениеРегиональныхИНВСрезПоследних
	|ГДЕ
	|	ПрименениеРегиональныхИНВСрезПоследних.ПрименяетсяРегиональныйИНВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НастройкаПримененияРегиональныхИНВ = ПустыеНастройкиПримененияИНВ();
		ЗаполнитьЗначенияСвойств(НастройкаПримененияРегиональныхИНВ, Выборка);
		ДействующиеНастройки.Вставить(Выборка.КодРегиона, НастройкаПримененияРегиональныхИНВ);
	КонецЦикла;
	
	Возврат ДействующиеНастройки;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Возращает значения учетных политик применения региональных ИНВ по подразделению для организации.
// 
// Параметры:
//  Период - Дата
//  Организация - СправочникСсылка.Организации
//  Подразделение - Неопределено, СправочникСсылка.СтруктураПредприятия - Подразделение.
// 
// Возвращаемое значение:
//  Строка - Текст запроса.
Функция ДействующиеПараметрыПримененияРегиональныхИНВПоПодразделению(Период, Организация, Подразделение = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);
	
	Если Подразделение = Неопределено Тогда
		Запрос.УстановитьПараметр("Подразделение", Справочники.СтруктураПредприятия.ПустаяСсылка());
	Иначе
		Запрос.УстановитьПараметр("Подразделение", Подразделение);
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане.КодРегиона КАК КодРегиона,
	|	РегистрацииВНалоговомОргане.Организация.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	РегистрСведений.РегистрацииВНалоговомОргане.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И Подразделение = &Подразделение) КАК РегистрацииВНалоговомОргане
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Организации.РегистрацияВНалоговомОргане.КодРегиона КАК КодРегиона,
	|	Организации.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|	Организации.Ссылка = &Организация";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	Выборка = РезультатЗапроса[0].Выбрать();
	
	ГоловнаяОрганизация = Неопределено;
	КодРегиона = Неопределено;
	Если Выборка.Следующий() Тогда
		КодРегиона = Выборка.КодРегиона;
		ГоловнаяОрганизация = Выборка.ГоловнаяОрганизация;
	Иначе
		Выборка = РезультатЗапроса[1].Выбрать();
		Выборка.Следующий();
		КодРегиона = Выборка.КодРегиона;
		ГоловнаяОрганизация = Выборка.ГоловнаяОрганизация;
	КонецЕсли;
	
	Если КодРегиона = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("КодРегиона", КодРегиона);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация", ГоловнаяОрганизация);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПрименениеРегиональныхИНВСрезПоследних.Период КАК Период,
	|	ПрименениеРегиональныхИНВСрезПоследних.Организация КАК Организация,
	|	ПрименениеРегиональныхИНВСрезПоследних.КодРегиона КАК КодРегиона,
	|	ПрименениеРегиональныхИНВСрезПоследних.ПрименяетсяРегиональныйИНВ КАК ПрименяетсяРегиональныйИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДоляРасходовИНВ_РБ КАК ДоляРасходовИНВ_РБ,
	|	ПрименениеРегиональныхИНВСрезПоследних.МинимальнаяСтавкаИНВ КАК МинимальнаяСтавкаИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.СрокПереносаВычета КАК СрокПереносаВычета,
	|	ПрименениеРегиональныхИНВСрезПоследних.ПрименяетсяФедеральныйИНВ КАК ПрименяетсяФедеральныйИНВ,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДоляРасходовИНВ_ФБ КАК ДоляРасходовИНВ_ФБ,
	|	ПрименениеРегиональныхИНВСрезПоследних.МинимальныйСрокФактическогоИспользования КАК
	|		МинимальныйСрокФактическогоИспользования,
	|	ПрименениеРегиональныхИНВСрезПоследних.ДатаЗаконаСубъекта КАК ДатаЗаконаСубъекта,
	|	ПрименениеРегиональныхИНВСрезПоследних.НомерЗаконаСубъекта КАК НомерЗаконаСубъекта,
	|	ПрименениеРегиональныхИНВСрезПоследних.Регион КАК Регион
	|ИЗ
	|	РегистрСведений.ПрименениеРегиональныхИНВ.СрезПоследних(&Период, Организация = &ГоловнаяОрганизация
	|	И КодРегиона = &КодРегиона) КАК ПрименениеРегиональныхИНВСрезПоследних";
	РезультатЗапроса = Запрос.Выполнить();
	Настройки = РезультатЗапроса.Выгрузить();
	
	Если Настройки.Количество()>0 Тогда
		
		ПараметрыУчетныхПолитик = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Настройки[0]);
	
		Возврат ПараметрыУчетныхПолитик;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

// Возращает временную таблицу Вт_ПрименениеРегиональныхИНВ.
// 
// Параметры:
//  СписокЗапросов - Массив из строка- Список текстов запросов
//  ТаблицаДанных - Строка - имя виртуальной таблицы с данными
Процедура ТекстЗапросаПараметровПримененияРегиональныхИНВ(СписокЗапросов, ТаблицаДанных = Неопределено) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ДанныеДляПримененияРИНВ.Организация КАК Организация,
	|	ДанныеДляПримененияРИНВ.Подразделение КАК Подразделение,
	|	ДанныеДляПримененияРИНВ.Период КАК Дата,
	|	ДанныеДляПримененияРИНВ.ГоловнаяОрганизация КАК ГоловнаяОрганизация
	|ПОМЕСТИТЬ ДанныеДляПримененияРИНВ
	|ИЗ
	|	&ДанныеДокументов КАК ДанныеДляПримененияРИНВ
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляПримененияРИНВ.Организация,
	|	ДанныеДляПримененияРИНВ.Подразделение,
	|	ДанныеДляПримененияРИНВ.Период,
	|	ДанныеДляПримененияРИНВ.ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляПримененияРИНВ.Организация КАК Организация,
	|	ДанныеДляПримененияРИНВ.Подразделение КАК Подразделение,
	|	ДанныеДляПримененияРИНВ.Дата КАК Дата,
	|	ДанныеДляПримененияРИНВ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	МАКСИМУМ(РегистрацииВНалоговомОргане.Период) КАК Период
	|ПОМЕСТИТЬ РегистрацииВНалоговомОрганеПредварительная
	|ИЗ
	|	ДанныеДляПримененияРИНВ КАК ДанныеДляПримененияРИНВ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО ДанныеДляПримененияРИНВ.Организация = РегистрацииВНалоговомОргане.Организация
	|		И ДанныеДляПримененияРИНВ.Подразделение = РегистрацииВНалоговомОргане.Подразделение
	|		И ДанныеДляПримененияРИНВ.Дата >= РегистрацииВНалоговомОргане.Период
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляПримененияРИНВ.Организация,
	|	ДанныеДляПримененияРИНВ.Подразделение,
	|	ДанныеДляПримененияРИНВ.Дата,
	|	ДанныеДляПримененияРИНВ.ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацииВНалоговомОрганеПредварительная.Организация КАК Организация,
	|	РегистрацииВНалоговомОрганеПредварительная.Подразделение КАК Подразделение,
	|	РегистрацииВНалоговомОрганеПредварительная.Дата КАК Дата,
	|	РегистрацииВНалоговомОрганеПредварительная.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РегистрацииВНалоговомОргане.РегистрацияВНалоговомОргане КАК РегистрацияВНалоговомОргане
	|ПОМЕСТИТЬ РегистрацияВНалоговомОрганеДляИНВ
	|ИЗ
	|	РегистрацииВНалоговомОрганеПредварительная КАК РегистрацииВНалоговомОрганеПредварительная
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РегистрацииВНалоговомОргане КАК РегистрацииВНалоговомОргане
	|		ПО РегистрацииВНалоговомОрганеПредварительная.Организация = РегистрацииВНалоговомОргане.Организация
	|		И РегистрацииВНалоговомОрганеПредварительная.Подразделение = РегистрацииВНалоговомОргане.Подразделение
	|		И РегистрацииВНалоговомОрганеПредварительная.Период = РегистрацииВНалоговомОргане.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегистрацияВНалоговомОрганеДляИНВ.Организация КАК Организация,
	|	РегистрацияВНалоговомОрганеДляИНВ.Подразделение КАК Подразделение,
	|	РегистрацияВНалоговомОрганеДляИНВ.Дата КАК Дата,
	|	РегистрацияВНалоговомОрганеДляИНВ.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	РегистрацияВНалоговомОрганеДляИНВ.РегистрацияВНалоговомОргане.КодРегиона КАК КодРегиона
	|ПОМЕСТИТЬ ДанныеРегистраций
	|ИЗ
	|	РегистрацияВНалоговомОрганеДляИНВ КАК РегистрацияВНалоговомОрганеДляИНВ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеДляПримененияРИНВ.Организация,
	|	ДанныеДляПримененияРИНВ.Подразделение,
	|	ДанныеДляПримененияРИНВ.Дата,
	|	ДанныеДляПримененияРИНВ.ГоловнаяОрганизация,
	|	ВЫРАЗИТЬ(ДанныеДляПримененияРИНВ.Организация КАК Справочник.Организации).РегистрацияВНалоговомОргане.КодРегиона
	|ИЗ
	|	ДанныеДляПримененияРИНВ КАК ДанныеДляПримененияРИНВ
	|ГДЕ
	|	НЕ (ДанныеДляПримененияРИНВ.Организация, ДанныеДляПримененияРИНВ.Подразделение, ДанныеДляПримененияРИНВ.Дата) В
	|		(ВЫБРАТЬ
	|			Организация,
	|			Подразделение,
	|			Дата
	|		ИЗ
	|			РегистрацияВНалоговомОрганеДляИНВ)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КодРегиона
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеРегистраций.Организация КАК Организация,
	|	ДанныеРегистраций.Подразделение КАК Подразделение,
	|	ДанныеРегистраций.Дата КАК Дата,
	|	ДанныеРегистраций.ГоловнаяОрганизация КАК ГоловнаяОрганизация,
	|	ДанныеРегистраций.КодРегиона КАК КодРегиона,
	|	МАКСИМУМ(ПрименениеРегиональныхИНВ.Период) КАК Период
	|ПОМЕСТИТЬ РегиональныеИНВПредварительная
	|ИЗ
	|	ДанныеРегистраций КАК ДанныеРегистраций
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименениеРегиональныхИНВ КАК ПрименениеРегиональныхИНВ
	|		ПО ДанныеРегистраций.ГоловнаяОрганизация = ПрименениеРегиональныхИНВ.Организация
	|		И ДанныеРегистраций.КодРегиона = ПрименениеРегиональныхИНВ.КодРегиона
	|		И ДанныеРегистраций.Дата >= ПрименениеРегиональныхИНВ.Период
	|СГРУППИРОВАТЬ ПО
	|	ДанныеРегистраций.Организация,
	|	ДанныеРегистраций.Подразделение,
	|	ДанныеРегистраций.ГоловнаяОрганизация,
	|	ДанныеРегистраций.Дата,
	|	ДанныеРегистраций.КодРегиона
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ГоловнаяОрганизация,
	|	КодРегиона,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РегиональныеИНВПредварительная.ГоловнаяОрганизация КАК Организация,
	|	РегиональныеИНВПредварительная.Подразделение КАК Подразделение,
	|	РегиональныеИНВПредварительная.Дата КАК Дата,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.ПрименяетсяРегиональныйИНВ, ЛОЖЬ) КАК ПрименяетсяРегиональныйИНВ,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.ДоляРасходовИНВ_РБ, 0)/100 КАК ДоляРасходовИНВ_РБ,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.МинимальнаяСтавкаИНВ, 0)/100 КАК МинимальнаяСтавкаИНВ,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.СрокПереносаВычета, 0) КАК СрокПереносаВычета,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.ПрименяетсяФедеральныйИНВ, ЛОЖЬ) КАК ПрименяетсяФедеральныйИНВ,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.ДоляРасходовИНВ_ФБ, 0)/100 КАК ДоляРасходовИНВ_ФБ,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.МинимальныйСрокФактическогоИспользования, 0) КАК
	|		МинимальныйСрокФактическогоИспользования,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.ДатаЗаконаСубъекта, НЕОПРЕДЕЛЕНО) КАК ДатаЗаконаСубъекта,
	|	ЕСТЬNULL(ПрименениеРегиональныхИНВ.НомерЗаконаСубъекта, НЕОПРЕДЕЛЕНО) КАК НомерЗаконаСубъекта
	|ПОМЕСТИТЬ Вт_ПрименениеРегиональныхИНВ
	|ИЗ
	|	РегиональныеИНВПредварительная КАК РегиональныеИНВПредварительная
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПрименениеРегиональныхИНВ КАК ПрименениеРегиональныхИНВ
	|		ПО РегиональныеИНВПредварительная.ГоловнаяОрганизация = ПрименениеРегиональныхИНВ.Организация
	|		И РегиональныеИНВПредварительная.КодРегиона = ПрименениеРегиональныхИНВ.КодРегиона
	|		И РегиональныеИНВПредварительная.Период = ПрименениеРегиональныхИНВ.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Подразделение,
	|	Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрацииВНалоговомОрганеПредварительная
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегистрацияВНалоговомОрганеДляИНВ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ РегиональныеИНВПредварительная";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ДанныеДокументов",
		?(ТаблицаДанных = Неопределено, "ДанныеДокументов", ТаблицаДанных));
		
	СписокЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры
	
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПустыеНастройкиПримененияИНВ()
	
	МенеджерЗаписи = РегистрыСведений.ПрименениеРегиональныхИНВ.СоздатьМенеджерЗаписи();
	МетаданныеРегистра = Метаданные.РегистрыСведений.ПрименениеРегиональныхИНВ;
	
	НастройкаПримененияРегиональныхИНВ = ОбщегоНазначения.СтруктураПоМенеджеруЗаписи(МенеджерЗаписи, МетаданныеРегистра);
	
	Возврат НастройкаПримененияРегиональныхИНВ;
	
КонецФункции

#КонецОбласти
 
#КонецЕсли
