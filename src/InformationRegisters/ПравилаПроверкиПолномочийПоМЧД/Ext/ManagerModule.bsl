// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Ищет доверенность-владельца правила проверки МЧД.
// 
// Параметры:
//  Правило - СправочникСсылка.ПравилаПроверкиПолномочийМЧД
// 
// Возвращаемое значение:
//  - СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//  - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  - Неопределено
//  
Функция Доверенность(Правило) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Правило) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПравилаПроверкиПолномочийПоМЧД.Доверенность КАК Доверенность
		|ИЗ
		|	РегистрСведений.ПравилаПроверкиПолномочийПоМЧД КАК ПравилаПроверкиПолномочийПоМЧД
		|ГДЕ
		|	ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки = &Правило";
	
	Запрос.УстановитьПараметр("Правило", Правило);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Доверенность;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

// Ищет правило проверки МЧД.
// 
// Параметры:
//  МЧД - СправочникСсылка.МашиночитаемыеДоверенностиОрганизаций
//  	- СправочникСсылка.МашиночитаемыеДоверенностиКонтрагентов
//  	- СправочникСсылка.МЧД003
// 
// Возвращаемое значение:
//  Структура:
//   * Правило - СправочникСсылка.ПравилаПроверкиПолномочийМЧД
//   * Настроено - Булево
//   * Используется - Булево
//  
Функция ПравилоПроверки(МЧД) Экспорт
	
	Результат = НовыйСведенияПравилаПроверкиПолномочийМЧД();
	
	Если Не ЗначениеЗаполнено(МЧД) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки КАК Правило,
	|	ПравилаПроверкиПолномочийПоМЧД.ПравилоПроверки.ПравилоНастроено КАК Настроено,
	|	ПравилаПроверкиПолномочийПоМЧД.Используется КАК Используется
	|ИЗ
	|	РегистрСведений.ПравилаПроверкиПолномочийПоМЧД КАК ПравилаПроверкиПолномочийПоМЧД
	|ГДЕ
	|	ПравилаПроверкиПолномочийПоМЧД.Доверенность = &МЧД";
	
	Запрос.УстановитьПараметр("МЧД", МЧД);
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(Результат, Выборка);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
// * Правило - СправочникСсылка.ПравилаПроверкиПолномочийМЧД
// * Настроено - Булево
// * Используется - Булево
Функция НовыйСведенияПравилаПроверкиПолномочийМЧД() 

	Результат = Новый Структура;
	Результат.Вставить("Правило", Справочники.ПравилаПроверкиПолномочийМЧД.ПустаяСсылка());
	Результат.Вставить("Настроено", Ложь);
	Результат.Вставить("Используется", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
