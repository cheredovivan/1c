#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Объект.Организация)
	|	И ЗначениеРазрешено(ФизическоеЛицо)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УдалитьБракованныеПодписиСотрудников(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписиДокументовЭДОБЗК КАК ПодписиДокументовЭДОБЗК
		|			ПО ЭлектронныеПодписи.ПодписанныйОбъект = ПодписиДокументовЭДОБЗК.Объект.ЭлектронныйДокумент
		|				И ЭлектронныеПодписи.Отпечаток = ПодписиДокументовЭДОБЗК.Отпечаток
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
		|			ПО ЭлектронныеПодписи.Отпечаток = СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ЭлектронныеПодписи.ПодписанныйОбъект
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И НЕ ЭлектронныеПодписи.ПодписанныйОбъект ЕСТЬ NULL
		|	И ПодписиДокументовЭДОБЗК.Отпечаток ЕСТЬ NULL
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка ЕСТЬ NULL";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Запрос.УстановитьПараметр("МассивОбновляемых", РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Ссылка"));
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.ЭлектронныйДокумент КАК ПодписанныйОбъект,
		|	ЭлектронныеПодписи.ПорядковыйНомер КАК ПорядковыйНомер
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписиДокументовЭДОБЗК КАК ПодписиДокументовЭДОБЗК
		|			ПО ЭлектронныеПодписи.ПодписанныйОбъект = ПодписиДокументовЭДОБЗК.Объект.ЭлектронныйДокумент
		|				И ЭлектронныеПодписи.Отпечаток = ПодписиДокументовЭДОБЗК.Отпечаток
		|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
		|			ПО ЭлектронныеПодписи.Отпечаток = СертификатыКлючейЭлектроннойПодписиИШифрования.Отпечаток
		|		ПО ДокументЭДОБЗК.ЭлектронныйДокумент = ЭлектронныеПодписи.ПодписанныйОбъект
		|ГДЕ
		|	ДокументЭДОБЗК.Ссылка В(&МассивОбновляемых)
		|	И НЕ ЭлектронныеПодписи.ПодписанныйОбъект ЕСТЬ NULL
		|	И ПодписиДокументовЭДОБЗК.Отпечаток ЕСТЬ NULL
		|	И СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка ЕСТЬ NULL
		|
		|УПОРЯДОЧИТЬ ПО
		|	Ссылка,
		|	ПодписанныйОбъект";
	
	ЭлектронныеДокументы = Новый Массив;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Ссылка") Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		Пока Выборка.Следующий() Цикл
			
			Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.ЭлектронныеПодписи", "ПодписанныйОбъект", Выборка.ПодписанныйОбъект) Тогда
				Продолжить;
			КонецЕсли;
			
			НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(Выборка.ПодписанныйОбъект);
			НаборЗаписей.Отбор.ПорядковыйНомер.Установить(Выборка.ПорядковыйНомер);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() > 0 Тогда
				ДанныеСертификата = НаборЗаписей[0].Сертификат.Получить();
				Если ДанныеСертификата <> Неопределено Тогда
					Если СтрНайти(СтрЗаменить(НРег(ДанныеСертификата), " ", ""), "301206072a85030202230106072a850302021e01") > 0 Тогда
						НаборЗаписей.Очистить();
						ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			ЭлектронныеДокументы.Добавить(Выборка.ПодписанныйОбъект);
			
		КонецЦикла;
		
	КонецЦикла;
	
	ЭДОБЗКВызовСервера.ЗапланироватьДействияСПечатнымиФормами(ЭлектронныеДокументы,
		Перечисления.ДействияСФайламиДокументовЭДОБЗК.ПередатьВКабинетСотрудников);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли