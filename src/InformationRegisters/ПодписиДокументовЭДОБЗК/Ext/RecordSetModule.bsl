#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПрежниеПодписи = РегистрыБЗК.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение);
	УдаляемыеЗаписи = Новый Массив;
	Для Каждого ПрежняяПодпись Из ПрежниеПодписи Цикл
		Если Не ЗначениеЗаполнено(ПрежняяПодпись.Отпечаток) Тогда
			УдаляемыеЗаписи.Добавить(ПрежняяПодпись);
		КонецЕсли;
	КонецЦикла;
	Для Каждого УдаляемаяЗапись Из УдаляемыеЗаписи Цикл
		ПрежниеПодписи.Удалить(УдаляемаяЗапись);
	КонецЦикла;
	Если ЗначениеЗаполнено(ПрежниеПодписи) Тогда
		ДополнительныеСвойства.Вставить("ПрежниеПодписи", ПрежниеПодписи);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("ПрежниеПодписи") Тогда
		ЭлектронныеДокументы = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(ВыгрузитьКолонку("Объект"), "ЭлектронныйДокумент");
		ПрежниеПодписи = ДополнительныеСвойства.ПрежниеПодписи;
		Для Каждого Запись Из ЭтотОбъект Цикл
			СтруктураОтобора = Новый Структура;
			СтруктураОтобора.Вставить("Объект", Запись.Объект);
			СтруктураОтобора.Вставить("ФизическоеЛицо", Запись.ФизическоеЛицо);
			ДанныеПодписей = ПрежниеПодписи.НайтиСтроки(СтруктураОтобора);
			Для Каждого ДанныеПодписи Из ДанныеПодписей Цикл
				Если ДанныеПодписи.Отпечаток = Запись.Отпечаток Тогда
					Продолжить;
				КонецЕсли;
				ЭлектронныйДокумент = ЭлектронныеДокументы.Получить(Запись.Объект);
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("ЭлектронныйДокумент", ЭлектронныйДокумент);
				Запрос.УстановитьПараметр("Отпечаток", ДанныеПодписи.Отпечаток);
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ЭлектронныеПодписи.ПорядковыйНомер КАК ПорядковыйНомер
					|ИЗ
					|	РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
					|ГДЕ
					|	ЭлектронныеПодписи.ПодписанныйОбъект = &ЭлектронныйДокумент
					|	И ЭлектронныеПодписи.Отпечаток = &Отпечаток";
				Выборка = Запрос.Выполнить().Выбрать();
				Если Выборка.Следующий() Тогда
					НаборЗаписей = РегистрыСведений.ЭлектронныеПодписи.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ПодписанныйОбъект.Установить(ЭлектронныйДокумент);
					НаборЗаписей.Отбор.ПорядковыйНомер.Установить(Выборка.ПорядковыйНомер);
					НаборЗаписей.Записать();
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли
