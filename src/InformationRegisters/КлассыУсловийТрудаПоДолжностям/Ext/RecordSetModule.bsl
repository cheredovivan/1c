#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	КонтрольИзмененийПередЗаписью(ЭтотОбъект, Замещение);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	КонтрольИзмененийПриЗаписи(ЭтотОбъект, Замещение);
	ИнтеграцияУправлениеПерсоналомСобытияВнутренний.КлассыУсловийТрудаПоДолжностямПриЗаписи(ТаблицаИзменившихсяДанныхНабора());
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ТаблицаИзменившихсяДанныхНабора() Экспорт
	Возврат ЗарплатаКадрыПериодическиеРегистры.ТаблицаИзменившихсяДанныхНабора(ЭтотОбъект);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура КонтрольИзмененийПередЗаписью(НаборЗаписей, Замещение)

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	НаборЗаписей.ДополнительныеСвойства.Вставить("МенеджерВТИзмененияРегистра", МенеджерВременныхТаблиц);
	
	Должности = РегистрыБЗК.ЗначенияОтбораЗаписейНабора(НаборЗаписей, Замещение, "Должность");
	НаборЗаписей.ДополнительныеСвойства.Вставить("ДолжностиКлассыУсловийТрудаПоДолжностямПередЗаписью",Должности);
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Должности", Должности);
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	*
	|ПОМЕСТИТЬ ВТСтарыйНаборЗаписей 
	|ИЗ
	|	РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК Таблица
	|ГДЕ
	|	Таблица.Должность В (&Должности)";
	Результат = Запрос.Выполнить();
	
	СтарыеЗаписи = Результат.Выбрать();
	СтарыеЗаписи.Следующий();
	НаборЗаписей.ДополнительныеСвойства.Вставить("ЭтоНовыйНабор", СтарыеЗаписи.Количество = 0
		И Не РегистрыБЗК.ЭтоУдалениеНабораЗаписей(Замещение));

КонецПроцедуры

Процедура КонтрольИзмененийПриЗаписи(НаборЗаписей, Замещение)
	
	Запрос = ЗапросВТИзмененияВНаборе(НаборЗаписей);
	Запрос.МенеджерВременныхТаблиц = НаборЗаписей.ДополнительныеСвойства.МенеджерВТИзмененияРегистра;
	КэшТаблицаИзменившихсяДанных = Запрос.Выполнить().Выгрузить();
	НаборЗаписей.ДополнительныеСвойства.Вставить("КэшТаблицаИзменившихсяДанных", КэшТаблицаИзменившихсяДанных);
	
КонецПроцедуры

Функция ЗапросВТИзмененияВНаборе(НаборЗаписей)
	
	Запрос = Новый Запрос;
	
	Должности = Неопределено;
	НаборЗаписей.ДополнительныеСвойства.Свойство("ДолжностиКлассыУсловийТрудаПоДолжностямПередЗаписью",Должности);
	
	Запрос.УстановитьПараметр("Должности", Должности);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Период КАК Период,
	|	ВложенныйЗапрос.Должность КАК Должность,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.ФлагИзменений) = 1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удаление,
	|	ВЫБОР
	|		КОГДА СУММА(ВложенныйЗапрос.ФлагИзменений) = -1
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Добавление
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтарыйНабор.Период КАК Период,
	|		СтарыйНабор.Должность КАК Должность,
	|		СтарыйНабор.КлассУсловийТруда КАК КлассУсловийТруда,
	|		1 КАК ФлагИзменений,
	|		ЛОЖЬ КАК НовыйНабор
	|	ИЗ
	|		ВТСтарыйНаборЗаписей КАК СтарыйНабор
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НовыйНабор.Период,
	|		НовыйНабор.Должность,
	|		НовыйНабор.КлассУсловийТруда,
	|		-1,
	|		ИСТИНА
	|	ИЗ
	|		РегистрСведений.КлассыУсловийТрудаПоДолжностям КАК НовыйНабор
	|	ГДЕ
	|		НовыйНабор.Должность В (&Должности)) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Период,
	|	ВложенныйЗапрос.Должность
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ВложенныйЗапрос.ФлагИзменений) <> 0
	|		ИЛИ ВЫБОР
	|			КОГДА МАКСИМУМ(ВложенныйЗапрос.КлассУсловийТруда) <> МИНИМУМ(ВложенныйЗапрос.КлассУсловийТруда)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ)";
	
	Возврат Запрос;

КонецФункции

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли