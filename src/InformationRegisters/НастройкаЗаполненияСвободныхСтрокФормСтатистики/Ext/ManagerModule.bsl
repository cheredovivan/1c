#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Заполняет настройки заполнения данными из регистра НастройкиРаспределенияВыручки.
//
// Параметры:
//  НаборЗаписейРаспределенияВыручки - РегистрСведенийНаборЗаписей.НастройкиРаспределенияВыручки или Неопределено.
//  Организация - СправочникСсылка.Организации - организация, для которой нужно записать настройку.
//  ОбъектНаблюдения - СправочникСсылка.ОбъектыСтатистическогоНаблюдения - объект наблюдения, для которого нужно записать настройку.
//  НаборЗаписей - РегистрСведенийНаборЗаписей.НастройкаЗаполненияСвободныхСтрокФормСтатистики.
//
Процедура ЗаполнитьНастройкиПоОбъектамНаблюдения(НаборЗаписейРаспределенияВыручки, Организация, 
	ОбъектНаблюдения = Неопределено, НаборЗаписей = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(ОбъектНаблюдения) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ОбъектыСтатистическогоНаблюдения.Ссылка КАК ОбъектНаблюдения
		|ИЗ
		|	Справочник.ОбъектыСтатистическогоНаблюдения КАК ОбъектыСтатистическогоНаблюдения
		|ГДЕ
		|	ОбъектыСтатистическогоНаблюдения.Детализация = ЗНАЧЕНИЕ(Перечисление.ВидыСвободныхСтрокФормСтатистики.ВидыДеятельности)
		|	И НЕ ОбъектыСтатистическогоНаблюдения.ПометкаУдаления";
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			ОбъектНаблюдения = Выборка.ОбъектНаблюдения;
			ЗаполнитьНаборЗаписей(НаборЗаписейРаспределенияВыручки, Организация, ОбъектНаблюдения);
		КонецЦикла;
		
	Иначе
		ЗаполнитьНаборЗаписей(НаборЗаписейРаспределенияВыручки, Организация, ОбъектНаблюдения, НаборЗаписей);
	КонецЕсли;
	
КонецПроцедуры

// Проверяет есть ли записи в регистре, по заданному отбору.
// Параметры:
//	Отбор - Структура - Отбор по которому нужно искать записи.
//	 * Организация - СправочникСсылка.Организации - Организация для которой нужно определить наличие настройки.
//	 * ОбъектНаблюдения - СправочникСсылка.ОбъектыСтатистическогоНаблюдения - 
//		Объект наблюдения для которого нужно определить наличие настройки.
// Возвращаемое значение:
//	Булево - истина - записи есть, Ложь - Записей нет.
//
Функция ЕстьЗаписи(Отбор) Экспорт
	
	Запрос = Новый ПостроительЗапроса;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.Организация
	|ИЗ
	|	РегистрСведений.НастройкаЗаполненияСвободныхСтрокФормСтатистики КАК НастройкаЗаполненияСвободныхСтрокФормСтатистики
	|{ГДЕ
	|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.Организация КАК Организация,
	|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.ОбъектНаблюдения КАК ОбъектНаблюдения,
	|	НастройкаЗаполненияСвободныхСтрокФормСтатистики.ДетализацияОбъектаНаблюдения КАК ДетализацияОбъектаНаблюдения}";
	
	Для Каждого КлючИЗначение Из Отбор Цикл
		ЭлементОтбора = Запрос.Отбор.Добавить(КлючИЗначение.Ключ);
		ЭлементОтбора.Установить(КлючИЗначение.Значение);
	КонецЦикла;
	
	Запрос.Выполнить();
	Возврат Не Запрос.Результат.Пустой();
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаборЗаписей(НаборЗаписейРаспределенияВыручки, Организация, ОбъектНаблюдения, НаборЗаписей = Неопределено)
	
	Если НаборЗаписей = Неопределено Тогда
		НаборЗаписей = СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		НаборЗаписей.Отбор.ОбъектНаблюдения.Установить(ОбъектНаблюдения);
	КонецЕсли;
	
	Если ОбъектНаблюдения.Код = "ПроизводствоИПродажи" Тогда
		ИмяОтчета = "РегламентированныйОтчетСтатистикаФорма1П";
		СписокВерсий = ИнтерфейсыВзаимодействияБРО.ПолучитьВерсииСписковОтчета(ИмяОтчета);
		Если СписокВерсий.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ИмяМакетаСписков = СписокВерсий[СписокВерсий.Количество() - 1].Значение;
		ЭлементыКлассификатораИзМакета = ЭлементыКлассификатораИзМакета(ИмяОтчета, ИмяМакетаСписков);
	Иначе
		ИмяСправочникаДетализации = "КлассификаторВидовЭкономическойДеятельности";
		ПараметрыКлассификатора = ИнтерфейсыВзаимодействияБРО.ПолучитьРасположениеКлассификатораСтатистики(ИмяСправочникаДетализации);
		ДополнятьКодПриЧтенииДанныхКлассификатора = Справочники[ИмяСправочникаДетализации].ДополнятьКодПриЧтенииДанныхКлассификатора();
		ДлинаКодаСправочника = Метаданные.Справочники[ИмяСправочникаДетализации].ДлинаКода;
		
		СписокВерсий = ИнтерфейсыВзаимодействияБРО.ПолучитьВерсииСписковОтчета(ПараметрыКлассификатора.ОтчетИсточникДанных);
		Если СписокВерсий.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		ИмяМакетаСписков = СписокВерсий[СписокВерсий.Количество() - 1].Значение;
		ЭлементыКлассификатораИзМакета = ИнтерфейсыВзаимодействияБРО.ПолучитьЗначенияИзСпискаВыбораОтчета(
			ПараметрыКлассификатора.ОтчетИсточникДанных,
			ИмяМакетаСписков,
			ПараметрыКлассификатора.ОбластьИсточникДанных,
			ДополнятьКодПриЧтенииДанныхКлассификатора,
			ДлинаКодаСправочника);
	КонецЕсли;
	
	Для Каждого Строка Из НаборЗаписейРаспределенияВыручки Цикл
		
		НайденнаяСтрока = ЭлементыКлассификатораИзМакета.Найти(Строка.Детализация.Код, "Код");
		Если НайденнаяСтрока = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = НаборЗаписей.Добавить();
		Запись.ОбъектНаблюдения = ОбъектНаблюдения;
		Запись.ДетализацияОбъектаНаблюдения = Строка.Детализация;
		ЗаполнитьЗначенияСвойств(Запись, Строка);
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Истина);
	НаборЗаписей.Записать();
	
КонецПроцедуры

Функция ЭлементыКлассификатораИзМакета(ИмяОтчета, ИмяМакетаСписков)
	
	ИмяСписка = "ОКВЭД8_9";
	МакетСоставаПоказателей = Отчеты[ИмяОтчета].ПолучитьМакет(ИмяМакетаСписков);
	Список = МакетСоставаПоказателей.Области.Найти(ИмяСписка);
	
	ТаблицаСписка = Новый ТаблицаЗначений;
	Если Список = Неопределено Тогда
		Возврат ТаблицаСписка;
	КонецЕсли;
	
	Если Список.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
		
		ВерхОбласти = Список.Верх;
		НизОбласти = Список.Низ;
		
		ТаблицаСписка.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)));
		ТаблицаСписка.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(255)));
		
		Для НомСтр = ВерхОбласти По НизОбласти Цикл
			КодПоказателя = СокрП(МакетСоставаПоказателей.Область(НомСтр, 1).Текст);
			
			НовСтрока = ТаблицаСписка.Добавить();
			НовСтрока.Код = КодПоказателя;
			НовСтрока.Наименование = СокрП(МакетСоставаПоказателей.Область(НомСтр, 2).Текст);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТаблицаСписка;
	
КонецФункции

#КонецОбласти

#КонецЕсли