
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Обработчик перед записью набора записей
// 
// Параметры:
//  Отказ - Булево
//  Режим - Булево 
//        - РежимЗамещения
Процедура ПередЗаписью(Отказ, Режим)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьХэш(Режим);
	ПрочитатьНачальноеСостояниеЗаписей(Режим);
	
КонецПроцедуры

// Обработчик при записи набора записей
// 
// Параметры:
//  Отказ - Булево
//  Режим - Булево 
//        - РежимЗамещения
Процедура ПриЗаписи(Отказ, Режим)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьПересчетИтоговПоСтатусам(Режим);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ЗаполнениеЗначений

// Заполняет хэш в добавляемых и изменяемых записях
// 
// Параметры:
//  Режим - Булево 
//        - РежимЗамещения
Процедура ЗаполнитьХэш(Знач Режим)
	
	Если ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Режим) Тогда
		Возврат; // Для удаляемых записей расчет не требуется
	КонецЕсли;
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ХэшТипаДокументаИСтатуса = РегистрыСведений.СостоянияДокументовEDI.ХэшТипаДокументаИСтатуса(
			Запись.ТипДокумента, Запись.ТекущийСтатус);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ПересчетИтогов

#Область Проверка

// Имя дополнительного свойства, которые указывает на то, что пересчет по статусам выполнять не требуется
// 
// Возвращаемое значение:
//  Строка - Имя свойства
Функция ИмяСвойстваНеВыполнятьРасчетПоСтатусам()
	Возврат "НеВыполнятьРасчетПоСтатусам";
КонецФункции

// Требуется выполнять пересчет итогов по статусам.
// 
// Возвращаемое значение:
//  Булево - Истина, если требуется выполнять пересчет итогов по статусам.
//           Ложь, в ином случае.
Функция ТребуетсяВыполнятьПересчетИтоговПоСтатусам()
	Возврат ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
		ДополнительныеСвойства, ИмяСвойстваНеВыполнятьРасчетПоСтатусам(), Ложь) = Ложь;
КонецФункции

#КонецОбласти

#Область ЛогикаПересчета

// Получает уникальные значения колонки таблицы значений.
// 
// Параметры:
//  ТаблицаЗначений - ТаблицаЗначений - Таблица, уникальные значения колонки которой требуется получить
//  ИмяКолонки      - Строка          - Имя колонки, уникальные значения которой требуется получить
// 
// Возвращаемое значение:
//  Массив Из Произвольный - Набор уникальных значений колонки таблицы значений
Функция УникальныеЗначенияКолонкиТаблицыЗначений(Знач ТаблицаЗначений, Знач ИмяКолонки)
	
	Контекст = "РегистрыСведений.СостоянияДокументовEDI.УникальныеЗначенияКолонкиТаблицыЗначений";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст, "ТаблицаЗначений", ТаблицаЗначений, Тип("ТаблицаЗначений"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(
		Контекст, "ИмяКолонки", ИмяКолонки, Тип("Строка"));
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		ТаблицаЗначений.Колонки.Найти(ИмяКолонки) <> Неопределено,
		СтрШаблон(
			НСтр(
				"ru = 'Искомая колонка [%1] отсутствует в переданной таблице';
				|en = 'The desired column [%1] is missing in the passed table'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			ИмяКолонки),
		Контекст);
	
	Значения = ТаблицаЗначений.ВыгрузитьКолонку(ИмяКолонки);
	Возврат ОбщегоНазначенияКлиентСервер.СвернутьМассив(Значения);
КонецФункции

// Читает текущее состояние записей перед записью набора
// 
// Параметры:
//  Режим - Булево 
//        - РежимЗамещения
Процедура ПрочитатьНачальноеСостояниеЗаписей(Знач Режим)
	
	Если Не ТребуетсяВыполнятьПересчетИтоговПоСтатусам() Тогда
		Возврат;
	КонецЕсли;
	
	Данные = ОбщегоНазначения.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Режим, 
		"ТипДокумента, ТекущийСтатус, Менеджер, Организация, СостояниеПрикладногоОбъекта");
	
	ДополнительныеСвойства.Вставить("НачальноеСостояниеЗаписей", Данные);
	
КонецПроцедуры

// Выполняет расчет итогов по статусам при записи набора
// 
// Параметры:
//  Режим - Булево 
//        - РежимЗамещения
Процедура ВыполнитьПересчетИтоговПоСтатусам(Знач Режим)
	
	Если Не ТребуетсяВыполнятьПересчетИтоговПоСтатусам() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеДляРасчета = ОбщегоНазначения.ИзменениеЗаписейНабора(
		ДополнительныеСвойства.НачальноеСостояниеЗаписей, 
		ЭтотОбъект, 
		Режим);
	
	РегистрыСведений.ТекущиеИтогиПоСтатусамEDI.РассчитатьИтоги(
		УникальныеЗначенияКолонкиТаблицыЗначений(ДанныеДляРасчета, "ТипДокумента"),
		УникальныеЗначенияКолонкиТаблицыЗначений(ДанныеДляРасчета, "ТекущийСтатус"), 
		УникальныеЗначенияКолонкиТаблицыЗначений(ДанныеДляРасчета, "Менеджер"), 
		УникальныеЗначенияКолонкиТаблицыЗначений(ДанныеДляРасчета, "Организация"),
		УникальныеЗначенияКолонкиТаблицыЗначений(ДанныеДляРасчета, "СостояниеПрикладногоОбъекта"));
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецОбласти

#КонецЕсли
