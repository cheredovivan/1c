#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если (Количество() = 0 Или ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение)) И Модифицированность() Тогда
		Отказ = Истина;
		ВызватьИсключение НСтр("ru = 'Запрещено удалять данные вида приложения';
								|en = 'Cannot delete application type data'");
	КонецЕсли;
	
КонецПроцедуры

// @skip-check data-exchange-load
Процедура ПриЗаписи(Отказ, Замещение)
	
	// ОбменДанными.Загрузка не проверяется, чтобы нельзя было добавить некорректную запись.
	// Проверку нужно выполнять именно в ПриЗаписи, т.к. здесь гарантированно заполнен ОбластьДанныхВспомогательныеДанные.
	
	Если Не ОбщегоНазначения.ЭтоУдалениеНабораЗаписей(Замещение) Тогда
		Для Каждого Запись Из ЭтотОбъект Цикл
			Если Запись.ОбластьДанныхВспомогательныеДанные = 0 Тогда
				ВызватьИсключение НСтр("ru = 'Запрещено устанавливать вид приложения для области со значением разделителя 0';
										|en = 'Cannot set application type for areas with 0 separator'");
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли