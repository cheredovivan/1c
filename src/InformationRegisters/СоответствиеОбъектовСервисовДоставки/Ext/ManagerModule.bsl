
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ОбновлениеВерсииИБ

// Регистрирует данные к обновлению в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	ПолноеИмяРегистра = Метаданные.РегистрыСведений.СоответствиеОбъектовСервисовДоставки.ПолноеИмя();

	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = ПолноеИмяРегистра;

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствиеОбъектовСервисовДоставки.Организация КАК Организация,
	|	СоответствиеОбъектовСервисовДоставки.ТипГрузоперевозки КАК ТипГрузоперевозки,
	|	СоответствиеОбъектовСервисовДоставки.ТипОбъекта КАК ТипОбъекта
	|ИЗ
	|	РегистрСведений.СоответствиеОбъектовСервисовДоставки КАК СоответствиеОбъектовСервисовДоставки
	|ГДЕ
	|	СоответствиеОбъектовСервисовДоставки.ТипОбъекта = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектовСервисовДоставки.ПунктВыдачи)
	|	И СоответствиеОбъектовСервисовДоставки.Организация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)";
	
	Выгрузка = Запрос.Выполнить();

	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Выгрузка, ДополнительныеПараметры);
КонецПроцедуры

// Обрабатывает данные, зарегистрированные в плане обмена ОбновлениеИнформационнойБазы
//  см. Стандарты и методики разработки прикладных решений: Параллельный режим отложенного обновления.
// 
// Параметры:
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	МетаданныеОбъекта = Метаданные.РегистрыСведений.СоответствиеОбъектовСервисовДоставки;
	ПолноеИмяОбъекта  = МетаданныеОбъекта.ПолноеИмя();
	
	Если ОбновлениеИнформационнойБазы.ЕстьЗаблокированныеПредыдущимиОчередямиДанные(
		Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	ПараметрыВыборки = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыВыборкиДанныхДляОбработки();
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ВыбратьИзмеренияНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь, ПолноеИмяОбъекта, ПараметрыВыборки);
	
	ЕстьОтработанныеЗаписи = Ложь;
	ПроизошлаОшибка = Ложь;
	ТекстСообщения = "";
	
	Пока ОбновляемыеДанные.Следующий() Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			НаборЗаписейРегистра = СоздатьНаборЗаписей();
			НаборЗаписейРегистра.Отбор.Организация.Установить(ОбновляемыеДанные.Организация);
			НаборЗаписейРегистра.Отбор.ТипГрузоперевозки.Установить(ОбновляемыеДанные.ТипГрузоперевозки);
			НаборЗаписейРегистра.Отбор.ТипОбъекта.Установить(ОбновляемыеДанные.ТипОбъекта);
			
			ОбщегоНазначенияБЭД.УстановитьУправляемуюБлокировкуПоНаборуЗаписей(НаборЗаписейРегистра);
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписейРегистра);
			
			ЕстьОтработанныеЗаписи = Истина;
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
				
			ШаблонСообщения = НСтр("ru = 'Не удалось очистить записи для %1 по причине: %2%3';
									|en = 'Cannot clear the records for %1 due to: %2%3'");
			ПредставлениеОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстСообщения = СтрШаблон(
				ШаблонСообщения,
				СервисДоставкиКлиентСервер.ПеречислениеГрузоперевозчика(ОбновляемыеДанные.ТипГрузоперевозки),
				Символы.ПС, 
				ПредставлениеОшибки);
			
			Событие = ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации();
			ЗаписьЖурналаРегистрации(
				Событие, 
				УровеньЖурналаРегистрации.Ошибка,
				МетаданныеОбъекта,
				ОбновляемыеДанные.УдалитьИдентификаторДокумента,
				ТекстСообщения);
			
			ПроизошлаОшибка = Истина;
			
		КонецПопытки;
		
	КонецЦикла;
	
	Если Не ЕстьОтработанныеЗаписи И ПроизошлаОшибка Тогда
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = 
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

// Устанавливает соответствие между идентификатором объекта внешней системы
// и объектом информационной базы.
// 
// Параметры:
//	ПараметрыЗаписи - Структура - параметры соответствия.
//
Процедура ДобавитьЗапись(Знач ПараметрыЗаписи) Экспорт
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоответствиеОбъектовСервисовДоставки");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.УстановитьЗначение("Организация", ПараметрыЗаписи.Организация);
		ЭлементБлокировки.УстановитьЗначение("ТипГрузоперевозки", ПараметрыЗаписи.ТипГрузоперевозки);
		ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ПараметрыЗаписи.ТипОбъекта);
		
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ПараметрыЗаписи);
		
		Если ЗначениеЗаполнено(МенеджерЗаписи.Значение) Или МенеджерЗаписи.ЗначениеПоУмолчанию Тогда
			
			МенеджерЗаписи.Записать();
			
			Если МенеджерЗаписи.ЗначениеПоУмолчанию Тогда
				
				Запрос = Новый Запрос;
				Запрос.УстановитьПараметр("Организация", МенеджерЗаписи.Организация);
				Запрос.УстановитьПараметр("ТипГрузоперевозки", МенеджерЗаписи.ТипГрузоперевозки);
				Запрос.УстановитьПараметр("ТипОбъекта", МенеджерЗаписи.ТипОбъекта);
				Запрос.УстановитьПараметр("ИдентификаторОбъекта", МенеджерЗаписи.ИдентификаторОбъекта);
				Запрос.Текст =
					"ВЫБРАТЬ
					|	ТаблицаСоответствий.Организация КАК Организация,
					|	ТаблицаСоответствий.ТипГрузоперевозки КАК ТипГрузоперевозки,
					|	ТаблицаСоответствий.ТипОбъекта КАК ТипОбъекта,
					|	ТаблицаСоответствий.ИдентификаторОбъекта КАК ИдентификаторОбъекта
					|ИЗ
					|	РегистрСведений.СоответствиеОбъектовСервисовДоставки КАК ТаблицаСоответствий
					|ГДЕ
					|	ТаблицаСоответствий.Организация = &Организация
					|	И ТаблицаСоответствий.ТипГрузоперевозки = &ТипГрузоперевозки
					|	И ТаблицаСоответствий.ТипОбъекта = &ТипОбъекта
					|	И ТаблицаСоответствий.ИдентификаторОбъекта <> &ИдентификаторОбъекта
					|	И ТаблицаСоответствий.ЗначениеПоУмолчанию";
				
				Выборка = Запрос.Выполнить().Выбрать();
				Пока Выборка.Следующий() Цикл
					
					МенеджерЗаписи = СоздатьМенеджерЗаписи();
					ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Выборка);
					
					МенеджерЗаписи.Прочитать();
					Если МенеджерЗаписи.Выбран() Тогда
						МенеджерЗаписи.ЗначениеПоУмолчанию = Ложь;
						МенеджерЗаписи.Записать();
					КонецЕсли;
					
				КонецЦикла;
				
			КонецЕсли;
			
		Иначе
			
			МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда
				МенеджерЗаписи.Удалить();
			КонецЕсли;
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры

// Удаляет записи по переданному массиву идентификаторов бъекта
// 
// Параметры:
//  ПараметрыЗаписи - Структура - структура с ключами:
//  	* Организация - ОпределяемыйТип.ОрганизацияСервисДоставки
//  	* ТипГрузоперевозки - Число
//  	* ТипОбъекта - ПеречислениеСсылка.ТипыОбъектовСервисовДоставки
//  ИдентификаторыКУдалению - Массив Из Строка - Идентификаторы пунктов выдачи, подлежащих удалению к удалению
Процедура УдалитьЗаписиПоИдентификаторам(Знач ПараметрыЗаписи, Знач ИдентификаторыКУдалению) Экспорт
	
	Если Не ЗначениеЗаполнено(ИдентификаторыКУдалению) Тогда
		Возврат;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных();
	ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоответствиеОбъектовСервисовДоставки");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Организация", ПараметрыЗаписи.Организация);
	ЭлементБлокировки.УстановитьЗначение("ТипГрузоперевозки", ПараметрыЗаписи.ТипГрузоперевозки);
	ЭлементБлокировки.УстановитьЗначение("ТипОбъекта", ПараметрыЗаписи.ТипОбъекта);
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка	
		Блокировка.Заблокировать();

		РежимУдаления = ОбщегоНазначения.РежимУдаленияНабораЗаписей();
		Если РежимУдаления = Неопределено Тогда
			
			УдаляемыеЗаписи = СоздатьНаборЗаписей();
			Для Каждого Параметр Из ПараметрыЗаписи Цикл
				УдаляемыеЗаписи.Отбор[Параметр.Ключ].Установить(Параметр.Значение);
			КонецЦикла;
			Для Каждого Идентификатор Из ИдентификаторыКУдалению Цикл
				УдаляемыеЗаписи.Отбор.ИдентификаторОбъекта.Установить(Идентификатор);
				УдаляемыеЗаписи.Записать();
			КонецЦикла;
			
		Иначе
			
			УдаляемыеЗаписи = СоздатьНаборЗаписей();
			Для Каждого Идентификатор Из ИдентификаторыКУдалению Цикл
				УдаляемаяЗапись = УдаляемыеЗаписи.Добавить();
				ЗаполнитьЗначенияСвойств(УдаляемаяЗапись, ПараметрыЗаписи);
				УдаляемаяЗапись.ИдентификаторОбъекта = Идентификатор;
			КонецЦикла;
			УдаляемыеЗаписи.Записать(РежимУдаления);
			
		КонецЕсли;
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
