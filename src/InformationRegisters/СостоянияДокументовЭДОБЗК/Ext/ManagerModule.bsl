#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Объект)";
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

// Регистрирует получение оригиналов документов (на бумажном носителе) завизированных собственноручными подписями.
//
// Параметры:
//  ДокументыЭДОБЗК     - Массив ссылок на ДокументЭДОБЗК
//  ОригиналыПолучены - Булево, Истина если оригиналы получены
//
Процедура ЗарегистрироватьПолучениеПодписанныхОригиналов(ДокументыЭДОБЗК, ОригиналыПолучены) Экспорт
	
	ДанныеДляРасчета = ДанныеДляРасчетаСостоянийДокументовЭДОБЗК(ДокументыЭДОБЗК);
	Для Каждого ДанныеДокумента Из ДанныеДляРасчета Цикл
		
		НачатьТранзакцию();
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДокументыЭДОБЗККПересчетуСостояний");
			ЭлементБлокировки.УстановитьЗначение("Объект", ДанныеДокумента.Ключ);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияДокументовЭДОБЗК");
			ЭлементБлокировки.УстановитьЗначение("Объект", ДанныеДокумента.Ключ);
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СостоянияДокументовЭДОБЗК.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДанныеДокумента.Ключ);
			
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
			Иначе
				Запись = НаборЗаписей[0];
			КонецЕсли;
			
			Запись.ПодписанныеОригиналыПолучены = ОригиналыПолучены;
			Если ОригиналыПолучены Тогда
				Запись.МоментПолученияОригиналов = ТекущаяДатаСеанса();
				Запись.ПолучениеОригиналовЗарегистрировал = Пользователи.ТекущийПользователь();
			Иначе
				Запись.МоментПолученияОригиналов = '00010101';
				Запись.ПолучениеОригиналовЗарегистрировал = Справочники.Пользователи.ПустаяСсылка();
			КонецЕсли;
			
			ЗаполнитьСостояниеДокументаЭДОБЗК(Запись, ДанныеДокумента.Значение);
			Запись.Объект = ДанныеДокумента.Ключ;
			
			НаборЗаписей.Записать();
			
			НаборЗаписей = РегистрыСведений.ДокументыЭДОБЗККПересчетуСостояний.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДанныеДокумента.Ключ);
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			Ошибка = ИнформацияОбОшибке();
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура РассчитатьСостоянияДокументовЭДОБЗК(ДокументыЭДОБЗК) Экспорт
	
	ДанныеДляРасчета = ДанныеДляРасчетаСостоянийДокументовЭДОБЗК(ДокументыЭДОБЗК);
	Для Каждого ДанныеДокумента Из ДанныеДляРасчета Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДокументыЭДОБЗККПересчетуСостояний");
			ЭлементБлокировки.УстановитьЗначение("Объект", ДанныеДокумента.Ключ);
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СостоянияДокументовЭДОБЗК");
			ЭлементБлокировки.УстановитьЗначение("Объект", ДанныеДокумента.Ключ);
			
			Блокировка.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.СостоянияДокументовЭДОБЗК.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДанныеДокумента.Ключ);
			
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
			Иначе
				Запись = НаборЗаписей[0];
			КонецЕсли;
			
			ЗаполнитьСостояниеДокументаЭДОБЗК(Запись, ДанныеДокумента.Значение);
			Запись.Объект = ДанныеДокумента.Ключ;
			
			НаборЗаписей.Записать();
			
			НаборЗаписей = РегистрыСведений.ДокументыЭДОБЗККПересчетуСостояний.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(ДанныеДокумента.Ключ);
			НаборЗаписей.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			Ошибка = ИнформацияОбОшибке();
			ОтменитьТранзакцию();
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьСостояния(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбновленных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбновленных") Тогда
			МассивОбновленных = ПараметрыОбновления.МассивОбновленных;
		Иначе
			МассивОбновленных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбновленных", МассивОбновленных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбновленных", МассивОбновленных);
	
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЭДОБЗК КАК СостоянияДокументовЭДОБЗК
		|		ПО ДокументЭДОБЗК.Ссылка = СостоянияДокументовЭДОБЗК.Объект
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДокументыЭДОБЗККПересчетуСостояний КАК ДокументыЭДОБЗККПересчетуСостояний
		|		ПО ДокументЭДОБЗК.Ссылка = ДокументыЭДОБЗККПересчетуСостояний.Объект
		|ГДЕ
		|	НЕ ДокументЭДОБЗК.Ссылка В (&МассивОбновленных)
		|	И СостоянияДокументовЭДОБЗК.Объект ЕСТЬ NULL
		|	И ДокументыЭДОБЗККПересчетуСостояний.Объект ЕСТЬ NULL";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	
	ДокументыКЗаполнениюСостояний = Новый Массив;
	Пока Выборка.Следующий() Цикл
		ДокументыКЗаполнениюСостояний.Добавить(Выборка.Ссылка);
	КонецЦикла;
	
	ДанныеДляРасчета = ДанныеДляРасчетаСостоянийДокументовЭДОБЗК(ДокументыКЗаполнениюСостояний);
	
	Выборка.Сбросить();
	Пока Выборка.Следующий() Цикл
		
		МассивОбновленных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(ПараметрыОбновления, "РегистрСведений.СостоянияДокументовЭДОБЗК", "Объект", Выборка.Ссылка) Тогда
			Продолжить;
		КонецЕсли;
		
		ДанныеДокумента = ДанныеДляРасчета.Получить(Выборка.Ссылка);
		Если ДанныеДокумента <> Неопределено Тогда
			
			НаборЗаписей = РегистрыСведений.СостоянияДокументовЭДОБЗК.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
			
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() = 0 Тогда
				Запись = НаборЗаписей.Добавить();
			Иначе
				Запись = НаборЗаписей[0];
			КонецЕсли;
			
			ЗаполнитьСостояниеДокументаЭДОБЗК(Запись, ДанныеДокумента);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ДанныеДляРасчетаСостоянийДокументовЭДОБЗК(ДокументыЭДОБЗК)
	
	ДанныеДляРасчетаСостояний = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументыЭДОБЗК", ДокументыЭДОБЗК);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ДокументЭДОБЗК.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗК.ЭлектронныйДокумент КАК ЭлектронныйДокумент,
		|	ДокументЭДОБЗК.Расширение КАК Расширение,
		|	ДокументЭДОБЗК.Внешний КАК Внешний,
		|	ДокументЭДОБЗК.ВариантПодписания КАК ВариантПодписания
		|ПОМЕСТИТЬ ВТДанныеДокументовЭДОБЗК
		|ИЗ
		|	Документ.ДокументЭДОБЗК КАК ДокументЭДОБЗК
		|ГДЕ
		|	ДокументЭДОБЗК.Ссылка В(&ДокументыЭДОБЗК)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокументовЭДОБЗК.Ссылка КАК Ссылка,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ТаблицаРегистра.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК ОжидаетПодписания,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ТаблицаРегистра.Действие = ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовЭДОБЗК.Ознакомиться)
		|				ТОГДА ИСТИНА
		|			ИНАЧЕ ЛОЖЬ
		|		КОНЕЦ) КАК ТребуетВнимания
		|ПОМЕСТИТЬ ВТЗапланированныеДействия
		|ИЗ
		|	РегистрСведений.ЗапланированныеДействияСФайламиДокументовЭДОБЗК КАК ТаблицаРегистра
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|		ПО ТаблицаРегистра.ПрисоединенныйФайл = ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент
		|ГДЕ
		|	ТаблицаРегистра.Действие В (ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовЭДОБЗК.Подписать), ЗНАЧЕНИЕ(Перечисление.ДействияСФайламиДокументовЭДОБЗК.Ознакомиться))
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокументовЭДОБЗК.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПодписиДокументовЭДОБЗК.Объект КАК Объект,
		|	ПодписиДокументовЭДОБЗК.ФизическоеЛицо КАК ФизическоеЛицо,
		|	ПодписиДокументовЭДОБЗК.Отпечаток КАК Отпечаток,
		|	ПодписиДокументовЭДОБЗК.ДатаПодписи КАК ДатаПодписи,
		|	ПодписиДокументовЭДОБЗК.РезультатСогласования КАК РезультатСогласования
		|ПОМЕСТИТЬ ВТПодписи
		|ИЗ
		|	РегистрСведений.ПодписиДокументовЭДОБЗК КАК ПодписиДокументовЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|		ПО ПодписиДокументовЭДОБЗК.Объект = ДанныеДокументовЭДОБЗК.Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДокументЭДОБЗКВнешниеПодписанты.Ссылка КАК Ссылка,
		|	ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо КАК ФизическоеЛицо,
		|	МАКСИМУМ(ВЫБОР
		|			КОГДА ПодписанныеОбъектыПользователей.ПодписанныйОбъект ЕСТЬ NULL
		|				ТОГДА ЛОЖЬ
		|			ИНАЧЕ ИСТИНА
		|		КОНЕЦ) КАК ЕстьПодписи
		|ПОМЕСТИТЬ ВТПодписанты
		|ИЗ
		|	Документ.ДокументЭДОБЗК.ВнешниеПодписанты КАК ДокументЭДОБЗКВнешниеПодписанты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|		ПО ДокументЭДОБЗКВнешниеПодписанты.Ссылка = ДанныеДокументовЭДОБЗК.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеОбъектыПользователей КАК ПодписанныеОбъектыПользователей
		|		ПО (ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент = ПодписанныеОбъектыПользователей.ПодписанныйОбъект)
		|
		|СГРУППИРОВАТЬ ПО
		|	ДокументЭДОБЗКВнешниеПодписанты.Ссылка,
		|	ДокументЭДОБЗКВнешниеПодписанты.ФизическоеЛицо
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеДокументовЭДОБЗК.Ссылка КАК Объект,
		|	ДанныеДокументовЭДОБЗК.Расширение КАК Расширение,
		|	ДанныеДокументовЭДОБЗК.Внешний КАК Внешний,
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Подписанты.ФизическоеЛицо) КАК КоличествоПодписантов,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеТребуется)
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) = ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
		|				ТОГДА 1
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоОтказов,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеТребуется)
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
		|				ТОГДА ВЫБОР
		|						КОГДА ЕСТЬNULL(Подписи.Отпечаток, """") <> """"
		|								И ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.Требуется)
		|							ТОГДА 1
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоУНЭПУКЭП,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеТребуется)
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
		|				ТОГДА ВЫБОР
		|						КОГДА ЕСТЬNULL(Подписи.Отпечаток, НЕОПРЕДЕЛЕНО) = """"
		|								ИЛИ ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеобходимаСобственноручнаяПодпись)
		|							ТОГДА 1
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоПЭП,
		|	СУММА(ВЫБОР
		|			КОГДА ДанныеДокументовЭДОБЗК.ВариантПодписания = ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеТребуется)
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(Подписи.РезультатСогласования, ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Перечисление.РезультатыСогласованияБЗК.Отклонено)
		|				ТОГДА ВЫБОР
		|						КОГДА Подписи.ФизическоеЛицо ЕСТЬ NULL
		|								И ДанныеДокументовЭДОБЗК.ВариантПодписания <> ЗНАЧЕНИЕ(Перечисление.ВариантыПодписанияДокументовЭДОБЗК.НеобходимаСобственноручнаяПодпись)
		|							ТОГДА 1
		|						ИНАЧЕ 0
		|					КОНЕЦ
		|			ИНАЧЕ 0
		|		КОНЕЦ) КАК КоличествоНеОзнакомленных,
		|	ЕСТЬNULL(Подписанты.ЕстьПодписи, ЛОЖЬ) КАК ЕстьПодписи,
		|	ЕСТЬNULL(ЗапланированныеДействия.ОжидаетПодписания, ЛОЖЬ) КАК ОжидаетПодписания,
		|	ЕСТЬNULL(ЗапланированныеДействия.ТребуетВнимания, ЛОЖЬ) КАК ТребуетВнимания,
		|	ЕСТЬNULL(ЭлектронныеПодписи.КоличествоПодписей, 0) КАК ОбщееКоличествоУНЭПУКЭП,
		|	ЕСТЬNULL(ПодписанныеПечатныеФормы.ЭтоПечатнаяФорма, ЛОЖЬ) КАК ЭтоПечатнаяФорма,
		|	ДанныеДокументовЭДОБЗК.ВариантПодписания КАК ВариантПодписания
		|ИЗ
		|	ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТПодписанты КАК Подписанты
		|		ПО ДанныеДокументовЭДОБЗК.Ссылка = Подписанты.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПодписи КАК Подписи
		|		ПО ДанныеДокументовЭДОБЗК.Ссылка = Подписи.Объект
		|			И (Подписанты.ФизическоеЛицо = Подписи.ФизическоеЛицо)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗапланированныеДействия КАК ЗапланированныеДействия
		|		ПО ДанныеДокументовЭДОБЗК.Ссылка = ЗапланированныеДействия.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ПодписанныеПечатныеФормы.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|			МАКСИМУМ(ИСТИНА) КАК ЭтоПечатнаяФорма
		|		ИЗ
		|			ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПодписанныеПечатныеФормы КАК ПодписанныеПечатныеФормы
		|				ПО ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ПодписанныеПечатныеФормы.ПрисоединенныйФайл) КАК ПодписанныеПечатныеФормы
		|		ПО ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент = ПодписанныеПечатныеФормы.ПрисоединенныйФайл
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			ЭлектронныеПодписи.ПодписанныйОбъект КАК ПодписанныйОбъект,
		|			КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЭлектронныеПодписи.ПорядковыйНомер) КАК КоличествоПодписей
		|		ИЗ
		|			ВТДанныеДокументовЭДОБЗК КАК ДанныеДокументовЭДОБЗК
		|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЭлектронныеПодписи КАК ЭлектронныеПодписи
		|				ПО ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент = ЭлектронныеПодписи.ПодписанныйОбъект
		|		
		|		СГРУППИРОВАТЬ ПО
		|			ЭлектронныеПодписи.ПодписанныйОбъект) КАК ЭлектронныеПодписи
		|		ПО ДанныеДокументовЭДОБЗК.ЭлектронныйДокумент = ЭлектронныеПодписи.ПодписанныйОбъект
		|
		|СГРУППИРОВАТЬ ПО
		|	ДанныеДокументовЭДОБЗК.Ссылка,
		|	ДанныеДокументовЭДОБЗК.Расширение,
		|	ДанныеДокументовЭДОБЗК.Внешний,
		|	Подписанты.ЕстьПодписи,
		|	ЗапланированныеДействия.ОжидаетПодписания,
		|	ЗапланированныеДействия.ТребуетВнимания,
		|	ЭлектронныеПодписи.КоличествоПодписей,
		|	ПодписанныеПечатныеФормы.ЭтоПечатнаяФорма,
		|	ДанныеДокументовЭДОБЗК.ВариантПодписания";
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеСостояний = ОбщегоНазначения.ТаблицаЗначенийВМассив(Запрос.Выполнить().Выгрузить());
	УстановитьПривилегированныйРежим(Ложь);
	Для Каждого ДанныеОбъекта Из ДанныеСостояний Цикл
		ДанныеДляРасчетаСостояний.Вставить(ДанныеОбъекта.Объект, ДанныеОбъекта);
	КонецЦикла;
	
	Возврат ДанныеДляРасчетаСостояний
	
КонецФункции

Процедура ЗаполнитьСостояниеДокументаЭДОБЗК(Запись, ДанныеДокумента)
	
	ЗаполнитьЗначенияСвойств(Запись, ДанныеДокумента);
	
	СостояниеПодписей = "";
	Запись.КоличествоПодписейОтветственныхЛиц = Макс(ДанныеДокумента.ОбщееКоличествоУНЭПУКЭП - ДанныеДокумента.КоличествоУНЭПУКЭП, 0);
	Если Запись.КоличествоОтказов > 0 Тогда
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Отклонено (%1)';
				|en = 'Rejected (%1)'"),
			Запись.КоличествоОтказов);
	ИначеЕсли Запись.КоличествоНеОзнакомленных > 0
		И Запись.КоличествоУНЭПУКЭП = 0
		И Запись.КоличествоПЭП = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Не подписано (%1)';
				|en = 'Not signed (%1)'"),
			Запись.КоличествоНеОзнакомленных);
	ИначеЕсли Запись.КоличествоУНЭПУКЭП > 0
		И Запись.КоличествоПЭП = 0
		И Запись.КоличествоНеОзнакомленных = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Подписано ЭП (%1)';
				|en = 'Digitally signed (%1)'"),
			Запись.КоличествоУНЭПУКЭП);
	ИначеЕсли Запись.КоличествоПЭП > 0
		И Запись.КоличествоУНЭПУКЭП = 0
		И Запись.КоличествоНеОзнакомленных = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Требуется собственноручная подпись (%1)';
				|en = 'Sign manually (%1)'"),
			Запись.КоличествоПЭП);
	ИначеЕсли Запись.КоличествоУНЭПУКЭП > 0
		И Запись.КоличествоПЭП > 0
		И Запись.КоличествоНеОзнакомленных = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Подписано ЭП (%1), требуется собственноручная подпись (%2)';
				|en = 'Digitally signed (%1), sign manually (%2)'"),
			Запись.КоличествоУНЭПУКЭП,
			Запись.КоличествоПЭП);
	ИначеЕсли Запись.КоличествоУНЭПУКЭП > 0
		И Запись.КоличествоНеОзнакомленных > 0
		И Запись.КоличествоПЭП = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Подписано ЭП (%1), не подписано (%2)';
				|en = 'Digitally signed (%1), not signed (%2)'"),
			Запись.КоличествоУНЭПУКЭП,
			Запись.КоличествоНеОзнакомленных);
	ИначеЕсли Запись.КоличествоПЭП > 0
		И Запись.КоличествоНеОзнакомленных > 0
		И Запись.КоличествоУНЭПУКЭП = 0 Тогда
		
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Требуется собственноручная подпись (%1), не подписано (%2)';
				|en = 'Sign manually (%1), not signed (%2)'"),
			Запись.КоличествоПЭП,
			Запись.КоличествоНеОзнакомленных);
	ИначеЕсли Запись.КоличествоУНЭПУКЭП = 0
		И Запись.КоличествоПЭП = 0
		И Запись.КоличествоНеОзнакомленных = 0 Тогда
		
		СостояниеПодписей = НСтр("ru = 'Подписи не требуются';
								|en = 'No signatures required'");
	Иначе
		СостояниеПодписей = СтрШаблон(
			НСтр("ru = 'Подписано ЭП (%1), требуется собственноручная подпись (%2), не подписано (%3)';
				|en = 'Digitally signed (%1), sign manually (%2), not signed (%3)'"),
			Запись.КоличествоУНЭПУКЭП,
			Запись.КоличествоПЭП,
			Запись.КоличествоНеОзнакомленных);
	КонецЕсли;
	Если ДанныеДокумента.ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.НеТребуется Тогда
		Запись.СостояниеПодписей = "";
	Иначе
		Если Запись.КоличествоПодписантов = 1 Тогда
			Запись.СостояниеПодписей = СтрЗаменить(СостояниеПодписей, " (1)", "");
		Иначе
			Запись.СостояниеПодписей = СостояниеПодписей;
		КонецЕсли;
		Если ДанныеДокумента.ВариантПодписания = Перечисления.ВариантыПодписанияДокументовЭДОБЗК.НеобходимаСобственноручнаяПодпись Тогда
			Если Запись.ПодписанныеОригиналыПолучены Тогда
				Запись.СостояниеПодписей = СтрЗаменить(Запись.СостояниеПодписей, "требуется собственноручная подпись", "оригиналы получены");
				Запись.СостояниеПодписей = СтрЗаменить(Запись.СостояниеПодписей, "Требуется собственноручная подпись", "Оригиналы получены");
			КонецЕсли;
		Иначе
			Запись.ПодписанныеОригиналыПолучены = Ложь;
			Запись.МоментПолученияОригиналов = '00010101';
			Запись.ПолучениеОригиналовЗарегистрировал = Справочники.Пользователи.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	Если ДанныеДокумента.ОбщееКоличествоУНЭПУКЭП > 0 Тогда
		Если ДанныеДокумента.ЭтоПечатнаяФорма Тогда
			Если ДанныеДокумента.Внешний Тогда
				Если ЭДОБЗККлиентСервер.ЭтоРасширениеPDFДокумента(ДанныеДокумента.Расширение) Тогда
					Запись.ЕстьВерсияДляПечати = Истина;
				Иначе
					Запись.ЕстьВерсияДляПечати = ЭДОБЗККлиентСервер.ЭтоРасширениеФайлаСПредставлением(ДанныеДокумента.Расширение);
				КонецЕсли;
			Иначе
				Запись.ЕстьВерсияДляПечати = Не ЭДОБЗККлиентСервер.ЭтоРасширениеJSONДокумента(ДанныеДокумента.Расширение);
			КонецЕсли;
		Иначе
			Запись.ЕстьВерсияДляПечати = ЭДОБЗККлиентСервер.ЭтоРасширениеФайлаСПредставлением(ДанныеДокумента.Расширение);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьСостояниеПриемаОригиналовСогласийНаПрисоединениеКЭДО(ПараметрыОбновления = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Если ПараметрыОбновления = Неопределено Тогда
		МассивОбработанных = Новый Массив;
	Иначе
		
		Если ПараметрыОбновления.Свойство("МассивОбработанных") Тогда
			МассивОбработанных = ПараметрыОбновления.МассивОбработанных;
		Иначе
			МассивОбработанных = Новый Массив;
			ПараметрыОбновления.Вставить("МассивОбработанных", МассивОбработанных);
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.УстановитьПараметр("МассивОбработанных", МассивОбработанных);
	
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1000
		|	СостоянияДокументовЭДОБЗК.Объект КАК Ссылка
		|ИЗ
		|	РегистрСведений.СостояниеСогласияНаПрисоединениеКЭДОБЗК КАК СостояниеСогласияНаПрисоединениеКЭДОБЗК
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияДокументовЭДОБЗК КАК СостоянияДокументовЭДОБЗК
		|		ПО СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка = СостоянияДокументовЭДОБЗК.Объект.ОснованиеДокумента
		|			И (НЕ СостоянияДокументовЭДОБЗК.ПодписанныеОригиналыПолучены)
		|ГДЕ
		|	НЕ СостояниеСогласияНаПрисоединениеКЭДОБЗК.Ссылка В (&МассивОбработанных)
		|	И СостояниеСогласияНаПрисоединениеКЭДОБЗК.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСогласийНаПрисоединениеКЭДОБЗК.Согласие)
		|	И СостояниеСогласияНаПрисоединениеКЭДОБЗК.СостояниеПриема = ЗНАЧЕНИЕ(Перечисление.СостоянияПриемаСогласияНаПрисоединениеКЭДОБЗК.Подтверждено)";
	
	Если ПараметрыОбновления = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1000","");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Истина);
		Возврат;
	КонецЕсли;
	
	ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.УстановитьПараметрОбновления(ПараметрыОбновления, "ОбработкаЗавершена", Ложь);
	Пока Выборка.Следующий() Цикл
		
		МассивОбработанных.Добавить(Выборка.Ссылка);
		Если Не ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ПодготовитьОбновлениеДанных(
			ПараметрыОбновления, "РегистрСведений.СостоянияДокументовЭДОБЗК", "Объект", Выборка.Ссылка) Тогда
			
			Продолжить;
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.СостоянияДокументовЭДОБЗК.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Ссылка);
		НаборЗаписей.Прочитать();
		Если НаборЗаписей.Выбран() Тогда
			Запись = НаборЗаписей[0];
			Запись.ПодписанныеОригиналыПолучены = Истина;
			Запись.МоментПолученияОригиналов = ТекущаяДатаСеанса();
			Запись.ПолучениеОригиналовЗарегистрировал = Пользователи.ТекущийПользователь();
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
		
		ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ЗавершитьОбновлениеДанных(ПараметрыОбновления);
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли



