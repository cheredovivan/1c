#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ДокументыПоНМА.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.27";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("126c18f7-b03c-49db-bf0a-9799c94b51f1");
	Обработчик.Многопоточный = Истина;
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ДокументыПоНМА.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Комментарий = НСтр(
		"ru = 'Удаляет записи в регистре сведений ""Документы по НМА"" движения по документу ""Распределение НДС"".';
		|en = 'Deletes records in the ""Intangible asset documents"" information register for the ""VAT allocation"" document.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ДокументыПоНМА.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ДокументыПоНМА.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ДокументыПоНМА";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ДокументыПоНМА");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ 
	|	ДокументыПоНМА.ТипСсылки КАК ТипСсылки,
	|	ДокументыПоНМА.ХозяйственнаяОперация КАК ХозяйственнаяОперация,
	|	ДокументыПоНМА.НомерЗаписи КАК НомерЗаписи,
	|	ДокументыПоНМА.Дата КАК Дата,
	|	ДокументыПоНМА.НематериальныйАктив КАК НематериальныйАктив,
	|	ДокументыПоНМА.Организация КАК Организация,
	|	ДокументыПоНМА.Подразделение КАК Подразделение,
	|	ДокументыПоНМА.Ссылка КАК Ссылка,
	|	ДокументыПоНМА.ДополнительнаяЗапись КАК ДополнительнаяЗапись
	|ИЗ
	|	РегистрСведений.ДокументыПоНМА КАК ДокументыПоНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ИдентификаторыОбъектовМетаданных КАК ИдентификаторыОбъектовМетаданных
	|		ПО ДокументыПоНМА.ТипСсылки = ИдентификаторыОбъектовМетаданных.Ссылка
	|ГДЕ
	|	ИдентификаторыОбъектовМетаданных.Имя = ""РаспределениеНДС""";
	
	РегистрируемыеИзмерения = Запрос.Выполнить().Выгрузить();
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, РегистрируемыеИзмерения, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ДокументыПоНМА";
	
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяРегистра) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	

	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);

	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр(
			"ru = 'Не удалось обработать задания по обработчику регистра сведений ""Документы по НМА"".';
			|en = 'Cannot process the jobs of the ""Intangible asset documents"" information register handler.'"));
		
	Для каждого Строка Из ОбновляемыеДанные Цикл
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ДокументыПоНМА");
		ЭлементБлокировки.УстановитьЗначение("ТипСсылки", Строка.ТипСсылки);
		ЭлементБлокировки.УстановитьЗначение("ХозяйственнаяОперация", Строка.ХозяйственнаяОперация);
		ЭлементБлокировки.УстановитьЗначение("НомерЗаписи", Строка.НомерЗаписи);
		ЭлементБлокировки.УстановитьЗначение("Дата", Строка.Дата);
		ЭлементБлокировки.УстановитьЗначение("НематериальныйАктив", Строка.НематериальныйАктив);
		ЭлементБлокировки.УстановитьЗначение("Организация", Строка.Организация);
		ЭлементБлокировки.УстановитьЗначение("Подразделение", Строка.Подразделение);
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Строка.Ссылка);
		ЭлементБлокировки.УстановитьЗначение("ДополнительнаяЗапись", Строка.ДополнительнаяЗапись);
		
		Блокировка.Заблокировать();
				
		Набор = РегистрыСведений.ДокументыПоНМА.СоздатьНаборЗаписей();
		Набор.Отбор.ТипСсылки.Установить(Строка.ТипСсылки);
		Набор.Отбор.ХозяйственнаяОперация.Установить(Строка.ХозяйственнаяОперация);
		Набор.Отбор.НомерЗаписи.Установить(Строка.НомерЗаписи);
		Набор.Отбор.Дата.Установить(Строка.Дата);
		Набор.Отбор.НематериальныйАктив.Установить(Строка.НематериальныйАктив);
		Набор.Отбор.Организация.Установить(Строка.Организация);
		Набор.Отбор.Подразделение.Установить(Строка.Подразделение);
		Набор.Отбор.Ссылка.Установить(Строка.Ссылка);
		Набор.Отбор.ДополнительнаяЗапись.Установить(Строка.ДополнительнаяЗапись);
		
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(Набор);
		
		ЗафиксироватьТранзакцию(); 
		
	Исключение
		
		ОтменитьТранзакцию();
		ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), Набор);
		
	КонецПопытки;
	
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = 
		ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
