
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Сохраняет изображение товара, указанное для строки документа Запрос коммерческих предложений у поставщиков
// 
// Параметры: 
//  Номенклатура - ОпределяемыйТип.НоменклатураEDI
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД
//  Изображение - ОпределяемыйТип.ПрисоединенныеФайлыИзображенийЗКППоставщика
Процедура СохранитьИзображениеТовара(Номенклатура, Характеристика, Изображение) Экспорт

	Запись = СоздатьМенеджерЗаписи();
	Запись.Номенклатура = Номенклатура;
	Запись.Характеристика = Характеристика;
	Запись.ФайлИзображения = Изображение;
		
	НачатьТранзакцию();
	Попытка	
		Запись.Записать();	
		ЗафиксироватьТранзакцию();
	Исключение				
		
		ОтменитьТранзакцию();
		
		ВидОперации = НСтр("ru = 'Сохранение записи в регистр';
							|en = 'Save the record to the register'", ОбщегоНазначения.КодОсновногоЯзыка());
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		КраткийТекстОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
		ПодробныйТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
		ЭлектронноеВзаимодействие.ОбработатьОшибку(
			ВидОперации, ПодробныйТекстОшибки, КраткийТекстОшибки, "КоммерческиеПредложения");
			
		ВызватьИсключение КраткийТекстОшибки;
		
	КонецПопытки;

КонецПроцедуры

// Возвращает ссылку на файл соответствующего справочника присоединенных файлов, которую ранее сохранили для товара 
// в качестве изображения для выгрузки в сервис ЗКП. 
// Либо Неопределено, если файл изображения не был подобран системой автоматически и пользователем вручную, а также, 
// если отсутствуют сохраненные значения.
//
// Параметры:
//  Номенклатура - ОпределяемыйТип.НоменклатураБЭД - Номенклатура товарной позиции
//  Характеристика - ОпределяемыйТип.ХарактеристикаНоменклатурыБЭД - характеристика товарной позиции
//  ИзображениеОчищено - Булево - В регистре сохранено пустое значение. 
// 
// Возвращаемое значение:
//  Неопределено, ОпределяемыйТип.ПрисоединенныеФайлыИзображенийЗКППоставщика - Сохраненное значение изображения товара
Функция СохраненноеИзображениеТовара(Номенклатура, Характеристика, ИзображениеОчищено) Экспорт

	Результат = Неопределено;
	ИзображениеОчищено = Ложь;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИзображенияЗапросовКоммерческихПредложений.ФайлИзображения
		|ИЗ
		|	РегистрСведений.ИзображенияЗапросовКоммерческихПредложений КАК ИзображенияЗапросовКоммерческихПредложений
		|ГДЕ
		|	ИзображенияЗапросовКоммерческихПредложений.Номенклатура = &Номенклатура
		|	И ИзображенияЗапросовКоммерческихПредложений.Характеристика = &Характеристика";
	
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ИзображениеОчищено = Не ЗначениеЗаполнено(Выборка.ФайлИзображения);
		Если Не ИзображениеОчищено 
			И Не ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.ФайлИзображения, "ПометкаУдаления") Тогда
			Результат = Выборка.ФайлИзображения;				
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
