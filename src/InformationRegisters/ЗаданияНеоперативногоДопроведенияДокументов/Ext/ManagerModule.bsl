#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ФормированиеЗаданий

// Создание заданий неоперативной очереди.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  МенеджерРегистра - РегистрНакопленияМенеджер, РегистрСведенийМенеджер - Менеджер записываемого регистра
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗадания(МенеджерРегистра, ВидыЗаданий, НаборЗаписей, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт
	
	Если ВидыЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДанныеКФормированиюЗаданий = МенеджерРегистра.ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументов(
		НаборЗаписей, ВидыЗаданий, МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);

	Если ДанныеКФормированиюЗаданий.Количество() > 0 Тогда

		Задания = СоздатьНаборЗаписей();

		Для Каждого ТекущиеДанные Из ДанныеКФормированиюЗаданий Цикл
			
			Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Продолжить;
			КонецЕсли;
			
			НовоеЗадание = Задания.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеЗадание, ТекущиеДанные);
			НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;

		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Истина);
		Задания.Записать(Ложь);
		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;

КонецПроцедуры

// Создание заданий неоперативной очереди по данным временной таблицы.
// Вызывается при записи набора записей регистра.
// 
// Параметры:
//  МенеджерРегистра - РегистрНакопленияМенеджер, РегистрСведенийМенеджер - Менеджер записываемого регистра
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  МенеджерВременныхТаблиц - МенеджерВременныхТаблиц - Данные временных таблиц будут использованы при формировании новых заданий.
//  ИмяТаблицы - Строка - Имя таблицы в Менеджере временных таблиц
//
Процедура СоздатьЗаданияПоДаннымВременнойТаблицы(МенеджерРегистра, ВидыЗаданий, МенеджерВременныхТаблиц, ИмяТаблицы) Экспорт
	
	Если ВидыЗаданий.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ДанныеКФормированиюЗаданий = МенеджерРегистра.ДанныеКФормированиюЗаданийНеоперативногоДопроведенияДокументовПоДаннымВТ(
		МенеджерВременныхТаблиц, ИмяТаблицы, ВидыЗаданий);

	Если ДанныеКФормированиюЗаданий.Количество() > 0 Тогда

		Задания = СоздатьНаборЗаписей();

		Для Каждого ТекущиеДанные Из ДанныеКФормированиюЗаданий Цикл
				
			Если ТипЗнч(ТекущиеДанные.Документ) = Тип("ДокументСсылка.КорректировкаРегистров") Тогда
				Продолжить;
			КонецЕсли;
			
			НовоеЗадание = Задания.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеЗадание, ТекущиеДанные);
			НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;

		КонецЦикла;
		
		УстановитьПривилегированныйРежим(Истина);
		Задания.Записать(Ложь);
		УстановитьПривилегированныйРежим(Ложь);

	КонецЕсли;

КонецПроцедуры

// Создать задания при обмене данными РИБ.
// 
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список видов заданий.
//                По данному списку должны зарегистрироваться новые задания согласно записываемым данным регистра.
//  МенеджерВременныхТаблиц - Неопределено, МенеджерВременныхТаблиц - При указании МВТ данные временных таблиц будут использованы при формировании новых заданий.
//  ДанныеКРегистрацииЗаданий - Неопределено, ТаблицаЗначений - Таблица с данными к регистрации новых заданий.
//
Процедура СоздатьЗаданияПриОбменеДанными(НаборЗаписей, ВидыЗаданий, МенеджерВременныхТаблиц = Неопределено,
	ДанныеКРегистрацииЗаданий = Неопределено) Экспорт

	МетаданныеРегистра = НаборЗаписей.Метаданные();
	МенеджерРегистра = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(МетаданныеРегистра.ПолноеИмя());

	СоздатьЗадания(МенеджерРегистра, ВидыЗаданий, НаборЗаписей, МенеджерВременныхТаблиц, ДанныеКРегистрацииЗаданий);
		
КонецПроцедуры

// Таблица данных для формирования заданий неоперативного допроведения документов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица данных для формирования заданий неоперативного допроведения документов:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано задание
// * ПериодЗадания - Дата - Период задания
// * Документ - ДокументСсылка - Ссылка на документ, по которому формируется задание
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Ссылка на вид задания
//
Функция ТаблицаФормированияЗаданий() Экспорт

	Таблица = Новый ТаблицаЗначений;
	Таблица.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Таблица.Колонки.Добавить("ПериодЗадания", ОбщегоНазначения.ОписаниеТипаДата(ЧастиДаты.ДатаВремя));
	Таблица.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	Таблица.Колонки.Добавить("ВидЗадания", Новый ОписаниеТипов("СправочникСсылка.ВидыНеоперативныхЗаданий"));
	Таблица.Индексы.Добавить("Организация, ПериодЗадания, Документ, ВидЗадания");

	Возврат Таблица;

КонецФункции

// Данные к формированию заданий для отражения движений по управленческому балансу.
//  
// Параметры:
//  НаборЗаписей - РегистрНакопленияНаборЗаписей, РегистрСведенийНаборЗаписей - Записываемый набор записей регистра - источника.
//  ВидыЗаданий - Массив Из СправочникСсылка.ВидыНеоперативныхЗаданий - Список заданий к регистрации
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Данные к формированию заданий отражение движений по управленческому балансу:
// * Организация - СправочникСсылка.Организации - Организация, по которой будет сформировано задание
// * ПериодЗадания - Дата - Период задания
// * Документ - ДокументСсылка - Ссылка на документ (регистратор), по которому формируется задание
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Функция ТаблицаФормированияЗаданийОтражениеВУправленческомБалансе(НаборЗаписей, ВидыЗаданий) Экспорт
	
	ТаблицаДанных = ТаблицаФормированияЗаданий();
	
	Если ВидыЗаданий.Найти(Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу) <> Неопределено Тогда

		ДанныеКФормированиюЗаданий = НаборЗаписей.Выгрузить( , "Период, Организация, Регистратор");
		ДанныеКФормированиюЗаданий.Свернуть("Период, Организация, Регистратор");

		ТаблицаДанных = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ТаблицаФормированияЗаданий();

		Для Каждого ТекущийВидЗадания Из ВидыЗаданий Цикл

			Если ТекущийВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда

				Для Каждого Запись Из ДанныеКФормированиюЗаданий Цикл

					НоваяСтрока = ТаблицаДанных.Добавить();
					НоваяСтрока.Организация = Запись.Организация;
					НоваяСтрока.ПериодЗадания = Запись.Период;
					НоваяСтрока.Документ = Запись.Регистратор;
					НоваяСтрока.ВидЗадания = ТекущийВидЗадания;

				КонецЦикла;

			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

	Возврат ТаблицаДанных;
	
КонецФункции

#КонецОбласти

#Область ОбработкаДанных

// Получает данные гранул расчета для многопоточной фоновой обработки.
// 
// Параметры:
//  СтруктураФормированияПотока - см. НеоперативнаяОчередьЗаданий.СтруктураФормированияПорцийДанных
//  КоличествоСвободныхПотоков - Число - Количество свободных потоков
// 
// Возвращаемое значение:
//  Структура - Данные для многопоточной фоновой обработки:
// * КоличествоПорций - Число - Число порций к обработке
// * ГранулыРасчетаКОбработке - см. ТаблицаГранулРасчета
//
Функция ДанныеДляМногопоточнойФоновойОбработки(СтруктураФормированияПотока, КоличествоСвободныхПотоков) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ДокументыКОбработке.Документ КАК Документ
	|ПОМЕСТИТЬ ВтДокументыКОбработке
	|ИЗ
	|	&ТаблицаДокументов КАК ДокументыКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ДокументыКОбработке.Документ КАК Документ,
	|	&ВидЗадания КАК ВидЗадания,
	|	МИНИМУМ(ЗаданияПоДокументам.ПериодЗадания) КАК ПериодЗадания
	|ИЗ
	|	ВтДокументыКОбработке КАК ДокументыКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК ЗаданияПоДокументам
	|		ПО ЗаданияПоДокументам.ВидЗадания = &ВидЗадания
	|		И ДокументыКОбработке.Документ = ЗаданияПоДокументам.Документ
	|СГРУППИРОВАТЬ ПО
	|	ДокументыКОбработке.Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПериодЗадания";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", СтруктураФормированияПотока.ТаблицаДокументов);
	Запрос.УстановитьПараметр("ВидЗадания", СтруктураФормированияПотока.ВидЗадания);

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "1000", Формат(КоличествоСвободныхПотоков
		* СтруктураФормированияПотока.МаксимальнаяПорция, "ЧН=0; ЧГ=0"));
	
	УстановитьПривилегированныйРежим(Истина);
	ДокументыКОбработке = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	НомерОчереди = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтруктураФормированияПотока.ВидЗадания, "НомерОчереди");
	ТаблицаПорцийДокументовКОбработке = ТаблицаГранулРасчета();
	ТаблицаПорцийДокументовКОбработке.Колонки.Добавить("Порция", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	ТаблицаПорцийДокументовКОбработке.Индексы.Добавить("Порция");

	РазмерТекущейПорции = 0;
	ПорядокОбработки = 1;

	Пока ДокументыКОбработке.Следующий() Цикл

		НоваяСтрока = ТаблицаПорцийДокументовКОбработке.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ДокументыКОбработке);
		НоваяСтрока.НомерОчереди = НомерОчереди;

		Если РазмерТекущейПорции >= СтруктураФормированияПотока.МаксимальнаяПорция Тогда

			ПорядокОбработки = ПорядокОбработки + 1;
			РазмерТекущейПорции = 0;

		КонецЕсли;

		НоваяСтрока.Порция = ПорядокОбработки;
		РазмерТекущейПорции = РазмерТекущейПорции + 1;

	КонецЦикла;

	СтруктураДанных = НеоперативнаяОчередьЗаданий.СтруктураДанныхОбработкиЗаданий();
	СтруктураДанных.КоличествоПорций = ПорядокОбработки;
	СтруктураДанных.ГранулыРасчетаКОбработке = ТаблицаПорцийДокументовКОбработке;
	
	Возврат СтруктураДанных;
	
КонецФункции

// Выполнить обработку порции гранул расчета.
// 
// Параметры:
//  ПараметрыДанных - см. НеоперативнаяОчередьЗаданий.ПараметрыОбработкиПорцииДанных
//
Процедура ВыполнитьОбработкуПорции(ПараметрыДанных) Экспорт

	Если ПараметрыДанных.ПорцияДанных.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЗапросИдентификаторовЗаданий = Новый Запрос;
	ЗапросИдентификаторовЗаданий.Текст = "
	|ВЫБРАТЬ
	|	ДокументыКОбработке.Документ КАК Документ,
	|	ДокументыКОбработке.ВидЗадания КАК ВидЗадания
	|ПОМЕСТИТЬ ВтДокументыКОбработке
	|ИЗ
	|	&ТаблицаДокументыКОбработке КАК ДокументыКОбработке
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	ВидЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВтДокументыКОбработке.Документ КАК Документ,
	|	ЗаданияНеоперативногоДопроведенияДокументов.ИдентификаторЗаписи КАК ИдентификаторЗадания,
	|	ЗаданияНеоперативногоДопроведенияДокументов.ВыполненоСОшибкой КАК ВыполненоСОшибкой
	|ИЗ
	|	ВтДокументыКОбработке КАК ВтДокументыКОбработке
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК
	|			ЗаданияНеоперативногоДопроведенияДокументов
	|		ПО ВтДокументыКОбработке.Документ = ЗаданияНеоперативногоДопроведенияДокументов.Документ
	|		И ВтДокументыКОбработке.ВидЗадания = ЗаданияНеоперативногоДопроведенияДокументов.ВидЗадания";

	ЗапросИдентификаторовЗаданий.УстановитьПараметр("ТаблицаДокументыКОбработке", ПараметрыДанных.ПорцияДанных);
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторыЗаданийДокументов = ЗапросИдентификаторовЗаданий.Выполнить().Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);
	ИдентификаторыЗаданийДокументов.Индексы.Добавить("Документ");
	ИдентификаторыЗаданийДокументов.Индексы.Добавить("ВыполненоСОшибкой");
	
	ОтборЗаданийБезОшибок = Новый Структура("ВыполненоСОшибкой", Ложь);
	
	Если Не ОбщегоНазначения.ИнформационнаяБазаФайловая() И Не ПараметрыДанных.ВыполнениеЗаданийВТранзакцииПроведения Тогда
	
	#Область БлокировкаЗаписейРегистраОчередиПоСпискуДокументов

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		ЭлементБлокировки.ИсточникДанных = ИдентификаторыЗаданийДокументов.Скопировать(ОтборЗаданийБезОшибок);
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторЗаписи", "ИдентификаторЗадания");

		ПорцияДокументовУспешноОбработана = Истина;

		НачатьТранзакцию();

		Попытка

			Блокировка.Заблокировать();

			СписокДокументов = ПараметрыДанных.ПорцияДанных.ВыгрузитьКолонку("Документ");

			УстановитьПривилегированныйРежим(Истина);

			ПараметрыОбработкиДокументов = ПараметрыОбработкиДокументов(ПараметрыДанных.ПорцияДанных[0].ВидЗадания,
				СписокДокументов);

			Для Каждого ДанныеКРасчету Из ПараметрыДанных.ПорцияДанных Цикл
				ВыполнитьОбработкуДокумента(ДанныеКРасчету.ВидЗадания, ДанныеКРасчету.Документ,
					ПараметрыОбработкиДокументов);
			КонецЦикла;

			НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ПараметрыДанных.ИмяРегистраОчереди,
				ИдентификаторыЗаданийДокументов.ВыгрузитьКолонку("ИдентификаторЗадания"),
				ПараметрыДанных.ОчередиЗаданий);

			УстановитьПривилегированныйРежим(Ложь);

			ПараметрыДанных.ПорцияДанных.Очистить();

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();

			ПорцияДокументовУспешноОбработана = Ложь;

		КонецПопытки;

		Если ПорцияДокументовУспешноОбработана Тогда
			Возврат;
		КонецЕсли;
	
	#КонецОбласти
	
	КонецЕсли;
	
	#Область ПодокументнаяОбработка
	
	ПараметрыДанных.ПорцияДанных.Колонки.Добавить("ОписаниеОшибки", Новый ОписаниеТипов("Строка"));
	
	МетаданныеРегистраОчереди = Неопределено;

	СтруктураОтбораЗаданий = Новый Структура;
	
	УспешноОбработанныеДанные = Новый Массив;
	
	НомерПопытки = 1;
	КоличествоПопыток = ?(ПараметрыДанных.ВыполнениеЗаданийВТранзакцииПроведения, 1, 3);
	Пока НомерПопытки <= КоличествоПопыток Цикл
		
		Если ПараметрыДанных.ПорцияДанных.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Для Каждого ДанныеКРасчету Из ПараметрыДанных.ПорцияДанных Цикл

			ОбработкаДокументаВыполненаУспешно = Истина;
			
			НачатьТранзакцию();

			Попытка
				
				СтруктураОтбораЗаданий.Вставить("Документ", ДанныеКРасчету.Документ);
				ТаблицаИдентификаторовДокумента = ИдентификаторыЗаданийДокументов.Скопировать(СтруктураОтбораЗаданий);
				ТаблицаИдентификаторовДокумента.Индексы.Добавить("ВыполненоСОшибкой");
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов");
				ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
				ЭлементБлокировки.ИсточникДанных = ТаблицаИдентификаторовДокумента.Скопировать(ОтборЗаданийБезОшибок);
				ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ИдентификаторЗаписи", "ИдентификаторЗадания");
				Блокировка.Заблокировать();
				
				УстановитьПривилегированныйРежим(Истина);
				
				СписокДокументов = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ДанныеКРасчету.Документ);
				ПараметрыОбработкиДокументов = ПараметрыОбработкиДокументов(ПараметрыДанных.ПорцияДанных[0].ВидЗадания,
					СписокДокументов);
				ВыполнитьОбработкуДокумента(ДанныеКРасчету.ВидЗадания, ДанныеКРасчету.Документ,
					ПараметрыОбработкиДокументов);
				
				СписокИдентификаторовЗаданий = ТаблицаИдентификаторовДокумента.ВыгрузитьКолонку("ИдентификаторЗадания");
				НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ПараметрыДанных.ИмяРегистраОчереди,
					СписокИдентификаторовЗаданий, ПараметрыДанных.ОчередиЗаданий);
					
				УстановитьПривилегированныйРежим(Ложь);
				
				ЗафиксироватьТранзакцию();

			Исключение

				ОтменитьТранзакцию();
				
				Если МетаданныеРегистраОчереди = Неопределено Тогда
					МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов;
				КонецЕсли;
				
				ОбработкаДокументаВыполненаУспешно = Ложь;

				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди,
					ДанныеКРасчету.ВидЗадания, ТекстОшибки, ДанныеКРасчету.Документ);
				
				ДанныеКРасчету.ОписаниеОшибки = ТекстОшибки;
				
			КонецПопытки;
			
			Если ОбработкаДокументаВыполненаУспешно Тогда
				УспешноОбработанныеДанные.Добавить(ДанныеКРасчету);
			КонецЕсли;
			
		КонецЦикла;
		
		Если УспешноОбработанныеДанные.Количество() > 0 Тогда
			
			Для Каждого ДанныеКУдалению Из УспешноОбработанныеДанные Цикл
				ПараметрыДанных.ПорцияДанных.Удалить(ДанныеКУдалению);
			КонецЦикла;

			УспешноОбработанныеДанные.Очистить();

		КонецЕсли;
		
		НомерПопытки = НомерПопытки + 1;

	КонецЦикла;
	
	Если Не ПараметрыДанных.ВыполнениеЗаданийВТранзакцииПроведения Тогда
		ЗарегистрироватьОшибкиОбработкиДанных(ПараметрыДанных, ИдентификаторыЗаданийДокументов, МетаданныеРегистраОчереди);
	Иначе
		СообщитьОбОшибкахОбработкиДанных(ПараметрыДанных);
	КонецЕсли;
	
	#КонецОбласти
	
КонецПроцедуры

// Переопределение параметров очереди заданий.
// 
// Параметры:
//  ИмяОчереди -Строка - Имя очереди заданий
// 
// Возвращаемое значение:
//  Структура - Переопределение параметров описания очереди заданий:
// * ПараметрЗапросаВидЗадания - Строка - Имя параметра, соответствующего полю "ВидЗадания" ,которое будет использовано при формировании текстов запросов.
// Переопределяется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания". 
// Имя параметра должно быть уникальным среди всех регистров заданий очередей.
// * ЗначениеВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Значение, которое будет подставлено в ПараметрЗапросаВидЗадания.
// Параметр заполняется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания".
// * ПараметрЗапросаНомерОчереди - Строка - Имя параметра, соответствующего полю "НомерОчереди" ,которое будет использовано при формировании текстов запросов.
// Переопределяется только в том случае, если в регистре заданий отсутствует поле "ВидЗадания".
// Имя параметра должно быть уникальным среди всех регистров заданий очередей.
// * ЗначениеНомерОчереди - Число - Значение, которое будет подставлено в ПараметрЗапросаНомерОчереди.
// * ТекстЗапросаГранулыРасчетаВОбработке - Строка - Текст запроса, по которому будет формироваться временная таблица с гранулами расчета заданий.
// Выбираемые поля должны соответствовать колонкам таблицы значений, получаемой в результате вызова функции ТаблицаГранулыРасчетаВОбработке модуля менеджера регистра заданий.
// Имя временной таблицы должно быть уникальным среди всех регистров заданий очередей.
// * ТекстЗапросаИсключаемыеДокументы - Строка - Текст запроса, результат выполнения которого получает список документов, по которым выполняется обработка гранул расчета.
// Текст запроса формируется на основании ТекстЗапросаГранулыРасчетаВОбработке.
// * ОбрабатываемыеГранулыИмяТаблицы - Строка - Имя параметра запроса, значение которого является Таблица значений с данными обрабатываемых гранул.
// Имя таблицы должно быть уникальным среди всех регистров заданий очередей.
//
Функция ОписаниеПараметровОчереди(ИмяОчереди) Экспорт
	
	ОписаниеОчереди = НеоперативнаяОчередьЗаданий.СтруктураОписанияПараметровОчереди();
	
	#Область ТекстЗапросаГранулыРасчетаВОбработке
	ТекстЗапросаГранулыРасчетаВОбработке = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОбрабатываемыеГранулы.Документ КАК Документ,
	|	ОбрабатываемыеГранулы.ВидЗадания КАК ВидЗадания,
	|	ОбрабатываемыеГранулы.НомерОчереди КАК НомерОчереди
	|ПОМЕСТИТЬ ВтОбрабатываемыеГранулы
	|ИЗ
	|	&ОбрабатываемыеГранулы КАК ОбрабатываемыеГранулы
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ";
	
	ТекстЗапросаГранулыРасчетаВОбработке = СтрЗаменить(ТекстЗапросаГранулыРасчетаВОбработке, "ВтОбрабатываемыеГранулы", "Вт" + ИмяОчереди);
	ТекстЗапросаГранулыРасчетаВОбработке = СтрЗаменить(ТекстЗапросаГранулыРасчетаВОбработке, "&ОбрабатываемыеГранулы", "&ОбрабатываемыеГранулы" + ИмяОчереди);
	ОписаниеОчереди.Вставить("ТекстЗапросаГранулыРасчетаВОбработке", ТекстЗапросаГранулыРасчетаВОбработке);
	ОписаниеОчереди.Вставить("ОбрабатываемыеГранулыИмяТаблицы", "ОбрабатываемыеГранулы" + ИмяОчереди);
	#КонецОбласти
	
	#Область ТекстЗапросаИсключаемыеДокументы
	ТекстЗапросаИсключаемыеДокументы = "
	|ВЫБРАТЬ
	|	ОбрабатываемыеДокументы.Документ КАК Документ,
	|	ОбрабатываемыеДокументы.ВидЗадания КАК ВидЗадания,
	|	ОбрабатываемыеДокументы.НомерОчереди КАК НомерОчереди
	|ИЗ
	|	ВтОбрабатываемыеГранулы КАК ОбрабатываемыеДокументы";
	
	ТекстЗапросаИсключаемыеДокументы = СтрЗаменить(ТекстЗапросаИсключаемыеДокументы, "ВтОбрабатываемыеГранулы", "Вт" + ИмяОчереди);
	ОписаниеОчереди.Вставить("ТекстЗапросаИсключаемыеДокументы", ТекстЗапросаИсключаемыеДокументы);
	#КонецОбласти
	
	Возврат ОписаниеОчереди;
	
КонецФункции

// Таблица гранул расчета в обработке.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см.ТаблицаГранулРасчета:
// * ИдентификаторПотока - УникальныйИдентификатор - Идентификатор потока
//
Функция ТаблицаГранулыРасчетаВОбработке() Экспорт
	
	ДокументыВОбработке = ТаблицаГранулРасчета();
	ДокументыВОбработке.Колонки.Добавить("ИдентификаторПотока", Новый ОписаниеТипов("УникальныйИдентификатор"));
	ДокументыВОбработке.Индексы.Добавить("ИдентификаторПотока");
	
	Возврат ДокументыВОбработке;
	
КонецФункции

// Зарегистрировать проблему выполнения расчета.
// 
// Параметры:
//  ПараметрыОбработчика - см. ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыОбработчикаЭтапаЗакрытияМесяцаДляПроверки
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Процедура ЗарегистрироватьПроблемуВыполненияРасчета(ПараметрыОбработчика, ВидЗадания = Неопределено) Экспорт
		
#Область Ошибки

	ГруппаПроблем = НСтр("ru = 'При выполнении операции зарегистрированы ошибки';
						|en = 'Errors are registered while executing the operation:'", ОбщегоНазначения.КодОсновногоЯзыка());
		
	ШаблонЗаданиеСОшибкой = НСтр("ru = 'Выполнение задания ""%1"" по документу %2 завершилось с ошибками.';
								|en = 'The ""%1"" job for the ""%2"" document is completed with errors.'", ОбщегоНазначения.КодОсновногоЯзыка());
	ШаблонЗаданиеНеВыполнено = НСтр("ru = 'Задание ""%1"" по документу %2 не было выполнено.';
									|en = 'The ""%1"" job for the ""%2"" document is not completed.'", ОбщегоНазначения.КодОсновногоЯзыка());
		
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	Запрос.УстановитьПараметр("ВидЗадания", ВидЗадания);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Задания.Организация КАК Организация,
	|	МИНИМУМ(НАЧАЛОПЕРИОДА(Задания.ПериодЗадания, МЕСЯЦ)) КАК ПериодЗадания,
	|	Задания.ВидЗадания КАК ВидЗадания,
	|	Задания.Документ КАК Документ,
	|	Задания.ВыполненоСОшибкой КАК ВыполненоСОшибкой
	|ПОМЕСТИТЬ ВТ_Ошибки
	|ИЗ
	|	РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК Задания
	|ГДЕ
	|	Задания.Организация В (&МассивОрганизаций)
	|	И Задания.ПериодЗадания <= &КонецПериода
	|	И &ОтборПоЗаданиям
	|СГРУППИРОВАТЬ ПО
	|	Задания.Организация,
	|	Задания.ВидЗадания,
	|	Задания.Документ,
	|	Задания.ВыполненоСОшибкой
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Ошибки.Организация КАК Организация,
	|	ВТ_Ошибки.ПериодЗадания КАК ПериодЗадания,
	|	ВТ_Ошибки.ВидЗадания КАК ВидЗадания,
	|	ВТ_Ошибки.Документ КАК Документ,
	|	ВТ_Ошибки.ВыполненоСОшибкой КАК ВыполненоСОшибкой
	|ИЗ
	|	ВТ_Ошибки КАК ВТ_Ошибки
	|ИТОГИ
	|ПО
	|	Организация,
	|	ПериодЗадания,
	|	ВидЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_Ошибки";
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос.Текст = СтрЗаменить(Запрос.Текст,"&ОтборПоЗаданиям" , ?(ВидЗадания = Неопределено, "ИСТИНА", "Задания.ВидЗадания = &ВидЗадания"));
	УстановитьПривилегированныйРежим(Ложь);
	
	ВыборкаПоОрганизации = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаПоОрганизации.Следующий() Цикл

		ВыборкаПоПериоду = ВыборкаПоОрганизации.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаПоПериоду.Следующий() Цикл

			ПараметрыРегистрации = ЗакрытиеМесяцаСервер.ИнициализироватьПараметрыРегистрацииПроблемыРасчета(
						ПараметрыОбработчика.ДанныеЭтапа.Код, ВыборкаПоОрганизации.Организация,
				ВыборкаПоПериоду.ПериодЗадания);

			ВыборкаПоВидуЗадания = ВыборкаПоПериоду.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаПоВидуЗадания.Следующий() Цикл

				ДетальныеЗаписи = ВыборкаПоВидуЗадания.Выбрать();

				Пока ДетальныеЗаписи.Следующий() Цикл

					ПолныйТекстПроблемы = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(?(
					ДетальныеЗаписи.ВыполненоСОшибкой, ШаблонЗаданиеСОшибкой, ШаблонЗаданиеНеВыполнено),
						ВыборкаПоВидуЗадания.ВидЗадания, ДетальныеЗаписи.Документ);

					ЗакрытиеМесяцаСервер.ЗарегистрироватьПроблемуВыполненияРасчета(
							ПараметрыРегистрации, ГруппаПроблем, Перечисления.ВажностьПроблемыУчета.Ошибка,
						ПолныйТекстПроблемы, ДетальныеЗаписи.Документ);

				КонецЦикла;

			КонецЦикла;

		КонецЦикла;

	КонецЦикла;
	
#КонецОбласти

КонецПроцедуры

// Формирует таблицу значений документов к обработке.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица документов к обработке:
// * Документ - ДокументСсылка - Документ
// * ВидЗадания - СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
// * НомерОчереди - Число - Номер очереди
//
Функция ТаблицаГранулРасчета() Экспорт

	ДокументыВОбработке = Новый ТаблицаЗначений;
	ДокументыВОбработке.Колонки.Добавить("Документ", Документы.ТипВсеСсылки());
	ДокументыВОбработке.Колонки.Добавить("ВидЗадания", Новый ОписаниеТипов("СправочникСсылка.ВидыНеоперативныхЗаданий"));
	ДокументыВОбработке.Колонки.Добавить("НомерОчереди", ОбщегоНазначения.ОписаниеТипаЧисло(10, 0));
	
	Возврат ДокументыВОбработке;

КонецФункции

// По переданному списку документов и виду задания обрабатываются данные об ошибках.
//
// Параметры:
//  СписокДокументов - Неопределено, Массив Из ДокументСсылка - Обрабатываемые документы
//  ИдентификаторыОшибок - Неопределено, Массив Из УникальныйИдентификатор - Список обработанных идентифкаторов ошибок
//  ВидЗадания - Неопределено, СправочникСсылка.ВидыНеоперативныхЗаданий - Вид задания
//
Процедура СнятьПризнакВыполненоСОшибкойПоДокументам(СписокДокументов, ИдентификаторыОшибок, ВидЗадания = Неопределено) Экспорт
	
	СписокИсключаемыхВидовЗаданий = Новый Массив;
	СписокИсключаемыхВидовЗаданий.Добавить(Справочники.ВидыНеоперативныхЗаданий.РаспределениеВзаиморасчетовПоСрокам);
		
	Если ВидЗадания <> Неопределено Тогда
		
		Если СписокИсключаемыхВидовЗаданий.Найти(ВидЗадания) <> Неопределено Тогда
			Возврат;
		КонецЕсли;
		 
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументов", СписокДокументов);
	Запрос.УстановитьПараметр("ВидЗадания", ВидЗадания);

	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаОчереди.ПериодЗадания КАК ПериодЗадания,
	|	ТаблицаОчереди.Организация КАК Организация,
	|	ТаблицаОчереди.ВидЗадания КАК ВидЗадания,
	|	ТаблицаОчереди.Документ КАК Документ,
	|	ТаблицаОчереди.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ТаблицаОчереди.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ПОМЕСТИТЬ ДанныеОбОшибках
	|ИЗ
	|	РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК ТаблицаОчереди
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОшибкиОчередейЗаданий КАК РегистрОшибки
	|		ПО ТаблицаОчереди.ИдентификаторОшибки = РегистрОшибки.ИдентификаторОшибки
	|ГДЕ
	|	ТаблицаОчереди.ВыполненоСОшибкой
	|	И &ОтборПоДокументам
	|	И &ОтборПоВидуЗадания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеОбОшибках.ПериодЗадания КАК ПериодЗадания,
	|	ДанныеОбОшибках.Организация КАК Организация,
	|	ДанныеОбОшибках.ВидЗадания КАК ВидЗадания,
	|	ДанныеОбОшибках.Документ КАК Документ,
	|	ДанныеОбОшибках.ИдентификаторЗаписи КАК ИдентификаторЗаписи,
	|	ДанныеОбОшибках.ИдентификаторОшибки КАК ИдентификаторОшибки
	|ИЗ
	|	ДанныеОбОшибках КАК ДанныеОбОшибках
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ДанныеОбОшибках.ПериодЗадания) КАК ПериодЗадания,
	|	ДанныеОбОшибках.Организация КАК Организация,
	|	ДанныеОбОшибках.ВидЗадания КАК ВидЗадания,
	|	ДанныеОбОшибках.Документ КАК Документ
	|ИЗ
	|	ДанныеОбОшибках КАК ДанныеОбОшибках
	|СГРУППИРОВАТЬ ПО
	|	ДанныеОбОшибках.Организация,
	|	ДанныеОбОшибках.ВидЗадания,
	|	ДанныеОбОшибках.Документ";
	
	Если СписокДокументов = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДокументам", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоДокументам", "ТаблицаОчереди.Документ В (&СписокДокументов)");
	КонецЕсли;

	Если ВидЗадания = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоВидуЗадания", "ИСТИНА");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ОтборПоВидуЗадания", "ТаблицаОчереди.ВидЗадания = &ВидЗадания");
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	Результат = Запрос.ВыполнитьПакет();
	ЗаданияСОшибками = Результат[Результат.ВГраница() - 1].Выгрузить();
	ЗаписиКФормированиюЗаданий = Результат[Результат.ВГраница()].Выгрузить();
	УстановитьПривилегированныйРежим(Ложь);

	Если ЗаданияСОшибками.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;

	ИдентификаторыОшибокОчереди = ЗаданияСОшибками.ВыгрузитьКолонку("ИдентификаторОшибки");

	НачатьТранзакцию();
	Попытка

		НаборЗаписей = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.СоздатьНаборЗаписей();
		НаборЗаписей.Загрузить(ЗаданияСОшибками);
		УстановитьПривилегированныйРежим(Истина);
		НаборЗаписей.Записать(РежимЗамещения.Удаление);
		УстановитьПривилегированныйРежим(Ложь);

		НовыйНаборЗаписей = РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.СоздатьНаборЗаписей();
		Для Каждого ЗаписьЗадания Из ЗаписиКФормированиюЗаданий Цикл
			НовоеЗадание = НовыйНаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(НовоеЗадание, ЗаписьЗадания);
			НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
		КонецЦикла;
		УстановитьПривилегированныйРежим(Истина);
		НовыйНаборЗаписей.Записать(Ложь);
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();

	Исключение

		ОтменитьТранзакцию();
		
		ИдентификаторыОшибокОчереди = Новый Массив;
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

		Событие = НСтр("ru = 'Неоперативная очередь заданий: снятие признака ""Выполнено с ошибкой""';
						|en = 'Backdate job queue: clear the ""Failed"" flag'",
			ОбщегоНазначения.КодОсновногоЯзыка());

		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);

	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ИдентификаторыОшибок, ИдентификаторыОшибокОчереди, Истина);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыполнитьОбработкуДокумента(ВидЗадания, Документ, ПараметрыОбработки)

	Если ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияСуммДокументовВВалютахУчета Тогда
		
		РассчитатьСуммыДокументовВВалютахУчета(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженийПоФинансовымРегистрам Тогда
		
		ОбновитьДвиженияДокументаПоФинансовымРегистрам(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентКлиенты Тогда
		
		СформироватьДвиженияДенежныеСредстваКонтрагент(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентПоставщики Тогда
		
		СформироватьДвиженияДенежныеСредстваКонтрагент(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияКонтрагентКонтрагент Тогда
		
		СформироватьДвиженияКонтрагентКонтрагент(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда
		
		ОтразитьДокументВУправленческомБалансе(Документ, ПараметрыОбработки);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоАкцизам Тогда
		
		СформироватьДвиженияПоАкцизам(Документ, ПараметрыОбработки);
		
	//++ НЕ УТ
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоПрочимАктивамПассивам Тогда
		
		СформироватьДвиженияПоПрочимАктивамПассивам(Документ, ПараметрыОбработки);
		
	//-- НЕ УТ
		
	КонецЕсли;

КонецПроцедуры

Функция ПараметрыОбработкиДокументов(ВидЗадания, ДокументыКОбработке)
	
	Если ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияСуммДокументовВВалютахУчета Тогда
		
		ПараметрыОбработки = ПараметрыРасчетаСуммДокументовВВалютахУчета(ДокументыКОбработке);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженийПоФинансовымРегистрам Тогда
		
		ПараметрыОбработки = ПараметрыАктуализацииДвиженийПоФинансовымРегистрам(ДокументыКОбработке);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентКлиенты Тогда
		
		ПараметрыОбработки = ПараметрыАктуализацииДвиженийДенежныеСредстваКонтрагент(ДокументыКОбработке, Истина);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентПоставщики Тогда
		
		ПараметрыОбработки = ПараметрыАктуализацииДвиженийДенежныеСредстваКонтрагент(ДокументыКОбработке, Ложь);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияКонтрагентКонтрагент Тогда
		
		ПараметрыОбработки = ПараметрыАктуализацииДвиженийКонтрагентКонтрагент(ДокументыКОбработке);
		
	ИначеЕсли ВидЗадания = Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу Тогда
		
		ПараметрыОбработки = ПараметрыОтраженияДокументовВУправленческомБалансе(ДокументыКОбработке);
		
	Иначе
		
		ПараметрыОбработки = Новый Структура;
		
	КонецЕсли;
	
	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура ЗарегистрироватьОшибкиОбработкиДанных(ПараметрыДанных, ИдентификаторыЗаданийДокументов, МетаданныеРегистраОчереди)
	
	Если ПараметрыДанных.ПорцияДанных.Количество() > 0 Тогда

		ВидЗадания = ПараметрыДанных.ПорцияДанных[0].ВидЗадания;
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Задания.ИдентификаторЗадания КАК ИдентификаторЗадания
		|ПОМЕСТИТЬ ВтИдентификаторы
		|ИЗ
		|	&ИдентификаторыЗаданийДокументов КАК Задания
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ИдентификаторЗадания
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Задания.Документ КАК Документ,
		|	ВтИдентификаторы.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	Задания.Организация КАК Организация,
		|	Задания.ПериодЗадания КАК ПериодЗадания,
		|	Задания.ВидЗадания КАК ВидЗадания
		|ПОМЕСТИТЬ ВтЗаданияПоДокументам
		|ИЗ
		|	ВтИдентификаторы КАК ВтИдентификаторы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов КАК Задания
		|		ПО ВтИдентификаторы.ИдентификаторЗадания = Задания.ИдентификаторЗаписи
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтЗаданияПоДокументам.Документ КАК Документ,
		|	ВтЗаданияПоДокументам.Организация КАК Организация,
		|	МИНИМУМ(ВтЗаданияПоДокументам.ПериодЗадания) КАК ПериодЗадания,
		|	ВтЗаданияПоДокументам.ВидЗадания КАК ВидЗадания
		|ИЗ
		|	ВтЗаданияПоДокументам КАК ВтЗаданияПоДокументам
		|СГРУППИРОВАТЬ ПО
		|	ВтЗаданияПоДокументам.Документ,
		|	ВтЗаданияПоДокументам.Организация,
		|	ВтЗаданияПоДокументам.ВидЗадания
		|ИТОГИ
		|ПО
		|	Документ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВтЗаданияПоДокументам.ИдентификаторЗадания КАК ИдентификаторЗадания,
		|	ВтЗаданияПоДокументам.Документ
		|ИЗ
		|	ВтЗаданияПоДокументам КАК ВтЗаданияПоДокументам";
		Запрос.УстановитьПараметр("ИдентификаторыЗаданийДокументов", ИдентификаторыЗаданийДокументов);
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.ВыполнитьПакет();
		УстановитьПривилегированныйРежим(Ложь);

		ОшибкиДокументов = ПараметрыДанных.ПорцияДанных.Скопировать( , "Документ, ОписаниеОшибки"); // ТаблицаЗначений
		ОшибкиДокументов.Свернуть("Документ, ОписаниеОшибки");
		ОшибкиДокументов.Индексы.Добавить("Документ");

		ИдентификаторыЗаданий = Результат[Результат.ВГраница()].Выгрузить();
		ИдентификаторыЗаданий.Индексы.Добавить("Документ");

		ВыборкаПоДокументам = Результат[Результат.ВГраница() - 1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		СтруктураОтбораПоДокументу = Новый Структура;
		Пока ВыборкаПоДокументам.Следующий() Цикл

			ИдентификаторОшибки = Новый УникальныйИдентификатор;

			СтруктураОтбораПоДокументу.Вставить("Документ", ВыборкаПоДокументам.Документ);
			ТекстыОшибок = ОшибкиДокументов.Скопировать(СтруктураОтбораПоДокументу, "ОписаниеОшибки").ВыгрузитьКолонку(
				"ОписаниеОшибки");
			ТекстыОшибок = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ТекстыОшибок);
			ОписаниеОшибки = СтрСоединить(ТекстыОшибок, Символы.ПС);

			НачатьТранзакцию();

			Попытка
				// Запись в регистр ошибок
				ОшибкиНаборЗаписей = РегистрыСведений.ОшибкиОчередейЗаданий.СоздатьНаборЗаписей();
				ЗаписьОшибки = ОшибкиНаборЗаписей.Добавить();
				ЗаписьОшибки.ИдентификаторОшибки = ИдентификаторОшибки;
				ЗаписьОшибки.ОписаниеОшибки = ОписаниеОшибки;
				ЗаписьОшибки.ДатаЗаписи = ТекущаяДатаСеанса();
				УстановитьПривилегированныйРежим(Истина);
				ОшибкиНаборЗаписей.Записать(Ложь);
				УстановитьПривилегированныйРежим(Ложь);

				ДетальныеЗаписи = ВыборкаПоДокументам.Выбрать();

				Задания = СоздатьНаборЗаписей();
				Пока ДетальныеЗаписи.Следующий() Цикл

					ЗаданиеСОшибкой = Задания.Добавить();
					ЗаполнитьЗначенияСвойств(ЗаданиеСОшибкой, ДетальныеЗаписи);
					ЗаданиеСОшибкой.ИдентификаторЗаписи = Новый УникальныйИдентификатор;
					ЗаданиеСОшибкой.ВыполненоСОшибкой = Истина;
					ЗаданиеСОшибкой.ИдентификаторОшибки = ИдентификаторОшибки;

				КонецЦикла;
				
				УстановитьПривилегированныйРежим(Истина);
				Задания.Записать(Ложь);
				УстановитьПривилегированныйРежим(Ложь);
				
				ИдентификаторыПоДокументу = ИдентификаторыЗаданий.Скопировать(СтруктураОтбораПоДокументу,
					"ИдентификаторЗадания").ВыгрузитьКолонку("ИдентификаторЗадания");
					
				УстановитьПривилегированныйРежим(Истина);
				НеоперативнаяОчередьЗаданий.УдалитьЗаданияОчереди(ПараметрыДанных.ИмяРегистраОчереди,
					ИдентификаторыПоДокументу, ПараметрыДанных.ОчередиЗаданий);
				УстановитьПривилегированныйРежим(Ложь);
				
				ЗафиксироватьТранзакцию();
			
			Исключение

				ОтменитьТранзакцию();

				Если МетаданныеРегистраОчереди = Неопределено Тогда
					МетаданныеРегистраОчереди = Метаданные.РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов;
				КонецЕсли;

				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());

				НеоперативнаяОчередьЗаданий.ЗаписатьОшибкуВЖурналРегистрации(МетаданныеРегистраОчереди, ВидЗадания,
					ТекстОшибки);

			КонецПопытки;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СообщитьОбОшибкахОбработкиДанных(ПараметрыДанных)
	
	Если ПараметрыДанных.ПорцияДанных.Количество() > 0 Тогда
		
		ВидЗадания = ПараметрыДанных.ПорцияДанных[0].ВидЗадания;
		
		ОшибкиДокументов = ПараметрыДанных.ПорцияДанных.Скопировать( , "Документ, ОписаниеОшибки"); // ТаблицаЗначений
		ОшибкиДокументов.Свернуть("Документ, ОписаниеОшибки");
		
		ШаблонОшибки = НСтр("ru = 'Выполнение задания %1 по документу %2 завершилось с ошибкой: %3';
							|en = 'The ""%1"" job for the ""%2"" document is completed with an error: %3'", ОбщегоНазначения.КодОсновногоЯзыка());
				
		МассивОшибок = Новый Массив;
		Для Каждого ОшибкаВыполненияЗадания Из ОшибкиДокументов Цикл
			Если ЗначениеЗаполнено(ОшибкаВыполненияЗадания.ОписаниеОшибки) Тогда
				ТекстОшибкиПоДокументу = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки,ВидЗадания,
					ОшибкаВыполненияЗадания.Документ, ОшибкаВыполненияЗадания.ОписаниеОшибки);
				МассивОшибок.Добавить(ТекстОшибкиПоДокументу);
			КонецЕсли;
		КонецЦикла;
		
		Если МассивОшибок.Количество() > 0 Тогда
			ТекстИсключения = СтрСоединить(МассивОшибок, Символы.ПС);
			ВызватьИсключение ТекстИсключения;
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#Область РасчетСуммДокументовВВалютахУчета

Функция ПараметрыРасчетаСуммДокументовВВалютахУчета(ДокументыКОбработке)
	
	ПараметрыОбработки = Новый Структура;
	
	ТаблицаДокументовДляПересчета = РегистрыСведений.СуммыДокументовВВалютахУчета.ТаблицаДокументовДляПересчетаСуммВВалютахУчета(
		ДокументыКОбработке);
	
	МассивДокументовСКлиентами = ТаблицаДокументовДляПересчета.Скопировать(
		Новый Структура("ТипРасчетов",Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом)).ВыгрузитьКолонку("РасчетныйДокумент");
	МассивДокументовСПоставщиками = ТаблицаДокументовДляПересчета.Скопировать(
		Новый Структура("ТипРасчетов",Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)).ВыгрузитьКолонку("РасчетныйДокумент");
	
	ПараметрыОбработки.Вставить("МассивДокументовСКлиентами", МассивДокументовСКлиентами);
	ПараметрыОбработки.Вставить("МассивДокументовСПоставщиками", МассивДокументовСПоставщиками);
	
	Если МассивДокументовСКлиентами.Количество() > 0 Тогда
		ПараметрыРасчетаСКлиентами = РегистрыСведений.СуммыДокументовВВалютахУчета.ПараметрыРасчетаДокументовПоДаннымВзаиморасчетов(
			"РасчетыСКлиентами", МассивДокументовСКлиентами);
		ПараметрыОбработки.Вставить("ПараметрыРасчетаСКлиентами", ПараметрыРасчетаСКлиентами);
	КонецЕсли;
	Если МассивДокументовСПоставщиками.Количество() > 0 Тогда
		ПараметрыРасчетаСПоставщиками = РегистрыСведений.СуммыДокументовВВалютахУчета.ПараметрыРасчетаДокументовПоДаннымВзаиморасчетов(
			"РасчетыСПоставщиками", МассивДокументовСПоставщиками);
		ПараметрыОбработки.Вставить("ПараметрыРасчетаСПоставщиками", ПараметрыРасчетаСПоставщиками);
	КонецЕсли;
	
	ПараметрыДляРасчетаДисконтирования = РегистрыСведений.СуммыДокументовВВалютахУчета.ПараметрыДляРасчетаДисконтированияПоДокументам(
		ДокументыКОбработке);
	ПараметрыОбработки.Вставить("ПараметрыДляРасчетаДисконтирования", ПараметрыДляРасчетаДисконтирования);

	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура РассчитатьСуммыДокументовВВалютахУчета(СсылкаНаДокумент, ПараметрыОбработки)
	
	МассивДокументовСКлиентами = ПараметрыОбработки.МассивДокументовСКлиентами;
	МассивДокументовСПоставщиками = ПараметрыОбработки.МассивДокументовСПоставщиками;
	ОтборПоДокументу = Новый Структура("Ссылка", СсылкаНаДокумент);
	
	Если Не МассивДокументовСКлиентами.Найти(СсылкаНаДокумент) = Неопределено Тогда
		
		РезультатПоДокументам = ПараметрыОбработки.ПараметрыРасчетаСКлиентами.РезультатЗапросаПоДокументам;

		ВыборкаПоДокументам = РезультатПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДокументам.НайтиСледующий(ОтборПоДокументу) Цикл

			РегистрыСведений.СуммыДокументовВВалютахУчета.РаспределитьСуммыПоДокументуПоДаннымВзаиморасчетов(
				ВыборкаПоДокументам, ПараметрыОбработки.ПараметрыРасчетаСКлиентами);

		КонецЦикла;
		
	КонецЕсли;
	
	Если Не МассивДокументовСПоставщиками.Найти(СсылкаНаДокумент) = Неопределено Тогда
		
		РезультатПоДокументам = ПараметрыОбработки.ПараметрыРасчетаСПоставщиками.РезультатЗапросаПоДокументам;

		ВыборкаПоДокументам = РезультатПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДокументам.НайтиСледующий(ОтборПоДокументу) Цикл

			РегистрыСведений.СуммыДокументовВВалютахУчета.РаспределитьСуммыПоДокументуПоДаннымВзаиморасчетов(
				ВыборкаПоДокументам, ПараметрыОбработки.ПараметрыРасчетаСПоставщиками);

		КонецЦикла;
		
	КонецЕсли;
	
	Если МассивДокументовСКлиентами.Найти(СсылкаНаДокумент) = Неопределено 
		И МассивДокументовСПоставщиками.Найти(СсылкаНаДокумент) = Неопределено Тогда
		
		ВыборкаПоДокументам = ПараметрыОбработки.ПараметрыДляРасчетаДисконтирования.ВыборкаПоДокументам;
		
		Пока ВыборкаПоДокументам.НайтиСледующий(ОтборПоДокументу) Цикл

			РегистрыСведений.СуммыДокументовВВалютахУчета.РассчитатьДисконтированиеПоДокументу(ВыборкаПоДокументам,
				ПараметрыОбработки.ПараметрыДляРасчетаДисконтирования);

		КонецЦикла;
	
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область АктуализацияДвиженийПоФинансовымРегистрам

Функция ПараметрыАктуализацииДвиженийПоФинансовымРегистрам(ДокументыКОбработке)
	
	ПараметрыОбработки = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СуммыДокументовВВалютахУчета.Регистратор КАК Документ
	|ИЗ
	|	РегистрСведений.СуммыДокументовВВалютахУчета КАК СуммыДокументовВВалютахУчета
	|ГДЕ
	|	СуммыДокументовВВалютахУчета.Регистратор В (&СписокДокументов)
	|	И СуммыДокументовВВалютахУчета.Активность";
	Запрос.УстановитьПараметр("СписокДокументов", ДокументыКОбработке);
	
	ПараметрыОбработки.Вставить("ПроведенныеДокументы", Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Документ"));

	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура ОбновитьДвиженияДокументаПоФинансовымРегистрам(Документ, ПараметрыОбработки)
	
	ПроведенныеДокументы = ПараметрыОбработки.ПроведенныеДокументы;
	Если ПроведенныеДокументы.Найти(Документ) <> Неопределено Тогда
		РегистрыСведений.СуммыДокументовВВалютахУчета.ОбновитьДвиженияДокументов(Документ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область АктуализацияДвиженияДенежныеСредстваКонтрагент

Функция ПараметрыАктуализацииДвиженийДенежныеСредстваКонтрагент(ДокументыКОбработке, ЭтоРасчетыСКлиентами = Истина)
	
	ПараметрыОбработки = Новый Структура;
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	ДанныеДвижений = УправленческийУчетПроведениеСервер.ДанныеДвиженийДенежныеСредстваКонтрагент(ДокументыКОбработке,
		ЭтоРасчетыСКлиентами, МенеджерВременныхТаблиц);
	
	ПараметрыОбработки.Вставить("ДанныеДвижений", ДанныеДвижений);

	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура СформироватьДвиженияДенежныеСредстваКонтрагент(СсылкаНаДокумент, ПараметрыОбработки)
	
	ДанныеДвижений = ПараметрыОбработки.ДанныеДвижений; //РезультатаЗапроса
	
	ВыборкаПоДокументам = ДанныеДвижений.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Регистратор", СсылкаНаДокумент)) Цикл
		УправленческийУчетПроведениеСервер.ЗаписатьДвиженияДенежныеСредстваКонтрагент(ВыборкаПоДокументам,
			Неопределено, Неопределено);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область АктуализацияДвиженияКонтрагентКонтрагент

Функция ПараметрыАктуализацииДвиженийКонтрагентКонтрагент(ДокументыКОбработке)
	
	ПараметрыОбработки = Новый Структура;
	
	ДанныеДвижений = УправленческийУчетПроведениеСервер.ДанныеДвиженийКонтрагентКонтрагент(ДокументыКОбработке);
	
	ПараметрыОбработки.Вставить("ДанныеДвижений", ДанныеДвижений);

	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура СформироватьДвиженияКонтрагентКонтрагент(СсылкаНаДокумент, ПараметрыОбработки)
	
	ДанныеДвижений = ПараметрыОбработки.ДанныеДвижений; //РезультатаЗапроса

	Если Не ДанныеДвижений = Неопределено Тогда
		ВыборкаПоДокументам = ДанныеДвижений.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Регистратор", СсылкаНаДокумент)) Цикл
			УправленческийУчетПроведениеСервер.ЗаписатьДвиженияКонтрагентКонтрагент(ВыборкаПоДокументам, Неопределено);
			УправленческийУчетПроведениеСервер.СформироватьДвиженияПоПрочимАктивамПассивам(ВыборкаПоДокументам);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОтразитьДокументыВУправленческомБалансе

Функция ПараметрыОтраженияДокументовВУправленческомБалансе(ДокументыКОбработке)
	
	ПараметрыОбработки = Новый Структура;
	
	ДанныеДвижений = Обработки.ДвиженияАктивовПассивов.ДанныеДвиженийОтраженияДокументовВУправленческомБалансе(
		ДокументыКОбработке);
	
	ПараметрыОбработки.Вставить("ДанныеДвижений", ДанныеДвижений);

	Возврат ПараметрыОбработки;
	
КонецФункции

Процедура ОтразитьДокументВУправленческомБалансе(СсылкаНаДокумент, ПараметрыОбработки)
	
	ДанныеДвижений = ПараметрыОбработки.ДанныеДвижений; //РезультатаЗапроса

	Если Не ДанныеДвижений = Неопределено Тогда
		ВыборкаПоДокументам = ДанныеДвижений.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаПоДокументам.НайтиСледующий(Новый Структура("Регистратор", СсылкаНаДокумент)) Цикл
			Обработки.ДвиженияАктивовПассивов.ЗаписатьДвиженияПрочиеАктивыПассивы(ВыборкаПоДокументам);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти 

#Область СформироватьДвиженияПоАкцизам

Процедура СформироватьДвиженияПоАкцизам(СсылкаНаДокумент, ПараметрыОбработки)
	
	УчетПодакцизныхТоваров.ЗаписатьСуммыАкциза(СсылкаНаДокумент);

КонецПроцедуры

#КонецОбласти

//++ НЕ УТ

#Область СформироватьДвиженияПоПрочимАктивамПассивам

Процедура СформироватьДвиженияПоПрочимАктивамПассивам(СсылкаНаДокумент, ПараметрыОбработки)
	
	ПрочиеАктивыПассивыСервер.ЗаписатьПрочиеАктивыПассивы(СсылкаНаДокумент);
	
КонецПроцедуры

#КонецОбласти

//-- НЕ УТ

#Область ОбновлениеИнформационнойБазы

// Добавляет в список процедуры-обработчики обновления данных ИБ
// для всех поддерживаемых версий библиотеки или конфигурации.
// Вызывается перед началом обновления данных ИБ для построения плана обновления.
//
//  Обработчики - ТаблицаЗначений - описание полей, см. в процедуре
//                ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.Версия              = "1.1.0.0";
//  Обработчик.Процедура           = "ОбновлениеИБ.ПерейтиНаВерсию_1_1_0_0";
//  Обработчик.МонопольныйРежим    = Ложь.
//
// Параметры:
// 	Обработчики - см. ОбновлениеИнформационнойБазы.НоваяТаблицаОбработчиковОбновления
//
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "11.5.23.17";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("3a7f1120-2cdb-11ef-841c-af5642bc9f0d");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Некритичный;
	Обработчик.Комментарий = НСтр("ru = 'Регистрация заданий неоперативного допроведения документов';
									|en = 'Register jobs of backdate additional document posting'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСКлиентами.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыНакопления.РасчетыСПоставщиками.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ЗаданияНеоперативногоДопроведенияДокументов.ПолноеИмя());
	
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСКлиентами.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыНакопления.РасчетыСПоставщиками.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ЗаданияКЗакрытиюМесяца.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "До";
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = "РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов";
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", "РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов");

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НоваяАрхитектураВзаиморасчетов", ПолучитьФункциональнуюОпцию("НоваяАрхитектураВзаиморасчетов"));
	Запрос.УстановитьПараметр("ГлавныйУзел", ПланыОбмена.ГлавныйУзел() = Неопределено);
	Запрос.УстановитьПараметр("ПервоначальнаяРегистрацияЗаданий", Константы.ВыполненаПервоначальнаяРегистрацияЗаданийНеоперативногоДопроведенияДокументов.Получить());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ЗаданияКЗакрытиюМесяца.Месяц) КАК Месяц,
	|	ЗаданияКЗакрытиюМесяца.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПериодыЗакрытия
	|ИЗ
	|	РегистрСведений.ЗаданияКЗакрытиюМесяца КАК ЗаданияКЗакрытиюМесяца
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаБлокировкиОтИзменений КАК НастройкаБлокировкиОтИзменений
	|	ПО ЗаданияКЗакрытиюМесяца.Организация = НастройкаБлокировкиОтИзменений.Организация
	|ГДЕ
	|	ЗаданияКЗакрытиюМесяца.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоРасчетамСПартнерамиИПереоценкаРасчетов)
	|	И ЗаданияКЗакрытиюМесяца.Месяц >= ЕСТЬNULL(НастройкаБлокировкиОтИзменений.ЗаблокироватьПо, ДАТАВРЕМЯ(1,1,1))
	|
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКЗакрытиюМесяца.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Таблица.Документ КАК Документ
	|ИЗ
	|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСКлиентами.Регистратор КАК Документ
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО РасчетыСКлиентами.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов И &ГлавныйУзел
	|		И НЕ &ПервоначальнаяРегистрацияЗаданий
	|		И ТИПЗНАЧЕНИЯ(РасчетыСКлиентами.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|		И РасчетыСКлиентами.Сумма <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		РасчетыСПоставщиками.Регистратор КАК Документ
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО РасчетыСПоставщиками.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		&НоваяАрхитектураВзаиморасчетов И &ГлавныйУзел
	|		И НЕ &ПервоначальнаяРегистрацияЗаданий
	|		И ТИПЗНАЧЕНИЯ(РасчетыСПоставщиками.Регистратор) <> ТИП(Документ.КорректировкаРегистров)
	|		И РасчетыСПоставщиками.Сумма <> 0) КАК Таблица";
	
	Регистраторы = Запрос.Выполнить().Выгрузить();
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, Регистраторы, ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяОбъекта = "РегистрСведений.ЗаданияНеоперативногоДопроведенияДокументов";
	Если Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(Параметры.Очередь, ПолноеИмяОбъекта) Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Константы.ВыполненаПервоначальнаяРегистрацияЗаданийНеоперативногоДопроведенияДокументов.Установить(Истина);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Данные.Документ КАК Документ
	|ПОМЕСТИТЬ ВтДанныеДляОбработки
	|ИЗ
	|	&ОбновляемыеДанные КАК Данные
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(ЗаданияКЗакрытиюМесяца.Месяц) КАК Месяц,
	|	ЗаданияКЗакрытиюМесяца.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ПериодыЗакрытия
	|ИЗ
	|	РегистрСведений.ЗаданияКЗакрытиюМесяца КАК ЗаданияКЗакрытиюМесяца
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НастройкаБлокировкиОтИзменений КАК НастройкаБлокировкиОтИзменений
	|		ПО ЗаданияКЗакрытиюМесяца.Организация = НастройкаБлокировкиОтИзменений.Организация
	|ГДЕ
	|	ЗаданияКЗакрытиюМесяца.Операция = ЗНАЧЕНИЕ(Справочник.ОперацииЗакрытияМесяца.ФормированиеДвиженийПоРасчетамСПартнерамиИПереоценкаРасчетов)
	|	И ЗаданияКЗакрытиюМесяца.Месяц >= ЕСТЬNULL(НастройкаБлокировкиОтИзменений.ЗаблокироватьПо, ДАТАВРЕМЯ(1, 1, 1))
	|СГРУППИРОВАТЬ ПО
	|	ЗаданияКЗакрытиюМесяца.Организация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Месяц,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеПоДокументам.Документ КАК Документ,
	|	ДанныеПоДокументам.Организация КАК Организация,
	|	МИНИМУМ(ДанныеПоДокументам.ПериодЗадания) КАК ПериодЗадания
	|ИЗ
	|	(ВЫБРАТЬ
	|		РасчетыСКлиентами.Регистратор КАК Документ,
	|		РасчетыСКлиентами.Период КАК ПериодЗадания,
	|		РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСКлиентами КАК РасчетыСКлиентами
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДляОбработки КАК ОбрабатываемыеДокументы
	|			ПО РасчетыСКлиентами.Регистратор = ОбрабатываемыеДокументы.Документ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО РасчетыСКлиентами.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И РасчетыСКлиентами.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		РасчетыСКлиентами.Сумма <> 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		РасчетыСПоставщиками.Регистратор КАК Документ,
	|		РасчетыСПоставщиками.Период КАК ПериодЗадания,
	|		РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.РасчетыСПоставщиками КАК РасчетыСПоставщиками
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВтДанныеДляОбработки КАК ОбрабатываемыеДокументы
	|			ПО РасчетыСПоставщиками.Регистратор = ОбрабатываемыеДокументы.Документ
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыЗакрытия КАК ВТ_ПериодыЗакрытия
	|			ПО РасчетыСПоставщиками.Период >= ВТ_ПериодыЗакрытия.Месяц
	|			И РасчетыСПоставщиками.АналитикаУчетаПоПартнерам.Организация = ВТ_ПериодыЗакрытия.Организация
	|	ГДЕ
	|		РасчетыСПоставщиками.Сумма <> 0) КАК ДанныеПоДокументам
	|СГРУППИРОВАТЬ ПО
	|	ДанныеПоДокументам.Документ,
	|	ДанныеПоДокументам.Организация";
	
	Запрос.УстановитьПараметр("ОбновляемыеДанные", ОбновляемыеДанные);
	ДанныеПоДокументам = Запрос.Выполнить().Выгрузить();
	ДанныеПоДокументам.Индексы.Добавить("Документ");
	
	ОбъектовОбработано = 0;
	ПроблемныхОбъектов = 0;
	ИсключенияПриОбновлении = Новый Массив;

	СписокОписаний = Новый Массив;
	СписокОписаний.Добавить(НСтр(
			"ru = 'Не удалось обработать документы по обработчику регистра сведений ""Задания неоперативного допроведения документов"":';
			|en = 'Cannot process the documents of the ""Jobs of backdate additional document posting"" information register handler:'"));
	СписокОписаний.Добавить(НСтр("ru = '- создание заданий неоперативной очереди';
								|en = '- create backdate queue jobs'"));
	
	ТипыРегистраторовСуммыДокументовВВалютахУчета = Метаданные.РегистрыСведений.СуммыДокументовВВалютахУчета.СтандартныеРеквизиты.Регистратор.Тип;
	ТипыРегистраторовДвиженияДенежныеСредстваКонтрагент = Метаданные.РегистрыНакопления.ДвиженияДенежныеСредстваКонтрагент.СтандартныеРеквизиты.Регистратор.Тип;
	ТипыРегистраторовДвиженияКонтрагентКонтрагент = Метаданные.РегистрыНакопления.ДвиженияКонтрагентКонтрагент.СтандартныеРеквизиты.Регистратор.Тип;
	ТипыРегистраторовПрочиеАктивыПассивы = Метаданные.РегистрыНакопления.ПрочиеАктивыПассивы.СтандартныеРеквизиты.Регистратор.Тип;
	
	Для Каждого ТекущиеДанные Из ОбновляемыеДанные Цикл

		ПричинаИсключения = 0;
		Рекомендация = "";

		НачатьТранзакцию();

		Попытка

			ПричинаИсключения = 1; // ПлохиеДанные
			Рекомендация = НСтр("ru = 'Перепроведите документ вручную.';
								|en = 'Repost the document manually.'");

			ТипРегистратора = ТипЗнч(ТекущиеДанные.Документ);
			ДанныеПоОрганизациям = ДанныеПоДокументам.НайтиСтроки(Новый Структура("Документ", ТекущиеДанные.Документ));

			НаборЗаписей = СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Документ.Установить(ТекущиеДанные.Документ);
			
			Если ДанныеПоОрганизациям.Количество() > 0 Тогда
				
				Если ТипыРегистраторовСуммыДокументовВВалютахУчета.СодержитТип(ТипРегистратора) Тогда
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.АктуализацияСуммДокументовВВалютахУчета,
						ДанныеПоОрганизациям);
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженийПоФинансовымРегистрам,
						ДанныеПоОрганизациям);
				КонецЕсли;

				Если ТипыРегистраторовДвиженияДенежныеСредстваКонтрагент.СодержитТип(ТипРегистратора) Тогда
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентКлиенты,
						ДанныеПоОрганизациям);
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияДенежныеСредстваКонтрагентПоставщики,
						ДанныеПоОрганизациям);
				КонецЕсли;

				Если ТипыРегистраторовДвиженияКонтрагентКонтрагент.СодержитТип(ТипРегистратора) Тогда
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.АктуализацияДвиженияКонтрагентКонтрагент,
						ДанныеПоОрганизациям);
				КонецЕсли;

				Если ТипыРегистраторовПрочиеАктивыПассивы.СодержитТип(ТипРегистратора) Тогда
					СоздатьЗаданияПоДокументу(НаборЗаписей, ТекущиеДанные.Документ,
						Справочники.ВидыНеоперативныхЗаданий.ОтражениеДвиженийПоУправленческомуБалансу,
						ДанныеПоОрганизациям);
				КонецЕсли;

				ПричинаИсключения = 2; // Запись
				Если НаборЗаписей.Количество() > 0 Тогда
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Ложь);
				Иначе
					ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
				КонецЕсли;
				
			Иначе
				ПричинаИсключения = 2; // Запись
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);
			КонецЕсли;

			ОбъектовОбработано = ОбъектовОбработано + 1;

			ЗафиксироватьТранзакцию();

		Исключение

			ОтменитьТранзакцию();

			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;

			ОбновлениеИнформационнойБазыУТ.СообщитьОНеудачнойОбработке(ИнформацияОбОшибке(), ТекущиеДанные.Документ);

			Если ПричинаИсключения = 1 Тогда

				ОписаниеПроблемы = ОбновлениеИнформационнойБазыУТ.ПроблемаСДанными(
							ТекущиеДанные.Документ, Рекомендация, ИнформацияОбОшибке());
				ИсключенияПриОбновлении.Добавить(ОписаниеПроблемы);
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(НаборЗаписей);

			ИначеЕсли ПричинаИсключения = 2 Тогда

				ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(
							ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);
				ВызватьИсключение СтрСоединить(СписокОписаний, Символы.ПС);

			КонецЕсли;

		КонецПопытки;

	КонецЦикла;//по обновляемым данным
	
	ОбновлениеИнформационнойБазыУТ.ЗаписатьПлохиеДанные(ИсключенияПриОбновлении, ОбъектовОбработано, Параметры);

	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяОбъекта);
	
	Если Параметры.ОбработкаЗавершена Тогда
		Константы.ВыполненаПервоначальнаяРегистрацияЗаданийНеоперативногоДопроведенияДокументов.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаданияПоДокументу(НаборЗаписей, Документ, ВидЗадания, ПараметрыЗаданий)
	
	Для каждого ДанныеПоОрганизации Из ПараметрыЗаданий Цикл
				
		НовоеЗадание = НаборЗаписей.Добавить();
		НовоеЗадание.Организация = ДанныеПоОрганизации.Организация;
		НовоеЗадание.ПериодЗадания = ДанныеПоОрганизации.ПериодЗадания;
		НовоеЗадание.ВидЗадания = ВидЗадания;
		НовоеЗадание.Документ = Документ;
		НовоеЗадание.ИдентификаторЗаписи = Новый УникальныйИдентификатор();
				
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли