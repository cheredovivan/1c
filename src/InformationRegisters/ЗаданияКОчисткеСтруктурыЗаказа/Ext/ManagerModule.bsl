#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет задания к расчету структуры заказа на производство
// 
// Параметры:
// 	ЗаказыНаПроизводство - Массив, ДокументСсылка.ЗаказНаПроизводство2_2	- заказ на производство или список заказов на производство.
Процедура ДобавитьЗадания(Знач ЗаказыНаПроизводство) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	ЗаказыНаПроизводство = ОбщегоНазначенияУТКлиентСервер.Массив(ЗаказыНаПроизводство);
	
	НаборЗаписей = РегистрыСведений.ЗаданияКОчисткеСтруктурыЗаказа.СоздатьНаборЗаписей();
	
	Для каждого ЗаказНаПроизводство Из ЗаказыНаПроизводство Цикл
	
		НоваяЗапись = НаборЗаписей.Добавить();
		НоваяЗапись.ЗаказНаПроизводство = ЗаказНаПроизводство;
		НоваяЗапись.Разделитель = Новый УникальныйИдентификатор()
		
	КонецЦикла;
	
	НаборЗаписей.Записать(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Выбирает изменения из очереди по границу расчета (включая)
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
// 
// Возвращаемое значение:
//  Число - номер очереди
//
Функция ВыбратьИзменения(ГраницаРасчета) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Граница                       КАК Очередь,
	|	&Разделитель                   КАК Разделитель,
	|	Таблица.ЗаказНаПроизводство    КАК ЗаказНаПроизводство
	|ИЗ
	|	РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Таблица
	|ГДЕ 
	|	Таблица.Очередь = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.ЗаказНаПроизводство
	|";
	Запрос.УстановитьПараметр("Граница", ГраницаРасчета);
	Запрос.УстановитьПараметр("Разделитель", Новый УникальныйИдентификатор);
	
	НачатьТранзакцию();
	Попытка
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа");
		ЭлементБлокировки.УстановитьЗначение("Очередь", 0);
		Блокировка.Заблокировать();
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Набор = РегистрыСведений.ЗаданияКОчисткеСтруктурыЗаказа.СоздатьНаборЗаписей();
			Набор.Загрузить(РезультатЗапроса.Выгрузить());
			Набор.Записать(Ложь);
			
			Набор = РегистрыСведений.ЗаданияКОчисткеСтруктурыЗаказа.СоздатьНаборЗаписей();
			Набор.Отбор.Очередь.Установить(0);
			Набор.Записать(Истина);
			
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации(СтруктураЗаказа.ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Таблица
		|ГДЕ
		|	Таблица.Очередь <= &ГраницаРасчета
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди;
	
КонецФункции

// Удаляет задания по номеру очереди
// 
// Параметры:
// 	Очередь - Число - номер очереди
Процедура УдалитьЗадания(Очередь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ЗаданияКОчисткеСтруктурыЗаказа.СоздатьНаборЗаписей();
	Набор.Отбор.Очередь.Установить(Очередь);
	Набор.Записать(Истина);
	
КонецПроцедуры

// Проверяет, есть ли задания к расчету
//
// Параметры:
//  ГраницаРасчета	 - Число - граница расчета
//  НомерОчереди	 - Число - минимальный номер очереди (возвращаемое значение)
// 
// Возвращаемое значение:
//  Булево - есть задания к расчету
//
Функция ЕстьЗаданияКРасчету(ГраницаРасчета = -1, НомерОчереди = -1) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("ГраницаРасчета", ГраницаРасчета);
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Очередь КАК Очередь
		|ИЗ
		|	РегистрСведений.ЗаданияКОчисткеСтруктурыЗаказа КАК Таблица
		|ГДЕ
		|	(&ГраницаРасчета = -1
		|			ИЛИ Таблица.Очередь <= &ГраницаРасчета)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Очередь";
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		НомерОчереди = -1;
	Иначе
		НомерОчереди = РезультатЗапроса.Выгрузить()[0].Очередь;
	КонецЕсли;
	
	Возврат НомерОчереди >= 0;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолноеИмя() Экспорт

	ПолноеИмя = Метаданные.РегистрыСведений.ЗаданияКОчисткеСтруктурыЗаказа.ПолноеИмя();

	Возврат ПолноеИмя;

КонецФункции

#КонецОбласти

#КонецЕсли