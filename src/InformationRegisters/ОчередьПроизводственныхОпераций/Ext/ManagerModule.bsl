#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Рассчитывает и записывает очередь производственных операций
//  (вызывается при обработки очереди заданий к расчету очереди производственных операций).
//
// Параметры:
//  Задания - ТаблицаЗначений - колонки соответствуют измерениям, ресурсам и реквизитам регистра сведений
//                              "ЗаданияКРасчетуОчередиПроизводственныхОпераций", есть и служебные колонки:
//             * ОбъектРасчета       - ДокументСсылка.ЭтапПроизводства2_2 -
//             * ИдентификаторЗаписи    - УникальныйИдентификатор -
//             * УдалитьДеньРегистрации - Число  -
//             * ДатаЗаписи             - Дата   -
//             * ИдентификаторЗадания   - УникальныйИдентификатор - служебная
//             * ДатаЗадания            - Дата                    - служебная
//  ИдентификаторыНеОбработанныхЗаписей - Соответствие из УникальныйИдентификатор - ключ, это идентификатор 
//                                                                                   не выполненных (выдающих ошибку при
//                                                                                   выполнении) записей регистра
//                                                                                   сведений очереди заданий
//                                                                                   (измерение ИдентификаторЗаписи
//                                                                                   регистра сведений очереди заданий),
//                                                                                  значение, это представление ошибки.
//  ДополнительныеСвойства - Неопределено, Структура - дополнительные свойства выполнения заданий.
//
Процедура РассчитатьОчередьОтложенно(
			Задания,
			ИдентификаторыНеОбработанныхЗаписей,
			ДополнительныеСвойства = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ПоляОшибки = Неопределено;
	
	Этапы = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Задания.ВыгрузитьКолонку("ОбъектРасчета"));
	
	ДанныеЭтапов = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этапы);
	
	МетаРегистра = Неопределено;
	ИмяСобытия   = Неопределено;
	
	Для каждого Этап Из Этапы Цикл
		
		ДанныеЭтапа = ДанныеЭтапов[Этап];
		
		НачатьТранзакцию();
		
		Попытка
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ЭтапПроизводства2_2");
			ЭлементБлокировки.УстановитьЗначение("Распоряжение",       ДанныеЭтапа.Распоряжение);
			ЭлементБлокировки.УстановитьЗначение("ПартияПроизводства", ДанныеЭтапа.ПартияПроизводства);
			ЭлементБлокировки.УстановитьЗначение("Ссылка",             Этап);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			БлокировкаДанных.Заблокировать();
			
			РассчитатьОчередьПоДаннымЭтапа(Этап, ДанныеЭтапа, ПараметрыРасчетаОчереди());
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			Если МетаРегистра = Неопределено Тогда
				МетаРегистра = Метаданные.РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций;
				ИмяСобытия   = ОтложенныеЗадания.ИмяСобытия();
			КонецЕсли;
			
			ТекстИсключения = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, МетаРегистра,, ТекстИсключения);
			
			Если ПоляОшибки = Неопределено Тогда
				
				Задания.Индексы.Добавить("ОбъектРасчета");
				
				ПоляОшибки = Новый Структура;
				ПоляОшибки.Вставить("ОбъектРасчета");
				
				ДанныеОшибки = Новый Структура;
				ДанныеОшибки.Вставить("ОбъектРасчета");
				
			КонецЕсли;
			
			ДанныеОшибки.ОбъектРасчета = Этап;
			
			ТекстОшибки = ОтложенныеЗадания.ТекстОшибкиВыполнения(ПоляОшибки, ДанныеОшибки, ТекстИсключения);
			
			НайденныеСтроки = Задания.НайтиСтроки(ДанныеОшибки);
			Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
				ИдентификаторыНеОбработанныхЗаписей.Вставить(СтрокаТаблицы.ИдентификаторЗаписи, ТекстОшибки);
			КонецЦикла;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

// Рассчитывает и записывает очередь производственных операций.
//
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2 - обрабатываемый документ
//  ДанныеЭтапа - Структура - данные этапа для расчета очереди операций (должны быть прочитаны ответственно)
//  ПараметрыРасчета - см. ПараметрыРасчетаОчереди
//
Процедура РассчитатьОчередьПоДаннымЭтапа(Этап, ДанныеЭтапа, ПараметрыРасчета) Экспорт
	
	Если НЕ ДанныеЭтапа.ТребуетсяРассчитать
			И НЕ ДанныеЭтапа.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		Возврат;
	КонецЕсли;
	
	Очередь = ОчередьОпераций(Этап, ДанныеЭтапа);
	ЗаписатьОчередь(Этап, Очередь);
	
	Если ДанныеЭтапа.ИспользоватьСменныеЗадания
		И НЕ ДанныеЭтапа.ВАрхиве
		И ПараметрыРасчета.РассчитыватьСменныеЗадания Тогда
		
		РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.РассчитатьОперации(Этап);
		
	КонецЕсли;
	
КонецПроцедуры

// Добавляет в очередь заданий к расчету данных регистра новое задание.
//
// Параметры:
//  Этап				 - ДокументСсылка.ЭтапПроизводства2_2	 - этап производства.
//  УдалениеПроведения	 - Булево								 - признак обработки удаления проведения.
//  Отказ				 - Булево								 - признак прерывания обработки проведения.
//
Процедура ДобавитьЗаданиеКРасчетуОчереди(Этап, УдалениеПроведения = Ложь, Отказ = Ложь) Экспорт
	
	ЗаблокироватьДанныеДляРасчетаОчереди(Этап, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеЭтапа = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этап)[Этап];
	
	ВыполнитьКонтрольПередРасчетомОчереди(ДанныеЭтапа, УдалениеПроведения, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеЭтапа.ИспользоватьСменныеЗадания Тогда
		РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.УдалитьНеназначенныеОперации(Этап);
	КонецЕсли;
	
	Если ДанныеЭтапа.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Индивидуальный Тогда
		Рассчитать = Истина;
	Иначе
		ОчиститьОчередь(Этап);
		Рассчитать = ДанныеЭтапа.ТребуетсяРассчитать И НЕ УдалениеПроведения;
	КонецЕсли;
	
	Если Рассчитать Тогда
		РегистрыСведений.ЗаданияКРасчетуОчередиПроизводственныхОпераций.ДобавитьЗадания(Этап);
	КонецЕсли;
	
КонецПроцедуры

// Пересчитывает очередь по указанной технологической операции.
//
// Параметры:
//  КлючОперации - см. УправлениеПроизводствомКлиентСервер.КлючПроизводственнойОперации
//  ПараметрыРасчета - см. ПараметрыРасчетаОчереди
//  Отказ - Булево - признак прерывания обработки проведения.
//
Процедура ПересчитатьОчередь(КлючОперации, ПараметрыРасчета, Отказ = Ложь) Экспорт
	
	ЗаблокироватьОчередьДляЗаписиПоКлючу(КлючОперации, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Операции = ОкружениеОперацииВОчереди(КлючОперации, ПараметрыРасчета);
	
	ДанныеОперации = НайтиОперациюПоКлючу(Операции, КлючОперации);
	Если ДанныеОперации = Неопределено Тогда
		СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ);
		Возврат;
	КонецЕсли;
	
	Этап = КлючОперации.Этап;
	
	ЗаблокироватьДанныеДляРасчетаОчередиПоОперации(Этап, ДанныеОперации, ПараметрыРасчета, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПересчитатьОчередьПоОперации(Этап, ДанныеОперации, ПараметрыРасчета, Отказ);
	
КонецПроцедуры

// Параметры пересчета очереди по указанной технологической операции.
// 
// Возвращаемое значение:
//  Структура - Параметры пересчета очереди:
// * РассчитыватьВсеПоследующиеОперации - Булево - признак необходимости пересчитать все операции после заданной
// * РассчитыватьСменныеЗадания - Булево - признак необходимости расчета данных регистра "Операции к созданию сменных заданий"
Функция ПараметрыРасчетаОчереди() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("РассчитыватьВсеПоследующиеОперации", Ложь);
	Результат.Вставить("РассчитыватьСменныеЗадания", Истина);
	
	Возврат Результат;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Подразделение)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Конструкторы

// Конструктор очереди производственных операций
// 
// Возвращаемое значение:
//  РегистрСведенийНаборЗаписей.ОчередьПроизводственныхОпераций
Функция ОчередьОперацийКонструктор() Экспорт
	
	НаборЗаписей = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	
	Возврат НаборЗаписей.ВыгрузитьКолонки();
	
КонецФункции

Функция ПоказателиХодаВыполненияКонструктор()
	
	Результат = Новый Структура;
	
	Результат.Вставить("Формируется",            0);
	Результат.Вставить("Создано",                0);
	Результат.Вставить("Выполняется",            0);
	Результат.Вставить("Выполнено",              0);
	Результат.Вставить("ТребуетПовторения",      0);
	Результат.Вставить("Пропущено",              0);
	Результат.Вставить("НаКонтроле",             0);
	Результат.Вставить("НаДоработке",            0);
	Результат.Вставить("Брак",                   0);
	Результат.Вставить("Отменено",               0);
	Результат.Вставить("ОтмененоИнд",            0);
	Результат.Вставить("ВремяСозданныхОпераций", 0);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РассчитатьПоЭтапу

// Блокировка данных для расчета очереди.
// 
// Параметры:
//  Этап - ДокументСсылка.ЭтапПроизводства2_2
// 
// Возвращаемое значение:
//  БлокировкаДанных - Блокировка данных для расчета очереди
Функция БлокировкаДанныхДляРасчетаОчереди(Этап) Экспорт
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
	ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьПооперационноеПланирование") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ПооперационноеРасписание2_2");
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСменныеЗадания") Тогда
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОперацииКСозданиюСменныхЗаданий");
		ЭлементБлокировки.УстановитьЗначение("СменноеЗадание", Документы.СменноеЗадание.ПустаяСсылка());
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	КонецЕсли;
	
	Возврат БлокировкаДанных;
	
КонецФункции

Процедура ЗаблокироватьДанныеДляРасчетаОчереди(Этап, Отказ)
	
	БлокировкаДанных = БлокировкаДанныхДляРасчетаОчереди(Этап);
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %1';
										|en = 'Cannot lock the queue of routing operations: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецПопытки;
	
КонецПроцедуры

Функция ОчередьОпераций(Этап, ДанныеЭтапа)
	
	КлючеваяОперация = "МежцеховоеУправление2_2.РасчетОчередиПроизводственныхОпераций";
	ОписаниеЗамера = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	
	ОчередьОпераций = ОчередьОперацийВнутриЗамераВремени(Этап, ДанныеЭтапа);
	
	ВремяНачалаЗамера = ОписаниеЗамера.Получить("ВремяНачала");
	Если ВремяНачалаЗамера <> Неопределено
		И ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяНачалаЗамера > 0 Тогда
		КоличествоДанных = ОчередьОпераций.Количество();
		ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(ОписаниеЗамера, КоличествоДанных);
	КонецЕсли;
	
	Возврат ОчередьОпераций;
	
КонецФункции

Функция ОчередьОперацийВнутриЗамераВремени(Этап, ДанныеЭтапа)
	
	ОчередьОпераций = ОчередьОперацийКонструктор();
	
	СписокОпераций = СписокОперацийПоДаннымЭтапа(ДанныеЭтапа, Истина);
	ДополнитьОперацииРезультатомВыполнения(Этап, СписокОпераций);
	ДобавитьЗаполнитьСлужебныеРеквизитыОпераций(СписокОпераций);
	
	Для Индекс = 0 По СписокОпераций.Количество() - 1 Цикл
		
		ДанныеОперации = СписокОпераций[Индекс];
		
		НоваяОперация = ОчередьОпераций.Добавить();
		НоваяОперация.Этап      = Этап;
		НоваяОперация.Требуется = ДанныеОперации.КоличествоНаПартию;
		
		ЗаполнитьЗначенияСвойств(
			НоваяОперация,
			ДанныеОперации,
			"Операция,
			|ИдентификаторОперации,
			|ИдентификаторКонтрольнойОперации,
			|НомерОперации,
			|НомерСледующейОперации,
			|РабочийЦентр,
			|ПроизводственнаяОперация
			//++ Локализация
			|,
			|Должность,
			|Разряд
			//-- Локализация
			|");
		
		ЗаполнитьЗначенияСвойств(
			НоваяОперация,
			ДанныеЭтапа,
			"Распоряжение,
			|Подразделение,
			|ВАрхиве");
			
		ЗаполнитьРеквизитыТехнологическогоПроцесса(НоваяОперация, ДанныеОперации, ДанныеЭтапа);
		
		РассчитатьПоказателиВыполненияОперации(НоваяОперация, ДанныеОперации);
		
		РазвернутьРезультатВыполненияОперации(ОчередьОпераций, НоваяОперация, ДанныеОперации, ДанныеЭтапа.ИспользоватьСменныеЗадания);
		
	КонецЦикла;
	
	Возврат ОчередьОпераций;
	
КонецФункции

// Список операций по данным этапа.
// 
// Параметры:
//  ДанныеЭтапа - см. Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций
//  ПрименитьОграниченияТехпроцесса - Булево - определяет необходимость сокращения маршрута настройками деления партии
// 
// Возвращаемое значение:
//  см. Справочники.ТехнологическиеОперации.ТаблицаОперацииКонструктор 
Функция СписокОперацийПоДаннымЭтапа(ДанныеЭтапа, ПрименитьОграниченияТехпроцесса = Ложь) Экспорт
	
	Трудозатраты = Новый ТаблицаЗначений();
	
	НормированиеИРасценкаРаботПоТарифнымСеткам = Ложь;
	//++ Локализация
	НормированиеИРасценкаРаботПоТарифнымСеткам = ПолучитьФункциональнуюОпцию("НормированиеИРасценкаРаботПоТарифнымСеткам");
	//-- Локализация
	
	ПереченьДанных = "Операции";
	
	ПереченьДанных = ПереченьДанных + ?(НормированиеИРасценкаРаботПоТарифнымСеткам, ",Трудозатраты", "");
	
	Если ДанныеЭтапа.Свойство("Операции") Тогда
		
		Операции = ДанныеЭтапа.Операции; //ТаблицаЗначений
	
	ИначеЕсли ТипЗнч(ДанныеЭтапа.ТехнологическийПроцесс) = Тип("СправочникСсылка.РесурсныеСпецификации") Тогда
		
		ДанныеПоНоменклатуре = Справочники.РесурсныеСпецификации.ДанныеПоНоменклатуреРасширенный();
		ДанныеПоНоменклатуре.Номенклатура   = ДанныеЭтапа.Номенклатура;
		ДанныеПоНоменклатуре.Характеристика = ДанныеЭтапа.Характеристика;
		ДанныеПоНоменклатуре.Спецификация   = ДанныеЭтапа.ТехнологическийПроцесс;
		ДанныеПоНоменклатуре.Количество     = ДанныеЭтапа.КоличествоУпаковокПлан * ДанныеЭтапа.КоэффициентУпаковки;
		
		ПараметрыВыборки = Справочники.РесурсныеСпецификации.ПараметрыВыборкиДанных(ПереченьДанных);
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.Использовать = Истина;
		ПараметрыВыборки.ПереопределениеНастройкиПартииВыпуска.ВыпускПроизвольнымиПорциями = Истина;
		
		ДанныеСпецификации = Справочники.РесурсныеСпецификации.ДанныеСпецификацииПоНоменклатуре(
			ДанныеПоНоменклатуре,
			ПараметрыВыборки,
			Новый Структура("Этап", ДанныеЭтапа.ЭтапСпецификации));
		
		Операции = ДанныеСпецификации.Операции; // ТаблицаЗначений
		
		Если НормированиеИРасценкаРаботПоТарифнымСеткам Тогда
			Трудозатраты = ДанныеСпецификации.Трудозатраты;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЭтапа.ТехнологическийПроцесс) = Тип("СправочникСсылка.ТехнологическиеПроцессы") Тогда
		
		ДанныеПоНоменклатуре = Справочники.ТехнологическиеПроцессы.ДанныеПоНоменклатуре();
		ДанныеПоНоменклатуре.Номенклатура   = ДанныеЭтапа.Номенклатура;
		ДанныеПоНоменклатуре.Характеристика = ДанныеЭтапа.Характеристика;
		ДанныеПоНоменклатуре.Количество = ДанныеЭтапа.КоличествоУпаковокПлан * ДанныеЭтапа.КоэффициентУпаковки;
		
		ПараметрыВыборки = Справочники.ТехнологическиеПроцессы.ПараметрыВыборкиДанных(ПереченьДанных);
		
		ДанныеТехпроцесса = Справочники.ТехнологическиеПроцессы.ДанныеТехнологическогоПроцессаПоНоменклатуре(
			ДанныеЭтапа.ТехнологическийПроцесс,
			ДанныеЭтапа.КоэффициентТехнологическогоПроцесса,
			ДанныеПоНоменклатуре,
			ПараметрыВыборки);
			
		Операции = ДанныеТехпроцесса.Операции; // ТаблицаЗначений
		
		Если НормированиеИРасценкаРаботПоТарифнымСеткам Тогда
			Трудозатраты = ДанныеТехпроцесса.Трудозатраты;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ДанныеЭтапа.ТехнологическийПроцесс) = Тип("СправочникСсылка.МаршрутныеКарты") Тогда
	
		ДанныеМаршрутнойКарты = Справочники.МаршрутныеКарты.ДанныеМаршрутнойКарты(
			ДанныеЭтапа.ТехнологическийПроцесс,
			ДанныеЭтапа.КоэффициентТехнологическогоПроцесса,
			Неопределено,
			Неопределено,
			ПереченьДанных);
			
		Операции = ДанныеМаршрутнойКарты.Операции; // ТаблицаЗначений
		
		Если НормированиеИРасценкаРаботПоТарифнымСеткам Тогда
			Трудозатраты = ДанныеМаршрутнойКарты.Трудозатраты;
		КонецЕсли;
		
	Иначе
		
		Операции = Справочники.ТехнологическиеОперации.ТаблицаОперацииКонструктор();
		
	КонецЕсли;
	
	Если ПрименитьОграниченияТехпроцесса Тогда
		
		ПрименитьОграниченияТехпроцесса(Операции, ДанныеЭтапа);
		
	КонецЕсли;
	
	//++ Локализация
	Если ЗначениеЗаполнено(Трудозатраты) Тогда
		
		ДополнитьОперацииДолжностьюРазрядом(Операции, Трудозатраты, "Операция");
		
	КонецЕсли;
	//-- Локализация
	
	Возврат Операции;
	
КонецФункции

Процедура ЗаписатьОчередь(Этап, Очередь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	Набор.Отбор.Этап.Установить(Этап);
	
	Набор.Загрузить(Очередь);
	
	Набор.Записать(Истина);

КонецПроцедуры

Процедура ОчиститьОчередь(Этап)

	УстановитьПривилегированныйРежим(Истина);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	Набор.Отбор.Этап.Установить(Этап);
	
	Набор.Записать(Истина);
	
КонецПроцедуры

// Применить ограничения техпроцесса.
// 
// Параметры:
//  Операции - см. Справочники.ТехнологическиеОперации.ТаблицаОперацииКонструктор
//  ОграниченияТехпроцесса - см. РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ОграниченияТехпроцессаКонструктор
Процедура ПрименитьОграниченияТехпроцесса(Операции, ОграниченияТехпроцесса) Экспорт
	
	// Сокращение маршрута слева
	Если ОграниченияТехпроцесса.ИдентификаторПервойОперации <> 0 Тогда
		Операция = Операции.Найти(ОграниченияТехпроцесса.ИдентификаторПервойОперации, "ИдентификаторОперации");
		Если Операция <> Неопределено Тогда
			Операции.Индексы.Добавить("НомерСледующейОперации");
			СтруктураПоиска = Новый Структура("НомерСледующейОперации", Операция.НомерОперации);
			Очередь = Операции.НайтиСтроки(СтруктураПоиска);
			Пока Очередь.ВГраница() <> -1 Цикл
				СтруктураПоиска.НомерСледующейОперации = Очередь[0].НомерОперации;
				Для каждого Строка Из Операции.НайтиСтроки(СтруктураПоиска) Цикл
					Очередь.Добавить(Строка);
				КонецЦикла;
				Операции.Удалить(Очередь[0]);
				Очередь.Удалить(0);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Сокращение маршрута справа
	Если ОграниченияТехпроцесса.ИдентификаторПоследнейОперации <> 0 Тогда
		Операция = Операции.Найти(ОграниченияТехпроцесса.ИдентификаторПоследнейОперации, "ИдентификаторОперации");
		Если Операция <> Неопределено Тогда
			Операции.Индексы.Добавить("НомерОперации");
			СтруктураПоиска = Новый Структура("НомерОперации", Операция.НомерСледующейОперации);
			Очередь = Операции.НайтиСтроки(СтруктураПоиска);
			Пока Очередь.ВГраница() <> -1 Цикл
				СтруктураПоиска.НомерОперации = Очередь[0].НомерСледующейОперации;
				Для каждого Строка Из Операции.НайтиСтроки(СтруктураПоиска) Цикл
					Очередь.Добавить(Строка);
				КонецЦикла;
				Операции.Удалить(Очередь[0]);
				Очередь.Удалить(0);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РассчитатьПоОперации

Процедура ЗаблокироватьОчередьДляЗаписиПоКлючу(КлючОперации, Отказ = Ложь) Экспорт
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЭлементБлокировки.УстановитьЗначение("Этап", КлючОперации.Этап);
	ЭлементБлокировки.УстановитьЗначение("Операция", КлючОперации.Операция);
	ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", КлючОперации.ИдентификаторОперации);

	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %1';
										|en = 'Cannot lock the queue of routing operations: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ЗаблокироватьДанныеДляРасчетаОчередиПоОперации(Этап, ДанныеОперации, ПараметрыРасчета, Отказ)
	
	БлокировкаДанных = Новый БлокировкаДанных;
	
	Для каждого ТекущаяСтрока Из ДанныеОперации.Владелец() Цикл
		
		ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ПроизводственнаяОперация2_2");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
		
		ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
		
		ЭлементБлокировки.УстановитьЗначение("Операция", ТекущаяСтрока.Операция);
		ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", ТекущаяСтрока.ИдентификаторОперации);
		
		Если ТекущаяСтрока.НомерОперации = ДанныеОперации.НомерСледующейОперации
			ИЛИ (ПараметрыРасчета.РассчитыватьВсеПоследующиеОперации
					И ТекущаяСтрока.НомерОперации >= ДанныеОперации.НомерСледующейОперации)
			ИЛИ ТекущаяСтрока = ДанныеОперации Тогда
		
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОчередьПроизводственныхОпераций");
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			ЭлементБлокировки.УстановитьЗначение("Этап", Этап);
			
			ЭлементБлокировки.УстановитьЗначение("Операция", ТекущаяСтрока.Операция);
			ЭлементБлокировки.УстановитьЗначение("ИдентификаторОперации", ТекущаяСтрока.ИдентификаторОперации);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Попытка
		БлокировкаДанных.Заблокировать();
	Исключение
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Не удалось заблокировать очередь производственных операций: %1';
										|en = 'Cannot lock the queue of routing operations: %1'"),
			ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
			
	КонецПопытки;
	
КонецПроцедуры

Процедура ПересчитатьОчередьПоОперации(Этап, ДанныеОперации, ПараметрыРасчета, Отказ)
	
	ДополнитьОперацииРезультатомВыполнения(Этап, ДанныеОперации.Владелец());
	
	Операции = СледующиеОперации(ДанныеОперации, ПараметрыРасчета.РассчитыватьВсеПоследующиеОперации);
	Операции.Вставить(0, ДанныеОперации);
	
	ПараметрыПодразделения = ПроизводствоСервер.ПараметрыПроизводственногоПодразделения(ДанныеОперации.Подразделение);
	
	Набор = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
	
	Для Индекс = 0 По Операции.ВГраница() Цикл
		
		ТекущаяСтрока = Операции[Индекс];
		
		Набор.Очистить();
		Набор.Отбор.Этап.Установить(Этап);
		Набор.Отбор.Операция.Установить(ТекущаяСтрока.Операция);
		Набор.Отбор.ИдентификаторОперации.Установить(ТекущаяСтрока.ИдентификаторОперации);
		
		НоваяЗапись = Набор.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, ТекущаяСтрока);
		
		РассчитатьПоказателиВыполненияОперации(НоваяЗапись, ТекущаяСтрока);
		
		РазвернутьРезультатВыполненияОперации(Набор, НоваяЗапись, ТекущаяСтрока, ПараметрыПодразделения.ИспользоватьСменныеЗадания);
		
		Попытка
			Набор.Записать(Истина);
		Исключение
			СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ);
			Возврат;
		КонецПопытки;
		
	КонецЦикла;
	
	Если ПараметрыПодразделения.ИспользоватьСменныеЗадания
			И ПараметрыРасчета.РассчитыватьСменныеЗадания Тогда
		РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.РассчитатьОперации(Этап);
	КонецЕсли;
	
КонецПроцедуры

#Область Последовательность

Функция ОкружениеОперацииВОчереди(КлючОперации, ПараметрыРасчета)
	
	ДополнительныеПоля = Новый Массив;
	ДополнительныеПоля.Добавить("МАКСИМУМ(Очередь.Требуется)  КАК Требуется");
	ДополнительныеПоля.Добавить("СУММА(Очередь.Запланировано - Очередь.ТребуетПовторения) КАК КоличествоНаПартию");
	
	ПараметрыТекстаЗапроса = ПараметрыТекстаЗапросаОчередьОпераций();
	ПараметрыТекстаЗапроса.ПоказателиНормативные = Ложь;
	ПараметрыТекстаЗапроса.РеквизитыИсполнителяОперации = Истина;
	ПараметрыТекстаЗапроса.РеквизитыРасчетаВремениВыполненияОперации = Истина;
	ПараметрыТекстаЗапроса.ПоляВыборки = ДополнительныеПоля;
	ПараметрыТекстаЗапроса.ИмяВыходнойТаблицы = "ВТОчередь";
	ПараметрыТекстаЗапроса.Отборы.Добавить("Очередь.Этап = &Этап");
	
	ТекстЗапроса = ТекстЗапросаОчередьОпераций(ПараметрыТекстаЗапроса) + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Операции.*
	|ИЗ
	|	ВТОчередь КАК Операции
	|ГДЕ
	|	ИСТИНА В
	|		(ВЫБРАТЬ ПЕРВЫЕ 1
	|			ИСТИНА
	|		ИЗ
	|			ВТОчередь КАК ТекущаяОперация
	|				ПОЛНОЕ СОЕДИНЕНИЕ ВТОчередь КАК СледующиеОперации
	|				ПО СледующиеОперации.НомерОперации = ТекущаяОперация.НомерСледующейОперации
	|					ИЛИ &ВсеПоследующиеОперации
	|						И (СледующиеОперации.НомерОперации >= ТекущаяОперация.НомерСледующейОперации
	|							И ТекущаяОперация.НомерСледующейОперации <> 0)
	|				ПОЛНОЕ СОЕДИНЕНИЕ ВТОчередь КАК ПредыдущиеОперации
	|				ПО ПредыдущиеОперации.НомерСледующейОперации = ТекущаяОперация.НомерОперации
	|					ИЛИ ПредыдущиеОперации.НомерСледующейОперации = СледующиеОперации.НомерОперации
	|		ГДЕ
	|			ТекущаяОперация.ИдентификаторОперации = &ИдентификаторОперации
	|			И Операции.ИдентификаторОперации В (
	|				ТекущаяОперация.ИдентификаторОперации,
	|					СледующиеОперации.ИдентификаторОперации,
	|						ПредыдущиеОперации.ИдентификаторОперации))
	|УПОРЯДОЧИТЬ ПО
	|	Операции.НомерОперации,
	|	Операции.Наименование
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
		
	Запрос.УстановитьПараметр("Этап", КлючОперации.Этап);
	Запрос.УстановитьПараметр("ИдентификаторОперации", КлючОперации.ИдентификаторОперации);
	Запрос.УстановитьПараметр("ВсеПоследующиеОперации", ПараметрыРасчета.РассчитыватьВсеПоследующиеОперации);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Результат.Индексы.Добавить("НомерОперации");
	Результат.Индексы.Добавить("НомерСледующейОперации");
	Результат.Индексы.Добавить("Операция,ИдентификаторОперации");
	
	Возврат Результат;
	
КонецФункции

Функция ПредыдущиеОперации(Курсор, Все = Ложь)
	
	Операции = Курсор.Владелец();
	
	Если Все Тогда
		Результат = Новый Массив;
		Для Индекс = 0 По Операции.Количество()-1 Цикл
			Операция = Операции[Индекс];
			Если Операция.НомерСледующейОперации <= Курсор.НомерОперации Тогда
				Результат.Добавить(Операция);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = Операции.НайтиСтроки(Новый Структура("НомерСледующейОперации", Курсор.НомерОперации));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция СледующиеОперации(Курсор, Все = Ложь)
	
	Операции = Курсор.Владелец();
	
	Если Все Тогда
		Результат = Новый Массив;
		Для Индекс = 0 По Операции.Количество()-1 Цикл
			Операция = Операции[Индекс];
			Если Операция.НомерОперации >= Курсор.НомерСледующейОперации
					И Курсор.НомерСледующейОперации <> 0 Тогда
				Результат.Добавить(Операция);
			КонецЕсли;
		КонецЦикла;
	Иначе
		Результат = Операции.НайтиСтроки(Новый Структура("НомерОперации", Курсор.НомерСледующейОперации));
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура СвернутьГруппыИндивидуальныхОпераций(Операции, МассивСтрок)
	
	Если МассивСтрок.Количество() <= 1 Тогда
		Возврат;
	КонецЕсли;
	
	Результат = Новый Массив;
	
	ТаблицаГрупп = Операции.СкопироватьКолонки();
	СтруктураПоиска = Новый Структура("НомерОперации,НомерСледующейОперации");
	
	Для Индекс = 0 По МассивСтрок.Количество()-1 Цикл
		
		ДанныеОперации = МассивСтрок[Индекс];
		
		Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(ДанныеОперации) Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ДанныеОперации);
			НайденныеСтроки = ТаблицаГрупп.НайтиСтроки(СтруктураПоиска);
			Если НЕ ЗначениеЗаполнено(НайденныеСтроки) Тогда
				ДанныеГруппы = ТаблицаГрупп.Добавить();
				ЗаполнитьЗначенияСвойств(ДанныеГруппы, ДанныеОперации);
			Иначе
				ДанныеГруппы = НайденныеСтроки[0];
				ДанныеГруппы.Требуется = ДанныеГруппы.Требуется + ДанныеОперации.Требуется;
				ДанныеГруппы.КоличествоНаПартию = ДанныеГруппы.КоличествоНаПартию + ДанныеОперации.КоличествоНаПартию;
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеГруппы.Результат, ДанныеОперации.Результат);
				Для каждого КлючИЗначение Из ДанныеГруппы.РезультатИтоги Цикл
					ДанныеГруппы.РезультатИтоги[КлючИЗначение.Ключ] =
						ДанныеГруппы.РезультатИтоги[КлючИЗначение.Ключ] + ДанныеОперации.РезультатИтоги[КлючИЗначение.Ключ];
				КонецЦикла;
			КонецЕсли;
		Иначе
			Результат.Добавить(ДанныеОперации);
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТаблицаГрупп.Количество() > 0 Тогда
		
		Для каждого ДанныеГруппы Из ТаблицаГрупп Цикл
			Результат.Добавить(ДанныеГруппы);
		КонецЦикла;
		
	КонецЕсли;
	
	МассивСтрок = Результат;
	
КонецПроцедуры

Функция НайтиОперациюПоКлючу(Операции, КлючОперации)
	
	ОтборОперация = Новый Структура("Операция,ИдентификаторОперации");
	ЗаполнитьЗначенияСвойств(ОтборОперация, КлючОперации);
	
	НайденныеСтроки = Операции.НайтиСтроки(ОтборОперация);
	Если НайденныеСтроки.ВГраница() <> -1 Тогда
		Возврат НайденныеСтроки[0];
	КонецЕсли;
	Возврат Неопределено;

КонецФункции

// Добавляет и заполняет служебные реквизиты для списка операций.
// 
// Параметры:
//  Операции  - ТаблицаЗначений - список операций
Процедура ДобавитьЗаполнитьСлужебныеРеквизитыОпераций(Операции) Экспорт
	
	ЗаполнитьИдентификаторКонтрольнойОперации = Ложь;
	ЗаполнитьТребуется = Ложь;
	
	Если Операции.Колонки.Найти("ИдентификаторКонтрольнойОперации") = Неопределено Тогда
		ОписаниеТипов = Новый ОписаниеТипов("Число",
			Новый КвалификаторыЧисла(20, 0, ДопустимыйЗнак.Неотрицательный));
		Операции.Колонки.Добавить("ИдентификаторКонтрольнойОперации", ОписаниеТипов);
		ЗаполнитьИдентификаторКонтрольнойОперации = Истина;
	КонецЕсли;
	
	Если Операции.Колонки.Найти("Требуется") = Неопределено Тогда
		ОписаниеТипов = Новый ОписаниеТипов("Число",
			Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Неотрицательный));
		Операции.Колонки.Добавить("Требуется", ОписаниеТипов);
		ЗаполнитьТребуется = Истина;
	КонецЕсли;
	
	Если ЗаполнитьИдентификаторКонтрольнойОперации ИЛИ ЗаполнитьТребуется Тогда
		Для Индекс1 = 0 По Операции.Количество() - 1 Цикл
			СтрокаОперация = Операции[Индекс1];
			Если ЗаполнитьТребуется Тогда
				СтрокаОперация.Требуется = СтрокаОперация.КоличествоНаПартию;
			КонецЕсли;
			Если ЗаполнитьИдентификаторКонтрольнойОперации Тогда
				Если СтрокаОперация.Контроль = Перечисления.ВариантыКонтроляТехнологическихОпераций.НеТребуется Тогда
					Продолжить;
				ИначеЕсли СтрокаОперация.Контроль = Перечисления.ВариантыКонтроляТехнологическихОпераций.Требуется Тогда
					СтрокаОперация.ИдентификаторКонтрольнойОперации = СтрокаОперация.ИдентификаторОперации;
				ИначеЕсли СтрокаОперация.Контроль = Перечисления.ВариантыКонтроляТехнологическихОпераций.ТребуетсяНаПоследующих Тогда
					Для Индекс2 = Индекс1+1 По Операции.Количество()-1 Цикл
						СтрокаКонтрольнаяОперация = Операции[Индекс2];
						Если СтрокаКонтрольнаяОперация.Контроль = Перечисления.ВариантыКонтроляТехнологическихОпераций.Требуется Тогда
							СтрокаОперация.ИдентификаторКонтрольнойОперации = СтрокаКонтрольнаяОперация.ИдентификаторОперации;
							Прервать;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ТекстЗапросаОчередьОпераций

// Параметры текста запроса очереди операций.
// 
// Возвращаемое значение:
//  Структура - из:
// * Отборы - Массив из Строка - отборы
// * ПоляВыборки - Массив из Строка
// * ПоляСортировки - Массив из Строка
// * ПоляИндексирования - Массив из Строка
// * ПоказателиНормативные - Булево - добавить в запрос нормативные показатели операции
// * ПоказателиВыполнения - Булево - добавить в запрос накопленные показатели выполнения
// * РеквизитыИсполнителяОперации - Булево - необходимость получить данные по ВРЦ / РЦ
// * РеквизитыРасчетаВремениВыполненияОперации - Булево - необходимость рассчитать временные показатели по НСИ
// * РеквизитыВремениВыполненияОперации - Булево - необходимость добавить в выборку временные показатели из очереди
// * РеквизитыОперацииРасширенные - Булево - расширенный набор реквизитов тех. операции
// * ПроизводственныеОперации - Число - (0 - никакие, 1 - только индивидуальные, 2 - все)
// * ИмяВыходнойТаблицы - Строка - имя временной таблицы
Функция ПараметрыТекстаЗапросаОчередьОпераций() Экспорт
	
	Результат = Новый Структура;
	
	Результат.Вставить("ПоказателиНормативные",                     Истина);
	Результат.Вставить("ПоказателиВыполнения",                      Ложь);
	Результат.Вставить("ПроизводственныеОперации",                  1);
	
	Результат.Вставить("РеквизитыИсполнителяОперации",              Ложь);
	Результат.Вставить("РеквизитыРасчетаВремениВыполненияОперации", Ложь);
	Результат.Вставить("РеквизитыВремениВыполненияОперации",        Ложь);
	Результат.Вставить("РеквизитыОперацииРасширенные",              Ложь);
	
	Результат.Вставить("Отборы",             Новый Массив);
	Результат.Вставить("ПоляВыборки",        Новый Массив);
	Результат.Вставить("ПоляСортировки",     Новый Массив);
	Результат.Вставить("ПоляИндексирования", Новый Массив);
	
	Результат.Вставить("РазделительПакета",  Ложь);
	Результат.Вставить("ИмяВыходнойТаблицы", "");
	
	Возврат Результат;
	
КонецФункции

// Текст запроса очередь операций.
// 
// Параметры:
//  ПараметрыЗапроса - см. ПараметрыТекстаЗапросаОчередьОпераций
// 
// Возвращаемое значение:
//  Строка - Текст запроса
Функция ТекстЗапросаОчередьОпераций(ПараметрыЗапроса) Экспорт
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	Очередь.Подразделение                       КАК Подразделение,
	|	Очередь.Распоряжение                        КАК Распоряжение,
	|	Очередь.Этап                                КАК Этап,
	|	Очередь.Операция                            КАК Операция,
	|	Очередь.ИдентификаторОперации               КАК ИдентификаторОперации,
	|	Очередь.ИдентификаторКонтрольнойОперации    КАК ИдентификаторКонтрольнойОперации,
	|	Очередь.НомерОперации                       КАК НомерОперации,
	|	Очередь.НомерСледующейОперации              КАК НомерСледующейОперации,
	|	Очередь.ТехнологическийПроцесс              КАК ТехнологическийПроцесс,
	|	Очередь.КоэффициентТехнологическогоПроцесса КАК КоэффициентТехнологическогоПроцесса,
	|	Очередь.ВАрхиве                             КАК ВАрхиве,
	//++ Локализация
	|	Очередь.Должность                           КАК Должность,
	|	Очередь.Разряд                              КАК Разряд,
	//-- Локализация
	|	ЕСТЬNULL(Очередь.Операция.Количество, 1)    КАК Количество,
	|	&ТекстДополнительныеПоляВыборки
	|ИЗ
	|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
	|";
	
	ТекстДопПолей = Новый Массив;
	
	Если ПараметрыЗапроса.ПроизводственныеОперации = 1 Тогда
		ТекстДопПолей.Добавить("ВЫБОР
		|	КОГДА Очередь.Операция = ЗНАЧЕНИЕ(Справочник.ТехнологическиеОперации.ПустаяСсылка)
		|		ТОГДА Очередь.ПроизводственнаяОперация
		|	ИНАЧЕ ЗНАЧЕНИЕ(Документ.ПроизводственнаяОперация2_2.ПустаяСсылка)
		|КОНЕЦ КАК ПроизводственнаяОперация");
	ИначеЕсли ПараметрыЗапроса.ПроизводственныеОперации = 2 Тогда
		ТекстДопПолей.Добавить("Очередь.ПроизводственнаяОперация КАК ПроизводственнаяОперация");
	КонецЕсли;
	
	Если ПараметрыЗапроса.РеквизитыИсполнителяОперации Тогда
		ТекстДопПолей.Добавить(Справочники.ТехнологическиеОперации.ТекстЗапросаРеквизитыИсполнителяОперации(
			ТекстЗапросаИсточникДанныхОперации()));
	КонецЕсли;
	
	Если ПараметрыЗапроса.РеквизитыРасчетаВремениВыполненияОперации Тогда
		ТекстДопПолей.Добавить(Справочники.ТехнологическиеОперации.ТекстЗапросаРеквизитыРасчетаВремениВыполненияОперации(
			ТекстЗапросаИсточникДанныхОперации()));
	КонецЕсли;
	
	РеквизитыОперации = Новый Массив;
	РеквизитыОперации.Добавить("Наименование");
	Если ПараметрыЗапроса.РеквизитыОперацииРасширенные Тогда
		РеквизитыОперации.Добавить("КТО");
		РеквизитыОперации.Добавить("ВидОперации");
		РеквизитыОперации.Добавить("Участок");
		РеквизитыОперации.Добавить("ВариантНаладки");
		РеквизитыОперации.Добавить("Контроль");
		РеквизитыОперации.Добавить("МожноПовторить");
		РеквизитыОперации.Добавить("МожноПропустить");
		РеквизитыОперации.Добавить("Загрузка");
		РеквизитыОперации.Добавить("ПередаточнаяПартия");
		РеквизитыОперации.Добавить("Непрерывная");
		РеквизитыОперации.Добавить("ЕдиницаИзмерения");
	КонецЕсли;
	Для каждого Реквизит Из РеквизитыОперации Цикл
		ТекстДопПолей.Добавить(СтрШаблон("%1.%2 КАК %2", ТекстЗапросаИсточникДанныхОперации(), Реквизит));
	КонецЦикла;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТекстДополнительныеПоляВыборки", СтрСоединить(ТекстДопПолей, ","+Символы.ПС));
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(ТекстЗапроса);
	
	Пакет = СхемаЗапроса.ПакетЗапросов[0];
	
	Для каждого ЭлементОтбора Из ПараметрыЗапроса.Отборы Цикл
		Пакет.Операторы[0].Отбор.Добавить(ЭлементОтбора);
	КонецЦикла;
	
	Если ПараметрыЗапроса.ПоказателиНормативные Тогда
		ТекстПоказателиНорматива = "
		|	МАКСИМУМ(Очередь.Требуется)  КАК Требуется,
		|	МАКСИМУМ(Очередь.Требуется)  КАК КоличествоНаПартию
		|";
		ДобавитьПоляВПакетЗапроса(Пакет, ТекстПоказателиНорматива);
	КонецЕсли;
	
	Если ПараметрыЗапроса.ПоказателиВыполнения Тогда
		ТекстПоказателиВыполнения = "
		|	СУММА(Очередь.Запланировано)          КАК Запланировано,
		|	СУММА(Очередь.Создано)                КАК Создано,
		|	СУММА(Очередь.Выполняется)            КАК Выполняется,
		|	СУММА(Очередь.Выполнено)              КАК Выполнено,
		|	СУММА(Очередь.ТребуетПовторения)      КАК ТребуетПовторения,
		|	СУММА(Очередь.Пропущено)              КАК Пропущено,
		|	СУММА(Очередь.НаКонтроле)             КАК НаКонтроле,
		|	СУММА(Очередь.НаДоработке)            КАК НаДоработке,
		|	СУММА(Очередь.Брак)                   КАК Брак,
		|	СУММА(Очередь.ОжиданиеПредшествующих) КАК ОжиданиеПредшествующих,
		|	СУММА(Очередь.НачатыПредшествующие)   КАК НачатыПредшествующие,
		|	СУММА(Очередь.МожноВыполнять)         КАК МожноВыполнять
		|";
		ДобавитьПоляВПакетЗапроса(Пакет, ТекстПоказателиВыполнения);
	КонецЕсли;
	
	Если ПараметрыЗапроса.РеквизитыВремениВыполненияОперации
		И НЕ ПараметрыЗапроса.РеквизитыРасчетаВремениВыполненияОперации Тогда
		РеквизитыВремениВыполнения = "
		|	СУММА(Очередь.ВремяОбщее) КАК ВремяОбщее,
		|	Очередь.ВремяШтучное      КАК ВремяШтучное,
		|	Очередь.ВремяПЗ           КАК ВремяПЗ,
		|	Очередь.ВремяЕдИзм        КАК ВремяЕдИзм
		|";
		ДобавитьПоляВПакетЗапроса(Пакет, РеквизитыВремениВыполнения);
	КонецЕсли;
	
	Для каждого Поле Из ПараметрыЗапроса.ПоляВыборки Цикл
		ДобавитьПоляВПакетЗапроса(Пакет, Поле);
	КонецЦикла;
	
	Для каждого Поле Из ПараметрыЗапроса.ПоляСортировки Цикл
		Пакет.Порядок.Добавить(Поле);
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.ИмяВыходнойТаблицы) Тогда
		Пакет.ТаблицаДляПомещения = ПараметрыЗапроса.ИмяВыходнойТаблицы;
		Для каждого Поле Из ПараметрыЗапроса.ПоляИндексирования Цикл
			Пакет.Индекс.Добавить(Поле);
		КонецЦикла;
	КонецЕсли;
	
	ТекстЗапроса = СхемаЗапроса.ПолучитьТекстЗапроса();
	Если ПараметрыЗапроса.РазделительПакета Тогда
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначения.РазделительПакетаЗапросов();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ДобавитьПоляВПакетЗапроса(Пакет, Поле)
	
	Если УправлениеДаннымиОбИзделиях.ЭтоВыражение(Поле) Тогда
		МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Поле, ","+Символы.ПС);
		Для каждого ВложенноеПоле Из МассивПолей Цикл
			ПозицияОпределенияПсевдонима = СтрНайти(ВложенноеПоле, " КАК", НаправлениеПоиска.СКонца);
			Если ПозицияОпределенияПсевдонима > 0 Тогда
				ТекстВыражения  = Сред(ВложенноеПоле, 1, ПозицияОпределенияПсевдонима);
				ТекстПсевдонима = Сред(ВложенноеПоле, ПозицияОпределенияПсевдонима+4);
				ДобавитьПолеВПакетЗапроса(Пакет, СокрЛП(ТекстВыражения), СокрЛП(ТекстПсевдонима));
			ИначеЕсли НЕ УправлениеДаннымиОбИзделиях.ЭтоВыражение(ВложенноеПоле) Тогда
				ДобавитьПолеВПакетЗапроса(Пакет, ВложенноеПоле, ВложенноеПоле);
			Иначе
				ДобавитьПолеВПакетЗапроса(Пакет, ВложенноеПоле);
			КонецЕсли;
		КонецЦикла;
	Иначе
		ДобавитьПолеВПакетЗапроса(Пакет, Поле, Поле);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПолеВПакетЗапроса(Пакет, Выражение, Псевдоним = "")
	
	Пакет.Операторы[0].ВыбираемыеПоля.Добавить(Выражение);
	Если НЕ ПустаяСтрока(Псевдоним) Тогда
		Пакет.Колонки[Пакет.Колонки.Количество()-1].Псевдоним = Псевдоним;
	КонецЕсли;
	
КонецПроцедуры

// Текст запроса источник данных операции.
// 
// Параметры:
//  ИмяТаблицы - Строка - Имя таблицы
// 
// Возвращаемое значение:
//  Строка
Функция ТекстЗапросаИсточникДанныхОперации(ИмяТаблицы = "") Экспорт
	
	Результат = "(ВЫБОР
	|	КОГДА Очередь.Операция = ЗНАЧЕНИЕ(Справочник.ТехнологическиеОперации.ПустаяСсылка)
	|		ТОГДА Очередь.ПроизводственнаяОперация
	|	ИНАЧЕ Очередь.Операция КОНЕЦ)";
	
	Если НЕ ПустаяСтрока(ИмяТаблицы) Тогда
		Результат = СтрЗаменить(Результат, "Очередь", ИмяТаблицы);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РасчетПоказателей

Процедура ДополнитьОперацииРезультатомВыполнения(Этап, Операции)
	
	СтруктураОтбора = Новый Структура("ИдентификаторОперации");
	
	РезультатВыполненияОпераций = РезультатВыполненияОпераций(Этап, Операции.ВыгрузитьКолонку("ИдентификаторОперации"));
	РезультатВыполненияОпераций.Индексы.Добавить("ИдентификаторОперации");
	
	Операции.Колонки.Добавить("Результат");
	Операции.Колонки.Добавить("РезультатИтоги");
	
	Для каждого ДанныеОперации Из Операции Цикл
		
		СтруктураОтбора.ИдентификаторОперации = ДанныеОперации.ИдентификаторОперации;
		
		Результат      = РезультатВыполненияОпераций.НайтиСтроки(СтруктураОтбора);
		РезультатИтоги = ПоказателиХодаВыполненияКонструктор();
		
		Для каждого НайденнаяСтрока Из Результат Цикл
			Для каждого КлючИЗначение Из РезультатИтоги Цикл
				РезультатИтоги[КлючИЗначение.Ключ] = РезультатИтоги[КлючИЗначение.Ключ] + НайденнаяСтрока[КлючИЗначение.Ключ];
			КонецЦикла;
		КонецЦикла;
		
		ДанныеОперации.Результат      = Результат;
		ДанныеОперации.РезультатИтоги = РезультатИтоги;

	КонецЦикла;
	
КонецПроцедуры

Функция РезультатВыполненияОпераций(Этап, ИдентификаторыОпераций)
	
	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВложенныйЗапрос.ИдентификаторОперации              КАК ИдентификаторОперации,
		|	ВложенныйЗапрос.ПроизводственнаяОперация           КАК ПроизводственнаяОперация,
		|	ВложенныйЗапрос.ПроизводственнаяОперацияСтатус     КАК ПроизводственнаяОперацияСтатус,
		|	ВложенныйЗапрос.СменноеЗадание                     КАК СменноеЗадание,
		|	ВложенныйЗапрос.РабочийЦентр                       КАК РабочийЦентр,
		|	ВложенныйЗапрос.Исполнитель                        КАК Исполнитель,
		|	ВложенныйЗапрос.ЭтоДокумент                        КАК ЭтоДокумент,
		|	СУММА(ВложенныйЗапрос.Создано)                     КАК Создано,
		|	СУММА(ВложенныйЗапрос.Выполняется)                 КАК Выполняется,
		|	СУММА(ВложенныйЗапрос.Выполнено)                   КАК Выполнено,
		|	СУММА(ВложенныйЗапрос.ТребуетПовторения)           КАК ТребуетПовторения,
		|	СУММА(ВложенныйЗапрос.Пропущено)                   КАК Пропущено,
		|	СУММА(ВложенныйЗапрос.НаКонтроле)                  КАК НаКонтроле,
		|	СУММА(ВложенныйЗапрос.НаДоработке)                 КАК НаДоработке,
		|	СУММА(ВложенныйЗапрос.Брак)                        КАК Брак,
		|	СУММА(ВложенныйЗапрос.Формируется)                 КАК Формируется,
		|	СУММА(ВложенныйЗапрос.Отменено)                    КАК Отменено,
		|	СУММА(ВложенныйЗапрос.ОтмененоИнд)                 КАК ОтмененоИнд,
		|	СУММА(ВложенныйЗапрос.ВремяСозданныхОпераций)      КАК ВремяСозданныхОпераций
		|ИЗ
		|	(
		|	ВЫБРАТЬ
		|		ПроизводственнаяОперация.ИдентификаторОперации КАК ИдентификаторОперации,
		|		ПроизводственнаяОперация.Ссылка                КАК ПроизводственнаяОперация,
		|		ПроизводственнаяОперация.Статус                КАК ПроизводственнаяОперацияСтатус,
		|		ПроизводственнаяОперация.СменноеЗадание        КАК СменноеЗадание,
		|		ПроизводственнаяОперация.РабочийЦентр          КАК РабочийЦентр,
		|		ПроизводственнаяОперация.Исполнитель           КАК Исполнитель,
		|		ИСТИНА                                         КАК ЭтоДокумент,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусФормируется
		|				ТОГДА ПроизводственнаяОперация.Количество
		|						- ПроизводственнаяОперация.КоличествоОтменено
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Формируется,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусСоздана, &СтатусВыполняется, &СтатусПропущена)
		|				ТОГДА ПроизводственнаяОперация.Количество
		|						- ПроизводственнаяОперация.КоличествоОтменено
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполнена
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|						+ ПроизводственнаяОперация.КоличествоНаКонтроле
		|						+ ПроизводственнаяОперация.КоличествоНаДоработке
		|						+ ПроизводственнаяОперация.КоличествоБрак
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Создано,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполняется
		|				ТОГДА ПроизводственнаяОперация.Количество
		|						- ПроизводственнаяОперация.КоличествоНаКонтроле
		|						- ПроизводственнаяОперация.КоличествоНаДоработке
		|						- ПроизводственнаяОперация.КоличествоБрак
		|						- ПроизводственнаяОперация.КоличествоФакт
		|						- ПроизводственнаяОперация.КоличествоОтменено
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Выполняется,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусВыполняется, &СтатусВыполнена)
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Выполнено,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусВыполнена
		|					И ПроизводственнаяОперация.ТребуетПовторения
		|				ТОГДА ПроизводственнаяОперация.КоличествоФакт
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК ТребуетПовторения,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусПропущена
		|				ТОГДА ПроизводственнаяОперация.Количество
		|						- ПроизводственнаяОперация.КоличествоОтменено
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Пропущено,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусВыполняется, &СтатусВыполнена)
		|				ТОГДА ПроизводственнаяОперация.КоличествоНаКонтроле
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НаКонтроле,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусВыполняется, &СтатусВыполнена)
		|				ТОГДА ПроизводственнаяОперация.КоличествоНаДоработке
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК НаДоработке,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус В (&СтатусВыполняется, &СтатусВыполнена)
		|				ТОГДА ПроизводственнаяОперация.КоличествоБрак
		|			ИНАЧЕ 0
		|		КОНЕЦ КАК Брак,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Статус = &СтатусНеВыполнена
		|				ТОГДА ПроизводственнаяОперация.Количество
		|			ИНАЧЕ ПроизводственнаяОперация.КоличествоОтменено
		|		КОНЕЦ КАК Отменено,
		|		ВЫБОР
		|			КОГДА ПроизводственнаяОперация.Операция = ЗНАЧЕНИЕ(Справочник.ТехнологическиеОперации.ПустаяСсылка)
		|				ТОГДА ВЫБОР
		|						КОГДА ПроизводственнаяОперация.Статус = &СтатусНеВыполнена
		|							ТОГДА ПроизводственнаяОперация.Количество
		|						ИНАЧЕ ПроизводственнаяОперация.КоличествоОтменено
		|				КОНЕЦ
		|		ИНАЧЕ 0
		|		КОНЕЦ КАК ОтмененоИнд,
		|		ВЫБОР ПроизводственнаяОперация.ВремяВыполненияЕдИзм
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Минута)
		|				ТОГДА 60
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Час)
		|				ТОГДА 3600
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.День)
		|				ТОГДА 86400
		|			КОГДА ЗНАЧЕНИЕ(Перечисление.ЕдиницыИзмеренияВремени.Сутки)
		|				ТОГДА 86400
		|			ИНАЧЕ 1
		|		КОНЕЦ * ПроизводственнаяОперация.ВремяВыполнения КАК ВремяСозданныхОпераций
		|	ИЗ
		|		Документ.ПроизводственнаяОперация2_2 КАК ПроизводственнаяОперация
		|	ГДЕ
		|		ПроизводственнаяОперация.Этап = &Этап
		|		И ПроизводственнаяОперация.ИдентификаторОперации В (&ИдентификаторыОпераций)
		|		И ПроизводственнаяОперация.Проведен
		|
		|	ОБЪЕДИНИТЬ ВСЕ
		|
		|	ВЫБРАТЬ
		|		Техпроцесс.ИдентификаторПоследнейОперации                      КАК ИдентификаторОперации,
		|		ЗНАЧЕНИЕ(Документ.ПроизводственнаяОперация2_2.ПустаяСсылка)    КАК ПроизводственнаяОперация,
		|		НЕОПРЕДЕЛЕНО                                                   КАК ПроизводственнаяОперацияСтатус,
		|		НЕОПРЕДЕЛЕНО                                                   КАК СменноеЗадание,
		|		НЕОПРЕДЕЛЕНО                                                   КАК РабочийЦентр,
		|		НЕОПРЕДЕЛЕНО                                                   КАК Исполнитель,
		|		ЛОЖЬ                                                           КАК ЭтоДокумент,
		|		0                                                              КАК Формируется,
		|		Техпроцесс.ОтмененоПоследняяОперация                           КАК Создано,
		|		0                                                              КАК Выполняется,
		|		0                                                              КАК Выполнено,
		|		0                                                              КАК ТребуетПовторения,
		|		Техпроцесс.ОтмененоПоследняяОперация                           КАК Пропущено,
		|		0                                                              КАК НаКонтроле,
		|		0                                                              КАК НаДоработке,
		|		0                                                              КАК Брак,
		|		0                                                              КАК Отменено,
		|		0                                                              КАК ОтмененоИнд,
		|		0                                                              КАК ВремяСозданныхОпераций
		|	ИЗ
		|		РегистрСведений.ТехнологическиеПроцессыЭтаповПроизводства КАК Техпроцесс
		|
		|	ГДЕ
		|		Техпроцесс.Этап = &Этап
		|		И Техпроцесс.ИдентификаторПоследнейОперации В (&ИдентификаторыОпераций)
		|		И Техпроцесс.ОтмененоПоследняяОперация <> 0
		|
		|	) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.ИдентификаторОперации,
		|	ВложенныйЗапрос.ПроизводственнаяОперация,
		|	ВложенныйЗапрос.ПроизводственнаяОперацияСтатус,
		|	ВложенныйЗапрос.СменноеЗадание,
		|	ВложенныйЗапрос.РабочийЦентр,
		|	ВложенныйЗапрос.Исполнитель,
		|	ВложенныйЗапрос.ЭтоДокумент
		|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Этап", Этап);
	Запрос.УстановитьПараметр("ИдентификаторыОпераций", ИдентификаторыОпераций);
	Запрос.УстановитьПараметр("СтатусФормируется", Перечисления.СтатусыПроизводственныхОпераций.Формируется);
	Запрос.УстановитьПараметр("СтатусСоздана", Перечисления.СтатусыПроизводственныхОпераций.Создана);
	Запрос.УстановитьПараметр("СтатусВыполняется", Перечисления.СтатусыПроизводственныхОпераций.Выполняется);
	Запрос.УстановитьПараметр("СтатусВыполнена", Перечисления.СтатусыПроизводственныхОпераций.Выполнена);
	Запрос.УстановитьПараметр("СтатусПропущена", Перечисления.СтатусыПроизводственныхОпераций.Пропущена);
	Запрос.УстановитьПараметр("СтатусНеВыполнена", Перечисления.СтатусыПроизводственныхОпераций.НеВыполнена);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура РассчитатьПоказателиВыполненияОперации(Операция, ДанныеОперации)
	
	Операция.ОжиданиеПредшествующих = 0;
	Операция.НачатыПредшествующие   = 0;
	Операция.МожноВыполнять         = 0;
	
	ОперацияИтоги = ДанныеОперации.РезультатИтоги;
	
	ПредыдущиеОперации = ПредыдущиеОперации(ДанныеОперации);
	
	СвернутьГруппыИндивидуальныхОпераций(ДанныеОперации.Владелец(), ПредыдущиеОперации);
	
	Если ПредыдущиеОперации.ВГраница() <> -1 Тогда
		
		Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(Операция) Тогда
			Операция.Запланировано = Операция.Требуется;
		Иначе
			Операция.Запланировано = КоэффициентПоправкиНаБрак(ПредыдущиеОперации) * Операция.Требуется;
			ДанныеОперации.КоличествоНаПартию = Операция.Запланировано;
		КонецЕсли;
		
		МожноВыполнятьРасчетное       = -1;
		НачатыПредшествующиеРасчетное = -1;
		
		Если Операция.Запланировано > 0 Тогда
		
			Для каждого ПредыдущаяОперация Из ПредыдущиеОперации Цикл
				
				Коэффициент = КоэффициентПриведенияНормативов(Операция, ПредыдущаяОперация);
				
				ПредыдущаяОперацияИтоги = ПредыдущаяОперация.РезультатИтоги;
				
				РасчетноеКоличество = Коэффициент * (ПредыдущаяОперацияИтоги.Выполнено - ПредыдущаяОперацияИтоги.ТребуетПовторения
													+ ПредыдущаяОперацияИтоги.Пропущено + ПредыдущаяОперацияИтоги.ОтмененоИнд);
				
				МожноВыполнятьРасчетное = ?(
					МожноВыполнятьРасчетное > -1, Мин(МожноВыполнятьРасчетное, РасчетноеКоличество), РасчетноеКоличество);
				
			КонецЦикла;
			
			Для каждого ПредыдущаяОперация Из ПредыдущиеОперации Цикл
				
				Коэффициент = КоэффициентПриведенияНормативов(Операция, ПредыдущаяОперация);
				
				ПредыдущаяОперацияИтоги = ПредыдущаяОперация.РезультатИтоги;
				
				РасчетноеКоличество = Коэффициент * (ПредыдущаяОперацияИтоги.Выполняется + ПредыдущаяОперацияИтоги.НаКонтроле + ПредыдущаяОперацияИтоги.НаДоработке)
									+ Макс(Коэффициент * (ПредыдущаяОперацияИтоги.Выполнено - ПредыдущаяОперацияИтоги.ТребуетПовторения
													+ ПредыдущаяОперацияИтоги.Пропущено + ПредыдущаяОперацияИтоги.ОтмененоИнд)
													- МожноВыполнятьРасчетное,
											0);
				
				НачатыПредшествующиеРасчетное = ?(
					НачатыПредшествующиеРасчетное > -1, Мин(НачатыПредшествующиеРасчетное, РасчетноеКоличество),  РасчетноеКоличество);
				
			КонецЦикла;
		
		КонецЕсли;
		
		НачатыПредшествующиеРасчетное = Макс(НачатыПредшествующиеРасчетное, 0);
		МожноВыполнятьРасчетное       = Макс(МожноВыполнятьРасчетное, 0);

		ОжидаетВыполнения = Макс(Операция.Запланировано + ОперацияИтоги.ТребуетПовторения, ОперацияИтоги.Создано)
			- (ОперацияИтоги.Выполняется
					+ ОперацияИтоги.Выполнено
					+ ОперацияИтоги.НаКонтроле
					+ ОперацияИтоги.НаДоработке
					+ ОперацияИтоги.Брак
					+ ОперацияИтоги.Пропущено
					+ ОперацияИтоги.ОтмененоИнд);
		
		Если МожноВыполнятьРасчетное < Макс(Операция.Запланировано + ОперацияИтоги.ТребуетПовторения, ОперацияИтоги.Создано) Тогда
			Операция.МожноВыполнять = МожноВыполнятьРасчетное
				- (ОперацияИтоги.Выполняется
						+ ОперацияИтоги.Выполнено
						+ ОперацияИтоги.НаКонтроле
						+ ОперацияИтоги.НаДоработке
						+ ОперацияИтоги.Брак
						+ ОперацияИтоги.Пропущено
						+ ОперацияИтоги.ОтмененоИнд
						- ОперацияИтоги.ТребуетПовторения);
		Иначе
			Операция.МожноВыполнять = ОжидаетВыполнения;
		КонецЕсли;
		
		Операция.НачатыПредшествующие = Мин(НачатыПредшествующиеРасчетное, ОжидаетВыполнения);
		Операция.ОжиданиеПредшествующих = ОжидаетВыполнения - Операция.МожноВыполнять - Операция.НачатыПредшествующие;
		
	Иначе
		
		Операция.Запланировано  = Операция.Требуется;
		Операция.МожноВыполнять = Макс(Операция.Запланировано + ОперацияИтоги.ТребуетПовторения, ОперацияИтоги.Создано)
				- (ОперацияИтоги.Выполняется
						+ ОперацияИтоги.Выполнено
						+ ОперацияИтоги.НаКонтроле
						+ ОперацияИтоги.НаДоработке
						+ ОперацияИтоги.Брак
						+ ОперацияИтоги.Пропущено
						+ ОперацияИтоги.ОтмененоИнд);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РазвернутьРезультатВыполненияОперации(Операции, Операция, ДанныеОперации, ИспользоватьСменныеЗадания) 
	
	ГруппаОпераций = ОчередьОперацийКонструктор();
	ГруппаОпераций.Колонки.Добавить("СтрокаРезультата");
	
	ИндивидуальнаяОперация = РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(Операция);
	
	Для каждого СтрокаРезультата Из ДанныеОперации.Результат Цикл
		
		НоваяСтрока = ГруппаОпераций.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Операция);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаРезультата);
		
		НоваяСтрока.СтрокаРезультата = СтрокаРезультата;
		НоваяСтрока.Запланировано    = Макс(СтрокаРезультата.Формируется, СтрокаРезультата.Создано);
		
		Если НЕ ИндивидуальнаяОперация Тогда
			
			ОжидаетВыполнения = НоваяСтрока.Запланировано
								- (СтрокаРезультата.Выполняется
										+ СтрокаРезультата.Выполнено
										+ СтрокаРезультата.НаКонтроле
										+ СтрокаРезультата.НаДоработке
										+ СтрокаРезультата.Брак
										+ СтрокаРезультата.Пропущено
										+ СтрокаРезультата.ОтмененоИнд);
			
			НоваяСтрока.МожноВыполнять         = Мин(Операция.МожноВыполнять, ОжидаетВыполнения);
			НоваяСтрока.НачатыПредшествующие   = Мин(ОжидаетВыполнения - НоваяСтрока.МожноВыполнять, Операция.НачатыПредшествующие);
			НоваяСтрока.ОжиданиеПредшествующих = Мин(ОжидаетВыполнения - НоваяСтрока.МожноВыполнять - НоваяСтрока.НачатыПредшествующие, Операция.ОжиданиеПредшествующих);
			
			Операция.Запланировано          = Операция.Запланировано          - НоваяСтрока.Запланировано + СтрокаРезультата.ТребуетПовторения;
			Операция.МожноВыполнять         = Операция.МожноВыполнять         - НоваяСтрока.МожноВыполнять;
			Операция.ОжиданиеПредшествующих = Операция.ОжиданиеПредшествующих - НоваяСтрока.ОжиданиеПредшествующих;
			Операция.НачатыПредшествующие   = Операция.НачатыПредшествующие   - НоваяСтрока.НачатыПредшествующие;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если НЕ ИндивидуальнаяОперация Тогда
		
		Если Операция.МожноВыполнять > 0 Тогда
			НоваяСтрока = ГруппаОпераций.Вставить(0);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Операция);
			НоваяСтрока.Запланировано          = Операция.МожноВыполнять;
			НоваяСтрока.МожноВыполнять         = Операция.МожноВыполнять;
			НоваяСтрока.ОжиданиеПредшествующих = 0;
			НоваяСтрока.НачатыПредшествующие   = 0;
			Операция.Запланировано = Операция.Запланировано - НоваяСтрока.Запланировано;
		КонецЕсли;
		
		Если Операция.Запланировано > 0 ИЛИ ГруппаОпераций.Количество() = 0 Тогда
			НоваяСтрока = ГруппаОпераций.Вставить(0);
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Операция);
			НоваяСтрока.Запланировано          = Операция.Запланировано;
			НоваяСтрока.МожноВыполнять         = 0;
			НоваяСтрока.ОжиданиеПредшествующих = Операция.ОжиданиеПредшествующих;
			НоваяСтрока.НачатыПредшествующие   = Операция.НачатыПредшествующие;
		КонецЕсли;
	
	КонецЕсли;
	
	Операции.Удалить(Операция);
	
	Если ГруппаОпераций.Количество() > 0 Тогда
		
		ГруппаОпераций[0].ОсновнаяЗапись = Истина;
		
		Для каждого ОперацияГруппы Из ГруппаОпераций Цикл
			
			СтрокаРезультата = ОперацияГруппы.СтрокаРезультата;
			
			ОперацияГруппы.Состояние = СостояниеОперации(ОперацияГруппы, СтрокаРезультата, ИспользоватьСменныеЗадания);
			ОперацияГруппы.Порядок   = ПорядокОперации(ОперацияГруппы,  СтрокаРезультата);
			
			РассчитатьВременныеПоказателиОперации(ОперацияГруппы, ДанныеОперации, СтрокаРезультата);
			
			ЗаполнитьЗначенияСвойств(Операции.Добавить(), ОперацияГруппы);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РассчитатьВременныеПоказателиОперации(Операция, ДанныеОперации, РезультатВыполнения = Неопределено)
	
	СтруктураРасчета = ОперативныйУчетПроизводстваКлиентСервер.СтруктураРасчетаОбщегоВремениВыполнения();
	ЗаполнитьЗначенияСвойств(СтруктураРасчета, ДанныеОперации);
	
	Коэффициент = Операция.Запланировано / ДанныеОперации.Количество;
	
	ОперативныйУчетПроизводстваКлиентСервер.РассчитатьОбщееВремяВыполненияОперации(СтруктураРасчета, Коэффициент);
	
	Операция.ВремяЕдИзм = СтруктураРасчета.ВремяВыполненияЕдИзм;
	
	Если Коэффициент = 0 Тогда
		Операция.ВремяОбщее = 0;
	ИначеЕсли РезультатВыполнения <> Неопределено И РезультатВыполнения.Создано > 0 Тогда
		Операция.ВремяОбщее = ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВЕдиницеИзмерения(
				РезультатВыполнения.ВремяСозданныхОпераций, Операция.ВремяЕдИзм);
	Иначе
		Операция.ВремяОбщее = СтруктураРасчета.ВремяВыполнения;
	КонецЕсли;
	
	Если ДанныеОперации.ВремяШтучное > 0 Тогда
		Если ДанныеОперации.ВремяШтучноеЕдИзм = Операция.ВремяЕдИзм Тогда
			Операция.ВремяШтучное = ДанныеОперации.ВремяШтучное;
		Иначе
			Операция.ВремяШтучное = ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВЕдиницеИзмерения(
				ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВСекундах(
					ДанныеОперации.ВремяШтучное,
					ДанныеОперации.ВремяШтучноеЕдИзм),
				Операция.ВремяЕдИзм);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОперации.ВремяПЗ > 0 Тогда
		Если ДанныеОперации.ВремяПЗЕдИзм = Операция.ВремяЕдИзм Тогда
			Операция.ВремяПЗ = ДанныеОперации.ВремяПЗ;
		Иначе
			Операция.ВремяПЗ = ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВЕдиницеИзмерения(
				ПланированиеПроизводстваКлиентСервер.ПолучитьВремяВСекундах(
					ДанныеОперации.ВремяПЗ,
					ДанныеОперации.ВремяПЗЕдИзм),
				Операция.ВремяЕдИзм);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СостояниеОперации(Операция, РезультатВыполнения, ИспользоватьСменныеЗадания)
	
	Результат = Перечисления.СостоянияВыполненияОпераций.ПустаяСсылка();
	
	ЕстьНазначенныйДокументОперация = ЗначениеЗаполнено(РезультатВыполнения)
		И ЗначениеЗаполнено(РезультатВыполнения.ПроизводственнаяОперация)
		И НЕ РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Формируется;
	
	Если (ЕстьНазначенныйДокументОперация
				И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.НеВыполнена)
			ИЛИ Операция.Запланировано = 0 Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.Отменена;
	ИначеЕсли (ЕстьНазначенныйДокументОперация
				И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Пропущена)
			ИЛИ Операция.Пропущено >= Операция.Запланировано Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.Пропущена;
	ИначеЕсли ЕстьНазначенныйДокументОперация
			И Операция.НаДоработке > 0
			И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Выполняется Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.НаДоработке;
	ИначеЕсли ЕстьНазначенныйДокументОперация
			И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Выполняется Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.Выполняется;
	ИначеЕсли ЕстьНазначенныйДокументОперация
			И Операция.НаКонтроле > 0
			И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.НаКонтроле;
	ИначеЕсли ЕстьНазначенныйДокументОперация
			И РезультатВыполнения.ПроизводственнаяОперацияСтатус = Перечисления.СтатусыПроизводственныхОпераций.Выполнена Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.Выполнена;
	ИначеЕсли НЕ ЕстьНазначенныйДокументОперация
			И ИспользоватьСменныеЗадания
			И Операция.МожноВыполнять > 0 Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.КНазначению;
	ИначеЕсли Операция.МожноВыполнять > 0
			И (Операция.ОжиданиеПредшествующих + Операция.НачатыПредшествующие) = 0 Тогда
		Результат = Перечисления.СостоянияВыполненияОпераций.КВыполнению;
	Иначе
		Результат = Перечисления.СостоянияВыполненияОпераций.ОжиданиеПредшествующих;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПорядокОперации(Операция, РезультатВыполнения = Неопределено)
	
	Результат = 1000 * Перечисления.СостоянияВыполненияОпераций.ПорядокВСписке(Операция.Состояние);
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьРеквизитыТехнологическогоПроцесса(Операция, ДанныеОперации, ДанныеЭтапа)
	
	Если ЗначениеЗаполнено(ДанныеОперации.ТехнологическийПроцесс) Тогда
		Операция.ТехнологическийПроцесс = ДанныеОперации.ТехнологическийПроцесс;
		Операция.КоэффициентТехнологическогоПроцесса = ДанныеОперации.КоэффициентТехнологическогоПроцесса;
	Иначе
		Операция.ТехнологическийПроцесс = ДанныеЭтапа.ТехнологическийПроцесс;
		Операция.КоэффициентТехнологическогоПроцесса = ДанныеЭтапа.КоэффициентТехнологическогоПроцесса;
	КонецЕсли;
	
КонецПроцедуры

Функция КоэффициентПоправкиНаБрак(ПредыдущиеОперации)
	
	Результат = 1;
	
	Для каждого ПредыдущаяОперация Из ПредыдущиеОперации Цикл
		Если ПредыдущаяОперация.Требуется > 0 Тогда
			Результат = Мин(Результат,
				(ПредыдущаяОперация.КоличествоНаПартию
					- ПредыдущаяОперация.РезультатИтоги.Брак) / ПредыдущаяОперация.Требуется);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Функция КоэффициентПриведенияНормативов(Операция, ПредыдущаяОперация)
	
	Если РедакторПроизводственногоПроцессаКлиентСервер.ЭтоОперацияИндивидуальная(Операция) Тогда
		Результат = Операция.Требуется/ПредыдущаяОперация.КоличествоНаПартию;
	Иначе
		Результат = Операция.Требуется/ПредыдущаяОперация.Требуется;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область Прочее

Функция ВыполнитьКонтрольПередРасчетомОчереди(ДанныеЭтапа, УдалениеПроведения = Ложь, Отказ = Ложь)
	
	ЕстьОшибка = Ложь;
	
	Если ДанныеЭтапа.ТребуетсяРассчитать И НЕ УдалениеПроведения Тогда
		
		Если ДанныеЭтапа.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Альтернативный
			ИЛИ (ДанныеЭтапа.ТипТехнологическогоПроцесса = Перечисления.ТипыТехнологическихПроцессовЭтапа.Стандартный
					И НЕ ПолучитьФункциональнуюОпцию("ХранитьОперацииВРесурсныхСпецификациях")) Тогда
		
			Запрос = Новый Запрос(
			"ВЫБРАТЬ
			|	СУММА(Очередь.Создано) > 0 КАК ОшибкаВОперациях
			|ИЗ
			|	РегистрСведений.ОчередьПроизводственныхОпераций КАК Очередь
			|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК НазначенныеОперации
			|	ПО Очередь.Подразделение = НазначенныеОперации.Подразделение
			|		И Очередь.Этап = НазначенныеОперации.Этап
			|		И Очередь.Операция = НазначенныеОперации.Операция
			|		И Очередь.ИдентификаторОперации = НазначенныеОперации.ИдентификаторОперации
			|		И НазначенныеОперации.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)
			|ГДЕ
			|	Очередь.Этап = &Этап
			|
			|ИМЕЮЩИЕ
			|	(СУММА(Очередь.Создано) > 0 
			|		ИЛИ МАКСИМУМ(НазначенныеОперации.Этап ЕСТЬ НЕ NULL) = ИСТИНА)
			|	И (МАКСИМУМ(ВЫБОР
			|				КОГДА Очередь.ТехнологическийПроцесс <> &ТехнологическийПроцесс
			|					ТОГДА ИСТИНА
			|				ИНАЧЕ ЛОЖЬ
			|			КОНЕЦ) = ИСТИНА
			|		ИЛИ МАКСИМУМ(ВЫБОР
			|				КОГДА Очередь.КоэффициентТехнологическогоПроцесса <> &КоэффициентТехнологическогоПроцесса
			|					ТОГДА ИСТИНА
			|				ИНАЧЕ ЛОЖЬ
			|			КОНЕЦ) = ИСТИНА)");
			Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Ссылка);
			Запрос.УстановитьПараметр("ТехнологическийПроцесс", ДанныеЭтапа.ТехнологическийПроцесс);
			Запрос.УстановитьПараметр("КоэффициентТехнологическогоПроцесса", ДанныеЭтапа.КоэффициентТехнологическогоПроцесса);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				
				ЕстьОшибка = Истина;
				
				Если Выборка.ОшибкаВОперациях Тогда
					ТекстСообщения = НСтр("ru = 'Запрещено изменять технологический процесс и коэффициент после создания производственных операций.';
											|en = 'Cannot edit the technological process or the factor after creating routing operations.'");
				Иначе
					ТекстСообщения = НСтр("ru = 'Запрещено изменять технологический процесс и коэффициент после создания сменных заданий.';
											|en = 'Cannot edit the technological process or the factor after creating shift tasks.'");
				КонецЕсли;
				
			КонецЕсли;
		
		КонецЕсли;
		
	Иначе
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ИСТИНА КАК ОшибкаВОперациях
		|ИЗ
		|	Документ.ПроизводственнаяОперация2_2 КАК Операции
		|ГДЕ
		|	Операции.Этап = &Этап
		|	И (Операции.НаОснованииНСИ
		|		ИЛИ Операции.Статус <> ЗНАЧЕНИЕ(Перечисление.СтатусыПроизводственныхОпераций.Формируется))
		|	И НЕ Операции.ПометкаУдаления
		|	И НЕ &ЗавершениеЭтапа
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЛОЖЬ
		|ИЗ
		|	РегистрСведений.ОперацииКСозданиюСменныхЗаданий КАК НазначенныеОперации
		|ГДЕ
		|	НазначенныеОперации.Этап = &Этап
		|	И НазначенныеОперации.СменноеЗадание <> ЗНАЧЕНИЕ(Документ.СменноеЗадание.ПустаяСсылка)");
		Запрос.УстановитьПараметр("Этап", ДанныеЭтапа.Ссылка);
		Запрос.УстановитьПараметр("ЗавершениеЭтапа", ДанныеЭтапа.Статус = Перечисления.СтатусыЭтаповПроизводства2_2.Завершен И НЕ УдалениеПроведения);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЕстьОшибка = Истина;
			
			Если Выборка.ОшибкаВОперациях Тогда
				ТекстСообщения = НСтр("ru = 'По этапу созданы операции.';
										|en = 'Operations are created for the stage.'");
			Иначе
				ТекстСообщения = НСтр("ru = 'По этапу созданы сменные задания.';
										|en = 'Shift tasks are created by the stage.'");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьОшибка Тогда
		
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Ошибка расчета очереди производственных операций. %1';
										|en = 'An error occurred while calculating routing operation queue. %1'"), ТекстСообщения);
		
		ОбщегоНазначения.СообщитьПользователю(
				ТекстСообщения,
				,
				,
				,
				Отказ);
		
		Возврат Ложь;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура СообщитьПользователюОбОшибкеРасчетаОчереди(Отказ)
	
	ТекстСообщения = НСтр("ru = 'Не удалось рассчитать очередь производственных операций.';
							|en = 'Cannot calculate the routing operation queue.'");
	
	ОбщегоНазначения.СообщитьПользователю(
			ТекстСообщения,
			,
			,
			, 
			Отказ);

КонецПроцедуры

//++ Локализация
Процедура ДополнитьОперацииДолжностьюРазрядом(Операции, Трудозатраты, ПолеОтбора) Экспорт
	
	Запрос = Новый Запрос("
		|ВЫБРАТЬ
		|	&ПолеОтбора,
		|	Трудозатраты.ВидРабот КАК ВидРабот,
		|	Трудозатраты.НомерСтроки КАК НомерСтроки
		|ПОМЕСТИТЬ ВТТрудозатраты
		|ИЗ
		|	&Трудозатраты КАК Трудозатраты
		|
		|;
		|
		|ВЫБРАТЬ
		|	&ПолеОтбора,
		|	МИНИМУМ(Трудозатраты.НомерСтроки) КАК НомерСтроки
		|ПОМЕСТИТЬ ВТОтбор
		|ИЗ
		|	ВТТрудозатраты КАК Трудозатраты
		|
		|СГРУППИРОВАТЬ ПО
		|	&ПолеОтбора
		|
		|;
		|
		|ВЫБРАТЬ
		|	&ПолеОтбора,
		|	ВидыРабот.Должность КАК Должность,
		|	ВидыРабот.КвалификационныйРазряд КАК Разряд
		|ИЗ
		|	ВТТрудозатраты КАК Трудозатраты
		|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРаботСотрудников КАК ВидыРабот
		|	ПО Трудозатраты.ВидРабот = ВидыРабот.Ссылка
		|ГДЕ
		|	(&ПолеОтбора, НомерСтроки) В (ВЫБРАТЬ
		|									&ТПолеОтбора,
		|									Т.НомерСтроки
		|								ИЗ
		|									ВТОтбор КАК Т)");
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПолеОтбора", "Трудозатраты." + ПолеОтбора);
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТПолеОтбора", "Т." + ПолеОтбора);
		
		Запрос.УстановитьПараметр("Трудозатраты", Трудозатраты);
		Результат = Запрос.Выполнить().Выбрать();
		
		Отбор = Новый Структура(ПолеОтбора);
		
		Пока Результат.Следующий() Цикл
			
			Если НЕ ЗначениеЗаполнено(Результат.Должность) Тогда
				Продолжить;
			КонецЕсли;
			
			Отбор[ПолеОтбора] = Результат[ПолеОтбора];
			ОперацииКЗаполнению = Операции.НайтиСтроки(Отбор);
			
			Для Каждого Операция Из ОперацииКЗаполнению Цикл
				Операция.Должность = Результат.Должность;
				Операция.Разряд = Результат.Разряд;
			КонецЦикла;
			
		КонецЦикла;
		
КонецПроцедуры
//-- Локализация

#КонецОбласти

#Область ОбновлениеИнформационнойБазы

// см. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

	Обработчик = Обработчики.Добавить();
	Обработчик.Процедура = "РегистрыСведений.ОчередьПроизводственныхОпераций.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	Обработчик.Версия = "2.5.25.12";
	Обработчик.РежимВыполнения = "Отложенно";
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("833b3f93-6c82-4ec3-805d-fc4f9f10a11a");
	Обработчик.Многопоточный = Истина;
	Обработчик.ПроцедураЗаполненияДанныхОбновления = "РегистрыСведений.ОчередьПроизводственныхОпераций.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию";
	Обработчик.ПроцедураПроверки = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Порядок = Перечисления.ПорядокОбработчиковОбновления.Обычный;
	Обработчик.Комментарий = НСтр("ru = 'Конвертация очереди в новый формат хранения, удаление архивных записей.';
									|en = 'Convert the queue to a new storage format, delete archived records.'");
	
	Читаемые = Новый Массив;
	Читаемые.Добавить(Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.РегистрыСведений.УдалитьОчередьПроизводственныхОпераций.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ЭтапПроизводства2_2.ПолноеИмя());
	Читаемые.Добавить(Метаданные.Документы.ПроизводственнаяОперация2_2.ПолноеИмя());
	Обработчик.ЧитаемыеОбъекты = СтрСоединить(Читаемые, ",");
	
	Изменяемые = Новый Массив;
	Изменяемые.Добавить(Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций.ПолноеИмя());
	Обработчик.ИзменяемыеОбъекты = СтрСоединить(Изменяемые, ",");
	
	Блокируемые = Новый Массив;
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ОперацииКСозданиюСменныхЗаданий.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.РегистрыСведений.ПооперационноеРасписание2_2.ПолноеИмя());
	Блокируемые.Добавить(Метаданные.Документы.ЭтапПроизводства2_2.ПолноеИмя());
	Обработчик.БлокируемыеОбъекты = СтрСоединить(Блокируемые, ",");
	
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();

	НоваяСтрока = Обработчик.ПриоритетыВыполнения.Добавить();
	НоваяСтрока.Процедура = "РегистрыСведений.ТехнологическиеПроцессыЭтаповПроизводства.ОбработатьДанныеДляПереходаНаНовуюВерсию";
	НоваяСтрока.Порядок = "После";
	
КонецПроцедуры

// Параметры:
// 	Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
//
Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ОчередьПроизводственныхОпераций";
	
	ПараметрыВыборки = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаРегистров = ПолноеИмяРегистра;
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Этап.Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Этап");
	ПараметрыВыборки.СпособВыборки = ОбновлениеИнформационнойБазы.СпособВыборкиИзмеренияНезависимогоРегистраСведений();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.Вставить("ЭтоНезависимыйРегистрСведений", Истина);
	ДополнительныеПараметры.Вставить("ПолноеИмяРегистра", ПолноеИмяРегистра);
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ОчередьСтарая.Этап          КАК Этап,
	|	ОчередьСтарая.Подразделение КАК Подразделение
	|ИЗ
	|	РегистрСведений.УдалитьОчередьПроизводственныхОпераций КАК ОчередьСтарая
	|ГДЕ
	|	ОчередьСтарая.Этап.Статус В (
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.КВыполнению),
	|		ЗНАЧЕНИЕ(Перечисление.СтатусыЭтаповПроизводства2_2.Начат))
	|	И НЕ ИСТИНА В
	|				(ВЫБРАТЬ ПЕРВЫЕ 1
	|					ИСТИНА
	|				ИЗ
	|					РегистрСведений.ОчередьПроизводственныхОпераций КАК ОчередьНовая
	|				ГДЕ
	|					ОчередьНовая.Этап = ОчередьСтарая.Этап)
	|	И НЕ ОчередьСтарая.ВАрхиве
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ОчередьНовая.Этап           КАК Этап,
	|	ОчередьНовая.Подразделение  КАК Подразделение
	|ИЗ
	|	РегистрСведений.ОчередьПроизводственныхОпераций КАК ОчередьНовая
	|ГДЕ
	|	ОчередьНовая.Операция <> ЗНАЧЕНИЕ(Справочник.ТехнологическиеОперации.ПустаяСсылка)
	|	И ОчередьНовая.ВремяШтучное <> 0
	|	И ОчередьНовая.ВремяПЗ = 0
	|	И ВЫБОР
	|		КОГДА ОчередьНовая.Операция.РабочийЦентр ССЫЛКА Справочник.ВидыРабочихЦентров
	|			ТОГДА ВЫБОР
	|					КОГДА ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.ВидыРабочихЦентров).ПараллельнаяЗагрузка
	|							И ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.ВидыРабочихЦентров).ВариантЗагрузки = ЗНАЧЕНИЕ(Перечисление.ВариантыЗагрузкиРабочихЦентров.Синхронный)
	|						ТОГДА ВЫБОР
	|								КОГДА ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.ВидыРабочихЦентров).ИспользуютсяВариантыНаладки
	|									ТОГДА ОчередьНовая.Операция.ВариантНаладки.ЕдиницаИзмерения
	|								ИНАЧЕ ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.ВидыРабочихЦентров).ЕдиницаИзмерения
	|							КОНЕЦ
	|					ИНАЧЕ ОчередьНовая.Операция.ВремяШтучноеЕдИзм
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.РабочиеЦентры).ВидРабочегоЦентра.ПараллельнаяЗагрузка
	|						И ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.РабочиеЦентры).ВидРабочегоЦентра.ВариантЗагрузки = ЗНАЧЕНИЕ(Перечисление.ВариантыЗагрузкиРабочихЦентров.Синхронный)
	|					ТОГДА ВЫБОР
	|							КОГДА ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.РабочиеЦентры).ВидРабочегоЦентра.ИспользуютсяВариантыНаладки
	|								ТОГДА ОчередьНовая.Операция.ВариантНаладки.ЕдиницаИзмерения
	|							ИНАЧЕ ВЫРАЗИТЬ(ОчередьНовая.Операция.РабочийЦентр КАК Справочник.РабочиеЦентры).ВидРабочегоЦентра.ЕдиницаИзмерения
	|						КОНЕЦ
	|				ИНАЧЕ ОчередьНовая.Операция.ВремяШтучноеЕдИзм
	|			КОНЕЦ
	|	КОНЕЦ <> ОчередьНовая.ВремяЕдИзм
	|	И ОчередьНовая.Операция.ВремяПЗЕдИзм = ОчередьНовая.ВремяЕдИзм
	|	И НЕ ОчередьНовая.ВАрхиве
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(
		Параметры,
		Запрос.Выполнить().Выгрузить(),
		ДополнительныеПараметры);
	
КонецПроцедуры

// Обработчик обновления.
// 
// Параметры:
//  Параметры - Структура - параметры.
//
Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ОчередьПроизводственныхОпераций";
	
	ОбновляемыеДанные = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(Параметры.Очередь, ПолноеИмяРегистра);
		Возврат;
	КонецЕсли;
	
	Этапы = Новый Массив;
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		Этапы.Добавить(Выборка.Этап);
	КонецЦикла;
	
	ДанныеЭтапов = Документы.ЭтапПроизводства2_2.ДанныеДляРасчетаОчередиОпераций(Этапы);
	
	Для каждого Выборка Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			ДанныеЭтапа = ДанныеЭтапов[Выборка.Этап]; // Структура
			
			БлокировкаДанных = БлокировкаДанныхДляРасчетаОчереди(Выборка.Этап);
			ЭлементБлокировки = БлокировкаДанных.Добавить("Документ.ЭтапПроизводства2_2");
			ЭлементБлокировки.УстановитьЗначение("Распоряжение", ДанныеЭтапа.Распоряжение);
			ЭлементБлокировки.УстановитьЗначение("ПартияПроизводства", ДанныеЭтапа.ПартияПроизводства);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Этап);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			БлокировкаДанных.Заблокировать();
			
			НаборЗаписей = РегистрыСведений.ОчередьПроизводственныхОпераций.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.Подразделение.Установить(Выборка.Подразделение);
			НаборЗаписей.Отбор.Этап.Установить(Выборка.Этап);
			
			Очередь = ОчередьОпераций(Выборка.Этап, ДанныеЭтапа);
			НаборЗаписей.Загрузить(Очередь);
			
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
					НСтр("ru = 'Не удалось записать данные в регистр ""Очередь производственных операций"" по этапу ""%1"", по причине: %2';
						|en = 'Cannot save data to the ""Queue of routing operations"" register for the ""%1"" stage due to: %2'"),
					Выборка.Этап,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.РегистрыСведений.ОчередьПроизводственныхОпераций,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Не ОбновлениеИнформационнойБазы.ЕстьДанныеДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли
