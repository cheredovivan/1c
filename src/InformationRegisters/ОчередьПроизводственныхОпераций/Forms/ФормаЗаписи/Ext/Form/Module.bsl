#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Операция.Видимость                 = ЗначениеЗаполнено(Запись.Операция);
	Элементы.ПроизводственнаяОперация.Видимость = ЗначениеЗаполнено(Запись.ПроизводственнаяОперация);
	Элементы.ТехнологическийПроцесс.Видимость   = ЗначениеЗаполнено(Запись.ТехнологическийПроцесс);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Операция.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА Операция.РабочийЦентр.Участок
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка)
	|	КОНЕЦ                     КАК Участок,
	|	Операция.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	Операция.Описание         КАК Описание
	|ИЗ
	|	Справочник.ТехнологическиеОперации КАК Операция
	|ГДЕ
	|	Операция.Ссылка = &Операция
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ДокОперация.РабочийЦентр ССЫЛКА Справочник.РабочиеЦентры
	|			ТОГДА ДокОперация.РабочийЦентр.Участок
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПроизводственныеУчастки.ПустаяСсылка)
	|	КОНЕЦ                        КАК Участок,
	|	ДокОперация.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ДокОперация.Комментарий      КАК Описание
	|ИЗ
	|	Документ.ПроизводственнаяОперация2_2 КАК ДокОперация
	|ГДЕ
	|	ДокОперация.Ссылка = &ПроизводственнаяОперация
	|");
	
	Запрос.УстановитьПараметр("Операция", Запись.Операция);
	Запрос.УстановитьПараметр("ПроизводственнаяОперация", Запись.ПроизводственнаяОперация);
	
	Результат = Запрос.Выполнить().Выбрать();
	
	Если Результат.Следующий() Тогда
		
		Участок = Результат.Участок;
		Элементы.Участок.Видимость = ЗначениеЗаполнено(Участок);
		
		ЕдиницаИзмеренияПредставление = УправлениеПроизводствомКлиентСервер.ПредставлениеЕдиницыИзмеренияОперации(
			Результат.ЕдиницаИзмерения,
			,
			Истина);
		
		Описание = Результат.Описание;
		Элементы.Описание.Видимость = ЗначениеЗаполнено(Описание);
		
	КонецЕсли;
	
	ОжидаетНазначения = 0;
	
	Если Запись.Запланировано + Запись.ТребуетПовторения > Запись.Создано Тогда
		ОжидаетНазначения = Запись.Запланировано + Запись.ТребуетПовторения - Запись.Создано;
	КонецЕсли;
	
	Файлы = Новый Массив;
	
	РаботаСФайлами.ЗаполнитьПрисоединенныеФайлыКОбъекту(Запись.Операция, Файлы);
	
	Элементы.ПрисоединенныеФайлы.Видимость = Файлы.Количество()>0;
	Элементы.ПрисоединенныеФайлы.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
												НСтр("ru = 'Дополнительные материалы (%1)';
													|en = 'Additional materials (%1)'"),
												Файлы.Количество());
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти