///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция ЗаписатьФайлы(
		ДокументЗаказа,
		ПрисоединенныеФайлы,
		Вложения) Экспорт
	
	НаборЗаписей = РегистрыСведений.ФайлыОнлайнЗаказов.СоздатьНаборЗаписей();
	НаборЗаписей.Заполнить(Неопределено);
	НаборЗаписей.Отбор.ДокументЗаказа.Установить(ДокументЗаказа);
	
	Для Каждого ПрисоединенныйФайл Из ПрисоединенныеФайлы Цикл
		
		ДанныеФайла = Вложения.Получить(Строка(ПрисоединенныйФайл.УникальныйИдентификатор()));
		
		Если ДанныеФайла = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, ДанныеФайла);
		Запись.ДокументЗаказа     = ДокументЗаказа;
		Запись.ПрисоединенныйФайл = ПрисоединенныйФайл;
		
	КонецЦикла;
	
	НаборЗаписей.Записать();
	
КонецФункции

Функция ФайлыЗаказа(ДокументЗаказа) Экспорт
	
	ПрисоединенныеФайлы = Новый Соответствие;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФайлыОнлайнЗаказов.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
	|	ФайлыОнлайнЗаказов.ДатаМодификации КАК ДатаМодификации,
	|	ФайлыОнлайнЗаказов.Размер КАК Размер,
	|	ФайлыОнлайнЗаказов.ХешСумма КАК ХешСумма
	|ИЗ
	|	РегистрСведений.ФайлыОнлайнЗаказов КАК ФайлыОнлайнЗаказов
	|ГДЕ
	|	ФайлыОнлайнЗаказов.ДокументЗаказа = &ДокументЗаказа";
	
	Запрос.УстановитьПараметр("ДокументЗаказа", ДокументЗаказа);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ДанныеПрисоединенногоФайла = Новый Структура;
		ДанныеПрисоединенногоФайла.Вставить("ДатаМодификации",    Выборка.ДатаМодификации);
		ДанныеПрисоединенногоФайла.Вставить("Размер",             Выборка.Размер);
		ДанныеПрисоединенногоФайла.Вставить("ХешСумма",           Выборка.ХешСумма);
		
		ПрисоединенныеФайлы.Вставить(Выборка.ПрисоединенныйФайл, ДанныеПрисоединенногоФайла);
		
	КонецЦикла;
	
	Возврат ПрисоединенныеФайлы;
	
КонецФункции

#КонецОбласти

#КонецЕсли