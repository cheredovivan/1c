#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПредельноеКоличество =
		ИнтеграцияС1СДокументооборотБазоваяФункциональность.ПредельноеКоличествоПопытокОтправкиСообщенияОбмена();
	
	Элементы.ИнформацияОписание.Заголовок = СтрШаблон(
		НСтр("ru = 'Максимальное количество попыток отправки сообщения равно %1. При достижении этого количества система не будет предпринимать новых попыток отправки. В такой ситуации нужно устранить ошибку отправки, затем либо удалить сообщение из очереди, либо сбросить счетчик попыток отправки.';
			|en = 'The maximum number of attempts to send a message is %1. When this number is reached, the system will not attempt sending again. In such situation, fix the sending error, then either delete the message from the queue, or reset the sending attempt counter.'"),
		ПредельноеКоличество);
	
	ЭлементОформления = Список.УсловноеОформление.Элементы.Добавить();
	
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.Идентификатор.Имя);
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.ДатаСоздания.Имя);
	ПолеОформления = ЭлементОформления.Поля.Элементы.Добавить();
	ПолеОформления.Поле = Новый ПолеКомпоновкиДанных(Элементы.КоличествоПопытокОтправки.Имя);
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КоличествоПопытокОтправки");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
	ЭлементОтбора.ПравоеЗначение = ПредельноеКоличество;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", WebЦвета.Красный);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоИдентификаторуПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(ПоИдентификатору) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Идентификатор",
			Неопределено,
			ВидСравненияКомпоновкиДанных.Равно,,
			Ложь);
		Возврат;
	КонецЕсли;
	
	Попытка
		Идентификатор = Новый УникальныйИдентификатор(ПоИдентификатору);
	Исключение
		ПоказатьПредупреждение(, НСтр("ru = 'Идентификатор неверен';
										|en = 'ID is incorrect'"));
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
			"Идентификатор",
			Неопределено,
			ВидСравненияКомпоновкиДанных.Равно,,
			Ложь);
		Возврат;
	КонецПопытки;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список,
		"Идентификатор",
		Идентификатор,
		ВидСравненияКомпоновкиДанных.Равно,,
		Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура СброситьСчетчикПопытокОтправки(Знач Команда)
	
	КоличествоСообщений = Элементы.Список.ВыделенныеСтроки.Количество();
	
	Если КоличествоСообщений = 0 Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбрано ни одного сообщения.';
										|en = 'No message is selected.'"));
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = СтрШаблон(
		НСтр("ru = 'Сбросить счетчик попыток отправки для %1?';
			|en = 'Reset the sending attempt counter for %1?'"),
		СклоненияСтрокиПоЧислу(НСтр("ru = 'сообщение';
									|en = 'Message'"), КоличествоСообщений, "ЧС=Количественное", "ПД=Родительный"));
	Ответ = Ждать ИнтеграцияС1СДокументооборотБазоваяФункциональностьКлиент.ПоказатьВопросДаНетАсинх(ТекстВопроса);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СброситьСчетчикПопытокОтправкиНаСервере(Элементы.Список.ВыделенныеСтроки);
		Элементы.Список.Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции


// Сбросить счетчик попыток отправки на сервере.
// 
// Параметры:
//  СписокВыбранныхСообщений - Массив из РегистрСведенийКлючЗаписи.ОчередьСообщенийВ1СДокументооборот - Список выбранных сообщений:
//     * Период - Дата - период отправления.
//     * Регистратор - Неопределено - регистратор сообщения.
//     * МоментВремени - Число - момент времени.
//     * Идентификатор - УникальныйИдентификатор - идентификатор сообщения.
//
&НаСервере
Процедура СброситьСчетчикПопытокОтправкиНаСервере(Знач СписокВыбранныхСообщений)
	
	Для Каждого Сообщение Из СписокВыбранныхСообщений Цикл
		Запись = РегистрыСведений.ОчередьСообщенийВ1СДокументооборот.СоздатьМенеджерЗаписи();
		Запись.МоментВремени = Сообщение.МоментВремени;
		Запись.Идентификатор = Сообщение.Идентификатор;
		Запись.Прочитать();
		Если Запись.Выбран() Тогда
			Запись.КоличествоПопытокОтправки = 0;
			Запись.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция СклоненияСтрокиПоЧислу(Знач СклоняемаяСтрока, Знач Число, Знач ОписаниеСтроки, Знач ФорматнаяСтрока)
	
	Склонения = ПолучитьСклоненияСтрокиПоЧислу(СклоняемаяСтрока, Число,, ОписаниеСтроки, ФорматнаяСтрока);
	
	Если Склонения.Количество() = 1 Тогда
		Возврат Склонения[0];
	Иначе
		Возврат Склонения;
	КонецЕсли;
	
КонецФункции

#КонецОбласти