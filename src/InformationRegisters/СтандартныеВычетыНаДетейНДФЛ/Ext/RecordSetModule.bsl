#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	СписокФизическихЛиц = Новый Массив;
	Если Количество() Тогда
		СписокФизическихЛиц = ВыгрузитьКолонку("ФизическоеЛицо");
	КонецЕсли;
	
	ИменаПолей = "ФизическоеЛицо";
	ПрежнийНабор = РегистрыБЗК.ЗаписиНабораИзБазыДанных(ЭтотОбъект, Замещение, ИменаПолей);
	Физлица = КоллекцииБЗК.УникальныеЗначенияКолонки(ПрежнийНабор, "ФизическоеЛицо");
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокФизическихЛиц, Физлица, Истина);
	
	ДополнительныеСвойства.Вставить("ФизическиеЛица", СписокФизическихЛиц);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ЗарплатаКадры.ОтключитьБизнесЛогикуПриЗаписи(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СписокФизическихЛиц = Неопределено;
	Если Не ДополнительныеСвойства.Свойство("ФизическиеЛица", СписокФизическихЛиц) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.СтандартныеВычетыПоНДФЛВторичный.ЗаполнитьВторичныеДанныеВычетыНаДетей(СписокФизическихЛиц);
	// АПК:277-выкл допустимое исключение
	ИнтеграцияУправлениеПерсоналомСобытия.ЗарегистрироватьОбновлениеВычетов(СписокФизическихЛиц);
	// АПК:277-вкл
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.';
						|en = 'Invalid object call on the client.'");
#КонецЕсли