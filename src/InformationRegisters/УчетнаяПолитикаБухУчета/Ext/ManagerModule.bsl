#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает значения по умолчанию для ресурсов регистра.
// Имена ключей структуры должны строго соответствовать именам ресурсов регистра.
// 
// Параметры:
// Возвращаемое значение:
// 	Структура - структура значений ресурсов регистра.
Функция ЗначенияПоУмолчанию() Экспорт
	
	СтруктураЗначений = Новый Структура;
	
	СтруктураЗначений.Вставить("ПрименяетсяПБУ18", Ложь);
	СтруктураЗначений.Вставить("ВедетсяУчетПостоянныхИВременныхРазниц", Ложь);
	СтруктураЗначений.Вставить("ВариантУчетаОтложенногоНалога", Перечисления.ВариантыУчетаОтложенногоНалога.Нет);
	СтруктураЗначений.Вставить("ИспользоватьСчет40", Ложь);
	СтруктураЗначений.Вставить("ЗабалансовыйУчетТМЦВЭксплуатации", Ложь);
	СтруктураЗначений.Вставить("СборкаТоваровЧерезСчет20", Ложь);
	СтруктураЗначений.Вставить("УчетПродукцииПереработчикаНа20Счете", Ложь);
	СтруктураЗначений.Вставить("ВзаимозачетЧерезСчет76", Ложь);
	СтруктураЗначений.Вставить("ВидРазницПБУ18РезервовПоСомнительнымДолгам", Перечисления.ВидыРазницПБУ18.ВР);
	СтруктураЗначений.Вставить("ПроводкиПоРаботникам", Ложь);
	СтруктураЗначений.Вставить("УчетНИОКРПоФСБУ26", ПолучитьФункциональнуюОпцию("ИспользоватьВнеоборотныеАктивы2_4"));
	СтруктураЗначений.Вставить("СтатьяРасходовПоИсследованиям", Неопределено);
	СтруктураЗначений.Вставить("АналитикаРасходовПоИсследованиям", Неопределено);
	СтруктураЗначений.Вставить("ПрименяетсяФСБУ6", Истина);
	СтруктураЗначений.Вставить("ПрименяетсяФСБУ14", Истина);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииОС", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииНМА", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	СтруктураЗначений.Вставить("ПорядокНачисленияАмортизацииАренда", Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету);
	СтруктураЗначений.Вставить("СписыватьСтоимостьМалоценныхТМЦНаРасходы", Ложь);
	СтруктураЗначений.Вставить("СтатьяРасходовДляСписанияМалоценныхТМЦ", Неопределено);
	СтруктураЗначений.Вставить("АналитикаСписанияМалоценныхТМЦ", Неопределено);
	СтруктураЗначений.Вставить("УчетнаяПолитикаСуществует", Ложь);
	СтруктураЗначений.Вставить("НастройкаЗаполненияБухОтчетности", Справочники.НастройкиЗаполненияБухОтчетности.НеНастраивается);
	СтруктураЗначений.Вставить("МетодПереоценкиОС", Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета);
	СтруктураЗначений.Вставить("МетодПереоценкиНМА", Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета);
	
	Возврат СтруктураЗначений

КонецФункции

// Возращает текст запроса по данным регистра.
// 
// Возвращаемое значение:
// 	Строка - Текст запроса.
Функция ТекстЗапросаДействующиеПараметрыНалоговУчетныхПолитик() Экспорт
	ТекстЗапроса = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ТаблицаСрезПоследних.Период КАК Период,
	|	ГоловныеОрганизации.ОбособленноеПодразделение КАК Организация,
	|	ТаблицаСрезПоследних.ВариантУчетаОтложенногоНалога <> ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОтложенногоНалога.Нет)
	|		И ТаблицаСрезПоследних.ВариантУчетаОтложенногоНалога <> ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаОтложенногоНалога.ПустаяСсылка) КАК ПрименяетсяПБУ18,
	|	ТаблицаСрезПоследних.ВедетсяУчетПостоянныхИВременныхРазниц КАК ВедетсяУчетПостоянныхИВременныхРазниц,
	|	ТаблицаСрезПоследних.ВариантУчетаОтложенногоНалога КАК ВариантУчетаОтложенногоНалога,
	|	ТаблицаСрезПоследних.ИспользоватьСчет40 КАК ИспользоватьСчет40,
	|	ТаблицаСрезПоследних.ЗабалансовыйУчетТМЦВЭксплуатации КАК ЗабалансовыйУчетТМЦВЭксплуатации,
	|	ТаблицаСрезПоследних.СборкаТоваровЧерезСчет20 КАК СборкаТоваровЧерезСчет20,
	|	ТаблицаСрезПоследних.УчетПродукцииПереработчикаНа20Счете КАК УчетПродукцииПереработчикаНа20Счете,
	|	ТаблицаСрезПоследних.ВзаимозачетЧерезСчет76 КАК ВзаимозачетЧерезСчет76,
	|	ТаблицаСрезПоследних.ВидРазницПБУ18РезервовПоСомнительнымДолгам КАК ВидРазницПБУ18РезервовПоСомнительнымДолгам,
	|	ТаблицаСрезПоследних.ПроводкиПоРаботникам КАК ПроводкиПоРаботникам,
	|	ТаблицаСрезПоследних.УчетНИОКРПоФСБУ26 КАК УчетНИОКРПоФСБУ26,
	|	ТаблицаСрезПоследних.СтатьяРасходовПоИсследованиям КАК СтатьяРасходовПоИсследованиям,
	|	ТаблицаСрезПоследних.АналитикаРасходовПоИсследованиям КАК АналитикаРасходовПоИсследованиям,
	|	ТаблицаСрезПоследних.ПрименяетсяФСБУ6 КАК ПрименяетсяФСБУ6,
	|	ТаблицаСрезПоследних.ПрименяетсяФСБУ14 КАК ПрименяетсяФСБУ14,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииОС КАК ПорядокНачисленияАмортизацииОС,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииНМА КАК ПорядокНачисленияАмортизацииНМА,
	|	ТаблицаСрезПоследних.ПорядокНачисленияАмортизацииАренда КАК ПорядокНачисленияАмортизацииАренда,
	|	ТаблицаСрезПоследних.МетодПереоценкиОС КАК МетодПереоценкиОС,
	|	ТаблицаСрезПоследних.МетодПереоценкиНМА КАК МетодПереоценкиНМА,
	|	ТаблицаСрезПоследних.НастройкаЗаполненияБухОтчетности КАК НастройкаЗаполненияБухОтчетности,
	|	ТаблицаСрезПоследних.СписыватьСтоимостьМалоценныхТМЦНаРасходы КАК СписыватьСтоимостьМалоценныхТМЦНаРасходы,
	|	ТаблицаСрезПоследних.СтатьяРасходовДляСписанияМалоценныхТМЦ КАК СтатьяРасходовДляСписанияМалоценныхТМЦ,
	|	ТаблицаСрезПоследних.РеформацияБалансаВРазрезеОП КАК РеформацияБалансаВРазрезеОП,
	|	ТаблицаСрезПоследних.АналитикаСписанияМалоценныхТМЦ КАК АналитикаСписанияМалоценныхТМЦ
	|	
	|ИЗ
	|	ВтГоловныеОрганизации КАК ГоловныеОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаБухУчета.СрезПоследних(&Период, Организация В
	|			(ВЫБРАТЬ
	|				ГоловныеОрганизации.Организация
	|			ИЗ
	|				ВтГоловныеОрганизации КАК ГоловныеОрганизации)) КАК ТаблицаСрезПоследних
	|		ПО ГоловныеОрганизации.Организация = ТаблицаСрезПоследних.Организация
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

// Формирует текстовое описание установленных параметров.
// 
// Параметры:
// 	Организация - СправочникСсылка.Организации - ссылка на организацию.
// 	ДатаДействия - Дата - период действия настроек.
// 	ДействующиеНастройки - Структура - действующие параметры учетной политики.
// 	Все - Булево - если Истина, то возвращает описание всех настроек.
// Возвращаемое значение:
// 	Строка - Описание действующих параметров строкой.
Функция ОписаниеДействующихПараметров(Организация, ДатаДействия = Неопределено, ДействующиеНастройки = Неопределено, Все = Ложь) Экспорт
	
	Если ДействующиеНастройки = Неопределено Тогда
		ДействующиеНастройки = НастройкиНалоговУчетныхПолитик.ДействующиеПараметрыНалоговУчетныхПолитикНаДату(
			"УчетнаяПолитикаБухУчета",
			Организация,
			ДатаДействия,
			Ложь);
	КонецЕсли;
	СтрокаШаблон = "%1: %2." + Символы.ПС;
	СтрокаШаблонБулево = "%1." + Символы.ПС;
	Если ДействующиеНастройки = Неопределено Тогда
		СтрокаОписанияНастроек = НСтр("ru = 'Не заданы параметры.';
										|en = 'Parameters are not specified.'");
		Возврат СтрокаОписанияНастроек;
	КонецЕсли;
	
	СтрокаОписанияНастроек = "";
	
	Если ЗначениеЗаполнено(ДействующиеНастройки.ВариантУчетаОтложенногоНалога) Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Учет по ПБУ 18';
					|en = 'Accounting as per PBU 18'"),
				ДействующиеНастройки.ВариантУчетаОтложенногоНалога);
	КонецЕсли;
	
	Если ДействующиеНастройки.ИспользоватьСчет40 Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Выпуск продукции с использованием счета 40 ""Выпуск продукции""';
					|en = 'Product release using account 40 ""Product release""'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ЗабалансовыйУчетТМЦВЭксплуатации Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Ведется дополнительный учет ТМЦ в эксплуатации на забалансовых счетах';
					|en = 'Additional accounting of inventory is kept in operation on off-balance accounts'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.СборкаТоваровЧерезСчет20 Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Сборка товаров через счет 20';
					|en = 'Goods assembly via account 20'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.УчетПродукцииПереработчикаНа20Счете Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Учет продукции переработчика на 20 счете';
					|en = 'Account for subcontractor products on account 20'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ВзаимозачетЧерезСчет76 Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Взаимозачет задолженности производится через счет 76.09 (76.39) ""Прочие расчеты с разными дебиторами и кредиторами""';
					|en = 'Debt is offset using account 76.09 (76.39) ""Other settlements with various debtors and creditors""'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ВедетсяУчетПостоянныхИВременныхРазниц Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Вид разниц ПБУ 18/02';
					|en = 'Kind of differences according to PBU 18/02'"),
				ДействующиеНастройки.ВидРазницПБУ18РезервовПоСомнительнымДолгам);
	КонецЕсли;
	
	Если ДействующиеНастройки.ПроводкиПоРаботникам Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Ведется учет расходов на оплату труда по счету 70 ""Оплата труда"" с детализацией по работникам организации';
					|en = 'The record of labor expenses is kept on account 70 ""Labor compensation"" with details by company employees'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.СписыватьСтоимостьМалоценныхТМЦНаРасходы Или Все Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Списывать стоимость малоценных ТМЦ на расходы в месяце приобретения в соответствии с ФСБУ 5 ""Запасы""';
					|en = 'Write off low-cost inventory as expenses in the purchase month in accordance with FSBU 5 “Inventory”'"));
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Статья расходов для списания малоценных ТМЦ';
					|en = 'Expense item to write off low-cost inventory'"),
				ДействующиеНастройки.СтатьяРасходовДляСписанияМалоценныхТМЦ);
		

		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Аналитика списания малоценных ТМЦ';
					|en = 'Dimension of low-cost inventory write-off'"),
				ДействующиеНастройки.АналитикаСписанияМалоценныхТМЦ);
	КонецЕсли;
	
	#Область ВнеоборотныеАктивы
	
	Если ДействующиеНастройки.ПрименяетсяФСБУ6 Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Учет ОС ведется в соответствии с ФСБУ 6';
					|en = 'Records of fixed assets are kept in accordance with FAS 6'"));
	КонецЕсли;
	
	Если ДействующиеНастройки.ПрименяетсяФСБУ14 Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Учет НМА ведется в соответствии с ФСБУ 14/2022';
					|en = 'Records of intangible assets are kept in accordance with FAS 14/2022'"));
	КонецЕсли;
		
	Если ДействующиеНастройки.УчетНИОКРПоФСБУ26 Или Все Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Учет НИОКР ведется в соответствии с ФСБУ 26/2020';
					|en = 'R&D records are kept in accordance with FAS 26/2020'"));
				
	КонецЕсли;
		
	Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
		И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца
		И ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Или Все Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Амортизация ОС и НМА начисляется с первого числа месяца, следующего за месяцем принятия к учету';
					|en = 'Depreciation of fixed assets and intangible assets is accrued from the first day of the month following the month of recognition'"));
				
	ИначеЕсли ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
		И ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету
		И ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СДатыПринятияКУчету Или Все Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблонБулево,
				НСтр("ru = 'Амортизация ОС и НМА начисляется с даты принятия к учету';
					|en = 'Depreciation of fixed assets and intangible assets is accrued from the date of recognition'"));
				
	Иначе
		
		Если ДействующиеНастройки.ПорядокНачисленияАмортизацииОС = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Или Все Тогда
				
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация собственных ОС начисляется с первого числа месяца, следующего за месяцем принятия к учету';
						|en = 'Depreciation of own fixed assets is charged from the first day of the month following the month of recognition'"));
		Иначе
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация собственных ОС начисляется с даты принятия к учету';
						|en = 'Depreciation of own fixed assets is charged from the date of recognition'"));
					
		КонецЕсли;
				
		Если ДействующиеНастройки.ПорядокНачисленияАмортизацииАренда = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Или Все Тогда
				
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация арендованных ОС начисляется с первого числа месяца, следующего за месяцем принятия к учету';
						|en = 'Depreciation of leased fixed assets is charged from the first day of the month following the month of recognition'"));
		Иначе
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация арендованных ОС начисляется с даты принятия к учету';
						|en = 'Depreciation of leased fixed assets is charged from the date of recognition'"));
					
		КонецЕсли;
				
		Если ДействующиеНастройки.ПорядокНачисленияАмортизацииНМА = Перечисления.ПорядокНачисленияАмортизации.СоСледующегоМесяца Или Все Тогда
				
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация НМА начисляется с первого числа месяца, следующего за месяцем принятия к учету';
						|en = 'Intangible assets — Amortization charge is accrued from the first day of the month following the month of recognition'"));
		Иначе
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Амортизация НМА начисляется с даты принятия к учету';
						|en = 'Depreciation of intangible assets is accrued from the date of recognition'"));
					
		КонецЕсли;
				
	КонецЕсли;
	
	Если ДействующиеНастройки.ПрименяетсяФСБУ6
		И ДействующиеНастройки.ПрименяетсяФСБУ14 Или Все Тогда
		
		Если ДействующиеНастройки.МетодПереоценкиОС = ДействующиеНастройки.МетодПереоценкиНМА Или Все Тогда
			
			Если ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета Или Все Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки ОС и НМА применяется метод пропорционального пересчета';
							|en = 'Для переоценки ОС и НМА применяется метод пропорционального пересчета'"));
						
			ИначеЕсли ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.СальдовыйМетод Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки ОС и НМА применяется сальдовый метод';
							|en = 'Для переоценки ОС и НМА применяется сальдовый метод'"));
						
			КонецЕсли;
			
		Иначе
			
			Если ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки ОС применяется метод пропорционального пересчета';
							|en = 'Для переоценки ОС применяется метод пропорционального пересчета'"));
						
			ИначеЕсли ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.СальдовыйМетод Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки ОС применяется сальдовый метод';
							|en = 'Для переоценки ОС применяется сальдовый метод'"));
						
			КонецЕсли;

			Если ДействующиеНастройки.МетодПереоценкиНМА = Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки НМА применяется метод пропорционального пересчета';
							|en = 'Для переоценки НМА применяется метод пропорционального пересчета'"));
						
			ИначеЕсли ДействующиеНастройки.МетодПереоценкиНМА = Перечисления.МетодыПереоценкиВНА.СальдовыйМетод Тогда
				
				СтрокаОписанияНастроек = СтрокаОписанияНастроек
					+ СтрШаблон(СтрокаШаблонБулево,
						НСтр("ru = 'Для переоценки НМА применяется сальдовый метод';
							|en = 'Для переоценки НМА применяется сальдовый метод'"));
						
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ДействующиеНастройки.ПрименяетсяФСБУ6 Тогда
		
		Если ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Для переоценки ОС применяется метод пропорционального пересчета';
						|en = 'Для переоценки ОС применяется метод пропорционального пересчета'"));
					
		ИначеЕсли ДействующиеНастройки.МетодПереоценкиОС = Перечисления.МетодыПереоценкиВНА.СальдовыйМетод Тогда
				
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Для переоценки ОС применяется сальдовый метод';
						|en = 'Для переоценки ОС применяется сальдовый метод'"));
						
		КонецЕсли;
		
	ИначеЕсли ДействующиеНастройки.ПрименяетсяФСБУ14 Тогда
		
		Если ДействующиеНастройки.МетодПереоценкиНМА = Перечисления.МетодыПереоценкиВНА.МетодПропорциональногоПересчета Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Для переоценки НМА применяется метод пропорционального пересчета';
						|en = 'Для переоценки НМА применяется метод пропорционального пересчета'"));
					
		ИначеЕсли ДействующиеНастройки.МетодПереоценкиНМА = Перечисления.МетодыПереоценкиВНА.СальдовыйМетод Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблонБулево,
					НСтр("ru = 'Для переоценки НМА применяется сальдовый метод';
						|en = 'Для переоценки НМА применяется сальдовый метод'"));
					
		КонецЕсли;
			
	КонецЕсли;
	
	Если ДействующиеНастройки.УчетНИОКРПоФСБУ26 Или Все Тогда
		
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Статья расходов по исследованиям';
					|en = 'Research expense item'"),
				ДействующиеНастройки.СтатьяРасходовПоИсследованиям);
		
		Если ЗначениеЗаполнено(ДействующиеНастройки.АналитикаРасходовПоИсследованиям) Тогда
			
			СтрокаОписанияНастроек = СтрокаОписанияНастроек
				+ СтрШаблон(СтрокаШаблон,
					НСтр("ru = 'Аналитика расходов по исследованиям';
						|en = 'Research expense dimension'"),
					ДействующиеНастройки.АналитикаРасходовПоИсследованиям);
				
		КонецЕсли;
		
	КонецЕсли;
	
	#КонецОбласти
	
	#Область Отчетность
	
	Если ЗначениеЗаполнено(ДействующиеНастройки.НастройкаЗаполненияБухОтчетности) Или Все Тогда
		СтрокаОписанияНастроек = СтрокаОписанияНастроек
			+ СтрШаблон(СтрокаШаблон,
				НСтр("ru = 'Настройка заполнения бухгалтерской (финансовой)  отчетности';
					|en = 'Set filling of accounting (financial) reporting'"),
				ДействующиеНастройки.НастройкаЗаполненияБухОтчетности);
	КонецЕсли;

	#КонецОбласти
		
	Если НЕ ЗначениеЗаполнено(СтрокаОписанияНастроек) Тогда
		СтрокаОписанияНастроек = НСтр("ru = 'Не заданы параметры.';
										|en = 'Parameters are not specified.'");
	КонецЕсли;
	
	Возврат СтрокаОписанияНастроек;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// Параметры:
//   Ограничение - см. УправлениеДоступомПереопределяемый.ПриЗаполненииОграниченияДоступа.Ограничение.
//
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЗначениеРазрешено(Организация)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли
