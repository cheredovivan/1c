#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// Параметры записи регистра.
// 
// Параметры:
//  Идентификатор - Неопределено, Строка - Идентификатор.
// 
// Возвращаемое значение:
//  Структура - Параметры записи регистра:
// * Идентификатор - Строка 
// * ИдентификаторЭДО - Неопределено 
// * ИдентификаторМП - Неопределено 
// * ВидСообщения - Неопределено 
// * Прочитано - Неопределено 
// * ДатаПолучения - Неопределено 
// * Исходящее - Неопределено 
// * Содержимое - Неопределено 
// * ДанныеСообщения - Неопределено 
// * Автор - Неопределено 
// * ВнешнийИД - Неопределено 
// * Дата - Неопределено 
// * ИдентификаторДокумента - Неопределено 
// * ИдентификаторРодителя - Неопределено 
// * ДокументЭПД - Неопределено 
// * Найден - Булево 
Функция ПараметрыЗаписиРегистра(Идентификатор = Неопределено) Экспорт
	
	Результат = Новый Структура();
	Если ЗначениеЗаполнено(Идентификатор) Тогда
		Результат.Вставить("Идентификатор", Идентификатор);
	Иначе
		Результат.Вставить("Идентификатор", Строка(Новый УникальныйИдентификатор));
	КонецЕсли;
	Результат.Вставить("ИдентификаторЭДО", Неопределено);
	Результат.Вставить("ИдентификаторМП", Неопределено);
	Результат.Вставить("ВидСообщения", Неопределено);
	Результат.Вставить("Прочитано", Неопределено);
	Результат.Вставить("ДатаПолучения", Неопределено);
	Результат.Вставить("Исходящее", Неопределено);
	Результат.Вставить("Содержимое", Неопределено);
	Результат.Вставить("ДанныеСообщения", Неопределено);
	Результат.Вставить("Автор", Неопределено);
	Результат.Вставить("ВнешнийИД", Неопределено);
	Результат.Вставить("Дата", Неопределено);
	Результат.Вставить("ИдентификаторДокумента", Неопределено);
	Результат.Вставить("ИдентификаторРодителя", Неопределено);
	Результат.Вставить("ДокументЭПД", Неопределено);
	
	Результат.Вставить("Найден", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Регистрировать запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Данные записи регистра.
// 
// Возвращаемое значение:
//  Булево - Успешность регистрации записи.
Функция РегистрироватьЗапись(ТекущаяЗапись) Экспорт
	
	Результат = Ложь;
	
	Попытка
		НашлиЗапись = НайтиЗапись(ТекущаяЗапись);
		СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(НашлиЗапись, ТекущаяЗапись);

		ЗаписьРегистра = СоздатьМенеджерЗаписи();
		СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(ЗаписьРегистра, НашлиЗапись);
		
		Если ЗаписьРегистра.Исходящее = Ложь Тогда
			ЗаписьРегистра.Дата = ЗаписьРегистра.Дата + СмещениеСтандартногоВремени();
			Если ЗначениеЗаполнено(ЗаписьРегистра.ДатаПолучения) = Ложь Тогда
				ЗаписьРегистра.ДатаПолучения = ТекущаяДатаСеанса();
			КонецЕсли;
		КонецЕсли;
		ЗаписьРегистра.Записать(Истина);
	Исключение
		СервисВзаимодействияМПЭПД.РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Удалить запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Данные записи регистра.
// 
// Возвращаемое значение:
//  Булево - Успешность удаления записи.
Функция УдалитьЗапись(ТекущаяЗапись) Экспорт
	
	Результат = Ложь;
	
	Попытка
		НашлиЗапись = НайтиЗапись(ТекущаяЗапись);
		Если НашлиЗапись.Найден Тогда
			ЗаписьРегистра = СоздатьМенеджерЗаписи();
			СервисВзаимодействияМПЭПД.ЗаполнитьЗначенияОбъекта(ЗаписьРегистра, НашлиЗапись);
			ЗаписьРегистра.Удалить();
			Результат = Истина;
		КонецЕсли;
	Исключение
		СервисВзаимодействияМПЭПД.РегистрацияВЖурнале(УровеньЖурналаРегистрации.Ошибка,
			ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Найти запись.
// 
// Параметры:
//  ТекущаяЗапись - Структура - Текущая запись
// 
// Возвращаемое значение:
//  Структура - Найти запись:
// * Идентификатор - Строка - 
// * ИдентификаторЭДО - Неопределено, Строка 
// * ИдентификаторМП - Неопределено, Строка 
// * ВидСообщения - Неопределено, ПеречислениеСсылка.ТипыВзаимодействияМПЭПД 
// * Прочитано - Неопределено, Булево 
// * ДатаПолучения - Неопределено, Дата 
// * Исходящее - Неопределено, Булево 
// * Содержимое - Неопределено, Строка 
// * ДанныеСообщения - Неопределено, Строка 
// * Автор - Неопределено, ОпределяемыйТип.Пользователь 
// * ВнешнийИД - Неопределено, Строка 
// * Дата - Неопределено, Дата 
// * ИдентификаторДокумента - Неопределено, Строка 
// * ИдентификаторРодителя - Неопределено, Строка 
// * ДокументЭПД - Неопределено, ОпределяемыйТип.ДокументыЭПД 
// * Найден - Булево 
// 
//@skip-check doc-comment-field-type-strict
//@skip-check doc-comment-field-type
//@skip-check doc-comment-description-ends-on-dot
Функция НайтиЗапись(ТекущаяЗапись) Экспорт
	
	Результат = ПараметрыЗаписиРегистра(ТекущаяЗапись.Идентификатор);
	
	Если ЗначениеЗаполнено(ТекущаяЗапись.ВнешнийИД) Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ ПЕРВЫЕ 1 РАЗРЕШЕННЫЕ
		|	КэшВзаимодействияМПЭПД.Идентификатор КАК Идентификатор
		|ИЗ
		|	РегистрСведений.КэшВзаимодействияМПЭПД КАК КэшВзаимодействияМПЭПД
		|ГДЕ
		|	КэшВзаимодействияМПЭПД.ВнешнийИД = &ВнешнийИД";
		Запрос = Новый Запрос(ТекстЗапроса);
		Запрос.УстановитьПараметр("ВнешнийИД", ТекущаяЗапись.ВнешнийИД);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекущаяЗапись.Идентификатор = Выборка.Идентификатор;
		КонецЕсли;
	КонецЕсли;
	
	ЗаписьРегистра = СоздатьМенеджерЗаписи();
	ЗаписьРегистра.Идентификатор = ТекущаяЗапись.Идентификатор;
	ЗаписьРегистра.Прочитать();
	Если ЗаписьРегистра.Выбран() Тогда
		ЗаполнитьЗначенияСвойств(Результат, ЗаписьРегистра);
		Результат.Найден = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли