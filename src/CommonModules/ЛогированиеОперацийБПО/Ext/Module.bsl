///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// ++ НеМобильноеПриложение 

#Область ЛогированиеОборудования

// Возвращает параметры для логирования оборудования
// 
// Возвращаемое значение:
//  Структура - Параметры логирования операций с оборудованием:
//   * ИспользоватьЛогирование - Булево - 
//   * ИнтервалПереносаНаСервер - Число - интервал переноса на сервер в секундах, минимум 60
//
Функция ПараметрыЛогированияОперацийСОборудованием() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Результат = Новый Структура();
	Результат.Вставить("ИспользоватьЛогирование", Истина);
	Результат.Вставить("ИнтервалПереносаНаСервер", 60);
	ЛогированиеОперацийБПОПереопределяемый.ПриОпределенииПараметровЛогированияПодключаемогоОборудования(Результат);
	Возврат Результат;
	
КонецФункции

Процедура УстановитьСрокХраненияОперацийСОборудованием() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	СрокХраненияОперацийСОборудованием = Константы.СрокХраненияОперацийСОборудованием.Получить();
	Если НЕ ЗначениеЗаполнено(СрокХраненияОперацийСОборудованием) Тогда
		Константы.СрокХраненияОперацийСОборудованием.Установить(Перечисления.ПериодХраненияИсторииОпераций.Месяц);
	КонецЕсли;
	Если СрокХраненияОперацийСОборудованием <> Перечисления.ПериодХраненияИсторииОпераций.ВсеВремя Тогда
		ВключитьИспользованиеРегламентногоЗадания("ОчисткаИсторииОперацийСОборудованием");
	КонецЕсли;
	
КонецПроцедуры

// Возвращает период хранения операций с оборудованием
//
// Возвращаемое значение:
//   ПеречислениеСсылка
//
Функция ПериодХраненияИсторииОперацийСОборудованием() Экспорт
	
	ПериодХранения = Константы.СрокХраненияОперацийСОборудованием.Получить();
	Возврат ПериодХранения;
	
КонецФункции

// Выполнят очистку истории операций с оборудованием
//
Процедура ОчисткаИсторииОперацийСОборудованием() Экспорт
	
	ОбщегоНазначенияБПО.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийСОборудованием);
	
	Если ОбщегоНазначенияБПО.ИспользуетсяПодсистемаЛогирования() Тогда
		
		ПериодХранения = ПериодХраненияИсторииОперацийСОборудованием();
		ДатаОчистки    = ОпределитьДатуОчисткиОпераций(ПериодХранения);
		Если ДатаОчистки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МодульОперацииСПодключаемымОборудованием = 
			ОбщегоНазначенияБПО.ОбщийМодуль("РегистрыСведений.ОперацииСПодключаемымОборудованием");
		МодульОперацииСПодключаемымОборудованием.ОчиститьРегистрЗаПрошлыеМесяцы(ДатаОчистки);
		МодульОперацииСПодключаемымОборудованием.ОчиститьРегистрЗаТекущийМесяц(ДатаОчистки);
		
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗадания

// Вызывается из процедуры РегламентныеЗаданияПереопределяемый.ПриОпределенииНастроекРегламентныхЗаданий
// для установки доступности регламентного задания, определяет зависимость от функциональных опций.
// 
// Параметры:
//  Настройки - ТаблицаЗначений
//  ФункциональнаяОпция - ОбъектМетаданныхФункциональнаяОпция
Процедура ПриОпределенииНастроекРегламентныхЗаданий(Настройки, ФункциональнаяОпция = Неопределено) Экспорт
	
	Настройка = Настройки.Добавить();
	Настройка.РегламентноеЗадание = Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийСОборудованием;
	Настройка.ФункциональнаяОпция = ФункциональнаяОпция;
	Настройка.РаботаетСВнешнимиРесурсами = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область РегламентныеЗаданияТехнологияСервиса

// См. ОчередьЗаданийПереопределяемый.ПриПолученииСпискаШаблонов
Процедура ПриПолученииСпискаШаблонов(ШаблоныЗаданий) Экспорт
	
	ШаблоныЗаданий.Добавить(Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийСОборудованием.Имя);
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийСОборудованием.ИмяМетода);
	
КонецПроцедуры

#КонецОбласти

// Выполнят очистку истории платежных операций
//
Процедура ОчисткаИсторииПлатежныхОпераций() Экспорт
	
	ОбщегоНазначенияБПО.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаИсторииПлатежныхОпераций);
	
	Если ОбщегоНазначенияБПО.ИспользуетсяПлатежныеСистемы() Тогда
		
		ПериодХранения = ПериодХраненияИсторииПлатежныхОпераций();
		ДатаОчистки = ОпределитьДатуОчисткиОпераций(ПериодХранения);
		
		МодульОборудованиеПлатежныеСистемы = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеПлатежныеСистемы");
		МодульОборудованиеПлатежныеСистемы.ОчисткаИсторииПлатежныхОпераций(ДатаОчистки);
		
	КонецЕсли;
	
КонецПроцедуры

// Выполнят очистку истории фискальных операций
//
Процедура ОчисткаИсторииФискальныхОпераций() Экспорт
	
	ОбщегоНазначенияБПО.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаИсторииФискальныхОпераций);
	
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		
		ПериодХранения = ПериодХраненияИсторииФискальныхОпераций();
		ДатаОчистки = ОпределитьДатуОчисткиОпераций(ПериодХранения);
		
		МодульОборудованиеЧекопечатающиеУстройства = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства");
		МодульОборудованиеЧекопечатающиеУстройства.ОчисткаИсторииФискальныхОпераций(ДатаОчистки);
		
	КонецЕсли;
	
КонецПроцедуры

// ++ Локализация

// Выполнят очистку истории операций очереди чеков
//
Процедура ОчисткаИсторииОперацийОчередиЧеков() Экспорт
	
	ОбщегоНазначенияБПО.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийОчередиЧеков);
	
	Если ОбщегоНазначенияБПО.ИспользуетсяРаспределеннаяФискализация() Тогда
		МодульРаспределеннаяФискализация = ОбщегоНазначенияБПО.ОбщийМодуль("РаспределеннаяФискализация");
		
		ПериодХранения = ПериодХраненияИсторииОперацийОчередиЧеков();
		ДатаОчистки    = ОпределитьДатуОчисткиОпераций(ПериодХранения);
		МодульРаспределеннаяФискализация.ОчисткаИсторииОперацийОчередиЧеков(ДатаОчистки);
	КонецЕсли;
	
КонецПроцедуры

// Выполнят очистку истории операций проверки КМ
//
Процедура ОчисткаИсторииОперацийПроверкиКМ() Экспорт
	
	ОбщегоНазначенияБПО.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ОчисткаИсторииОперацийПроверкиКМ);
	
	Если ОбщегоНазначенияБПО.ИспользуетсяМаркировка() Тогда
		
		ПериодХранения = ПериодХраненияИсторииОперацийПроверкиКМ();
		ДатаОчистки    = ОпределитьДатуОчисткиОпераций(ПериодХранения);
		Если ДатаОчистки = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		МодульМенеджерОборудованияМаркировка = ОбщегоНазначенияБПО.ОбщийМодуль("МенеджерОборудованияМаркировка");
		МодульМенеджерОборудованияМаркировка.ОчисткаИсторииОперацийПроверкиКМ(ДатаОчистки);
		
	КонецЕсли;
	
КонецПроцедуры

// -- Локализация
// -- НеМобильноеПриложение 

Функция ОпределитьДатуОчисткиОпераций(ПериодХранения) Экспорт
	
	ТекущаяДата = ОбщегоНазначенияБПО.ДатаСеанса();
	Если ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.День Тогда
		ОдинДеньСекунды = 60*60*24*1;
		ДатаОчистки = ТекущаяДата - ОдинДеньСекунды;
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Неделя Тогда
		НеделяСекунды = 60*60*24*7;
		ДатаОчистки = ТекущаяДата - НеделяСекунды;
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Декада Тогда
		ДекадаСекунды = 60*60*24*10;
		ДатаОчистки = ТекущаяДата - ДекадаСекунды;
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Месяц Тогда
		ДатаОчистки = ДобавитьМесяц(ТекущаяДата, -1);
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Квартал Тогда
		ДатаОчистки = ДобавитьМесяц(ТекущаяДата, -3);
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Полугодие Тогда
		ДатаОчистки = ДобавитьМесяц(ТекущаяДата, -6);
	ИначеЕсли ПериодХранения = Перечисления.ПериодХраненияИсторииОпераций.Год Тогда
		ДатаОчистки = ДобавитьМесяц(ТекущаяДата, -12);
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ДатаОчистки;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ НеМобильноеПриложение 

// Возвращает период хранения фискальных операций в регистре сведений
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ПериодХраненияИсторииОпераций
Функция ПериодХраненияИсторииФискальныхОпераций()
	
	ПериодХранения = Неопределено;
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодульОборудованиеЧекопечатающиеУстройства = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеЧекопечатающиеУстройства");
		ПериодХранения = МодульОборудованиеЧекопечатающиеУстройства.ПериодХраненияИсторииФискальныхОпераций();
	КонецЕсли;
	Возврат ПериодХранения;
	
КонецФункции

// Возвращает период хранения платежных операций в регистре сведений
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ПериодХраненияИсторииОпераций
Функция ПериодХраненияИсторииПлатежныхОпераций()
	
	ПериодХранения = Неопределено;
	Если ОбщегоНазначенияБПО.ИспользуетсяПлатежныеСистемы() Тогда
		МодульОборудованиеПлатежныеСистемы = ОбщегоНазначенияБПО.ОбщийМодуль("ОборудованиеПлатежныеСистемы");
		ПериодХранения = МодульОборудованиеПлатежныеСистемы.ПериодХраненияИсторииПлатежныхОпераций();
	КонецЕсли;
	Возврат ПериодХранения;
	
КонецФункции

// ++ Локализация

// Возвращает период хранения операций очереди чеков в регистре сведений
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ПериодХраненияИсторииОпераций
Функция ПериодХраненияИсторииОперацийОчередиЧеков()
	
	ПериодХранения = Неопределено;
	Если ОбщегоНазначенияБПО.ИспользуетсяЧекопечатающиеУстройства() Тогда
		МодульРаспределеннаяФискализация = ОбщегоНазначенияБПО.ОбщийМодуль("РаспределеннаяФискализация");
		ПериодХранения = МодульРаспределеннаяФискализация.ПериодХраненияИсторииОперацийОчередиЧеков();
	КонецЕсли;
	Возврат ПериодХранения;
	
КонецФункции

// Возвращает период хранения операций проверки КМ в регистре сведений
//
// Возвращаемое значение:
//   ПеречислениеСсылка.ПериодХраненияИсторииОпераций
Функция ПериодХраненияИсторииОперацийПроверкиКМ()
	
	ПериодХранения = Неопределено;
	Если ОбщегоНазначенияБПО.ИспользуетсяМаркировка() Тогда
		МодульМенеджерОборудованияМаркировка = ОбщегоНазначенияБПО.ОбщийМодуль("МенеджерОборудованияМаркировка");
		ПериодХранения = МодульМенеджерОборудованияМаркировка.ПериодХраненияИсторииОперацийПроверкиКМ();
	КонецЕсли;
	Возврат ПериодХранения;
	
КонецФункции

// -- Локализация

Процедура ЗаписатьЛогОперацииНаСервере(Записи) Экспорт
	
	РегистрыСведений.ОперацииСПодключаемымОборудованием.ЗаписатьЛогОперацииНаСервере(Записи);
	
КонецПроцедуры

Функция ЗаписатьЛогОперацииНаСервере_ПреобразованиеВJSON(Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт  
	
	Возврат Строка(Значение);   
	
КонецФункции

Функция СвернутьЗаписиЛогаОперации(Записи, СворачиваемыеОперации) Экспорт
	
	НовыеЗаписи = Новый Массив();
	СворачиваемаяЗапись = Неопределено;
	
	Для Индекс = 0 По Записи.ВГраница() Цикл
		
		Запись = Записи[Индекс];
		Операция = Запись.Операция;
		Если СтрНайти(СворачиваемыеОперации, Операция)=0 Тогда
			СворачиваемаяЗапись = Неопределено;
			НовыеЗаписи.Добавить(Запись);
			Продолжить;
		КонецЕсли;
		
		Если СворачиваемаяЗапись = Неопределено Тогда
			СворачиваемаяЗапись = Запись;
			НовыеЗаписи.Добавить(Запись);
			Продолжить;
		КонецЕсли;
		
		Если Не ОдинаковаяОперация(СворачиваемаяЗапись, Запись) Тогда
			СворачиваемаяЗапись = Неопределено;
			НовыеЗаписи.Добавить(Запись);
			Продолжить;
		КонецЕсли;
		
		// предыдущая и текущая операции одинаковые
		// текущую запись пропускаем и заполняем предыдущую (сворачиваемую) которая уже занесена в массив ранее
		Если ТипЗнч(СворачиваемаяЗапись.ВходящиеПараметры) <> Тип("Массив") Тогда
			НовыеПараметры = Новый Массив();
			НовыеПараметры.Добавить(СворачиваемаяЗапись.ВходящиеПараметры);
			СворачиваемаяЗапись.ВходящиеПараметры = НовыеПараметры;
			
			НовыеПараметры = Новый Массив();
			НовыеПараметры.Добавить(СворачиваемаяЗапись.ИсходящиеПараметры);
			СворачиваемаяЗапись.ИсходящиеПараметры = НовыеПараметры;
		КонецЕсли;
		
		СворачиваемаяЗапись.ВходящиеПараметры.Добавить(Запись.ВходящиеПараметры);
		СворачиваемаяЗапись.ИсходящиеПараметры.Добавить(Запись.ИсходящиеПараметры);
		СворачиваемаяЗапись.ВремяВыполнения = СворачиваемаяЗапись.ВремяВыполнения + Запись.ВремяВыполнения;
		
	КонецЦикла;
	Возврат НовыеЗаписи;
	
КонецФункции

#Область ЛогированиеОборудования

Функция ОдинаковаяОперация(Запись1, Запись2)
	
	Если Запись1.Операция = Запись2.Операция
		И Запись1.Оборудование = Запись2.Оборудование
		И Запись1.ВерсияДрайвера = Запись2.ВерсияДрайвера
		И Запись1.Результат = Запись2.Результат
		И Запись1.ТипЗаписи = Запись2.ТипЗаписи
		И Запись1.РевизияИнтерфейса = Запись2.РевизияИнтерфейса Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции

Функция ФорматПараметраЛогирования(Текст) Экспорт
	
	Если Не ЗначениеЗаполнено(Текст) Тогда
		Возврат "";
	КонецЕсли;
	Если СтрНачинаетсяС(Текст, "{") Или СтрНачинаетсяС(Текст, "[") Тогда
		Результат = "";
		ЧтениеJSON = Новый ЧтениеJSON();
		ЧтениеJSON.УстановитьСтроку(Текст);
		
		Данные = ПрочитатьJSON(ЧтениеJSON, Истина);
		Результат = СтрокаИзЗначения(Данные);
		Возврат Результат;
	Иначе
		Возврат Текст;
	КонецЕсли;
	
КонецФункции

Функция СтрокаИзЗначения(Значение, Отступ="")
	Результат = "";
	Если ТипЗнч(Значение) = Тип("Массив") Тогда
		Для Индекс = 0 По Значение.ВГраница() Цикл
			ЗначениеРеквизита = СтрокаИзЗначения(Значение[Индекс], Символы.Таб);
			Результат = Результат + Отступ + СтрШаблон("[%1]: %2", Индекс, ЗначениеРеквизита) + Символы.ПС;
		КонецЦикла;
	Иначе
		Для Каждого КлючЗначение Из Значение Цикл
			Результат = Результат + Отступ + СтрШаблон("%1:", КлючЗначение.Ключ) + Символы.ПС;
			Если ТипЗнч(КлючЗначение.Значение) = Тип("Соответствие") Тогда
				ЗначениеРеквизита = СтрокаИзЗначения(КлючЗначение.Значение, Символы.Таб);
				Результат = Результат + Отступ + ЗначениеРеквизита + Символы.ПС;
			ИначеЕсли ТипЗнч(КлючЗначение.Значение) = Тип("Структура") Тогда
				ЗначениеРеквизита = СтрокаИзЗначения(КлючЗначение.Значение, Символы.Таб);
				Результат = Результат + Отступ + ЗначениеРеквизита + Символы.ПС;
			ИначеЕсли ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
				Для Каждого Элемент Из КлючЗначение.Значение Цикл
					ЗначениеРеквизита = СтрокаИзЗначения(Элемент, Символы.Таб);
					Результат = Результат + Отступ + "- " + ЗначениеРеквизита + Символы.ПС;
				КонецЦикла;
			ИначеЕсли СтрНачинаетсяС(КлючЗначение.Значение, "<?xml") Тогда
				Результат = Результат + Отступ + ФорматXML(КлючЗначение.Значение) + Символы.ПС;
			Иначе
				Результат = Результат + Символы.Таб + Отступ + КлючЗначение.Значение + Символы.ПС;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Возврат Результат;
	
КонецФункции

Функция ФорматXML(Текст)
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.УстановитьСтроку(Текст);
	ЧтениеXML.Прочитать();
	
	Построитель = новый ПостроительDOM();
	Попытка
		Значение = Построитель.Прочитать(ЧтениеXML)
	Исключение
		Возврат Текст;
	КонецПопытки;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку(Новый ПараметрыЗаписиXML("UTF-8",,Истина, Истина));
	
	ЗаписьDOM = Новый ЗаписьDOM();
	ЗаписьDOM.Записать(Значение, ЗаписьXML);
	Возврат ЗаписьXML.Закрыть();
	
КонецФункции

Функция РегламентноеЗаданиеПоИмени(ИмяЗадания)
	
	// Вызов БСП
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания") Тогда
		
		МодульРегламентныеЗаданияСервер = ОбщегоНазначенияБПО.ОбщийМодуль("РегламентныеЗаданияСервер");
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Метаданные", ИмяЗадания);
		РегЗадания = МодульРегламентныеЗаданияСервер.НайтиЗадания(ПараметрыОтбора);
		Если РегЗадания.Количество() > 0 Тогда
			Возврат РегЗадания[0];
		КонецЕсли;
		
	КонецЕсли;
	// Конец Вызов БСП
	
	Возврат Неопределено;
	
КонецФункции

Процедура ВключитьИспользованиеРегламентногоЗадания(ИмяЗадания)
	
	// Вызов БСП
	Если ОбщегоНазначенияБПО.ПодсистемаСуществует("СтандартныеПодсистемы.РегламентныеЗадания") Тогда
		
		МодульРегламентныеЗаданияСервер = ОбщегоНазначенияБПО.ОбщийМодуль("РегламентныеЗаданияСервер");
		
		РегЗадание = РегламентноеЗаданиеПоИмени(ИмяЗадания);
		Если РегЗадание = Неопределено Тогда
			Возврат;
		КонецЕсли;

		ПараметрыЗадания = Новый Структура;
		ПараметрыЗадания.Вставить("Использование", Истина);
		МодульРегламентныеЗаданияСервер.ИзменитьЗадание(РегЗадание.УникальныйИдентификатор, ПараметрыЗадания);
		
	Иначе
		#Если Не МобильноеПриложениеСервер Тогда
		РегЗаданиеМетаданные = Метаданные.РегламентныеЗадания.Найти(ИмяЗадания);
		РегЗадание = РегламентныеЗадания.НайтиПредопределенное(РегЗаданиеМетаданные);
		РегЗадание.Использование = Истина;
		РегЗадание.Записать();
		#КонецЕсли
	КонецЕсли;
	// Конец Вызов БСП
	
КонецПроцедуры

#КонецОбласти

// -- НеМобильноеПриложение 

#КонецОбласти


