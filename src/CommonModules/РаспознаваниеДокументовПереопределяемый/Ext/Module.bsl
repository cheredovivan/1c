#Область ПрограммныйИнтерфейс

// Параметры:
//  Логин - Строка 
//  Пароль - Строка
//
Процедура ПриВыполненииАвторизации(ПараметрыАвторизации) Экспорт
	
КонецПроцедуры

// Реализует обработку события изменения авторизации информационной базы
// в сервисе распознавания документов.
//
// Параметры:
//  ПараметрыАвторизации - Структура - структура с полями
//    * Состояние - Строка - возможные варианты "Ожидает", "Активирован";
//    * ТипАутентификации - Строка - возможные варианты "НеВыполнена", "ПоТикетуИТС", "ПоЛогинуПаролю";
//
Процедура ПриИзмененииДанныхАвторизации(ПараметрыАвторизации) Экспорт
	
КонецПроцедуры

Процедура ПередЗаписьюРаспознанногоДокумента(
		ДокументОбъект,
		РезультатОбратнойСвязи = Неопределено
	) Экспорт
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
		
		ПараметрНомер = "";
		ПараметрДата = "";
		ПараметрСумма = "";
		
		ОсновныеРеквизиты = Новый Структура;
		Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
			ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
			
			Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
				ОсновныеРеквизиты.Вставить("Покупатель", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПродавецОрганизация", "Организация");
			Иначе
				ОсновныеРеквизиты.Вставить("Продавец", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПокупательОрганизация", "Организация");
			КонецЕсли;
		ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
			
			Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
				ОсновныеРеквизиты.Вставить("Покупатель", "Контрагент");
				ОсновныеРеквизиты.Вставить("Исполнитель", "Организация");
			Иначе
				ОсновныеРеквизиты.Вставить("Продавец", "Контрагент");
				ОсновныеРеквизиты.Вставить("ПокупательОрганизация", "Организация");
			КонецЕсли;
		КонецЕсли;
		
		Для Каждого СтрокаТаблицы Из ДокументОбъект.РеквизитыДокумента Цикл
			Если СтрокаТаблицы.ИмяРеквизита = "НомерДокумента" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрНомер = СтрокаТаблицы.Значение;
				Иначе
					ПараметрНомер = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли СтрокаТаблицы.ИмяРеквизита = "ДатаДокумента" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрДата = Формат(СтрокаТаблицы.Значение, "ДФ='dd.MM.yyyy'");
				Иначе
					ПараметрДата = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли СтрокаТаблицы.ИмяРеквизита = "ИтогоВсего" Тогда
				Если ЗначениеЗаполнено(СтрокаТаблицы.Значение) Тогда
					ПараметрСумма = СтрокаТаблицы.Значение;
				Иначе
					ПараметрСумма = СтрокаТаблицы.РаспознанныйТекст;
				КонецЕсли;
			ИначеЕсли ОсновныеРеквизиты.Свойство(СтрокаТаблицы.ИмяРеквизита) Тогда
				
				ИмяРеквизитаДокумента = ОсновныеРеквизиты[СтрокаТаблицы.ИмяРеквизита];
				ДокументОбъект[ИмяРеквизитаДокумента] = СтрокаТаблицы.Значение;
				
				Если ИмяРеквизитаДокумента = "Контрагент"
					И ТипЗнч(СтрокаТаблицы.Значение) = Тип("СправочникСсылка.Контрагенты") Тогда
					Отбор = Новый Структура("ИмяРеквизита", "Партнер");
					СтрокиТаблицы = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
					Если СтрокиТаблицы.Количество() > 0 Тогда
						ДокументОбъект.Партнер = Общегоназначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.Значение, "Партнер", Истина);
						СтрокиТаблицы[0].Значение = ДокументОбъект.Партнер;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
		КонецЦикла;
		
		Если РезультатОбратнойСвязи <> Неопределено Тогда
			РезультатОбратнойСвязи.НомерРаспознанногоДокумента = ДокументОбъект.Номер;
			РезультатОбратнойСвязи.ЭтоВходящийДокумент = (ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий);
			РезультатОбратнойСвязи.НомерДокумента = ПараметрНомер;
			РезультатОбратнойСвязи.ДатаДокумента = ПараметрДата;
			РезультатОбратнойСвязи.СуммаДокумента = ПараметрСумма;
			РезультатОбратнойСвязи.Контрагент = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(ДокументОбъект.Контрагент);
			РезультатОбратнойСвязи.Организация = РаспознаваниеДокументов.УбратьОрганизационнуюФорму(ДокументОбъект.Организация);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// При сопоставлении типов документов.
// 
// Параметры:
//  СоответствиеТиповДокументов - Соответствие Из Строка, ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов
//
Процедура ПриСопоставленииТиповДокументов(СоответствиеТиповДокументов) Экспорт
	
КонецПроцедуры

// При сопоставлении реквизитов документа.
// 
// Параметры:
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов 
//  РеквизитыДокумента - ТаблицаЗначений:
// * Адрес - Строка 
// * ИмяРеквизита - Строка 
// * ОписаниеТипа - ОписаниеТипов
// * НеОтображать - Булево 
// * НетНаПечатнойФорме - Булево
//
Процедура ПриСопоставленииРеквизитовДокумента(ТипДокумента, РеквизитыДокумента) Экспорт
	
КонецПроцедуры

// При приведении типа реквизита в объекте.
// 
// Параметры:
//  МетаданныеОбъекта - ОбъектМетаданных
//  ПереданноеЗначение - Произвольный
//  ТипЭтогоЗначения - Тип
//  ТипРеквизитаВОбъекте - Тип
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыСтавокНДС - При приведении типа реквизита в объекте
Функция ПриПриведенииТипаРеквизитаВОбъекте(
		МетаданныеОбъекта,
		ПереданноеЗначение,
		ТипЭтогоЗначения,
		ТипРеквизитаВОбъекте
	) Экспорт
	
	//++ НЕ УТ
	Если ТипРеквизитаВОбъекте = Тип("ПеречислениеСсылка.ВидыСтавокНДС")
		И ТипЭтогоЗначения = Тип("ПеречислениеСсылка.СтавкиНДС") Тогда
		
		Результат = Перечисления.ВидыСтавокНДС.ВидСтавки(ПереданноеЗначение);
	Иначе
		Результат = ПереданноеЗначение;
	КонецЕсли;
	Возврат Результат;
	//-- НЕ УТ
	
КонецФункции

// При определении юр физ лица по организационной форме.
// 
// Параметры:
//  Результат - Структура Из ПеречислениеСсылка.ЮридическоеФизическоеЛицо
//
Процедура ПриОпределенииЮрФизЛицаПоОрганизационнойФорме(Результат) Экспорт
	
	Значение = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо;
	Результат.Вставить("публичное акционерное общество", Значение);
	Результат.Вставить("открытое акционерное общество", Значение);
	Результат.Вставить("закрытое акционерное общество", Значение);
	Результат.Вставить("общество с ограниченной ответственностью", Значение);
	Результат.Вставить("акционерное общество", Значение);
	Результат.Вставить("федеральное государственное унитарное предприятие", Значение);
	Результат.Вставить("пао", Значение);
	Результат.Вставить("оао", Значение);
	Результат.Вставить("зао", Значение);
	Результат.Вставить("ооо", Значение);
	Результат.Вставить("ао", Значение);
	Результат.Вставить("фгуп", Значение);
	
	Значение = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо;
	Результат.Вставить("индивидуальный предприниматель", Значение);
	Результат.Вставить("ип", Значение);
	
КонецПроцедуры

// В параметре Параметры.ТекстЗапросаКандидаты требуется выбрать:
// - Ссылка
// - ДополнительнаяСсылка
// - Все поля, добавленные в параметр ВесаРеквизитовПредварительнойОценки
// или оставить параметр пустым, тогда будет составлен запрос по-умолчанию к объекту метаданного.
//
Процедура ПриОпределенииПараметровНечеткогоПоиска(Параметры, МетаданныеОбъекта) Экспорт
	
	Если МетаданныеОбъекта = Метаданные.Справочники.Номенклатура Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НаименованиеПолное", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Артикул", "Артикул");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.ВесаРеквизитов.Вставить("Артикул", 0.1);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("НаименованиеПолное");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Артикул");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НаименованиеПолное", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Артикул", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.НоменклатураКонтрагентов Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Артикул", "Артикул");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.ВесаРеквизитов.Вставить("Артикул", 0.1);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Артикул");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Артикул", 1);

	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.Контрагенты
		Или МетаданныеОбъекта = Метаданные.Справочники.Организации Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НаименованиеПолное", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("ИНН", "ИНН");
		Параметры.СоответствиеРеквизитов.Вставить("КПП", "КПП");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.ВесаРеквизитов.Вставить("ИНН", 1);
		Параметры.ВесаРеквизитов.Вставить("КПП", 0.5);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("НаименованиеПолное");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("ИНН");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НаименованиеПолное", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.5);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("ИНН", 1);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("КПП", 0);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.КлассификаторБанков Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);       
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.БанковскиеСчетаКонтрагентов
		Или  МетаданныеОбъекта = Метаданные.Справочники.БанковскиеСчетаОрганизаций Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("НомерСчета", "НомерСчета");
		Параметры.ВесаРеквизитов.Вставить("НомерСчета", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("НомерСчета");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("НомерСчета", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.УпаковкиЕдиницыИзмерения Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		
		Параметры.ВесаРеквизитов.Вставить("Наименование", 0.75);
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.75);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.ДоговорыКонтрагентов Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Номер", "Номер");
		Параметры.СоответствиеРеквизитов.Вставить("Дата", "Дата");
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		
		Параметры.ВесаРеквизитов.Вставить("Номер", 1);
		Параметры.ВесаРеквизитов.Вставить("Дата", 0.75);
		Параметры.ВесаРеквизитов.Вставить("Наименование", 0.75);
		
		Параметры.РеквизитыПолногоСоответствия.Добавить("Номер");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Дата");
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Номер", 1);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Дата", 0.75);
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 0.75);
		
	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.НомераГТД Тогда
		
		Параметры.СоответствиеРеквизитов.Вставить("Код", "Код");
		Параметры.ВесаРеквизитов.Вставить("Код", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Код");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Код", 1);

	ИначеЕсли МетаданныеОбъекта = Метаданные.Справочники.СтавкиНДС Тогда
		
		Параметры.Вставить("МинимальнаяДлинаСтрокиДляПоиска", 1);
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 1);
		
	Иначе
		
		Параметры.СоответствиеРеквизитов.Вставить("Наименование", "Наименование");
		Параметры.ВесаРеквизитов.Вставить("Наименование", 1);
		Параметры.РеквизитыПолногоСоответствия.Добавить("Наименование");
		Параметры.ВесаРеквизитовПредварительнойОценки.Вставить("Наименование", 1);
		
	КонецЕсли;
	
КонецПроцедуры

// При заполнении распознанного документа.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ДокументСсылка - ДокументСсылка.РаспознанныйДокумент
//
Процедура ПриЗаполненииРаспознанногоДокумента(ДокументОбъект, ДокументСсылка) Экспорт
	
	ПроверяемыеРеквизиты = Новый Структура;
	ПроверяемыеРеквизиты.Вставить("Исполнитель", Неопределено);
	ПроверяемыеРеквизиты.Вставить("ПродавецОрганизация", Неопределено);
	ПроверяемыеРеквизиты.Вставить("ПокупательОрганизация", Неопределено);
	ПроверяемыеРеквизиты.Вставить("СуммаВключаетНДС", Ложь);
	ПроверяемыеРеквизиты.Вставить("ТекстНДС", "");
	ПроверяемыеРеквизиты.Вставить("ИтогоСумма", 0);
	ПроверяемыеРеквизиты.Вставить("ИтогоСуммаНДС", 0);
	ПроверяемыеРеквизиты.Вставить("ИтогоВсего", 0);
	
	Для Каждого РеквизитШапки Из ДокументОбъект.РеквизитыДокумента Цикл
		// Если Грузоотправитель совпадает с полем Продавец или Грузополучатель - с полем Покупатель, 
		// тогда Значение нужно оставить пустым
		Если РеквизитШапки.ИмяРеквизита = "Грузоотправитель" Тогда
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("Продавец", "ИмяРеквизита");
			Если НайденнаяСтрока <> Неопределено И РеквизитШапки.Значение = НайденнаяСтрока.Значение Тогда
				РеквизитШапки.Значение = Справочники.Контрагенты.ПустаяСсылка();
			КонецЕсли;
		ИначеЕсли РеквизитШапки.ИмяРеквизита = "Грузополучатель" Тогда
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("Покупатель", "ИмяРеквизита");
			Если НайденнаяСтрока <> Неопределено И РеквизитШапки.Значение = НайденнаяСтрока.Значение Тогда
				РеквизитШапки.Значение = Справочники.Контрагенты.ПустаяСсылка();
			КонецЕсли;
		ИначеЕсли РеквизитШапки.ИмяРеквизита = "СуммаВключаетНДС" Тогда
			СтароеЗначение = РеквизитШапки.Значение;
			
			Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг 
				ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
				
				Если СтрНачинаетсяС(РеквизитШапки.РаспознанныйТекст, "В том числе НДС") Тогда
					РеквизитШапки.Значение = Истина;
				Иначе
					// "Без налога (НДС)"
					// "Сумма НДС"
					РеквизитШапки.Значение = Ложь;
				КонецЕсли;
				
			КонецЕсли;
			
			ПроверяемыеРеквизиты.Вставить("СуммаВключаетНДС", РеквизитШапки.Значение);
			ПроверяемыеРеквизиты.Вставить("ТекстНДС", РеквизитШапки.РаспознанныйТекст);
			ПроверяемыеРеквизиты.Вставить("ИзмениласьСуммаВключаетНДС", СтароеЗначение <> РеквизитШапки.Значение);
		ИначеЕсли ПроверяемыеРеквизиты.Свойство(РеквизитШапки.ИмяРеквизита) Тогда
			ПроверяемыеРеквизиты.Вставить(РеквизитШапки.ИмяРеквизита, РеквизитШапки.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
		
		Если ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПродавецОрганизация) И НЕ ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПокупательОрганизация) Тогда
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
		Иначе
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
		КонецЕсли;
	ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		Если ЗначениеЗаполнено(ПроверяемыеРеквизиты.Исполнитель) И НЕ ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПокупательОрганизация) Тогда
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий;
		Иначе
			ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий;
		КонецЕсли;
	КонецЕсли;
	
	// На основании направления можно определить Контрагента и Организацию, а по ним заполнить Договор
	// 
	ПараметрыВыбораДоговора = РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора(ДокументОбъект);
	ЕстьНезаполненныйПараметр = Ложь;
	Для Каждого ПараметрВыбора Из ПараметрыВыбораДоговора Цикл
		Если Не ЗначениеЗаполнено(ПараметрВыбора.Значение) Тогда
			// для массива тоже подходит. т.е. при условии ТипЗнч(ПараметрВыбора.Значение) = Тип("Массив")
			ЕстьНезаполненныйПараметр = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьНезаполненныйПараметр Тогда
		// Используем заполнение без нечеткого поиска
		РаспознаваниеДокументовСлужебный.ЗаполнитьДоговорКонтрагента(ДокументОбъект);
	Иначе
		
		ОтборМетаданных = Новый Массив;
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Организация", "=", ПараметрыВыбораДоговора.Организация));  
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"Контрагент", "=", ПараметрыВыбораДоговора.Контрагент));
		ОтборМетаданных.Добавить(Новый Структура("Свойство, ВидСравнения, Значение",
			"ТипДоговора", "В", ПараметрыВыбораДоговора.ВидыДоговоров));
		
		ДанныеПоиска = Новый Структура;
		РеквизитыПоиска = Новый Структура; // Ключ - имя реквизита из РаспознанныйДокумент.РеквизитыДокумента, Значение - имя реквизита объекта метаданных
		РеквизитыПоиска.Вставить("НомерДоговора", "Номер");
		РеквизитыПоиска.Вставить("ДатаДоговора", "Дата");
		РеквизитыПоиска.Вставить("Договор", "Наименование");
		
		Для Каждого ИмяРеквизита Из РеквизитыПоиска Цикл
			Отбор = Новый Структура("ИмяРеквизита", ИмяРеквизита.Ключ);
			СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
			Если СтрокиПоиска.Количество() <> 0 Тогда
				ЗначениеПоиска = СтрокиПоиска[0].РаспознанныйТекст;
				//ЗначениеПоиска = СтрокиПоиска[0].Значение;
				//Если Не ЗначениеЗаполнено(ЗначениеПоиска) Тогда
				//	// Пустую дату и т.д. заменяем на пустую строку
				//	ЗначениеПоиска = "";
				//КонецЕсли;
			Иначе
				ЗначениеПоиска = Неопределено;
			КонецЕсли;
			
			ДанныеПоиска.Вставить(ИмяРеквизита.Значение, ЗначениеПоиска);
		КонецЦикла;
		
		Тип = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
		Кандидаты = РаспознаваниеДокументов.ПолучитьКандидатов(
			ДанныеПоиска,
			Тип,
			,
			ОтборМетаданных,
			ДокументОбъект.ИдентификаторРезультата
		);
		РаспознаваниеДокументовСлужебный.ДобавитьКандидатовВСписокВыбора(ДокументОбъект, ДокументСсылка, "Договор", Кандидаты);
		
		Отбор = Новый Структура("ИмяРеквизита", "Договор");
		СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
		Если СтрокиПоиска.Количество() <> 0 Тогда
			Реквизит = СтрокиПоиска[0];
			Если Реквизит.УверенностьНайденногоЗначения >= РаспознаваниеДокументовСлужебныйКлиентСервер.ГраницаПроблемныхЗначений()
				И НЕ РаспознаваниеДокументовКлиентСервер.ЭтоПримитивныйТип(ТипЗнч(Реквизит.Значение))
				И НЕ РаспознаваниеДокументовКлиентСервер.РаспознанныйТекстСодержитПустоеЗначениеПоля(Реквизит.ИмяРеквизита, Реквизит.РаспознанныйТекст) Тогда
				
				Реквизит.Значение = Реквизит.НайденноеЗначение;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// На основании Контрагента и Договора, можно найти Партнера	
	РаспознаваниеДокументовУП.ЗаполнитьПартнераКонтрагента(ДокументОбъект);
	
	РаспознаваниеДокументовУП.ЗаполнитьСоглашениеКонтрагента(ДокументОбъект);
	
	// На основании Контрагента, Организации, Валюты из Договора, данных о Банке и Счете можно найти банковский счет
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		РаспознаваниеДокументовСлужебный.ЗаполнитьБанковскийСчет(ДокументОбъект);
	КонецЕсли;
	
	Для Каждого ЯчейкаТаблицы Из ДокументОбъект.РеквизитыТабличныхЧастей Цикл
		
		Если ЯчейкаТаблицы.ИмяРеквизита = "СтавкаНДС" Тогда
			ЯчейкаТаблицы.Значение = РаспознаваниеДокументовУП.СтавкаНДСПоРаспознанномуТексту(ЯчейкаТаблицы.РаспознанныйТекст, ЯчейкаТаблицы.Значение);
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаДокумента = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	
	Если ПроверяемыеРеквизиты.Свойство("ИзмениласьСуммаВключаетНДС") Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			РаспознаваниеДокументовСлужебныйКлиентСервер.ПриИзмененииСтавкаНДС(СтрокаТаблицы, ПроверяемыеРеквизиты.СуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		
		ИтогоСуммаСкидкиРаспознано = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", 0);
		ИтогоСуммаСкидкиПоТаблице = ТаблицаДокумента.Итог("СуммаСкидки");
		
		ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена;
		Если ИтогоСуммаСкидкиПоТаблице <> 0 Тогда
			ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции;
		ИначеЕсли ИтогоСуммаСкидкиПоТаблице = 0 И ИтогоСуммаСкидкиРаспознано <> 0 Тогда
			ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом;
		КонецЕсли;
		
		РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ВидСкидки", ВидСкидки);
		
		Если ИтогоСуммаСкидкиРаспознано = 0 И ИтогоСуммаСкидкиПоТаблице <> 0 Тогда
			РаспознаваниеДокументовСлужебныйКлиентСервер.УстановитьЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", ИтогоСуммаСкидкиПоТаблице);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ДокументОбъект.ТипДокумента <> Перечисления.ТипыДокументовРаспознаваниеДокументов.УКД Тогда
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			Если СтрокаТаблицы.Количество = 0
				И СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПроверяемыеРеквизиты.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС) Тогда
				СтрокаТаблицы.Количество = 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		
		// Определение ставки НДС
		Если ПроверяемыеРеквизиты.ТекстНДС = "Без налога (НДС)" Тогда
			СтавкаНДС = Новый Структура("Числом, Ссылкой", 0, Справочники.СтавкиНДС.БезНДС);
		Иначе 
			СтавкаНДС = РаспознаваниеДокументовУП.ОпределитьСтавкуНДС(ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС);
		КонецЕсли;
		
		Если СтавкаНДС = Неопределено Тогда
			// Ставку НДС не удалось определить по полям итогов. Будем брать из номенклатуры
			ВсяНоменклатура = ТаблицаДокумента.ВыгрузитьКолонку("Номенклатура"); 
			
			Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
				 Поиск = "ПокупательОрганизация" ;   
			 Иначе
				 Поиск = "ПродавецОрганизация" ; 
			 КонецЕсли;
			 
			Отбор = Новый Структура("ИмяРеквизита", Поиск);
			СтрокиПоиска = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
			Если СтрокиПоиска.Количество() <> 0 Тогда  
				Организация = СтрокиПоиска[0].Значение;
			Иначе
				Организация = Справочники.Организации.ПустаяСсылка();
			КонецЕсли;
			
			НДСПоНоменклатуре = Новый Соответствие();
			
			НайденнаяСтрока = ДокументОбъект.РеквизитыДокумента.Найти("ДатаДокумента", "ИмяРеквизита");
			Если НайденнаяСтрока = Неопределено Тогда
				НДСПоНоменклатуре = Неопределено; 
				ДатаДокумента  = Неопределено; 
			Иначе
				ДатаДокумента = НайденнаяСтрока.Значение;
			КонецЕсли;
			
			Для Каждого Позиция Из ВсяНоменклатура Цикл 
				Если ЗначениеЗаполнено( Позиция) и ЗначениеЗаполнено(ДатаДокумента) Тогда
				СтавкаНДСн = УчетНДСУП.СтавкаНДСНоменклатуры(Позиция,Организация,ДатаДокумента); 
			 	НДСПоНоменклатуре.Вставить(Позиция, СтавкаНДСн);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Цена");
		ПоляПроверки.Добавить("Количество");
		ПоляПроверки.Добавить("Сумма");
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			// Восстановление нулей и потерянных запятых
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьЕдинственныйНоль(СтрокаТаблицы, ПоляПроверки, ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС);
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьПотеряннуюЗапятую(СтрокаТаблицы, ПоляПроверки, ДокументОбъект);
			
			// Пытаемся восстановить ставку НДС
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
				Если СтавкаНДС <> Неопределено Тогда
					СтрокаТаблицы.СтавкаНДС = СтавкаНДС.Ссылкой;
					СтавкаНДСЧислом = СтавкаНДС.Числом;
				ИначеЕсли НДСПоНоменклатуре <> Неопределено Тогда
					СтрокаТаблицы.СтавкаНДС = НДСПоНоменклатуре.Получить(СтрокаТаблицы.Номенклатура);
					СтавкаНДСЧислом = РаспознаваниеДокументовСлужебныйВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
					СтрокаТаблицы.СуммаНДС = РаспознаваниеДокументовСлужебныйКлиентСервер.РассчитатьСуммуНДС(
						СтрокаТаблицы.Сумма, ПроверяемыеРеквизиты.СуммаВключаетНДС, СтавкаНДСЧислом);
					СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПроверяемыеРеквизиты.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
	ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
		
		// Этот расчет происходит до того, как пользователь что-то увидит + в УПД, Торг12 и Счетах-фактурах
		// введены 4 колонки, которые можно легко сверить для каждой строки по формулам:
		// 1) Если (Сумма без НДС) + (Сумма НДС) = (Сумма с НДС), то проверить и при необходимости восстановить Ставку НДС.
		// 2) Если (Сумма без НДС) * (100 + Ставка НДС)/100 = (Сумма с НДС), то проверить и при необходимости восстановить Сумму НДС.
		
		// Если СуммаВключаетНДС = Истина, то такая проверка не пройдет и ее нужно исключить. Но для этих трех документов
		// при печате на бумаге Сумма без НДС не должна совпадать с колонкой Всего даже на нетиповых документах.
		Если Не ПроверяемыеРеквизиты.СуммаВключаетНДС Тогда
			Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
				СтавкаНДСЧислом = РаспознаваниеДокументовСлужебныйВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС);
				СложениемСовпало = (СтрокаТаблицы.Сумма + СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Всего);
				УмножениемСовпало = (Окр(СтрокаТаблицы.Сумма * (100 + СтавкаНДСЧислом)/100, 2) = СтрокаТаблицы.Всего);
				Если СложениемСовпало Или УмножениемСовпало Тогда
					Если Не СложениемСовпало Тогда
						СтрокаТаблицы.СуммаНДС = СтрокаТаблицы.Всего - СтрокаТаблицы.Сумма;
					ИначеЕсли Не УмножениемСовпало Тогда
						СтруктураСтавкиНДС = РаспознаваниеДокументовУП.ОпределитьСтавкуНДС(СтрокаТаблицы.Всего, СтрокаТаблицы.СуммаНДС);
						Если СтруктураСтавкиНДС <> Неопределено Тогда
							СтрокаТаблицы.СтавкаНДС = СтруктураСтавкиНДС.Ссылкой;
						КонецЕсли;
					КонецЕсли;
					Если Не ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) И СтавкаНДСЧислом = 0 Тогда
						СтрокаТаблицы.СтавкаНДС = Справочники.СтавкиНДС.БезНДС;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Всего");
		ПоляПроверки.Добавить("СуммаНДС");
		ПоляПроверки.Добавить("Сумма");
		
		Счетчик = 0;
		РаспознаваниеДокументовСлужебный.ВосстановлениеПо2Формулам(ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПоляПроверки);
		НеобходимоПроверять = Истина;
		Пока НеобходимоПроверять И Счетчик < 5 Цикл
			Счетчик = Счетчик + 1;
			НеобходимоПроверять = РаспознаваниеДокументовСлужебный.ВосстановлениеПо1ФормулеСУверенностью(ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПроверяемыеРеквизиты.ИтогоСумма);
			НеобходимоПроверять = НеобходимоПроверять Или РаспознаваниеДокументовСлужебный.ВосстановлениеПо2Формулам(ПроверяемыеРеквизиты.СуммаВключаетНДС, ТаблицаДокумента, ПроверяемыеРеквизиты.ИтогоВсего, ПроверяемыеРеквизиты.ИтогоСуммаНДС, ПоляПроверки);
		КонецЦикла;
		
		ПоляПроверки = Новый Массив;
		ПоляПроверки.Добавить("Цена");
		ПоляПроверки.Добавить("Количество");
		СтрокаСДатой = ДокументОбъект.РеквизитыДокумента.Найти("ДатаДокумента", "ИмяРеквизита");
		Для Каждого СтрокаТаблицы Из ТаблицаДокумента Цикл
			Если СтрокаСДатой <> Неопределено И ЗначениеЗаполнено(СтрокаТаблицы.Номенклатура) И НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтавкаНДС) Тогда
				Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий И ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПродавецОрганизация) Тогда
					СтрокаТаблицы.СтавкаНДС = УчетНДСУП.СтавкаНДСНоменклатуры(СтрокаТаблицы.Номенклатура,ПроверяемыеРеквизиты.ПродавецОрганизация,СтрокаСДатой.Значение);
				ИначеЕсли  ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий И ЗначениеЗаполнено(ПроверяемыеРеквизиты.ПокупательОрганизация) Тогда
					СтрокаТаблицы.СтавкаНДС = УчетНДСУП.СтавкаНДСНоменклатуры(СтрокаТаблицы.Номенклатура,ПроверяемыеРеквизиты.ПокупательОрганизация,СтрокаСДатой.Значение);
				КонецЕсли;
			КонецЕсли;
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьЕдинственныйНоль(СтрокаТаблицы, ПоляПроверки, ДокументОбъект, ПроверяемыеРеквизиты.СуммаВключаетНДС);
			РаспознаваниеДокументовСлужебный.НайтиИВосстановитьПотеряннуюЗапятую(СтрокаТаблицы, ПоляПроверки, ДокументОбъект);
		КонецЦикла;
	КонецЕсли;
	
	РаспознаваниеДокументовСлужебный.СохранитьТаблицуДокумента(ДокументОбъект, ТаблицаДокумента);
	
КонецПроцедуры

// При заполнении параметров выбора договора.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыВыбораДоговора - см. РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора
//
Процедура ПриЗаполненииПараметровВыбораДоговора(ДокументОбъект, ПараметрыВыбораДоговора) Экспорт
	
	ВидыДоговоров = Новый Массив;
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		ВидыДоговоров.Добавить(Перечисления.ТипыДоговоров.СПокупателем);
	Иначе
		ВидыДоговоров.Добавить(Перечисления.ТипыДоговоров.СПоставщиком);
	КонецЕсли;
	
	ПараметрыВыбораДоговора.Вставить("ВидыДоговоров", ВидыДоговоров);

	Отбор = Новый Структура("ИмяРеквизита", "Партнер");
	НайденныеСтроки = ДокументОбъект.РеквизитыДокумента.НайтиСтроки(Отбор);
	Если НайденныеСтроки.Количество() <> 0 Тогда
		ПараметрыВыбораДоговора.Вставить("Партнер", НайденныеСтроки[0].Значение);
	КонецЕсли;
		
КонецПроцедуры

// При заполнении договора контрагента.
// 
// Параметры:
//  ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыДоговора - см. РаспознаваниеДокументовСлужебный.ПараметрыВыбораДоговора
//
Процедура ПриЗаполненииДоговораКонтрагента(ДоговорКонтрагента, ДокументОбъект, ПараметрыДоговора) Экспорт
	
	ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
	
	Если Не ЗначениеЗаполнено(ПараметрыДоговора.Организация) Или Не ЗначениеЗаполнено(ПараметрыДоговора.Контрагент) Тогда 
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметрыПоиска = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрыДоговора.НомерДоговора) Тогда
		ДополнительныеПараметрыПоиска.Вставить("Номер", Новый Структура("ЗначениеОтбора", ПараметрыДоговора.НомерДоговора));
	КонецЕсли;
	Если ЗначениеЗаполнено(ПараметрыДоговора.ДатаДоговора) Тогда
		ДополнительныеПараметрыПоиска.Вставить("Дата", Новый Структура("ЗначениеОтбора", ПараметрыДоговора.ДатаДоговора));
	КонецЕсли;
	
	Договор = РаспознаваниеДокументовУП.ДоговорКонтрагента(ПараметрыДоговора.Контрагент, 
		ПараметрыДоговора.Организация, ПараметрыДоговора.ВидыДоговоров, ДополнительныеПараметрыПоиска);
	
	Если Не ЗначениеЗаполнено(Договор) Тогда
		Договор = РаспознаваниеДокументовУП.ДоговорКонтрагента(
			ПараметрыДоговора.Контрагент, ПараметрыДоговора.Организация, ПараметрыДоговора.ВидыДоговоров);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Договор) Тогда
		ДоговорКонтрагента = Договор;
	КонецЕсли;

КонецПроцедуры

// При заполнении банковского счета.
// 
// Параметры:
//  Счет - СправочникСсылка.БанковскиеСчета
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыСчета - Структура:
//	*Банк - СправочникСсылка.Банки
//	*БанкНаименование - Строка
//	*БанкСчет - СправочникСсылка.БанковскиеСчета
//	*Договор - СправочникСсылка.ДоговорыКонтрагентов
//
Процедура ПриЗаполненииБанковскогоСчета(Счет, ДокументОбъект, ПараметрыСчета) Экспорт
	
	Если ЗначениеЗаполнено(Счет) Тогда
		// Проверим соответствие счета и владельца, т.к. нечеткий поиск этого пока не делает
		Если ПараметрыСчета.Владелец = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "Владелец") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Тут можно использовать поиск по данным из ПараметрыСчета (реквизиты "Банк", "БанкНаименование", "БанкСчет"), но
	// для демонстрации заполняем основным банковским счетом владельца
	
	ВалютаОплаты = РаспознаваниеДокументовСлужебный.ВалютаОплаты(ПараметрыСчета.Договор);
	РаспознаваниеДокументовУП.УстановитьБанковскийСчет(Счет, ПараметрыСчета.Владелец, ВалютаОплаты, Истина);
	
КонецПроцедуры

// При заполнении валюты оплаты.
// 
// Параметры:
//  ДоговорКонтрагента - СправочникСсылка.ДоговорыКонтрагентов
//  ВалютаОплаты - СправочникСсылка.Валюты
//
Процедура ПриЗаполненииВалютыОплаты(ДоговорКонтрагента, ВалютаОплаты) Экспорт
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДоговорКонтрагента, "ВалютаВзаиморасчетов, ОплатаВВалюте");
		
		Если РеквизитыДоговора.ОплатаВВалюте Тогда
			ВалютаОплаты = ВалютаРегламентированногоУчета;
		Иначе
			ВалютаОплаты = РеквизитыДоговора.ВалютаВзаиморасчетов;
		КонецЕсли;
	Иначе
		ВалютаОплаты = ВалютаРегламентированногоУчета;
	КонецЕсли;
	
КонецПроцедуры

// Определяет числовое значение ставки НДС на основе переданного типа ставки.
//
// Параметры:
//   СтавкаНДСЧислом - Число - числовое представление ставки НДС, результат определения
//   СтавкаНДС - ПеречислениеСсылка.СтавкиНДС - тип ставки НДС для определения числового значения
//   ПрименяютсяСтавки4и2 - Булево
//
Процедура ПриОпределенииСтавкиНДС(СтавкаНДСЧислом, СтавкаНДС, ПрименяютсяСтавки4и2 = Ложь) Экспорт
		
	Если СтавкаНДС = Перечисления.СтавкиНДС.НДС20 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС20_120 Тогда
		СтавкаНДСЧислом = 20;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС10 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС10_110 Тогда
		СтавкаНДСЧислом = ?(ПрименяютсяСтавки4и2, 2, 10);
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС18 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС18_118 Тогда
		СтавкаНДСЧислом = ?(ПрименяютсяСтавки4и2, 4, 18);
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС5 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС5_105 Тогда
		СтавкаНДСЧислом = 5;
	ИначеЕсли СтавкаНДС = Перечисления.СтавкиНДС.НДС7 Или СтавкаНДС = Перечисления.СтавкиНДС.НДС7_107 Тогда
		СтавкаНДСЧислом = 7;
	Иначе
		СтавкаНДСЧислом = 0;
	КонецЕсли;

КонецПроцедуры

// Заполняет данные для выбора ставок НДС
//
// Параметры:
//  ДанныеВыбора - Массив 
//  СтандартнаяОбработка - Булево 
//
Процедура ПриПолученииДанныхВыбораСтавкиНДС(ДанныеВыбора, СтандартнаяОбработка) Экспорт

	ПараметрыВыбора = Новый Структура;
	ПараметрыВыбора.Вставить("Отбор", Новый Структура);
	ПараметрыВыбора.Вставить("ПолнотекстовыйПоиск");
	ПараметрыВыбора.Вставить("СпособПоискаСтроки");
	ПараметрыВыбора.Вставить("СтрокаПоиска");
	ПараметрыВыбора.Вставить("БезОтбораПоДате", Истина);
	
	УчетНДСУП.ПолучитьДанныеВыбораСтавкиНДС(ДанныеВыбора, ПараметрыВыбора, СтандартнаяОбработка);

КонецПроцедуры // ПриПолученииДанныхВыбораСтавкиНДС()


// При определении проверяемых реквизитов.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПроверяемыеРеквизиты - Произвольный
//
Процедура ПриОпределенииПроверяемыхРеквизитов(ДокументОбъект, ПроверяемыеРеквизиты) Экспорт
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		Или ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12
		Или ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		Или ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг
		Или ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.НеопределенныйДокумент
		И (ДокументОбъект.ВариантОбработки = Перечисления.ВариантыОбработкиЗаданияРаспознавания.ПоступлениеТоваров
		Или ДокументОбъект.ВариантОбработки = Перечисления.ВариантыОбработкиЗаданияРаспознавания.РеализацияТоваров) Тогда
			   
		Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
			ПроверяемыеРеквизиты.Добавить("СоглашениеСКлиентом");
			ПроверяемыеРеквизиты.Добавить("Договор");
			ПроверяемыеРеквизиты.Добавить("Подразделение");
		КонецЕсли;
			   			   
	ИначеЕсли ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату
		Или ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.НеопределенныйДокумент
		И (ДокументОбъект.ВариантОбработки = Перечисления.ВариантыОбработкиЗаданияРаспознавания.СчетНаОплатуКлиента
		Или ДокументОбъект.ВариантОбработки = Перечисления.ВариантыОбработкиЗаданияРаспознавания.СчетНаОплатуПоставщика) Тогда

		ИндексЗначения = ПроверяемыеРеквизиты.Найти("БанковскийСчетОрганизации");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;   
							
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("БанковскийСчетКонтрагента");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;   
				
		Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
			ПроверяемыеРеквизиты.Добавить("СоглашениеСКлиентом");
			ПроверяемыеРеквизиты.Добавить("Договор"); 
			ПроверяемыеРеквизиты.Добавить("Склад");
		КонецЕсли;
	КонецЕсли;
			
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Договор");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	ИначеЕсли ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Договор");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСКлиентами") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("СоглашениеСКлиентом");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	ИначеЕсли ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьСоглашенияСПоставщиками") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("СоглашениеСПоставщиком");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;

	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("СоглашениеСПоставщиком");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	ИначеЕсли ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("СоглашениеСКлиентом");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Подразделение");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;  
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Склад");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
	 Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда
		ИндексЗначения = ПроверяемыеРеквизиты.Найти("Партнер");
		Если ИндексЗначения <> Неопределено Тогда
			ПроверяемыеРеквизиты.Удалить(ИндексЗначения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// При обработке проверки заполнения реквизитов.
// 
// Параметры:
//  ДокументОбъект - ДокументОбъект.РаспознанныйДокумент
//  ТаблицаПроблем - ТаблицаЗначений 
//
Процедура ПриОбработкеПроверкиЗаполненияРеквизитов(ДокументОбъект, ТаблицаПроблем) Экспорт
	
	// Заполнение для шапки
	Для Каждого Реквизит Из ТаблицаПроблем Цикл
		
		ИмяРеквизита = Реквизит.ИмяРеквизита; 
		Если ИмяРеквизита = "Партнер" Тогда
			Реквизит.ТекстПодсказки = РаспознаваниеДокументовУП.ПодсказкаНеНайденПартнерКонтрагента();
		ИначеЕсли Реквизит.ИмяРеквизита = "Подразделение" Тогда
			Реквизит.ТекстПодсказки = РаспознаваниеДокументовСлужебныйКлиентСервер.ПодсказкаТребуетсяВыбрать();
		ИначеЕсли Реквизит.ИмяРеквизита = "СоглашениеСКлиентом" Или Реквизит.ИмяРеквизита = "СоглашениеСПоставщиком" Тогда
			Реквизит.ТекстПодсказки = РаспознаваниеДокументовУП.ПодсказкаНеНайденоСоглашениеСКонтрагентом();
		КонецЕсли;
			
    КонецЦикла;	
	
КонецПроцедуры

Процедура ПриЗаполненииНовогоЭлементаСправочника(СправочникОбъект, ДанныеДляЗаполнения) Экспорт
	
	СправочникОбъект.Заполнить(ДанныеДляЗаполнения);
	
	Если ТипЗнч(СправочникОбъект) = Тип("СправочникОбъект.ДоговорыКонтрагентов") Тогда
		УчетНДСУП.ЗаполнитьСтавкуНДСДляПлатежей(СправочникОбъект.СтавкаНДС, СправочникОбъект.НалогообложениеНДС,
			СправочникОбъект.Организация, СправочникОбъект.ДатаОкончанияДействия);
	КонецЕсли;
	
КонецПроцедуры

// При создании документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//  РаспознанныйДокумент - ДокументСсылка.РаспознанныйДокумент
//  ПараметрыЗаполнения - см. РаспознаваниеДокументовСлужебныйВызовСервера.ПараметрыЗаполненияТиповойНаОсновеСчетНаОплату
//
Процедура ПриСозданииДокументаНаОснованииРаспознанного(ДокументОбъект, РаспознанныйДокумент, ПараметрыЗаполнения) Экспорт

	Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ЗаказПоставщику") 
		И ПараметрыЗаполнения.Свойство("ОснованиеСчет") Тогда
		
		Если ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.Согласован Тогда
			ДокументОбъект.Статус = Перечисления.СтатусыЗаказовПоставщикам.Подтвержден;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.ДатаОтгрузки) Тогда
			ДокументОбъект.ДатаОтгрузки = ДокументОбъект.Дата;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.ДатаПоступления) Тогда
			ДокументОбъект.ДатаПоступления = ДокументОбъект.Дата;
		КонецЕсли;
		
		Если ДокументОбъект.Товары.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		СекундВСутках = 86400;
		
		Если Не ДокументОбъект.ПоступлениеОднойДатой Тогда
			Если ЗначениеЗаполнено(ДокументОбъект.ЖелаемаяДатаПоступления) 
				И ДокументОбъект.ЖелаемаяДатаПоступления - ДокументОбъект.ДлительностьДоставки * СекундВСутках >= ДокументОбъект.Дата Тогда
				ДатаОтгрузки = ДокументОбъект.ЖелаемаяДатаПоступления - ДокументОбъект.ДлительностьДоставки * СекундВСутках;
			Иначе
				ДатаОтгрузки = ТекущаяДатаСеанса();
			КонецЕсли;
		Иначе
			Если НЕ ЗначениеЗаполнено(ДокументОбъект.ДатаПоступления) Тогда
				Если ЗначениеЗаполнено(ДокументОбъект.ДатаОтгрузки) Тогда
					ДокументОбъект.ДатаПоступления = ДокументОбъект.ДатаОтгрузки + ДокументОбъект.ДлительностьДоставки * СекундВСутках;
				ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.ЖелаемаяДатаПоступления) И ДокументОбъект.ЖелаемаяДатаПоступления 
						- ДокументОбъект.ДлительностьДоставки * СекундВСутках >= ДокументОбъект.Дата Тогда
					ДокументОбъект.ДатаПоступления = ДокументОбъект.ЖелаемаяДатаПоступления;
					ДокументОбъект.ДатаОтгрузки = ДокументОбъект.ДатаПоступления - ДокументОбъект.ДлительностьДоставки * СекундВСутках;
				Иначе
					ДокументОбъект.ДатаОтгрузки = ТекущаяДатаСеанса();
					ДокументОбъект.ДатаПоступления = ДокументОбъект.ДатаОтгрузки + ДокументОбъект.ДлительностьДоставки * СекундВСутках;
				КонецЕсли;
			КонецЕсли;
			
			ДатаОтгрузки = ДокументОбъект.ДатаОтгрузки;
		КонецЕсли;
		
		Для Каждого СтрокаТЧ Из ДокументОбъект.Товары Цикл
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ДатаОтгрузки) Тогда
				СтрокаТЧ.ДатаОтгрузки = ДатаОтгрузки;
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ДатаПоступления) Тогда
				СтрокаТЧ.ДатаПоступления = ДатаОтгрузки + ДокументОбъект.ДлительностьДоставки * СекундВСутках;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	ЕстьПустоеСодержание = Ложь;	
	
	Если ПараметрыЗаполнения.Свойство("УслугиРаботы") Тогда 
		Для Каждого СтрокаТаблицы Из ПараметрыЗаполнения.УслугиРаботы Цикл
			Если Не ЗначениеЗаполнено(СтрокаТаблицы.Содержание) Тогда
				ЕстьПустоеСодержание = Истина;
					
				ЭтоСообщение = Новый СообщениеПользователю;
				ЭтоСообщение.Текст = НСтр("ru = 'Не заполнена колонка ""Содержание"" в строке ';
											|en = 'The ""Content"" column is not filled in line '") + СтрокаТаблицы.ПорядокСтроки;
				ЭтоСообщение.Поле = СтрШаблон("ТаблицаДокумента[%1].Содержание", Формат(СтрокаТаблицы.ПорядокСтроки - 1, "ЧГ="));
				ЭтоСообщение.Сообщить();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЕстьПустоеСодержание Тогда
		ВызватьИсключение НСтр("ru = 'Необходимо заполнить содержание во всех строках';
								|en = 'Fill content in all lines'");
	КонецЕсли;

КонецПроцедуры

// При проведении документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//
Процедура ПриПроведенииДокументаНаОснованииРаспознанного(ДокументОбъект) Экспорт

КонецПроцедуры

// После создания документа на основании распознанного.
// 
// Параметры:
//  ДокументОбъект - ОпределяемыйТип.ДокументыСоздаваемыеИзРаспознанногоДокументаБРДОбъект
//
Процедура ПослеСозданияДокументаНаОснованииРаспознанного(ДокументОбъект) Экспорт
		
КонецПроцедуры

// При создании счет фактуры.
// 
// Параметры:
//  ПараметрыСоздания - Структура:
//		*ТипДокументаСтрокой - Строка,
//		*Основание - ДокументСсылка - Документ-основание
//		*ПараметрыЗаполнения - Структура:
//  		**Контрагент - СправочникСсылка.Контрагенты
//  		**Организация - СправочникСсылка.Организации
//  		**ИмяРеквизитаНомерДокумента - Строка
//  		**ИмяРеквизитаДатаДокумента  - Строка 
//
//  СчетФактураСсылка - ДокументСсылка.СчетФактураВыданный, ДокументСсылка.СчетФактураПолученный- Счет фактура ссылка
//
Процедура ПриСозданииСчетФактуры(ПараметрыСоздания, СчетФактураСсылка = Неопределено) Экспорт
	
	Если Не ПараметрыСоздания.ДокументОснование.Проведен Тогда
		Предупреждение = СтрШаблон(НСтр("ru = 'Документ-основание %1 не проведен. 
		|Счет-фактуру можно провести только на основании проведенных документов.';
		|en = 'The %1 base document is not posted. 
		|You can post a tax invoice only on the basis of posted documents.'"), ПараметрыСоздания.ДокументОснование);
		ВызватьИсключение Предупреждение;
	КонецЕсли;

	ТипДокументаСтрокой = ПараметрыСоздания.ТипДокументаСтрокой;
	ПараметрыЗаполнения = ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыСоздания.ПараметрыЗаполнения);
	ПараметрыЗаполнения.Вставить("ДокументОснование", ПараметрыСоздания.ДокументОснование);
	
	Если ПараметрыЗаполнения.Свойство("НомерДокумента")Тогда
		ПараметрыЗаполнения.Вставить("Номер",ПараметрыЗаполнения.НомерДокумента);
	Иначе
		ПараметрыЗаполнения.Вставить("Номер",ПараметрыЗаполнения.Номер);
	КонецЕсли;
	
	Если ТипДокументаСтрокой = "СчетФактураПолученный"
		Или ТипДокументаСтрокой = "ПриобретениеТоваровУслуг"
		Или ТипДокументаСтрокой = "ПриобретениеУслугПрочихАктивов" Тогда
		СоздаваемыйДокумент = Документы.СчетФактураПолученный.СоздатьДокумент();
	Иначе
		СоздаваемыйДокумент = Документы.СчетФактураВыданный.СоздатьДокумент();
	КонецЕсли;
	
	СоздаваемыйДокумент.Заполнить(ПараметрыЗаполнения); 
	ЗаполнитьЗначенияСвойств(СоздаваемыйДокумент, ПараметрыЗаполнения);
	
	Если Не СоздаваемыйДокумент.ПроверитьЗаполнение() Тогда
		ВызватьИсключение НСтр("ru = 'При проверке заполнения обнаружены ошибки';
								|en = 'Errors were found while checking the filling'");
	КонецЕсли;
	
	СоздаваемыйДокумент.Записать(РежимЗаписиДокумента.Проведение);

	СчетФактураСсылка = СоздаваемыйДокумент.Ссылка;
	
КонецПроцедуры

// При определении параметров создания номенклатуры.
// 
// Параметры:
//  Параметры - ТаблицаЗначений
//
Процедура ПриОпределенииПараметровСозданияНоменклатуры(Параметры) Экспорт
	
	МетаданныеНоменклатура = Метаданные.Справочники.Номенклатура;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.СтандартныеРеквизиты.Наименование);
	Параметр.ОписаниеРеквизита.Синоним = НСтр("ru = 'Наименование';
												|en = 'Description'");
	Параметр.ОписаниеРеквизита.ОтображатьСвязанныеЭлементы = Ложь;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.Артикул);
	Параметр.ОписаниеРеквизита.ОтображатьСвязанныеЭлементы = Ложь;
	
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.СтандартныеРеквизиты.Родитель);
	Параметр.ОписаниеРеквизита.Синоним = НСтр("ru = 'Группа списка';
												|en = 'List group'");
	Параметр.ОписаниеЭлемента = РаспознаваниеДокументов.ОписаниеЭлемента();
	Параметр.ОписаниеЭлемента.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
		
	Параметр = Параметры.Добавить();
	Параметр.ОписаниеРеквизита = РаспознаваниеДокументов.ОписаниеРеквизита(
		МетаданныеНоменклатура.Реквизиты.ВидНоменклатуры);
	Параметр.ОписаниеРеквизита.ПроверкаЗаполнения = ПроверкаЗаполнения.НеПроверять;
		
КонецПроцедуры

// Обработка создания номенклатуры.
// 
// Параметры:
//  РезультатСсылка - СправочникСсылка.Номенклатура
//  ДанныеЗаполнения - см. РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента
//
Процедура ОбработкаСозданияНоменклатуры(РезультатСсылка, ДанныеЗаполнения) Экспорт
	
	НоваяНоменклатура = Справочники.Номенклатура.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НоваяНоменклатура, ДанныеЗаполнения);

	ЗаполнитьНоменклатуруПоВидуНоменклатуры(НоваяНоменклатура);
	
	НоваяНоменклатура.Заполнить(ДанныеЗаполнения);
	
	Если НоваяНоменклатура.ПроверитьЗаполнение() Тогда
		НоваяНоменклатура.Записать();
		РезультатСсылка = НоваяНоменклатура.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

// При заполнении параметров создания нового элемента.
// 
// Параметры:
//  СвязанныеКолонки - см. РаспознаваниеДокументовСлужебный.ПараметрыСозданияНовогоЭлемента
//  ИмяЭлемента - Строка
//  ТипДокумента - ПеречислениеСсылка.ТипыДокументовРаспознаваниеДокументов
//  Направление - ПеречислениеСсылка.НаправленияРаспознанногоДокумента
//
Процедура ПриЗаполненииПараметровСозданияНовогоЭлемента(СвязанныеКолонки, ИмяЭлемента, ТипДокумента, Направление) Экспорт
	
	Если ИмяЭлемента = "Номенклатура" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Номенклатура", Истина, Истина, "НаименованиеПолное"));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "Артикул", "Артикул", Истина, Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ВыборГруппИЭлементов", "Родитель", "Родитель", "Входит в группу", Ложь, ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"), Ложь, ГруппыИЭлементы.Группы));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "СтавкаНДС", "СтавкаНДС", "% НДС", Ложь, ПредопределенноеЗначение("Справочник.СтавкиНДС.ПустаяСсылка"), Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЕдиницаИзмерения", "ЕдиницаИзмерения", "Единица измерения", Ложь, ПредопределенноеЗначение("Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"ВидНоменклатуры", "ВидНоменклатуры", "Вид номенклатуры", Ложь,
			ПредопределенноеЗначение("Справочник.ВидыНоменклатуры.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", 
			"ТипНоменклатуры", "ТипНоменклатуры", "Тип номенклатуры", Ложь, 
			ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура(
			"Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", 
			"ИспользованиеХарактеристик", "ИспользованиеХарактеристик", "Использование характеристик", Ложь, 
			ПредопределенноеЗначение("Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "ЕдиницаИзмерения" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Наименование", "ЕдиницаИзмерения", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "КодЕдиницыИзмерения", "Код", Истина, Истина));
	ИначеЕсли ИмяЭлемента = "СтранаПроисхождения" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Наименование", "СтранаПроисхождения", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "КодСтраныПроисхождения", "Код", Истина, Истина));
	ИначеЕсли ИмяЭлемента = "НомерГТД" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "Код", "НомерГТД", "Номер", Истина, Истина));
		
	ИначеЕсли ИмяЭлемента = "БанковскийСчетКонтрагента" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ПодходящиеОбязательноеРавенство",
			"Владелец", "Продавец", "Контрагент", Истина, ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка"), Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "НомерСчета", "БанковскийСчетКонтрагента", "Номер счета", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Банк", "Банк", "Банк", Истина, ПредопределенноеЗначение("Справочник.КлассификаторБанков.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВалютаДенежныхСредств", "Валюта", Ложь, ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета(), Истина));
	ИначеЕсли ИмяЭлемента = "БанковскийСчетОрганизации" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный, ПодходящиеОбязательноеРавенство",
			"Владелец", "ПродавецОрганизация", "Контрагент", Истина, ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка"), Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный", "НомерСчета", "БанковскийСчетОрганизации", "Номер счета", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Банк", "Банк", "Банк", Истина, ПредопределенноеЗначение("Справочник.КлассификаторБанков.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ВалютаДенежныхСредств", "Валюта", Ложь, ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета(), Истина));
		
	ИначеЕсли ИмяЭлемента = "Продавец" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Продавец", "Наименование", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППродавца", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППродавца", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь,
			ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Партнер",
			"Партнер", Ложь, ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"), Ложь));
	ИначеЕсли ИмяЭлемента = "ПродавецОрганизация" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "ПродавецОрганизация", Истина, Истина, "НаименованиеПолное,НаименованиеСокращенное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППродавца", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПродавца", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППродавца", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Покупатель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Покупатель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППокупателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППокупателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", 
			"ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь,
			ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "Партнер",
			"Партнер", Ложь, ПредопределенноеЗначение("Справочник.Партнеры.ПустаяСсылка"), Ложь));
	ИначеЕсли ИмяЭлемента = "ПокупательОрганизация" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "ПокупательОрганизация", Истина, Истина, "НаименованиеПолное,НаименованиеСокращенное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКПППокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКПППокупателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННПокупателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КПППокупателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Грузоотправитель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Грузоотправитель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКППГрузоотправителя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКППГрузоотправителя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННГрузоотправителя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КППГрузоотправителя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Грузополучатель" Тогда
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный, РеквизитыЗаполнения", "Наименование", "Грузополучатель", Истина, Истина, "НаименованиеПолное"));
		Если ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
			ИЛИ ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННКППГрузополучателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "ИННКППГрузополучателя", Ложь, Ложь));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "ИНН", "ИННГрузополучателя", Истина, Ложь));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, ИскатьПодходящие, Обязательный", "КПП", "КППГрузополучателя", Ложь, Ложь));
		КонецЕсли;
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный", "ЮридическоеФизическоеЛицо", "Вид контрагента", Ложь, ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ПустаяСсылка"), Истина));
	ИначеЕсли ИмяЭлемента = "Договор" Тогда
		
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"ХозяйственнаяОперация", "Операция", Истина, Перечисления.ХозяйственныеОперации.ПустаяСсылка(), Истина));
		
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"ТипДоговора", "Тип договора", Истина, Перечисления.ТипыДоговоров.ПустаяСсылка(), Истина));
		
		Если Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Контрагент", "Покупатель", "Контрагент", Истина, Справочники.Контрагенты.ПустаяСсылка(), Истина));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Организация", "ПродавецОрганизация", "Организация", Истина, Справочники.Организации.ПустаяСсылка(), Истина));
		Иначе
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Контрагент", "Продавец", "Контрагент", Истина, Справочники.Контрагенты.ПустаяСсылка(), Истина));
			СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
				"Организация", "ПокупательОрганизация", "Организация", Истина, Справочники.Организации.ПустаяСсылка(), Истина));
		КонецЕсли;
	
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный",
			"Наименование", "Договор", "Наименование", Истина, Истина));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, Обязательный",
			"Номер", "НомерДоговора", "Номер договора", Истина, Ложь));
		СвязанныеКолонки.Добавить(Новый Структура("Реквизит, ИмяРеквизита, Синоним, ИскатьПодходящие, ПустаяСсылка, Обязательный",
			"Дата", "ДатаДоговора", "Дата договора", Истина, '00010101', Ложь));
	КонецЕсли;
	
КонецПроцедуры

// При создании формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура ПриСозданииФормыРаспознаванияНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка) Экспорт		
	
	Объект = ЭтотОбъект.Объект;
	Если Объект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату Тогда
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[29].Значение) Тогда
			// Соглашение
			Объект.РеквизитыДокумента[29].Значение = Справочники.СоглашенияСКлиентами.ПустаяСсылка()
		КонецЕсли;	
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[30].Значение) Тогда
			// Соглашение
			Объект.РеквизитыДокумента[30].Значение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[28].Значение) Тогда
			// Партнер
			Объект.РеквизитыДокумента[28].Значение = Справочники.Партнеры.ПустаяСсылка()
		КонецЕсли; 
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[2].Значение) Тогда
			// КонтрагентКлиент
			Объект.РеквизитыДокумента[2].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[3].Значение) Тогда
			// КонтрагентПокупатель
			Объект.РеквизитыДокумента[3].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[31].Значение) Тогда
			// Склад
			Объект.РеквизитыДокумента[31].Значение = Справочники.Склады.ПустаяСсылка();
		КонецЕсли; 
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[21].Значение) Тогда
			// БанкСчет контр
			Объект.РеквизитыДокумента[21].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[22].Значение) Тогда
			// БанкСчет орг
			Объект.РеквизитыДокумента[22].Значение = Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
		
		ЭтотОбъект.СкладЗаполнен = ЗначениеЗаполнено(Объект.РеквизитыДокумента[31].Значение);
		ЭтотОбъект.СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Объект.РеквизитыДокумента[31].Значение);
		
	Иначе
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[16].Значение) Тогда
			// Соглашение
			Объект.РеквизитыДокумента[16].Значение = Справочники.СоглашенияСКлиентами.ПустаяСсылка()
		КонецЕсли;	
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[17].Значение) Тогда
			// Соглашение
			Объект.РеквизитыДокумента[17].Значение = Справочники.СоглашенияСПоставщиками.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[15].Значение) Тогда
			// Партнер
			Объект.РеквизитыДокумента[15].Значение = Справочники.Партнеры.ПустаяСсылка()
		КонецЕсли; 
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[2].Значение) Тогда
			// КонтрагентКлиент
			Объект.РеквизитыДокумента[2].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[3].Значение) Тогда
			// КонтрагентПокупатель
			Объект.РеквизитыДокумента[3].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;     
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[4].Значение) Тогда
			// Грузополучатель
			Объект.РеквизитыДокумента[4].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли;
	    Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[5].Значение) Тогда
			// Грузоотправитель
			Объект.РеквизитыДокумента[5].Значение = Справочники.Контрагенты.ПустаяСсылка()
		КонецЕсли; 
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[18].Значение) Тогда
			// Подразделение
			Объект.РеквизитыДокумента[18].Значение = Справочники.СтруктураПредприятия.ПустаяСсылка();
		КонецЕсли;
		Если Не УправлениеДоступом.ЧтениеРазрешено(Объект.РеквизитыДокумента[7].Значение) Тогда
			// Склад
			Объект.РеквизитыДокумента[7].Значение = Справочники.Склады.ПустаяСсылка();
		КонецЕсли;
		
		ЭтотОбъект.СкладЗаполнен = ЗначениеЗаполнено(Объект.РеквизитыДокумента[7].Значение); 
		ЭтотОбъект.СкладГруппа = Справочники.Склады.ЭтоГруппаИСкладыИспользуютсяВТЧДокументовПродажи(Объект.РеквизитыДокумента[7].Значение); 
		
		Если Объект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг Тогда
			ЭтотОбъект.Элементы.СтранаПроисхождения.Видимость = Ложь;
			ЭтотОбъект.Элементы.НомерГТД.Видимость = Ложь;
			ЭтотОбъект.Элементы.ЕдиницаИзмерения.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли; 
	
КонецПроцедуры

// Перед записью формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  Отказ - Булево
//  ТекущийОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыЗаписи - Структура
//
Процедура ПередЗаписьюФормыРаспознаванияНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи) Экспорт	
	
КонецПроцедуры

// После записи формы распознавания на сервере.
// 
// Параметры:
//  ЭтотОбъект - Форма
//  ТекущийОбъект - ДокументОбъект.РаспознанныйДокумент
//  ПараметрыЗаписи - Структура
//
Процедура ПослеЗаписиФормыРаспознаванияНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи) Экспорт
	
КонецПроцедуры

// Заполнить добавленные колонки таблиц.
// 
// Параметры:
//  Объект - ДанныеФормыСтруктура
//  Строки - ТаблицаЗначений
//  Очистить - Булево
//
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(Объект, Строки, Очистить = Ложь) Экспорт
	
КонецПроцедуры

// Создание контрагента в информационной базе по реквизитам.
//
// Параметры:
//   РеквизитыКонтрагента - Структура - реквизиты необходимые для создания контрагента.
//    * ИНН - Строка - ИНН контрагента.
//    * КПП - Строка - КПП контрагента.
//    * Наименование - Строка - наименование контрагента.
//   Контрагент - СправочникСсылка - ссылка на созданного контрагента.
//   Отказ - Булево - признак ошибки.
//
Процедура СоздатьКонтрагентаПоРеквизитам(Знач РеквизитыКонтрагента, Контрагент, Отказ = Ложь) Экспорт
	Контрагент = ОбменСКонтрагентамиУТ.СоздатьКонтрагентаВБД(РеквизитыКонтрагента);
КонецПроцедуры

#Область УстаревшиеПроцедурыИФункции

Процедура ПриИзмененииКолонкиНаСервере(Объект, СтрокаТаблицы, ИмяРеквизита, НомерСтрокиТаблицы, ВыбранноеЗначение) Экспорт
		
КонецПроцедуры

Процедура СтавкиНДСПустаяСсылка(Тип) Экспорт
	
КонецПроцедуры

Процедура СоздатьДокументыКомплектаВызовСервера(ПараметрыСоздания, Результат) Экспорт
	
КонецПроцедуры

Процедура НужноПрикрепитьДополнительныйСкан(ТипКомплекта, ТипРаспознанного, ТипСозданного, Результат) Экспорт
	
КонецПроцедуры

Процедура ОтобразитьРезультатПроверкиКонтрагентовВФорме(Форма, РезультатПроверкиСуществования = "") Экспорт
	
КонецПроцедуры

#КонецОбласти

// Получение количества товаров и услуг документа-основания корректировки
//
// Параметры:
//   Документ - ДокументСсылка - ссылка на документ-основание корректировки.
//   ТоварыИУслугиДокумента - Массив - возвращаеммый список товаров и услуг.
//
Процедура СтрокиДокументаОснованияКорректировки(Знач Документ, ТоварыИУслугиДокумента) Экспорт
		
КонецПроцедуры

Процедура СвойстваСтрокиДокументаОснованияКорректировки(Знач Документ, Знач НомерСтроки, ДанныеСтроки) Экспорт
		
КонецПроцедуры

// Функция производит поиск счета-фактуры полученного с видом "на поступление" или "корректировочный" 
// по документу-основанию.
//
// Параметры:
//  ДокументОснование	- ДокументСсылка.* - Ссылка на документ, для которого надо найти счет-фактуру.
//  ИсключаемыйСФ		- ДокументСсылка.* - Ссылка на счет-фактуру, исключаемый при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//  ДокументСсылка.СчетФактураПолученный - Если нашли, то возвращаем ссылку, не нашли - Неопределено.
//
Функция НайтиПодчиненныйСчетФактуруПолученный(
	ДокументОснование, ИсключаемыйСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт

	Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.СчетФактураПолученный") Тогда
		// Если в качестве документа-основания указана счет-фактура, то искать нет необходимости.
		
		Если ДокументОснование = ИсключаемыйСФ Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		РеквизитыСчетаФактуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			ДокументОснование, "ПометкаУдаления, ВидСчетаФактуры");
		Если РеквизитыСчетаФактуры.ПометкаУдаления <> ПометкаУдаления
		 Или РеквизитыСчетаФактуры.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.НаПоступление
			И РеквизитыСчетаФактуры.ВидСчетаФактуры <> Перечисления.ВидСчетаФактурыПолученного.Корректировочный Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат ДокументОснование;
		
	КонецЕсли;
	
	НайденныеСФ = НайтиПодчиненныеСчетаФактурыПолученные(ДокументОснование, ИсключаемыйСФ, ПометкаУдаления, СтруктураОтбора);

	Возврат НайденныеСФ[ДокументОснование];

КонецФункции

// Функция производит поиск счетов-фактур полученных с видом "на поступление" или "корректировочный" 
// по документам-основаниям.
//
// Параметры:
//  СписокДокументовОснований - ДокументСсылка.*, Массив, СписокЗначений - Ссылки на документы, для которых надо найти счета-фактуры.
//  ИсключаемыеСФ		- ДокументСсылка.СчетФактураПолученный, Массив, СписокЗначений - Ссылки на счета-фактуры, исключаемые при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//	Соответствие - Содержит:
//		* Ключ - ДокументСсылка.* - Документ основание
//  	* Значение - ДокументСсылка.СчетФактураПолученный - Найденный счет-фактура для документа-основания.
//
Функция НайтиПодчиненныеСчетаФактурыПолученные(СписокДокументовОснований, ИсключаемыеСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	Результат = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(СписокДокументовОснований) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураПолученный) Тогда
		Возврат Результат;
	КонецЕсли;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументовОснований", СписокДокументовОснований);
	Запрос.УстановитьПараметр("ИсключаемыеСФ",     ИсключаемыеСФ);
	Запрос.УстановитьПараметр("ПометкаУдаления",   ПометкаУдаления);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	СчетФактураПолученный.ДокументОснование КАК ДокументОснование,
	|	СчетФактураПолученный.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.ДокументОснование В(&СписокДокументовОснований)
	|	И СчетФактураПолученный.Ссылка.ПометкаУдаления = &ПометкаУдаления
	|	И НЕ СчетФактураПолученный.Ссылка В (&ИсключаемыеСФ)
	|	И &ДопУсловия";

	// Отбор одной записи.
	Если ТипЗнч(СписокДокументовОснований) = Тип("Массив") 
		ИЛИ ТипЗнч(СписокДокументовОснований) = Тип("СписокЗначений") Тогда
		ВыбиратьПервые = СписокДокументовОснований.Количество() = 1;
	Иначе
		ВыбиратьПервые = Истина;
	КонецЕсли;
	Если НЕ ВыбиратьПервые Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "");
	КонецЕсли;
	
	// Фильтрация исключаемых счетов-фактур.
	Если НЕ ЗначениеЗаполнено(ИсключаемыеСФ) Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"НЕ СчетФактураПолученный.Ссылка В (&ИсключаемыеСФ)",
			"ИСТИНА");
	КонецЕсли;
	
	// Дополнительные отборы.
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") И СтруктураОтбора.Количество() > 0 Тогда
		ТекстУсловия = "";
		Для Каждого КлючЗначение Из СтруктураОтбора Цикл
			Если НЕ ПустаяСтрока(ТекстУсловия) Тогда
				ТекстУсловия = ТекстУсловия + "
				| И ";
			КонецЕсли;
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("СписокЗначений") 
				ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
				ТекстУсловия = ТекстУсловия + "СчетФактураПолученный.Ссылка." + КлючЗначение.Ключ + " В (&" + КлючЗначение.Ключ + ")";
			Иначе
				ТекстУсловия = ТекстУсловия + "СчетФактураПолученный.Ссылка." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
			КонецЕсли;
			
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			
		КонецЦикла;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  ТекстУсловия);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ДокументОснование, Выборка.СчетФактура);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Функция производит поиск счета-фактуры выданного на реализацию или корректировочного с указанным документом-основанием.
//
// Параметры:
//  ДокументОснование	- ДокументСсылка.* - Ссылка на документ, для которого надо найти счет-фактурую.
//  ИсключаемыйСФ		- ДокументСсылка.СчетФактураВыданный - Ссылка на счет-фактуру, исключаемый при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//  ДокументСсылка.СчетФактураВыданный - Если нашли, то возвращаем ссылку, не нашли - Неопределено.
//
Функция НайтиПодчиненныйСчетФактуруВыданныйНаРеализацию(ДокументОснование, ИсключаемыйСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт
	
	НайденныеСФ	= НайтиПодчиненныеСчетаФактурыВыданныеНаРеализацию(ДокументОснование, ИсключаемыйСФ, ПометкаУдаления, СтруктураОтбора);

	Возврат НайденныеСФ[ДокументОснование];

КонецФункции

// Функция производит поиск счетов-фактур выданных на реализацию или корректировочного с указанными документами-основаниями.
//
// Параметры:
//  СписокДокументовОснований - ДокументСсылка.*, Массив, СписокЗначений - Ссылки на документы, для которых надо найти счета-фактуры.
//  ИсключаемыеСФ		- ДокументСсылка.СчетФактураВыданный, Массив, СписокЗначений - Ссылки на счета-фактуры, исключаемые при поиске.
//  ПометкаУдаления		- Булево - Значение пометки счета-фактуры для отбора при поиске.
//	СтруктураОтбора     - Структура - Дополнительный отбор счетов-фактур.
//
// Возвращаемое значение:
//	Соответствие - Содержит:
//		* Ключ - ДокументСсылка.* - Документ основание
//  	* Значение - ДокументСсылка.СчетФактураВыданный - Найденный счет-фактура для документа-основания.
//
Функция НайтиПодчиненныеСчетаФактурыВыданныеНаРеализацию(СписокДокументовОснований, ИсключаемыеСФ = Неопределено, ПометкаУдаления = Ложь, СтруктураОтбора = Неопределено) Экспорт

	Результат = Новый Соответствие;
	
	Если НЕ ЗначениеЗаполнено(СписокДокументовОснований) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Если Не ПравоДоступа("Чтение", Метаданные.Документы.СчетФактураВыданный) Тогда
		Возврат Результат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СписокДокументовОснований", СписокДокументовОснований);
	Запрос.УстановитьПараметр("ИсключаемыеСФ",     ИсключаемыеСФ);
	Запрос.УстановитьПараметр("ПометкаУдаления",   ПометкаУдаления);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ДокументыОснования.ДокументОснование КАК ДокументОснование,
	|	ДокументыОснования.Ссылка КАК СчетФактура
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК ДокументыОснования
	|ГДЕ
	|	ДокументыОснования.ДокументОснование В (&СписокДокументовОснований)
	|	И ДокументыОснования.Ссылка.ПометкаУдаления = &ПометкаУдаления
	|	И НЕ ДокументыОснования.Ссылка В (&ИсключаемыеСФ)
	|	И &ДопУсловия";

	// Отбор одной записи.
	Если ТипЗнч(СписокДокументовОснований) = Тип("Массив") 
		ИЛИ ТипЗнч(СписокДокументовОснований) = Тип("СписокЗначений") Тогда
		ВыбиратьПервые = СписокДокументовОснований.Количество() = 1;
	Иначе
		ВыбиратьПервые = Истина;
	КонецЕсли;
	Если НЕ ВыбиратьПервые Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 1", "");
	КонецЕсли;

	// Фильтрация исключаемых счетов-фактур.
	Если НЕ ЗначениеЗаполнено(ИсключаемыеСФ) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст,
			"НЕ ДокументыОснования.Ссылка В (&ИсключаемыеСФ)",
			"ИСТИНА");
	КонецЕсли;

	// Дополнительные отборы.
	Если ТипЗнч(СтруктураОтбора) = Тип("Структура") И СтруктураОтбора.Количество() > 0 Тогда
		ТекстУсловия = "";
		Для Каждого КлючЗначение Из СтруктураОтбора Цикл
			Если НЕ ПустаяСтрока(ТекстУсловия) Тогда
				ТекстУсловия = ТекстУсловия + "
				| И ";
			КонецЕсли;
			
			Если ТипЗнч(КлючЗначение.Значение) = Тип("СписокЗначений") 
				ИЛИ ТипЗнч(КлючЗначение.Значение) = Тип("Массив") Тогда
				ТекстУсловия = ТекстУсловия + "ДокументыОснования.Ссылка." + КлючЗначение.Ключ + " В (&" + КлючЗначение.Ключ + ")";
			Иначе
				ТекстУсловия = ТекстУсловия + "ДокументыОснования.Ссылка." + КлючЗначение.Ключ + " = &" + КлючЗначение.Ключ;
			КонецЕсли;
			
			Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
			
		КонецЦикла;
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  ТекстУсловия);
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ДопУсловия",  "ИСТИНА");
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Результат.Вставить(Выборка.ДокументОснование, Выборка.СчетФактура);
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

// Если по сериям для переданных Номенклатуры/Склада учитывается себестоимость, то рассчитывает статус указания серий.
//  Проверяет принадлежность уже указанной серии переданной номенклатуре.
//
// Параметры:
//  ТекущаяСтрока			 - Структура - для которой рассчитывается статус указания серий;
//  Склад					 - СправочникСсылка.Склады	 - склад, для которого осуществляется расчет статуса указания серий;
//  ПараметрыУказанияСерий	 - Структура				 - структура, описанная в функции НоменклатураКлиентСервер.ПараметрыУказанияСерий.
// 
// Возвращаемое значение:
//  Структура - структура со следующими ключами:
//  * Серия - СправочникСсылка.СерииНоменклатуры - если серия указана и она может использоваться с новым значением номенклатуры,
//  	на указанном складе, то возвращается переданное значение, если нет - пустая ссылка;
//  * СтатусУказанияСерий - Число - если серии указываются в ТЧ "Товары", то возвращается рассчитанный статус,
//  	если для переданной номенклатуры/склада серии не используется - возвращается 0
//  	иначе возвращается переданный статус.
//
Функция ПроверитьСериюРассчитатьСтатусПриИзмененииРеквизитаВТЧ(ТекущаяСтрока, Склад, ПараметрыУказанияСерий) Экспорт
	
	МетаданныеОбъекта = ОбщегоНазначенияУТ.МетаданныеОбъектаПоПолномуИмени(ПараметрыУказанияСерий.ПолноеИмяОбъекта);
		
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Серия");
	СтруктураВозврата.Вставить("УказыватьСерии");
	
	СтруктураВозврата.Серия = ТекущаяСтрока.Серия;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИСТИНА КАК РазличаютсяВладельцыСерииИНоменклатуры
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура,
	|	Справочник.СерииНоменклатуры КАК Серии
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура
	|	И Серии.Ссылка = &Серия
	|	И НЕ(Серии.ВидНоменклатуры.ВладелецСерий = Номенклатура.ВидНоменклатуры.ВладелецСерий
	|					И Серии.ВидНоменклатуры.ВладелецСерий <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|					И Номенклатура.ВидНоменклатуры.ВладелецСерий <> ЗНАЧЕНИЕ(Справочник.ВидыНоменклатуры.ПустаяСсылка)
	|				ИЛИ Номенклатура.ВладелецСерий = Серии.ВидНоменклатуры
	|				ИЛИ Номенклатура.ВидНоменклатуры = Серии.ВидНоменклатуры)";
	Запрос.УстановитьПараметр("Номенклатура", ТекущаяСтрока.Номенклатура);
	Запрос.УстановитьПараметр("Серия", ТекущаяСтрока.Серия);
	Если Не Запрос.Выполнить().Пустой() Тогда
		СтруктураВозврата.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		СтруктураВозврата.Вставить("СтатусУказанияСерий");
	Иначе
		Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			СтруктураВозврата.Вставить(ИмяПоляСтатус);
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		
		СтруктураВозврата.Серия               = Справочники.СерииНоменклатуры.ПустаяСсылка();
		Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
			СтруктураВозврата.СтатусУказанияСерий = 0;
		Иначе
			Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
				СтруктураВозврата[ИмяПоляСтатус] = 0;
			КонецЦикла;
		КонецЕсли;
		СтруктураВозврата.УказыватьСерии      = Ложь;
		
		Возврат СтруктураВозврата;
	КонецЕсли;
	
	ТипНоменклатура = Новый ОписаниеТипов("СправочникСсылка.Номенклатура");
	ТипХарактеристикиНоменклатуры = Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры");
	ТипСерииНоменклатуры = Новый ОписаниеТипов("СправочникСсылка.СерииНоменклатуры");
	
	ТаблицаТоваров = Новый ТаблицаЗначений;
	ТаблицаТоваров.Колонки.Добавить("Номенклатура", ТипНоменклатура);
	ТаблицаТоваров.Колонки.Добавить("Характеристика", ТипХарактеристикиНоменклатуры);
	ТаблицаТоваров.Колонки.Добавить("Серия", ТипСерииНоменклатуры);
		
	ТаблицаТоваров.Колонки.Добавить(ПараметрыУказанияСерий.ИмяПоляКоличество,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	ТаблицаТоваров.Колонки.Добавить("ПорядокСтроки",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(5,0,ДопустимыйЗнак.Неотрицательный)));
	
	ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров, МетаданныеОбъекта);
	
	ТаблицаСерий = Новый ТаблицаЗначений;
	ТаблицаСерий.Колонки.Добавить("Номенклатура",Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	ТаблицаСерий.Колонки.Добавить("Характеристика",Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	ТаблицаСерий.Колонки.Добавить(ПараметрыУказанияСерий.ИмяПоляКоличество,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(15,3,ДопустимыйЗнак.Неотрицательный)));
	
	ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, ТаблицаСерий, МетаданныеОбъекта, Истина);
	
	СтрокаТовара = ТаблицаТоваров.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаТовара,ТекущаяСтрока);
	СтрокаТовара.ПорядокСтроки = 1;
	СтрокаТовара.Серия = СтруктураВозврата.Серия;
	
	Выборка = НоменклатураСервер.ВыборкаИзЗапросаРасчетаСтатусовУказанияСерий(ПараметрыУказанияСерий, ТаблицаТоваров, ТаблицаСерий, Склад);
	
	Если Выборка.Следующий() Тогда
		
		// Если серии указываются в отдельной ТЧ, то при изменении реквизитов
		// будут пересчитаны только статусы, связанные с сериями, указываемыми
		// в ТЧ "Товары" (т.е. по которым ведется учет себестоимости).
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииУказываютсяВТЧТовары(Выборка.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
			
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				СтруктураВозврата.СтатусУказанияСерий = Выборка.СтатусУказанияСерий;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					СтруктураВозврата[ИмяПоляСтатус] = Выборка[ИмяПоляСтатус];
				КонецЦикла;
			КонецЕсли;
						
		Иначе
			Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
				СтруктураВозврата.СтатусУказанияСерий = ТекущаяСтрока.СтатусУказанияСерий;
			Иначе
				Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
					СтруктураВозврата[ИмяПоляСтатус] = ТекущаяСтрока[ИмяПоляСтатус];
				КонецЦикла;
			КонецЕсли;

		КонецЕсли;
		
		Если НоменклатураКлиентСервер.ВЭтомСтатусеСерииНеУказываются(СтруктураВозврата.СтатусУказанияСерий, ПараметрыУказанияСерий) Тогда
			СтруктураВозврата.УказыватьСерии = Ложь;
			СтруктураВозврата.Серия = Справочники.СерииНоменклатуры.ПустаяСсылка();
		КонецЕсли;
		
	Иначе
		Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
			СтруктураВозврата.СтатусУказанияСерий = ТекущаяСтрока.СтатусУказанияСерий;
		Иначе
			Для Каждого ИмяПоляСтатус Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
				СтруктураВозврата[ИмяПоляСтатус] = ТекущаяСтрока[ИмяПоляСтатус];
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

Процедура ДополнитьТаблицуКолонкамиПоПолямПараметровУказанияСерий(ПараметрыУказанияСерий, Таблица, Знач Метаданные = Неопределено, ЭтоТаблицаСерий = Ложь) Экспорт	
	
	Если ЭтоТаблицаСерий Тогда
		Таблица.Колонки.Добавить("Склад",Новый ОписаниеТипов("СправочникСсылка.Склады"));
		Возврат;
	КонецЕсли;   
	
	//Склада не будет точно
	Таблица.Колонки.Добавить("ТСклад",Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Если ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий.Количество() = 0 Тогда
		Если Таблица.Колонки.Найти("СтатусУказанияСерий") = Неопределено Тогда
			Таблица.Колонки.Добавить("СтатусУказанияСерий",Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
		КонецЕсли;
	Иначе
		Для Каждого СтрМас Из ПараметрыУказанияСерий.ИменаПолейСтатусУказанияСерий Цикл
			Если Таблица.Колонки.Найти(СтрМас) = Неопределено Тогда
				Таблица.Колонки.Добавить(СтрМас,Новый ОписаниеТипов("Число",Новый КвалификаторыЧисла(2,0,ДопустимыйЗнак.Неотрицательный)));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

// Процедура пересчитывает статусы указания серий в строках товаров, если это необходимо,
//  переподчиняет строки серий другим строкам ТЧ "Товары".
//
// Параметры:
//  Объект						 - ДанныеФормыСтруктура	 - основной реквизит формы документа.
//  ПараметрыУказанияСерий		 - Структура - структура параметров указания серий, возвращаемая соответствующей процедурой модуля менеджера документа.
//  ТекущаяСтрокаИдентификатор	 - Число - идентификатор текущей строки товаров в форме документа.
//  КэшированныеЗначения		 - Структура - структура кеша реквизитов текущей строки товаров.
//
Процедура ЗаполнитьСтатусыУказанияСерийПриОкончанииРедактированияСтрокиТЧ(Объект,
																			ПараметрыУказанияСерий,
																			ТекущаяСтрокаИдентификатор,
																			КэшированныеЗначения) Экспорт
	
	Если Не ПараметрыУказанияСерий.ИспользоватьСерииНоменклатуры Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыУказанияСерий.ИмяТЧТовары = ПараметрыУказанияСерий.ИмяТЧСерии Тогда
		Возврат; // Если ТЧ Серии нет, тогда все статусы пересчитываются при изменении реквизитов ТЧ, а не при окончании редактирования
	КонецЕсли;
	
	Если ТекущаяСтрокаИдентификатор <> Неопределено Тогда
		ТекущаяСтрока = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиПоИдентификатору(ТекущаяСтрокаИдентификатор); // ДокументТабличнаяЧастьСтрока.АктОРасхожденияхПослеПриемки.Товары - указано для типизации
	Иначе
		ТекущаяСтрока = Неопределено //значит строку удалили;
	КонецЕсли;
	
	ТекстПоляСвязи = "";
	
	Для Каждого СтрМас Из ПараметрыУказанияСерий.ПоляСвязи Цикл
		ТекстПоляСвязи = ТекстПоляСвязи + "," + СтрМас  ;
	КонецЦикла;
	
	ЕстьУпаковки = ПараметрыУказанияСерий.ПоляСвязи.Найти("Упаковка") <> Неопределено; 
	
	// Если строка новая (в т.ч. скопированная) или используется разделение по вариантам продажи - будет закешированно Неопределено
	// Тогда не нужно искать строки со старыми значениями.
	Если КэшированныеЗначения.Номенклатура <> Неопределено Тогда
		
		СтруктураПоискаСтарыеЗначения = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
		ЗаполнитьЗначенияСвойств(СтруктураПоискаСтарыеЗначения,КэшированныеЗначения);
		
		НайденныеСтрокиТоваров = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоискаСтарыеЗначения);
		НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаСтарыеЗначения);
	Иначе
		НайденныеСтрокиТоваров = Новый Массив;
		НайденныеСтрокиСерий   = Новый Массив;
	КонецЕсли;
	
	// Если поменялись ключевые поля:
	// - возможно нужно переподчинять серии
	// - статус указания серий нужно пересчитывать в строках по новым ключевым полям и по старым.
	Если ТекущаяСтрока <> Неопределено 
		И Не ОбщегоНазначенияУТКлиентСервер.СтруктурыРавны(КэшированныеЗначения, ТекущаяСтрока, "Номенклатура,Характеристика"+ТекстПоляСвязи) Тогда
		
		УчитыватьОстатки      = Ложь;
		ПереподчинитьСерии    = Ложь;
		ПересчитатьКоличество = Ложь;
		// Определим, нужно ли переподчинять серии. Это нужно если:
		// - серии относились только к одной строке
		// - новые и старые ключевые поля поддерживают одну политику учета
		// - серии относились к нескольким строкам, но изменилось значение действия по отражению расхождения со строкой
		// Если строки нужно переподчинять, то определим, нужно ли пересчитывать количество.
		Если КэшированныеЗначения.Номенклатура <> Неопределено
			И ((КэшированныеЗначения.Свойство("Действие")
					И КэшированныеЗначения.Действие <> ТекущаяСтрока.Действие)
				Или НайденныеСтрокиТоваров.Количество() = 0) Тогда//т.к. строк с такими ключевыми полями не осталось, значит такая строка была одна
			
			Если НайденныеСтрокиТоваров.Количество() > 0 Тогда
				УчитыватьОстатки   = Истина;
				ПереподчинитьСерии = Истина;
			ИначеЕсли КэшированныеЗначения.Номенклатура = ТекущаяСтрока.Номенклатура Тогда //т.е. изменились поля, от которых политика учета не зависит
				ПереподчинитьСерии = Истина;
			Иначе //будем переподчинять, если не поменялся вид номенклатуры
				ВидНоменклатурыТекущий = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КэшированныеЗначения.Номенклатура,"ВидНоменклатуры");
				ВидНоменклатурыНовый   = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТекущаяСтрока.Номенклатура, "ВидНоменклатуры");
				
				ПереподчинитьСерии = (ВидНоменклатурыТекущий = ВидНоменклатурыНовый);
			КонецЕсли;
			
			Если ПереподчинитьСерии
				И ЕстьУпаковки
				И КэшированныеЗначения.Упаковка <> ТекущаяСтрока.Упаковка Тогда
				
				ПересчитатьКоличество = Истина;
				
			КонецЕсли;
			
		КонецЕсли;	
		
		// Если строка удалена, то в качестве текущих значений будет передано Неопределено
		// Тогда не нужно искать строки с новыми значениями.
		СтруктураПоискаНовыеЗначения = Новый Структура("Номенклатура,Характеристика"+ТекстПоляСвязи);
		ЗаполнитьЗначенияСвойств(СтруктураПоискаНовыеЗначения,ТекущаяСтрока);
		
		НайденныеСтрокиТоваровНовые = Объект[ПараметрыУказанияСерий.ИмяТЧТовары].НайтиСтроки(СтруктураПоискаНовыеЗначения);    
		
		// Добавим строки по новым ключевым полям в массив строк для пересчета статуса указания серий.
		
		// При объединении массивов будем обходить меньший массив
		Если НайденныеСтрокиТоваров.Количество() < НайденныеСтрокиТоваровНовые.Количество() Тогда
			Для Каждого СтрМас Из НайденныеСтрокиТоваров Цикл
				НайденныеСтрокиТоваровНовые.Добавить(СтрМас);
			КонецЦикла;
			НайденныеСтрокиТоваров = НайденныеСтрокиТоваровНовые;
		Иначе
			Для Каждого СтрМас Из НайденныеСтрокиТоваровНовые Цикл
				НайденныеСтрокиТоваров.Добавить(СтрМас);
			КонецЦикла;
		КонецЕсли;
		
		// Определим массив строк серий, который должен участвовать в пересчете статусов,
		Если ПереподчинитьСерии Тогда		
			// Сначала переподчиним серии
			
			Если УчитыватьОстатки Тогда
				Если ТекущаяСтрока.КоличествоПоДокументу > ТекущаяСтрока.Количество Тогда
					КоличествоОстаток = ТекущаяСтрока.Количество;
				Иначе
					
					ЕстьНовыеСерии = Ложь;
					
					КоличествоСтрокСИзлишками = 0;
					
					Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
						Если СтрМас.КоличествоПоДокументу = 0 Тогда
							ЕстьНовыеСерии = Истина;
						ИначеЕсли СтрМас.КоличествоПоДокументу < СтрМас.Количество Тогда
							КоличествоСтрокСИзлишками = КоличествоСтрокСИзлишками + 1;
						КонецЕсли;
					КонецЦикла;
					
					Если ЕстьНовыеСерии
						И КоличествоСтрокСИзлишками > 0 Тогда //Переподчиним количество серий за вычетом количества строк с излишками
						
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу - КоличествоСтрокСИзлишками;
						
					ИначеЕсли ЕстьНовыеСерии Тогда //Переподчиним количество серий равное количеству серий в документе
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу;
					Иначе //Переподчиним количество серий равное фактическому количеству серий в обрабатываемой строке
						КоличествоОстаток = ТекущаяСтрока.КоличествоПоДокументу
											- (ТекущаяСтрока.Количество - ТекущаяСтрока.КоличествоПоДокументу);
					КонецЕсли;
					
				КонецЕсли;
			КонецЕсли;
			
			ОбрабатываемыеСтроки = Новый Массив;
			
			Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
				
				Если УчитыватьОстатки Тогда
					Если КоличествоОстаток > 0
						Или СтрМас.Количество <> СтрМас.КоличествоПоДокументу Тогда
						
						КоличествоОстаток = КоличествоОстаток - СтрМас.Количество;
						
						ЗаполнитьЗначенияСвойств(СтрМас, ТекущаяСтрока, ТекстПоляСвязи);
					КонецЕсли;
				Иначе
					ЗаполнитьЗначенияСвойств(СтрМас, ТекущаяСтрока, "Номенклатура,Характеристика" + ТекстПоляСвязи);
				КонецЕсли;
				
				Если ПересчитатьКоличество Тогда
					ОбрабатываемыеСтроки.Добавить(СтрМас);
				КонецЕсли;
				
			КонецЦикла;
			
			СтруктураДействий = Новый Структура;
			СтруктураДействий.Вставить("ПересчитатьКоличествоУпаковок");
			ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокиТЧ(
				ОбрабатываемыеСтроки,
				СтруктураДействий,
				Объект[ПараметрыУказанияСерий.ИмяТЧСерии], КэшированныеЗначения);
			
			Если Не УчитыватьОстатки Тогда
				// Если серии переподчинены, то достаточно произвести поиск по новым полям поиска
				НайденныеСтрокиСерий = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаНовыеЗначения);
			КонецЕсли;
			
		Иначе	
			НайденныеСтрокиСерийНовые = Объект[ПараметрыУказанияСерий.ИмяТЧСерии].НайтиСтроки(СтруктураПоискаНовыеЗначения);
			
			// Если серии не переподчинены, то к строкам по старым ключевым полям нужно добавить строки по новым ключевым полям.
			
			// При объединении массивов будем обходить меньший массив
			Если НайденныеСтрокиСерий.Количество() < НайденныеСтрокиСерийНовые.Количество() Тогда
				Для Каждого СтрМас Из НайденныеСтрокиСерий Цикл
					НайденныеСтрокиСерийНовые.Добавить(СтрМас);
				КонецЦикла;
				НайденныеСтрокиСерий = НайденныеСтрокиСерийНовые;
			Иначе
				Для Каждого СтрМас Из НайденныеСтрокиСерийНовые Цикл
					НайденныеСтрокиСерий.Добавить(СтрМас);
				КонецЦикла;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДополнительныеРеквизиты(Форма) Экспорт
		
КонецПроцедуры

// Определяет склад по умолчанию для использования в системе.
//
// Параметры:
//  СкладПоУмолчанию - СправочникСсылка.Склады - Склад, используемый по умолчанию
Процедура ОпределитьСкладПоУмолчанию(СкладПоУмолчанию) Экспорт
	
	СкладПоУмолчанию = Справочники.Склады.СкладПоУмолчанию();
	
	Если Не ЗначениеЗаполнено(СкладПоУмолчанию) Тогда
		НастройкиРаспознования = РегистрыСведений.ОбщиеНастройкиРаспознаваниеДокументов.ТекущиеНастройки();
		Если ЗначениеЗаполнено(НастройкиРаспознования.СкладПоУмолчанию) Тогда
			СкладПоУмолчанию = НастройкиРаспознования.СкладПоУмолчанию;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры 

Функция ПолучитьПервыйСкладПоУмолчанию() Экспорт
  	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Склады.Ссылка КАК Склад
		|ИЗ
		|	Справочник.Склады КАК Склады
		|ГДЕ
		|	НЕ Склады.ЭтоГруппа";
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		
		Выборка = Результат.Выбрать();
		Если Выборка.Количество() = 1 Тогда
			Если Выборка.Следующий() Тогда
				Возврат Выборка.Склад;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
КонецФункции

Процедура ПолучитьСоответствиеСтавокНДС(Соответствие) Экспорт 
		
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюУчетДоговоров(ДокументОбъект, УчетДоговоров) Экспорт
		
	Если ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий Тогда
		УчетДоговоров = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами");
	Иначе
		УчетДоговоров = ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюУчетПоНесколькимСкладам(УчетПоНесколькимСкладам) Экспорт
		
	УчетПоНесколькимСкладам = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов");
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюИспользоватьХарактеристики(ИспользоватьХарактеристики) Экспорт

	ИспользоватьХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристикиНоменклатуры");
	
КонецПроцедуры

Процедура ПолучитьФункциональнуюОпциюИспользоватьКорректировочныеДокументы(ИспользоватьКорректировочныеДокументы) Экспорт
			
КонецПроцедуры

// Устанавливает имя колонки для услуги в таблице документа.
//
// Параметры:
//   ИмяКолонки - Строка
Процедура ИмяКолонкиУслугиТаблицыДокумента(ИмяКолонки) Экспорт

	ИмяКолонки = "Услуга";

КонецПроцедуры // ИмяКолонкиУслугиТаблицыДокумента(ИмяКолонки)

Процедура ЭтоНоменклатураУслуга(Ссылка, Результат) Экспорт
		
	ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, "ТипНоменклатуры"); 
	
	Результат = (ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга);
		
КонецПроцедуры

Процедура ЗаполнитьСписокУслугиПоНоменклатурам(СписокНоменклатур, СписокУслугиПоНоменклатурам) Экспорт

	СписокУслугиПоНоменклатурам = ОбщегоНазначения.ЗначениеРеквизитаОбъектов(СписокНоменклатур, "ТипНоменклатуры");
	
	Для Каждого ЭлементСоответствия Из СписокУслугиПоНоменклатурам Цикл
		
		Если ЭлементСоответствия.Значение = Перечисления.ТипыНоменклатуры.Услуга
			Или ЭлементСоответствия.Значение = Перечисления.ТипыНоменклатуры.Работа Тогда
			СписокУслугиПоНоменклатурам[ЭлементСоответствия.Ключ] = Истина;
		Иначе
			СписокУслугиПоНоменклатурам[ЭлементСоответствия.Ключ] = Ложь;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриИзмененииНоменклатуры(СтрокаТаблицы, НомерСтрокиТаблицы, ВыбранноеЗначение, Объект) Экспорт	
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтрокаТаблицы.ХарактеристикаИспользуется = Справочники.Номенклатура.ХарактеристикиИспользуются(ВыбранноеЗначение);  
		СтрокаТаблицы.ТипНоменклатуры = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ТипНоменклатуры");
		СтрокаТаблицы.Услуга = РаспознаваниеДокументовСлужебныйВызовСервера.ЭтоНоменклатураУслуга(ВыбранноеЗначение);
		СтрокаТаблицы.Работа = (СтрокаТаблицы.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа);	
		СтрокаТаблицы.ВедетсяУчетПоГТД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ВестиУчетПоГТД");  
		ВедетсяУчетПоРНПТ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ПрослеживаемыйТовар");
		ВедетсяУчетПоРНПТ = (СтрокаТаблицы.ВедетсяУчетПоГТД И ВедетсяУчетПоРНПТ И ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрослеживаемыхИмпортныхТоваров"));
		СтрокаТаблицы.ВедетсяУчетПоРНПТ = ВедетсяУчетПоРНПТ;	
		СтрокаТаблицы.ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ЕдиницаИзмерения");;
		Если СтрокаТаблицы.Услуга Тогда
			СтрокаТаблицы.СписатьНаРасходы = Истина;
		ИначеЕсли СтрокаТаблицы.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Товар
			Или СтрокаТаблицы.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара Тогда			
			СтрокаТаблицы.СписатьНаРасходы = Ложь;
		КонецЕсли;
		Если СтрокаТаблицы.Статья = Неопределено Тогда
			СтрокаТаблицы.Статья = ПланыВидовХарактеристик.СтатьиРасходов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьРеквизитыПоискаДоговора(РеквизитыПоиска, ДанныеДоговора) Экспорт
	
	РеквизитыПоиска.Вставить("Номер", ДанныеДоговора.Номер);
	РеквизитыПоиска.Вставить("Дата", ДанныеДоговора.Дата);
	РеквизитыПоиска.Вставить("Наименование", ДанныеДоговора.Наименование);
	
КонецПроцедуры

Процедура ЗаполнитьСкидкиВТаблицеТовары(ДокументОбъект, ТаблицаТовары, ПараметрыЗаполнения, ТипДокументаСтрокой) Экспорт
	
	ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
		ДокументОбъект, "ВидСкидки", РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена);
	Если ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
		Если ТипДокументаСтрокой = "СчетНаОплатуПоставщика" Тогда
			
			// В счете от поставщика сумма принимает смысл "Сумма с учетом скидки".
			
			Для Каждого Строка Из ТаблицаТовары Цикл
				Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
			КонецЦикла;
			
		Иначе
			
			// Сумма используемая в типовом документе имеет смысл "Сумма без скидки".
			
			Для Каждого Строка Из ТаблицаТовары Цикл
				Если Строка.СуммаСкидки < 0 Тогда
					Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
				Иначе
					Строка.Сумма = Строка.Сумма + Строка.СуммаСкидки;
					Строка.Всего = Строка.Сумма;
					Строка.ПроцентСкидки = ?(Строка.Сумма = 0, 0, Строка.СуммаСкидки / Строка.Сумма * 100);
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	ИначеЕсли ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом Тогда
		
		Если ТипДокументаСтрокой = "СчетНаОплатуПоставщика" Тогда
			
			ИтогоСуммаСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(ДокументОбъект, "ИтогоСуммаСкидки", 0);
			ДанныеОбъекта = Новый Структура;
			ДанныеОбъекта.Вставить("СуммаСкидки", ИтогоСуммаСкидки);
			ДанныеОбъекта.Вставить("СуммаВключаетНДС", ПараметрыЗаполнения.СуммаВключаетНДС);
			
		Иначе
			
			ПараметрыЗаполнения.Вставить("СуммаСкидки", ПараметрыЗаполнения.ИтогоСуммаСкидки);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

// Заполняет параметры для создания типового документа на основе формы ТОРГ-12.
//
// Параметры:
//   НашДокумент - ДокументСсылка.РаспознанныйДокумент или ДокументОбъект.РаспознанныйДокумент - документ, на основе которого создается новый документ
//   ВидОперации - Строка - вид операции создаваемого документа
//   ТипДокументаСтрокой - Строка - строковое представление типа документа
//   ПараметрыЗаполнения - Структура - параметры, которые будут заполнены для создания нового документа
Процедура ПараметрыЗаполненияТиповойНаОсновеФормыТОРГ12(НашДокумент,
														ВидОперации,
														ТипДокументаСтрокой,
														ПараметрыЗаполнения) Экспорт
	
	Если ТипЗнч(НашДокумент) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
		ДокументОбъект = НашДокумент.ПолучитьОбъект();
	Иначе
		ДокументОбъект = НашДокумент;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Основание", ДокументОбъект.Ссылка);
	ПараметрыЗаполнения.Вставить("ВидОперации", ВидОперации);
	ПараметрыЗаполнения.Вставить("НомерВходящегоДокумента", ДокументОбъект.РеквизитыДокумента[0].Значение);
	ПараметрыЗаполнения.Вставить("ДатаВходящегоДокумента", ДокументОбъект.РеквизитыДокумента[1].Значение);
	
	Если ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		ИЛИ ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД Тогда
		
		ПараметрыЗаполнения.Вставить("НомерВходящегоДокументаСчетаФактуры", ДокументОбъект.РеквизитыДокумента[0].Значение);
		ПараметрыЗаполнения.Вставить("ДатаВходящегоДокументаСчетаФактуры", ДокументОбъект.РеквизитыДокумента[1].Значение);
		
	КонецЕсли;
	
	ИзменениеЗапрещено = РаспознаваниеДокументовКомплектыВызовСервера.ЗапрещеноСозданиеДокументаВЗакрытомПериоде(
		ТипДокументаСтрокой, ДокументОбъект.РеквизитыДокумента[1].Значение);
	
	Если ИзменениеЗапрещено Тогда
		ПараметрыЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	Иначе
		ПараметрыЗаполнения.Вставить("Дата", ДокументОбъект.РеквизитыДокумента[1].Значение);
	КонецЕсли;
	
	ПараметрыЗаполнения.Вставить("ЭтоУниверсальныйДокумент", ДокументОбъект.ТипДокумента = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД);

	ПараметрыЗаполнения.Вставить("ДоговорКонтрагента", ДокументОбъект.РеквизитыДокумента[6].Значение);
	
	Для Каждого ДанныеРеквизита Из ДокументОбъект.РеквизитыДокумента Цикл
		ПараметрыЗаполнения.Вставить(ДанныеРеквизита.ИмяРеквизита, ДанныеРеквизита.Значение);
	КонецЦикла;
	
	Если ТипДокументаСтрокой = "ПриобретениеТоваровУслуг"
		Или ТипДокументаСтрокой = "ПриобретениеУслугПрочихАктивов" Тогда 
		
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.ПокупательОрганизация);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Продавец);
		ПараметрыЗаполнения.Вставить("Соглашение", ПараметрыЗаполнения.СоглашениеСПоставщиком);
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.СоглашениеСПоставщиком) Тогда
			ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.СоглашениеСПоставщиком, "ВидЦеныПоставщика");
		КонецЕсли;
		
		//++ НЕ УТ
		ОтражениеВУСН = ПредопределенноеЗначение("Перечисление.ОтражениеВУСН.Принимаются");
		//-- НЕ УТ
				
	Иначе
		
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.ПродавецОрганизация);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Покупатель);
		ПараметрыЗаполнения.Вставить("Соглашение", ПараметрыЗаполнения.СоглашениеСКлиентом);
		ПараметрыЗаполнения.Вставить("СкидкиРассчитаны", Истина);

		ПараметрыЗаполнения.Вставить("Номер", ДокументОбъект.РеквизитыДокумента[0].Значение);
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.СоглашениеСКлиентом) Тогда
			ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.СоглашениеСКлиентом, "ВидЦен");
		КонецЕсли;
		
	КонецЕсли;
	
	ТипДокументаОснования = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.Основание, "ТипДокумента");
	
	Если ТипДокументаОснования = Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура
		Или ТипДокументаОснования = Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД
		Или ТипДокументаОснования = Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12 Тогда
		
		ПараметрыЗаполнения.Вставить("СуммаВключаетНДС", Ложь);
	Иначе
		
		ПараметрыЗаполнения.Вставить("СуммаВключаетНДС", ДокументОбъект.РеквизитыДокумента[22].Значение);
	КонецЕсли;
	
	СтрокиТаблицыТовары = Новый Массив;
	СтрокиТаблицыУслуги = Новый Массив;
	
	ТаблицаДокумента = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	КолонкиТаблицы = ТаблицаДокумента.Колонки;
	ПоискЕдиницыИзмеренияПоКоду = Не КолонкиТаблицы.Найти("КодЕдиницыИзмерения") = Неопределено;
	
	Для Каждого СтрокаТаблицыДокумента Из ТаблицаДокумента Цикл
		ДанныеСтрокиТаблицы = Новый Структура();
		Для Каждого Колонка Из КолонкиТаблицы Цикл
			Если Колонка.Имя = "ЕдиницаИзмерения" Тогда
				ЕдиницаИзмерения = Неопределено;
				Если ЗначениеЗаполнено(СтрокаТаблицыДокумента.ЕдиницаИзмерения) Тогда
					 ЕдиницаИзмерения = СтрокаТаблицыДокумента.ЕдиницаИзмерения;
				ИначеЕсли ПоискЕдиницыИзмеренияПоКоду И ЗначениеЗаполнено(СтрокаТаблицыДокумента.КодЕдиницыИзмерения) Тогда
					 ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(СтрокаТаблицыДокумента.КодЕдиницыИзмерения);
				ИначеЕсли ЕдиницаИзмерения = Неопределено Тогда  
					 ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицыДокумента.Номенклатура, "ЕдиницаИзмерения");
				КонецЕсли;
				Если ТипДокументаСтрокой = "ПриобретениеТоваровУслуг" Или ТипДокументаСтрокой = "РеализацияТоваровУслуг" Тогда 
					ДанныеСтрокиТаблицы.Вставить("Упаковка", ЕдиницаИзмерения);
				Иначе
					ДанныеСтрокиТаблицы.Вставить("ЕдиницаИзмерения", ЕдиницаИзмерения);
				КонецЕсли;
			ИначеЕсли Колонка.Имя = "ТПодразделение" Тогда
				ДанныеСтрокиТаблицы.Вставить("Подразделение", СтрокаТаблицыДокумента[Колонка.Имя]);
			ИначеЕсли Колонка.Имя = "ТСклад" Тогда
				ДанныеСтрокиТаблицы.Вставить("Склад",
				?(ЗначениеЗаполнено(СтрокаТаблицыДокумента[Колонка.Имя]), СтрокаТаблицыДокумента[Колонка.Имя],
				ДокументОбъект.РеквизитыДокумента[7].Значение));
			ИначеЕсли Колонка.Имя = "Статья" И ТипДокументаСтрокой = "ПриобретениеУслугПрочихАктивов" Тогда
				ДанныеСтрокиТаблицы.Вставить("СтатьяРасходов", СтрокаТаблицыДокумента[Колонка.Имя]);
			Иначе
				ДанныеСтрокиТаблицы.Вставить(Колонка.Имя, СтрокаТаблицыДокумента[Колонка.Имя]);
			КонецЕсли;
		КонецЦикла;
		
		Если ТипДокументаСтрокой = "ПриобретениеТоваровУслуг" Тогда
			//++ НЕ УТ
			ДанныеСтрокиТаблицы.Вставить("ОтражениеВУСН", ОтражениеВУСН);
			//-- НЕ УТ
			ДанныеСтрокиТаблицы.Вставить("КоличествоУпаковок", СтрокаТаблицыДокумента.Количество);
			Если ЗначениеЗаполнено(ВидЦены) Тогда
				ДанныеСтрокиТаблицы.Вставить("ВидЦеныПоставщика", ВидЦены);
			КонецЕсли;
			
		ИначеЕсли ТипДокументаСтрокой = "РеализацияТоваровУслуг" Тогда
			
			ДанныеСтрокиТаблицы.Вставить("КоличествоУпаковок", СтрокаТаблицыДокумента.Количество);
			Если ЗначениеЗаполнено(ВидЦены) Тогда
				ДанныеСтрокиТаблицы.Вставить("ВидЦены", ВидЦены);
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСтрокиТаблицы.Вставить("СуммаСНДС", СтрокаТаблицыДокумента.Всего);
 
		Если Не ТипДокументаСтрокой = "АктВыполненныхРабот" Тогда
			Если ТипДокументаСтрокой = "ПриобретениеУслугПрочихАктивов" 
				Или ТипДокументаСтрокой = "РеализацияУслугПрочихАктивов" Тогда
				СтрокиТаблицыУслуги.Добавить(ДанныеСтрокиТаблицы);
			ИначеЕсли СтрокаТаблицыДокумента.Услуга Тогда
				СтрокиТаблицыТовары.Добавить(ДанныеСтрокиТаблицы);
			Иначе
				ДанныеСтрокиТаблицы.Вставить("Коэффициент", 1);
				СтрокиТаблицыТовары.Добавить(ДанныеСтрокиТаблицы);
			КонецЕсли;
		Иначе	
			Если ЗначениеЗаполнено(ВидЦены) Тогда
				ДанныеСтрокиТаблицы.Вставить("ВидЦены", ВидЦены);
			КонецЕсли;
			Если СтрокаТаблицыДокумента.Услуга Тогда
				СтрокиТаблицыУслуги.Добавить(ДанныеСтрокиТаблицы);
			Иначе
				ДанныеСтрокиТаблицы.Вставить("Коэффициент", 1);
				СтрокиТаблицыТовары.Добавить(ДанныеСтрокиТаблицы);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыЗаполнения.Вставить("ТоварыУслуги", СтрокиТаблицыТовары);
	ПараметрыЗаполнения.Вставить("УслугиРаботы", СтрокиТаблицыУслуги);
		
КонецПроцедуры

// Заполняет параметры для создания типового документа на основе счета на оплату.
//
// Параметры:
//   НашДокумент         - ДокументСсылка.РаспознанныйДокумент, ДокументОбъект - исходный документ
//   ТипДокументаСтрокой - Строка - тип создаваемого документа
//   ПараметрыЗаполнения - Структура - заполняемые параметры
Процедура ПараметрыЗаполненияТиповойНаОсновеСчетНаОплату(НашДокумент, ТипДокументаСтрокой, ПараметрыЗаполнения) Экспорт

	ВидЦены = Неопределено;
	
	Если ТипЗнч(НашДокумент) = Тип("ДокументСсылка.РаспознанныйДокумент") Тогда
		ДокументОбъект = НашДокумент.ПолучитьОбъект();
	Иначе
		ДокументОбъект = НашДокумент;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("ОснованиеСчет", ДокументОбъект.Ссылка);
	ПараметрыЗаполнения.Вставить("НомерВходящегоДокумента", ДокументОбъект.РеквизитыДокумента[0].Значение);
	ПараметрыЗаполнения.Вставить("ДатаВходящегоДокумента", ДокументОбъект.РеквизитыДокумента[1].Значение);
	ПараметрыЗаполнения.Вставить("ДоговорКонтрагента", ДокументОбъект.РеквизитыДокумента[6].Значение);
	ПараметрыЗаполнения.Вставить("СрокОплаты", ДокументОбъект.РеквизитыДокумента[7].Значение);
	
	ИзменениеЗапрещено = РаспознаваниеДокументовКомплектыВызовСервера.ЗапрещеноСозданиеДокументаВЗакрытомПериоде(
		ТипДокументаСтрокой, ДокументОбъект.РеквизитыДокумента[1].Значение);
	
	Если ИзменениеЗапрещено Тогда
		ПараметрыЗаполнения.Вставить("Дата", ТекущаяДатаСеанса());
	Иначе
		ПараметрыЗаполнения.Вставить("Дата", ДокументОбъект.РеквизитыДокумента[1].Значение);
	КонецЕсли;
	
	Для Каждого ДанныеРеквизита Из ДокументОбъект.РеквизитыДокумента Цикл
		ПараметрыЗаполнения.Вставить(ДанныеРеквизита.ИмяРеквизита, ДанныеРеквизита.Значение);
	КонецЦикла;
	
	Если ТипДокументаСтрокой = "ПлатежноеПоручение" Тогда
		ПараметрыЗаполнения.Вставить("СуммаДокумента", ДокументОбъект.РеквизитыДокумента[10].Значение);
		ПараметрыЗаполнения.Вставить("СуммаНДС", ДокументОбъект.РеквизитыДокумента[11].Значение);
	КонецЕсли;
	
	Если ТипДокументаСтрокой = "СчетНаОплатуПоставщика"
		Или ТипДокументаСтрокой = "ПлатежноеПоручение" Тогда 
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.ПокупательОрганизация);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Продавец);

	ИначеЕсли ТипДокументаСтрокой = "ЗаказПоставщику" Тогда 
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.РеквизитыДокумента[6].Значение) 
			Или ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСПоставщиками") Тогда
			ПараметрыЗаполнения.Вставить("ПорядокРасчетов", Перечисления.ПорядокРасчетов.ПоЗаказам);
		КонецЕсли;
		
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.ПокупательОрганизация);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Продавец); 
		ПараметрыЗаполнения.Вставить("Соглашение", ПараметрыЗаполнения.СоглашениеСПоставщиком);
		
		ПараметрыЗаполнения.Вставить("НомерПоДаннымПоставщика", ДокументОбъект.РеквизитыДокумента[0].Значение);
		ПараметрыЗаполнения.Вставить("ДатаПоДаннымПоставщика", ДокументОбъект.РеквизитыДокумента[1].Значение);
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.СоглашениеСПоставщиком) Тогда
			ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.СоглашениеСПоставщиком, "ВидЦеныПоставщика");
		КонецЕсли;
		
	ИначеЕсли ТипДокументаСтрокой = "ЗаказКлиента" Тогда
		
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьДоговорыСКлиентами") Тогда
			ПараметрыЗаполнения.Вставить("ПорядокРасчетов", Перечисления.ПорядокРасчетов.ПоЗаказам);
		КонецЕсли;
		
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.ПродавецОрганизация);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Покупатель);
		ПараметрыЗаполнения.Вставить("Соглашение", ПараметрыЗаполнения.СоглашениеСКлиентом);
		
		ПараметрыЗаполнения.Вставить("НомерПоДаннымКлиента", ДокументОбъект.РеквизитыДокумента[0].Значение);
		ПараметрыЗаполнения.Вставить("ДатаПоДаннымКлиента", ДокументОбъект.РеквизитыДокумента[1].Значение);

		ПараметрыЗаполнения.Вставить("СтруктурнаяЕдиница", ПараметрыЗаполнения.БанковскийСчетОрганизации);
		ПараметрыЗаполнения.Вставить("Номер", ДокументОбъект.РеквизитыДокумента[0].Значение);
		ПараметрыЗаполнения.Вставить("ПоступлениеОднойДатой", Истина);
		ПараметрыЗаполнения.Вставить("ДатаОтгрузки", ПараметрыЗаполнения.Дата);
		
		Если ЗначениеЗаполнено(ПараметрыЗаполнения.СоглашениеСКлиентом) Тогда
			ВидЦены = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.СоглашениеСКлиентом, "ВидЦен");
		КонецЕсли;
		
	Иначе
		ПараметрыЗаполнения.Вставить("Организация", ПараметрыЗаполнения.Исполнитель);
		ПараметрыЗаполнения.Вставить("Контрагент", ПараметрыЗаполнения.Покупатель);
		ПараметрыЗаполнения.Вставить("ОрганизацияПолучатель", ПараметрыЗаполнения.ПродавецОрганизация);
		
		ПараметрыЗаполнения.Вставить("СтруктурнаяЕдиница", ПараметрыЗаполнения.БанковскийСчетОрганизации);
		ПараметрыЗаполнения.Вставить("Номер", ДокументОбъект.РеквизитыДокумента[0].Значение);
	КонецЕсли;
	
	ПараметрыЗаполнения.Вставить("СуммаВключаетНДС", ДокументОбъект.РеквизитыДокумента[26].Значение);
	
	ТаблицаТовары = РаспознаваниеДокументовСлужебный.ЗаполненнаяТаблицаДокумента(ДокументОбъект);
	
	Если ТипДокументаСтрокой = "ЗаказПоставщику" Или ТипДокументаСтрокой = "ЗаказКлиента" Тогда
		
		Операция = ?(ДокументОбъект.Направление = Перечисления.НаправленияРаспознанногоДокумента.Исходящий,
			Перечисления.ХозяйственныеОперации.РеализацияКлиенту,
			Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика);
		ПараметрыЗаполнения.Вставить("ХозяйственнаяОперация", Операция);
		ПараметрыЗаполнения.Вставить("Договор", ДокументОбъект.РеквизитыДокумента[6].Значение);
		
		ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
			ДокументОбъект, "ВидСкидки", РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НеПредоставлена);
			
		Если ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().НаОтдельныеПозиции Тогда
			
			Для Каждого Строка Из ТаблицаТовары Цикл
				Если Строка.СуммаСкидки < 0 Тогда
					Строка.Цена = Строка.Сумма / ?(Строка.Количество = 0, 1, Строка.Количество);
				Иначе
					Строка.Сумма = Строка.Сумма;
					Строка.Всего = Строка.Всего;
					Строка.ПроцентСкидки = ?(Строка.Сумма = 0, 0, Строка.СуммаСкидки / Строка.Сумма * 100);
				КонецЕсли;
			КонецЦикла;
				
		ИначеЕсли ВидСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ВидыСкидок().ПоДокументуВЦелом Тогда
			
			Если ТипДокументаСтрокой = "ЗаказПоставщику" Тогда			
				ИтогоСуммаСкидки = РаспознаваниеДокументовСлужебныйКлиентСервер.ЗначениеРеквизитаДокумента(
					ДокументОбъект, "ИтогоСуммаСкидки", 0);
				ДанныеОбъекта = Новый Структура;
				ДанныеОбъекта.Вставить("СуммаСкидки", ИтогоСуммаСкидки);
				ДанныеОбъекта.Вставить("СуммаВключаетНДС", ПараметрыЗаполнения.СуммаВключаетНДС);
			Иначе
				ПараметрыЗаполнения.Вставить("СуммаСкидки", ПараметрыЗаполнения.ИтогоСуммаСкидки);
			КонецЕсли;
			
		КонецЕсли;
		
		ПараметрыЗаполнения.Вставить("СкидкиРассчитаны", Истина);
	
	КонецЕсли;
	
	ЗаполнитьСкидкиВТаблицеТовары(ДокументОбъект, ТаблицаТовары, ПараметрыЗаполнения, ТипДокументаСтрокой);
			
	СтрокиТаблицыТовары = Новый Массив;
	
	КолонкиТаблицы = ТаблицаТовары.Колонки;
	
	Если Не КолонкиТаблицы.Найти("ИтогоСумма") = Неопределено Тогда
		КолонкиТаблицы.Удалить("ИтогоСумма");
	КонецЕсли;
	Если Не КолонкиТаблицы.Найти("ИтогоСуммаНДС") = Неопределено Тогда
		КолонкиТаблицы.Удалить("ИтогоСуммаНДС");
	КонецЕсли;
	
	ПоискЕдиницыИзмеренияПоКоду = Не КолонкиТаблицы.Найти("КодЕдиницыИзмерения") = Неопределено;
	ВариантОбеспечения = ?(ТипДокументаСтрокой = "ЗаказКлиента",
		Перечисления.ВариантыОбеспечения.Отгрузить,
		Перечисления.ВариантыОбеспечения.НеТребуется);
		
	Для Каждого СтрокаТаблицыДокумента Из ТаблицаТовары Цикл
		
		ДанныеСтрокиТаблицы = Новый Структура();
		Для Каждого Колонка Из КолонкиТаблицы Цикл
			
			Если Колонка.Имя = "ЕдиницаИзмерения" Тогда
				ЕдиницаИзмерения = Неопределено;
				Если ЗначениеЗаполнено(СтрокаТаблицыДокумента.ЕдиницаИзмерения) Тогда
					 ЕдиницаИзмерения = СтрокаТаблицыДокумента.ЕдиницаИзмерения;
				ИначеЕсли ПоискЕдиницыИзмеренияПоКоду И ЗначениеЗаполнено(СтрокаТаблицыДокумента.КодЕдиницыИзмерения) Тогда
					 ЕдиницаИзмерения = Справочники.УпаковкиЕдиницыИзмерения.НайтиПоКоду(СтрокаТаблицыДокумента.КодЕдиницыИзмерения);
				ИначеЕсли ЕдиницаИзмерения = Неопределено Тогда  
					 ЕдиницаИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицыДокумента.Номенклатура, "ЕдиницаИзмерения");
				КонецЕсли;
				ДанныеСтрокиТаблицы.Вставить("Упаковка", ЕдиницаИзмерения);
			ИначеЕсли Колонка.Имя = "ТПодразделение" Тогда
				ДанныеСтрокиТаблицы.Вставить("Подразделение", СтрокаТаблицыДокумента[Колонка.Имя]);
			ИначеЕсли Колонка.Имя = "ТСклад" Тогда
				ДанныеСтрокиТаблицы.Вставить("Склад",
				?(ЗначениеЗаполнено(СтрокаТаблицыДокумента[Колонка.Имя]), СтрокаТаблицыДокумента[Колонка.Имя],
				ДокументОбъект.РеквизитыДокумента[31].Значение));
			ИначеЕсли Колонка.Имя = "СуммаСкидки" Тогда
				ДанныеСтрокиТаблицы.Вставить("СуммаРучнойСкидки", СтрокаТаблицыДокумента[Колонка.Имя]);
			ИначеЕсли Колонка.Имя = "ПроцентСкидки" Тогда
				ДанныеСтрокиТаблицы.Вставить("ПроцентРучнойСкидки", СтрокаТаблицыДокумента[Колонка.Имя]);
			Иначе
				ДанныеСтрокиТаблицы.Вставить(Колонка.Имя, СтрокаТаблицыДокумента[Колонка.Имя]);
			КонецЕсли;
			
		КонецЦикла;
		
		ДанныеСтрокиТаблицы.Вставить("КоличествоУпаковок", СтрокаТаблицыДокумента.Количество); 
		ДанныеСтрокиТаблицы.Вставить("СуммаСНДС", СтрокаТаблицыДокумента.Всего);
		ДанныеСтрокиТаблицы.Вставить("СуммаСНДСБезВозвратнойТары", СтрокаТаблицыДокумента.Всего);
		ДанныеСтрокиТаблицы.Вставить("ВариантОбеспечения", ВариантОбеспечения);
		ДанныеСтрокиТаблицы.Вставить("Коэффициент", 1);
		Если ТипДокументаСтрокой = "ЗаказПоставщику" И ЗначениеЗаполнено(ВидЦены) Тогда
			ДанныеСтрокиТаблицы.Вставить("ВидЦеныПоставщика", ВидЦены);	
		ИначеЕсли ТипДокументаСтрокой = "ЗаказКлиента" И ЗначениеЗаполнено(ВидЦены) Тогда
			ДанныеСтрокиТаблицы.Вставить("ВидЦены", ВидЦены);
		КонецЕсли;
		
		СтрокиТаблицыТовары.Добавить(ДанныеСтрокиТаблицы);
	КонецЦикла;
	
	ПараметрыЗаполнения.Вставить("ТоварыТЧ", СтрокиТаблицыТовары);
	
КонецПроцедуры

Процедура ОбработкаАвтоПодбораБанка(Текст, ДанныеВыбора, СтандартнаяОбработка, Параметры) Экспорт 
	
КонецПроцедуры

Процедура ОбработатьВыборБанка(ВыбранноеЗначение) Экспорт 
	
КонецПроцедуры

Процедура ПроверитьНомерСчетаБИК(Банк, БанкРаспознанныйТекст, НомерСчета) Экспорт
	
КонецПроцедуры

Процедура ИмяРеквизитаОбъекта(ИмяРеквизита, ТипОбъекта = Неопределено) Экспорт
	
КонецПроцедуры

Процедура ВидыОперацийПоступлениеТоваровУслуг(Результат) Экспорт
	
	Результат.Вставить("ЗакупкаУПоставщика", Перечисления.ХозяйственныеОперации.ЗакупкаУПоставщика)
	
КонецПроцедуры

Процедура ВидыОперацийРеализацияТоваров(Результат) Экспорт
	
	Результат.Вставить("РеализацияКлиенту", Перечисления.ХозяйственныеОперации.РеализацияКлиенту)
	
КонецПроцедуры  

Процедура ТекстЗапросаПоПервичнымДокументам(ИмяТипа, ТекстЗапроса, ЕстьДокументОснование = Истина) Экспорт
	
	ИмяТаблицы = ИмяТипа;
	
	Если ИмяТаблицы = "Документ.ЗаказПоставщику" Или ИмяТаблицы = "Документ.ЗаказКлиента"   
		Или ИмяТаблицы = "Документ.СчетНаОплатуКлиенту" Тогда
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ВЫРАЗИТЬ(ДанныеПервичныхДокументов.Ссылка КАК &ИмяТаблицы) КАК Документ
		|ПОМЕСТИТЬ ВсеДокументы
		|ИЗ
		|	РегистрСведений.РеестрДокументов КАК ДанныеПервичныхДокументов
		|ГДЕ
		|	ДанныеПервичныхДокументов.Ссылка ССЫЛКА &ИмяТаблицы
		|	И ДанныеПервичныхДокументов.Организация = &Организация
		|	И ДанныеПервичныхДокументов.ДатаДокументаИБ МЕЖДУ &ДатаНачала И &ДатаКонца
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВсеДокументы.Документ КАК Документ,
		|	&ДокументОснование КАК ДокументОснование
		|ИЗ
		|	ВсеДокументы КАК ВсеДокументы
		|ГДЕ
		|	НЕ ВсеДокументы.Документ.ПометкаУдаления
		|	И ВсеДокументы.Документ.Контрагент = &Контрагент
		|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);   
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументОснование", "NULL");
	Иначе	
		
		ТекстЗапроса = 
			"ВЫБРАТЬ
			|	ВЫРАЗИТЬ(ДанныеПервичныхДокументов.Документ КАК &ИмяТаблицы) КАК Документ
			|ПОМЕСТИТЬ ВсеДокументы
			|ИЗ
			|	РегистрСведений.ДанныеПервичныхДокументов КАК ДанныеПервичныхДокументов
			|ГДЕ
			|	ДанныеПервичныхДокументов.Документ ССЫЛКА &ИмяТаблицы
			|	И ДанныеПервичныхДокументов.Организация = &Организация
			|	И ДанныеПервичныхДокументов.Дата МЕЖДУ &ДатаНачала И &ДатаКонца
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеДокументы.Документ КАК Документ,
			|	&ДокументОснование КАК ДокументОснование
			|ИЗ
			|	ВсеДокументы КАК ВсеДокументы
			|ГДЕ
			|	НЕ ВсеДокументы.Документ.ПометкаУдаления
			|	И ВсеДокументы.Документ.Контрагент = &Контрагент
			|";
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ИмяТаблицы", ИмяТаблицы);
		Если ЕстьДокументОснование Тогда
			//1. СчетФактураПолученный
			//2. СчетФактураВыданный
			//3. ПриобретениеТоваровУслуг
			//4. РеализацияТоваровУслуг
			//6. СчетНаОплатуКлиенту
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументОснование",
				"ВЫБОР
				|	КОГДА ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетФактураПолученный)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетФактураВыданный)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.ПриобретениеТоваровУслуг)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.РеализацияТоваровУслуг)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.РеализацияУслугПрочихАктивов)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.ПриобретениеУслугПрочихАктивов)		
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.ЗаказКлиента)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.ЗаказПоставщику)   
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.СчетНаОплатуКлиенту)
				|		ИЛИ ТИПЗНАЧЕНИЯ(ВсеДокументы.Документ.ДокументОснование) = ТИП(Документ.АктВыполненныхРабот)		
				|		ТОГДА ВсеДокументы.Документ.ДокументОснование
				|	ИНАЧЕ NULL
				|КОНЕЦ"
			);
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ДокументОснование", "NULL");
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо, РезультатПроверки) Экспорт
	
	//++ НЕ УТ
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиковКлиентСервер.ПроверитьСоответствиеТребованиямИНН(ИНН, ЭтоЮрЛицо);
	//-- НЕ УТ
	
КонецПроцедуры

Процедура ПроверитьСоответствиеТребованиямКПП(КПП, ЭтоЮрЛицо, РезультатПроверки) Экспорт
	
	//++ НЕ УТ
	РезультатПроверки = ИдентификационныеНомераНалогоплательщиковКлиентСервер.ПроверитьСоответствиеТребованиямКПП(КПП, ЭтоЮрЛицо, Ложь);
	//-- НЕ УТ
	
КонецПроцедуры

Процедура ТребуетсяВключениеФункциональныхОпцийПоСкидкам(ВидСкидки, Направление, Результат) Экспорт
	
КонецПроцедуры

Процедура ЕстьПравоУстановкиФункциональныхОпцийПоСкидкам(ЕстьПраво, Направление) Экспорт
	
КонецПроцедуры

Процедура УстановитьФункциональныеОпцииПоСкидкам(Направление) Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьСправочникиБезПравНаСоздание(СправочникиБезПравНаСоздание) Экспорт
	
	Типы = РаспознаваниеДокументовТипы.Типы();
	
	ПроверяемыеЭлементы = Новый Структура;
	
	ПроверяемыеЭлементы.Вставить("Продавец", Типы.Контрагент);
	ПроверяемыеЭлементы.Вставить("Покупатель", Типы.Контрагент);
	ПроверяемыеЭлементы.Вставить("Грузоотправитель", Типы.Контрагент);
	ПроверяемыеЭлементы.Вставить("Грузополучатель", Типы.Контрагент);
	ПроверяемыеЭлементы.Вставить("Контрагент", Типы.Контрагент);
	ПроверяемыеЭлементы.Вставить("БанковскийСчетОрганизации", Типы.БанковскиеСчетаОрганизаций);
	ПроверяемыеЭлементы.Вставить("БанковскийСчетКонтрагента", Типы.БанковскиеСчетаКонтрагентов);
	ПроверяемыеЭлементы.Вставить("Договор", Типы.Договор);
	ПроверяемыеЭлементы.Вставить("Номенклатура", Типы.Номенклатура); 
	ПроверяемыеЭлементы.Вставить("Партнер", Типы.Партнер);
	ПроверяемыеЭлементы.Вставить("Характеристика", Типы.ХарактеристикаНоменклатуры);
	
	Для Каждого ПроверяемыйЭлемент Из ПроверяемыеЭлементы Цикл
		
		Тип = ПроверяемыйЭлемент.Значение.Типы()[0];
		
		ПустаяСсылка = Новый(Тип);
		ИмяСправочника = ПустаяСсылка.Метаданные().Имя;
		
		Если Не ПравоДоступа("Добавление", Метаданные.Справочники[ИмяСправочника]) Тогда
			СправочникиБезПравНаСоздание.Добавить(ПроверяемыйЭлемент.Ключ);
		КонецЕсли;
		
	КонецЦикла;
	
	СправочникиБезПравНаСоздание.Добавить("ЕдиницаИзмерения");
	СправочникиБезПравНаСоздание.Добавить("НомерГТД");
	СправочникиБезПравНаСоздание.Добавить("СтавкаНДС");

КонецПроцедуры

Процедура СозданиеЭлементаСОтображениемКартинкиПриСозданииНаСервере(Форма) Экспорт
	
	ПараметрыОткрытия = Форма.Параметры;
	Объект = Новый Структура("ТипДокумента, Направление");
	ЗаполнитьЗначенияСвойств(Объект, ПараметрыОткрытия);
	Форма.ПараметрыОткрытияФормыОбъекта = Новый Структура("ИмяЭлемента, НаборДанных, ДополнительныеДанные");
	ЗаполнитьЗначенияСвойств(Форма.ПараметрыОткрытияФормыОбъекта, ПараметрыОткрытия);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("Объект", Объект);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("ЗначениеПодходящейСтроки", ПараметрыОткрытия.СоздаваемыйОбъект);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("Направление", ПараметрыОткрытия.Направление);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("ТипДокумента", ПараметрыОткрытия.ТипДокумента);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("НаборДанных", ПараметрыОткрытия.НаборДанных);
	Форма.ПараметрыОткрытияФормыОбъекта.Вставить("ДополнительныеДанные", ПараметрыОткрытия.ДополнительныеДанные);
	
КонецПроцедуры

Процедура ГрупповаяОбработкаПриСозданииНаСервере(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихАктивовПассивов") 
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов") Тогда
		Элементы.ТаблицаСуммСтатья.Видимость = Ложь;
	КонецЕсли;
	
	Если  Не ПолучитьФункциональнуюОпцию("ИспользоватьУчетПрочихДоходовРасходов") Тогда
		Элементы.ТаблицаСуммСтатьяДоходов.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаСозданияКорректировкиПоступленияРеализации(СозданныеДокументы, ОбработкаОбъект) Экспорт
	
КонецПроцедуры

Процедура ОбработкаСозданияВозвратаТоваровПоставщику(СозданныеДокументы, ОбработкаОбъект) Экспорт
	
КонецПроцедуры

Процедура СтавкиНДСПоДатеРаспознанногоДокумента(ДатаДокумента, ДоступныеСтавки) Экспорт
	
	До2019Года = ДатаДокумента < '20190101' И ДатаДокумента > '00010101';
	
	СтавкиНДС = Новый Массив;
	Если До2019Года Тогда
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС18);
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС18_118);
	Иначе
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС20);
		СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС20_120);
	КонецЕсли;
	
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС10);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС10_110);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.НДС0);
	СтавкиНДС.Добавить(Перечисления.СтавкиНДС.БезНДС);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СтавкиНДС.Ссылка КАК СтавкаНДС
		|ИЗ
		|	Справочник.СтавкиНДС КАК СтавкиНДС
		|ГДЕ
		|	СтавкиНДС.ПеречислениеСтавкаНДС В(&СтавкиНДС)
		|	И НЕ СтавкиНДС.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СтавкиНДС", СтавкиНДС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если ДоступныеСтавки.НайтиПоЗначению(Выборка.СтавкаНДС) = Неопределено Тогда
			ДоступныеСтавки.Добавить(Выборка.СтавкаНДС);
		КонецЕсли;
	КонецЦикла;
		
КонецПроцедуры

Процедура ПриОбработкеДокументовСоСкидками(ВидСкидки, Направление, Результат) Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьПредупрежденияПоСкидкам(РеквизитыШапки, Форма) Экспорт
	
КонецПроцедуры

Процедура ДоступноРаспознаваниеЧеков(ДоступноРаспознаваниеЧеков) Экспорт
	
КонецПроцедуры

Процедура ДоступноСозданиеПлатежныхПоручений(ДоступноСозданиеПлатежныхПоручений) Экспорт
	
КонецПроцедуры

Процедура ПриПолученииДанныхВыбораТипыДокументов(ДанныеВыбора, Параметры, СтандартнаяОбработка) Экспорт
	
КонецПроцедуры

Процедура ЗаполнитьСтрокиТаблицыТоварыКорректировкиИзОснований(Объект) Экспорт
		
КонецПроцедуры

Процедура НайтиИЗаполнитьДокументыОснованияКорректировки(Объект) Экспорт
		
КонецПроцедуры

Процедура ПриЗаписиАвансовогоОтчета(Источник) Экспорт
		
КонецПроцедуры

// Установить условное оформление доп реквизитов (Групповая обработка)
//
// Параметры:
//  Форма	 - Управляемая форма 
//  НаборЦветов - Структура
//
Процедура УстановитьУсловноеОформлениеДопРеквизитовГрупповаяОбработка(Форма, НаборЦветов) Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	
	// Договор
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", НаборЦветов.КрасныйЦвет);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Текст", Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.ДоговорРаспознано"));
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.Договор",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, 
		"ТаблицаДопРеквизитов.ИспользуютсяДоговорыКонтрагентов",
		ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовДоговор");
	
	// Партнер (Видимость)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор, 
		"ТаблицаДопРеквизитов.ИспользоватьПартнеров", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПартнер");
	
	// Партнер (ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор, 
		"ТаблицаДопРеквизитов.Партнер", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПартнер");
	
	// Склад	
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоСкладов") Тогда
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
		
		ВидыДокумента = Новый СписокЗначений;
		ВидыДокумента.Добавить("РУИПА");
		ВидыДокумента.Добавить("ПУИПА");
		
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
			"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ВидДокумента",
			ВидСравненияКомпоновкиДанных.ВСписке, ВидыДокумента);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ТипДокумента",
			ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСклад");
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
		
		СписокОтбора = Новый СписокЗначений;
		СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.УПД);
		СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12);
		СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетФактура);
		СписокОтбора.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.АктОбОказанииУслуг);
		
		ВидыДокумента = Новый СписокЗначений;
		ВидыДокумента.Добавить("ПТИУ");
		ВидыДокумента.Добавить("РТИУ");
				
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
			"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.Склад",
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ТипДокумента",
			ВидСравненияКомпоновкиДанных.ВСписке, СписокОтбора);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ВидДокумента",
			ВидСравненияКомпоновкиДанных.ВСписке, ВидыДокумента);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ЕстьТовары",
			ВидСравненияКомпоновкиДанных.Равно, Истина);
		
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСклад");
		
	Иначе
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();	
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСклад");
		
	КонецЕсли;
	
	// Соглашение 
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор, 
		"ТаблицаДопРеквизитов.ИспользоватьСоглашение", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСоглашение");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ИспользоватьСоглашение",
		ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.Соглашение",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСоглашение");
	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	Если ПолучитьФункциональнуюОпцию("ИспользоватьПартнеровИКонтрагентов") Тогда
		ИмяПоля = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.Партнер");
	Иначе
		ИмяПоля = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитов.Контрагент");
	КонецЕсли;
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ИспользоватьСоглашение",
		ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, ИмяПоля, ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовСоглашение");
	
	// Подразделение 
	
	Если ПолучитьФункциональнуюОпцию("ФормироватьВидыЗапасовПоПодразделениямМенеджерам") Тогда
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
			"ТаблицаДопРеквизитов.Направление", ВидСравненияКомпоновкиДанных.Равно,
			Перечисления.НаправленияРаспознанногоДокумента.Входящий);
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПодразделение");
			
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
		ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
			"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.Направление",
			ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Исходящий);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.Подразделение",
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаДопРеквизитов.ТипДокумента",
			ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПодразделение");
		
	Иначе
		
		ЭлементОформления = УсловноеОформление.Элементы.Добавить();
		ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаДопРеквизитовПодразделение");
		
	КонецЕсли;
	
КонецПроцедуры

// Установить условное оформление таблица номенклатуры (Групповая обработка)
//
// Параметры:
//  Форма	 - Управляемая форма 
//  НаборЦветов - Структура
//
Процедура УстановитьУсловноеОформлениеШагСуммыГрупповаяОбработка(Форма, НаборЦветов) Экспорт
	
	УсловноеОформление = Форма.УсловноеОформление;
	
	// Статья доходов (ТолькоПросмотр, Отображать)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
			
	ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.УчетПрочихДоходовРасходов",
		ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, "РУИПА");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
			
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяДоходов");
	
	// СтатьяДоходов (ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.СтатьяДоходов",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.Равно, "РУИПА");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяДоходов");

	// Статья (ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
		
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.Статья",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.УчетПрочихДоходовРасходов",
		ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.УчетПрочихАктивовПассивов",
		ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.Равно, "ПУИПА");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
			
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатья");
	
	// Статья (ТолькоПросмотр, ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);

	ГруппаИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.УчетПрочихДоходовРасходов",
		ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.УчетПрочихАктивовПассивов",
		ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, "ПУИПА");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИли,
		"ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатья");
		
	// СписатьНаРасходы, СтатьяРасходов (ТолькоПросмотр, Отображать)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"ТаблицаСумм.УчетПрочихДоходовРасходов", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСписатьНаРасходы");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");
	
	// СписатьНаРасходы, СтатьяРасходов (ТолькоПросмотр не для Счетов на оплату)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);

	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, "ПТИУ");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, "");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.НеРавно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСписатьНаРасходы");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");
	
	// СписатьНаРасходы, СтатьяРасходов (ТолькоПросмотр для Счетов на оплату)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
		
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.Направление",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Исходящий);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСписатьНаРасходы");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");
	
	// СтатьяРасходов (ЦветФона не для Счетов на оплату)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.СтатьяРасходов", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.СписатьНаРасходы", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ВидДокумента", ВидСравненияКомпоновкиДанных.Равно, "ПТИУ");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ТипДокумента", ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");
	
	// СтатьяРасходов (ЦветФона для Счетов на оплату)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.СтатьяРасходов", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.СписатьНаРасходы", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.Направление", ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ТипДокумента", ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
		
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");
	
	// СтатьяРасходов (ТолькоПросмотр, Отображать)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Отображать", Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"ТаблицаСумм.СписатьНаРасходы", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтатьяРасходов");

	// Подразделение (ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	ЭлементОформления.Представление = "ТаблицаСуммТПодразделение_ЦветФона";
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.СписатьНаРасходы", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ТПодразделение", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.Направление", ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"ТаблицаСумм.ВидДокумента", ВидСравненияКомпоновкиДанных.Равно,
		"ПТИУ");
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммТПодразделение"); 
	
	// Склад (Видимость)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	ИспользоватьУчетПоНесколькимСкладам = Ложь;
	ПолучитьФункциональнуюОпциюУчетПоНесколькимСкладам(ИспользоватьУчетПоНесколькимСкладам);
	
	Если ИспользоватьУчетПоНесколькимСкладам Тогда
		
		ВидыДокумента = Новый СписокЗначений;
		ВидыДокумента.Добавить("ПТИУ");
		ВидыДокумента.Добавить("РТИУ");
		ВидыДокумента.Добавить("");

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
			"ТаблицаСумм.ВидДокумента", ВидСравненияКомпоновкиДанных.НеВСписке, ВидыДокумента);
		
	КонецЕсли;
		
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммТСклад");
	
	// Склад (Видимость)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементОформления.Отбор,
		"ТаблицаСумм.СкладГруппа", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммТСклад");
	
	// Склад (Видимость)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	ГруппаОтбораИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораИли,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	СписокТиповРабота = Новый СписокЗначений;
	СписокТиповРабота.Добавить(Перечисления.ТипыНоменклатуры.Работа);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.ВСписке, СписокТиповРабота);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ, "ТаблицаСумм.Направление", 
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Исходящий);

	ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораИли,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	СписокТиповРаботаУслуга = Новый СписокЗначений;
	СписокТиповРаботаУслуга.Добавить(Перечисления.ТипыНоменклатуры.Работа);
	СписокТиповРаботаУслуга.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.ВСписке, СписокТиповРаботаУслуга);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ, "ТаблицаСумм.Направление", 
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли, "ТаблицаСумм.ВидДокумента",
		ВидСравненияКомпоновкиДанных.Равно, "ПУИПА");

	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммТСклад");
	
	// Отключение видимости подразделения, если товар или тара или услуга - для исх., если товар или тара - для вх.	
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	ЭлементОформления.Представление = "ТаблицаСуммТПодразделение_Видимость";

	ГруппаОтбораИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

	ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораИли,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	СписокТиповТоварТараУслуга = Новый СписокЗначений;
	СписокТиповТоварТараУслуга.Добавить(Перечисления.ТипыНоменклатуры.Товар);
	СписокТиповТоварТараУслуга.Добавить(Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
	СписокТиповТоварТараУслуга.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.ВСписке, СписокТиповТоварТараУслуга);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ, "ТаблицаСумм.Направление",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Исходящий);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.СкладГруппа", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ГруппаОтбораИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаОтбораИли,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	СписокТиповТоварТара = Новый СписокЗначений;
	СписокТиповТоварТара.Добавить(Перечисления.ТипыНоменклатуры.Товар);
	СписокТиповТоварТара.Добавить(Перечисления.ТипыНоменклатуры.МногооборотнаяТара);
		
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.ТипНоменклатуры", ВидСравненияКомпоновкиДанных.ВСписке, СписокТиповТоварТара);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ, "ТаблицаСумм.Направление",
		ВидСравненияКомпоновкиДанных.Равно, Перечисления.НаправленияРаспознанногоДокумента.Входящий);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИ,
		"ТаблицаСумм.СкладГруппа", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммТПодразделение");
		
	// НомерГТД (ЦветФона)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.РозовыйЦвет);
	
	СписокИсключенийТиповДокументов = Новый СписокЗначений;
	СписокИсключенийТиповДокументов.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.СчетНаОплату);
	СписокИсключенийТиповДокументов.Добавить(Перечисления.ТипыДокументовРаспознаваниеДокументов.ТОРГ12);
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.НомерГТД",
		ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ВедетсяУчетПоГТД",
		ВидСравненияКомпоновкиДанных.Равно, Истина);	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ, "ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.НеВСписке, СписокИсключенийТиповДокументов);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммНомерГТД");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтранаПроисхождения");
	
	// НомерГТД (ЦветФона, ТолькоПросмотр)
	ЭлементОформления = УсловноеОформление.Элементы.Добавить();
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", НаборЦветов.СерыйЦвет);
	
	ГруппаОтбораИли = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементОформления.Отбор.Элементы,
		"", ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли, "ТаблицаСумм.ВедетсяУчетПоГТД",
		ВидСравненияКомпоновкиДанных.Равно, Ложь);	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли, "ТаблицаСумм.ТипДокумента",
		ВидСравненияКомпоновкиДанных.ВСписке, СписокИсключенийТиповДокументов);
	
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммНомерГТД");
	ОформляемоеПоле = ЭлементОформления.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных("ТаблицаСуммСтранаПроисхождения");
	
КонецПроцедуры

// Исключает комплектные документы, если на какой-то документ у пользователя нет права на "Добавление"
//
// Параметры:
//  КомплектныеДокументы - Массив из ДокументСсылка.РаспознанныйДокумент 
//  ОбязательныйДокумент - ДокументСсылка.РаспознанныйДокумент 
//
Процедура СкорректироватьКомплектДокументовПоПравуДоступаДобавление(КомплектныеДокументы, ОбязательныйДокумент) Экспорт
	
	Направление = Перечисления.НаправленияРаспознанногоДокумента.ПустаяСсылка();
	ВидДокумента = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РаспознанныйДокумент.Направление КАК Направление,
	|	ЕСТЬNULL(РаспознанныйДокументРеквизитыДокумента.Значение, """") КАК ВидДокумента
	|ИЗ
	|	Документ.РаспознанныйДокумент КАК РаспознанныйДокумент
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РаспознанныйДокумент.РеквизитыДокумента КАК РаспознанныйДокументРеквизитыДокумента
	|		ПО РаспознанныйДокумент.Ссылка = РаспознанныйДокументРеквизитыДокумента.Ссылка
	|			И (РаспознанныйДокументРеквизитыДокумента.ИмяРеквизита = ""ВидДокумента"")
	|ГДЕ
	|	РаспознанныйДокумент.Ссылка = &ОбязательныйДокумент";
	
	Запрос.УстановитьПараметр("ОбязательныйДокумент", ОбязательныйДокумент);
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		Направление = Выборка.Направление;
		ВидДокумента = Выборка.ВидДокумента;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ВидДокумента = ?(Направление = Перечисления.НаправленияРаспознанногоДокумента.Входящий, "ПТИУ", "РТИУ");
	КонецЕсли;
			
	ТипКомплекта = РаспознаваниеДокументовКомплекты.СформироватьТипКомплекта(КомплектныеДокументы, "ПоТаблице");
	ПараметрыСоздания = Новый Структура("НаправлениеДокумента, ВидДокумента, ТипКомплекта", Направление,
		ВидДокумента, ТипКомплекта);
	ТипыДокументов = РаспознаваниеДокументовКомплектыКлиентСервер.СоздаваемыеДокументыКомплекта(ПараметрыСоздания);
	
	ПравоДобавления = Истина;	
	Для Каждого ТипДокумента Из ТипыДокументов Цикл	
		Если Не ПравоДоступа("Добавление", Метаданные.НайтиПоТипу(ТипДокумента)) Тогда
			ПравоДобавления = Ложь;
		    Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПравоДобавления Тогда
		КомплектныеДокументы = Новый Массив;
		КомплектныеДокументы.Добавить(ОбязательныйДокумент);
	КонецЕсли;

КонецПроцедуры // СкорректироватьКомплектПоПравуДобавления()
	
#Область ЗаполнениеНоменклатурыПоВидуНоменклатуры

Процедура ЗаполнитьНоменклатуруПоВидуНоменклатуры(НоменклатураОбъект)
	
	РеквизитыВидаНоменклатуры = Неопределено;
	ПравилаНаименований = Новый Структура("ФормироватьРабочееНаименование, ФормироватьНаименованиеДляПечати", Ложь, Ложь);
	ОбновитьКешРеквизитовВидаНоменклатуры(НоменклатураОбъект, РеквизитыВидаНоменклатуры, ПравилаНаименований);
	Справочники.Номенклатура.ЗаполнитьРеквизитыПоВидуНоменклатуры(НоменклатураОбъект);
	
	Если ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонРабочегоНаименованияНоменклатуры) Тогда
		ПравилаНаименований.ФормироватьРабочееНаименование = Истина;
	КонецЕсли;

	Если ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонНаименованияДляПечатиНоменклатуры) Тогда
		ПравилаНаименований.ФормироватьНаименованиеДляПечати = Истина;
	КонецЕсли;
	
	ТаблицаНастроекРеквизитов = Справочники.Номенклатура.ТаблицаНастроекРеквизитов(НоменклатураОбъект.ВидНоменклатуры,
		НоменклатураОбъект.ТипНоменклатуры, НоменклатураОбъект.ОсобенностьУчета, 
		НоменклатураОбъект.ИспользованиеХарактеристик, РеквизитыВидаНоменклатуры.ИспользоватьСрокГодностиСерии,
		"Номенклатура");
		
	ИспользуетсяИндивидуальныйШаблонЦенника  = ЗначениеЗаполнено(НоменклатураОбъект.ШаблонЦенника)
		И Не НоменклатураОбъект.ШаблонЦенника = РеквизитыВидаНоменклатуры.ШаблонЦенника;
	ИспользуетсяИндивидуальныйШаблонЭтикетки = ЗначениеЗаполнено(НоменклатураОбъект.ШаблонЭтикетки)
		И Не НоменклатураОбъект.ШаблонЭтикетки = РеквизитыВидаНоменклатуры.ШаблонЭтикетки;
	НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЦенника  = ИспользуетсяИндивидуальныйШаблонЦенника;
	НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЭтикетки = ИспользуетсяИндивидуальныйШаблонЭтикетки;
	НоменклатураОбъект.ШаблонЦенника  = ?(ИспользуетсяИндивидуальныйШаблонЦенника, НоменклатураОбъект.ШаблонЦенника,
		Справочники.ШаблоныЭтикетокИЦенников.ПустаяСсылка());
	НоменклатураОбъект.ШаблонЭтикетки = ?(ИспользуетсяИндивидуальныйШаблонЭтикетки, НоменклатураОбъект.ШаблонЭтикетки,
		Справочники.ШаблоныЭтикетокИЦенников.ПустаяСсылка());
	
	ОтборНастроекШаблонаЦенников = Новый Структура("ИмяРеквизита", "ШаблонЦенника");
	НастройкиШаблонаЦенников     = ТаблицаНастроекРеквизитов.НайтиСтроки(ОтборНастроекШаблонаЦенников);
	Если НастройкиШаблонаЦенников.Количество() > 0 Тогда
		
		Если Не НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЦенника
			И НастройкиШаблонаЦенников[0].ЗаполнятьОбязательно
			И Не ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонЦенника) Тогда
			
			ИспользоватьИндивидуальныйШаблонЦенника = 1;
			НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЦенника = Истина;
			
		ИначеЕсли Не НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЦенника Тогда
			ИспользоватьИндивидуальныйШаблонЦенника = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	ОтборНастроекШаблонаЭтикеток = Новый Структура("ИмяРеквизита", "ШаблонЭтикетки");
	НастройкиШаблонаЭтикеток     = ТаблицаНастроекРеквизитов.НайтиСтроки(ОтборНастроекШаблонаЭтикеток);
	
	Если НастройкиШаблонаЭтикеток.Количество() > 0 Тогда
		
		Если Не НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЭтикетки
			И НастройкиШаблонаЭтикеток[0].ЗаполнятьОбязательно
			И Не ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонЭтикетки) Тогда
			
			ИспользоватьИндивидуальныйШаблонЭтикетки = 1;
			НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЭтикетки = Истина;
			
		ИначеЕсли Не НоменклатураОбъект.ИспользоватьИндивидуальныйШаблонЭтикетки Тогда
			ИспользоватьИндивидуальныйШаблонЭтикетки = 0;
		КонецЕсли;
		
	КонецЕсли;
	
	ВариантыФормирования = Новый Массив;
	Если ПравилаНаименований.ФормироватьРабочееНаименование Тогда
		ВариантыФормирования.Добавить("Рабочее");
	КонецЕсли;	
	Если ПравилаНаименований.ФормироватьНаименованиеДляПечати Тогда
		ВариантыФормирования.Добавить("ДляПечати");
	КонецЕсли;
	РезультатЗаполнения = Неопределено;
	ЗаполнитьНаименованиеПоШаблонуСервер(НоменклатураОбъект, СтрСоединить(ВариантыФормирования, ", "),
		РеквизитыВидаНоменклатуры, РезультатЗаполнения, ПравилаНаименований);
	Если РезультатЗаполнения.ЕстьОшибки Тогда
		ВызватьИсключение РезультатЗаполнения.ТекстОшибки;
	КонецЕсли;

КонецПроцедуры

Процедура ОбновитьКешРеквизитовВидаНоменклатуры(НоменклатураОбъект, РеквизитыВидаНоменклатуры, ПравилаНаименований)
	
	ИменаРеквизитов =
	"ШаблонРабочегоНаименованияНоменклатуры,
	|ЗапретРедактированияРабочегоНаименованияНоменклатуры,
	|ШаблонНаименованияДляПечатиНоменклатуры,
	|ЗапретРедактированияНаименованияДляПечатиНоменклатуры,
	|НастройкаИспользованияСерий,
	|СодержитДрагоценныеМатериалы,
	|ИспользованиеХарактеристик,
	|ИспользоватьСерии,
	|ИспользоватьСрокГодностиСерии,
	|НаборУпаковок,
	|ГруппаДоступа,
	|НаборСвойств,
	|ТипНоменклатуры,
	|ШаблонЦенника,
	|ШаблонЭтикетки";
	
	РеквизитыВидаНоменклатуры = Новый ФиксированнаяСтруктура(
		ОбщегоНазначенияУТ.ЗначенияРеквизитовОбъектаПоУмолчанию(НоменклатураОбъект.ВидНоменклатуры, ИменаРеквизитов));
	ПересчитатьНеобходимостьФормированияНаименований(НоменклатураОбъект, РеквизитыВидаНоменклатуры, ПравилаНаименований);
	
КонецПроцедуры

Процедура ПересчитатьНеобходимостьФормированияНаименований(НоменклатураОбъект,
															РеквизитыВидаНоменклатуры,
															ПравилаНаименований)

	ПравилаНаименований.ФормироватьРабочееНаименование =
		РеквизитыВидаНоменклатуры.ЗапретРедактированияРабочегоНаименованияНоменклатуры
		Или (Не ЗначениеЗаполнено(НоменклатураОбъект.Наименование)
		И ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонРабочегоНаименованияНоменклатуры));
	ПравилаНаименований.ФормироватьНаименованиеДляПечати =
		РеквизитыВидаНоменклатуры.ЗапретРедактированияНаименованияДляПечатиНоменклатуры
		Или (Не ЗначениеЗаполнено(НоменклатураОбъект.НаименованиеПолное)
		И ЗначениеЗаполнено(РеквизитыВидаНоменклатуры.ШаблонНаименованияДляПечатиНоменклатуры));

КонецПроцедуры //ПересчитатьНеобходимостьФормированияНаименований()

Процедура ЗаполнитьНаименованиеПоШаблонуСервер(НоменклатураОбъект, 
												ВариантыФормирования, 
												РеквизитыВидаНоменклатуры, 
												РезультатЗаполнения,
												ПравилаНаименований)
	
	РезультатЗаполнения = Новый Структура;
	РезультатЗаполнения.Вставить("ЕстьОшибки", Ложь);
	РезультатЗаполнения.Вставить("ТекстОшибки", "");
	
	Если Не ЗначениеЗаполнено(ВариантыФормирования) Тогда
		Возврат;
	КонецЕсли;
	
	МассивВариантов = СтрРазделить(ВариантыФормирования, ", ", Ложь);
	Если МассивВариантов.Найти("Рабочее") <> Неопределено Тогда
		
		НовоеРабочееНаименование = НоменклатураСервер.НаименованиеПоШаблону(
			РеквизитыВидаНоменклатуры.ШаблонРабочегоНаименованияНоменклатуры,
			НоменклатураОбъект);

		НоменклатураОбъект.Наименование = НовоеРабочееНаименование;
		
		ДлинаРабочегоНаименования = СтрДлина(НовоеРабочееНаименование);
		МаксимальнаяДлинаРабочегоНаименования = Метаданные.Справочники.Номенклатура.ДлинаНаименования;
		
		Если ДлинаРабочегоНаименования > МаксимальнаяДлинаРабочегоНаименования Тогда
			
			Если ТекущийЯзык().КодЯзыка = "ru" Тогда
				
				МассивПредставлений = ПолучитьСклоненияСтрокиПоЧислу("символ", ДлинаРабочегоНаименования,, "L=ru_RU");
				Если МассивПредставлений.Количество() > 0 Тогда
					ПредставлениеДлины = МассивПредставлений.Получить(0);
				Иначе
					ПредставлениеДлины = Строка(ДлинаРабочегоНаименования);
				КонецЕсли;
				МассивПредставлений = ПолучитьСклоненияСтрокиПоЧислу("символ", МаксимальнаяДлинаРабочегоНаименования,, "L=ru_RU");
				Если МассивПредставлений.Количество() > 0 Тогда
					ПредставлениеМаксимальнойДлины = МассивПредставлений.Получить(0);
				Иначе
					ПредставлениеМаксимальнойДлины = Строка(МаксимальнаяДлинаРабочегоНаименования);
				КонецЕсли;
				
			Иначе
				ПредставлениеДлины             = Строка(ДлинаРабочегоНаименования);
				ПредставлениеМаксимальнойДлины = Строка(МаксимальнаяДлинаРабочегоНаименования);
			КонецЕсли;
			
			РезультатЗаполнения.ЕстьОшибки = Истина;
			РезультатЗаполнения.ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Длина сформированного по шаблону наименования (%1) превышает установленное ограничение (%2).';
					|en = 'The length of the name generated from the template (%1) exceeds the maximum value (%2).'"),
				ПредставлениеДлины,
				ПредставлениеМаксимальнойДлины);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если МассивВариантов.Найти("ДляПечати") <> Неопределено Тогда
		
		НоменклатураОбъект.НаименованиеПолное = НоменклатураСервер.НаименованиеПоШаблону(
			РеквизитыВидаНоменклатуры.ШаблонНаименованияДляПечатиНоменклатуры,
			НоменклатураОбъект);
		
	КонецЕсли;
	
	ПересчитатьНеобходимостьФормированияНаименований(НоменклатураОбъект, РеквизитыВидаНоменклатуры, ПравилаНаименований);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти