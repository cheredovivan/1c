#Область ПрограммныйИнтерфейс

#Область РазрешительныйРежим

// Инициализация конфигурации Локального модуля Честный знак. Выполняется для первичной установки и настройки
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
//  ИдентификаторФискальногоНакопителя - Строка - идентификатор фискального накопителя
// 
// Возвращаемое значение:
//  Структура - результат иницициализации:
// * ТребуетсяОбновлениеКлючаСессииРозница - Булево - признак необходимости обновления ключа сессии
// * ОтказАвторизации - Булево - Истина, если переданы некорректные данные администратора ЛМ ЧЗ
// * РезультатОтправкиЗапроса - Неопределено,
// 		см. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON
// * ТекстОшибки - Строка - текст ошибки, полученной от ЛМ ЧЗ
Функция ИнициализацияКонфигурацииЛокальногоМодуляЧЗ(Организация, ПараметрыУстановки, ИдентификаторФискальногоНакопителя = "") Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПВызовСервера.ПроверитьОбновитьКлючСессии(ПараметрыЗапросаКлючаСессииИСМПРозница(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессииРозница", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("ОтказАвторизации",                      Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",              Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                           "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ПараметрыУстановки.Логин, ПараметрыУстановки.Пароль));
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
	
	Если ЗначениеЗаполнено(ИдентификаторФискальногоНакопителя) Тогда
		ЗаголовокHTTP.Вставить("X-ClientId",     ИдентификаторФискальногоНакопителя);
	КонецЕсли;
	
	URLЗапроса = "api/v1/init";
	
	ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки);
	
	ПараметрыЗапроса = Новый Структура("token", КлючСессии);
	ТелоЗапросаJSON  = ОбщегоНазначенияИСКлиентСервер.ОбъектВТекстJSON(ПараметрыЗапроса, Истина);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		ТелоЗапросаJSON,
		,
		"POST",
		ПараметрыЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ОбработатьОтветИнициализацияКонфигурацииЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Запрос статуса системы Локального модуля Честный знак. Выполняется для проверки актуального состояния
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
// 
// Возвращаемое значение:
//  Структура - результат иницициализации:
// * ОтказАвторизации - Булево - Истина, если переданы некорректные данные администратора ЛМ ЧЗ
// * РезультатОтправкиЗапроса - (См. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON). 
// * ТекстОшибки - Строка - текст ошибки, полученной от ЛМ ЧЗ
// * ВерсияСборки - Строка - номер версии сборки ЛМ ЧЗ
// * Статус - Строка - статус текущего состояния системы - готова к работе, в процессе инициализации, ошибка синхронизации
// * ДатаПоследнейСинхронизации - Дата - дата последней синхронизации системы
// * ТребуетсяЗагрузка - Булево - Истина, если требуется обновление токена
Функция ЗапроситьСтатусЛокальногоМодуляЧЗ(Организация, ПараметрыУстановки) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ОтказАвторизации",           Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",   Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                "");
	ВозвращаемоеЗначение.Вставить("ВерсияСборки",               "");
	ВозвращаемоеЗначение.Вставить("Статус",                     "");
	ВозвращаемоеЗначение.Вставить("ДатаПоследнейСинхронизации", Дата(1, 1, 1));
	ВозвращаемоеЗначение.Вставить("ТребуетсяЗагрузка",          Ложь);
	
	ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ПараметрыУстановки.Логин, ПараметрыУстановки.Пароль));
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
	
	URLЗапроса = "api/v1/status";
	
	ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		,,
		"GET",
		ПараметрыЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ОбработатьОтветЗапроситьСтатусЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Изменение пароля администратора системы Локального модуля Честный знак. Выполняется по желанию
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
//  НовыйПароль - Строка - согласно API ЛМ ЧЗ должна отвечать следующим требованиям:
//		Пароль не может содержать пустую строку
//		Длина пароля должна быть не менее 8 символов. 
//		Пароль должен содержать буквы и цифры
// 
// Возвращаемое значение:
//  Структура - результат иницициализации:
// * ОтказАвторизации - Булево - Истина, если переданы некорректные данные администратора ЛМ ЧЗ
// * РезультатОтправкиЗапроса - (См. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON). 
// * ТекстОшибки - Строка - текст ошибки, полученной от ЛМ ЧЗ
// * ОшибкаПроверкиНовогоПароля - Булево - если передан пароль, не соответствующий заявленным параметрам
Функция ИзменитьПарольАдминистратораЛокальногоМодуляЧЗ(Организация, ПараметрыУстановки, НовыйПароль) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ОтказАвторизации",           Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",   Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                "");
	ВозвращаемоеЗначение.Вставить("ОшибкаПроверкиНовогоПароля", Ложь);
	
	ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ПараметрыУстановки.Логин, ПараметрыУстановки.Пароль));
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
	
	URLЗапроса = "api/v1/changePassword";
	
	ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки);
	
	ПараметрыЗапроса = Новый Структура("newPassword", НовыйПароль);
	ТелоЗапросаJSON  = ОбменДаннымиИСМПКлиентСервер.ОбъектВТекстJSON(ПараметрыЗапроса, Истина);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		ТелоЗапросаJSON,
		,
		"PUT",
		ПараметрыЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ОбработатьОтветИзменитьПарольАдминистратораЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Настройка товарных групп Локального модуля Честный знак. Выполняется для ограничения объемов выгрузки блэк-листов
// до используемых товарных групп
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
//  ПараметрыНастройкиТоварныхГрупп - Соответствие из КлючИЗначение:
//  	Ключ - ПеречислениеСсылка.ВидыПродукцииИС - товарная группа для настройки
//  	Значение - Число - количество дней для выгрузки карточек КИ
//  ИдентификаторФискальногоНакопителя - Строка - идентификатор фискального накопителя
// 
// Возвращаемое значение:
//  Структура - результат иницициализации:
// * ТребуетсяОбновлениеКлючаСессииРозница - Булево - признак необходимости обновления ключа сессии
// * ОтказАвторизации - Булево - Истина, если переданы некорректные данные администратора ЛМ ЧЗ
// * РезультатОтправкиЗапроса - Неопределено,
// 		см. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON
// * ТекстОшибки - Строка - текст ошибки, полученной от ЛМ ЧЗ
Функция НастройкаТоварныхГруппЛокальногоМодуляЧЗ(Организация, ПараметрыУстановки, ПараметрыНастройкиТоварныхГрупп, ИдентификаторФискальногоНакопителя = "") Экспорт
	
	КлючСессии = ИнтерфейсАвторизацииИСМПВызовСервера.ПроверитьОбновитьКлючСессии(ПараметрыЗапросаКлючаСессииИСМПРозница(Организация));
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбновлениеКлючаСессииРозница", КлючСессии = Неопределено);
	ВозвращаемоеЗначение.Вставить("ОтказАвторизации",                      Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса",              Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                           "");
	
	Если ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница Тогда
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ПараметрыУстановки.Логин, ПараметрыУстановки.Пароль));
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
	
	Если ЗначениеЗаполнено(ИдентификаторФискальногоНакопителя) Тогда
		ЗаголовокHTTP.Вставить("X-ClientId",     ИдентификаторФискальногоНакопителя);
	КонецЕсли;
	
	URLЗапроса = "api/v1/groups";
	
	ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки);
	
	ПараметрыЗапроса = Новый Структура();
	
	ПараметрыЗапроса.Вставить("token", КлючСессии);
	
	Если ПараметрыНастройкиТоварныхГрупп.Количество() Тогда
		ПараметрыЗапроса.Вставить("productsGroupSettings", ПараметрыНастройкиТоварныхГрупп);
	КонецЕсли;
	
	ТелоЗапросаJSON  = ОбменДаннымиИСМПКлиентСервер.ОбъектВТекстJSON(ПараметрыЗапроса, Истина);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		ТелоЗапросаJSON,
		,
		"POST",
		ПараметрыЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ОбработатьОтветНастройкаТоварныхГруппЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Запрос конфигурации системы Локального модуля Честный знак. Выполняется для проверки актуальной конфигурации
// 
// Параметры:
//  Организация - ОпределяемыйТип.Организация - Организация
//  ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
// 
// Возвращаемое значение:
//  Структура - результат иницициализации:
// * ОтказАвторизации - Булево - Истина, если переданы некорректные данные администратора ЛМ ЧЗ
// * РезультатОтправкиЗапроса - (См. ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON).
// * ТекстОшибки - Строка - текст ошибки, полученной от ЛМ ЧЗ
// * АдресСервернойЧасти - Строка - адрес серверной части ЛМ ЧЗ
// * НастройкаТоварныхГрупп - Неопределено, Соответствие из КлючИЗначение - настройка загруженных карточек ТГ
Функция ЗапроситьКонфигурациюЛокальногоМодуляЧЗ(Организация, ПараметрыУстановки) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("ОтказАвторизации",         Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатОтправкиЗапроса", Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",              "");
	ВозвращаемоеЗначение.Вставить("АдресСервернойЧасти",      "");
	ВозвращаемоеЗначение.Вставить("НастройкаТоварныхГрупп",   Неопределено);
	
	ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ПараметрыУстановки.Логин, ПараметрыУстановки.Пароль));
	
	ЗаголовокHTTP = Новый Соответствие();
	ЗаголовокHTTP.Вставить("Accept",         "application/json");
	ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
	ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
	
	URLЗапроса = "api/v1/config";
	
	ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки);
	
	РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
		URLЗапроса,
		,,
		"GET",
		ПараметрыЗапросов,
		ЗаголовокHTTP);
	
	РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	ОбработатьОтветЗапросКонфигурацииЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса);
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Получить данные по кодам идентификации из Локального модуля Честный знак.
// 
// Параметры:
//  ДанныеПроверки - Массив Из СтрокаТабличнойЧасти: см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки - Коллекция строк таблицы
//  Организация - ОпределяемыйТип.Организация - Организация
//  ИдентификаторФискальногоНакопителя - Строка - идентификатор фискального накопителя
//  РабочееМесто - ОпределяемыйТип.РабочиеМестаИС - рабочее место кассира
//  ДанныеУстановленныхЛокальныхМодулей - Структура:
//  * СерверныеМодули - Массив из см. ОбщегоНазначенияИСМП.КонструкторТаблицыЛокальныхМодулейЧестныйЗнак
//	* КлиентскиеМодули - Массив из см. ОбщегоНазначенияИСМП.КонструкторТаблицыЛокальныхМодулейЧестныйЗнак
//  ЭтоОперацияПробития - Булево - флаг устанавливается, если процедура вызывается длительной операцией при пробитии
//  ОбменНаСервере - Булево - Истина, если идет запрос к Локальным модулям, установленным на сервере 1С:Предприятия
// Возвращаемое значение:
//  Структура - Получить информацию по КМ от ККТ:
//  * АварийныйРежим - Булево - Истина, если действует аварийный режим или в ходе проверки он был объявлен
//  * ОтсутствуютУстановленныеЛМ - Булево - Истина, если не найдено ни одного установленного ЛМ, подходящего по отбору
//  	по организации и рабочему месту
//  * РезультатыОтправкиЗапросов - Массив из см. ИнтерфейсМОТПСлужебный.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON
//  * ТекстОшибки - Строка - текст ошибки, полученной от ГИС МТ
// 		Общий текст ошибки, ошибки запросов по всем кодам
//  * СтатусыКодовМаркировкиГИСМТ - Соответствие Из КлючИЗначение - Статусы кодов маркировки:
//		** Ключ - СтрокаТабличнойЧасти: см. ШтрихкодированиеОбщегоНазначенияИС.ИнициализацияТаблицыДанныхКодовМаркировки - Код маркировки.
//		** Значение - см. ПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля
//  * ТребуетсяОбработкаНаКлиенте - Булево - Истина, если нет доступных серверных ЛМ или по ним не получен ответ, и нужно
//  	вернуться на клиент для получения ответа с клиентских ЛМ по данным штрихкодам
//  * ДанныеУстановленныхЛокальныхМодулей - Структура из КлючИЗначение:
//   ** СерверныеМодули - Массив из см. ОбщегоНазначенияИСМП.КонструкторТаблицыЛокальныхМодулейЧестныйЗнак
//	 ** КлиентскиеМодули - Массив из см. ОбщегоНазначенияИСМП.КонструкторТаблицыЛокальныхМодулейЧестныйЗнак - список
//		локальных модулей для возврата на клиент. Если нет установленных ЛМ на клиенте, то возвращаться на клиент не требуется
//  * НекорректныеДанныеАвторизации - Булево - Истина, если указаны некорректные логин/пароль подключения
//  * НезавершеннаяНастройкаЛМ - Булево - Истина, если настройка подключения к ЛМ ЧЗ не завершена, возвращается код статуса не готовый к работе
//  * ОтсутствиеСинхронизацииЛМ - Булево - Истина, если ЛМ возвращает статус отсутствия синхронизации более 72 часов
//  * ПараметрыНастройкиЛМЧЗ - Неопределено, Структура из КлючИЗначение - структура для поиска настройки ЛМ ЧЗ.
Функция ДанныеЛокальногоМодуляЧЗПоКИПриРозничнойПродаже(ДанныеПроверки, Организация, ИдентификаторФискальногоНакопителя, РабочееМесто = Неопределено, ДанныеУстановленныхЛокальныхМодулей = Неопределено, ЭтоОперацияПробития = Ложь, ОбменНаСервере = Ложь) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура;
	ВозвращаемоеЗначение.Вставить("АварийныйРежим",                      Ложь);
	ВозвращаемоеЗначение.Вставить("ОтсутствуютУстановленныеЛМ",          Ложь);
	ВозвращаемоеЗначение.Вставить("РезультатыОтправкиЗапросов",          Новый Массив);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                         "");
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировкиГИСМТ",         Новый Соответствие);
	ВозвращаемоеЗначение.Вставить("ТребуетсяОбработкаНаКлиенте",         Ложь);
	ВозвращаемоеЗначение.Вставить("ДанныеУстановленныхЛокальныхМодулей", Неопределено);
	ВозвращаемоеЗначение.Вставить("НекорректныеДанныеАвторизации",       Ложь);
	ВозвращаемоеЗначение.Вставить("НезавершеннаяНастройкаЛМ",            Ложь);
	ВозвращаемоеЗначение.Вставить("ОтсутствиеСинхронизацииЛМ",           Ложь);
	ВозвращаемоеЗначение.Вставить("ПараметрыНастройкиЛМЧЗ",              Неопределено);
	
	ВозвращаемоеЗначение.АварийныйРежим = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ДействуетАварийныйРежимДляРозничныхПродаж();
	
	Если ВозвращаемоеЗначение.АварийныйРежим Тогда
		
		// на срок действия аварийного режима разрешена продажа без разрешительного режима
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	НастройкиСканирования = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.НастройкиСканированияКодовМаркировки();
	РежимыКонтроля        = НастройкаПараметровСканированияСлужебныйКлиентСерверПовтИсп.РежимыКонтроляСредствамиККТ();
	
	ЛогироватьУспешныеПроверки = (НастройкиСканирования.РежимКонтроляСредствамиККТ = РежимыКонтроля.ПередПробитиемЧека)
		И ОбщегоНазначенияИСМПКлиентСерверПовтИсп.ХранитьУспешныеОперацииПриПроверкеКМ()
		И Не ЭтоОперацияПробития;
	
	Если ДанныеУстановленныхЛокальныхМодулей = Неопределено Тогда
		
		ДанныеУстановленныхЛокальныхМодулей = 
			ОбщегоНазначенияИСМПВызовСервера.СортированнаяТаблицаЛокальныхМодулейЧестныйЗнак(Организация, РабочееМесто);
			
	КонецЕсли;
	
	Если ДанныеУстановленныхЛокальныхМодулей.КлиентскиеМодули.Количество() = 0
		И ДанныеУстановленныхЛокальныхМодулей.СерверныеМодули.Количество() = 0 Тогда
		
		// если локальных модулей не установлено, то не отправлять запрос
		ВозвращаемоеЗначение.ОтсутствуютУстановленныеЛМ = Истина;
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	СтруктураДополнительныхПараметров = Новый Структура();
	СтруктураДополнительныхПараметров.Вставить("Организация",                Организация);
	СтруктураДополнительныхПараметров.Вставить("ИННОрганизации",             "");
	СтруктураДополнительныхПараметров.Вставить("НомерФН",                    ИдентификаторФискальногоНакопителя);
	СтруктураДополнительныхПараметров.Вставить("ЛогироватьУспешныеПроверки", ЛогироватьУспешныеПроверки);
	
	СтруктураДополнительныхПараметров.ИННОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Организация).ИНН;
	
	РезультатНастройкиЛМ = Новый Структура();
	РезультатНастройкиЛМ.Вставить("НекорректныеДанныеАвторизации", Ложь);
	РезультатНастройкиЛМ.Вставить("НезавершеннаяНастройкаЛМ",      Ложь);
	РезультатНастройкиЛМ.Вставить("ОтсутствиеСинхронизацииЛМ",     Ложь);
	
	СписокСерверныхМодулей    = ДанныеУстановленныхЛокальныхМодулей.СерверныеМодули;
	СписокКлиентскихМодулей   = ДанныеУстановленныхЛокальныхМодулей.КлиентскиеМодули;
	
	СписокМодулейДляОбработки = ?(ОбменНаСервере, СписокСерверныхМодулей, СписокКлиентскихМодулей);
	
	Если ОбменНаСервере И СписокСерверныхМодулей = Неопределено И Не СписокКлиентскихМодулей = Неопределено Тогда 
		
		// обмен на сервере, нет настроенных серверных, но есть настроенные клиентские модули 
		// вернуть флаг необходимости возврата на клиент для запроса на клиентских ЛМ
		ВозвращаемоеЗначение.ТребуетсяОбработкаНаКлиенте         = Истина;
		ВозвращаемоеЗначение.ДанныеУстановленныхЛокальныхМодулей = ДанныеУстановленныхЛокальныхМодулей;
		
		Возврат ВозвращаемоеЗначение;
		
	КонецЕсли;
	
	РезультатОбработкиКИ = ОбработатьКодМаркировкиНаЛокальномМодулеЧестногоЗнака(ДанныеПроверки,
		СписокМодулейДляОбработки,
		СтруктураДополнительныхПараметров,
		РезультатНастройкиЛМ,
		ОбменНаСервере);
	
	ВозвращаемоеЗначение.НезавершеннаяНастройкаЛМ      = РезультатНастройкиЛМ.НезавершеннаяНастройкаЛМ;
	ВозвращаемоеЗначение.НекорректныеДанныеАвторизации = РезультатНастройкиЛМ.НекорректныеДанныеАвторизации;
	ВозвращаемоеЗначение.ОтсутствиеСинхронизацииЛМ     = РезультатНастройкиЛМ.ОтсутствиеСинхронизацииЛМ;
	ВозвращаемоеЗначение.ПараметрыНастройкиЛМЧЗ        = РезультатОбработкиКИ.ПараметрыНастройкиЛМЧЗ;
	
	Для Каждого РезультатОтправкиЗапросаНаПлощадку Из РезультатОбработкиКИ.РезультатыОтправкиЗапросов Цикл
		ВозвращаемоеЗначение.РезультатыОтправкиЗапросов.Добавить(РезультатОтправкиЗапросаНаПлощадку);
	КонецЦикла;
	
	Если ОбменНаСервере И СписокКлиентскихМодулей = Неопределено
		Или Не ОбменНаСервере Тогда
		
		// нет клиентских модулей для обработки, необходимо вернуть ошибки, полученные от серверных модулей
		ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
			+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
			+ РезультатОбработкиКИ.ТекстОшибки;
			
	КонецЕсли;
	
	Если РезультатОбработкиКИ.СтатусыКодовМаркировкиГИСМТ.Количество() Тогда
	
		// по КМ был получен ответ из ГИС МТ
		ВозвращаемоеЗначение.СтатусыКодовМаркировкиГИСМТ = РезультатОбработкиКИ.СтатусыКодовМаркировкиГИСМТ;
		
	ИначеЕсли ОбменНаСервере И Не СписокКлиентскихМодулей = Неопределено Тогда
		
		// обмен на сервере, нет ответа на серверном ЛМ, но есть настроенные клиентские модули 
		// вернуть флаг необходимости возврата на клиент для запроса на клиентских ЛМ
		ВозвращаемоеЗначение.ТребуетсяОбработкаНаКлиенте         = Истина;
		ВозвращаемоеЗначение.ДанныеУстановленныхЛокальныхМодулей = ДанныеУстановленныхЛокальныхМодулей;
		
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

#КонецОбласти

// Инициализировать структуру параметров запроса в ИС МОТП (ИС МП) для получения ключа сессии.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация - Организация.
// Возвращаемое значение:
// 	(См. ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии).
Функция ПараметрыЗапросаКлючаСессии(Организация = Неопределено) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = ПараметрыОтправкиHTTPЗапросов(Неопределено, Истина);
	
	ПараметрыЗапроса = ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии();
	ПараметрыЗапроса.Организация = Организация;
	
	ПараметрыЗапроса.ПредставлениеСервиса             = ПараметрыОтправкиHTTPЗапросов.ПредставлениеСервиса;
	ПараметрыЗапроса.Сервер                           = ПараметрыОтправкиHTTPЗапросов.Сервер;
	ПараметрыЗапроса.Порт                             = ПараметрыОтправкиHTTPЗапросов.Порт;
	ПараметрыЗапроса.Таймаут                          = ПараметрыОтправкиHTTPЗапросов.Таймаут;
	ПараметрыЗапроса.ИспользоватьЗащищенноеСоединение = ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение;
	ПараметрыЗапроса.ВремяЖизни                       = 60 * 60 * 10; // 10 часов
	
	ПараметрыЗапроса.ИмяПараметраСеанса                = ИмяДанныхКлючаСессии(ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМП"));
	ПараметрыЗапроса.АдресЗапросаПараметровАвторизации = "api/v3/true-api/auth/key";
	ПараметрыЗапроса.АдресЗапросаКлючаСессии           = "api/v3/true-api/auth/simpleSignIn";
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Инициализировать структуру параметров запроса в ИС МОТП (ИС МП) для получения ключа сессии СУЗ.
// 
// Параметры:
// 	ПараметрыСУЗ - Структура -
// Возвращаемое значение:
// 	(См. ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии).
Функция ПараметрыЗапросаКлючаСессииСУЗ(ПараметрыСУЗ) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = ПараметрыОтправкиHTTPЗапросов(Неопределено, Истина);
	
	ПараметрыЗапроса = ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии();
	ПараметрыЗапроса.Организация = ПараметрыСУЗ.Организация;
	
	ПараметрыЗапроса.ПредставлениеСервиса             = НСтр("ru = 'ГИС МТ (СУЗ)';
															|en = 'ГИС МТ (СУЗ)'");
	ПараметрыЗапроса.Сервер                           = ПараметрыОтправкиHTTPЗапросов.Сервер;
	ПараметрыЗапроса.Порт                             = ПараметрыОтправкиHTTPЗапросов.Порт;
	ПараметрыЗапроса.Таймаут                          = ПараметрыОтправкиHTTPЗапросов.Таймаут;
	ПараметрыЗапроса.ИспользоватьЗащищенноеСоединение = ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение;
	ПараметрыЗапроса.ИмяПараметраСеанса               = ИмяДанныхКлючаСессии(ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ"));
	ПараметрыЗапроса.ПроизводственныйОбъект           = ПараметрыСУЗ.ПроизводственныйОбъект;
	ПараметрыЗапроса.ВремяЖизни                       = 60 * 60 * 9; // 9 часов
	
	ПараметрыЗапроса.АдресЗапросаПараметровАвторизации = "api/v3/true-api/auth/key";
	
	ПараметрыЗапроса.АдресЗапросаКлючаСессии = СтрШаблон(
		"api/v3/true-api/auth/simpleSignIn/%1",
		ПараметрыСУЗ.ИдентификаторСоединения);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Инициализировать структуру параметров запроса в ГИС МТ для получения ключа сессии
// розничной продажи.
// 
// Параметры:
// 	Организация - ОпределяемыйТип.Организация - Организация.
// Возвращаемое значение:
// 	(См. ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии).
Функция ПараметрыЗапросаКлючаСессииИСМПРозница(Организация = Неопределено) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = ПараметрыОтправкиHTTPЗапросов(Неопределено, Истина);
	
	ПараметрыЗапроса = ИнтерфейсАвторизацииИСМПКлиентСервер.ПараметрыЗапросаКлючаСессии();
	ПараметрыЗапроса.Организация = Организация;
	
	ПараметрыЗапроса.ПредставлениеСервиса             = ПараметрыОтправкиHTTPЗапросов.ПредставлениеСервиса;
	ПараметрыЗапроса.Сервер                           = ПараметрыОтправкиHTTPЗапросов.Сервер;
	ПараметрыЗапроса.Порт                             = ПараметрыОтправкиHTTPЗапросов.Порт;
	ПараметрыЗапроса.Таймаут                          = ПараметрыОтправкиHTTPЗапросов.Таймаут;
	ПараметрыЗапроса.ИспользоватьЗащищенноеСоединение = ПараметрыОтправкиHTTPЗапросов.ИспользоватьЗащищенноеСоединение;
	
	ПараметрыЗапроса.ИмяПараметраСеанса                  = ИмяДанныхКлючаСессии(ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМПРозница"));
	ПараметрыЗапроса.АдресЗапросаПараметровАвторизации   = "api/v3/true-api/auth/key";
	ПараметрыЗапроса.АдресЗапросаКлючаСессии             = "api/v3/true-api/auth/permissive-access";
	ПараметрыЗапроса.РазрешенаПодписьДоверенногоЛицаСМЧД = Ложь;
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Возвращает имя параметра сеанса, в котором хранится информация о токене авторизации.
// 
// Параметры:
// 	ТипТокенаАвторизации - ПеречислениеСсылка.ТипыТокеновАвторизации - Тип токена авторизации.
// Возвращаемое значение:
// 	Строка - имя параметра сеанса.
Функция ИмяДанныхКлючаСессии(ТипТокенаАвторизации) Экспорт
	
	Если ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМП") Тогда
		Возврат "ДанныеКлючаСессииИСМП";
	ИначеЕсли ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ") Тогда
		Возврат "ДанныеКлючаСессииСУЗ";
	ИначеЕсли ТипТокенаАвторизации = ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМПРозница") Тогда
		Возврат "ДанныеКлючаСессииИСМПРозница";
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Фиксированный срок действия токена ИС МП (розница).
// Данный тип токена может добавляться вручную, поэтому не получает срок истечения действия из ГИС,
// а устанавливает фиксированную дату.
//
// Возвращаемое значение:
//  Дата - дата окончания действия токена ИС МП (розница)
Функция ДатаДействияТокенаИСМПРозница() Экспорт
	Возврат Дата(2026, 03, 01);
КонецФункции

// Возвращает тип токена по имени параметра сеанса, в котором хранится информация о данном типе токена.
//
// Параметры:
// 	ИмяДанныхКлючаСессии - Строка - имя параметра сеанса.
// Возвращаемое значение:
// 	ПеречислениеСсылка.ТипыТокеновАвторизации
Функция ТипТокенаАвторизацииПоИмениДанныхКлючаСессии(ИмяДанныхКлючаСессии) Экспорт

	Если ИмяДанныхКлючаСессии = "ДанныеКлючаСессииИСМП" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМП");
	ИначеЕсли ИмяДанныхКлючаСессии = "ДанныеКлючаСессииСУЗ" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.СУЗ");
	ИначеЕсли ИмяДанныхКлючаСессии = "ДанныеКлючаСессииИСМПРозница" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ИСМПРозница");
	КонецЕсли;

	Возврат ПредопределенноеЗначение("Перечисление.ТипыТокеновАвторизации.ПустаяСсылка");

КонецФункции

// Возвращает адрес сервера ИС МП.
//
// Параметры:
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции (В тестовом контуре адреса серверов
//                                                       могут отличаться для различных видов продукции).
//   ИспользоватьTrueAPI - Булево - Использовать true-api
//   РазрешительныйРежим - Булево - Истина, если обращение к выделенному серверу для загрузки CDN-площадок и токена
//
// Возвращаемое значение:
//  Строка - адрес сервера ИС МП.
//
Функция АдресСервера(ВидПродукции = Неопределено, ИспользоватьTrueAPI = Ложь, РазрешительныйРежим = Ложь) Экспорт
	
	РежимРаботыСТестовымКонтуромИСМП = ОбщегоНазначенияИСМПКлиентСерверПовтИсп.РежимРаботыСТестовымКонтуромИСМП();
	
	Если РежимРаботыСТестовымКонтуромИСМП Тогда
		Если ИспользоватьTrueAPI Или РазрешительныйРежим Тогда
			Возврат "markirovka.sandbox.crptech.ru";
		Иначе
			Возврат "sandbox.crptech.ru";
		КонецЕсли;
	Иначе
		Если РазрешительныйРежим Тогда
			Возврат "cdn.crpt.ru";
		ИначеЕсли ИспользоватьTrueAPI Тогда
			Возврат "markirovka.crpt.ru";
		Иначе
			Возврат "ismp.crpt.ru";
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Возвращает параметры для отправки HTTP запросов ИС МП.
//
// Параметры:
//   ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС - Вид продукции (В тестовом контуре адреса серверов
//                                                       могут отличаться для различных видов продукции).
//   ИспользоватьTrueAPI - Булево - Использовать true-api.
//   РазрешительныйРежим - Булево - Истина, если обращение к выделенному серверу для загрузки CDN-площадок и токена
//
// Возвращаемое значение:
//  Структура - Описание:
//   * ИспользоватьЗащищенноеСоединение - Булево - Признак использования SSL.
//   * Таймаут - Число - Таймаут соединения.
//   * Порт - Число - Порт соединения.
//   * Сервер - Строка - Адрес сервера.
//   * ПредставлениеСервиса - Строка - Представления сервиса.
//
Функция ПараметрыОтправкиHTTPЗапросов(ВидПродукции = Неопределено, ИспользоватьTrueAPI = Ложь, РазрешительныйРежим = Ложь) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = Новый Структура;
	ПараметрыОтправкиHTTPЗапросов.Вставить("ПредставлениеСервиса",             НСтр("ru = 'ГИС МТ';
																					|en = 'ГИС МТ'"));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Сервер",                           АдресСервера(ВидПродукции, ИспользоватьTrueAPI, РазрешительныйРежим));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Порт",                             443);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Таймаут",                          60);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИспользоватьЗащищенноеСоединение", Истина);
	
	Возврат ПараметрыОтправкиHTTPЗапросов;
	
КонецФункции

// Возвращает параметры для отправки HTTP запросов Локальному модулю ЧЗ
// по переданным настройкам подключения
//
// Параметры:
//   ПараметрыУстановки - см. ОбщегоНазначенияИСМПКлиентСервер.ПараметрыПодключенияЛокальногоМодуляЧЗ
//
// Возвращаемое значение:
//  Структура - Описание:
//   * ИспользоватьЗащищенноеСоединение - Булево - Признак использования SSL.
//   * Таймаут - Число - Таймаут соединения.
//   * Порт - Число - Порт соединения.
//   * Сервер - Строка - Адрес сервера.
//   * ПредставлениеСервиса - Строка - Представления сервиса.
//
Функция ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ПараметрыУстановки) Экспорт
	
	ПараметрыОтправкиHTTPЗапросов = Новый Структура;
	ПараметрыОтправкиHTTPЗапросов.Вставить("ПредставлениеСервиса",             НСтр("ru = 'ЛМ ЧЗ';
																					|en = 'ЛМ ЧЗ'"));
	ПараметрыОтправкиHTTPЗапросов.Вставить("Сервер",                           ПараметрыУстановки.Сервер);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Порт",                             ПараметрыУстановки.Порт);
	ПараметрыОтправкиHTTPЗапросов.Вставить("Таймаут",                          60);
	ПараметрыОтправкиHTTPЗапросов.Вставить("ИспользоватьЗащищенноеСоединение", ПараметрыУстановки.ЗащищенноеСоединение);
	
	Возврат ПараметрыОтправкиHTTPЗапросов;
	
КонецФункции

// Функция преобразовывает список переданных сокращенных наименований гос. органов, устанавливающих
//  блокировку на реализацию конкретных кодов маркировки по данным ГИС МТ, в читаемое представление
// 
// Параметры:
//  МассивИдентификаторов - Массив из Строка - список сокращенных наименований организаций
// 
// Возвращаемое значение:
//  Массив из Строка - список полных наименований организаций
Функция ОрганыВластиУстанавливающиеБлокировкуНаКМПоДаннымГИСМТ(МассивИдентификаторов) Экспорт
	
	МассивПредставлений = Новый Массив;
	
	Если Не ТипЗнч(МассивИдентификаторов) = Тип("Массив") Тогда
		Возврат МассивПредставлений;
	КонецЕсли;
	
	СоответствиеПредставлений = Новый Соответствие();
	СоответствиеПредставлений.Вставить("RAR",  НСтр("ru = 'Росалкогольрегулирование';
													|en = 'Росалкогольрегулирование'"));
	СоответствиеПредставлений.Вставить("FTS",  НСтр("ru = 'ФТС России';
													|en = 'ФТС России'"));
	СоответствиеПредставлений.Вставить("FNS",  НСтр("ru = 'ФНС России';
													|en = 'ФНС России'"));
	СоответствиеПредставлений.Вставить("RSHN", НСтр("ru = 'Россельхознадзор (РСХН)';
													|en = 'Россельхознадзор (РСХН)'"));
	СоответствиеПредставлений.Вставить("RPN",  НСтр("ru = 'Роспотребнадзор (РПН)';
													|en = 'Роспотребнадзор (РПН)'"));
	СоответствиеПредставлений.Вставить("MVD",  НСтр("ru = 'МВД России';
													|en = 'МВД России'"));
	СоответствиеПредставлений.Вставить("RZN",  НСтр("ru = 'Росздравнадзор';
													|en = 'Росздравнадзор'"));
	
	Для Каждого Идентификатор Из МассивИдентификаторов Цикл
		
		ЗначениеПредставления = СоответствиеПредставлений.Получить(Идентификатор);
		
		Если ЗначениеПредставления = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		МассивПредставлений.Добавить(ЗначениеПредставления);
		
	КонецЦикла;
	
	Возврат МассивПредставлений;
	
КонецФункции

// Преобразовывает текстовое представление товарной группы в значение перечисления и наоборот.
//
// Параметры:
//  ЗначениеПоиска - ПеречислениеСсылка.ВидыПродукцииИС, Строка - значение для перекодировки
//  ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Неопределено - Вид продукции
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.ВидыПродукцииИС, Строка - Товарная группа.
Функция ТоварнаяГруппаЧислом(Знач ЗначениеПоиска, ВидПродукции = Неопределено) Экспорт
	
	Если ЗначениеПоиска = Неопределено Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПустаяСсылка");
	КонецЕсли;
	
	ИсходноеЗначениеПоиска = ЗначениеПоиска;
	
	Если ТипЗнч(ЗначениеПоиска) = Тип("Строка") Тогда
		ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
		ЗначениеПоиска = ОписаниеТипаЧисло.ПривестиЗначение(ЗначениеПоиска);
	КонецЕсли;
	
	Если ТипЗнч(ЗначениеПоиска) = Тип("Число") Тогда
		Если ЗначениеПоиска = 1 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность2025") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность2025");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 2 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь");
		ИначеЕсли ЗначениеПоиска = 3 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак");
		ИначеЕсли ЗначениеПоиска = 4 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи");
		ИначеЕсли ЗначениеПоиска = 5 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины");
		ИначеЕсли ЗначениеПоиска = 6 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты");
		ИначеЕсли ЗначениеПоиска = 8 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 9 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Велосипеды");
		ИначеЕсли ЗначениеПоиска = 10 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации");
			ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия");
			ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия20") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия20");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 12 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак");
		ИначеЕсли ЗначениеПоиска = 13 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода");
		ИначеЕсли ЗначениеПоиска = 14 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха");
		ИначеЕсли ЗначениеПоиска = 15 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 16 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция");
		ИначеЕсли ЗначениеПоиска = 17 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы2025")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СпортивноеПитание") Тогда
				Возврат ВидПродукции;
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 19 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ДезинфицирующиеСредства") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ДезинфицирующиеСредства");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 21 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС");
		ИначеЕсли ЗначениеПоиска = 22 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво");
		ИначеЕсли ЗначениеПоиска = 23 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольныеНапитки")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НапиткиССодержаниемМолокаКакаоПрочие") Тогда
				Возврат ВидПродукции;
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СоковаяПродукция");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 20 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС");
			ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС");
			ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 25 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МясоПодконтрольноеВЕТИС");
		ИначеЕсли ЗначениеПоиска = 26 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ВетеринарныеПрепараты");
		ИначеЕсли ЗначениеПоиска = 27 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ИгрыИИгрушкиДляДетей");
		ИначеЕсли ЗначениеПоиска = 28 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТелефоныИНоутбуки") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТелефоныИНоутбуки");
			ИначеЕсли ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатныеПлаты") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатныеПлаты");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РадиоэлектроннаяПродукция");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 31 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТитановаяМеталлопродукция");
		ИначеЕсли ЗначениеПоиска = 32 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияБезВЕТИС") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияБезВЕТИС");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияПодконтрольнаяВЕТИС");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = 33 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РастительныеМасла");
		ИначеЕсли ЗначениеПоиска = 34 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ОптоволокноИОптоволоконнаяПродукция");
		ИначеЕсли ЗначениеПоиска = 35 Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТоварыЛичнойГигиены")
				Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТоварыЛичнойГигиены") Тогда
				Возврат ВидПродукции;
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = ПечатнаяПродукцияКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатнаяПродукция");
		ИначеЕсли ЗначениеПоиска = СтроительныеМатериалыКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СтроительныеМатериалы");
		ИначеЕсли ЗначениеПоиска = ОтопительныеПриборыКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ОтопительныеПриборы");
		ИначеЕсли ЗначениеПоиска = БакалеяКодТоварнойГруппы() Тогда
			Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РастворимыеЗавариваемыеНапитки") Тогда
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РастворимыеЗавариваемыеНапитки");
			Иначе
				Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Бакалея");
			КонецЕсли;
		ИначеЕсли ЗначениеПоиска = АлкогольнаяПродукцияДо9ПроцентовКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов");
		ИначеЕсли ЗначениеПоиска = ПиротехническиеИзделияИСредстваПожарнойБезопасностиКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПиротехническиеИзделияИСредстваПожарнойБезопасности");
		ИначеЕсли ЗначениеПоиска = КабельнаяПродукцияКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КабельнаяПродукция");
		ИначеЕсли ЗначениеПоиска = МоторныеМаслаКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МоторныеМасла");
		ИначеЕсли ЗначениеПоиска = АвтозапчастиИКомплектующиеТранспортныхСредствКодТоварнойГруппы() Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АвтозапчастиИКомплектующиеТранспортныхСредств");
		ИначеЕсли ЗначениеПоиска = 44 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПолимерныеТрубы");
		ИначеЕсли ЗначениеПоиска = 45 Тогда
			Возврат ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СладостиИКондитерскиеИзделия");
		КонецЕсли;
	ИначеЕсли ТипЗнч(ЗначениеПоиска) = Тип("ПеречислениеСсылка.ВидыПродукцииИС") Тогда
		Если ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ЛегкаяПромышленность2025") Тогда
			Возврат 1;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Обувь") Тогда
			Возврат 2;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Табак") Тогда
			Возврат 3;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Духи") Тогда
			Возврат 4;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Шины") Тогда
			Возврат 5;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Фотоаппараты") Тогда
			Возврат 6;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияПодконтрольнаяВЕТИС")
				Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МолочнаяПродукцияБезВЕТИС") Тогда
			Возврат 8;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Велосипеды") Тогда
			Возврат 9;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КреслаКоляски")
				Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТехническиеСредстваРеабилитации")
				Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия")
				Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МедицинскиеИзделия20") Тогда
			Возврат 10;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АльтернативныйТабак") Тогда
			Возврат 12;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.УпакованнаяВода") Тогда
			Возврат 13;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПродукцияИзНатуральногоМеха") Тогда
			Возврат 14;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
				Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках") Тогда
			Возврат 15;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НикотиносодержащаяПродукция") Тогда
			Возврат 16;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БАДы2025")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СпортивноеПитание") Тогда
			Возврат 17;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Антисептики")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ДезинфицирующиеСредства") Тогда
			Возврат 19;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС") Тогда
			Возврат 21;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво") Тогда
			Возврат 22;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СоковаяПродукция")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольныеНапитки")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.НапиткиССодержаниемМолокаКакаоПрочие") Тогда
			Возврат 23;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхПодконтрольныеВЕТИС")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхБезВЕТИС")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеПодконтрольныеВЕТИС")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КормаДляЖивотныхВлажныеБезВЕТИС") Тогда
			Возврат 20;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МясоПодконтрольноеВЕТИС") Тогда
			Возврат 25;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ВетеринарныеПрепараты") Тогда
			Возврат 26;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ИгрыИИгрушкиДляДетей") Тогда
			Возврат 27;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РадиоэлектроннаяПродукция")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТелефоныИНоутбуки")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатнаяПродукция") Тогда
			Возврат 28;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТитановаяМеталлопродукция") Тогда
			Возврат 31;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияПодконтрольнаяВЕТИС")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КонсервированнаяПродукцияБезВЕТИС") Тогда
			Возврат 32;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РастительныеМасла") Тогда
			Возврат 33;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ОптоволокноИОптоволоконнаяПродукция") Тогда
			Возврат 34;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПарфюмерныеИКосметическиеСредстваИБытоваяХимия")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ТоварыЛичнойГигиены")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БритвыИЛезвия") Тогда
			Возврат 35;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПечатнаяПродукция") Тогда
			Возврат ПечатнаяПродукцияКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СтроительныеМатериалы") Тогда
			Возврат СтроительныеМатериалыКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ОтопительныеПриборы") Тогда
			Возврат ОтопительныеПриборыКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Бакалея")
			Или ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.РастворимыеЗавариваемыеНапитки") Тогда
			Возврат БакалеяКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АлкогольнаяПродукцияДо9Процентов") Тогда
			Возврат АлкогольнаяПродукцияДо9ПроцентовКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПиротехническиеИзделияИСредстваПожарнойБезопасности") Тогда
			Возврат ПиротехническиеИзделияИСредстваПожарнойБезопасностиКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.КабельнаяПродукция") Тогда
			Возврат КабельнаяПродукцияКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МоторныеМасла") Тогда
			Возврат МоторныеМаслаКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.АвтозапчастиИКомплектующиеТранспортныхСредств") Тогда
			Возврат АвтозапчастиИКомплектующиеТранспортныхСредствКодТоварнойГруппы();
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПолимерныеТрубы") Тогда
			Возврат 44;
		ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.СладостиИКондитерскиеИзделия") Тогда
			Возврат 45;
		КонецЕсли;
	КонецЕсли;
	
	ВызватьИсключение
		СтрШаблон(
			НСтр("ru = 'Неизвестная товарная группа: %1';
				|en = 'Неизвестная товарная группа: %1'"),
			ИсходноеЗначениеПоиска);
	
КонецФункции

// Перекодирует группу статусов документа сертификации для обмена с ИС МП.
//
// Параметры:
//  ЗначениеПоиска - Число, ПеречислениеСсылка.ГруппыСтатусовРазрешительныхДокументовИСМП - значение для перекодировки.
//
// Возвращаемое значение:
//  Число, ПеречислениеСсылка.ГруппыСтатусовРазрешительныхДокументовИСМП - Результат перекодирования.
Функция ГруппаСтатусовРазрешительныхДокументов(Знач ЗначениеПоиска) Экспорт
	
	МассивЗеленыхСтатусов = Новый Массив;
	МассивЖелтыхСтатусов  = Новый Массив;
	МассивКрасныхСтатусов = Новый Массив;
	
	МассивЗеленыхСтатусов.Добавить(1);
	МассивЗеленыхСтатусов.Добавить(26);
	МассивЗеленыхСтатусов.Добавить(17);
	МассивЗеленыхСтатусов.Добавить(28);
	МассивЗеленыхСтатусов.Добавить(29);
	МассивЗеленыхСтатусов.Добавить(30);
	
	МассивЖелтыхСтатусов.Добавить(2);
	МассивЖелтыхСтатусов.Добавить(11);
	МассивЖелтыхСтатусов.Добавить(12);
	МассивЖелтыхСтатусов.Добавить(13);
	МассивЖелтыхСтатусов.Добавить(14);
	МассивЖелтыхСтатусов.Добавить(15);
	МассивЖелтыхСтатусов.Добавить(21);
	МассивЖелтыхСтатусов.Добавить(22);
	МассивЖелтыхСтатусов.Добавить(23);
	МассивЖелтыхСтатусов.Добавить(24);
	МассивЖелтыхСтатусов.Добавить(25);
	
	МассивКрасныхСтатусов.Добавить(3);
	МассивКрасныхСтатусов.Добавить(16);
	МассивКрасныхСтатусов.Добавить(17);
	МассивКрасныхСтатусов.Добавить(18);
	МассивКрасныхСтатусов.Добавить(19);
	МассивКрасныхСтатусов.Добавить(20);
	
	Если МассивЗеленыхСтатусов.Найти(ЗначениеПоиска) <> Неопределено Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.ЗеленыйСтатус");
	ИначеЕсли МассивЖелтыхСтатусов.Найти(ЗначениеПоиска) <> Неопределено Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.ЖелтыйСтатус");
	ИначеЕсли МассивКрасныхСтатусов.Найти(ЗначениеПоиска) <> Неопределено Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.КрасныйСтатус");
	ИначеЕсли ЗначениеЗаполнено(ЗначениеПоиска) И ТипЗнч(ЗначениеПоиска) = Тип("Число") Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.НеизвестныйСтатус");
	ИначеЕсли ЗначениеПоиска = 0 Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.ПустаяСсылка");
	
	ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.ЗеленыйСтатус") Тогда
		Возврат 1;
	ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.ЖелтыйСтатус") Тогда
		Возврат 2;
	ИначеЕсли ЗначениеПоиска = ПредопределенноеЗначение("Перечисление.ГруппыСтатусовРазрешительныхДокументовИСМП.КрасныйСтатус") Тогда
		Возврат 3;
	ИначеЕсли ТипЗнч(ЗначениеПоиска) = Тип("ПеречислениеСсылка.ГруппыСтатусовРазрешительныхДокументовИСМП") Тогда
		Возврат 0;
	
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОбработатьОтветИнициализацияКонфигурацииЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса) Экспорт
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если Не РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбщегоНазначенияИСКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ВозвращаемоеЗначение.ОтказАвторизации = Истина;
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				
				Если ДанныеОбработки.Получить("errorCode") = 4015 Тогда
					
					// некорректный токен
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница = Истина;
					
				ИначеЕсли ДанныеОбработки.Получить("errorCode") = 6010 Тогда
					
					// не передан токен
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница = Истина;
					
				Иначе
					
					ТекстОшибки = ДанныеОбработки.Получить("reason");
					
					Если ТекстОшибки <> Неопределено Тогда
						ВозвращаемоеЗначение.ТекстОшибки = ТекстОшибки;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветЗапроситьСтатусЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса) Экспорт
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если ДанныеОбработки.Получить("version") <> Неопределено Тогда
				ВозвращаемоеЗначение.ВерсияСборки = ДанныеОбработки.Получить("version");
			КонецЕсли;
			
			Если ДанныеОбработки.Получить("status") <> Неопределено Тогда
				ВозвращаемоеЗначение.Статус = ДанныеОбработки.Получить("status");
			КонецЕсли;
			
			Если ДанныеОбработки.Получить("requiresDownload") <> Неопределено Тогда
				ВозвращаемоеЗначение.ТребуетсяЗагрузка = ДанныеОбработки.Получить("requiresDownload");
			КонецЕсли;
			
			Если ДанныеОбработки.Получить("lastSync") <> Неопределено Тогда
				
				ДатаПоследнейСинхронизации = ДанныеОбработки.Получить("lastSync");
				
				Если ЗначениеЗаполнено(ДатаПоследнейСинхронизации) Тогда
					ВозвращаемоеЗначение.ДатаПоследнейСинхронизации = ОбщегоНазначенияИСКлиентСервер.ДатаИзСтрокиUNIX(ДатаПоследнейСинхронизации);
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ВозвращаемоеЗначение.ОтказАвторизации = Истина;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветИзменитьПарольАдминистратораЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса) Экспорт
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если Не РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ВозвращаемоеЗначение.ОтказАвторизации = Истина;
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				
				Если ДанныеОбработки.Получить("errorCode") = 6020 Тогда
					
					ВозвращаемоеЗначение.ОшибкаПроверкиНовогоПароля = Истина;
					
				Иначе
					
					ТекстОшибки = ДанныеОбработки.Получить("reason");
					
					Если ТекстОшибки <> Неопределено Тогда
						ВозвращаемоеЗначение.ТекстОшибки = ТекстОшибки;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветНастройкаТоварныхГруппЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса) Экспорт
	
	ВозвращаемоеЗначение.РезультатОтправкиЗапроса = РезультатОтправкиЗапроса;
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если Не РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ВозвращаемоеЗначение.ОтказАвторизации = Истина;
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				
				Если ДанныеОбработки.Получить("errorCode") = 4010 Тогда
					
					// некорректный токен
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница = Истина;
					
				ИначеЕсли ДанныеОбработки.Получить("errorCode") = 6010 Тогда
					
					// не передан токен
					ВозвращаемоеЗначение.ТребуетсяОбновлениеКлючаСессииРозница = Истина;
					
				Иначе
					
					ТекстОшибки = ДанныеОбработки.Получить("reason");
					
					Если ТекстОшибки <> Неопределено Тогда
						ВозвращаемоеЗначение.ТекстОшибки = ТекстОшибки;
					КонецЕсли;
					
				КонецЕсли;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтветЗапросКонфигурацииЛокальногоМодуляЧЗ(РезультатОтправкиЗапроса, ВозвращаемоеЗначение, URLЗапроса) Экспорт
	
	Если РезультатОтправкиЗапроса.ОтветПолучен Тогда
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			АдресаСервернойЧасти = ДанныеОбработки.Получить("urlLoadDb");
			
			Если АдресаСервернойЧасти <> Неопределено Тогда
				
				Если ТипЗнч(АдресаСервернойЧасти) = Тип("Массив") И АдресаСервернойЧасти.Количество() Тогда
					ВозвращаемоеЗначение.АдресСервернойЧасти = АдресаСервернойЧасти[0];
				КонецЕсли;
				
			КонецЕсли;
			
			Если ДанныеОбработки.Получить("productsGroupSettings") <> Неопределено Тогда
				ВозвращаемоеЗначение.НастройкаТоварныхГрупп = ДанныеОбработки.Получить("productsGroupSettings");
			КонецЕсли;
			
		Иначе
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ВозвращаемоеЗначение.ОтказАвторизации = Истина;
				
			Иначе
				
				ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		// не получен ответ при инициализации, таймаут или иные проблемы
		ВозвращаемоеЗначение.ТекстОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
			URLЗапроса,
			РезультатОтправкиЗапроса);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДоступныеМетодыИнтерфейса() Экспорт
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("ЕмкостьУпаковкиПоGTIN",                          "ЕмкостьУпаковкиПоGTIN");
	ВозвращаемоеЗначение.Вставить("ВыполнитьАвторизацию",                           "ВыполнитьАвторизацию");
	ВозвращаемоеЗначение.Вставить("МестаОсуществленияДеятельности",                 "МестаОсуществленияДеятельности");
	ВозвращаемоеЗначение.Вставить("АктуализацияCDNПлощадок",                        "АктуализацияCDNПлощадок");
	ВозвращаемоеЗначение.Вставить("ПолучениеРозничногоТокена",                      "ПолучениеРозничногоТокена");
	ВозвращаемоеЗначение.Вставить("СписокМестОсуществленияДеятельности",            "СписокМестОсуществленияДеятельности");
	ВозвращаемоеЗначение.Вставить("ИнициализацияКонфигурацииЛокальногоМодуляЧЗ",    "ИнициализацияКонфигурацииЛокальногоМодуляЧЗ");
	ВозвращаемоеЗначение.Вставить("ОбновлениеСтатусаЛокальногоМодуляЧЗ",            "ОбновлениеСтатусаЛокальногоМодуляЧЗ");
	ВозвращаемоеЗначение.Вставить("СменаПароляАдминистрированияЛокальногоМодуляЧЗ", "СменаПароляАдминистрированияЛокальногоМодуляЧЗ");
	ВозвращаемоеЗначение.Вставить("НастройкаСпискаТоварныхГрупп",                   "НастройкаСпискаТоварныхГрупп");
	ВозвращаемоеЗначение.Вставить("ПолучениеКонфигурацииЛокальногоМодуляЧЗ",        "ПолучениеКонфигурацииЛокальногоМодуляЧЗ");
	ВозвращаемоеЗначение.Вставить("ПолучениеДанныхНастройкиТСПИоТ",                 "ПолучениеДанныхНастройкиТСПИоТ");
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

Функция ОбработатьКодМаркировкиНаЛокальномМодулеЧестногоЗнака(ДанныеПроверки, ДанныеУстановленныхЛокальныхМодулей, СтруктураДополнительныхПараметров, РезультатНастройкиЛМ, ОбменНаСервере)
	
	ВозвращаемоеЗначение = Новый Структура();
	ВозвращаемоеЗначение.Вставить("СтатусыКодовМаркировкиГИСМТ", Новый Соответствие);
	ВозвращаемоеЗначение.Вставить("РезультатыОтправкиЗапросов",  Новый Массив);
	ВозвращаемоеЗначение.Вставить("ПараметрыНастройкиЛМЧЗ",      Неопределено);
	ВозвращаемоеЗначение.Вставить("ТекстОшибки",                 "");
	
	URLЗапроса = "api/v1/cis/outCheck";
	
	МассивКодовИдентификацииДляОтправки = Новый Массив;
	СоответствиеКИЗСтрокам              = Новый Соответствие;
	
	Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
		
		МассивКодовИдентификацииДляОтправки.Добавить(ДанныеШтрихкода.КодИдентификацииДляОтправкиВЛокальныйМодуль);
		СоответствиеКИЗСтрокам.Вставить(ДанныеШтрихкода.КодИдентификацииДляОтправкиВЛокальныйМодуль, ДанныеШтрихкода);
		
	КонецЦикла;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("cis_list", МассивКодовИдентификацииДляОтправки);
	
	ТелоЗапросаJSON = ОбменДаннымиИСМПКлиентСервер.ОбъектВТекстJSON(ПараметрыЗапроса, Истина);
	
	Для Каждого ДанныеЛокальногоМодуля Из ДанныеУстановленныхЛокальныхМодулей Цикл
		
		ДанныеАвторизацииBase64 = ШтрихкодированиеОбщегоНазначенияИСКлиентСервер.ШтрихкодВBase64(СтрШаблон("%1:%2", ДанныеЛокальногоМодуля.Логин, ДанныеЛокальногоМодуля.Пароль));
		
		ЗаголовокHTTP = Новый Соответствие();
		ЗаголовокHTTP.Вставить("Accept",         "application/json");
		ЗаголовокHTTP.Вставить("Accept-Charset", "utf-8");
		ЗаголовокHTTP.Вставить("Authorization",  СтрШаблон("Basic %1", ДанныеАвторизацииBase64));
		ЗаголовокHTTP.Вставить("X-ClientId",     СтруктураДополнительныхПараметров.НомерФН);
		
		ПараметрыЗапросов = ПараметрыОтправкиHTTPЗапросовКЛокальномуМодулюЧЗ(ДанныеЛокальногоМодуля);
		ПараметрыЗапросов.Таймаут = ДанныеЛокальногоМодуля.Таймаут;
		
		РезультатЗапроса = ОбменДаннымиИСМПКлиентСервер.ОтправитьДанныеВСервис(
			URLЗапроса,
			ТелоЗапросаJSON,,
			"POST",
			ПараметрыЗапросов,
			ЗаголовокHTTP);
		
		РезультатОтправкиЗапроса = ОбменДаннымиИСМПКлиентСервер.ОбработатьРезультатОтправкиHTTPЗапросаКакJSON(РезультатЗапроса, Истина);
		
		ВозвращаемоеЗначение.РезультатыОтправкиЗапросов.Добавить(РезультатОтправкиЗапроса);
		
		Если Не РезультатОтправкиЗапроса.ОтветПолучен Тогда
			
			// не получен ответ при инициализации, таймаут или иные проблемы
			ДополнениеТекстаОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
				URLЗапроса,
				РезультатОтправкиЗапроса);
			
			ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
				+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
				+ ДополнениеТекстаОшибки;
			
			Продолжить;
			
		КонецЕсли;
		
		Если РезультатОтправкиЗапроса.КодСостояния = 200 Тогда
			
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если ДанныеОбработки = Неопределено Тогда
				
				ДополнениеТекстаОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
				ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
					+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
					+ ДополнениеТекстаОшибки;
					
				Продолжить;
				
			КонецЕсли;
			
			КодОтветаОшибки = ДанныеОбработки.Получить("code");
			
			Если ЗначениеЗаполнено(КодОтветаОшибки) Тогда
				
				ШаблонСообщения = НСтр("ru = 'Локальный модуль ""Честный знак"" вернул ошибку.
					|Код ошибки %1, описание ошибки:
					|%2.';
					|en = 'Локальный модуль ""Честный знак"" вернул ошибку.
					|Код ошибки %1, описание ошибки:
					|%2.'");
				ТекстОшибки = СтрШаблон(ШаблонСообщения,
					КодОтветаОшибки,
					ДанныеОбработки.Получить("description"));
				
				ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
					+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
					+ ТекстОшибки;
				
				Продолжить;
				
			КонецЕсли;
			
			ДанныеОтветаЛокальногоМодуля = ДанныеОбработки.Получить("results");
			
			Если Не ТипЗнч(ДанныеОтветаЛокальногоМодуля) = Тип("Массив")
				Или Не ДанныеОтветаЛокальногоМодуля.Количество() Тогда
				
				ДополнениеТекстаОшибки = НСтр("ru = 'Некорректный формат ответа от Локального модуля Честный знак.';
												|en = 'Некорректный формат ответа от Локального модуля Честный знак.'");
				
				ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
					+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
					+ ДополнениеТекстаОшибки;
				
				Продолжить;
				
			КонецЕсли;
			
			Для Каждого ОбщиеДанныеОтветаЛокальногоМодуля Из ДанныеОтветаЛокальногоМодуля Цикл
				
				ДанныеПоКодамИдентификации = ОбщиеДанныеОтветаЛокальногоМодуля.Получить("codes");
				
				Если ДанныеПоКодамИдентификации <> Неопределено Тогда
					
					Если ТипЗнч(ДанныеПоКодамИдентификации) = Тип("Массив") И ДанныеПоКодамИдентификации.Количество() Тогда
						
						Для Каждого СтрокаДанныхПоКодамИдентификации Из ДанныеПоКодамИдентификации Цикл
							
							КИЗСтроки       = СтрокаДанныхПоКодамИдентификации.Получить("cis");
							ДанныеШтрихкода = Неопределено;
							
							Если Не КИЗСтроки = Неопределено Тогда
								ДанныеШтрихкода = СоответствиеКИЗСтрокам.Получить(КИЗСтроки);
							КонецЕсли;
							
							Если ДанныеШтрихкода = Неопределено Тогда
								Продолжить;
							КонецЕсли;
							
							ПараметрыКодаМаркировки = ПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля(СтрокаДанныхПоКодамИдентификации);
							
							РазрешительныйРежимДатаЗапросаГИСМТ                                  = ОбщиеДанныеОтветаЛокальногоМодуля.Получить("reqTimestamp");
							ПараметрыКодаМаркировки.РазрешительныйРежимИдентификаторЗапросаГИСМТ = ОбщиеДанныеОтветаЛокальногоМодуля.Получить("reqId");
							ПараметрыКодаМаркировки.РазрешительныйРежимИдентификаторЭкземпляраПО = ОбщиеДанныеОтветаЛокальногоМодуля.Получить("inst");
							ПараметрыКодаМаркировки.РазрешительныйРежимВерсияБазы                = ОбщиеДанныеОтветаЛокальногоМодуля.Получить("version");
							
							ПараметрыКодаМаркировки.РазрешительныйРежимДатаЗапросаГИСМТ = Формат(РазрешительныйРежимДатаЗапросаГИСМТ, "ЧГ=;");
							ПараметрыКодаМаркировки.РазрешительныйРежимАдресСервера     = ДанныеЛокальногоМодуля.АдресПодключения;
							ПараметрыКодаМаркировки.РазрешительныйРежимТелоЗапросаJSON  = URLЗапроса;
							ПараметрыКодаМаркировки.РазрешительныйРежимТелоОтветаJSON   = РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON;
							ПараметрыКодаМаркировки.РазрешительныйРежимКодОтвета        = Формат(РезультатЗапроса.HTTPОтвет.КодСостояния, "ЧГ=0;");
							
							ЭтоПродукцияМОТП = ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ДанныеШтрихкода.ВидПродукции);
							
							Если ПараметрыКодаМаркировки.Заблокирован Тогда
							
								Если ЭтоПродукцияМОТП Тогда
									ПараметрыКодаМаркировки.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ЗаблокированОГВ");
								Иначе
									ПараметрыКодаМаркировки.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ЗаблокированОГВ");
								КонецЕсли;
								
								Если Не ОбменНаСервере Тогда
									
									ТекстОшибки = СтрШаблон(НСтр("ru = 'Статус: %1.
									|Оборот товара был заблокирован органами государственной власти.';
									|en = 'Статус: %1.
									|Оборот товара был заблокирован органами государственной власти.'"), ПараметрыКодаМаркировки.Статус);
								
									ДанныеШтрихкода.ТекстОшибки = ТекстОшибки;
									ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
									
								КонецЕсли;
								
							КонецЕсли;
							
							Если ПараметрыКодаМаркировки.GTINЗапрещенКПродаже Тогда
							
								ТекстОшибки = НСтр("ru = 'GTIN запрещен к продаже.';
													|en = 'GTIN запрещен к продаже.'");
								
								ДанныеШтрихкода.ТекстОшибки = ТекстОшибки;
								ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
								
							КонецЕсли;
							
							Если ПараметрыКодаМаркировки.КИЗапрещенКПродаже Тогда
								
								ТекстОшибки = НСтр("ru = 'КИ запрещен к продаже.';
													|en = 'КИ запрещен к продаже.'");
								
								ДанныеШтрихкода.ТекстОшибки = ТекстОшибки;
								ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
								
							КонецЕсли;
							
							Если ПараметрыКодаМаркировки.Продан Тогда
								
								Если ЭтоПродукцияМОТП Тогда
									ПараметрыКодаМаркировки.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВыведенИзОборота");
								Иначе
									ПараметрыКодаМаркировки.Статус = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборотаРозничнаяПродажа");
								КонецЕсли;
								
							КонецЕсли;
							
							Если ПараметрыКодаМаркировки.СерияМедицинскогоПрепаратаЗапрещенаКПродаже Тогда
								
								ТекстОшибки = НСтр("ru = 'Серия медицинского препарата запрещена к продаже.';
													|en = 'Серия медицинского препарата запрещена к продаже.'");
								
								ДанныеШтрихкода.ТекстОшибки = ТекстОшибки;
								ДанныеШтрихкода.КонтролиРазрешительногоРежима.Добавить(ТекстОшибки);
								
							КонецЕсли;
							
							ВозвращаемоеЗначение.СтатусыКодовМаркировкиГИСМТ.Вставить(ДанныеШтрихкода, ПараметрыКодаМаркировки);
							
						КонецЦикла;
						
					КонецЕсли;
					
				Иначе
					
					ДополнениеТекстаОшибки = НСтр("ru = 'Некорректный формат ответа от Локального модуля Честный знак.';
													|en = 'Некорректный формат ответа от Локального модуля Честный знак.'");
					
					ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
						+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
						+ ДополнениеТекстаОшибки;
					
				КонецЕсли;
			
			КонецЦикла;
			
			// достаточно одного ответа от одного ЛМ
			Прервать;
			
		Иначе
			
			#Область РазборОшибокРаботыЛокальногоМодуля
			ДанныеОбработки = ОбменДаннымиИСМПКлиентСервер.ТекстJSONВОбъект(РезультатОтправкиЗапроса.ТекстВходящегоСообщенияJSON, Истина);
			
			Если РезультатОтправкиЗапроса.КодСостояния = 401 Тогда
				
				// некорректный логин/пароль администратора
				ШаблонПодсказки             = НСтр("ru = 'Указаны некорректные логин/пароль подключения к Локальному модулю ""Честный знак"" %1.';
													|en = 'Указаны некорректные логин/пароль подключения к Локальному модулю ""Честный знак"" %1.'");
				
				Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
					ДанныеШтрихкода.ТекстОшибки = СтрШаблон(ШаблонПодсказки, ДанныеЛокальногоМодуля.АдресПодключения);
				КонецЦикла;
				
				РезультатНастройкиЛМ.НекорректныеДанныеАвторизации = Истина;
				
				ВозвращаемоеЗначение.ПараметрыНастройкиЛМЧЗ = Новый Структура("РабочееМесто, Приоритет");
				ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение.ПараметрыНастройкиЛМЧЗ, ДанныеЛокальногоМодуля);
				
			ИначеЕсли РезультатОтправкиЗапроса.КодСостояния = 400 Тогда
				
				// код ошибки 4045 - не инициализировано, или в статусе инициализации
				// код ошибки 4050 - ошибка синхронизации
				
				// в обоих случаях переходим к следующей площадке
				
				Если ДанныеОбработки = Неопределено Тогда
					// в некоторых случаях при проверке ЛМ просто выдает код 400 без текста ответа
					
					КодРасшифровкиОшибки = Неопределено;
				Иначе
					КодРасшифровкиОшибки = ДанныеОбработки.Получить("errorCode");
				КонецЕсли;
				
				Если КодРасшифровкиОшибки <> Неопределено Тогда
					
					Если КодРасшифровкиОшибки = 4045 Тогда
						
						ШаблонПодсказки             = НСтр("ru = 'Не завершена настройка подключения к Локальному модулю ""Честный знак"" %1.';
															|en = 'Не завершена настройка подключения к Локальному модулю ""Честный знак"" %1.'");
						
						Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
							ДанныеШтрихкода.ТекстОшибки = СтрШаблон(ШаблонПодсказки, ДанныеЛокальногоМодуля.АдресПодключения);
						КонецЦикла;
						
						РезультатНастройкиЛМ.НезавершеннаяНастройкаЛМ = Истина;
						
					ИначеЕсли КодРасшифровкиОшибки = 4050 Тогда
						
						ШаблонПодсказки             = НСтр("ru = 'Статус Локального модуля ""Честный знак"" %1 - ошибка синхронизации.';
															|en = 'Статус Локального модуля ""Честный знак"" %1 - ошибка синхронизации.'");
						
						Для Каждого ДанныеШтрихкода Из ДанныеПроверки Цикл
							ДанныеШтрихкода.ТекстОшибки = СтрШаблон(ШаблонПодсказки, ДанныеЛокальногоМодуля.АдресПодключения);
						КонецЦикла;
						
						РезультатНастройкиЛМ.ОтсутствиеСинхронизацииЛМ = Истина;
						
					Иначе
						
						ТекстОшибкиИзСервиса = ДанныеОбработки.Получить("reason");
						
						Если ТекстОшибкиИзСервиса <> Неопределено Тогда
							
							ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
								+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
								+ ТекстОшибкиИзСервиса;
							
						Иначе
							
							ДополнениеТекстаОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
								URLЗапроса,
								РезультатОтправкиЗапроса);
							
							ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
								+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
								+ ДополнениеТекстаОшибки;
							
						КонецЕсли;
						
					КонецЕсли;
					
				Иначе
					
					ДополнениеТекстаОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
						URLЗапроса,
						РезультатОтправкиЗапроса);
					
					ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
						+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
						+ ДополнениеТекстаОшибки;
					
				КонецЕсли;
				
				ВозвращаемоеЗначение.ПараметрыНастройкиЛМЧЗ = Новый Структура("РабочееМесто, Приоритет");
				ЗаполнитьЗначенияСвойств(ВозвращаемоеЗначение.ПараметрыНастройкиЛМЧЗ, ДанныеЛокальногоМодуля);
				
			Иначе
				
				ДополнениеТекстаОшибки = ОбщегоНазначенияИСКлиентСервер.ТекстОшибкиПоРезультатуОтправкиЗапроса(
					URLЗапроса,
					РезультатОтправкиЗапроса);
				
				ВозвращаемоеЗначение.ТекстОшибки = ВозвращаемоеЗначение.ТекстОшибки
					+ ?(ЗначениеЗаполнено(ВозвращаемоеЗначение.ТекстОшибки), Символы.ПС, "")
					+ ДополнениеТекстаОшибки;
				
			КонецЕсли;
			#КонецОбласти
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Конструктор для передачи данных по коду маркировки из Локального модуля "Честный знак"
// 
// Возвращаемое значение:
//  Структура:
//  * Найден - Булево - Признак проверки наличия КИ в БД «Карточки КИ» (до версии ЛМ 1.0.6)
//  * Продан - Булево - Признак проверки факта продажи КИ в БД «Проданные товары» (до версии ЛМ 1.0.6)
//  * КИЗапрещенКПродаже - Булево - Признак проверки наличия КИ в БД «Запрещённые к продаже КИ» (до версии ЛМ 1.0.6)
//  * GTINЗапрещенКПродаже - Булево - Признак проверки наличия КИ (GTIN в составе КИ) в БД «Запрещённые к продаже GTIN» (до версии ЛМ 1.0.6)
//  * СерияМедицинскогоПрепаратаЗапрещенаКПродаже - Булево - Признак проверки наличия КИ в БД «Запрещённых к продаже серии медицинских препаратов» (до версии ЛМ 1.0.6)
//  * Заблокирован - Булево - Признак блокировки КИ по решению ОГВ (начиная с версии ЛМ 1.0.6)
//  * Статус - ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП, ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - статус кода по данным ЛМ
//  * РазрешительныйРежимИдентификаторЗапросаГИСМТ - Строка - Уникальный идентификатор квитанции
//  * РазрешительныйРежимДатаЗапросаГИСМТ - Строка - Время формирования квитанции в формате timestamp
//  * РазрешительныйРежимИдентификаторЭкземпляраПО - Строка - Идентификатор экземпляра ПО «Локальный модуль «Честный ЗНАК»
//  * РазрешительныйРежимВерсияБазы - Строка - Версия базы «чёрного списка», на которой выполнялась проверка КИ
//  * РазрешительныйРежимАдресСервера - Строка - адрес ЛМ ЧЗ
//  * РазрешительныйРежимТелоЗапросаJSON - Строка - текст запроса в ЛМ ЧЗ
//  * РазрешительныйРежимТелоОтветаJSON - Строка - текст ответа из ЛМ ЧЗ
//  * РазрешительныйРежимКодОтвета - Строка - код ответа HTTP
Функция ИнициализироватьПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля() Экспорт
	
	ПараметрыКодаМаркировки = Новый Структура;
	
	ПараметрыКодаМаркировки.Вставить("Найден",                                       Ложь);
	ПараметрыКодаМаркировки.Вставить("Продан",                                       Ложь);
	ПараметрыКодаМаркировки.Вставить("КИЗапрещенКПродаже",                           Ложь);
	ПараметрыКодаМаркировки.Вставить("GTINЗапрещенКПродаже",                         Ложь);
	ПараметрыКодаМаркировки.Вставить("СерияМедицинскогоПрепаратаЗапрещенаКПродаже",  Ложь);
	ПараметрыКодаМаркировки.Вставить("Заблокирован",                                 Ложь);
	ПараметрыКодаМаркировки.Вставить("Статус",                                       Неопределено);
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимИдентификаторЗапросаГИСМТ", "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимДатаЗапросаГИСМТ",          "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимИдентификаторЭкземпляраПО", "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимВерсияБазы",                "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимАдресСервера",              "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимТелоЗапросаJSON",           "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимТелоОтветаJSON",            "");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимКодОтвета",                 "");
	
	Возврат ПараметрыКодаМаркировки;
	
КонецФункции

// Возвращает структуру данных кода маркировки из Локального модуля "Честный знак"
// Параметры:
// 	ЭлементДанных - Соответствие из КлючИЗначение, Неопределено - Данные ЛМ ЧЗ
//
// Возвращаемое значение:
//  см. ИнициализироватьПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля
Функция ПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля(ЭлементДанных) Экспорт
	
	ПараметрыКодаМаркировки = ИнициализироватьПараметрыКодаМаркировкиПоДаннымЛокальногоМодуля();
	
	Значение = ЭлементДанных["е"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.Найден = Значение;
	КонецЕсли;
	
	Значение = ЭлементДанных["sc"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.Продан = Значение;
	КонецЕсли;
	
	Значение = ЭлементДанных["bc"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.КИЗапрещенКПродаже = Значение;
	КонецЕсли;
	
	Значение = ЭлементДанных["be"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.GTINЗапрещенКПродаже = Значение;
	КонецЕсли;
	
	Значение = ЭлементДанных["bs"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.СерияМедицинскогоПрепаратаЗапрещенаКПродаже = Значение;
	КонецЕсли;
	
	Значение = ЭлементДанных["isBlocked"];
	Если Не Значение = Неопределено Тогда
		ПараметрыКодаМаркировки.Заблокирован = Значение;
	КонецЕсли;
	
	Возврат ПараметрыКодаМаркировки;
	
КонецФункции

// Конструктор для передачи данных по коду маркировки по данным разрешительного режима
// 
// Возвращаемое значение:
//  Структура:
//  * РезультатРазбора - Произвольный
//  * Статус - ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП, ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - статус кода по данным разрешительного режима
//  * ОсобоеСостояние - ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП, ПеречислениеСсылка.СтатусыКодовМаркировкиМОТП - особое состояние кода маркировки по данным
//  	разрешительного режима
//  * ИННВладельца - Строка
//  * ИННПроизводителя - Строка
//  * GTIN - Строка
//  * ДатаПроизводства - Дата
//  * ГоденДо - Дата
//  * РодительскаяУпаковка - Произвольный
//  * ВложенныеУпаковки - Произвольный
//  * ВидУпаковки - ПеречислениеСсылка.ВидыУпаковокИС
//  * ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС
//  * МРЦ - Число
//  * ЕмкостьПотребительскойУпаковки - Число
//  * ЧастичноеВыбытиеВыведено - Число
//  * ЧастичноеВыбытиеОстатокГИСМТ - Число
//  * ЧастичноеВыбытиеНеКорректно - Булево
//  * ПродажаЗаблокирована - Булево
//  * СписокОрганизацийБлокировкиКМ - Массив из Строка
//  * ВСеройЗоне - Булево
//  * КоличествоВПотребительскойУпаковке - Число
//  * ПродажаПоДаннымОффлайнРежима - Булево - Истина, если код маркировки был проверен офлайн. Влечет за собой ограничение по проверкам, потому что владельца и статус
//  	ЛМ ЧЗ не может вернуть.
//  * РазрешительныйРежимИдентификаторЗапросаГИСМТ - Строка - Уникальный идентификатор квитанции
//  * РазрешительныйРежимДатаЗапросаГИСМТ - Строка - Время формирования квитанции в формате timestamp
//  * РазрешительныйРежимИдентификаторЭкземпляраПО - Строка - Идентификатор экземпляра ПО «Локальный модуль «Честный ЗНАК»
//  * РазрешительныйРежимВерсияБазы - Строка - Версия базы «чёрного списка», на которой выполнялась проверка КИ
//  * РазрешительныйРежимАдресСервера - Строка - адрес разрешительного режима
//  * РазрешительныйРежимТелоЗапросаJSON - Строка - текст запроса из разрешительного режима
//  * РазрешительныйРежимТелоОтветаJSON - Строка - текст ответа из разрешительного режима
//  * РазрешительныйРежимКодОтвета - Строка - код ответа HTTP
Функция ИнициализироватьПараметрыКодаМаркировкиПоДаннымРазрешительногоРежима() Экспорт
	
	ПараметрыКодаМаркировки = Новый Структура;
	ПараметрыКодаМаркировки.Вставить("РезультатРазбора");
	
	ПараметрыКодаМаркировки.Вставить("Статус");
	ПараметрыКодаМаркировки.Вставить("ОсобоеСостояние");
	
	ПараметрыКодаМаркировки.Вставить("ИННВладельца");
	ПараметрыКодаМаркировки.Вставить("ИННПроизводителя");
	ПараметрыКодаМаркировки.Вставить("GTIN");

	ПараметрыКодаМаркировки.Вставить("ДатаПроизводства", '00010101');
	ПараметрыКодаМаркировки.Вставить("ГоденДо",          '00010101');
	
	ПараметрыКодаМаркировки.Вставить("РодительскаяУпаковка");
	ПараметрыКодаМаркировки.Вставить("ВложенныеУпаковки");
	ПараметрыКодаМаркировки.Вставить("ВидУпаковки");
	ПараметрыКодаМаркировки.Вставить("ВидПродукции");
	ПараметрыКодаМаркировки.Вставить("МРЦ");
	
	ПараметрыКодаМаркировки.Вставить("ЕмкостьПотребительскойУпаковки");
	ПараметрыКодаМаркировки.Вставить("ЧастичноеВыбытиеВыведено");
	ПараметрыКодаМаркировки.Вставить("ЧастичноеВыбытиеОстатокГИСМТ");
	ПараметрыКодаМаркировки.Вставить("ЧастичноеВыбытиеНеКорректно");
	
	ПараметрыКодаМаркировки.Вставить("ПродажаЗаблокирована");
	ПараметрыКодаМаркировки.Вставить("СписокОрганизацийБлокировкиКМ");
	ПараметрыКодаМаркировки.Вставить("ВСеройЗоне");
	ПараметрыКодаМаркировки.Вставить("КоличествоВПотребительскойУпаковке");
	
	ПараметрыКодаМаркировки.Вставить("ПродажаПоДаннымОффлайнРежима");
	
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимИдентификаторЗапросаГИСМТ");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимДатаЗапросаГИСМТ");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимИдентификаторЭкземпляраПО");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимВерсияБазы");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимАдресСервера");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимТелоЗапросаJSON");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимТелоОтветаJSON");
	ПараметрыКодаМаркировки.Вставить("РазрешительныйРежимКодОтвета");
	
	Возврат ПараметрыКодаМаркировки;
	
КонецФункции

// Возвращает структуру данных кода маркировки по данным разрешительного режима (прямое обращение и через ТС ПИоТ)
// Параметры:
// 	ЭлементДанных - Соответствие из КлючИЗначение, Неопределено - Данные РР
// 	ВидПродукции - ПеречислениеСсылка.ВидыПродукцииИС, Неопределено - Вид продукции.
// 	ДополнительныеПараметры - Структура:
// 		* НастройкиРазбора - см. РазборКодаМаркировкиИССлужебный.НастройкиРазбораКодаМаркировки.
// 		* Организация - ОпределяемыйТип.Организация
// 		* КешОрганизаций - Соответствие из КлючИЗначение - кеш для получения ИНН организаций-владельцев кода:
// 			** Ключ - ОпределяемыйТип.Организация - организация
// 			** Значение - Строка - ИНН организации
//
// Возвращаемое значение:
//  см. ИнициализироватьПараметрыКодаМаркировкиПоДаннымРазрешительногоРежима
Функция ПараметрыКодаМаркировкиПоДаннымРазрешительногоРежима(ЭлементДанных, ВидПродукции = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	ПараметрыКодаМаркировки = ИнициализироватьПараметрыКодаМаркировкиПоДаннымРазрешительногоРежима();
	
	ТипЭлементаДанных = ТипЗнч(ЭлементДанных);
	Если ТипЭлементаДанных = Тип("Соответствие") Тогда
		ИсточникДанных = ЭлементДанных;
	ИначеЕсли ТипЭлементаДанных = Тип("КлючИЗначение") Тогда
		ИсточникДанных = ЭлементДанных.Значение;
	ИначеЕсли ТипЭлементаДанных = Тип("Структура") Тогда
		ИсточникДанных = Новый Соответствие();
		ОбщегоНазначенияКлиентСервер.ДополнитьСоответствие(ИсточникДанных, ЭлементДанных);
	КонецЕсли;
	
	Значение = ИсточникДанных["groupIds"];
	
	Если Значение <> Неопределено И Значение.Количество() Тогда
		ПараметрыКодаМаркировки.ВидПродукции = ТоварнаяГруппаЧислом(Значение[0], ВидПродукции);
	КонецЕсли;
	
	ВидПродукцииДляРазбораКода = ВидПродукции;
	Если ЗначениеЗаполнено(ПараметрыКодаМаркировки.ВидПродукции) Тогда
		ВидПродукцииДляРазбораКода = ПараметрыКодаМаркировки.ВидПродукции;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ВидПродукцииДляРазбораКода) Тогда
		ВызватьИсключение НСтр("ru = 'Вид продукции определить не удалось';
								|en = 'Вид продукции определить не удалось'");
	КонецЕсли;
	
	СтруктураДанныхСтатуса = Новый Структура();
	СтруктураДанныхСтатуса.Вставить("Валидность",                    Ложь);
	СтруктураДанныхСтатуса.Вставить("Наличие",                       Ложь);
	СтруктураДанныхСтатуса.Вставить("УспешнаяПроверкаКриптоподписи", Ложь);
	СтруктураДанныхСтатуса.Вставить("Нанесен",                       Ложь);
	СтруктураДанныхСтатуса.Вставить("ВозможностьРеализации",         Ложь);
	СтруктураДанныхСтатуса.Вставить("Продано",                       Ложь);
	
	Валидность                    = ИсточникДанных["valid"];
	Наличие                       = ИсточникДанных["found"];
	УспешнаяПроверкаКриптоподписи = ИсточникДанных["verified"];
	Нанесен                       = ИсточникДанных["utilised"];
	ВозможностьРеализации         = ИсточникДанных["realizable"];
	Продано                       = ИсточникДанных["sold"];
	
	Если Валидность <> Неопределено Тогда
		СтруктураДанныхСтатуса.Валидность = Валидность;
	КонецЕсли;
	
	Если Наличие <> Неопределено Тогда
		СтруктураДанныхСтатуса.Наличие = Наличие;
	КонецЕсли;
	
	Если УспешнаяПроверкаКриптоподписи <> Неопределено Тогда
		СтруктураДанныхСтатуса.УспешнаяПроверкаКриптоподписи = УспешнаяПроверкаКриптоподписи;
	КонецЕсли;
	
	Если Нанесен <> Неопределено Тогда
		СтруктураДанныхСтатуса.Нанесен = Нанесен;
	КонецЕсли;
	
	Если ВозможностьРеализации <> Неопределено Тогда
		СтруктураДанныхСтатуса.ВозможностьРеализации = ВозможностьРеализации;
	КонецЕсли;
	
	Если Продано <> Неопределено Тогда
		СтруктураДанныхСтатуса.Продано = Продано;
	КонецЕсли;
	
	ПараметрыКодаМаркировки.Статус = СтатусКодаМаркировкиПоДаннымГИСМТ(СтруктураДанныхСтатуса, ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукцииДляРазбораКода));
	
	Значение = ИсточникДанных["isOwner"];
	Если Значение <> Неопределено И Значение Или ИсточникДанных["isTracking"] = Ложь Тогда
		
		Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("Организация") Тогда
			
			Организация    = ДополнительныеПараметры.Организация;
			ИННОрганизации = Неопределено;
			
			Если ДополнительныеПараметры.Свойство("КешОрганизаций") Тогда
				ИННОрганизации = ДополнительныеПараметры.КешОрганизаций.Получить(Организация);
			КонецЕсли;
			
			Если ИННОрганизации = Неопределено Тогда
				ИННОрганизации = РаботаСКонтрагентамиИСВызовСервера.ИННКПППоОрганизацииКонтрагенту(Организация).ИНН;
			КонецЕсли;
			
			ПараметрыКодаМаркировки.ИННВладельца = ИННОрганизации;
		
		КонецЕсли;
		
	КонецЕсли;
	
	Значение = ИсточникДанных["gtin"];
	Если Значение <> Неопределено Тогда
		ПараметрыКодаМаркировки.GTIN = Значение;
	КонецЕсли;
	
	Значение = ИсточникДанных["producerInn"];
	Если Значение <> Неопределено Тогда
		ПараметрыКодаМаркировки.ИННПроизводителя = Значение;
	КонецЕсли;
	
	Значение = ИсточникДанных["grayZone"];
	Если Значение <> Неопределено Тогда
		ПараметрыКодаМаркировки.ВСеройЗоне = Значение;
	Иначе
		ПараметрыКодаМаркировки.ВСеройЗоне = Ложь;
	КонецЕсли;
	
	Значение = ИсточникДанных["innerUnitCount"];
	Если Значение <> Неопределено Тогда
		ПараметрыКодаМаркировки.КоличествоВПотребительскойУпаковке = Значение;
	КонецЕсли;
	
	Значение = ИсточникДанных["mrp"];
	Если Значение <> Неопределено Тогда
		
		ПараметрыКодаМаркировки.МРЦ = Значение;
		ПривестиЗначениеМРЦ(ПараметрыКодаМаркировки);
		
	КонецЕсли;
	
	ПроверкаЧерезТСПИоТ = Ложь;
	Если ДополнительныеПараметры <> Неопределено И ДополнительныеПараметры.Свойство("ПроверкаЧерезТСПИоТ") И ДополнительныеПараметры.ПроверкаЧерезТСПИоТ Тогда
		ПроверкаЧерезТСПИоТ = Истина;
	КонецЕсли;
	
	Значение = ИсточникДанных["expireDate"];
	Если Значение <> Неопределено Тогда
		Если ПроверкаЧерезТСПИоТ Тогда
			ПараметрыКодаМаркировки.ГоденДо = ИнтеграцияТСПИоТКлиентСервер.ДатаИзСтроки(Значение);
		Иначе
			ПараметрыКодаМаркировки.ГоденДо = ОбщегоНазначенияИСВызовСервера.ДатаИзСтроки(Значение);
		КонецЕсли;
	КонецЕсли;
	
	Значение = ИсточникДанных["productionDate"];
	Если Значение <> Неопределено Тогда
		Если ПроверкаЧерезТСПИоТ Тогда
			ПараметрыКодаМаркировки.ДатаПроизводства = ИнтеграцияТСПИоТКлиентСервер.ДатаИзСтроки(Значение);
		Иначе
			ПараметрыКодаМаркировки.ДатаПроизводства = ОбщегоНазначенияИСВызовСервера.ДатаИзСтроки(Значение);
		КонецЕсли;
	КонецЕсли;
	
	КоэффициентПересчета = КоэффициентПересчетаКоличестваЧастичногоВыбытияДляРазрешительногоРежима(
		ПараметрыКодаМаркировки.ВидПродукции);
	
	ДанныеЧастичногоВыбытия = Новый Структура();
	ДанныеЧастичногоВыбытия.Вставить("ЕмкостьПотребительскойУпаковки", 0);
	ДанныеЧастичногоВыбытия.Вставить("ЧастичноеВыбытиеВыведено",       0);
	ДанныеЧастичногоВыбытия.Вставить("ЧастичноеВыбытиеОстатокГИСМТ",   0);
	ДанныеЧастичногоВыбытия.Вставить("ЕстьДанныеЧастичногоВыбытия",    Ложь);
	
	Если ИсточникДанных["innerUnitCount"] <> Неопределено Тогда
		
		ДанныеЧастичногоВыбытия.ЕмкостьПотребительскойУпаковки = ИсточникДанных["innerUnitCount"];
		ДанныеЧастичногоВыбытия.ЕстьДанныеЧастичногоВыбытия    = Истина;
		
	КонецЕсли;
	
	Если ИсточникДанных["soldUnitCount"] <> Неопределено Тогда
		
		ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеВыведено       = ИсточникДанных["soldUnitCount"];
		ДанныеЧастичногоВыбытия.ЕстьДанныеЧастичногоВыбытия    = Истина;
		
	КонецЕсли;
	
	Если ДанныеЧастичногоВыбытия.ЕстьДанныеЧастичногоВыбытия Тогда
		
		ПараметрыКодаМаркировки.ЕмкостьПотребительскойУпаковки = ДанныеЧастичногоВыбытия.ЕмкостьПотребительскойУпаковки * КоэффициентПересчета;
		ПараметрыКодаМаркировки.ЧастичноеВыбытиеВыведено       = ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеВыведено * КоэффициентПересчета;
		ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеОстатокГИСМТ   = ПараметрыКодаМаркировки.ЕмкостьПотребительскойУпаковки - ПараметрыКодаМаркировки.ЧастичноеВыбытиеВыведено;
		ПараметрыКодаМаркировки.ЧастичноеВыбытиеОстатокГИСМТ   = ДанныеЧастичногоВыбытия.ЧастичноеВыбытиеОстатокГИСМТ;
		КорректностьОстатка                                    = ПараметрыКодаМаркировки.ЧастичноеВыбытиеОстатокГИСМТ > 0;
		Если КорректностьОстатка <> Неопределено Тогда
			ПараметрыКодаМаркировки.ЧастичноеВыбытиеНеКорректно = Не (КорректностьОстатка);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ИсточникДанных["parent"] <> Неопределено Тогда
		ПараметрыКодаМаркировки.РодительскаяУпаковка = ИсточникДанных["parent"];
	КонецЕсли;
	
	ПараметрыКодаМаркировки.СписокОрганизацийБлокировкиКМ = Новый Массив;
	
	Если ИсточникДанных["isBlocked"] <> Неопределено Тогда
		
		ПараметрыКодаМаркировки.ПродажаЗаблокирована = ИсточникДанных["isBlocked"];
		
		Значение = ИсточникДанных["ogvs"];
		
		Если Значение <> Неопределено Тогда
			ПараметрыКодаМаркировки.СписокОрганизацийБлокировкиКМ = Значение;
		КонецЕсли;
		
	ИначеЕсли ИсточникДанных["ogvs"] <> Неопределено Тогда
		
		ПараметрыКодаМаркировки.СписокОрганизацийБлокировкиКМ = ИсточникДанных["ogvs"];
		ПараметрыКодаМаркировки.ПродажаЗаблокирована          = ПараметрыКодаМаркировки.СписокОрганизацийБлокировкиКМ.Количество();
		
	Иначе
		ПараметрыКодаМаркировки.ПродажаЗаблокирована = Ложь;
	КонецЕсли;
	
	Если ПараметрыКодаМаркировки.ПродажаЗаблокирована Тогда
		
		Если ОбщегоНазначенияИСКлиентСервер.ЭтоПродукцияМОТП(ВидПродукцииДляРазбораКода) Тогда
			ПараметрыКодаМаркировки.ОсобоеСостояние = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ЗаблокированОГВ");
		Иначе
			ПараметрыКодаМаркировки.ОсобоеСостояние = ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ЗаблокированОГВ");
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыКодаМаркировки.ВидУпаковки <> ПредопределенноеЗначение("Перечисление.ВидыУпаковокИС.Потребительская") Тогда
		ПараметрыКодаМаркировки.ВложенныеУпаковки = Новый Соответствие;
	КонецЕсли;
	
	Возврат ПараметрыКодаМаркировки;
	
КонецФункции

// Статус кода маркировки по структуре данных ГИС МТ - признаков продажи, нанесения, возможности реализации
// 
// Параметры:
//  СтруктураКода - Структура - Структура признаков:
//  	* Продано - Булево
//  	* ВозможностьРеализации - Булево
//  	* Нанесен - Булево
//  	* Валидность - Булево
//  ЭтоПродукцияМОТП - Булево - признак, что продукция является МОТП
// 
// Возвращаемое значение:
//  ПеречислениеСсылка.СтатусыКодовМаркировкиИСМП - Статус кода маркировки по данным ГИС МТ
Функция СтатусКодаМаркировкиПоДаннымГИСМТ(Знач СтруктураКода, ЭтоПродукцияМОТП = Ложь) Экспорт
	
	Если ЭтоПродукцияМОТП Тогда
		
		Если СтруктураКода.Продано Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Продан");
		ИначеЕсли СтруктураКода.ВозможностьРеализации Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.ВведенВОборот");
		ИначеЕсли Не СтруктураКода.ВозможностьРеализации
			И СтруктураКода.Нанесен Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Нанесен");
		ИначеЕсли Не СтруктураКода.Нанесен
			И СтруктураКода.Валидность Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Эмитирован");
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиМОТП.Неопределен");
		КонецЕсли;
		
	Иначе
	
		Если СтруктураКода.Продано Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВыведенИзОборота");
		ИначеЕсли СтруктураКода.ВозможностьРеализации Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.ВведенВОборот");
		ИначеЕсли Не СтруктураКода.ВозможностьРеализации
			И СтруктураКода.Нанесен Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Нанесен");
		ИначеЕсли Не СтруктураКода.Нанесен
			И СтруктураКода.Валидность Тогда
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Эмитирован");
		Иначе
			Возврат ПредопределенноеЗначение("Перечисление.СтатусыКодовМаркировкиИСМП.Неопределен");
		КонецЕсли;
	
	КонецЕсли;
	
КонецФункции

Функция ТекстОшибкиРазрешительногоРежимаПоКодуОшибкиГИСМТ(КодОшибки, ОписаниеОшибкиГИСМТ = Неопределено) Экспорт
	
	ТекстОшибки = "";
	
	Если КодОшибки = 1 Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка валидации КМ в ГИС МТ.';
							|en = 'Ошибка валидации КМ в ГИС МТ.'");
	ИначеЕсли КодОшибки = 2 Тогда
		ТекстОшибки = НСтр("ru = 'КМ не содержит GTIN.';
							|en = 'КМ не содержит GTIN.'");
	ИначеЕсли КодОшибки = 3 Тогда
		ТекстОшибки = НСтр("ru = 'КМ не содержит серийный номер.';
							|en = 'КМ не содержит серийный номер.'");
	ИначеЕсли КодОшибки = 4 Тогда
		ТекстОшибки = НСтр("ru = 'КМ содержит недопустимые символы.';
							|en = 'КМ содержит недопустимые символы.'");
	ИначеЕсли КодОшибки = 5 Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (формат крипто-подписи не соответствует типу КМ).';
							|en = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (формат крипто-подписи не соответствует типу КМ).'");
	ИначеЕсли КодОшибки = 6 Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (крипто-подпись не валидная).';
							|en = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (крипто-подпись не валидная).'");
	ИначеЕсли КодОшибки = 7 Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (крипто-ключ не валиден).';
							|en = 'Ошибка верификации крипто-подписи КМ в ГИС МТ (крипто-ключ не валиден).'");
	ИначеЕсли КодОшибки = 8 Тогда
		ТекстОшибки = НСтр("ru = 'КМ не прошел верификацию в стране эмитента.';
							|en = 'КМ не прошел верификацию в стране эмитента.'");
	ИначеЕсли КодОшибки = 9 Тогда
		ТекстОшибки = НСтр("ru = 'Найденные AI в КМ не поддерживаются.';
							|en = 'Найденные AI в КМ не поддерживаются.'");
	ИначеЕсли КодОшибки = 10 Тогда
		ТекстОшибки = НСтр("ru = 'КМ не найден в ГИС МТ.';
							|en = 'КМ не найден в ГИС МТ.'");
	Иначе
		
		ШаблонПодсказки   = НСтр("ru = 'Неклассифицированная ошибка отправки запроса по КМ в ГИС МТ.
			|Описание из сервиса: %1.';
			|en = 'Неклассифицированная ошибка отправки запроса по КМ в ГИС МТ.
			|Описание из сервиса: %1.'");
			
		Если ОписаниеОшибкиГИСМТ <> Неопределено Тогда
			ТекстИзГИСМТ = СтрЗаменить(ОписаниеОшибкиГИСМТ, Символ(29), "");
		Иначе
			ТекстИзГИСМТ = "отсутствует";
		КонецЕсли;
		
		ТекстОшибки = СтрШаблон(ШаблонПодсказки, ТекстИзГИСМТ);
		
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

Процедура ПривестиЗначениеМРЦ(СтрокаДанных) Экспорт
	
	Если ТипЗнч(СтрокаДанных.МРЦ) = Тип("Число") Тогда
		СтрокаДанных.МРЦ = СтрокаДанных.МРЦ / 100;
	КонецЕсли;
	
КонецПроцедуры

Функция КоэффициентПересчетаКоличестваЧастичногоВыбытияДляРазрешительногоРежима(ВидПродукции) Экспорт
	
	Если ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.Пиво")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.ПивоВПотребительскихУпаковках")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.БезалкогольноеПиво")
		Или ВидПродукции = ПредопределенноеЗначение("Перечисление.ВидыПродукцииИС.МорепродуктыПодконтрольныеВЕТИС") Тогда
		КоэффициентПересчета = 0.001;
	Иначе
		КоэффициентПересчета = 1;
	КонецЕсли;
	
	Возврат КоэффициентПересчета;

КонецФункции

#Область КодТоварнойГруппы

Функция ПечатнаяПродукцияКодТоварнойГруппы()
	Возврат 36;
КонецФункции

Функция СтроительныеМатериалыКодТоварнойГруппы()
	Возврат 39;
КонецФункции

Функция ОтопительныеПриборыКодТоварнойГруппы()
	Возврат 41;
КонецФункции

Функция БакалеяКодТоварнойГруппы()
	Возврат 37
КонецФункции

Функция АлкогольнаяПродукцияДо9ПроцентовКодТоварнойГруппы()
	Возврат 0;
КонецФункции

Функция ПиротехническиеИзделияИСредстваПожарнойБезопасностиКодТоварнойГруппы()
	Возврат 40;
КонецФункции

Функция КабельнаяПродукцияКодТоварнойГруппы()
	Возврат 42;
КонецФункции

Функция МоторныеМаслаКодТоварнойГруппы()
	Возврат 43;
КонецФункции

Функция АвтозапчастиИКомплектующиеТранспортныхСредствКодТоварнойГруппы()
	Возврат 48;
КонецФункции

#КонецОбласти

#КонецОбласти
