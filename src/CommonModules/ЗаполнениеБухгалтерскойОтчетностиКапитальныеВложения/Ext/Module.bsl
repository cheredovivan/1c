
#Область СлужебныйПрограммныйИнтерфейс

// Добавляет описание числовых значений.
// 
// Параметры:
//  ЧисловыеЗначения - Массив из Строка
Процедура ДобавитьОписаниеЧисловыеЗначенияКапитальныеВложения(ЧисловыеЗначения) Экспорт

	ЧисловыеЗначения.Добавить("НачалоЗатраты");
	ЧисловыеЗначения.Добавить("Затраты");
	ЧисловыеЗначения.Добавить("Списано");
	ЧисловыеЗначения.Добавить("Завершено");
	ЧисловыеЗначения.Добавить("КонецЗатраты");
	
КонецПроцедуры

Процедура ЗаполнитьПояснениеКапитальныеВложения(ПолучениеДанныхТаблицОтчета, ИмяТаблицы, Классификаторы, КонтекстЗаполненияОтчета) Экспорт
	
	КлассификаторСтадий = Классификаторы[ИмяТаблицы].Стадия;

	ВидКапитальныхВложений = ВидКапитальныхВложений(ИмяТаблицы);

	СчетаУчетаПоВидуАктивов = СчетаУчетаПоВидуАктивов(ВидКапитальныхВложений);
	ВидыСубконто = ВидыСубконтоПоВидуАктивов(ВидКапитальныхВложений);

	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц();
	СоздатьСтоимостьКапитальныхВложений(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ИмяТаблицы, СчетаУчетаПоВидуАктивов, ВидыСубконто, КлассификаторСтадий);
	
	ВнеоборотныеАктивыНаНачалоПоГруппам = ВнеоборотныеАктивыНаНачалоПоГруппам(МенеджерВременныхТаблиц, ВидКапитальныхВложений);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	ГруппаПоУмолчанию = Перечисления.ГруппыОС.ДругиеВидыОсновныхСредств;
	Если ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы() Тогда
		ГруппаПоУмолчанию = Перечисления.ВидыНМА.ПрочиеНематериальныеАктивы;
	КонецЕсли;
	Запрос.УстановитьПараметр("ГруппаПоУмолчанию", ГруппаПоУмолчанию);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КапитальныеВложения.Организация КАК Организация,
	|	КапитальныеВложения.Счет КАК Счет,
	|	КапитальныеВложения.КорСчет КАК КорСчет,
	|	ЕСТЬNULL(КапитальныеВложения.Группа, &ГруппаПоУмолчанию) КАК Группа,
	|	КапитальныеВложения.НачалоЗатраты КАК НачалоЗатраты,
	|	КапитальныеВложения.КонецЗатраты КАК КонецЗатраты,
	|	КапитальныеВложения.Затраты КАК Затраты,
	|	КапитальныеВложения.Списано КАК Списано,
	|	КапитальныеВложения.СчетКод КАК СчетКод,
	|	КапитальныеВложения.КорСчетКод КАК КорСчетКод,
	|	КапитальныеВложения.ОрганизацияПредставление КАК ОрганизацияПредставление,
	|	КапитальныеВложения.ЭтоГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	ВсеСтадии.Стадия КАК Стадия,
	|	КапитальныеВложения.ВидДвижения КАК ВидДвижения,
	|	КапитальныеВложения.ПорядокСчета КАК ПорядокСчета,
	|	КапитальныеВложения.ПорядокКорСчета КАК ПорядокКорСчета,
	|	КапитальныеВложения.ВидСубконто КАК ВидСубконто
	|ИЗ
	|	ВсеСтадии КАК ВсеСтадии
	|		ЛЕВОЕ СОЕДИНЕНИЕ КапитальныеВложения КАК КапитальныеВложения
	|		ПО ВсеСтадии.Стадия = КапитальныеВложения.Стадия
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЭтоГоловнаяОрганизация УБЫВ,
	|	ОрганизацияПредставление,
	|	Организация,
	|	Группа,
	|	ПорядокСчета,
	|	Счет,
	|	ВидДвижения,
	|	ПорядокКорСчета,
	|	КорСчет
	|ИТОГИ
	|	СУММА(НачалоЗатраты),
	|	СУММА(КонецЗатраты),
	|	СУММА(Затраты),
	|	СУММА(Списано),
	|	МАКСИМУМ(СчетКод),
	|	МАКСИМУМ(ОрганизацияПредставление),
	|	МАКСИМУМ(ЭтоГоловнаяОрганизация),
	|	МАКСИМУМ(ВидДвижения),
	|	МАКСИМУМ(ПорядокСчета),
	|	МАКСИМУМ(ПорядокКорСчета),
	|	МАКСИМУМ(ВидСубконто)
	|ПО
	|	Стадия,
	|	Группа,
	|	Организация,
	|	Счет,
	|	ВидДвижения";

	ВыборкаСтадия = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ПараметрыТекстаРасшифровки = ПараметрыТекстовРасшифровкиКапитальныеВложения(КонтекстЗаполненияОтчета);
	ШаблоныКолонок = Новый Структура; // Ключ - имя колонки; Значение - строка
	ШаблоныЗаголовковРасшифровки = Новый Соответствие; // Ключ - имя колонки; Значение - см. ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки
	ЗаполнитьШаблоныКолонокКапитальныеВложения(ШаблоныКолонок, ШаблоныЗаголовковРасшифровки, ВидКапитальныхВложений);
	
	КапитальныеВложения = ЗаполнениеБухгалтерскойОтчетностиТабличное.НачатьВыводТаблицы(
		ПолучениеДанныхТаблицОтчета,
		ИмяТаблицы,
		ШаблоныКолонок,
		ПараметрыТекстаРасшифровки);
	
	Дебет = НСтр("ru = 'Дт';
				|en = 'Dr'");
	Кредит = НСтр("ru = 'Кт';
					|en = 'Cr'");
	ШаблонПроводки =  НСтр("ru = '%1 %2';
							|en = '%1 %2'");
	
	Пока ВыборкаСтадия.Следующий() Цикл
		
		УстановитьПараметрыТекстовРасшифровкиПоСтадииКапитальныхВложений(ПараметрыТекстаРасшифровки, СчетаУчетаПоВидуАктивов, КлассификаторСтадий, ВыборкаСтадия.Стадия);
		
		ВыборкаГруппа = ВыборкаСтадия.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

		Пока ВыборкаГруппа.Следующий() Цикл
			
			УстановитьПараметрыТекстовРасшифровкиПоГруппеКапитальныхВложений(ПараметрыТекстаРасшифровки, ВыборкаГруппа.Группа);
			
			НачатьДобавлениеСтрокиКапитальныеВложения(
				КапитальныеВложения,
				ВыборкаГруппа,
				ШаблоныЗаголовковРасшифровки,
				ПараметрыТекстаРасшифровки,
				Классификаторы);
				
			ВыборкаОрганизация = ВыборкаГруппа.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

			Пока ВыборкаОрганизация.Следующий() Цикл
				// Данных в выборке нет, выводится только пояснение
				Если Не ЗначениеЗаполнено(ВыборкаОрганизация.Организация) Тогда
					Продолжить;
				КонецЕсли;
				
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьФилиал(
					КапитальныеВложения,
					ШаблоныЗаголовковРасшифровки,
					ВыборкаОрганизация.Организация,
					ВыборкаОрганизация.ОрганизацияПредставление);
						
				ВыборкаСчет = ВыборкаОрганизация.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);

				Пока ВыборкаСчет.Следующий() Цикл
					ЗначенияНастроекОтчета = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.ЗначенияНастроекОтчетаКапитальныеВложения(ВыборкаСчет);
					УстановленОтборПоЗавершеннымОбъектам = Ложь;
					
					Если ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы() Тогда
						УстановленОтборПоЗавершеннымОбъектам = НастроитьОтчетНематериальныеАктивы(
								ЗначенияНастроекОтчета,
								ВыборкаСчет,
								СчетаУчетаПоВидуАктивов,
								ВидыСубконто,
								КлассификаторСтадий,
								ВнеоборотныеАктивыНаНачалоПоГруппам);
					Иначе
						НастроитьОтчетОсновныеСредства(ЗначенияНастроекОтчета, ВыборкаСчет, СчетаУчетаПоВидуАктивов, ВидыСубконто, КлассификаторСтадий);
					КонецЕсли;
					
					ДобавитьСлагаемое(КапитальныеВложения, "НачалоЗатраты", ВыборкаСчет, ВыборкаСчет.НачалоЗатраты, "", ЗначенияНастроекОтчета);
					ДобавитьСлагаемое(КапитальныеВложения, "КонецЗатраты", ВыборкаСчет, ВыборкаСчет.КонецЗатраты, "", ЗначенияНастроекОтчета);
					ДобавитьСлагаемое(КапитальныеВложения, "Затраты", ВыборкаСчет, ВыборкаСчет.Затраты, Дебет, ЗначенияНастроекОтчета);
					ДобавитьСлагаемое(КапитальныеВложения, "Списано", ВыборкаСчет, ВыборкаСчет.Списано, Кредит, ЗначенияНастроекОтчета);
				
					ВыборкаВидДвижения = ВыборкаСчет.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
					
					Пока ВыборкаВидДвижения.Следующий() Цикл
						Если Не ЗначениеЗаполнено(ВыборкаВидДвижения.ВидДвижения) Тогда
							Продолжить;
						КонецЕсли;
						ОписаниеВидаДвижения = "";
						Если ВыборкаВидДвижения.ВидДвижения = "Перемещение" Тогда
							ОписаниеВидаДвижения = НСтр("ru = 'За вычетом оборота внутри счетов капитальных вложений:';
														|en = 'Minus the turnover within the capital investment accounts:'");
						ИначеЕсли ВыборкаВидДвижения.ВидДвижения = "ПринятиеКУчету" Тогда
							ОписаниеВидаДвижения = НСтр("ru = 'За вычетом принятых к учету:';
														|en = 'Minus recognized ones:'");
						ИначеЕсли ВыборкаВидДвижения.ВидДвижения = "МежфилиальныйОборот" Тогда
							ОписаниеВидаДвижения = НСтр("ru = 'За вычетом перемещения между филиалами:';
														|en = 'Minus the transfer between branch offices:'");
						КонецЕсли;
	
						ВыводитьЗаголовокЗатраты = Истина;
						ВыводитьЗаголовокСписание = Истина;
						
						Выборка = ВыборкаВидДвижения.Выбрать();
						
						ЗначенияНастроекОтчета.ИмяОтчета = "АнализСчета";
						ЗначенияНастроекОтчета.Отбор.Удалить("Счет");
						Если Не УстановленОтборПоЗавершеннымОбъектам И (ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы()
								Или ВыборкаВидДвижения.ВидСубконто = ВидыСубконто.ВнеоборотныйАктив
									И Не ЭтоСчетМодернизации(ВыборкаВидДвижения, СчетаУчетаПоВидуАктивов)) Тогда
							УстановитьОтборПоЗавершеннымОбъектам(
								ЗначенияНастроекОтчета,
								ВыборкаВидДвижения,
								ВнеоборотныеАктивыНаНачалоПоГруппам,
								КлассификаторСтадий);
						КонецЕсли;
						
						Пока Выборка.Следующий() Цикл
							ЗначенияНастроекОтчета.КорСчет = Выборка.КорСчет;
							Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроекОтчета, КонтекстЗаполненияОтчета.Период);

							Если Выборка.Затраты <> 0 И Выборка.ВидДвижения <> "ПринятиеКУчету" Тогда
								
								ВывестиЗаголовокИсключения(КапитальныеВложения, "Затраты", ОписаниеВидаДвижения, - ВыборкаВидДвижения.Затраты, ВыводитьЗаголовокЗатраты);
								Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПроводки, Кредит, Выборка.КорСчетКод);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(КапитальныеВложения, "Затраты", Описание, -Выборка.Затраты, , Отчет, 1);
								
							КонецЕсли;
							
							Если Выборка.Списано <> 0 Тогда
								
								ВывестиЗаголовокИсключения(КапитальныеВложения, "Списано", ОписаниеВидаДвижения, - ВыборкаВидДвижения.Списано, ВыводитьЗаголовокСписание);
								Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПроводки, Дебет, Выборка.КорСчетКод);
								ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(КапитальныеВложения, "Списано", Описание, -Выборка.Списано, , Отчет, 1);
								
								Если Выборка.ВидДвижения = "ПринятиеКУчету" Тогда
									ОписаниеДт = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПроводки, Дебет, Выборка.КорСчетКод);
									ОписаниеКт = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПроводки, Кредит, Выборка.СчетКод);
									Описание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПроводки, ОписаниеДт, ОписаниеКт);
									ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(КапитальныеВложения, "Завершено", Описание, Выборка.Списано, , Отчет);
								КонецЕсли;
								
							КонецЕсли;
						КонецЦикла;
					КонецЦикла;
				КонецЦикла
			КонецЦикла;
		ЗаполнениеБухгалтерскойОтчетностиТабличное.ЗакончитьДобавлениеСтрокиТаблицыОтчета(КапитальныеВложения, ШаблоныЗаголовковРасшифровки);
		КонецЦикла;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НастроитьОтчетНематериальныеАктивы(ЗначенияНастроекОтчета, ВыборкаСчет, СчетаУчетаПоВидуАктивов, ВидыСубконто, КлассификаторСтадий, ВнеоборотныеАктивыНаНачалоПоГруппам)
	УстановленОтборПоЗавершеннымОбъектам = Ложь;
	ЗначенияНастроекОтчета.ИмяОтчета = "АнализСубконто";
	ЗначенияНастроекОтчета.ГруппировкаСубконто.Добавить(ВидыСубконто.ВнеоборотныйАктив);
	// Для кап. вложений в НМА субконто на счете 04 - НМА, а на счете 08 - Расходы на НИОКР,
	// отчет Анализ субконто не подходит.
	Если ВидыСубконто.ВнеоборотныйАктив <> ВыборкаСчет.ВидСубконто Тогда
		ЗначенияНастроекОтчета.ИмяОтчета = "ОборотноСальдоваяВедомостьПоСчету";
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета, 
			"Субконто%1.ВидНематериальногоАктива",
			ВыборкаСчет.Счет,
			ВыборкаСчет.ВидСубконто,
			ВыборкаСчет.Группа);
		УстановитьОтборПоЗавершеннымОбъектам(
			ЗначенияНастроекОтчета,
			ВыборкаСчет,
			ВнеоборотныеАктивыНаНачалоПоГруппам,
			КлассификаторСтадий);
		УстановленОтборПоЗавершеннымОбъектам = Истина;
	Иначе
		ЗначенияНастроекОтчета.Отбор.Вставить("Счет", ОтборСчетовУчетаАнализСубконто(ВыборкаСчет, СчетаУчетаПоВидуАктивов));
		ЗначенияНастроекОтчета.Отбор.Вставить("Субконто1.ВидНематериальногоАктива", ВыборкаСчет.Группа);
	КонецЕсли;
	
	Возврат УстановленОтборПоЗавершеннымОбъектам;
КонецФункции

Процедура НастроитьОтчетОсновныеСредства(ЗначенияНастроекОтчета, ВыборкаСчет, СчетаУчетаПоВидуАктивов, ВидыСубконто, КлассификаторСтадий)

	ЗначенияНастроекОтчета.ИмяОтчета = "ОборотноСальдоваяВедомостьПоСчету";
	// Счета 23.05 и 08.04.5 всегда отражаются в "Последующих вложениях", специальных отчетов
	// не требуется.
	Если ЭтоСчетМодернизации(ВыборкаСчет, СчетаУчетаПоВидуАктивов) Тогда
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета,
			"Субконто%1.ГруппаОС",
			ЗначенияНастроекОтчета.Счет,
			ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства,
			ВыборкаСчет.Группа);
	ИначеЕсли ВыборкаСчет.ВидСубконто = ВидыСубконто.ВнеоборотныйАктив Тогда
		ЗначенияНастроекОтчета.ИмяОтчета = "АнализСубконто";
		ЗначенияНастроекОтчета.ГруппировкаСубконто.Добавить(ВидыСубконто.ВнеоборотныйАктив);
		ЗначенияНастроекОтчета.Отбор.Вставить("Субконто1.ГруппаОС", ВыборкаСчет.Группа);
		ЗначенияНастроекОтчета.Отбор.Вставить("Счет", ОтборСчетовУчетаАнализСубконто(ВыборкаСчет, СчетаУчетаПоВидуАктивов));
	ИначеЕсли ВыборкаСчет.ВидСубконто = ВидыСубконто.Компоненты Тогда
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета,
			"Субконто%1.Родитель",
			ЗначенияНастроекОтчета.Счет,
			ВидыСубконто.Компоненты,
			ВыборкаСчет.Группа);
	ИначеЕсли ВыборкаСчет.ВидСубконто = ВидыСубконто.СозданиеВнеоборотногоАктива Тогда
		ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
			ЗначенияНастроекОтчета,
			"Субконто%1",
			ЗначенияНастроекОтчета.Счет,
			ВидыСубконто.СозданиеВнеоборотногоАктива,
			ВыборкаСчет.Группа);
	КонецЕсли;
КонецПроцедуры

Функция ЭтоСчетМодернизации(ВыборкаСчет, СчетаУчетаПоВидуАктивов)
	Возврат СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения.Найти(ВыборкаСчет.Счет) <> Неопределено;
КонецФункции

Функция ОтборСчетовУчетаАнализСубконто(ВыборкаСчет, СчетаУчетаПоВидуАктивов)

	СчетаУчета = Новый СписокЗначений();
	СчетаУчета.ЗагрузитьЗначения(СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивыИерархия);
	СчетаУчета.Добавить(ВыборкаСчет.Счет);
	ЭлементаОтбораСчетУчета = ЗаполнениеРасшифровкаРегламентированнойОтчетности.НовыйЗначениеЭлементаОтбора();
	ЭлементаОтбораСчетУчета.ВидСравненияКомпоновкиДанных = ВидСравненияКомпоновкиДанных.ВСпискеПоИерархии;
	ЭлементаОтбораСчетУчета.Значение = СчетаУчета;
	
	Возврат ЭлементаОтбораСчетУчета;
КонецФункции

Процедура УстановитьОтборПоЗавершеннымОбъектам(ЗначенияНастроекОтчета, Выборка, ВнеоборотныеАктивыНаНачалоПоГруппам, КлассификаторСтадий)
	ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастроитьОтборПоВидуСубконто(
		ЗначенияНастроекОтчета,
		"Субконто%1",
		Выборка.Счет,
		Выборка.ВидСубконто,
		ВнеоборотныеАктивыНаНачалоПоГруппам[Выборка.Группа],
		ВидСравненияПоСтадии(Выборка.Стадия, КлассификаторСтадий));
КонецПроцедуры

Функция ВидСравненияПоСтадии(Стадия, КлассификаторСтадий)
	ВидСравненияКомпоновки = ВидСравненияКомпоновкиДанных.ВСписке;
	Если Стадия <> КлассификаторСтадий.ПоследующиеВложения Тогда
		ВидСравненияКомпоновки = ВидСравненияКомпоновкиДанных.НеВСписке;
	КонецЕсли;
	Возврат ВидСравненияКомпоновки;
КонецФункции

// Счета учета по виду активов.
//
// Параметры:
//  ВидКапитальныхВложений - Строка
// 
// Возвращаемое значение:
//  см. НовыйКлассификаторСчетовУчета
//
Функция СчетаУчетаПоВидуАктивов(ВидКапитальныхВложений)
	
	СчетаУчетаПоВидуАктивов = НовыйКлассификаторСчетовУчета();
	
	// Пользователь может добавлять субсчета на счет 08. Считаем, что субсчета с субконто НМА - аналогичны счету 08.05,
	// субсчета с субконто Расходы на НИОКР - счету 08.08, остальные - относятся к кап. вложениям в основные средства.
	ПользовательскиеСчетаРазработки = ЗаполнениеБухгалтерскойОтчетностиТабличное.СчетаПоСубконто(
		СчетаУчетаПоКоду("08", "08.08, 08.05"), ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР);
	ПользовательскиеСчетаНМА = ЗаполнениеБухгалтерскойОтчетностиТабличное.СчетаПоСубконто(
		СчетаУчетаПоКоду("08", "08.08, 08.05"), ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы);
		
	ПользовательскиеСчетаРазработкиНМА = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПользовательскиеСчетаРазработкиНМА, ПользовательскиеСчетаРазработки);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПользовательскиеСчетаРазработкиНМА, ПользовательскиеСчетаНМА);
	
	Если ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы() Тогда
	
		СчетаУчетаПоВидуАктивов.ЗатратыСоздание =  СчетаУчетаПоКоду("08.08");
		СчетаУчетаПоВидуАктивов.ЗатратыПриобретение =  СчетаУчетаПоКоду("08.05");
		СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения = Новый Массив; // Нет счетов учета, свойственных только модернизации
		СчетаУчетаПоВидуАктивов.ВсеЗатраты = СчетаУчетаПоКоду("08.05, 08.08");
		СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивы = СчетаУчетаПоКоду("04");
		СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивыИерархия =
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.НематериальныеАктивы);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчетаПоВидуАктивов.ЗатратыСоздание, ПользовательскиеСчетаРазработки);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчетаПоВидуАктивов.ЗатратыПриобретение, ПользовательскиеСчетаНМА);
		
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СчетаУчетаПоВидуАктивов.ВсеЗатраты, ПользовательскиеСчетаРазработкиНМА);
		
	ИначеЕсли ВидКапитальныхВложений = ВидКапитальныхВложенийОсновныеСредства() Тогда
	
		СчетаУчетаПоВидуАктивов.ЗатратыСоздание =  СчетаУчетаПоКоду("08.03");
		СчетаУчетаПоВидуАктивов.ЗатратыПриобретение =  СчетаУчетаПоКоду("08.01, 08.02, 08.04, 08.06, 08.07");
		СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения = Новый Массив;
		СчетаУчетаПоВидуАктивов.ВсеЗатраты = СчетаУчетаПоКоду("08.01, 08.02, 08.03, 08.04, 08.06, 08.07");
		СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивы = СчетаУчетаПоКоду("01");
		// Затраты на ремонты ОС анализируем особым образом, исключив его из ВсеЗатраты:
		// кроме субконто ОС, важно и второе субконто Вид ремонта, он должен быть капитализируемым.
		СчетаУчетаПоВидуАктивов.Ремонты = Новый Массив;
		СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивыИерархия =
			ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ПланыСчетов.Хозрасчетный.ОсновныеСредства);
		
		// Исключим пользовательские счета НМА и разработок.
		СчетаУчетаПоВидуАктивов.ЗатратыПриобретение =
			ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчетаПоВидуАктивов.ЗатратыПриобретение, ПользовательскиеСчетаРазработкиНМА);
		СчетаУчетаПоВидуАктивов.ВсеЗатраты = 
			ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчетаПоВидуАктивов.ВсеЗатраты, ПользовательскиеСчетаРазработкиНМА);
			
	КонецЕсли;
	
	Возврат СчетаУчетаПоВидуАктивов;
	
КонецФункции

Процедура СоздатьСтоимостьКапитальныхВложений(МенеджерВременныхТаблиц, КонтекстЗаполненияОтчета, ИмяТаблицы, СчетаУчетаПоВидуАктивов, ВидыСубконто, КлассификаторСтадий)

	ВидКапитальныхВложений = ВидКапитальныхВложений(ИмяТаблицы);
	
	ВидСубконтоВнеоборотныйАктив = ВидыСубконто.ВнеоборотныйАктив;
	ВидСубконтоСоздание =          ВидыСубконто.СозданиеВнеоборотногоАктива;
	ВидСубконтоКомпоненты =        ВидыСубконто.Компоненты;
	
	СчетаУчетаБезИспользуемыхВидовСубконто = СчетаУчетаБезИспользуемыхВидовСубконто(СчетаУчетаПоВидуАктивов.ВсеЗатраты, ВидыСубконто);
	
	СчетаЗатрат                     = СчетаУчетаПоВидуАктивов.ВсеЗатраты;
	СчетаВнеоборотныеАктивы         = СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивы;
	СчетаЗатратыПоследующиеВложения = СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения;
	СчетаЗатратыСоздание            = СчетаУчетаПоВидуАктивов.ЗатратыСоздание;
	СчетаЗатратыПриобретение        = СчетаУчетаПоВидуАктивов.ЗатратыПриобретение;
	
	СчетаМежфилиальныйОборот        = СчетаУчетаПоКоду("79");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачалоПериода",              КонтекстЗаполненияОтчета.Период.Начало);
	Запрос.УстановитьПараметр("КонецПериода",               КонтекстЗаполненияОтчета.Период.Конец);
	Запрос.УстановитьПараметр("Организации",                КонтекстЗаполненияОтчета.Организации);
	
	Запрос.УстановитьПараметр("СчетаУчетаБезИспользуемыхВидовСубконто", СчетаУчетаБезИспользуемыхВидовСубконто);
	Запрос.УстановитьПараметр("СчетаВнеоборотныеАктивы",                СчетаВнеоборотныеАктивы);
	Запрос.УстановитьПараметр("СчетаЗатрат",                            СчетаЗатрат);
	Запрос.УстановитьПараметр("СчетаЗатратыПоследующиеВложения",        СчетаЗатратыПоследующиеВложения);
	Запрос.УстановитьПараметр("СчетаЗатратыСоздание",                   СчетаЗатратыСоздание);
	Запрос.УстановитьПараметр("СчетаЗатратыПриобретение",               СчетаЗатратыПриобретение);
	Запрос.УстановитьПараметр("СчетаМежфилиальныйОборот",               СчетаМежфилиальныйОборот);
		
	Запрос.УстановитьПараметр("ВидСубконтоВнеоборотныйАктив", ВидСубконтоВнеоборотныйАктив);
	Запрос.УстановитьПараметр("ВидСубконтоСоздание",          ВидСубконтоСоздание);
	Запрос.УстановитьПараметр("ВидСубконтоКомпоненты",        ВидСубконтоКомпоненты);
	
	Запрос.УстановитьПараметр("Создание",            КлассификаторСтадий.Создание);
	Запрос.УстановитьПараметр("Приобретение",        КлассификаторСтадий.Приобретение);
	Запрос.УстановитьПараметр("ПоследующиеВложения", КлассификаторСтадий.ПоследующиеВложения);
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	// Для поля Группа в последнем запросе пакета вместо выражения "&ВыражениеГруппа" используется более сложное выражение 
	// "ВЫБОР КОГДА СальдоЗатраты.Организация ЕСТЬ НЕ NULL ТОГДА &ВыражениеГруппа ИНАЧЕ НЕОПРЕДЕЛЕНО КОНЕЦ",
	// в котором условие "СальдоЗатраты.Организация ЕСТЬ НЕ NULL" заведомо всегда будет являться Истина.
	// Так сделано, потому что при редактировании запроса конструктором запроса простые поля
	// (не зависящие от полей таблиц, из которых выбираются данные), могут автоматически исключиться из полей группировки.
	// Но выражение, которым в тексте запроса заменяется строка "&ВыражениеГруппа", должно входить и в поле группировки.
	
	//@skip-check ql-temp-table-index
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Создание КАК Стадия
	|ПОМЕСТИТЬ ВсеСтадии
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Приобретение
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&ПоследующиеВложения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация КАК Организация,
	|	ХозрасчетныйОстатки.Счет КАК Счет,
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто,
	|	ХозрасчетныйОстатки.СуммаОстаток КАК Начало,
	|	&ВидСубконтоВнеоборотныйАктив КАК ВидСубконто
	|ПОМЕСТИТЬ Сальдо
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаЗатрат), &ВидСубконтоВнеоборотныйАктив, Организация В (&Организации)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация,
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстаток,
	|	&ВидСубконтоСоздание
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаЗатрат), &ВидСубконтоСоздание, Организация В (&Организации)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация,
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстаток,
	|	&ВидСубконтоКомпоненты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаЗатрат), &ВидСубконтоКомпоненты, Организация В (&Организации)) КАК ХозрасчетныйОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОстатки.Организация,
	|	ХозрасчетныйОстатки.Счет,
	|	ХозрасчетныйОстатки.Субконто1,
	|	ХозрасчетныйОстатки.СуммаОстаток,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(&НачалоПериода, Счет В (&СчетаУчетаБезИспользуемыхВидовСубконто), , Организация В (&Организации)) КАК ХозрасчетныйОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация КАК Организация,
	|	ХозрасчетныйОбороты.Счет КАК Счет,
	|	ХозрасчетныйОбороты.КорСчет КАК КорСчет,
	|	ХозрасчетныйОбороты.Субконто1 КАК Субконто,
	|	ХозрасчетныйОбороты.СуммаОборотДт КАК Дт,
	|	ХозрасчетныйОбороты.СуммаОборотКт КАК Кт,
	|	ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто1
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто2
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто3 КАК ЭтоОборотПоСовпадающейАналитике,
	|	&ВидСубконтоВнеоборотныйАктив КАК ВидСубконто
	|ПОМЕСТИТЬ Затраты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатрат), &ВидСубконтоВнеоборотныйАктив, Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.КорСчет,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт,
	|	ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто1
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто2
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто3,
	|	&ВидСубконтоСоздание
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатрат), &ВидСубконтоСоздание, Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.КорСчет,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт,
	|	ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто1
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто2
	|		ИЛИ ХозрасчетныйОбороты.Субконто1 = ХозрасчетныйОбороты.КорСубконто3,
	|	&ВидСубконтоКомпоненты
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаЗатрат), &ВидСубконтоКомпоненты, Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ХозрасчетныйОбороты.Организация,
	|	ХозрасчетныйОбороты.Счет,
	|	ХозрасчетныйОбороты.КорСчет,
	|	ХозрасчетныйОбороты.Субконто1,
	|	ХозрасчетныйОбороты.СуммаОборотДт,
	|	ХозрасчетныйОбороты.СуммаОборотКт,
	|	ИСТИНА,
	|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка)
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Обороты(&НачалоПериода, &КонецПериода, , Счет В (&СчетаУчетаБезИспользуемыхВидовСубконто), , Организация В (&Организации), , ) КАК ХозрасчетныйОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сальдо.Организация КАК Организация,
	|	Сальдо.Счет КАК Счет,
	|	NULL КАК КорСчет,
	|	Сальдо.Субконто КАК Субконто,
	|	Сальдо.Начало КАК НачалоЗатраты,
	|	Сальдо.Начало КАК КонецЗатраты,
	|	0 КАК Затраты,
	|	0 КАК Списано,
	|	ЛОЖЬ КАК ЭтоОборотПоСовпадающейАналитике,
	|	Сальдо.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ СальдоЗатраты
	|ИЗ
	|	Сальдо КАК Сальдо
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Затраты.Организация,
	|	Затраты.Счет,
	|	Затраты.КорСчет,
	|	Затраты.Субконто,
	|	0,
	|	Затраты.Дт - Затраты.Кт,
	|	Затраты.Дт,
	|	-Затраты.Кт,
	|	Затраты.ЭтоОборотПоСовпадающейАналитике,
	|	Затраты.ВидСубконто
	|ИЗ
	|	Затраты КАК Затраты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ХозрасчетныйОстатки.Субконто1 КАК Субконто
	|ПОМЕСТИТЬ ВнеоборотныеАктивыСальдоНачало
	|ИЗ
	|	РегистрБухгалтерии.Хозрасчетный.Остатки(
	|			&НачалоПериода,
	|			Счет В (&СчетаВнеоборотныеАктивы),
	|			&ВидСубконтоВнеоборотныйАктив,
	|			Организация В (&Организации)
	|				И Субконто1 В
	|					(ВЫБРАТЬ
	|						СальдоЗатраты.Субконто
	|					ИЗ
	|						СальдоЗатраты КАК СальдоЗатраты)) КАК ХозрасчетныйОстатки
	|ГДЕ
	|	ХозрасчетныйОстатки.СуммаОстатокДт > 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Субконто
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СальдоЗатраты.Организация КАК Организация,
	|	СальдоЗатраты.Счет КАК Счет,
	|	СальдоЗатраты.КорСчет КАК КорСчет,
	|	ВЫБОР
	|		КОГДА СальдоЗатраты.Организация ЕСТЬ НЕ NULL 
	|			ТОГДА &ВыражениеГруппа
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Группа,
	|	СУММА(СальдоЗатраты.НачалоЗатраты) КАК НачалоЗатраты,
	|	СУММА(СальдоЗатраты.КонецЗатраты) КАК КонецЗатраты,
	|	СУММА(СальдоЗатраты.Затраты) КАК Затраты,
	|	СУММА(СальдоЗатраты.Списано) КАК Списано,
	|	Счета.Код КАК СчетКод,
	|	КорСчета.Код КАК КорСчетКод,
	|	СальдоЗатраты.Организация.Представление КАК ОрганизацияПредставление,
	|	СальдоЗатраты.Организация = СальдоЗатраты.Организация.ГоловнаяОрганизация КАК ЭтоГоловнаяОрганизация,
	|	ВЫБОР
	|		КОГДА СальдоЗатраты.Счет В (&СчетаЗатратыПоследующиеВложения)
	|				ИЛИ ВнеоборотныеАктивыСальдоНачало.Субконто ЕСТЬ НЕ NULL 
	|			ТОГДА &ПоследующиеВложения
	|		КОГДА СальдоЗатраты.Счет В (&СчетаЗатратыСоздание)
	|			ТОГДА &Создание
	|		ИНАЧЕ &Приобретение
	|	КОНЕЦ КАК Стадия,
	|	ВЫБОР
	|		КОГДА (СальдоЗатраты.Счет В (&СчетаЗатратыПоследующиеВложения)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыПоследующиеВложения)
	|				ИЛИ СальдоЗатраты.Счет В (&СчетаЗатратыСоздание)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыСоздание)
	|				ИЛИ СальдоЗатраты.Счет В (&СчетаЗатратыПриобретение)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыПриобретение))
	|				И СальдоЗатраты.ЭтоОборотПоСовпадающейАналитике
	|			ТОГДА ""Перемещение""
	|		КОГДА СальдоЗатраты.КорСчет В (&СчетаМежфилиальныйОборот)
	|			ТОГДА ""МежфилиальныйОборот""
	|		КОГДА СальдоЗатраты.КорСчет В (&СчетаВнеоборотныеАктивы)
	|			ТОГДА ""ПринятиеКУчету""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидДвижения,
	|	Счета.Порядок КАК ПорядокСчета,
	|	КорСчета.Порядок КАК ПорядокКорСчета,
	|	СальдоЗатраты.ВидСубконто КАК ВидСубконто
	|ПОМЕСТИТЬ КапитальныеВложения
	|ИЗ
	|	СальдоЗатраты КАК СальдоЗатраты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК Счета
	|		ПО СальдоЗатраты.Счет = Счета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК КорСчета
	|		ПО СальдоЗатраты.КорСчет = КорСчета.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВнеоборотныеАктивыСальдоНачало КАК ВнеоборотныеАктивыСальдоНачало
	|		ПО СальдоЗатраты.Субконто = ВнеоборотныеАктивыСальдоНачало.Субконто
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА СальдоЗатраты.Организация ЕСТЬ НЕ NULL 
	|			ТОГДА &ВыражениеГруппа
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА СальдоЗатраты.Счет В (&СчетаЗатратыПоследующиеВложения)
	|				ИЛИ ВнеоборотныеАктивыСальдоНачало.Субконто ЕСТЬ НЕ NULL 
	|			ТОГДА &ПоследующиеВложения
	|		КОГДА СальдоЗатраты.Счет В (&СчетаЗатратыСоздание)
	|			ТОГДА &Создание
	|		ИНАЧЕ &Приобретение
	|	КОНЕЦ,
	|	СальдоЗатраты.КорСчет,
	|	СальдоЗатраты.Организация,
	|	СальдоЗатраты.Счет,
	|	КорСчета.Код,
	|	Счета.Код,
	|	КорСчета.Порядок,
	|	Счета.Порядок,
	|	СальдоЗатраты.Организация.Представление,
	|	ВЫБОР
	|		КОГДА (СальдоЗатраты.Счет В (&СчетаЗатратыПоследующиеВложения)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыПоследующиеВложения)
	|				ИЛИ СальдоЗатраты.Счет В (&СчетаЗатратыСоздание)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыСоздание)
	|				ИЛИ СальдоЗатраты.Счет В (&СчетаЗатратыПриобретение)
	|					И СальдоЗатраты.КорСчет В (&СчетаЗатратыПриобретение))
	|				И СальдоЗатраты.ЭтоОборотПоСовпадающейАналитике
	|			ТОГДА ""Перемещение""
	|		КОГДА СальдоЗатраты.КорСчет В (&СчетаМежфилиальныйОборот)
	|			ТОГДА ""МежфилиальныйОборот""
	|		КОГДА СальдоЗатраты.КорСчет В (&СчетаВнеоборотныеАктивы)
	|			ТОГДА ""ПринятиеКУчету""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	СальдоЗатраты.ВидСубконто,
	|	СальдоЗатраты.Организация = СальдоЗатраты.Организация.ГоловнаяОрганизация
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Стадия";
		
	Если ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы() Тогда
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ВыражениеГруппа",
			"	ВЫБОР
			|		КОГДА СальдоЗатраты.Субконто ССЫЛКА Справочник.НематериальныеАктивы
			|			ТОГДА ВЫРАЗИТЬ(СальдоЗатраты.Субконто КАК Справочник.НематериальныеАктивы).ВидНМА
			|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыНМА.ПрочиеНематериальныеАктивы)
			|	КОНЕЦ");
	Иначе
		Запрос.Текст = СтрЗаменить(
			Запрос.Текст,
			"&ВыражениеГруппа",
			"	ВЫБОР
			|		КОГДА СальдоЗатраты.Субконто ССЫЛКА Справочник.ОбъектыЭксплуатации
			|			ТОГДА ВЫРАЗИТЬ(СальдоЗатраты.Субконто КАК Справочник.ОбъектыЭксплуатации).ГруппаОС
			|		КОГДА СальдоЗатраты.Субконто ССЫЛКА Справочник.Номенклатура
			|			ТОГДА ВЫРАЗИТЬ(СальдоЗатраты.Субконто КАК Справочник.Номенклатура).Родитель
			|		ИНАЧЕ СальдоЗатраты.Субконто
			|	КОНЕЦ");
	КонецЕсли;
			
	Запрос.Выполнить();
		
КонецПроцедуры

Функция ВнеоборотныеАктивыНаНачалоПоГруппам(МенеджерВременныхТаблиц, ВидКапитальныхВложений)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ВидКапитальныхВложенийНМА", ВидКапитальныхВложений = ВидКапитальныхВложенийНематериальныеАктивы());
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВнеоборотныеАктивыСальдоНачало.Субконто КАК Субконто,
	|	ВЫБОР
	|		КОГДА &ВидКапитальныхВложенийНМА
	|			ТОГДА ВЫРАЗИТЬ(ВнеоборотныеАктивыСальдоНачало.Субконто КАК Справочник.НематериальныеАктивы).ВидНМА
	|		ИНАЧЕ ВЫРАЗИТЬ(ВнеоборотныеАктивыСальдоНачало.Субконто КАК Справочник.ОбъектыЭксплуатации).ГруппаОС
	|	КОНЕЦ КАК Группа
	|ИЗ
	|	ВнеоборотныеАктивыСальдоНачало КАК ВнеоборотныеАктивыСальдоНачало
	|ИТОГИ ПО
	|	Группа";
	
	ВыборкаГруппа = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ВнеоборотныеАктивыНаНачалоПоГруппам = Новый Соответствие();
	
	Пока ВыборкаГруппа.Следующий() Цикл
		Если Не ЗначениеЗаполнено(ВыборкаГруппа.Группа) Тогда
			Продолжить;
		КонецЕсли;
		Выборка = ВыборкаГруппа.Выбрать();
		ВнеоборотныеАктивыГруппы = Новый СписокЗначений();
		Пока Выборка.Следующий() Цикл
			ВнеоборотныеАктивыГруппы.Добавить(Выборка.Субконто);
		КонецЦикла;
		ВнеоборотныеАктивыНаНачалоПоГруппам.Вставить(ВыборкаГруппа.Группа, ВнеоборотныеАктивыГруппы);
	КонецЦикла;
	
	Возврат ВнеоборотныеАктивыНаНачалоПоГруппам;
	
КонецФункции

// Параметры текстов расшифровки капитальные вложения.
// 
// Параметры:
//  КонтекстЗаполненияОтчета - см. БухгалтерскаяОтчетностьБРО.НовыйКонтекстЗаполненияОтчета
// 
// Возвращаемое значение:
//  Структура:
// * Период - Строка
// * НачалоПериода - Строка
// * КонецПериода - Строка
// * НаименованиеГруппы - Строка
// * СчетуКапитальныхВложений - Строка
// * СчетаКапитальныхВложений - Строка
// * СчетамиКапитальныхВложений - Строка
// * СчетаВнеоборотныхАктивов - Строка
// * СчетуВнеоборотныхАктивов - Строка
// * СчетомВнеоборотныхАктивов - Строка
// * Уточнение - Строка
Функция ПараметрыТекстовРасшифровкиКапитальныеВложения(КонтекстЗаполненияОтчета)
	
	ПараметрыТекстов = Новый Структура;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.УстановитьПараметрыТекстовРасшифровки(ПараметрыТекстов, КонтекстЗаполненияОтчета);
	
	// См. УстановитьПараметрыТекстовРасшифровкиПоГруппеКапитальныхВложений, УстановитьПараметрыТекстовРасшифровкиПоСтадииКапитальныхВложений
	ПараметрыТекстов.Вставить("НаименованиеГруппы", "");
	ПараметрыТекстов.Вставить("СчетуКапитальныхВложений", "");
	ПараметрыТекстов.Вставить("СчетаКапитальныхВложений", "");
	ПараметрыТекстов.Вставить("СчетамиКапитальныхВложений", "");
	ПараметрыТекстов.Вставить("СчетаВнеоборотныхАктивов", "");
	ПараметрыТекстов.Вставить("СчетуВнеоборотныхАктивов", "");
	ПараметрыТекстов.Вставить("СчетомВнеоборотныхАктивов", "");
	ПараметрыТекстов.Вставить("Уточнение", "");
	
	Возврат ПараметрыТекстов;
	
КонецФункции

Процедура ЗаполнитьШаблоныКолонокКапитальныеВложения(ШаблоныКолонок, ШаблоныРасшифровки, ВидКапитальныхВложений)
		
	ТекстВнеоборотныхАктивов = НСтр("ru = 'нематериальных активов';
									|en = 'intangible assets'");
	Если ВидКапитальныхВложений = ВидКапитальныхВложенийОсновныеСредства() Тогда
		ТекстВнеоборотныхАктивов = НСтр("ru = 'основных средств';
										|en = 'fixed assets'");
	КонецЕсли;
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"НачалоЗатраты",
		НСтр("ru = 'фактические затраты на [НачалоПериода]';
			|en = 'actual costs as of [НачалоПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуКапитальныхВложений] [Уточнение]';
			|en = 'balance on [СчетуКапитальныхВложений] [Уточнение]'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"Затраты",
		НСтр("ru = 'затраты за [Период]';
			|en = 'costs for [Период]'"),
		НСтр("ru = 'Оборот по дебету [СчетаКапитальныхВложений] [Уточнение]
					|Исключаются внутренние обороты: внутри счетов капитальных вложений, между филиалами';
					|en = '[СчетаКапитальныхВложений] debit turnover [Уточнение]
					|Internal turnover is excluded: within capital investment accounts, between branch offices'"));
		
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"Списано",
		НСтр("ru = 'списано за [Период]';
			|en = 'written off for [Период]'"),
		НСтр("ru = 'Оборот по кредиту [СчетаКапитальныхВложений] [Уточнение]
					|Исключаются внутренние обороты: внутри счетов капитальных вложений, в корреспонденции со [СчетомВнеоборотныхАктивов], между филиалами';
					|en = '[СчетаКапитальныхВложений] credit turnover [Уточнение]
					|Internal turnover is excluded: within capital investment accounts, in correspondence with [СчетомВнеоборотныхАктивов], between branch offices'"));
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"Завершено",
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'принято к учету в качестве %1 за [Период]';
				|en = 'Recognized as %1 for [Период]'"),
			ТекстВнеоборотныхАктивов),
		НСтр("ru = 'Оборот по дебету [СчетаВнеоборотныхАктивов] в корреспонденции со [СчетамиКапитальныхВложений] [Уточнение]';
			|en = '[СчетаВнеоборотныхАктивов] debit turnover in correspondence with [СчетамиКапитальныхВложений] [Уточнение]'"));
	
	ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки,
		"КонецЗатраты",
		НСтр("ru = 'фактические затраты на [КонецПериода]';
			|en = 'actual costs as of [КонецПериода]'"),
		НСтр("ru = 'Сальдо по [СчетуКапитальныхВложений] [Уточнение]';
			|en = 'balance on [СчетуКапитальныхВложений] [Уточнение]'"));
		
КонецПроцедуры

Процедура ДобавитьШаблонЗаголовковРасшифровки(ШаблоныКолонок, ШаблоныРасшифровки, ИмяКолонки, ШаблонЗаголовкаКолонки, ШаблонПорядкаРасчета)
	
	ШаблоныКолонок.Вставить(ИмяКолонки, ШаблонЗаголовкаКолонки);
	
	ШаблонЗаголовковРасшифровки = ЗаполнениеБухгалтерскойОтчетностиТабличное.НовыйШаблонЗаголовковРасшифровки();
	
	ШаблонЗаголовковРасшифровки.ЗаголовокГруппы = НСтр("ru = '[НаименованиеГруппы]: [ПредставлениеКолонки]';
														|en = '[НаименованиеГруппы]: [ПредставлениеКолонки]'");
	ШаблонЗаголовковРасшифровки.ПорядокРасчета  = ШаблонПорядкаРасчета;
	
	ШаблоныРасшифровки.Вставить(ИмяКолонки, ШаблонЗаголовковРасшифровки);
	
КонецПроцедуры

Процедура УстановитьПараметрыТекстовРасшифровкиПоСтадииКапитальныхВложений(ПараметрыТекста, СчетаУчетаПоВидуАктивов, КлассификаторСтадий, Стадия)
	
	ПредставлениеСпискаВнеоборотныеАктивы = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаУчетаПоВидуАктивов.ВнеоборотныеАктивыИерархия);
	ПараметрыТекста.СчетаВнеоборотныхАктивов = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(ПредставлениеСпискаВнеоборотныеАктивы);
	ПараметрыТекста.СчетуВнеоборотныхАктивов = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСпискаВнеоборотныеАктивы);
	ПараметрыТекста.СчетомВнеоборотныхАктивов = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовТворительныйПадеж(ПредставлениеСпискаВнеоборотныеАктивы);
	
	ПредставлениеСпискаЗатраты = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаУчетаПоВидуАктивов.ЗатратыСоздание);
	Если Стадия = КлассификаторСтадий.Приобретение Тогда
		ПредставлениеСпискаЗатраты = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаУчетаПоВидуАктивов.ЗатратыПриобретение);
	ИначеЕсли Стадия = КлассификаторСтадий.ПоследующиеВложения Тогда
		СчетаЗатрат = ОбщегоНазначенияКлиентСервер.РазностьМассивов(СчетаУчетаПоВидуАктивов.ВсеЗатраты, СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения);
		ПредставлениеСпискаЗатраты = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаЗатрат);
	КонецЕсли;
	
	ПараметрыТекста.СчетуКапитальныхВложений = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовДательныйПадеж(ПредставлениеСпискаЗатраты);
	ПараметрыТекста.СчетаКапитальныхВложений = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовРодительныйПадеж(ПредставлениеСпискаЗатраты);
	ПараметрыТекста.СчетамиКапитальныхВложений = ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовТворительныйПадеж(ПредставлениеСпискаЗатраты);
		
	Если Стадия = КлассификаторСтадий.ПоследующиеВложения Тогда
		Уточнения = Новый Массив;
		Уточнения.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'по объектам, имеющим на %1 сальдо по %2';
				|en = 'for objects with balance on %2 as of %1'"),
			ПараметрыТекста.НачалоПериода,
			ПараметрыТекста.СчетуВнеоборотныхАктивов));
		Если СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения.Количество() > 0 Тогда
			ПредставлениеСпискаПоследующиеВложения =
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСписка(СчетаУчетаПоВидуАктивов.ЗатратыПоследующиеВложения);
			ДополнительноеУточнение =  СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = ', а также в корреспонденции со %1';
					|en = ', as well as in correspondence with %1'"),
				ЗаполнениеБухгалтерскойОтчетностиТабличное.ПредставлениеСпискаСчетовТворительныйПадеж(ПредставлениеСпискаПоследующиеВложения));
			Уточнения.Добавить(ДополнительноеУточнение);
		КонецЕсли;
		ПараметрыТекста.Уточнение = СтрСоединить(Уточнения);
	Иначе
		ПараметрыТекста.Уточнение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'по объектам, поступившим в течение отчетного периода. Объектами, поступившими в течение периода считаются не имеющие сальдо по %1 на %2';
				|en = 'for objects received during the accounting period. Objects received during the period are considered to have no balance on %1 as of %2'"),
			ПараметрыТекста.СчетуВнеоборотныхАктивов,
			ПараметрыТекста.НачалоПериода);
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьПараметрыТекстовРасшифровкиПоГруппеКапитальныхВложений(ПараметрыТекстовРасшифровки, Группа)
	
	ПараметрыТекстовРасшифровки.НаименованиеГруппы = Группа;
	
КонецПроцедуры

Процедура НачатьДобавлениеСтрокиКапитальныеВложения(КапитальныеВложения, Выборка, ШаблоныКолонок, ПараметрыТекстовРасшифровки, Классификаторы)
	
	НоваяСтрока = ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСтрокуТаблицыОтчета(
		КапитальныеВложения,
		ШаблоныКолонок,
		ПараметрыТекстовРасшифровки);
		
	НоваяСтрока.Группа = Выборка.Группа;
	НоваяСтрока.Стадия = Классификаторы[КапитальныеВложения.ИмяТаблицы].Стадия[Выборка.Стадия];
	НоваяСтрока.Вывод = ЗначениеЗаполнено(Выборка.Группа);
	
КонецПроцедуры

Функция ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, Выборка, Сумма, ДтКт, ЗначенияНастроек)
	
	ПредметЗаписи      = Выборка.Счет;
	ПредставлениеСчета = Выборка.СчетКод;
	Если Не ПустаяСтрока(ДтКт) Тогда
		ПредставлениеСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1 %2", ДтКт, Выборка.СчетКод);
	КонецЕсли;

	Отчет = ЗаполнениеБухгалтерскойОтчетностиРасшифровка.НастройкиОтчетаПоЗначениям(ЗначенияНастроек, ВыводТаблицы.КонтекстЗаполненияОтчета.Период);
	
	Возврат ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьСлагаемое(ВыводТаблицы, ИмяКолонки, ПредставлениеСчета, Сумма, ПредметЗаписи, Отчет);
	
КонецФункции

Процедура ВывестиЗаголовокИсключения(ВыводТаблицы, ИмяКолонки, Описание, Сумма, Выводить)
	
	Если Не Выводить Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнениеБухгалтерскойОтчетностиТабличное.ДобавитьЗаписьРасшифровкиСтроки(
		ВыводТаблицы,
		ИмяКолонки,
		"ИерархияСлагаемых",
		Описание,
		0);
		
		Выводить = Ложь;
		
КонецПроцедуры

// Коллекция видов субконто для каждого вида капитальных вложений.
// 
// Возвращаемое значение:
//  Структура:
// * ВнеоборотныйАктив - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
// * СозданиеВнеоборотногоАктива - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
// * Компоненты - ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
// * Ремонты - Массив из ПланВидовХарактеристикСсылка.ВидыСубконтоХозрасчетные
//
Функция НовыйВидыСубконто()

	ВидыСубконто = Новый Структура;
	ВидыСубконто.Вставить("ВнеоборотныйАктив", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	ВидыСубконто.Вставить("СозданиеВнеоборотногоАктива", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	ВидыСубконто.Вставить("Компоненты", ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
	ВидыСубконто.Вставить("Ремонты", Новый Массив);
	
	Возврат ВидыСубконто
	
КонецФункции

// Классификация счетов учета.
// 
// Возвращаемое значение:
//  Структура:
// * ВсеЗатраты - Массив из ПланСчетовСсылка.Хозрасчетный
// * ЗатратыСоздание - Массив из ПланСчетовСсылка.Хозрасчетный
// * ЗатратыПриобретение - Массив из ПланСчетовСсылка.Хозрасчетный
// * ЗатратыПоследующиеВложения - Массив из ПланСчетовСсылка.Хозрасчетный
// * ВнеоборотныеАктивы - Массив из ПланСчетовСсылка.Хозрасчетный
// * ВнеоборотныеАктивыИерархия - Массив из ПланСчетовСсылка.Хозрасчетный
// * Ремонты - Массив из ПланСчетовСсылка.Хозрасчетный
//
Функция НовыйКлассификаторСчетовУчета()

	КлассификацияСчетовУчета = Новый Структура;
	КлассификацияСчетовУчета.Вставить("ВсеЗатраты", Новый Массив);
	КлассификацияСчетовУчета.Вставить("ЗатратыСоздание", Новый Массив);
	КлассификацияСчетовУчета.Вставить("ЗатратыПриобретение", Новый Массив);
	КлассификацияСчетовУчета.Вставить("ЗатратыПоследующиеВложения", Новый Массив);
	КлассификацияСчетовУчета.Вставить("ВнеоборотныеАктивы", Новый Массив);
	КлассификацияСчетовУчета.Вставить("ВнеоборотныеАктивыИерархия", Новый Массив);
	КлассификацияСчетовУчета.Вставить("Ремонты", Новый Массив);

	Возврат КлассификацияСчетовУчета;
	
КонецФункции

// Виды субконто по виду активов.
// 
// Возвращаемое значение:
//  Структура - см. НовыйВидыСубконто
Функция ВидыСубконтоПоВидуАктивов(ВидАктивов)

	ВидыСубконтоПоВидуАктивов = НовыйВидыСубконто();
	
	Если ВидАктивов = ВидКапитальныхВложенийОсновныеСредства() Тогда

		ВидыСубконтоПоВидуАктивов.ВнеоборотныйАктив = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства;
		ВидыСубконтоПоВидуАктивов.СозданиеВнеоборотногоАктива = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОбъектыСтроительства;
		ВидыСубконтоПоВидуАктивов.Компоненты = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура;
		СубконтоРемонты = Новый Массив;
		СубконтоРемонты.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ОсновныеСредства);
		ВидыСубконтоПоВидуАктивов.Ремонты = СубконтоРемонты;

	Иначе

		ВидыСубконтоПоВидуАктивов.ВнеоборотныйАктив = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.НематериальныеАктивы;
		ВидыСубконтоПоВидуАктивов.СозданиеВнеоборотногоАктива = ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.РасходыНаНИОКР;
		// Чтобы не модифицировать текст запроса в зависимости от вида капитальных вложений, заполним виды субконто пустыми значениями.
		// В противном случае при выполнении запроса возникнет ошибка при обращении к Субконто2 - ВидыРемонтов.
		СубконтоРемонты = Новый Массив;
		СубконтоРемонты.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
		СубконтоРемонты.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.ПустаяСсылка());
		ВидыСубконтоПоВидуАктивов.Ремонты = СубконтоРемонты;
	
	КонецЕсли;
	
	Возврат ВидыСубконтоПоВидуАктивов;
	
КонецФункции

// Счета учета без субконто.
// 
// Параметры:
//  СчетаУчета - Массив из ПланСчетовСсылка.Хозрасчетный
//  ВидыСубконто - см. НовыйВидыСубконто
// 
// Возвращаемое значение:
//  Массив из ПланСчетовСсылка.Хозрасчетный
//
Функция СчетаУчетаБезИспользуемыхВидовСубконто(СчетаУчета, ВидыСубконто)
	
	ИспользуемыеВидыСубконто = Новый Массив;
	Для Каждого ВидСубконто Из ВидыСубконто Цикл
		ИспользуемыеВидыСубконто.Добавить(ВидСубконто.Значение);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК СчетУчета
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный.ВидыСубконто КАК ХозрасчетныйВидыСубконто
		|		ПО Хозрасчетный.Ссылка = ХозрасчетныйВидыСубконто.Ссылка
		|			И (ХозрасчетныйВидыСубконто.ВидСубконто В (&ИспользуемыеСубконто))
		|ГДЕ
		|	ХозрасчетныйВидыСубконто.Ссылка ЕСТЬ NULL
		|	И Хозрасчетный.Ссылка В(&СчетаУчета)";
	
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Запрос.УстановитьПараметр("ИспользуемыеСубконто", ИспользуемыеВидыСубконто);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СчетУчета");
	
КонецФункции

// Возвращает иерархию счетов учета по списку условных кодов.
// 
// Параметры:
//  КодыСчетов- Строка
//  КодыИсключений - Строка
// 
// Возвращаемое значение:
//  Массив из ПланСчетовСсылка.Хозрасчетный
Функция СчетаУчетаПоКоду(КодыСчетов, КодыИсключений = "")
	
	СчетаУчета = Новый Массив;
	Для Каждого КодСчета Из СтрРазделить(КодыСчетов, ",") Цикл
		СчетаУчета.Добавить(ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет(СокрЛП(КодСчета)));
	КонецЦикла;
	
	СчетаИсключений = Новый Массив;
	Если Не ПустаяСтрока(КодыИсключений) Тогда
		Для Каждого КодИсключения Из СтрРазделить(КодыИсключений, ",") Цикл
			СчетаИсключений.Добавить(ЗаполнениеБухгалтерскойОтчетностиИтоги.Счет(СокрЛП(КодИсключения)));
		КонецЦикла;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Хозрасчетный.Ссылка КАК СчетУчета
		|ИЗ
		|	ПланСчетов.Хозрасчетный КАК Хозрасчетный
		|ГДЕ
		|	НЕ Хозрасчетный.ЗапретитьИспользоватьВПроводках
		|	И Хозрасчетный.Ссылка В ИЕРАРХИИ(&СчетаУчета)
		|	И НЕ Хозрасчетный.Ссылка В ИЕРАРХИИ (&СчетаИсключений)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Хозрасчетный.Порядок";
	
	Запрос.УстановитьПараметр("СчетаУчета", СчетаУчета);
	Запрос.УстановитьПараметр("СчетаИсключений", СчетаИсключений);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СчетУчета");
	
КонецФункции

// Возвращает вид капитальных вложений, для которого составляется пояснение.
// 
// Параметры:
//  ИмяТаблицы - Строка
// 
// Возвращаемое значение:
//  Строка - Вид капитальных вложений
//
Функция ВидКапитальныхВложений(ИмяТаблицы)
	
	Если ИмяТаблицы = "КапитальныеВложенияНематериальныеАктивы" Тогда
		Возврат ВидКапитальныхВложенийНематериальныеАктивы();
	КонецЕсли;
	
	Возврат ВидКапитальныхВложенийОсновныеСредства();
	
КонецФункции

// Капитальные вложения в нематериальные активы.
// 
// Возвращаемое значение:
//  Строка
//
Функция ВидКапитальныхВложенийНематериальныеАктивы()
	Возврат "НематериальныеАктивы";
КонецФункции

// Капитальные вложения в основные средства.
// 
// Возвращаемое значение:
//  Строка
//
Функция ВидКапитальныхВложенийОсновныеСредства()
	Возврат "ОсновныеСредства";
КонецФункции

#КонецОбласти
