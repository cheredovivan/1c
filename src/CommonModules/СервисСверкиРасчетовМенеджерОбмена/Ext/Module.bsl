
#Область СлужебныйПрограммныйИнтерфейс

Функция ВыполнитьЗапросОтправитьСообщения(ПараметрыЗапроса) Экспорт
	
	ЕстьОшибки = Ложь;
	ЗапросКСервису = ПараметрыЗапросаОтправитьСообщения(ПараметрыЗапроса);
	ДанныеСервиса = ВыполнитьЗапрос(ЗапросКСервису, ЕстьОшибки);
	
	Возврат ДанныеСервиса;
	
КонецФункции

Функция ВыполнитьЗапросПолучитьРасхождения(ПараметрыЗапроса) Экспорт
	
	ЕстьОшибки = Ложь;
	ЗапросКСервису = ПараметрыЗапросаПолучитьРасхождения(ПараметрыЗапроса);
	ДанныеСервиса = ВыполнитьЗапрос(ЗапросКСервису, ЕстьОшибки);
	
	Возврат ДанныеСервиса;
	
КонецФункции

Функция РезультатПодключенияОрганизациюКСервису(Организация) Экспорт
	
	Результат = ОписаниеРезультатаПодключенияОрганизацииКСервису(Организация);
	
	Если Не ИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки() Тогда
		Результат.ТекстОшибки = НСтр("ru = 'Не подключена Интернет-поддержка пользователей';
									|en = 'Online user support not connected'");
		Возврат Результат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеИТС = ИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки("1c-bn-access");
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Не ЗначениеЗаполнено(ДанныеИТС.Тикет) Тогда
		Результат.ТекстОшибки = ТекстОшибкиПодключенияОрганизации();
		Возврат Результат;
	КонецЕсли;
	
	Отказ = Ложь;
	ВременныйКлючДоступа = КлючДоступаПоТикетуИТС(ДанныеИТС.Тикет, Отказ);
	
	Если Отказ Тогда
		Результат.ТекстОшибки = ТекстОшибкиПодключенияОрганизации();
		Возврат Результат;
	КонецЕсли;

	Попытка
		НатуральныйИдентификаторОрганизации = БизнесСеть.НатуральныйИдентификаторОрганизации(Организация);
	Исключение
		Результат.ТекстОшибки = ТекстОшибкиПодключенияОрганизации(); 
		МетаданныеОбъекта = Метаданные.Справочники.Контрагенты;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось зарегистрировать организацию %1 в 1С:Бизнес-сеть по причине: %2';
									|en = 'Cannot register the %1 company in 1C:Business Network due to: %2'"),
			Организация, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(СервисСверкиРасчетов.ИмяСервиса(), УровеньЖурналаРегистрации.Ошибка, МетаданныеОбъекта,, ТекстОшибки);
		Возврат Результат;
	КонецПопытки;
	
	НатуральныеИдентификаторы = 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НатуральныйИдентификаторОрганизации);
	
	ДанныеОрганизации = БизнесСеть.ДанныеОрганизацийПоНатуральнымИдентификаторам(
		НатуральныеИдентификаторы, Отказ);
		
	// Организация ранее не регистрировалась
	Если Не ЗначениеЗаполнено(ДанныеОрганизации) Тогда
		
		ЗарегистрироватьОрганизациюНеинтерактивно(Организация, ВременныйКлючДоступа.ТокенДоступа);
		
		ДанныеОрганизации = БизнесСеть.ДанныеОрганизацийПоНатуральнымИдентификаторам(
			НатуральныеИдентификаторы, Отказ);
		
		Если Отказ Или ДанныеОрганизации.Количество() = 0 Тогда
			Результат.ТекстОшибки = НСтр("ru = 'Ошибка получения данных организации по идентификатору.';
										|en = 'Error receiving company data by ID.'");
			Возврат Результат;
		КонецЕсли;
	КонецЕсли;
	
	ИдентификаторОрганизации = ДанныеОрганизации[0].Идентификатор;
	
	КодАктивации = КодАктивацииВБизнесСети(ВременныйКлючДоступа.ТокенДоступа, ИдентификаторОрганизации, Отказ);
	Если Не ЗначениеЗаполнено(КодАктивации) Тогда
		Результат.ТекстОшибки = ТекстОшибкиПолученияКодаДоступаБизнесСеть();
		ЗаписатьКэшСтатусаРегистрацииОрганизации(Организация);
		Возврат Результат;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура();
	ПараметрыМетода.Вставить("Организация",  Организация);
	ПараметрыМетода.Вставить("КодАктивации", КодАктивации);
	
	Возврат РезультатПодключенияОрганизацииПоКодуАктивацииВФоне(ПараметрыМетода);
	
КонецФункции

#КонецОбласти

#Область НеинтерактивнаяРегистрацияОрганизации

Функция ТекстОшибкиПолученияКодаДоступаБизнесСеть()
	
	Возврат НСтр("ru = 'Ошибка получения кода активации 1С:Бизнес-сеть.';
				|en = 'Error receiving 1C:Business Network activation code.'");
	
КонецФункции

Функция ТекстОшибкиПодключенияОрганизации()
	
	Результат = НСтр("ru = 'Не удалось подключить организацию к Бизнес-сети.';
					|en = 'It failed to connect the company to the Business network.'") 
		+ Символы.ПС + НСтр("ru = 'Подробности см. в журнале регистрации.';
							|en = 'See the Event log for details.'");
		
	Возврат Результат;	
		
КонецФункции

Функция ТекстОшибкиПотериДоступа(ПричинаОшибки = "", Расширенный = Ложь)
	
	Шаблон = НСтр("ru = 'Доступ к сервису 1С:Бизнес-сеть приостановлен. %1
		|Необходимо повторное подключение организации к Бизнес-сети.';
		|en = 'Access to 1C:Business Network service is suspended. %1
		|Reconnect your company to 1C:Business Network.'");
	
	Если НЕ ПустаяСтрока(ПричинаОшибки) Тогда
		ПричинаОшибки = Символы.ПС + ПричинаОшибки;
	КонецЕсли;
	
	Результат = СтрШаблон(Шаблон, ПричинаОшибки);
	
	Если Расширенный Тогда
		Результат = Результат + БизнесСетьКлиентСервер.ПодробностиВЖурналеРегистрации();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ПодключитьОрганизациюЛокально(Организация, ИдентификаторОрганизации, КлючДоступа)
	
	НачатьТранзакцию();
	
	Попытка
		
		ЗаписатьИдентификаторОрганизации(Организация, ИдентификаторОрганизации);
		ЗаписатьКлючДоступаВЛокальноеХранилище(ИдентификаторОрганизации, КлючДоступа);
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ТекстОшибкиПодключенияОрганизации();
	КонецПопытки;

КонецПроцедуры

Процедура ЗаписатьИдентификаторОрганизации(Организация, Идентификатор)
	
	НачатьТранзакцию();

	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОрганизацииБизнесСеть");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Блокировка.Заблокировать();
		
		НаборЗаписей = РегистрыСведений.ОрганизацииБизнесСеть.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		
		НоваяЗапись = НаборЗаписей.Добавить();
		
		НоваяЗапись.Организация   = Организация;
		НоваяЗапись.Идентификатор = Идентификатор;
		
		НаборЗаписей.Записать();
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение НСтр("ru = 'Не удалось записать идентификатор организации';
								|en = 'Cannot write down the company''s identifier'");
	КонецПопытки;
		
КонецПроцедуры

Процедура ЗаписатьКлючДоступаВЛокальноеХранилище(ИдентификаторОрганизации, ДанныеТокена)
	
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		ВызватьИсключение НСтр("ru = 'Не указан идентификатор организации';
								|en = 'Company ID is not specified'");
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(
		ИдентификаторОрганизации, 
		ДанныеТокена, 
		ИдентификаторХраненияКлючаДоступа());
		
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры 

Функция ИдентификаторХраненияКлючаДоступа()
	
	Возврат "БизнесСетьТокенОрганизации";
	
КонецФункции

Функция ОписаниеРезультатаПодключенияОрганизацииКСервису(Организация = Неопределено)
	
	Результат = Новый Структура;
	
	Результат.Вставить("СтатусПодключения", СтатусыПодключения().ТребуетсяИнтерактивноеПодключение);
	Результат.Вставить("Организация",       Организация);
	Результат.Вставить("Идентификатор",     "");
	Результат.Вставить("ТекстОшибки",       "");
		
	Возврат Результат;
	
КонецФункции

Функция РезультатПодключенияОрганизацииПоКодуАктивацииВФоне(ПараметрыМетода)
	
	Организация  = ПараметрыМетода.Организация;
	КодАктивации = ПараметрыМетода.КодАктивации;
	
	Отказ = Ложь;
	
	ПодключитьОрганизациюПоКодуАктивации(Организация, КодАктивации, Отказ);
	
	Результат = ОписаниеРезультатаПодключенияОрганизацииКСервису(Организация);
	
	Если Не Отказ Тогда
		
		Результат.СтатусПодключения = СтатусыПодключения().Подключена;
		Результат.Идентификатор     = БизнесСеть.ИдентификаторОрганизации(Организация);
		
		СервисСверкиРасчетов.ВключитьИспользованиеОбменаБизнесСети();
		ОчиститьКэшСтатусаПодключенияОрганизации(Организация);
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаписатьКэшСтатусаРегистрацииОрганизации(Организация)
	
	СервисСверкиРасчетовПереопределяемый.ЗаписатьДанныеВХранилище(Организация, Истина,
		СервисСверкиРасчетов.ИдентификаторОрганизацияЗарегистрированаПодИнымЛогиномИТС());

КонецПроцедуры

Процедура ОчиститьКэшСтатусаПодключенияОрганизации(Организация)
	
	ЗарегистрированаВБизнесСетиПодИнымЛогиномИТС = СервисСверкиРасчетовПереопределяемый.ПрочитатьДанныеИзХранилища(Организация,
		СервисСверкиРасчетов.ИдентификаторОрганизацияЗарегистрированаПодИнымЛогиномИТС());
	
	Если ЗначениеЗаполнено(ЗарегистрированаВБизнесСетиПодИнымЛогиномИТС)
		И ЗарегистрированаВБизнесСетиПодИнымЛогиномИТС = Истина Тогда
		СервисСверкиРасчетовПереопределяемый.ЗаписатьДанныеВХранилище(Организация, Ложь,
			СервисСверкиРасчетов.ИдентификаторОрганизацияЗарегистрированаПодИнымЛогиномИТС());
	КонецЕсли;

КонецПроцедуры

Функция СтатусыПодключения()
	
	Результат = Новый Структура;
	
	Результат.Вставить("ТребуетсяИнтерактивноеПодключение", "ТребуетсяИнтерактивноеПодключение");
	Результат.Вставить("Подключена",                        "Подключена");
	Результат.Вставить("ТребуетсяОбращениеКСервису",        "ТребуетсяОбращениеКСервису");
	Результат.Вставить("ТребуетсяПовторноеПодключение",     "ТребуетсяПовторноеПодключение");
	
	Возврат Результат;
	
КонецФункции

Процедура ПодключитьОрганизациюПоКодуАктивации(Организация, КодАктивации, Отказ)
	
	// Порядок регистрации:
	// - получение ключа доступа БЕЗ записи в ИБ
	// - получение идентификатора организации по натуральному ключу
	// - запись идентификатора в ИБ
	// - запись ключа доступа в ИБ
	
	НатуральныйИдентификаторОрганизации = БизнесСеть.НатуральныйИдентификаторОрганизации(Организация);
	
	КлючДоступа = КлючДоступаПоКодуАктивации(КодАктивации, НатуральныйИдентификаторОрганизации, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	НатуральныеИдентификаторы = 
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(НатуральныйИдентификаторОрганизации);
	
	ДанныеОрганизации = БизнесСеть.ДанныеОрганизацийПоНатуральнымИдентификаторам(НатуральныеИдентификаторы, Отказ);
		
	Если Отказ Или Не ЗначениеЗаполнено(ДанныеОрганизации) Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторОрганизации = ДанныеОрганизации[0].Идентификатор;
	
	ПодключитьОрганизациюЛокально(Организация, ИдентификаторОрганизации, КлючДоступа);
	
КонецПроцедуры

Функция ЗарегистрироватьОрганизациюНеинтерактивно(Организация, ТокенДоступа, Отказ = Ложь)
	
	ПараметрыКоманды = ПараметрыКомандыЗарегистрироватьОрганизациюНеинтерактивно(Организация, ТокенДоступа);
	ДанныеСервиса = БизнесСеть.ВыполнитьКомандуСервиса(ПараметрыКоманды, Отказ);
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

Функция ПараметрыКомандыЗарегистрироватьОрганизациюНеинтерактивно(Организация, ТокенДоступа)
	
	РеквизитыОрганизации = ДанныеОрганизации(Организация);
	
	ДанныеJSON = Новый Структура;
	ДанныеJSON.Вставить("name", ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(РеквизитыОрганизации.НаименованиеДляПечатныхФорм));
	ДанныеJSON.Вставить("description", "");
	
	Если СервисСверкиРасчетовПереопределяемый.ЭтоЮридическоеЛицо(РеквизитыОрганизации.ЮридическоеФизическоеЛицо) Тогда
		ДанныеJSON.Вставить("legalRequisites", Новый Структура("inn, kpp", РеквизитыОрганизации.ИНН, РеквизитыОрганизации.КПП));
		Должность = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(РеквизитыОрганизации.ДолжностьРуководителяПредставление);
		КонтактноеЛицо = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(Строка(РеквизитыОрганизации.Руководитель));
	Иначе
		ДанныеJSON.Вставить("legalRequisites", Новый Структура("inn", РеквизитыОрганизации.ИНН));
		Должность = НСтр("ru = 'Индивидуальный предприниматель';
						|en = 'Individual entrepreneur'");
		КонтактноеЛицо = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(РеквизитыОрганизации.ФИОФизлица);
	КонецЕсли;
	
	ТелефонОрганизации = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(РеквизитыОрганизации.Телефоны);
	Если Не ЗначениеЗаполнено(ТелефонОрганизации) Тогда
		Телефон = "+79000000001";
	Иначе
		Телефон = Лев(ТелефонОрганизации, 16);
	КонецЕсли;
	
	EmailОрганизации = ОбщегоНазначенияКлиентСервер.УдалитьНедопустимыеСимволыXML(РеквизитыОрганизации.Email);
	Если ЗначениеЗаполнено(EmailОрганизации)
		И ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(EmailОрганизации) Тогда
		Email = EmailОрганизации;
	Иначе
		Email = "test@test.ru";
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КонтактноеЛицо) Тогда
		КонтактноеЛицо = НСтр("ru = 'Служебное контактное лицо. Автоматическая регистрация в сервисе.';
								|en = 'Service contact person. Automatic registration in the service.'");
	КонецЕсли;
	
	ДанныеJSON.Вставить("contactPerson", Новый Структура(
		"fullName, email, phone, position",
		КонтактноеЛицо, Email, Телефон, Должность));
		
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("X-Client-App", "verify");
	
	ПараметрыКоманды = БизнесСеть.ОписаниеПараметровКомандыСервиса();
	ПараметрыКоманды.Вставить("Заголовки", Заголовки);
	ПараметрыКоманды.Наименование             = НСтр("ru = 'Получение данных организации по натуральному идентификатору.';
													|en = 'Receipt of company data by natural ID.'");
	ПараметрыКоманды.Адрес                    = "/organization-profiles";
	ПараметрыКоманды.Метод                    = "POST";
	ПараметрыКоманды.Аутентификация           = Истина;
	ПараметрыКоманды.ТипКонтента              = ТипКонтентаПриРаботеСТокенами();
	ПараметрыКоманды.Данные                   = БизнесСеть.ЗначениеВJSON(ДанныеJSON);
	ПараметрыКоманды.ВременныйТокен           = ТокенДоступа;
	
	ПараметрыКоманды.Ошибки.Вставить(404, НСтр("ru = 'Не найдена учетная запись для программы.';
												|en = 'Account for application is not found.'"));
	ПараметрыКоманды.Ошибки.Вставить(403, НСтр("ru = 'Недостаточно прав для выполнения операции.';
												|en = 'Insufficient rights to perform the operation.'"));
	ПараметрыКоманды.Ошибки.Вставить(400, НСтр("ru = 'Пользователь принадлежит другой учетной записи.';
												|en = 'User belongs to another account.'"));
	ПараметрыКоманды.Ошибки.Вставить(422, НСтр("ru = 'Пользователь принадлежит другой учетной записи.';
												|en = 'User belongs to another account.'"));

	Возврат ПараметрыКоманды;
	
КонецФункции

Функция ДанныеОрганизации(Организация)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = СервисСверкиРасчетовПереопределяемый.СведенияОЮрФизЛице(Организация, ТекущаяДата());
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ТипКонтентаПриРаботеСТокенами()
	
	Результат = "application/vnd.1cbn.v1+json";
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолученияКодаАктивации

Функция КодАктивацииВБизнесСети(ТокенДоступа, ИдентификаторОрганизации, Отказ = Ложь)
	
	Если Не ЗначениеЗаполнено(ИдентификаторОрганизации) Тогда
		ВызватьИсключение НСтр("ru = 'Организация не зарегистрирована Бизнес-сети';
								|en = 'The company is not registered Business Networks'");
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	
	ПараметрыМетода.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	ПараметрыМетода.Вставить("ТокенДоступа",             ТокенДоступа);
	ПараметрыМетода.Вставить("Комментарий",              СтрШаблон(НСтр("ru = 'Ключ выдан пользователю 1С:Предприятия %1';
																		|en = 'Key is issued to 1C:Enterprise user %1'"), Пользователи.ТекущийПользователь()));
	
	ПараметрыКоманды = ПараметрыКомандыПолучитьКодАктивацииВБизнесСети(ПараметрыМетода);
	ДанныеСервиса = БизнесСеть.ВыполнитьКомандуСервиса(ПараметрыКоманды, Отказ);
	
	Если Отказ Тогда
		
		Если ПараметрыКоманды.КодСостояния = 403 Тогда
			ПриостановитьДоступКСервису(ИдентификаторОрганизации);
		КонецЕсли;
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	Результат = ДанныеКодаАктивацииИзJSON(ДанныеСервиса);
	
	Возврат Результат.КодАктивации;
	
КонецФункции

Функция ПараметрыКомандыПолучитьКодАктивацииВБизнесСети(ПараметрыМетода)
	
	Данные = ТелоЗапросаПолученияКодаАктивацииВБизнесСети(ПараметрыМетода.ИдентификаторОрганизации, ПараметрыМетода.Комментарий);
	
	ПараметрыКоманды = БизнесСеть.ОписаниеПараметровКомандыСервиса();
	ПараметрыКоманды.Наименование   = НСтр("ru = 'Получение кода активации';
											|en = 'Get the activation code'");
	ПараметрыКоманды.Адрес          = "/access-keys";
	ПараметрыКоманды.Метод          = "POST";
	ПараметрыКоманды.Данные         = БизнесСеть.ЗначениеВJSON(Данные);
	ПараметрыКоманды.ВременныйТокен = ПараметрыМетода.ТокенДоступа;
	ПараметрыКоманды.ТипКонтента    = ТипКонтентаПриРаботеСТокенами();
	
	ПричинаОшибки = НСтр("ru = 'Недостаточно прав для выполнения операции. Токен обновления Бизнес-сети инвалидирован.';
						|en = 'Insufficient rights to perform the operation. The update token of 1C:Business Network is disabled.'");
	ТекстОшибки = ТекстОшибкиПотериДоступа(ПричинаОшибки, Истина);
	
	ПараметрыКоманды.Ошибки.Вставить(403, ТекстОшибки);
	ПараметрыКоманды.Ошибки.Вставить(422, НСтр("ru = 'Ошибка данных, переданных в теле запроса.';
												|en = 'Error in data passed in the query body.'"));

	Возврат ПараметрыКоманды;
	
КонецФункции

Функция ТелоЗапросаПолученияКодаАктивацииВБизнесСети(ИдентификаторОрганизации, Комментарий)
	
	Результат = Новый Структура;
	
	Результат.Вставить("organizationId", ИдентификаторОрганизации);
	Результат.Вставить("app",            ПредставлениеПрикладногоРешения());
	Результат.Вставить("notes",          Комментарий);
	
	Возврат Результат;
	
КонецФункции 

Функция ДлинаСтрокиПредставленияПрикладногоРешения()
	
	Возврат 70;
	
КонецФункции

Функция ПредставлениеПрикладногоРешения()
	
	МаксимальнаяДлинаСтроки = ДлинаСтрокиПредставленияПрикладногоРешения();
	
	ВерсияПрикладногоРешения = Метаданные.Версия;
	ИмяПрикладногоРешения    = Лев(Метаданные.Синоним, МаксимальнаяДлинаСтроки - (СтрДлина(ВерсияПрикладногоРешения) + 1));
	
	Результат = СтрШаблон("%1 %2", ИмяПрикладногоРешения, ВерсияПрикладногоРешения);
	
	Возврат Результат;
	
КонецФункции

Процедура ПриостановитьДоступКСервису(ИдентификаторОрганизации)
	
	Организация = ОрганизацияПоИдентификатору(ИдентификаторОрганизации);
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ИзменитьПризнакПовторногоПодключения(Организация, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИзменитьПризнакПовторногоПодключения(Организация, ТребуетсяПовторноеПодключение)
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОрганизацииБизнесСеть");
		ЭлементБлокировки.УстановитьЗначение("Организация", Организация);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Блокировка.Заблокировать();

		// Корректировка записей
		
		НаборЗаписей = РегистрыСведений.ОрганизацииБизнесСеть.СоздатьНаборЗаписей();
		
		НаборЗаписей.Отбор.Организация.Установить(Организация);
		
		НаборЗаписей.Прочитать();
		
		Если НаборЗаписей.Количество() = 0 Тогда
			ОтменитьТранзакцию();
			Возврат;
		КонецЕсли;
		
		НоваяЗапись = НаборЗаписей[0];
		
		НоваяЗапись.Организация = Организация;
		
		НоваяЗапись.ТребуетсяПовторноеПодключение = Истина;
		
		УстановитьПривилегированныйРежим(Истина);
		
		НаборЗаписей.Записать();
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
	КонецПопытки;
	
КонецПроцедуры

Функция ОрганизацияПоИдентификатору(Идентификатор)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОрганизацииБизнесСеть.Организация КАК Организация
		|ИЗ
		|	РегистрСведений.ОрганизацииБизнесСеть КАК ОрганизацииБизнесСеть
		|ГДЕ
		|	ОрганизацииБизнесСеть.Идентификатор = &Идентификатор";
	
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Организация;
	Иначе
		Результат = Неопределено;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДанныеКодаАктивацииИзJSON(СтрокаJSON)
	
	ДанныеСервиса = БизнесСеть.ЗначениеИзСтрокиJSON(СтрокаJSON);
	
	Результат = Новый Структура;
	
	Результат.Вставить("КодАктивации",     ДанныеСервиса.accessCode);
	Результат.Вставить("ДатаАктуальности", ПрочитатьДатуJSON(ДанныеСервиса.accessCodeExpirationDate, ФорматДатыJSON.ISO));
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеКлючаДоступаПоКодуАктивации

Функция КлючДоступаПоКодуАктивации(КодАктивации, НатуральныйИдентификаторОрганизации, Отказ = Ложь)
	
	ПараметрыМетода = Новый Структура;
	
	ПараметрыМетода.Вставить("КодАктивации",                        КодАктивации);
	ПараметрыМетода.Вставить("НатуральныйИдентификаторОрганизации", НатуральныйИдентификаторОрганизации);
	
	ПараметрыКоманды = ПараметрыКомандыПолучитьКлючДоступаПоКодуАктивации(ПараметрыМетода);
	ДанныеСервиса = БизнесСеть.ВыполнитьКомандуСервиса(ПараметрыКоманды, Отказ);
	
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ДанныеКлючаДоступаИзJSON(ДанныеСервиса);
		
	Возврат Результат;
	
КонецФункции 

// Данные ключа доступа из JSON.
// 
// Параметры:
//  ДанныеСервиса - Структура
// 
// Возвращаемое значение:
//  Структура - Данные ключа доступа из JSON:
// * ТокенДоступа - Строка
// * ТокенОбновления - Строка
// * СрокДействияТокенаДоступа - Дата
// * СрокДействияТокенаОбновления - Дата
// * ТипТокена - Строка
// * ИдентификаторИнформационнойБазы - Строка
//
Функция ДанныеКлючаДоступаИзJSON(ДанныеСервиса)
	
	Результат = Новый Структура; 
	
	Результат.Вставить("ТокенДоступа", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "access_token", ""));
	Результат.Вставить("ТокенОбновления", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "refresh_token", ""));
	Результат.Вставить("СрокДействияТокенаДоступа", ТекущаяДатаСеанса() + 
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "access_token_expires", Дата(1, 1, 1)));
	Результат.Вставить("СрокДействияТокенаОбновления", ТекущаяДатаСеанса() + 
		ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "refresh_token_expires", Дата(1, 1, 1)));
	Результат.Вставить("ТипТокена", ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеСервиса, "token_type", ""));
	Результат.Вставить("ИдентификаторИнформационнойБазы", БизнесСеть.ИдентификаторИнформационнойБазы());
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыКомандыПолучитьКлючДоступаПоКодуАктивации(ПараметрыМетода)
	
	Данные = ТелоЗапросаПолученияКлючаДоступаПоКодуАктивации(
		ПараметрыМетода.КодАктивации, 
		ПараметрыМетода.НатуральныйИдентификаторОрганизации);
	
	ПараметрыКоманды = БизнесСеть.ОписаниеПараметровКомандыСервиса();
	ПараметрыКоманды.Наименование   = НСтр("ru = 'Получение токена по коду активации';
											|en = 'Getting a token by activation code'");
	ПараметрыКоманды.Адрес          = "/auth/token/by-access-code";
	ПараметрыКоманды.Метод          = "POST";
	ПараметрыКоманды.Данные         = БизнесСеть.ЗначениеВJSON(Данные);
	ПараметрыКоманды.Аутентификация = Ложь;
	ПараметрыКоманды.ТипКонтента    = ТипКонтентаПриРаботеСТокенами();
	
	ПараметрыКоманды.Ошибки.Вставить(422, НСтр("ru = 'Одноразовый пароль не прошел проверку на корректность.';
												|en = 'The one-time password did not pass the correctness test.'"));
	ПараметрыКоманды.Ошибки.Вставить(403, НСтр("ru = 'Одноразовый пароль уже был использован.';
												|en = 'One-time password has already been used.'"));

	Возврат ПараметрыКоманды;
	
КонецФункции

Функция ТелоЗапросаПолученияКлючаДоступаПоКодуАктивации(КодАктивации, НатуральныйИдентификаторОрганизации)
	
	Результат = Новый Структура;
	
	Результат.Вставить("accessCode", КодАктивации);
	Результат.Вставить("cid",        БизнесСеть.ИдентификаторИнформационнойБазы());
	Результат.Вставить("legalId",    НатуральныйИдентификаторОрганизации);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПолучениеКлючаДоступаПоТикетуИТС

Функция КлючДоступаПоТикетуИТС(ТикетИТС, Отказ = Ложь)
	
	ПараметрыМетода = Новый Структура;
	
	ПараметрыМетода.Вставить("ТикетИТС", ТикетИТС);
	
	ПараметрыКоманды = ПараметрыКомандыПолучитьКлючДоступаПоТикетуИТС(ПараметрыМетода);
	ДанныеСервиса = БизнесСеть.ВыполнитьКомандуСервиса(ПараметрыКоманды, Отказ);
	
	Если Не ЗначениеЗаполнено(ДанныеСервиса) Тогда
		Отказ = Истина;
	КонецЕсли;
		
	Если Отказ Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Результат = ДанныеКлючаДоступаИзJSON(ДанныеСервиса);
		
	Возврат Результат;
	
КонецФункции

Функция ПараметрыКомандыПолучитьКлючДоступаПоТикетуИТС(ПараметрыМетода)
	
	Данные = ТелоЗапросаПолученияКлючаДоступаПоТикетуИТС(ПараметрыМетода.ТикетИТС);
	
	ПараметрыКоманды = БизнесСеть.ОписаниеПараметровКомандыСервиса();
	ПараметрыКоманды.Наименование   = НСтр("ru = 'Получение токена по тикету Интернет-поддержки';
											|en = 'Receiving a token from an online support ticket'");
	ПараметрыКоманды.Адрес          = "/auth/token/by-its-onetime-ticket";
	ПараметрыКоманды.Метод          = "POST";
	ПараметрыКоманды.Данные         = БизнесСеть.ЗначениеВJSON(Данные);
	ПараметрыКоманды.Аутентификация = Ложь;
	ПараметрыКоманды.ТипКонтента    = ТипКонтентаПриРаботеСТокенами();
	
	ПараметрыКоманды.Ошибки.Вставить(403, НСтр("ru = 'Недостаточно прав для выполнения операции.';
												|en = 'Insufficient rights to perform the operation.'"));
	ПараметрыКоманды.Ошибки.Вставить(429, НСтр("ru = 'Превышено максимальное количество запросов.';
												|en = 'Maximum request quantity exceeded.'"));

	Возврат ПараметрыКоманды;
	
КонецФункции

Функция ТелоЗапросаПолученияКлючаДоступаПоТикетуИТС(ТикетИТС)
	
	Результат = Новый Структура;
	
	Результат.Вставить("itsOnetimeTicket", ТикетИТС);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

#Область Запрос

Функция ВыполнитьЗапрос(Знач ЗапросКСервису, ЕстьОшибки,
	ОжидатьОтвета = Ложь, ПодробноеОписаниеОшибки = "", КоличествоПопытокВыполнения = 0)
	
	ЕстьОшибки = Ложь;
	ТекстОшибки = "";
	
	УстановитьПривилегированныйРежим(Истина);
	
	// Инициализация параметров соединения.
	ПараметрыСоединения = ПараметрыСоединенияССервисом(ЗапросКСервису.ИмяСервиса);
	
	
	// Если в запросе установлен таймаут, переопределяем значение в параметрах соединения
	Если ЗначениеЗаполнено(ЗапросКСервису.Таймаут) Тогда
		ПараметрыСоединения.Таймаут = ЗапросКСервису.Таймаут;
	КонецЕсли;
	
	// Инициализация соединения.
	Соединение = НовоеСоединениеССервером(ПараметрыСоединения, ЕстьОшибки, ТекстОшибки);
	Если ЕстьОшибки Тогда
		ПодробноеОписаниеОшибки = ЗафиксироватьОшибкуВЗапросе(ПараметрыСоединения, ЗапросКСервису, ТекстОшибки);
		
		Возврат Неопределено;
	КонецЕсли;
	
	// Установка параметров запроса.
	HTTPЗапрос = Новый HTTPЗапрос(ПолныйАдресЗапроса(ПараметрыСоединения, ЗапросКСервису),
		ЗапросКСервису.Заголовки);
	
	// Заполнение стандартных заголовков
	Если ЗначениеЗаполнено(ЗапросКСервису.ПараметрыАвторизации) Тогда
		ДанныеТокена = ТокенАвторизации(ЗапросКСервису.ПараметрыАвторизации, ЕстьОшибки, ТекстОшибки);
		Если ЕстьОшибки Тогда
			ПодробноеОписаниеОшибки = ЗафиксироватьОшибкуВЗапросе(ПараметрыСоединения, ЗапросКСервису, ТекстОшибки);
			
			Возврат Неопределено;
		КонецЕсли;
	
		HTTPЗапрос.Заголовки.Вставить(ИмяЗаголовкаСтрокаАвторизации(), ДанныеТокена.ТокенДоступа);
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ЗапросКСервису.ТипПередаваемогоКонтента) И ЗапросКСервису.Метод <> "GET" Тогда
		HTTPЗапрос.Заголовки.Вставить(ИмяЗаголовкаТипПередаваемогоКонтента(),
			ЗапросКСервису.ТипПередаваемогоКонтента);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗапросКСервису.ТипВозвращаемогоКонтента) Тогда
		HTTPЗапрос.Заголовки.Вставить(ИмяЗаголовкаТипВозвращаемогоКонтента(),
			ЗапросКСервису.ТипВозвращаемогоКонтента);
	КонецЕсли;
	
	// Установка тела запроса.
	Если ЗначениеЗаполнено(ЗапросКСервису.Тело) Тогда
		Если ТипЗнч(ЗапросКСервису.Тело) = Тип("ДвоичныеДанные") Тогда
			HTTPЗапрос.УстановитьТелоИзДвоичныхДанных(ЗапросКСервису.Тело);
		Иначе
			HTTPЗапрос.УстановитьТелоИзСтроки(ЗапросКСервису.Тело, КодировкаТекста.UTF8,
				ИспользованиеByteOrderMark.НеИспользовать);
		КонецЕсли;
	КонецЕсли;
	
	// Выполнение запроса
	Попытка
		HTTPОтвет = Соединение.ВызватьHTTPМетод(ЗапросКСервису.Метод, HTTPЗапрос);
	Исключение
		ТекстОшибки = СтрШаблон(НСтр("ru = 'не удалось выполнить HTTP запрос по причине: %1';
									|en = 'cannot execute HTTP request. Reason: %1'",
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		
		ПодробноеОписаниеОшибки = ЗафиксироватьОшибкуВЗапросе(ПараметрыСоединения, ЗапросКСервису, ТекстОшибки, ЕстьОшибки);
		
		Возврат Неопределено;
	КонецПопытки;
	
	// Разбор тела ответа
	Результат = ОбработатьДанныеОтвета(ЗапросКСервису, HTTPОтвет, ОжидатьОтвета,
		ЕстьОшибки, ТекстОшибки, КоличествоПопытокВыполнения);
		
	Если ЕстьОшибки Тогда
		ПодробноеОписаниеОшибки = ЗафиксироватьОшибкуВЗапросе(ПараметрыСоединения, ЗапросКСервису, ТекстОшибки);
		
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыЗапросаОтправитьСообщения(ПараметрыМетода)
	
	ЗапросКСервису = НовыйЗапросКСервису();
	
	ИдентификаторОрганизации = ИдентификаторОрганизацииБизнесСети(ПараметрыМетода.Организация);
	СтрокаЗапроса = СтрШаблон("%1/checkDocuments/manageData", ИдентификаторОрганизации);
	
	ЗапросКСервису.Наименование = СтрШаблон("%1: %2",
		СервисСверкиРасчетов.ИмяСервиса(),
		СервисСверкиРасчетов.НазваниеОперацииОтправкаСообщений());
	ЗапросКСервису.Метод = "POST";
	ЗапросКСервису.СтрокаЗапроса = СтрокаЗапроса;
	ЗапросКСервису.ПараметрыАвторизации = НовыйПараметрыАвторизации(ПараметрыМетода.Организация);
	ЗапросКСервису.ТипВозвращаемогоКонтента = "application/vnd-1cbn-doc-list.v1+json";
	ЗапросКСервису.Тело = ПараметрыМетода.Данные;
	
	// Определение параметров восстановления тела ответа из двоичных данных
	ЗапросКСервису.ПараметрыВосстановления = НовыйПараметрыВосстановления();
	ЗапросКСервису.ПараметрыВосстановления.СвойстваТипаДата = 
		"date,created,currentStatusDate,supplierFulfilmentExtendedStatusDate";
	
	Возврат ЗапросКСервису;

КонецФункции

Функция ПараметрыЗапросаПолучитьРасхождения(ПараметрыМетода)
	
	ЗапросКСервису = НовыйЗапросКСервису();
	ДопустимыеКодыСостояния = ЗапросКСервису.ДопустимыеКодыСостояния;
	ДопустимыеКодыСостояния.Добавить(201);
	
	ИдентификаторОрганизации = ИдентификаторОрганизацииБизнесСети(ПараметрыМетода.Организация);
	СтрокаЗапроса = СтрШаблон("%1/checkDocuments/findData", ИдентификаторОрганизации);
	
	ЗапросКСервису.Заголовки.Вставить("datebegin", Формат(ПараметрыМетода.ДатаНачала, "ДФ=yyyy-MM-dd"));
	ЗапросКСервису.Заголовки.Вставить("dateend", Формат(ПараметрыМетода.ДатаОкончания, "ДФ=yyyy-MM-dd"));
	
	ЗапросКСервису.Наименование = СтрШаблон("%1: %2",
		СервисСверкиРасчетов.ИмяСервиса(),
		СервисСверкиРасчетов.НазваниеОперацииПолучениеРасхождений());
	ЗапросКСервису.Метод = "GET";
	ЗапросКСервису.СтрокаЗапроса = СтрокаЗапроса;
	ЗапросКСервису.ПараметрыАвторизации = НовыйПараметрыАвторизации(ПараметрыМетода.Организация);
	ЗапросКСервису.ТипВозвращаемогоКонтента = "application/json";
	
	// Определение параметров восстановления тела ответа из двоичных данных
	ЗапросКСервису.ПараметрыВосстановления = НовыйПараметрыВосстановления();
	ЗапросКСервису.ПараметрыВосстановления.СвойстваТипаДата = 
		"date,created,currentStatusDate,supplierFulfilmentExtendedStatusDate";
	
	Возврат ЗапросКСервису;
	
КонецФункции

Функция ПолныйАдресЗапроса(Знач ПараметрыСоединения, Знач ЗапросКСервису)
	
	Если ПустаяСтрока(ПараметрыСоединения.ПутьНаСервере) Тогда
		Возврат ЗапросКСервису.СтрокаЗапроса;
	КонецЕсли;
	
	Возврат СтрШаблон("%1/%2", ПараметрыСоединения.ПутьНаСервере, ЗапросКСервису.СтрокаЗапроса);
	
КонецФункции

Функция НовыйЗапросКСервису()
	
	Результат = Новый Структура;
	Результат.Вставить("ИмяСервиса", ИмяСервисаСверкиРасчетов());                       // Имя сервиса, к которому будет выполнен запрос.
	Результат.Вставить("Наименование", "");                                             // Полное наименование метода.
	Результат.Вставить("СтрокаЗапроса", "");                                            // Полный адрес URI метода с параметрами, задаваемыми как часть пути.
	Результат.Вставить("Метод",        "");                                             // Метод HTTP соединения, например POST. См. МетодыСоединения().
	Результат.Вставить("Заголовки", Новый Соответствие);                                // Заголовки запроса.
	Результат.Вставить("Таймаут");                                                      // Длительность ожидания ответа. Значение по умолчанию установлено в параметрах соединения,
	                                                                                    //   в этом параметре его можно переопределить для конкретного запроса.
	Результат.Вставить("ДопустимыеКодыСостояния", ДопустимыеКодыСостоянияПоУмолчанию());// Коды состояния, означающие успешное выполнение запроса.
	Результат.Вставить("Тело");                                                         // Двоичные данные или строка.
	Результат.Вставить("ХешТелаЗапроса");                                               // MD5 хеш сумма от тела запроса. Если тело не заполнено - неопределено.
	
	Результат.Вставить("ТелоОтветаИгнорируется", Ложь);                                // При чтении тела ответа вместо структур использовать соответствия.
	Результат.Вставить("ВозвратДвоичныхДанныхТела", Ложь);                              // Вернуть тело ответа в виде двоичных данных.
	Результат.Вставить("ПутьКДаннымВТеле");                                             // Путь к возвращаемым данным.
	Результат.Вставить("ПараметрыАвторизации");
	Результат.Вставить("ТипПередаваемогоКонтента");
	Результат.Вставить("ТипВозвращаемогоКонтента");
	
	Результат.Вставить("ПараметрыВосстановления");                                      // Параметры парсинга JSON тела ответа
	
	Возврат Результат;
	
КонецФункции

Функция ДопустимыеКодыСостоянияПоУмолчанию()
	
	Результат = Новый Массив;
	Результат.Добавить(200);
	
	Возврат Результат;
	
КонецФункции

Функция ИдентификаторОрганизацииБизнесСети(Знач Организация, ЕстьОшибки = Ложь, ТекстОшибки = "")
	
	УстановитьПривилегированныйРежим(Истина);
	
	Результат = Неопределено;
	Если БизнесСеть.ОрганизацияПодключена(Организация) Тогда
		Результат = БизнесСеть.ИдентификаторОрганизации(Организация);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = СтрШаблон(НСтр("ru = 'Организация %1 не зарегистрирована в Бизнес Сети';
									|en = 'Company %1 is not registered in Business Network'"), Строка(Организация));
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция НовыйПараметрыВосстановления()
	
	Результат = Новый Структура;
	Результат.Вставить("СвойстваТипаДата", "");
	Результат.Вставить("ФорматДаты", ФорматДатыJSON.ISO);
	Результат.Вставить("ЧитатьВСоответствие", Ложь);
	Результат.Вставить("ОписаниеОбработчика", НовыйОбработчикВосстановления());
	
	Возврат Результат;
	
КонецФункции

Функция НовыйОбработчикВосстановления()
	
	Результат = Новый Структура;
	Результат.Вставить("Имя", Неопределено);
	Результат.Вставить("Менеджер", Неопределено);
	Результат.Вставить("ДополнительныеПараметры", Неопределено);

	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область РаботаСJSON

Функция ЗначениеКоллекцииПоАбсолютномуПути(Знач Значение, Знач Путь, ЕстьОшибки = Ложь)
	
	СвойстваПути = СтрРазделить(Путь, ".", Ложь);
	
	Попытка
		Результат = ЗначениеСвойстваКоллекцииВРекурсии(Значение, СвойстваПути, ЕстьОшибки);
	Исключение
		Результат = Неопределено;
		ЕстьОшибки = Истина;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ЗначениеСвойстваКоллекцииВРекурсии(Знач Значение, Знач СвойстваПути, ЕстьОшибки = Ложь)
	
	Свойство = СвойстваПути[0];
	МеткаКоллекции = СтрНайти(Свойство, "[");
	ИндексКоллекции = Неопределено;
	Если МеткаКоллекции <> 0 Тогда
		ИндексКоллекции = Сред(Свойство, МеткаКоллекции + 1);
		ИндексКоллекции = Лев(ИндексКоллекции, СтрДлина(ИндексКоллекции) - 1);
		Свойство = Лев(Свойство, МеткаКоллекции - 1);
	КонецЕсли;
	
	// Проверка свойства структуры.
	Если Не ЭтоКоллекция(Значение) Тогда
		ЕстьОшибки = Истина;
		
		Возврат Неопределено;
	КонецЕсли;

	Если ТипЗнч(Значение) = Тип("Структура") И Не Значение.Свойство(Свойство) 
		Или ТипЗнч(Значение) = Тип("Соответствие") И Значение.Получить(Свойство) = Неопределено Тогда
		ЕстьОшибки = Истина;
		
		Возврат Неопределено;
	КонецЕсли;
	
	// Поиск нового элемента
	Если МеткаКоллекции = 0 Тогда
		Значение = Значение[Свойство];
	Иначе
		Значение = Значение[Свойство][Число(ИндексКоллекции)];
	КонецЕсли;
	
	Если СвойстваПути.Количество() = 1 Тогда
		// Последний элемент пути.
		Возврат Значение;
	Иначе
		СвойстваПути.Удалить(0);
		Возврат ЗначениеСвойстваКоллекцииВРекурсии(Значение, СвойстваПути, ЕстьОшибки);
	КонецЕсли;
	
КонецФункции

Функция ЭтоКоллекция(Знач Значение)
	
	ТипЗначения = ТипЗнч(Значение);
	
	Возврат ТипЗначения = Тип("Структура") Или ТипЗначения = Тип("Соответствие");
	
КонецФункции

Функция СтруктураИзПотокаJSON(Знач ПотокДанных, Знач ПараметрыВосстановления = Неопределено)
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.ОткрытьПоток(ПотокДанных);
	
	Если ЗначениеЗаполнено(ПараметрыВосстановления) Тогда
		СвойстваТипаДата = СтрРазделить(ПараметрыВосстановления.СвойстваТипаДата, ",", Ложь);
		ФорматДаты = ПараметрыВосстановления.ФорматДаты;
		ЧитатьВСоответствие = ПараметрыВосстановления.ЧитатьВСоответствие;
		ИмяОбработчика = ЗначениеКоллекцииПоАбсолютномуПути(ПараметрыВосстановления, "ОписаниеОбработчика.Имя");
		МенеджерОбработчика = ЗначениеКоллекцииПоАбсолютномуПути(ПараметрыВосстановления, "ОписаниеОбработчика.Менеджер");
		ПараметрыОбработчика = ЗначениеКоллекцииПоАбсолютномуПути(ПараметрыВосстановления, "ОписаниеОбработчика.ДополнительныеПараметры");
		
		Результат = ПрочитатьJSON(ЧтениеJSON, ЧитатьВСоответствие, СвойстваТипаДата,
			ФорматДаты, ИмяОбработчика, МенеджерОбработчика, ПараметрыОбработчика);
	Иначе
		Результат = ПрочитатьJSON(ЧтениеJSON,,,ФорматДатыJSON.ISO);
	КонецЕсли;
	
	ЧтениеJSON.Закрыть();
	ПотокДанных.Закрыть();
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ОбработкаРезультатаЗапроса

Функция ЗапросВыполненКорректно(Знач ЗапросКСервису, Знач HTTPОтвет, ТекстОшибки = "")
	
	КодСостояния = HTTPОтвет.КодСостояния;
	
	Если ЗапросКСервису.ДопустимыеКодыСостояния.Найти(КодСостояния) <> Неопределено Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПодстрокиОшибки = Новый Массив;
	Если ЕстьПодробноеОписаниеОшибки(HTTPОтвет) Тогда
		ОписаниеОшибки = СтруктураИзПотокаJSON(HTTPОтвет.ПолучитьТелоКакПоток());
		
		ПодстрокиОшибки.Добавить(СтрШаблон(НСтр("ru = 'код состояния %1 - %2';
												|en = 'status code %1 - %2'"), XMLСтрока(КодСостояния), ОписаниеОшибки.title));
		ПодстрокиОшибки.Добавить(СтрШаблон(НСтр("ru = 'компонента сервиса: %1';
												|en = 'service component: %1'"), ОписаниеОшибки.type));
		
		ИдентификаторТрассировки = ЗначениеКоллекцииПоАбсолютномуПути(ОписаниеОшибки, "traceId");
		Если ЗначениеЗаполнено(ИдентификаторТрассировки) Тогда
			ПодстрокиОшибки.Добавить(СтрШаблон(НСтр("ru = 'идентификатор трассировки: %1';
													|en = 'trace ID: %1'"), ИдентификаторТрассировки));
		КонецЕсли;
		
		ПодробноеОписание = ЗначениеКоллекцииПоАбсолютномуПути(ОписаниеОшибки, "detail");
		Если ЗначениеЗаполнено(ПодробноеОписание) Тогда
			ПодстрокиОшибки.Добавить(СтрШаблон(НСтр("ru = 'подробное описание: %1';
													|en = 'details: %1'"), ПодробноеОписание));
		КонецЕсли;
	Иначе
		ТекстОшибки = СтандартныеТекстыОшибокHTTPЗапросов().Получить(КодСостояния);
		Если Не ЗначениеЗаполнено(ТекстОшибки) Тогда
			Если КодСостояния >= 500 Тогда
				ТекстОшибки = НСтр("ru = 'внутренняя ошибка сервиса';
									|en = 'internal service error'");
			ИначеЕсли КодСостояния >= 400 Тогда
				ТекстОшибки = НСтр("ru = 'ошибка параметров запроса';
									|en = 'request parameter error'");
			Иначе
				ТекстОшибки = НСтр("ru = 'неизвестная ошибка';
									|en = 'unknown error'");
			КонецЕсли;
		КонецЕсли;
		
		ПодстрокиОшибки.Добавить(СтрШаблон(НСтр("ru = 'код состояния %1 - %2';
												|en = 'status code %1 - %2'"), XMLСтрока(КодСостояния), ТекстОшибки));
	КонецЕсли;
	
	ТекстОшибки = СтрСоединить(ПодстрокиОшибки, ", ");
	
	Возврат Ложь;
	
КонецФункции

Функция ОбработатьДанныеОтвета(Знач ЗапросКСервису, Знач HTTPОтвет, ОжидатьОтвета,
	ЕстьОшибки, ТекстОшибки = "", КоличествоПопытокВыполнения = 0)
	
	ЕстьОшибки = Ложь;
	ОжидатьОтвета = Ложь;
	Результат = Неопределено;
	
	Если ИстекСрокЖизниТокенаАвторизации(HTTPОтвет)
		И КоличествоПопытокВыполнения <= КоличествоПопытокВыполненияHTTPЗапроса() Тогда
	
		Возврат ВыполнитьЗапрос(ЗапросКСервису, ЕстьОшибки, ОжидатьОтвета, ТекстОшибки, КоличествоПопытокВыполнения + 1);
	КонецЕсли;
	
	Если Не ЗапросВыполненКорректно(ЗапросКСервису, HTTPОтвет, ТекстОшибки) Тогда
		ЕстьОшибки = Истина;
			
		Возврат Неопределено;
	КонецЕсли;
	
	Если АсинхронноеВыполнениеЗапроса(HTTPОтвет) Тогда
		ОжидатьОтвета = Истина;

		Возврат ИдентификаторАсинхронногоЗадания(HTTPОтвет, ЕстьОшибки, ТекстОшибки)
	КонецЕсли;
	
	Если ЗапросКСервису.ТелоОтветаИгнорируется Тогда
		Возврат Истина;
	КонецЕсли;
	
	Попытка
		Если ЗапросКСервису.ВозвратДвоичныхДанныхТела Тогда
			Возврат HTTPОтвет.ПолучитьТелоКакДвоичныеДанные();
		Иначе
			ПотокДанных = HTTPОтвет.ПолучитьТелоКакПоток();
			Результат = СтруктураИзПотокаJSON(ПотокДанных, ЗапросКСервису.ПараметрыВосстановления);
		КонецЕсли;
	Исключение
		ЕстьОшибки = Истина;
		
		ТекстОшибки = СтрШаблон(НСтр("ru = 'не удалось прочитать тело сообщения: %1';
									|en = 'cannot read message body: %1'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
		Возврат Неопределено;
	КонецПопытки;
	
	Если Не ПустаяСтрока(ЗапросКСервису.ПутьКДаннымВТеле) Тогда
		Попытка
			Результат = ЗначениеКоллекцииПоАбсолютномуПути(Результат, ЗапросКСервису.ПутьКДаннымВТеле, ЕстьОшибки);
		Исключение
			ЕстьОшибки = Истина;
		КонецПопытки;
		
		Если ЕстьОшибки Тогда
			ТекстОшибки = СтрШаблон(НСтр("ru = 'в теле отсутствует ключ %1';
										|en = 'the %1 key is missing in the body'"), ЗапросКСервису.ПутьКДаннымВТеле);
			
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ИстекСрокЖизниТокенаАвторизации(Знач HTTPОтвет)
	
	Возврат HTTPОтвет.КодСостояния = КодСостоянияПроблемыСАвторизацией();
	
КонецФункции

Функция АсинхронноеВыполнениеЗапроса(Знач HTTPОтвет)
	
	Возврат HTTPОтвет.КодСостояния = КодСостоянияАсинхронноеВыполнениеЗапроса();
	
КонецФункции

Функция ИдентификаторАсинхронногоЗадания(Знач HTTPОтвет, ЕстьОшибки, ТекстОшибки = "")
	
	СсылкаСервиса = HTTPОтвет.Заголовки.Получить("Location");
	Если СсылкаСервиса = Неопределено Тогда
		ЕстьОшибки = Истина;
		ТекстОшибки = НСтр("ru = 'в заголовках ответа не найдена ссылка на асинхронных обработчик';
							|en = 'reference to asynchronous handler is not found in reply headers'");
		
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат Сред(СсылкаСервиса, СтрНайти(СсылкаСервиса, "/", НаправлениеПоиска.СКонца) +1);
	
КонецФункции

#КонецОбласти

#Область Соединение

Функция НовоеСоединениеССервером(Знач ПараметрыСоединения, ЕстьОшибки, ТекстОшибки = "")

	Соединение = Неопределено;
	ЕстьОшибки = Ложь;
	
	Попытка
		Соединение = Новый HTTPСоединение(
			ПараметрыСоединения.Сервер,
			ПараметрыСоединения.Порт,,,
			ПараметрыСоединения.Прокси,
			ПараметрыСоединения.Таймаут,
			ПараметрыСоединения.ЗащищенноеСоединение);
	Исключение
		ЕстьОшибки = Истина;
		
		ТекстОшибки = СтрШаблон(НСтр("ru = 'не удалось установить соединение по причине: %2';
									|en = 'cannot establish connection. Reason: %2'"),
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

Функция НовыйПараметрыСоединения()
	
	Результат = Новый Структура;
	Результат.Вставить("Сервер",         "");
	Результат.Вставить("Порт",           0);
	Результат.Вставить("ПутьНаСервере",  "");
	Результат.Вставить("Аутентификация", Ложь);
	Результат.Вставить("Таймаут", 60);
	Результат.Вставить("Прокси",         Неопределено);
	Результат.Вставить("ЗащищенноеСоединение", Неопределено);
	Результат.Вставить("ИдентификаторПрограммы", Неопределено);
	
	Возврат Результат;
	
КонецФункции

Функция ПараметрыСоединенияССервисом(ИмяСервиса)
	
	Если ИмяСервиса = ИмяСервисаСверкиРасчетов() Тогда
		Возврат ПараметрыСоединения(АдресСервисаСверкиРасчетов());
	КонецЕсли;
	
	ТекстОшибки = СтрШаблон(
		НСтр("ru = 'Ошибка формирования параметров соединения с сервисом %1: имя сервиса не опознано.';
			|en = 'Error when generating %1 service connection parameters: service name is not recognized.'"), ИмяСервиса);
	
	ВызватьИсключение ТекстОшибки;
	
КонецФункции

// Формирует параметры соединения к сервисом.
//
// Параметры:
//  АдресРесурса - Строка - ссылка на ресурс в формате:
//                       <протокол обмена>://<сервер>:<порт>.
//
// Возвращаемое значение:
//  Структура - параметры соединения:
//   * Сервер - Строка - имя сервера.
//   * Порт - Число - порт соединения.
//   * ПутьНаСервере - Строка - относительный путь на сервере.
//   * Аутентификация - Булево - требуется аутентификация по токену.
//   * ЗащищенноеСоединение - ЗащищенноеСоединениеOpenSSL - объект защищенного соединения OpenSSL.
//   * Прокси - ИнтернетПрокси - параметры прокси-сервера см. ПолучениеФайловИзИнтернета.ПолучитьПрокси()
//
Функция ПараметрыСоединения(Знач ПолныйАдресРесурса)
	
	ДанныеРесурса = ОбщегоНазначенияКлиентСервер.СтруктураURI(ПолныйАдресРесурса);
	
	Результат = НовыйПараметрыСоединения();
	Результат.Сервер        = ДанныеРесурса.Хост;
	Результат.Порт          = ДанныеРесурса.Порт;
	Результат.Прокси        = ПолучениеФайловИзИнтернета.ПолучитьПрокси(ДанныеРесурса.Схема);
	Результат.ПутьНаСервере = ДанныеРесурса.ПутьНаСервере;
	
	Если ДанныеРесурса.Схема = ЗащищенныйПротоколПередачиДанных() Тогда
		Результат.ЗащищенноеСоединение = ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ФормализацияЗначений

Функция АдресСервисаСверкиРасчетов()

	Возврат "https://verify.1cfresh.com:443/api"
	
КонецФункции

Функция ИмяСервисаСверкиРасчетов()
	
	Возврат "verify";
	
КонецФункции

Функция ЗащищенныйПротоколПередачиДанных()
	
	Возврат "https";
	
КонецФункции

Функция ИмяЗаголовкаСтрокаАвторизации()
	
	Возврат "Authorization"
	
КонецФункции 

Функция ИмяЗаголовкаТипПередаваемогоКонтента()
	
	Возврат "Content-Type";
	
КонецФункции

Функция ИмяЗаголовкаТипВозвращаемогоКонтента()
	
	Возврат "Accept";
	
КонецФункции

Функция ТипКонтентаОшибка()
	
	Возврат "application/problem+json"
	
КонецФункции

Функция КодСостоянияПроблемыСАвторизацией()
	
	Возврат 401
	
КонецФункции

Функция КодСостоянияАсинхронноеВыполнениеЗапроса()
	
	Возврат 202
	
КонецФункции

Функция КоличествоПопытокВыполненияHTTPЗапроса()
	
	Возврат 1;
	
КонецФункции

#КонецОбласти

#Область Авторизация

Функция ТокенАвторизации(Знач ПараметрыАвторизации, ЕстьОшибки, ТекстОшибки = "")
	
	Токен = ТокенДоступаОрганизации(ПараметрыАвторизации, ЕстьОшибки, ТекстОшибки);
	
	Если Не ЗначениеЗаполнено(Токен) Тогда
		
		// Попытка получения нового токена обновления
		РезультатПодключения = РезультатПодключенияОрганизациюКСервису(ПараметрыАвторизации.Организация);
		Если ЗначениеЗаполнено(РезультатПодключения.ТекстОшибки) Тогда
			ЕстьОшибки = Истина;
		Иначе
			Возврат ТокенДоступаОрганизации(ПараметрыАвторизации, ЕстьОшибки, ТекстОшибки);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка получения токена авторизации.';
							|en = 'Error when receiving authorization token.'")
			+ ?(ПустаяСтрока(ТекстОшибки), "", Символы.ПС)+ ТекстОшибки;
		Возврат Неопределено;
	КонецЕсли;

	Возврат Токен;
	
КонецФункции

Функция ТокенДоступаОрганизации(ПараметрыАвторизации, ЕстьОшибки, ТекстОшибки)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Токен = Неопределено;

	Попытка
		Токен = БизнесСеть.ТокенДоступаОрганизации(ПараметрыАвторизации.Организация);
		Если Не ЗначениеЗаполнено(Токен) Или ПустаяСтрока(Токен.ТокенДоступа) Тогда
			Токен = Неопределено;
		КонецЕсли;
	Исключение
		ЕстьОшибки = Истина;

		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Токен;

КонецФункции

Функция НовыйПараметрыАвторизации(Знач Организация)
	
	Возврат Новый Структура("Организация", Организация);
	
КонецФункции

#КонецОбласти

#Область РаботаСОшибками

Функция ЗафиксироватьОшибкуВЗапросе(Знач ПараметрыСоединения, Знач ЗапросКСервису,
	Знач ТекстОшибки, ЕстьОшибки = Неопределено)
	
	ЕстьОшибки = Истина;
	ПодробноеОписаниеОшибки = "";
	
	ПредставлениеОшибки = Новый Массив;
	ПредставлениеОшибки.Добавить(СтрШаблон("%1: ошибка взаимодействия", СервисСверкиРасчетов.ИмяСервиса()));
	ПредставлениеОшибки.Добавить(СтрШаблон(НСтр("ru = 'Хост: %1';
												|en = 'Host: %1'"), ПараметрыСоединения.Сервер));
	ПредставлениеОшибки.Добавить(СтрШаблон(НСтр("ru = 'Запрос: %1 %2.';
												|en = 'Request: %1 %2.'"), ЗапросКСервису.Метод,
		ПолныйАдресЗапроса(ПараметрыСоединения, ЗапросКСервису)));
	ПредставлениеОшибки.Добавить(СтрШаблон(НСтр("ru = 'Текст ошибки: %1';
												|en = 'Error text: %1'"), ТекстОшибки));
	
	ПодробноеОписаниеОшибки = СтрСоединить(ПредставлениеОшибки, Символы.ПС);
	
	ЗаписьЖурналаРегистрации(СтрШаблон(НСтр("ru = '%1: %2';
											|en = '%1: %2'", ОбщегоНазначения.КодОсновногоЯзыка()), 
		СервисСверкиРасчетов.ИмяСервиса(), ЗапросКСервису.Наименование),УровеньЖурналаРегистрации.Ошибка,,, 
		ПодробноеОписаниеОшибки);

	Возврат ПодробноеОписаниеОшибки

КонецФункции 

Функция ЕстьПодробноеОписаниеОшибки(Знач HTTPОтвет)
	
	Возврат HTTPОтвет.Заголовки.Получить(ИмяЗаголовкаТипПередаваемогоКонтента()) = ТипКонтентаОшибка();
	
КонецФункции

Функция СтандартныеТекстыОшибокHTTPЗапросов()

	КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	
	СоответствиеКодов = Новый Соответствие;
	СоответствиеКодов.Вставить(400, НСтр("ru = 'ошибка в запросе.';
										|en = 'error in request.'", КодЯзыка));
	СоответствиеКодов.Вставить(401, НСтр("ru = 'неверные данные аутентификации.';
										|en = 'invalid authentication data.'", КодЯзыка));
	СоответствиеКодов.Вставить(403, НСтр("ru = 'у клиента нет доступа к ресурсу.';
										|en = 'the client has no access to resource.'", КодЯзыка));
	СоответствиеКодов.Вставить(404, НСтр("ru = 'на сервере нет ресурса по указанному URI.';
										|en = 'the specified URI does not contain resource on server.'", КодЯзыка));
	СоответствиеКодов.Вставить(405, НСтр("ru = 'указанный метод нельзя применить к текущему ресурсу.';
										|en = 'the specified method cannot be applied to the current resource.'", КодЯзыка));
	СоответствиеКодов.Вставить(406, НСтр("ru = 'запрошенный URI не может удовлетворить переданным в заголовке
		|характеристикам.';
		|en = 'the requested URI cannot meet the characteristics passed in
		|the header.'", КодЯзыка));
	СоответствиеКодов.Вставить(407, НСтр("ru = 'для доступа к серверу требуется аутентификация
		|для прокси-сервера';
		|en = 'proxy server authentication is required to access
		|the server'", КодЯзыка));
	СоответствиеКодов.Вставить(408, НСтр("ru = 'время ожидания сервером передачи от клиента истекло.';
										|en = 'request timeout.'", КодЯзыка));
	СоответствиеКодов.Вставить(409, НСтр("ru = 'запрос не может быть выполнен из-за конфликтного
		|обращения к ресурсу.';
		|en = 'cannot execute the request due to an resource
		|access conflict.'", КодЯзыка));
	СоответствиеКодов.Вставить(410, НСтр("ru = 'ресурс был удален и теперь недоступен.';
										|en = 'resource has been deleted and is now unavailable.'", КодЯзыка));
	СоответствиеКодов.Вставить(411, НСтр("ru = 'не указан объем передаваемых данных в заголовке.';
										|en = 'scope of transferred data is not specified in the header.'", КодЯзыка));
	СоответствиеКодов.Вставить(412, НСтр("ru = 'ни одно из условных полей заголовка запроса не было выполнено.';
										|en = 'none of the request header conditional fields were met.'",
		КодЯзыка));
	СоответствиеКодов.Вставить(413, НСтр("ru = 'слишком большой размер тела запроса.';
										|en = 'request body is too large.'", КодЯзыка));
	СоответствиеКодов.Вставить(414, НСтр("ru = 'слишком длинный URL.';
										|en = 'URL is too long.'", КодЯзыка));
	СоответствиеКодов.Вставить(415, НСтр("ru = 'контент переданного типа не поддерживается.';
										|en = 'content of the passed type is not supported.'", КодЯзыка));
	СоответствиеКодов.Вставить(422, НСтр("ru = 'имеется логическая ошибка, из-за которой невозможно
		|произвести операцию.';
		|en = 'there is a logic error making the operation
		|unavailable.'", КодЯзыка));
	СоответствиеКодов.Вставить(423, НСтр("ru = 'ресурс заблокирован.';
										|en = 'resource is blocked.'", КодЯзыка));
	СоответствиеКодов.Вставить(426, НСтр("ru = 'необходимо обновить протокол.';
										|en = 'protocol update is required.'", КодЯзыка));
	СоответствиеКодов.Вставить(429, НСтр("ru = 'слишком много запросов в единицу времени.';
										|en = 'too many requests per time unit.'", КодЯзыка));
	СоответствиеКодов.Вставить(431, НСтр("ru = 'превышена допустимая длина заголовков.';
										|en = 'permissible header length is exceeded.'", КодЯзыка));
	СоответствиеКодов.Вставить(434, НСтр("ru = 'запрашиваемый адрес недоступен.';
										|en = 'requested address is not available.'", КодЯзыка));
	СоответствиеКодов.Вставить(449, НСтр("ru = 'поступило недостаточно информации.';
										|en = 'insufficient information received.'", КодЯзыка));
	
	СоответствиеКодов.Вставить(500, НСтр("ru = 'внутренняя ошибка сервера.';
										|en = 'internal server error.'", КодЯзыка));
	СоответствиеКодов.Вставить(501, НСтр("ru = 'сервер не поддерживает возможностей, необходимых
		|для обработки запроса.';
		|en = 'server does not support features required
		|to process the request.'", КодЯзыка));
	СоответствиеКодов.Вставить(502, НСтр("ru = 'сервер, выступая в роли шлюза или прокси-сервера,
		|получил недействительное ответное сообщение от вышестоящего сервера.';
		|en = 'server received an invalid response from the upstream server
		|while acting as a gateway or proxy server.'", КодЯзыка));
	СоответствиеКодов.Вставить(503, НСтр("ru = 'сервер временно не имеет возможности обрабатывать запросы
		|по техническим причинам.';
		|en = 'server temporarily cannot process request
		|due to technical reasons.'", КодЯзыка));
	СоответствиеКодов.Вставить(504, НСтр("ru = 'сервер в роли шлюза или прокси-сервера не дождался ответа
		|от вышестоящего сервера для завершения текущего запроса.';
		|en = 'server did not receive a timely response from the upstream server
		|while acting as a gateway or proxy server.'", КодЯзыка));
	СоответствиеКодов.Вставить(505, НСтр("ru = 'сервер не поддерживает указанную в запросе
		|версию протокола HTTP.';
		|en = 'server does not support HTTP version
		|specified in the request.'", КодЯзыка));
	СоответствиеКодов.Вставить(507, НСтр("ru = 'не хватает места для выполнения текущего запроса.';
										|en = 'not enough space to execute current request.'",
		КодЯзыка));
	СоответствиеКодов.Вставить(510, НСтр("ru = 'на сервере отсутствует расширение, которое желает
		|использовать клиент.';
		|en = 'the extension required by the client
		|is missing on server.'", КодЯзыка));
	СоответствиеКодов.Вставить(511, НСтр("ru = 'необходимо авторизоваться в сети провайдера.';
										|en = 'authentication in provider network is required.'", КодЯзыка));
	
	Возврат СоответствиеКодов;
	
КонецФункции

#КонецОбласти

#КонецОбласти