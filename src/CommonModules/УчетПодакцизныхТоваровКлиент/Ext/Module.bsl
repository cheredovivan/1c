
#Область ПрограммныйИнтерфейс

// Обновляет заголовок поля формы облагается акцизом после расчета количества подакцизных товаров в документе.
//
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - настраиваемая форма.
//
Процедура ПослеРасчетаКоличестваПодакцизныхТоваров(Форма) Экспорт
	
	Если Не Форма.ИспользоватьПодакцизныеТовары Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьЗаголовокПоляОблагаетсяАкцизом(Форма);
	
КонецПроцедуры

// Обновляет заголовок поля облагается акцизом и при необходимости подключает обработчик ожидания проверки
// вычисления количества подакцизных товаров.
//
// Параметры:
//  Форма  - ФормаКлиентскогоПриложения - обрабатываемая форма.
//
Процедура ОбновитьЗаголовокПоляОблагаетсяАкцизом(Форма) Экспорт
	
	Если ЗначениеЗаполнено(Форма.ДанныеРасчетаКоличестваПодакцизныхТоваров) Тогда
		
		ИдентификаторЗадания = Форма.ДанныеРасчетаКоличестваПодакцизныхТоваров.ИдентификаторЗадания;
		
		Если Форма.ДанныеРасчетаКоличестваПодакцизныхТоваров.Статус = "Выполнено"
			Или (ЗначениеЗаполнено(ИдентификаторЗадания)
				И УчетПодакцизныхТоваровВызовСервера.ЗаданиеВыполнено(ИдентификаторЗадания)) Тогда
			
			АдресРезультата = Форма.ДанныеРасчетаКоличестваПодакцизныхТоваров.АдресРезультата;
			Форма.Элементы.ОблагаетсяАкцизом.Заголовок =
				УчетПодакцизныхТоваровВызовСервера.ЗаголовокПоляОблагаетсяАкцизом(АдресРезультата);
			Форма.ДанныеРасчетаКоличестваПодакцизныхТоваров = Неопределено;
			Форма.ТребуетсяОбновитьЗаголовокОблагаетсяАкцизом = Ложь;
			
		Иначе
			
			Форма.Элементы.ОблагаетсяАкцизом.Заголовок = НСтр("ru = '<идет обновление данных>';
																|en = '<data is being updated>'");
			Форма.ПодключитьОбработчикОжидания("Подключаемый_ОбновитьЗаголовокПоляОблагаетсяАкцизом", 1, Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
