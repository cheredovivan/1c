#Область СлужебныйПрограммныйИнтерфейс

// См. МашиночитаемыеДоверенности.ПолучитьСведенияДоверенностиНаСервереМЧД
Функция ПолучитьСведенияДоверенностиНаСервереМЧД(НомерДоверенности, ИННДоверителя, ИННПредставителя, ТокенДоступа = "") Экспорт
	
	Возврат МашиночитаемыеДоверенности.ПолучитьСведенияДоверенностиНаСервереМЧД(
		НомерДоверенности, ИННДоверителя, ИННПредставителя, ТокенДоступа);
	
КонецФункции

// См. МашиночитаемыеДоверенности.ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧД
Функция ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧД(НомерДоверенности, ТокенДоступа = "", ПоказыватьСлужебныеСообщения = Истина) Экспорт
	
	Возврат МашиночитаемыеДоверенности.ПолучитьЧастичныеДанныеДоверенностиНаСервереМЧД(
		НомерДоверенности, ТокенДоступа, ПоказыватьСлужебныеСообщения );
	
КонецФункции

// См. МашиночитаемыеДоверенности.АвторизоватьсяНаСервереМЧД
Функция АвторизоватьсяНаСервереМЧД(ПоказыватьСлужебныеСообщения = Истина) Экспорт
	
	Возврат МашиночитаемыеДоверенности.АвторизоватьсяНаСервереМЧД(ПоказыватьСлужебныеСообщения).ТокенДоступа; 
	
КонецФункции

// Свойства сервера МЧД.
// 
// Возвращаемое значение:
//  Структура:
//  * АдресСервера - Строка
//  * АдресСервераБезАутентификации - Строка
//  * РесурсКорняAPI - Строка
//  * ЛогинОператора - Строка
//  * ПарольОператора - Строка
//  * ИспользоватьРасширенияAPI - Булево
// 
Функция СвойстваСервераМЧД() Экспорт
	
	СтруктураАдреса = Константы.АдресРеестраМЧД.ПрочитатьАдрес();
	МашиночитаемыеДоверенности.ПереопределитьАдресСервера(СтруктураАдреса);
	СтруктураАдреса.Вставить("АдресСервераБезАутентификации",	СтруктураАдреса.АдресСервера);
	СтруктураАдреса.Вставить("ЛогинОператора", 					"");
	СтруктураАдреса.Вставить("ПарольОператора", 				"");
		
	НачалоПараметровАутентификации = СтрНайти(СтруктураАдреса.АдресСервераБезАутентификации, "://");
	КонецПараметровАутентификации = СтрНайти(СтруктураАдреса.АдресСервераБезАутентификации, "@");
	Если НачалоПараметровАутентификации <> 0 И КонецПараметровАутентификации <> 0 Тогда
		СтруктураАдреса.АдресСервераБезАутентификации =
			Лев(СтруктураАдреса.АдресСервераБезАутентификации, НачалоПараметровАутентификации + 2)
			+ Сред(СтруктураАдреса.АдресСервераБезАутентификации, КонецПараметровАутентификации + 1);
	КонецЕсли;
	
	Возврат СтруктураАдреса;
	
КонецФункции

// Используется режим тестирования.
// 
// Возвращаемое значение:
//  Булево
//  
Функция ИспользуетсяРежимТестирования() Экспорт
	
	Возврат Константы.РежимТестированияМЧД.Получить();
	
КонецФункции

// Возвращаемое значение:
//  Булево
//  
Функция ОтключенПодборМЧДПоКПППриПодписанииИПроверке() Экспорт
	ОтключенПодборМЧДПоКПППриПодписанииИПроверке = Константы.ОтключитьПодборМЧДПоКПППриПодписанииИПроверке.Получить();
	
	Возврат ОтключенПодборМЧДПоКПППриПодписанииИПроверке;
	
КонецФункции

// Возвращаемое значение:
//  Булево
//  
Функция СервисАвторизацииСервераМЧДДоступен() Экспорт
	
	СвойстваСервераМЧД = МашиночитаемыеДоверенностиПовтИсп.СвойстваСервераМЧД();
	РесурсНаСервере = СвойстваСервераМЧД.РесурсКорняAPI + "/token";
	
	ПараметрыРесурсаНаСервере = "?ticket=checkstatus";
	
	ЗаголовкиHTTP = Новый Соответствие();
	ЗаголовкиHTTP.Вставить("Content-Type", "application/json");
	
	ЗапросHTTP = Новый HTTPЗапрос(РесурсНаСервере + ПараметрыРесурсаНаСервере, ЗаголовкиHTTP);
	ОписаниеСоединения =
		ИнтернетСоединениеБЭД.ОписаниеHTTPСоединения(СвойстваСервераМЧД.АдресСервераБезАутентификации, 15);
	Результат = ИнтернетСоединениеБЭД.ВызватьHTTPМетод(ОписаниеСоединения, ЗапросHTTP,
		ИнтернетСоединениеБЭД.HTTPМетоды().GET,  НСтр("ru = 'Проверка связи с сервисом авторизации сервера МЧД распределенного реестра.';
														|en = 'Check connection to the authorization service of the server of machine-readable letter of authority of the distributed ledger.'"));
	
	СервисДоступен = Результат.Успех;
	
	Если Результат.Ответ <> Неопределено И Результат.Ответ.КодСостояния >= 500 Тогда
		СервисДоступен = Ложь;
	КонецЕсли;
	
	Возврат СервисДоступен;
	
КонецФункции

#КонецОбласти
