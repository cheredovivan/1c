#Область СлужебныйПрограммныйИнтерфейс

Процедура ПодготовитьСтрокиВыписки(ДокументыКЗагрузке, БанковскийСчет, РеквизитыСчета, НастройкиЗагрузки) Экспорт
	
	ДокументыКЗагрузке.Колонки.Добавить("ТолькоПрочиеОтношения", Новый ОписаниеТипов("Булево"));
	ДокументыКЗагрузке.Колонки.Добавить("ТипСуммыКредитаДепозита", Новый ОписаниеТипов("ПеречислениеСсылка.ТипыСуммГрафикаКредитовИДепозитов"));
	ДокументыКЗагрузке.Колонки.Добавить("ВалютаВзаиморасчетов", Новый ОписаниеТипов("СправочникСсылка.Валюты"));
	ДокументыКЗагрузке.Колонки.Добавить("ЭтоКомиссияБанка", Новый ОписаниеТипов("Булево"));
	
	ТипыПлатежныхДокументовПоступления = Новый Соответствие;
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНОЕТРЕБОВАНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование);
	ТипыПлатежныхДокументовПоступления.Вставить("ПЛАТЕЖНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("БАНКОВСКИЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("МЕМОРИАЛЬНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовПоступления.Вставить("ИНКАССОВОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение);
	
	ТипыПлатежныхДокументовСписания = Новый Соответствие;
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение);
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНОЕТРЕБОВАНИЕ", Перечисления.ТипыПлатежныхДокументов.ПлатежноеТребование);
	ТипыПлатежныхДокументовСписания.Вставить("ПЛАТЕЖНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("БАНКОВСКИЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("МЕМОРИАЛЬНЫЙОРДЕР", Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер);
	ТипыПлатежныхДокументовСписания.Вставить("ИНКАССОВОЕПОРУЧЕНИЕ", Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение);
	
	СобственныеСчета = СобственныеСчета(БанковскийСчет);
	
	НомерСчета = РеквизитыСчета.НомерСчета;
	
	Для каждого СтрокаДокумента Из ДокументыКЗагрузке Цикл
		
		Если Не СтрокаДокумента.Загружать 
			Или (Не СтрокаДокумента.ПлательщикСчет = НомерСчета
			И Не СтрокаДокумента.ПолучательСчет = НомерСчета) Тогда
			СтрокаДокумента.Загружать = Ложь;
			Продолжить;
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	КлиентБанкЛокализация.ОтметитьНеЗагружаемыеПлатежи(СтрокиКЗагрузке, РеквизитыСчета);
	
	// Подготовка строк выписки к поиску документов и контрагентов, заполнение общих реквизитов. Первичное распознавание.
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	ПлатежиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого СтрокаДокумента Из ПлатежиКЗагрузке Цикл
		
		// Входящий или исходящий платеж
		Если СтрокаДокумента.ПлательщикСчет = НомерСчета И СтрокаДокумента.ПолучательСчет = НомерСчета Тогда // Если БИК банка изменился, а номер счета остался прежним
			Исходящий = ЗначениеЗаполнено(СтрокаДокумента.ДатаСписано);
		Иначе
			Исходящий = (СтрокаДокумента.ПлательщикСчет = НомерСчета);
		КонецЕсли;
		СтрокаДокумента.Исходящий = Исходящий;
		
		// Дата документа
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаДок) Тогда
			СтрокаДокумента.ДатаДок = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.Дата);
			Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаДок) Тогда
				СтрокаДокумента.ДатаДок = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
					?(Исходящий, СтрокаДокумента.ДатаСписано, СтрокаДокумента.ДатаПоступило));
			КонецЕсли;
		КонецЕсли;
		
		// Тип платежного документа
		ТипСтрокой = СтрЗаменить(ВРЕГ(СтрЗаменить(СокрЛП(СтрокаДокумента.Операция), " ", "")), "Ё", "Е");
		Если Исходящий Тогда
			ТипПлатежногоДокумента = ТипыПлатежныхДокументовСписания.Получить(ТипСтрокой);
		Иначе
			ТипПлатежногоДокумента = ТипыПлатежныхДокументовПоступления.Получить(ТипСтрокой);
		КонецЕсли;
		Если ТипПлатежногоДокумента = Неопределено Тогда
			ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежноеПоручение;
		КонецЕсли;

		СтрокаДокумента.ТипПлатежногоДокумента = ТипПлатежногоДокумента;
		
		// Хоз. операция по виду оплаты
		Если СтрокаДокумента.ВидОплаты = "03" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "04" Тогда
			СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет;
			
		ИначеЕсли СтрокаДокумента.ВидОплаты = "13" Тогда
			Если Исходящий Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту;
			Иначе
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте;
			КонецЕсли;
		КонецЕсли;
		
		// Переводы
		Если СтрокаДокумента.Исходящий Тогда
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ПолучательБИК) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, БИК", СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПолучательБИК);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.ПолучательСВИФТ) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, СВИФТ", СтрокаДокумента.ПолучательСчет, СтрокаДокумента.ПолучательСВИФТ);
			Иначе
				СтруктураПоиска = Новый Структура("НомерСчета", СтрокаДокумента.ПолучательСчет);
			КонецЕсли;
			
			СобственныеСчетаПолучатели = СобственныеСчета.НайтиСтроки(СтруктураПоиска);
			Если СобственныеСчетаПолучатели.Количество() > 0 Тогда
				РеквизитыПолучателя = СобственныеСчетаПолучатели[0];
				Если РеквизитыПолучателя.Валюта = РеквизитыСчета.Валюта Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет;
				Иначе
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
					СтрокаДокумента.ВалютаКонвертации = РеквизитыПолучателя.Валюта;
				КонецЕсли;
				СтрокаДокумента.СчетКонтрагента = РеквизитыПолучателя["Ссылка"];
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаСписано) Тогда
					СтрокаДокумента.Загружать = Ложь;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		Иначе
			
			Если ЗначениеЗаполнено(СтрокаДокумента.ПлательщикБИК) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, БИК", СтрокаДокумента.ПлательщикСчет, СтрокаДокумента.ПлательщикБИК);
			ИначеЕсли ЗначениеЗаполнено(СтрокаДокумента.ПлательщикСВИФТ) Тогда
				СтруктураПоиска = Новый Структура("НомерСчета, СВИФТ", СтрокаДокумента.ПлательщикСчет, СтрокаДокумента.ПлательщикСВИФТ);
			Иначе
				СтруктураПоиска = Новый Структура("НомерСчета", СтрокаДокумента.ПлательщикСчет);
			КонецЕсли;
			
			СобственныеСчетаОтправители = СобственныеСчета.НайтиСтроки(СтруктураПоиска);
			Если СобственныеСчетаОтправители.Количество() > 0 Тогда
				РеквизитыОтправителя = СобственныеСчетаОтправители[0];
				Если РеквизитыОтправителя.Валюта = РеквизитыСчета.Валюта Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета;
				Иначе
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
					СтрокаДокумента.ВалютаКонвертации = РеквизитыОтправителя.Валюта;
				КонецЕсли;
				СтрокаДокумента.СчетКонтрагента = РеквизитыОтправителя["Ссылка"];
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДатаПоступило) Тогда
					СтрокаДокумента.Загружать = Ложь;
					Продолжить;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Инкассация
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
			
			БалансовыйСчет = Лев(СтрокаДокумента.ПлательщикСчет, 5);
			Если БалансовыйСчет    = "20202" // поступление/списание наличных из кассы
				Или БалансовыйСчет = "20208" // поступление/списание наличных из банкомата, устройства Cash-in
			Тогда
				Если СтрокаДокумента.Исходящий Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств;
				Иначе
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		// Контрагент
		Если Исходящий Тогда
			Если СтрокаДокумента.Получатель1 <> "" Тогда
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Получатель1;
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Получатель;
			КонецЕсли;
		Иначе
			Если СтрокаДокумента.Плательщик1 <> "" Тогда
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Плательщик1;
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СтрокаДокумента.Плательщик;
			КонецЕсли;
		КонецЕсли;
		
		КлиентБанкЛокализация.ЗаполнитьДанныеКонтрагентаПоПлатежу(Исходящий, СтрокаДокумента, СтрокаДокумента.ИмяКонтрагента);
		
		СтруктураНаименования = ДенежныеСредстваСерверЛокализация.НаименованиеОрганизации(СтрокаДокумента.ИмяКонтрагента);
		СтрокаДокумента.СокрИмяКонтрагента = СтруктураНаименования.СокращенноеНаименование;
		
		Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(СтрокаДокумента.ИмяКонтрагента, 10)) Тогда
			Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(Лев(СтрокаДокумента.ИмяКонтрагента, 12)) Тогда
				СтрокаДокумента.ИмяКонтрагента = СокрЛП(Сред(СтрокаДокумента.ИмяКонтрагента, 13));
			Иначе
				СтрокаДокумента.ИмяКонтрагента = СокрЛП(Сред(СтрокаДокумента.ИмяКонтрагента, 11));
			КонецЕсли;
		КонецЕсли;
		
		ИННДляПоиска = ?(Исходящий, СтрокаДокумента.ПолучательИНН, СтрокаДокумента.ПлательщикИНН);
		СтрокаДокумента.ПроверятьИНН = ЗначениеЗаполнено(ИННДляПоиска)
										И ИННДляПоиска <> "000000000000"
										И ИННДляПоиска <> "0000000000";
	
		КППДляПоиска = ?(Исходящий, СтрокаДокумента.ПолучательКПП, СтрокаДокумента.ПлательщикКПП);
		СтрокаДокумента.ПроверятьКПП = (ЗначениеЗаполнено(КППДляПоиска) И КППДляПоиска <> "0");
		
		// УИП
		Если ЗначениеЗаполнено(СтрокаДокумента.Код)
			И СтрокаДокумента.Код <> "0" Тогда
			СтрокаДокумента.ЗаполненУИП = Истина;
		КонецЕсли;
		
		// Назначение платежа
		Если ПустаяСтрока(СтрокаДокумента.НазначениеПлатежа) Тогда
			СтрокаДокумента.НазначениеПлатежа = СтрокаДокумента.НазначениеПлатежа1;
			Для инд = 2 По 6 Цикл
				Если НЕ ЗначениеЗаполнено(СтрокаДокумента["НазначениеПлатежа" + инд]) Тогда
					Прервать;
				КонецЕсли;
				СтрокаДокумента.НазначениеПлатежа =
					СтрокаДокумента.НазначениеПлатежа + Символы.ПС + СтрокаДокумента["НазначениеПлатежа" + инд];
			КонецЦикла;
		КонецЕсли;
		
		СтрокаДокумента.ФорматированноеНазначениеПлатежа =
			ДенежныеСредстваСервер.ФорматироватьНазначениеПлатежа(СтрокаДокумента.НазначениеПлатежа);
		
		// Сумма
		СписокСумм = СтрРазделить(СтрокаДокумента.Сумма, "+");
		
		Для Каждого ТекущаяСумма Из СписокСумм Цикл
			
			Сумма = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(ТекущаяСумма);
			Если Сумма <> Неопределено Тогда
				Если Сумма < 0 Тогда
					Сумма = - Сумма;
				КонецЕсли;
				СтрокаДокумента.СуммаДокумента = СтрокаДокумента.СуммаДокумента + Сумма;
				Если Исходящий Тогда
					СтрокаДокумента.СуммаСписано   = СтрокаДокумента.СуммаСписано + Сумма;
				Иначе
					СтрокаДокумента.СуммаПоступило = СтрокаДокумента.СуммаПоступило + Сумма;
				КонецЕсли;
			Иначе
				ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки,
					СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Неверная сумма документа (%1)!';
							|en = 'Incorrect document amount (%1).'"), СтрокаДокумента.Сумма));
			КонецЕсли;
		
		КонецЦикла;
		
		// Дата проведения банком
		Если Исходящий Тогда
			Если Не ЗначениеЗаполнено(СтрокаДокумента.Списано) Тогда
				
				Если ЗначениеЗаполнено(СтрокаДокумента.ДатаСписано) Тогда
					СтрокаДокумента.Списано = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
						СтрокаДокумента.ДатаСписано);
				Иначе
					СтрокаДокумента.Списано = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
						СтрокаДокумента.Дата);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаДокумента.Списано) Тогда
					СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Списано;
				КонецЕсли;
			КонецЕсли;
		Иначе
			Если Не ЗначениеЗаполнено(СтрокаДокумента.Поступило) Тогда
				
				Если ЗначениеЗаполнено(СтрокаДокумента.ДатаПоступило) Тогда
					СтрокаДокумента.Поступило = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
						СтрокаДокумента.ДатаПоступило);
				Иначе
					СтрокаДокумента.Поступило = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(
						СтрокаДокумента.Дата);
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаДокумента.Поступило) Тогда
					СтрокаДокумента.ДатаПроведения = СтрокаДокумента.Поступило;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	КлиентБанкЛокализация.ПодготовитьСтрокиВыписки(СтрокиКЗагрузке, БанковскийСчет, РеквизитыСчета, НастройкиЗагрузки);
	
	// Ранее загруженные/введенные вручную документы
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, ДатаДок, Поступило, Списано, Номер, НомерСокр, Исходящий, ПолучательСчет, ПлательщикСчет, ТипПлатежногоДокумента,
		|ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП, ПроверятьИНН, ПроверятьКПП, ДанныеВыписки, Код, Операция, ЭтоЗагрузкаBankFeeds");
	ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	
	// Основание платежа по УИП
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("ЗаполненУИП", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	Если СтрокиКЗагрузке.Количество() Тогда
		ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(СтрокиКЗагрузке, "НомерСтроки, Код, Исходящий");
		ЗаполнитьОснованиеПлатежейПоУИП(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	КонецЕсли;
	
	// Контрагенты, счета контрагентов
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, Исходящий, ИмяКонтрагента, СокрИмяКонтрагента, Контрагент, ПолучательСчет, ПолучательБИК, ПлательщикСчет, ПлательщикБИК,
		|ПолучательИНН, ПлательщикИНН, ПолучательКПП, ПлательщикКПП, ПроверятьИНН, ПроверятьКПП, ВидОперации");
	ЗаполнитьКонтрагентов(ДокументыКЗагрузке, ТаблицаДокументов);
	
	// Партнеры
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденКонтрагент", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, Контрагент, Партнер");
	ЗаполнитьПартнеров(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет);
	
	// Заполнение операции комиссии банка
	Отбор = Новый Структура;
	Отбор.Вставить("ЭтоКомиссияБанка", Истина);
	Отбор.Вставить("НайденКонтрагент", Истина);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	КлиентБанкЛокализация.ЗаполнитьОперациюКомиссииБанка(РеквизитыСчета.Организация, БанковскийСчет, СтрокиКЗагрузке);
	
	// Образцы документов для заполнения
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденСчетКонтрагента", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьОбразцы(СтрокиКЗагрузке, БанковскийСчет);
	
	// Заполнение по косвенным данным
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденКонтрагент", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, БанковскийСчет);
	
	// Заполнение операции "Прочее поступление" или "Прочий расход"
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденКонтрагент", Истина);
	Отбор.Вставить("ТолькоПрочиеОтношения", Истина);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьПрочееПоступлениеРасход(СтрокиКЗагрузке);
	
	// Поиск ПКО при инкассации
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденОбразец", Ложь);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.СнятиеНаличныхДенежныхСредств);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, СуммаДокумента, ДатаПроведения");
	ЗаполнитьКассовыйДокумент(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет, Истина);
	
	// Поиск РКО при инкассации
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("НайденДокументВБазе", Ложь);
	Отбор.Вставить("НайденоОснованиеПлатежа", Ложь);
	Отбор.Вставить("НайденОбразец", Ложь);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзКассыНаРасчетныйСчет);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ТаблицаДокументов = ДокументыКЗагрузке.Скопировать(
		СтрокиКЗагрузке,
		"НомерСтроки, СуммаДокумента, ДатаПроведения");
	ЗаполнитьКассовыйДокумент(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет, Ложь);
	
	// Заполнение хозяйственной операции по умолчанию
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	Отбор.Вставить("ВидОперации", Перечисления.ХозяйственныеОперации.ПустаяСсылка());
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьВидОперацииПоУмолчанию(СтрокиКЗагрузке);
	
	// Заполнение Статьи движения денежных средств по умолчанию
	Отбор = Новый Структура;
	Отбор.Вставить("Загружать", Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	ЗаполнитьСтатьюДвиженияДенежныхСредств(СтрокиКЗагрузке);
	
КонецПроцедуры

Процедура ОбновитьДокументы(ДокументыКЗагрузке, БанковскийСчет, НастройкиЗагрузки) Экспорт
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	СоздаватьКонтрагентов = НастройкиЗагрузки.СоздаватьКонтрагентов;
	ПроводитьДокументы = НастройкиЗагрузки.ПроводитьДокументы;
	
	Отбор = Новый Структура("БанковскийСчет, Загружать, НайденДокументВБазе",
		БанковскийСчет, Истина, Истина);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		ДокументОбъект = СтрокаДокумента.Документ.ПолучитьОбъект();
		
		СделатьНепроведенным = Ложь;
		
		ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
		
		УстановитьСвойство(ДокументОбъект, "ПроведеноБанком", Истина);
		УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", СтрокаДокумента.ДатаПроведения);
		
		ЗаменитьСтароеЗначение = Не КлиентБанкЛокализация.ЭтоОперацияОплатыЧерезОнлайнСервис(СтрокаДокумента.Операция);
		
		УстановитьСвойство(ДокументОбъект, "НомерВходящегоДокумента", СтрокаДокумента.Номер, ЗаменитьСтароеЗначение);
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств") Тогда
			Документы.ПоступлениеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
				ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
				
		ИначеЕсли ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств") Тогда
			Документы.СписаниеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
				ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
		КонецЕсли;
		
		// Контрагент
		Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено И Не ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
				УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
			ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
				УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
			Иначе
				УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
			КонецЕсли;
		КонецЕсли;
		
		// Счет контрагента
		Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено И Не ЗначениеЗаполнено(ДокументОбъект.БанковскийСчетКонтрагента) Тогда
			Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
				УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
			ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
				УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
			КонецЕсли;
		КонецЕсли;
		
		СуммыРазличаются = (СтрокаДокумента.СуммаДокумента <> ДокументОбъект.СуммаДокумента);
		
		Если ТипЗнч(ДокументОбъект) = Тип("ДокументОбъект.СписаниеБезналичныхДенежныхСредств")
			И ДокументОбъект.ПлатежСКонвертацией Тогда
			УстановитьСвойство(ДокументОбъект, "СуммаВВалютеОтправителя", СтрокаДокумента.СуммаДокумента, Истина);
		Иначе
			УстановитьСвойство(ДокументОбъект, "СуммаДокумента", СтрокаДокумента.СуммаДокумента, Истина);
		КонецЕсли;
		
		Если СуммыРазличаются
			И ДокументОбъект.РасшифровкаПлатежа.Количество() <> 1 Тогда
			
			ДокументОбъект.РасшифровкаПлатежа.Очистить();
			
			Если СтрокаДокумента.Исходящий Тогда
				ЗаполнитьРасшифровкуСписания(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки);
			Иначе
				ЗаполнитьРасшифровкуПоступления(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки);
			КонецЕсли;
			
		Иначе
			
			СтатьяДвиженияДенежныхСредств =
				Справочники.СтатьиДвиженияДенежныхСредств.СтатьяДвиженияДенежныхСредствПоХозяйственнойОперации(ДокументОбъект.ХозяйственнаяОперация);
			
			Для каждого СтрокаРасшифровки Из ДокументОбъект.РасшифровкаПлатежа Цикл
				
				// Партнер
				Если Не ЗначениеЗаполнено(СтрокаРасшифровки.Партнер)
					И РеквизитыХозОперации.Найти("РасшифровкаПлатежа.Партнер") <> Неопределено Тогда
					СтрокаРасшифровки.Партнер = СтрокаДокумента.Партнер;
				КонецЕсли;
				
				Если СтрокаРасшифровки.КурсЧислительВзаиморасчетов = 0
					Или СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов = 0 Тогда
					РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
																			СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
																			ДокументОбъект.Валюта,
																			СтрокаРасшифровки.ВалютаВзаиморасчетов,
																			ДокументОбъект.Организация,
																			?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата),,
																			СтрокаРасшифровки.ОбъектРасчетов);
				КонецЕсли;
				
				// Статья ДДС
				Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа.СтатьяДвиженияДенежныхСредств") <> Неопределено
					И Не ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств) Тогда
					
					Если ЗначениеЗаполнено(СтрокаРасшифровки.ОбъектРасчетов) Тогда
						
						РеквизитыОбъектаРасчетов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
										СтрокаРасшифровки.ОбъектРасчетов,
										"Договор.СтатьяДвиженияДенежныхСредств, Соглашение.СтатьяДвиженияДенежныхСредств");
						
						СтатьяДвиженияДенежныхСредствОбъектаРасчетов = 
							?(ЗначениеЗаполнено(РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств),
							РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств,
							РеквизитыОбъектаРасчетов.СоглашениеСтатьяДвиженияДенежныхСредств);
						
						Если ЗначениеЗаполнено(СтатьяДвиженияДенежныхСредствОбъектаРасчетов) Тогда
							СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств =
								?(ЗначениеЗаполнено(РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств),
								РеквизитыОбъектаРасчетов.ДоговорСтатьяДвиженияДенежныхСредств,
								РеквизитыОбъектаРасчетов.СоглашениеСтатьяДвиженияДенежныхСредств);
						КонецЕсли;
						
					КонецЕсли;
					
					Если Не ЗначениеЗаполнено(СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств)
						И ЗначениеЗаполнено(СтатьяДвиженияДенежныхСредств) Тогда
						СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если СуммыРазличаются
				И ДокументОбъект.РасшифровкаПлатежа.Количество() = 1 Тогда
				
				СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа[0];
				СтрокаРасшифровки.Сумма = ДокументОбъект.СуммаДокумента;
				
				ДенежныеСредстваКлиентСервер.РассчитатьСуммуВзаиморасчетовВСтрокеРасшифровки(
					СтрокаРасшифровки, ДокументОбъект.Валюта, ВалютаРеглУчета);
					
				СтруктураПересчетаСуммы = Новый Структура;
				СтруктураПересчетаСуммы.Вставить("ЦенаВключаетНДС", Истина);
				СтруктураДействий = Новый Структура;
				СтруктураДействий.Вставить("ПересчитатьСуммуНДС", СтруктураПересчетаСуммы);
				ПакетнаяОбработкаТабличнойЧастиСервер.ОбработатьСтрокуТЧ(СтрокаРасшифровки, СтруктураДействий, Неопределено);
				
			КонецЕсли;
			
		КонецЕсли;
		
		УстановитьСвойство(ДокументОбъект, "НазначениеПлатежа", СтрокаДокумента.НазначениеПлатежа, Истина);
		УстановитьСвойство(ДокументОбъект, "ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа, Истина);
		УстановитьСвойство(ДокументОбъект, "ДанныеВыписки", СтрокаДокумента.ДанныеВыписки, Истина);
		
		УстановитьСвойство(ДокументОбъект, "Комментарий", СтрокаДокумента.Комментарий);
		
		Если ДокументОбъект.Модифицированность() Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаЗагрузки", ТекущаяДатаСеанса(), Истина);
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.Движения.ДенежныеСредстваБезналичные.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.ПроверитьЗаполнение();
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОшибкиЗаполнения") Тогда
			УстановитьСвойство(ДокументОбъект, "ОшибкиЗагрузки", ДокументОбъект.ДополнительныеСвойства.ОшибкиЗаполнения, Истина);
		КонецЕсли;
		
		Если ПроводитьДокументы
			И СокрЛП(ДокументОбъект.ОшибкиЗагрузки) = "" Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе
			
			РежимЗаписи = РежимЗаписиДокумента.Запись;
			
			Если СокрЛП(ДокументОбъект.ОшибкиЗагрузки) <> "" Тогда
				СделатьНепроведенным = ДокументОбъект.Проведен;
			КонецЕсли;
			
		КонецЕсли;
		
		Если СуммыРазличаются Тогда
			ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки, НСтр("ru = 'Изменилась сумма документа';
																	|en = 'The document amount has changed'"));
		КонецЕсли;
		
		ЗаписатьОбъект(ДокументОбъект, ?(СделатьНепроведенным, РежимЗаписиДокумента.ОтменаПроведения, РежимЗаписи), СтрокаДокумента);
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьДокументы(ДокументыКЗагрузке, БанковскийСчет, НастройкиЗагрузки) Экспорт
	
	ПроводитьДокументы = НастройкиЗагрузки.ПроводитьДокументы;
	
	РеквизитыСчета = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет);
	РазрешитьПлатежиБезУказанияЗаявок = РеквизитыСчета.РазрешитьПлатежиБезУказанияЗаявок;
	
	Организация          = РеквизитыСчета.Организация;
	Префикс              = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "Префикс");
	Валюта               = РеквизитыСчета.Валюта;
	ТекущийПользователь  = Пользователи.ТекущийПользователь();
	
	Отбор = Новый Структура("БанковскийСчет, Загружать, НайденДокументВБазе",
		БанковскийСчет, Истина, Ложь);
	СтрокиКЗагрузке = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			ДокументОбъект = Документы.СписаниеБезналичныхДенежныхСредств.СоздатьДокумент();
		Иначе
			ДокументОбъект = Документы.ПоступлениеБезналичныхДенежныхСредств.СоздатьДокумент();
		КонецЕсли;
		
		// Общие реквизиты
		ДатаВхДокумента = ДенежныеСредстваКлиентСервер.ПолучитьДатуИзСтроки(СтрокаДокумента.Дата);
		Если ЗначениеЗаполнено(ДатаВхДокумента) Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаВходящегоДокумента", ДатаВхДокумента);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ДатаВходящегоДокумента", СтрокаДокумента.ДатаДок);
		КонецЕсли;
		УстановитьСвойство(ДокументОбъект, "НомерВходящегоДокумента", СтрокаДокумента.Номер);
		
		УстановитьСвойство(ДокументОбъект, "Организация", Организация);
		УстановитьСвойство(ДокументОбъект, "БанковскийСчет", БанковскийСчет);
		УстановитьСвойство(ДокументОбъект, "Валюта", Валюта);
		
		УстановитьСвойство(ДокументОбъект, "ХозяйственнаяОперация", СтрокаДокумента.ВидОперации);
		УстановитьСвойство(ДокументОбъект, "ТипПлатежногоДокумента", СтрокаДокумента.ТипПлатежногоДокумента);
		УстановитьСвойство(ДокументОбъект, "СуммаДокумента", СтрокаДокумента.СуммаДокумента);
		
		УстановитьСвойство(ДокументОбъект, "НазначениеПлатежа", СтрокаДокумента.НазначениеПлатежа);
		УстановитьСвойство(ДокументОбъект, "ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа);
		УстановитьСвойство(ДокументОбъект, "ДанныеВыписки", СтрокаДокумента.ДанныеВыписки);
		УстановитьСвойство(ДокументОбъект, "ИдентификаторПлатежа", СтрокаДокумента.Код);
		
		УстановитьСвойство(ДокументОбъект, "Комментарий", СтрокаДокумента.Комментарий);
		УстановитьСвойство(ДокументОбъект, "ДатаЗагрузки", ТекущаяДатаСеанса());
		
		УстановитьСвойство(ДокументОбъект, "ТипНалога", СтрокаДокумента.ТипНалога);
		УстановитьСвойство(ДокументОбъект, "НастройкаСчетовУчета", СтрокаДокумента.НастройкаСчетовУчета);
		
		УстановитьСвойство(ДокументОбъект, "ОшибкиЗагрузки", СтрокаДокумента.ОшибкиЗагрузки);
		
		УстановитьСвойство(ДокументОбъект, "Ответственный", ТекущийПользователь);
		
		УстановитьСвойство(ДокументОбъект, "Дата", ДокументОбъект.ДатаВходящегоДокумента);
		
		Если НачалоДня(ДокументОбъект.Дата) = НачалоДня(ТекущаяДатаСеанса()) Тогда
			ДокументОбъект.Дата = ТекущаяДатаСеанса();
		Иначе
			ДокументОбъект.Дата = КонецДня(ДокументОбъект.Дата);
		КонецЕсли;
		
		УстановитьСвойство(ДокументОбъект, "ВалютаКонвертации", СтрокаДокумента.ВалютаКонвертации);
		ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
		
		Если ЗначениеЗаполнено(ДокументОбъект.ВалютаКонвертации) Тогда
			ДенежныеСредстваСервер.ПересчитатьПараметрыКонвертации(ДокументОбъект, ВалютаРеглУчета, "ВалютаКонвертации");
		КонецЕсли;
		
		// Индивидуальное заполнение
		Если СтрокаДокумента.Исходящий Тогда
			ЗаполнитьРеквизитыСписания(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, НастройкиЗагрузки, Префикс);
		Иначе
			ЗаполнитьРеквизитыПоступления(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, НастройкиЗагрузки);
		КонецЕсли;
		
		Если Не КлиентБанкЛокализация.ЭтоОперацияОплатыЧерезОнлайнСервис(СтрокаДокумента.Операция) Тогда
			УстановитьСвойство(ДокументОбъект, "ПроведеноБанком", Истина);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения) Тогда
			УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", СтрокаДокумента.ДатаПроведения);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ДатаПроведенияБанком", ДокументОбъект.Дата);
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДокументОбъект.ДатаВходящегоДокумента) Тогда
			ДокументОбъект.ДатаВходящегоДокумента = ДокументОбъект.Дата;
		КонецЕсли;
		Если Не ЗначениеЗаполнено(ДокументОбъект.НомерВходящегоДокумента) Тогда
			ДокументОбъект.НомерВходящегоДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДокументОбъект.Номер);
		КонецЕсли;
		
		ДокументОбъект.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.Движения.ДенежныеСредстваБезналичные.ДополнительныеСвойства.Вставить("ОбменСБанками", Истина);
		ДокументОбъект.ПроверитьЗаполнение();
		
		Если ДокументОбъект.ДополнительныеСвойства.Свойство("ОшибкиЗаполнения") Тогда
			ДокументОбъект.ОшибкиЗагрузки = СокрЛП(ДокументОбъект.ОшибкиЗагрузки + "
			|" + ДокументОбъект.ДополнительныеСвойства.ОшибкиЗаполнения);
		КонецЕсли;
		
		Если ПроводитьДокументы
			И СокрЛП(ДокументОбъект.ОшибкиЗагрузки) = "" Тогда
			РежимЗаписи = РежимЗаписиДокумента.Проведение;
		Иначе
			РежимЗаписи = РежимЗаписиДокумента.Запись;
		КонецЕсли;
		
		Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации 
			ИЛИ ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
			Если СтрокаДокумента.Исходящий Тогда
				ЗеркальныйПлатежСсылка = Документы.СписаниеБезналичныхДенежныхСредств.НайтиЗеркальныйПлатеж(ДокументОбъект.Ссылка);
				Если ЗначениеЗаполнено(ЗеркальныйПлатежСсылка) И ЗеркальныйПлатежСсылка.СуммаДокумента = ДокументОбъект.СуммаДокумента Тогда
					Документы.ПоступлениеБезналичныхДенежныхСредств.ПривестиВСоответствиеЗеркальныйПлатеж(ЗеркальныйПлатежСсылка, ДокументОбъект);
				КонецЕсли;
			Иначе
				ЗеркальныйПлатежСсылка = Документы.ПоступлениеБезналичныхДенежныхСредств.НайтиЗеркальныйПлатеж(ДокументОбъект.Ссылка);
				Если ЗначениеЗаполнено(ЗеркальныйПлатежСсылка) И ЗеркальныйПлатежСсылка.СуммаДокумента = ДокументОбъект.СуммаДокумента Тогда
					Документы.СписаниеБезналичныхДенежныхСредств.ПривестиВСоответствиеЗеркальныйПлатеж(ЗеркальныйПлатежСсылка, ДокументОбъект);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		ЗаписатьОбъект(ДокументОбъект, РежимЗаписи, СтрокаДокумента);
		
		СтрокаДокумента.Документ = ДокументОбъект.Ссылка;
	КонецЦикла;
	
КонецПроцедуры

// Создает элемент справочника "Контрагенты"
//
// Параметры:
//    ДокументыКЗагрузке - ТаблицаЗначений - Таблица документов выписки
//    СтрокаДокумента - СтрокаТаблицыЗначений - Содержит описание контрагента и связанных данных.
//
// Возвращаемое значение:
//    СправочникСсылка.Контрагенты - Ссылка на новый элемент справочника.
//
Функция СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Если СтрокаДокумента.Исходящий Тогда
		
		ИмяКолонкиНаименованиеМеждународное = "ПОЛУЧАТЕЛЬНаименованиеМеждународное";
		ИмяКолонкиИНН    = "ПОЛУЧАТЕЛЬИНН";
		ИмяКолонкиКПП    = "ПОЛУЧАТЕЛЬКПП";
		ИмяКолонкиСтрана = "ПОЛУЧАТЕЛЬСтрана";
		
	Иначе
		
		ИмяКолонкиНаименованиеМеждународное = "ПЛАТЕЛЬЩИКНаименованиеМеждународное";
		ИмяКолонкиИНН = "ПЛАТЕЛЬЩИКИНН";
		ИмяКолонкиКПП = "ПЛАТЕЛЬЩИККПП";
		ИмяКолонкиСтрана = "ПЛАТЕЛЬЩИКСтрана";	
		
	КонецЕсли;
	
	СтруктураНаименования = ДенежныеСредстваСерверЛокализация.НаименованиеОрганизации(СтрокаДокумента.ИмяКонтрагента);
	
	ДанныеКонтрагента = РеквизитыЗаполненияКонтрагента();
	ДанныеПартнера = РеквизитыЗаполненияПартнера();
	
	ДанныеКонтрагента.Вставить("Наименование", СтруктураНаименования.СокращенноеНаименование);
	ДанныеКонтрагента.Вставить("НаименованиеПолное", СтрокаДокумента.ИмяКонтрагента);
	ДанныеКонтрагента.Вставить("НаименованиеМеждународное", СтрокаДокумента[ИмяКолонкиНаименованиеМеждународное]);
	ДанныеКонтрагента.Вставить("ИНН", ?(СтрокаДокумента[ИмяКолонкиИНН] = "000000000000" Или СтрокаДокумента[ИмяКолонкиИНН] = "0000000000",
										"",
										СтрокаДокумента[ИмяКолонкиИНН]));
	ДанныеКонтрагента.Вставить("КПП", ?(СтрокаДокумента[ИмяКолонкиКПП] = "0", "", СтрокаДокумента[ИмяКолонкиКПП]));
	ДанныеКонтрагента.Вставить("СтранаРегистрации", Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа2", СтрокаДокумента[ИмяКолонкиСтрана]));
	
	ОсновнаяСтрана = Константы.ОсновнаяСтрана.Получить();
	Если Не ЗначениеЗаполнено(ДанныеКонтрагента.СтранаРегистрации) Тогда
		ДанныеКонтрагента.Вставить("СтранаРегистрации", ОсновнаяСтрана);
	КонецЕсли;
	
	Если СтрДлина(ДанныеКонтрагента.ИНН) = 10
			Или СтрокаДокумента.ЭтоЗагрузкаBankFeeds Тогда
		ДанныеКонтрагента.Вставить("ЮрФизЛицо", Перечисления.ЮрФизЛицо.ЮрЛицо);
	ИначеЕсли ЗначениеЗаполнено(ДанныеКонтрагента.СтранаРегистрации)
		И ДанныеКонтрагента.СтранаРегистрации <> ОсновнаяСтрана Тогда
		ДанныеКонтрагента.Вставить("ЮрФизЛицо", Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент);
	Иначе
		
		Если СтрокаДокумента.ЭтоФизЛицо Тогда
			ДанныеКонтрагента.Вставить("ЮрФизЛицо", Перечисления.ЮрФизЛицо.ФизЛицо);
			ДанныеПартнера.Вставить("ЮрФизЛицо", Перечисления.КомпанияЧастноеЛицо.ЧастноеЛицо);
		Иначе
			ДанныеКонтрагента.Вставить("ЮрФизЛицо", Перечисления.ЮрФизЛицо.ИндивидуальныйПредприниматель);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ДанныеПартнера, ДанныеКонтрагента, , "ЮрФизЛицо");
	
	Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
		Возврат СтрокаДокумента.Контрагент;
	КонецЕсли;
	
	Партнер = СоздатьПартнера(ДанныеПартнера);
	ДанныеКонтрагента.Вставить("Партнер", Партнер);
	
	Контрагент = НайтиСоздатьКонтрагента(СтрокаДокумента, ДанныеКонтрагента);
	
	СтрокаДокумента.Контрагент = Контрагент;
	СтрокаДокумента.Партнер = Партнер;
	
	// Нужно подставить нового контрагента и партнера в другие строки выписки
	Если Не ПустаяСтрока(ДанныеКонтрагента.ИНН) Тогда
		
		ПоляОтбора = "БанковскийСчет, ПОЛУЧАТЕЛЬИНН, Исходящий";
		СтруктураПоиска = Новый Структура(
			ПоляОтбора, СтрокаДокумента.БанковскийСчет, ДанныеКонтрагента.ИНН, Истина);
		ЗаполнитьКонтрагентаВДокументахЗагрузки(Контрагент, Партнер, ДокументыКЗагрузке, СтруктураПоиска);
		
		ПоляОтбора = "БанковскийСчет, ПЛАТЕЛЬЩИКИНН, Исходящий";
		СтруктураПоиска = Новый Структура(
			ПоляОтбора, СтрокаДокумента.БанковскийСчет, ДанныеКонтрагента.ИНН, Ложь);
		ЗаполнитьКонтрагентаВДокументахЗагрузки(Контрагент, Партнер, ДокументыКЗагрузке, СтруктураПоиска);
		
	Иначе
		
		ПоляОтбора = "БанковскийСчет, ИмяКонтрагента";
		СтруктураПоиска = Новый Структура(
			ПоляОтбора, СтрокаДокумента.БанковскийСчет, СтрокаДокумента.ИмяКонтрагента);
		ЗаполнитьКонтрагентаВДокументахЗагрузки(Контрагент, Партнер, ДокументыКЗагрузке, СтруктураПоиска);
		
	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

// Создает элемент справочника "БанковскиеСчетаКонтрагентов"
//
// Параметры:
//    ДокументыКЗагрузке - ТаблицаЗначений - Таблица документов выписки
//    СтрокаДокумента - СтрокаТаблицыЗначений - Содержит описание счета и связанных данных
//    ВладелецСчета - СправочникСсылка.Контрагенты - Контрагент - владелец счета.
//
// Возвращаемое значение:
//    СправочникСсылка.БанковскиеСчетаКонтрагентов - Ссылка на новый элемент справочника.
//
Функция СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ВладелецСчета) Экспорт
	
	Если СтрокаДокумента.Исходящий Тогда
		
		ИмяКолонкиСчет              = "ПОЛУЧАТЕЛЬСЧЕТ";
		ИмяКолонкиПрямыеРасчеты     = "ПОЛУЧАТЕЛЬ2";
		ИмяКолонкиНаименованиеБанкаНеПрямыеРасчеты     = "ПОЛУЧАТЕЛЬ3";
		ИмяКолонкиГородБанкаНеПрямыеРасчеты     = "ПОЛУЧАТЕЛЬ4";
		ИмяКолонкиБИК               = "ПОЛУЧАТЕЛЬБИК";
		ИмяКолонкиКорСчет           = "ПОЛУЧАТЕЛЬКОРСЧЕТ";
		ИмяКолонкиРасчСчет          = "ПОЛУЧАТЕЛЬРАСЧСЧЕТ";
		ИмяКолонкиКодСтраныБанка    = "ПОЛУЧАТЕЛЬБАНК5";
		ИмяКолонкиСВИФТ             = "ПОЛУЧАТЕЛЬСВИФТ";
		ИмяКолонкиНаименованиеБанка = "ПОЛУЧАТЕЛЬБАНК1";
		ИмяКолонкиГородБанка        = "ПОЛУЧАТЕЛЬБАНК2";
		
	Иначе
		
		ИмяКолонкиСчет              = "ПЛАТЕЛЬЩИКСЧЕТ";
		ИмяКолонкиПрямыеРасчеты     = "ПЛАТЕЛЬЩИК2";
		ИмяКолонкиНаименованиеБанкаНеПрямыеРасчеты     = "ПЛАТЕЛЬЩИК3";
		ИмяКолонкиГородБанкаНеПрямыеРасчеты     = "ПЛАТЕЛЬЩИК4";
		ИмяКолонкиБИК               = "ПЛАТЕЛЬЩИКБИК";
		ИмяКолонкиКорСчет           = "ПЛАТЕЛЬЩИККОРСЧЕТ";
		ИмяКолонкиРасчСчет          = "ПЛАТЕЛЬЩИКРАСЧСЧЕТ";
		ИмяКолонкиКодСтраныБанка    = "ПЛАТЕЛЬЩИКБАНК5";
		ИмяКолонкиСВИФТ             = "ПЛАТЕЛЬЩИКСВИФТ";
		ИмяКолонкиНаименованиеБанка = "ПЛАТЕЛЬЩИКБАНК1";
		ИмяКолонкиГородБанка        = "ПЛАТЕЛЬЩИКБАНК2";
		
	КонецЕсли;
	
	НомерСчета = СтрокаДокумента[ИмяКолонкиСчет];
	Если Не ЗначениеЗаполнено(НомерСчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеСчета = РеквизитыБанковскогоСчета();
	
	ДанныеСчета.Вставить("Владелец",              ВладелецСчета);
	ДанныеСчета.Вставить("ВалютаДенежныхСредств", СтрокаДокумента.Валюта);
	ДанныеСчета.Вставить("НомерСчета",            НомерСчета);
	
	Если СтрокаДокумента.ЭтоЗагрузкаBankFeeds 
		И ДенежныеСредстваКлиентСервер.ПравильнаяДлинаIBAN(НомерСчета) Тогда
		
		ДанныеСчета.Вставить("ЭтоIBAN",   Истина);
		
	КонецЕсли;
	
	ПрямыеРасчеты = ПустаяСтрока(СтрокаДокумента[ИмяКолонкиПрямыеРасчеты]);
	ОсновнаяСтрана = Константы.ОсновнаяСтрана.Получить();
	Если ПрямыеРасчеты Тогда
		
		// Банк контрагента
		БикБанка       = СтрокаДокумента[ИмяКолонкиБИК];
		КоррСчетБанка  = СтрокаДокумента[ИмяКолонкиКорСчет];
		
		Банк = Неопределено;
		Если Не ПустаяСтрока(БикБанка) Тогда
			Банк = Справочники.КлассификаторБанков.НайтиПоКоду(БикБанка);
		КонецЕсли;
		Если Не ЗначениеЗаполнено(Банк) И Не ПустаяСтрока(КоррСчетБанка) Тогда
			Банк = Справочники.КлассификаторБанков.НайтиПоРеквизиту("КоррСчет", КоррСчетБанка);
		КонецЕсли;
		
		КодСтраныБанка = СтрокаДокумента[ИмяКолонкиКодСтраныБанка];
		Если ЗначениеЗаполнено(КодСтраныБанка) Тогда
			СтранаБанка = Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа2", КодСтраныБанка);
			Если ЗначениеЗаполнено(СтранаБанка)
				//++ Локализация
				И СтранаБанка <> ОсновнаяСтрана
				//-- Локализация
				Тогда
				ДанныеСчета.Вставить("ИностранныйБанк", Истина);
			КонецЕсли;
			ДанныеСчета.Вставить("СтранаБанка", СтранаБанка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Банк) Тогда
			
			СтранаБанка = ДенежныеСредстваСервер.СтранаБанкаПоБанку(Банк);
			
			Если СтранаБанка = ОсновнаяСтрана Тогда
				ДанныеСчета.Вставить("Банк", Банк);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеСчета.Банк) Тогда
			ДанныеСчета.Вставить("РучноеИзменениеРеквизитовБанка", Истина);
			ДанныеСчета.Вставить("БИКБанка",          СтрокаДокумента[ИмяКолонкиБИК]);
			ДанныеСчета.Вставить("СВИФТБанка",        СтрокаДокумента[ИмяКолонкиСВИФТ]);
			ДанныеСчета.Вставить("НаименованиеБанка", СтрокаДокумента[ИмяКолонкиНаименованиеБанка]);
			ДанныеСчета.Вставить("ГородБанка",        СтрокаДокумента[ИмяКолонкиГородБанка]);
			ДанныеСчета.Вставить("КоррСчетБанка",     СтрокаДокумента[ИмяКолонкиКорСчет]);
		КонецЕсли;
		
	Иначе
		
		// Банк контрагента
		НаимБанка      = СтрокаДокумента[ИмяКолонкиНаименованиеБанкаНеПрямыеРасчеты];
		КоррСчетБанка  = СтрокаДокумента[ИмяКолонкиРасчСчет];
		
		Банк = Неопределено;
		Если Не ПустаяСтрока(НаимБанка) И Не ПустаяСтрока(КоррСчетБанка) Тогда
			Банк = Справочники.КлассификаторБанков.НайтиПоРеквизиту("КоррСчет", КоррСчетБанка);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Банк) Тогда
			
			СтранаБанка = ДенежныеСредстваСервер.СтранаБанкаПоБанку(Банк);
			
			Если СтранаБанка = ОсновнаяСтрана Тогда
				ДанныеСчета.Вставить("Банк", Банк);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеСчета.Банк) Тогда
			ДанныеСчета.Вставить("РучноеИзменениеРеквизитовБанка", Истина);
			ДанныеСчета.Вставить("НаименованиеБанка", НаимБанка);
			ДанныеСчета.Вставить("ГородБанка",        СтрокаДокумента[ИмяКолонкиГородБанкаНеПрямыеРасчеты]);
			ДанныеСчета.Вставить("КоррСчетБанка",     КоррСчетБанка);
		КонецЕсли;
		
		// РЦ банка контрагента
		РЦБанка         = СтрокаДокумента[ИмяКолонкиНаименованиеБанка];
		БикРЦБанка      = СтрокаДокумента[ИмяКолонкиБИК];
		КоррСчетРЦБанка = СтрокаДокумента[ИмяКолонкиКорСчет];
		
		РЦБанк = Неопределено;
		Если Не ПустаяСтрока(РЦБанка) И Не ПустаяСтрока(БикРЦБанка) Тогда
			РЦБанк = Справочники.КлассификаторБанков.НайтиПоКоду(БикРЦБанка);
			Если Не ЗначениеЗаполнено(РЦБанк) Тогда
				РЦБанк = Справочники.КлассификаторБанков.НайтиПоРеквизиту("КоррСчет", КоррСчетРЦБанка);
			КонецЕсли;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(РЦБанк) Тогда
			
			СтранаБанка = ДенежныеСредстваСервер.СтранаБанкаПоБанку(РЦБанк);
			
			Если СтранаБанка = ОсновнаяСтрана Тогда
				ДанныеСчета.Вставить("БанкДляРасчетов", РЦБанк);
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(ДанныеСчета.БанкДляРасчетов) Тогда
			ДанныеСчета.Вставить("РучноеИзменениеРеквизитовБанкаДляРасчетов", Истина);
			ДанныеСчета.Вставить("НаименованиеБанкаДляРасчетов", РЦБанка);
			ДанныеСчета.Вставить("ГородБанкаДляРасчетов",        СтрокаДокумента[ИмяКолонкиГородБанка]);
			ДанныеСчета.Вставить("БИКБанкаДляРасчетов",          БикРЦБанка);
			ДанныеСчета.Вставить("КоррСчетБанкаДляРасчетов",     КоррСчетРЦБанка);
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеСчета.РучноеИзменениеРеквизитовБанка Тогда
		СтрокаНаименования = Прав(СокрЛП(ДанныеСчета.НомерСчета), 4)
			+ ?(ЗначениеЗаполнено(ДанныеСчета.НаименованиеБанка), " в " + Строка(ДанныеСчета.НаименованиеБанка), "")
			+ " (" + Строка(ДанныеСчета.ВалютаДенежныхСредств) + ")";
	Иначе
		СтрокаНаименования = Прав(СокрЛП(ДанныеСчета.НомерСчета), 4) 
			+ ?(ЗначениеЗаполнено(ДанныеСчета.Банк), " в " + Строка(ДанныеСчета.Банк), "")
			+ " (" + Строка(ДанныеСчета.ВалютаДенежныхСредств) + ")";
	КонецЕсли;
	
	ДанныеСчета.Вставить("Наименование", Лев(СтрокаНаименования, 150));
	
	Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
		Возврат СтрокаДокумента.СчетКонтрагента;
	КонецЕсли;
	
	Счет = НайтиСоздатьБанковскийСчетКонтрагента(ДанныеСчета);
	
	// Нужно подставить новый счет в другие строки выписки
	СтруктураПоиска = Новый Структура("ПОЛУЧАТЕЛЬСЧЕТ, Контрагент, БанковскийСчет",
		ДанныеСчета.НомерСчета, ВладелецСчета, СтрокаДокумента.БанковскийСчет);
	
	ЗаписиКонтрагента = ДокументыКЗагрузке.НайтиСтроки(СтруктураПоиска);
	
	Для каждого ЗаписьКонтрагента Из ЗаписиКонтрагента Цикл
		ЗаписьКонтрагента.СчетКонтрагента = Счет;
	КонецЦикла;
	
	СтруктураПоиска = Новый Структура("ПЛАТЕЛЬЩИКСЧЕТ, Контрагент, БанковскийСчет",
		ДанныеСчета.НомерСчета, ВладелецСчета, СтрокаДокумента.БанковскийСчет);
	
	ЗаписиКонтрагента = ДокументыКЗагрузке.НайтиСтроки(СтруктураПоиска);
	
	Для каждого ЗаписьКонтрагента Из ЗаписиКонтрагента Цикл
		ЗаписьКонтрагента.СчетКонтрагента = Счет;
	КонецЦикла;
	
	Возврат Счет;
	
КонецФункции

Процедура ЗаполнитьКонтрагентов(ДокументыКЗагрузке, ТаблицаДокументов) Экспорт
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ТаблицаДокументов.ИмяКонтрагента КАК Наименование,
	|	ТаблицаДокументов.СокрИмяКонтрагента КАК СокращенноеНаименование,
	|	ТаблицаДокументов.Контрагент КАК Контрагент,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий ТОГДА
	|			ТаблицаДокументов.ПолучательИНН
	|		ИНАЧЕ
	|			ТаблицаДокументов.ПлательщикИНН
	|	КОНЕЦ КАК ИНН,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий ТОГДА
	|		ТаблицаДокументов.ПолучательКПП
	|	ИНАЧЕ
	|		ТаблицаДокументов.ПлательщикКПП
	|	КОНЕЦ КАК КПП,
	|	ТаблицаДокументов.ПроверятьИНН,
	|	ТаблицаДокументов.ПроверятьКПП,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий ТОГДА
	|			ТаблицаДокументов.ПолучательСчет
	|		ИНАЧЕ
	|			ТаблицаДокументов.ПлательщикСчет
	|	КОНЕЦ КАК НомерСчета,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА ТаблицаДокументов.Исходящий ТОГДА
	|			ТаблицаДокументов.ПолучательБИК
	|		ИНАЧЕ
	|			ТаблицаДокументов.ПлательщикБИК
	|	КОНЕЦ КАК Строка(9)) КАК БИКБанка,
	|	ТаблицаДокументов.ВидОперации
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Выполнить();
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Владелец КАК Контрагент,
	|	ДанныеСправочника.Ссылка КАК СчетКонтрагента,
	|	0 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаОрганизаций КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|		И (ДанныеСправочника.Банк.Код = ТаблицаДокументов.БИКБанка ИЛИ ДанныеСправочника.БИКБанка = ТаблицаДокументов.БИКБанка)
	|		И ДанныеСправочника.Владелец.ИНН = ТаблицаДокументов.ИНН
	|		И ТаблицаДокументов.ПроверятьИНН
	|		И НЕ ДанныеСправочника.Закрыт,
	|	Константа.ИспользоватьПередачиТоваровМеждуОрганизациями КАК ИспользоватьПередачиТоваровМеждуОрганизациями
	|ГДЕ
	|	ВЫБОР
	|		КОГДА ТаблицаДокументов.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПустаяСсылка)
	|				ИЛИ ТаблицаДокументов.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации)
	|				ИЛИ ТаблицаДокументов.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию)
	|			ТОГДА ИспользоватьПередачиТоваровМеждуОрганизациями.Значение
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ // Контрагент уже найден, ищем счет
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	Контрагенты.Ссылка КАК Контрагент,
	|	ДанныеСправочника.Ссылка КАК СчетКонтрагента,
	|	5 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК Контрагенты
	|	ПО
	|		Контрагенты.Ссылка = ТаблицаДокументов.Контрагент
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаКонтрагентов КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|		И ДанныеСправочника.Владелец = Контрагенты.Ссылка
	|		И НЕ ДанныеСправочника.Закрыт
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Владелец КАК Контрагент,
	|	ДанныеСправочника.Ссылка КАК СчетКонтрагента,
	|	10 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаКонтрагентов КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|		И ДанныеСправочника.Владелец.ИНН = ТаблицаДокументов.ИНН
	|		И ДанныеСправочника.Владелец.КПП = ТаблицаДокументов.КПП
	|		И ТаблицаДокументов.ПроверятьКПП
	|		И НЕ ДанныеСправочника.Закрыт
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Владелец КАК Контрагент,
	|	ДанныеСправочника.Ссылка КАК СчетКонтрагента,
	|	20 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаКонтрагентов КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|		И ДанныеСправочника.Владелец.ИНН = ТаблицаДокументов.ИНН
	|		И ТаблицаДокументов.ПроверятьИНН
	|		И НЕ ДанныеСправочника.Закрыт
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Владелец КАК Контрагент,
	|	ДанныеСправочника.Ссылка КАК СчетКонтрагента,
	|	30 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаКонтрагентов КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|		И (ДанныеСправочника.Владелец.НаименованиеПолное = ТаблицаДокументов.Наименование
	|			ИЛИ ДанныеСправочника.Владелец.НаименованиеПолное = ТаблицаДокументов.СокращенноеНаименование)
	|		И НЕ ДанныеСправочника.Закрыт
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Ссылка КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК СчетКонтрагента,
	|	40 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.ИНН = ТаблицаДокументов.ИНН
	|		И ДанныеСправочника.КПП = ТаблицаДокументов.КПП
	|		И ТаблицаДокументов.ПроверятьКПП
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Ссылка КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК СчетКонтрагента,
	|	50 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.ИНН = ТаблицаДокументов.ИНН
	|		И ТаблицаДокументов.ПроверятьИНН
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Ссылка КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК СчетКонтрагента,
	|	60 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.НаименованиеПолное = ТаблицаДокументов.Наименование
	|		И (ДанныеСправочника.ИНН = """"
	|			ИЛИ НЕ ТаблицаДокументов.ПроверятьИНН)
	|		И (ДанныеСправочника.КПП = """"
	|			ИЛИ НЕ ТаблицаДокументов.ПроверятьКПП)
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеСправочника.Ссылка КАК Контрагент,
	|	НЕОПРЕДЕЛЕНО КАК СчетКонтрагента,
	|	70 КАК Приоритет
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.ФизическиеЛица КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.Наименование = ТаблицаДокументов.Наименование
	|		И (ДанныеСправочника.ИНН = ТаблицаДокументов.ИНН
	|			ИЛИ ДанныеСправочника.ИНН = """"
	|			ИЛИ НЕ ТаблицаДокументов.ПроверятьИНН)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ДанныеСправочника.Владелец,
	|	ДанныеСправочника.Ссылка,
	|	80
	|
	|ИЗ
	|	ТаблицаДокументов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаКонтрагентов КАК ДанныеСправочника
	|		ПО ДанныеСправочника.НомерСчета = ТаблицаДокументов.НомерСчета
	|			И НЕ ДанныеСправочника.Закрыт
	|			И (ДанныеСправочника.Владелец.ИНН = """"
	|				ИЛИ НЕ ТаблицаДокументов.ПроверятьИНН)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки,
	|	Приоритет
	|";
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	УстановитьПривилегированныйРежим(Ложь);
	
	Пока Выборка.СледующийПоЗначениюПоля("НомерСтроки") Цикл
		
		СтрокаДокумента = ДокументыКЗагрузке.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаДокумента <> Неопределено Тогда
			
			НайденоКонтрагентов = 0;
			ТекущийПриоритет = 99;
			
			Пока Выборка.Следующий() Цикл
				Если ТекущийПриоритет < Выборка.Приоритет Тогда
					Прервать;
				КонецЕсли;
				
				Если СтрокаДокумента.ЭтоСБП
					И Не ЗначениеЗаполнено(СтрокаДокумента.Контрагент)
					И Выборка.Контрагент = Справочники.Контрагенты.РозничныйПокупатель Тогда
					Продолжить;
				КонецЕсли;
				
				ТекущийПриоритет = Выборка.Приоритет;
				НайденоКонтрагентов = НайденоКонтрагентов + 1;
				
				Если НайденоКонтрагентов = 1 Тогда
					
					СтрокаДокумента.Контрагент = Выборка.Контрагент;
					СтрокаДокумента.НайденКонтрагент = Истина;
					
					Если ЗначениеЗаполнено(Выборка.СчетКонтрагента) Тогда
						СтрокаДокумента.СчетКонтрагента = Выборка.СчетКонтрагента;
						СтрокаДокумента.НайденСчетКонтрагента = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			// Если найдено два одинаковых контрагента с одинаковым номером счета, тогда контрагента не заполняем, 
			// будет предложено выбрать контрагента, по выбранному контрагенту определится счет.
			// Если найдено два одинаковых контрагента без найденного счета, тогда контрагент не заполняем, 
			// будет предложено выбрать контрагента и для него будет создан счет.
			// Если у одного контрагента 2 и более одинаковых счета, тогда контрагента не заполняем.
			// В остальных случаях если найдены дубли с одинаковым приоритетом, то контрагент останется пустым и новый создаваться не будет.
			Если НайденоКонтрагентов > 1 Тогда
				
				СтрокаДокумента.Контрагент = Неопределено;
				СтрокаДокумента.СчетКонтрагента = Неопределено;
				СтрокаДокумента.СоздаватьКонтрагента = Ложь;
				СтрокаДокумента.НайденКонтрагент = Ложь;
				СтрокаДокумента.НайденСчетКонтрагента = Ложь;
			
				ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки,
					СтрШаблон(НСтр("ru = 'Найдено более одного контрагента или банковского счета контрагента (%1)!';
									|en = 'More than one counterparty or counterparty bank account is found (%1).'"), СтрокаДокумента.ИмяКонтрагента));
				
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Возвращает пустую таблицу счетов для проверки запрета редактирования данных в загружаемом периоде.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Пустая таблица счетов для проверки запрета редактирования:
// * Объект - СправочникСсылка.БанковскиеСчетаОрганизаций - банковский счет.
// * Дата - Дата - проверяемая дата.
Функция ПустаяТаблицаСчетовДляПроверкиЗапретаРедактирования() Экспорт

	Таблица = Новый ТаблицаЗначений;
	
	Таблица.Колонки.Добавить("Объект", Новый ОписаниеТипов("СправочникСсылка.БанковскиеСчетаОрганизаций"));
	Таблица.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	
	Возврат Таблица;

КонецФункции

// Возвращает список счетов с запретом изменения в загружаемом периоде.
// 
// Параметры:
//  ПроверяемыеСчета - см. ПустаяТаблицаСчетовДляПроверкиЗапретаРедактирования.
// 
// Возвращаемое значение:
//  Массив из СправочникСсылка.БанковскиеСчетаОрганизаций - список счетов.
//
Функция ЗаполнитьСписокСчетовСЗапретомИзмененияВЗагружаемомПериоде(ПроверяемыеСчета) Экспорт

	СписокСчетов = Новый Массив;
	
	Если ПроверяемыеСчета.Количество() = 0 Тогда
		Возврат СписокСчетов;
	КонецЕсли;
	
	ДанныеДляПроверки = ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	
	Для Каждого Счет Из ПроверяемыеСчета Цикл
		
		НоваяСтрока = ДанныеДляПроверки.Добавить();
		НоваяСтрока.Дата = Счет.Дата;
		НоваяСтрока.Раздел = "Банк";
		НоваяСтрока.Объект = Счет.Объект;
		
	КонецЦикла;
	
	ПараметрыСообщения = ДатыЗапретаИзменения.ПараметрыСообщенияОЗапрете();
	ОписаниеОшибки = Новый Структура;
	
	ИзмененияЗапрещены = ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, ПараметрыСообщения, ОписаниеОшибки);
	
	Если Не ИзмененияЗапрещены Тогда
		Возврат СписокСчетов;
	КонецЕсли;
	
	Для Каждого Ошибка Из ОписаниеОшибки.Запреты Цикл
		СписокСчетов.Добавить(Ошибка.Объект);
	КонецЦикла;
	
	Возврат СписокСчетов;

КонецФункции

// Обновляет значение свойства/реквизита объекта (документа) новым значением.
// Заполнение выполняется если новое значение заполнено и отлично от текущего, при этом свойство/реквизит может быть 
// не заполнено или заполнено с условием обновления текущего значения.
// 
// Параметры:
//  Объект - ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств, ДокументОбъект.СписаниеБезналичныхДенежныхСредств - Объект для обновления реквизитов.
//  ИмяСвойства - Строка - наименование (имя) свойства/реквизита.
//  ЗначениеСвойства - Произвольный - новое значение свойства/реквизита.
//  ОбязательнаяЗаменаСтарогоЗначения - Булево - признак замены текущего значения свойства/реквизита объекта, если новое 
//  и текущее значение отличаются.
//
Процедура УстановитьСвойство(Объект, ИмяСвойства, ЗначениеСвойства, ОбязательнаяЗаменаСтарогоЗначения = Ложь) Экспорт
	
	Если ЗначениеСвойства <> Неопределено И Объект[ИмяСвойства] <> ЗначениеСвойства Тогда
		Если (Не ЗначениеЗаполнено(Объект[ИмяСвойства]) Или ОбязательнаяЗаменаСтарогоЗначения)
			Или ТипЗнч(Объект[ИмяСвойства]) = Тип("Булево")
			Или ТипЗнч(Объект[ИмяСвойства]) = Тип("Дата") Тогда
			
			Объект[ИмяСвойства] = ЗначениеСвойства;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет строку в табличную часть "Расшифровка платежа" по объекту расчета.
// 
// Параметры:
//  СтрокаРасшифровки - СтрокаТабличнойЧасти - строка расшифровки платежа.
//  ДанныеПлатежа - СтрокаТаблицыЗначений - данные распознанного платежа.
//  ДокументОбъект - ДокументОбъект.СписаниеБезналичныхДенежныхСредств, ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств - Документ заполнения.
//  ДанныеОбъектаРасчета - СтрокаТаблицыЗначений - данные объекта расчета по таблице остатков.
//  ПараметрыЗаполнения - Структура - Параметры заполнения табличной части.
//
Процедура ЗаполнитьСтрокуРасшифровкиПлатежаПоОбъектуРасчета(
		СтрокаРасшифровки, ДанныеПлатежа, ДокументОбъект, ДанныеОбъектаРасчета, ПараметрыЗаполнения) Экспорт

	Если СтрокаРасшифровки = Неопределено Тогда
		СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
	КонецЕсли;
	
	СуммаСтроки = Мин(ПараметрыЗаполнения.РаспределяемаяСумма, ДанныеОбъектаРасчета.Сумма);
	ПараметрыЗаполнения.РаспределяемаяСумма = ПараметрыЗаполнения.РаспределяемаяСумма - ДанныеОбъектаРасчета.Сумма;
	
	ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, ДанныеОбъектаРасчета, , "Сумма, СтатьяДвиженияДенежныхСредств");
	
	// В случае возврата статья ДДС объекта расчетов не подходит для такой хозяйственной операции
	Если ПараметрыЗаполнения.ДоступныеСтатьиДДС.Найти(ДанныеОбъектаРасчета.СтатьяДвиженияДенежныхСредств) <> Неопределено Тогда
		СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = ДанныеОбъектаРасчета.СтатьяДвиженияДенежныхСредств;
		
	// Если статья ДДС объекта расчетов не соответствует хозяйственной операции, подобранной в документ, то
	// статья ДДС заполняется по найденному образцу, если образца не было, то по хозяйственной операции
	Иначе
		СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = ДанныеПлатежа.СтатьяДвиженияДенежныхСредств;
	КонецЕсли;
	
	СтрокаРасшифровки.Сумма = СуммаСтроки;
	
	РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
		СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
		СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
		ДокументОбъект.Валюта,
		СтрокаРасшифровки.ВалютаВзаиморасчетов,
		ДокументОбъект.Организация,
		?(ЗначениеЗаполнено(ДанныеПлатежа.ДатаПроведения), ДанныеПлатежа.ДатаПроведения, ДокументОбъект.Дата),,
		СтрокаРасшифровки.ОбъектРасчетов);
	
КонецПроцедуры

// Заполняет строку в табличную часть "Расшифровка платежа" по данным распознанного платежа из выписки.
// 
// Параметры:
//  СтрокаРасшифровки - СтрокаТабличнойЧасти - строка расшифровки платежа.
//  ДанныеПлатежа - СтрокаТаблицыЗначений - данные распознанного платежа.
//  ДокументОбъект - ДокументОбъект.СписаниеБезналичныхДенежныхСредств, ДокументОбъект.ПоступлениеБезналичныхДенежныхСредств - Документ заполнения.
//  ПараметрыЗаполнения - Структура - Параметры заполнения табличной части.
//
Процедура ЗаполнитьСтрокуРасшифровкиПлатежаПоПлатежу(
		СтрокаРасшифровки, ДанныеПлатежа, ДокументОбъект, ПараметрыЗаполнения) Экспорт

	Если СтрокаРасшифровки = Неопределено Тогда
		СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, ДанныеПлатежа, ПараметрыЗаполнения.ЗаполняемыеРеквизиты);
	ЗаполнитьРасшифровкуПлатежаПоОбъектуРасчетов(СтрокаРасшифровки);
	
	ЗаполнитьСтавкуНДСПоДоговору(ДокументОбъект, СтрокаРасшифровки);
	
	СтрокаРасшифровки.Сумма = ПараметрыЗаполнения.РаспределяемаяСумма;
	РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
		СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
		СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
		ДокументОбъект.Валюта,
		СтрокаРасшифровки.ВалютаВзаиморасчетов,
		ДокументОбъект.Организация,
		?(ЗначениеЗаполнено(ДанныеПлатежа.ДатаПроведения), ДанныеПлатежа.ДатаПроведения, ДокументОбъект.Дата),,
		СтрокаРасшифровки.ОбъектРасчетов);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьРасшифровкуПлатежаПоОбъектуРасчетов(ДанныеРасшифровки)

	Если Не ЗначениеЗаполнено(ДанныеРасшифровки.ОбъектРасчетов) Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыОбъектаРасчетов = "ВалютаВзаиморасчетов, НаправлениеДеятельности, Организация, Подразделение";
	ЗаполнитьЗначенияСвойств(ДанныеРасшифровки, ДанныеРасшифровки.ОбъектРасчетов, РеквизитыОбъектаРасчетов);

КонецПроцедуры

Процедура ЗаполнитьРеквизитыСписания(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, НастройкиЗагрузки, Префикс)
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	СоздаватьКонтрагентов = НастройкиЗагрузки.СоздаватьКонтрагентов;
	
	// Контроль заявок
	Если Не (ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ИнкассовоеПоручение
		Или ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.ПлатежныйОрдер
		Или ДокументОбъект.ТипПлатежногоДокумента = Перечисления.ТипыПлатежныхДокументов.БанковскийОрдер) Тогда
		ДокументОбъект.ОплатаПоЗаявкам = Не ДенежныеСредстваСервер.РазрешеныПлатежиБезУказанияЗаявок(ДокументОбъект);
	Иначе
		КлиентБанкЛокализация.РазрешитьОплатуБезЗаявки(ДокументОбъект);
	КонецЕсли;
	
	КлиентБанкЛокализация.ЗаполнитьРеквизитыПлательщика(ДокументОбъект);
	
	Документы.СписаниеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
	
	// Контрагент
	Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
		ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
		Иначе
			УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	// Счет контрагента
	Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
		КонецЕсли;
	КонецЕсли;
	
	// Подотчетное лицо
	Если РеквизитыХозОперации.Найти("ПодотчетноеЛицо") <> Неопределено Тогда
		Если СтрокаДокумента.НайденКонтрагент Тогда
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.Контрагент);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.ПодотчетноеЛицо);
		КонецЕсли;
	КонецЕсли;
	
	Если РеквизитыХозОперации.Найти("ДатаАвансовогоОтчета") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "ДатаАвансовогоОтчета",
			?(ЗначениеЗаполнено(ДокументОбъект.Дата), ДокументОбъект.Дата, ТекущаяДатаСеанса()) + 7 * 24 * 3600);
	КонецЕсли;
	
	// Банковский счет получатель
	Если РеквизитыХозОперации.Найти("БанковскийСчетПолучатель") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "БанковскийСчетПолучатель", СтрокаДокумента.СчетКонтрагента);
	КонецЕсли;
	
	// Прочие реквизиты
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтрокаДокумента,
		"СтатьяДвиженияДенежныхСредств, Подразделение, КассаПолучатель");
	
	КлиентБанкЛокализация.ЗаполнитьРеквизитыСписания(СтрокаДокумента, ДокументОбъект);
	
	// Расшифровка платежа
	ЗаполнитьРасшифровкуСписания(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки);
	
КонецПроцедуры

Процедура ЗаполнитьРеквизитыПоступления(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект, НастройкиЗагрузки)
	
	Перем РеквизитыВсе, РеквизитыХозОперации;
	
	СоздаватьКонтрагентов = НастройкиЗагрузки.СоздаватьКонтрагентов;
	ПроводитьДокументы = НастройкиЗагрузки.ПроводитьДокументы;
	
	Документы.ПоступлениеБезналичныхДенежныхСредств.ЗаполнитьИменаРеквизитовПоХозяйственнойОперации(
		ДокументОбъект, РеквизитыВсе, РеквизитыХозОперации);
	
	ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
	
	// Контрагент
	Если РеквизитыХозОперации.Найти("Контрагент") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СтрокаДокумента.Контрагент);
		ИначеЕсли СоздаватьКонтрагентов И СтрокаДокумента.СоздаватьКонтрагента Тогда
			УстановитьСвойство(ДокументОбъект, "Контрагент", СоздатьКонтрагента(ДокументыКЗагрузке, СтрокаДокумента));
		Иначе
			УстановитьСвойство(ДокументОбъект, "ИмяКонтрагента", СтрокаДокумента.ИмяКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	// Счет контрагента
	Если РеквизитыХозОперации.Найти("БанковскийСчетКонтрагента") <> Неопределено Тогда
		Если ЗначениеЗаполнено(СтрокаДокумента.СчетКонтрагента) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		ИначеЕсли ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
			УстановитьСвойство(ДокументОбъект, "БанковскийСчетКонтрагента",
				СоздатьБанковскийСчетКонтрагента(ДокументыКЗагрузке, СтрокаДокумента, ДокументОбъект.Контрагент));
		КонецЕсли;
	КонецЕсли;
	
	// Подотчетное лицо
	Если РеквизитыХозОперации.Найти("ПодотчетноеЛицо") <> Неопределено Тогда
		Если СтрокаДокумента.НайденКонтрагент Тогда
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.Контрагент);
		Иначе
			УстановитьСвойство(ДокументОбъект, "ПодотчетноеЛицо", СтрокаДокумента.ПодотчетноеЛицо);
		КонецЕсли;
	КонецЕсли;
	
	// Банковский счет отправитель
	Если РеквизитыХозОперации.Найти("БанковскийСчетОтправитель") <> Неопределено Тогда
		УстановитьСвойство(ДокументОбъект, "БанковскийСчетОтправитель", СтрокаДокумента.СчетКонтрагента);
	КонецЕсли;
	
	// Прочие реквизиты
	ЗаполнитьЗначенияСвойств(ДокументОбъект, СтрокаДокумента,
		"СтатьяДвиженияДенежныхСредств, Подразделение, КассаОтправитель");
	
	// Поступление оплаты по эквайрингу
	Если РеквизитыХозОперации.Найти("ДоговорЭквайринга") <> Неопределено Тогда
		
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ДоговорЭквайринга) Тогда
			
			СпособПроведенияПлатежа = Неопределено;
			
			Если СтрокаДокумента.ЭтоСБП Тогда
				СпособПроведенияПлатежа = Перечисления.СпособыПроведенияПлатежей.СистемаБыстрыхПлатежей;
			КонецЕсли;
			
			ПараметрыОтбора = Справочники.ДоговорыЭквайринга.ПараметрыОтбораДоговораЭквайринга();
		
			ПараметрыОтбора.Организация = ДокументОбъект.Организация;
			ПараметрыОтбора.Эквайер = ДокументОбъект.Контрагент;
			ПараметрыОтбора.БанковскийСчет = ДокументОбъект.БанковскийСчет;
			ПараметрыОтбора.СпособПроведенияПлатежа = СпособПроведенияПлатежа;
			
			СтрокаДокумента.ДоговорЭквайринга = Справочники.ДоговорыЭквайринга.ДоговорЭквайрингаПоУмолчанию(ПараметрыОтбора);
			
		КонецЕсли;
		
		ЗаполнитьПоДоговоруПодключения(СтрокаДокумента, ДокументОбъект);
		
	КонецЕсли;
	
	КлиентБанкЛокализация.ЗаполнитьРеквизитыПоступления(СтрокаДокумента, ДокументОбъект);
	
	// Расшифровка платежа
	ЗаполнитьРасшифровкуПоступления(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки);
	
	Если СтрокаДокумента.СуммаКонвертации <> 0 Тогда
		
		УстановитьСвойство(ДокументОбъект, "СуммаКонвертации", СтрокаДокумента.СуммаКонвертации, Истина);
		ДенежныеСредстваСервер.ПересчитатьПараметрыКонвертации(ДокументОбъект, ВалютаРеглУчета, "СуммаКонвертации");
		
	КонецЕсли;
	
	КлиентБанкЛокализация.ЗаполнитьКодВалютнойОперации(ДокументОбъект);
	
КонецПроцедуры

Процедура ЗаполнитьРасшифровкуСписания(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки)

	ПроводитьДокументы = НастройкиЗагрузки.ПроводитьДокументы;
	
	Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
		
		Если СтрокаДокумента.НайденоОснованиеПлатежа Тогда // УИП
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			
			ЗаполняемыеРеквизиты = "Контрагент, Партнер, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств";
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента, ЗаполняемыеРеквизиты);
			
			ЗаполнитьРасшифровкуПлатежаПоОбъектуРасчетов(СтрокаРасшифровки);
			КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
			
			СтрокаРасшифровки.Сумма = ДокументОбъект.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата),,
				СтрокаРасшифровки.ОбъектРасчетов);
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Истина));
			
			//++ Локализация
			УстановитьСвойство(ДокументОбъект, "ОперацияССамозанятым", СтрокаДокумента.ДоговорССамозанятым);
			//-- Локализация
			
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ДоговорКредитаДепозита") <> Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			
			// Кредит или заем полученный: оплата основоного долга, процента или комиссии
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДанныеРегистра.ОбъектОплаты КАК ДоговорКредитаДепозита,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).Партнер КАК Партнер,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).БанковскийСчетПроцентов КАК БанковскийСчетПроцентов,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).БанковскийСчетКомиссии КАК БанковскийСчетКомиссии,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСОсновногоДолга КАК СтатьяДДСОсновногоДолга,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСПроцентов КАК СтатьяДДСПроцентов,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСКомиссии КАК СтатьяДДСКомиссии
			|ИЗ
			|	РегистрСведений.ГрафикПлатежей КАК ДанныеРегистра
			|ГДЕ
			|	ДанныеРегистра.БанковскийСчетКасса = &БанковскийСчет
			|	И ДанныеРегистра.ПлательщикПолучатель = &Контрагент
			|	И ДанныеРегистра.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Списание)
			|	И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоКредитам)
			|СГРУППИРОВАТЬ ПО
			|	ДанныеРегистра.ОбъектОплаты
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеРегистра.ОбъектОплаты) = 1
			|";
			
			Запрос.УстановитьПараметр("БанковскийСчет", ДокументОбъект.БанковскийСчет);
			Запрос.УстановитьПараметр("Контрагент", ДокументОбъект.Контрагент);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, Выборка);
				
				Если ДокументОбъект.БанковскийСчетКонтрагента = Выборка.БанковскийСчетПроцентов Тогда
					СтрокаРасшифровки.ТипСуммыКредитаДепозита = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.Проценты;
					СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДСПроцентов;
				ИначеЕсли ДокументОбъект.БанковскийСчетКонтрагента = Выборка.БанковскийСчетКомиссии Тогда
					СтрокаРасшифровки.ТипСуммыКредитаДепозита = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.Комиссия;
					СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДСКомиссии;
				Иначе
					СтрокаРасшифровки.ТипСуммыКредитаДепозита = Перечисления.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг;
					СтрокаРасшифровки.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДСОсновногоДолга;
				КонецЕсли;
				
			Иначе
				
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДоговорКредитаДепозита) Тогда
					
					СтрокиКЗагрузке = Новый Массив;
					СтрокиКЗагрузке.Добавить(СтрокаДокумента);
					ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, ДокументОбъект.БанковскийСчет);
					
				КонецЕсли;

				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита, ТипСуммыКредитаДепозита, ВалютаВзаиморасчетов");
				
			КонецЕсли;
			
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата));
			ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
			ДенежныеСредстваКлиентСервер.РассчитатьСуммуВзаиморасчетовВСтрокеРасшифровки(
				СтрокаРасшифровки, ДокументОбъект.Валюта, ВалютаРеглУчета);
		
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ОбъектРасчетов") = Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, СтатьяДвиженияДенежныхСредств, СтатьяРасходов, АналитикаРасходов, АналитикаАктивовПассивов, Подразделение");
			КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата));
		
		Иначе
			
			Реквизиты = Новый Структура;
			Реквизиты.Вставить("Дата", ДокументОбъект.Дата);
			Реквизиты.Вставить("Организация", ДокументОбъект.Организация);
			Реквизиты.Вставить("СуммаДокумента", 0);
			Реквизиты.Вставить("Валюта", ДокументОбъект.Валюта);
			
			Если ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
				Реквизиты.Вставить("Контрагент", ДокументОбъект.Контрагент);
			Иначе
				Реквизиты.Вставить("Контрагент", ДокументОбъект.ИмяКонтрагента);
			КонецЕсли;
			
			Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Реквизиты.Вставить("Партнер", Справочники.Партнеры.НашеПредприятие);
			Иначе
				Реквизиты.Вставить("Партнер", Неопределено);
			КонецЕсли;
			
			Реквизиты.Вставить("ХозяйственнаяОперация", ДокументОбъект.ХозяйственнаяОперация);
			
			Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту
				Или ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию Тогда
				Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
			Иначе
				Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
			КонецЕсли;
			
			Реквизиты.Вставить("ПартнерПрочиеОтношения", Ложь);
			Реквизиты.Вставить("ПодборДебиторскойЗадолженности", Ложь);
			Реквизиты.Вставить("ПодборТолькоБезусловнойЗадолженности", Ложь);
			Реквизиты.Вставить("ПоДатамПлатежей", Истина);
			Реквизиты.Вставить("НаДатуВозникновения", ДокументОбъект.Дата);
			
			//++ Локализация

			//++ НЕ УТ
			Реквизиты.Вставить("ПлатежиПо275ФЗ", Ложь);
			Реквизиты.Вставить("ДоговорСУчастникомГОЗ", Ложь);
			//-- НЕ УТ

			ОперацияССамозанятым = Неопределено;

			//-- Локализация
			
			АдресПлатежейВХранилище = "";
			ТаблицаОстатковРасчетов = Неопределено;
			
			ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов(Реквизиты, АдресПлатежейВХранилище, ТаблицаОстатковРасчетов);
			КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки; // КоллекцияКолонокТаблицыЗначений 
			КолонкиТаблицыОстатковВзаиморасчетов.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
			
			РаспределяемаяСумма = ДокументОбъект.СуммаДокумента;
			ДоступныеСтатьиДДС = ДенежныеСредстваПовтИсп.СтатьиХозяйственнойОперации(ДокументОбъект.ХозяйственнаяОперация);
			
			ПараметрыДобавленияСтроки = Новый Структура;
			ПараметрыДобавленияСтроки.Вставить("РаспределяемаяСумма", РаспределяемаяСумма);
			ПараметрыДобавленияСтроки.Вставить("ДоступныеСтатьиДДС", ДоступныеСтатьиДДС);
			
			НомераОбъектовПоНазначению = КлиентБанкЛокализация.ОбъектыРасчетовНазначенияПлатежа(СтрокаДокумента.НазначениеПлатежа);
			КлиентБанкЛокализация.ДобавитьПараметрыЗаполненияРасшифровки(НастройкиЗагрузки, ПараметрыДобавленияСтроки);
			
			Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
				
				Если ТипЗнч(СтрокаТаблицы.ОбъектРасчетов) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
					
					НомерДокументаРасчетов = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТаблицы.Номер, Истина, Истина);
					
					Если КлиентБанкЛокализация.ЕстьОбъектРасчетаВНазначении(СтрокаДокумента.НазначениеПлатежа, НомераОбъектовПоНазначению, НомерДокументаРасчетов) Тогда
						
						КлиентБанкЛокализация.ДобавитьСтрокуРасшифровкиПлатежаПоОбъектуРасчета(
							СтрокаДокумента, ДокументОбъект, СтрокаТаблицы, ПараметрыДобавленияСтроки);
						СтрокаТаблицы.Обработано = Истина;
						
						Если ПараметрыДобавленияСтроки.РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ПроводитьДокументы И ПараметрыДобавленияСтроки.РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
				
				Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
					
					Если ЗначениеЗаполнено(СтрокаТаблицы.ОбъектРасчетов) Тогда
						
						Если СтрокаТаблицы.Обработано Тогда
							Продолжить;
						КонецЕсли;
						
						КлиентБанкЛокализация.ДобавитьСтрокуРасшифровкиПлатежаПоОбъектуРасчета(
							СтрокаДокумента, ДокументОбъект, СтрокаТаблицы, ПараметрыДобавленияСтроки);
						СтрокаТаблицы.Обработано = Истина;
						
						Если ПараметрыДобавленияСтроки.РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если ПараметрыДобавленияСтроки.РаспределяемаяСумма > 0 Или Не ДокументОбъект.РасшифровкаПлатежа.Количество() Тогда
				
				ЗаполняемыеРеквизиты = "Контрагент, Партнер, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств";
				ПараметрыДобавленияСтроки.Вставить("ЗаполняемыеРеквизиты", ЗаполняемыеРеквизиты);
				КлиентБанкЛокализация.ДобавитьСтрокуРасшифровкиПлатежаПоПлатежу(
					СтрокаДокумента, ДокументОбъект, ПараметрыДобавленияСтроки);
				
			КонецЕсли;
			
			ДокументОбъект.РасшифровкаПлатежа.Свернуть(
				"Контрагент, Партнер, СтатьяДвиженияДенежныхСредств, ВалютаВзаиморасчетов, Подразделение, Организация,
				|ОбъектРасчетов, КурсЧислительВзаиморасчетов, КурсЗнаменательВзаиморасчетов, СтатьяЦелевыхСредств, ДатаПогашения,
				|НаправлениеДеятельности, СтавкаНДС, НастройкаСчетовУчета",
				"Сумма, СуммаВзаиморасчетов, СуммаНДС");
			
			//++ Локализация
			УстановитьСвойство(ДокументОбъект, "ОперацияССамозанятым", ?(ПараметрыДобавленияСтроки.ОперацияССамозанятым = Неопределено, Ложь, ПараметрыДобавленияСтроки.ОперацияССамозанятым));
			//-- Локализация
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Ложь));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьРасшифровкуПоступления(СтрокаДокумента, ДокументОбъект, РеквизитыХозОперации, НастройкиЗагрузки)

	ПроводитьДокументы = НастройкиЗагрузки.ПроводитьДокументы;
	
	Если РеквизитыХозОперации.Найти("РасшифровкаПлатежа") <> Неопределено Тогда
		
		ВалютаРеглУчета = ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ДокументОбъект.Организация);
		
		Если СтрокаДокумента.НайденоОснованиеПлатежа Тогда // УИП
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, ОснованиеПлатежа, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств");
			ЗаполнитьРасшифровкуПлатежаПоОбъектуРасчетов(СтрокаРасшифровки);
			КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
			СтрокаРасшифровки.Сумма = ДокументОбъект.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата),,
				СтрокаРасшифровки.ОбъектРасчетов);
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Истина));
					
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ДоговорКредитаДепозита") <> Неопределено Тогда
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			
			// Кредит или заем полученный: поступление кредита
			Запрос = Новый Запрос;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ДанныеРегистра.ОбъектОплаты КАК ДоговорКредитаДепозита,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).Партнер КАК Партнер,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).СтатьяДДСПоступленияВыдачи КАК СтатьяДвиженияДенежныхСредств,
			|	ВЫРАЗИТЬ(ДанныеРегистра.ОбъектОплаты КАК Справочник.ДоговорыКредитовИДепозитов).ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
			|ИЗ
			|	РегистрСведений.ГрафикПлатежей КАК ДанныеРегистра
			|ГДЕ
			|	ДанныеРегистра.БанковскийСчетКасса = &БанковскийСчет
			|	И ДанныеРегистра.ПлательщикПолучатель = &Контрагент
			|	И ДанныеРегистра.ПоступлениеСписание = ЗНАЧЕНИЕ(Перечисление.ТипыДвиженияДенежныхСредств.Поступление)
			|	И ДанныеРегистра.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам)
			|СГРУППИРОВАТЬ ПО
			|	ДанныеРегистра.ОбъектОплаты
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДанныеРегистра.ОбъектОплаты) = 1
			|";
			
			Запрос.УстановитьПараметр("БанковскийСчет", ДокументОбъект.БанковскийСчет);
			Запрос.УстановитьПараметр("Контрагент", ДокументОбъект.Контрагент);
			
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Следующий() Тогда
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, Выборка);
			Иначе
				
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ДоговорКредитаДепозита) Тогда
					
					СтрокиКЗагрузке = Новый Массив;
					СтрокиКЗагрузке.Добавить(СтрокаДокумента);
					ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, ДокументОбъект.БанковскийСчет);
					
				КонецЕсли;
				
				ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
					"Партнер, СтатьяДвиженияДенежныхСредств, ДоговорКредитаДепозита, ТипСуммыКредитаДепозита, ВалютаВзаиморасчетов");
				
			КонецЕсли;
			
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата));
			ДенежныеСредстваКлиентСервер.РассчитатьСуммуВзаиморасчетовВСтрокеРасшифровки(
				СтрокаРасшифровки, ДокументОбъект.Валюта, ВалютаРеглУчета);
			
		ИначеЕсли РеквизитыХозОперации.Найти("РасшифровкаПлатежа.ОбъектРасчетов") = Неопределено Тогда // Прочие платежи
			
			СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРасшифровки, СтрокаДокумента,
				"Партнер, СтатьяДвиженияДенежныхСредств, СтатьяДоходов, АналитикаДоходов, АналитикаАктивовПассивов, Подразделение");
			КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
			СтрокаРасшифровки.Сумма = СтрокаДокумента.СуммаДокумента;
			РаботаСКурсамиВалютУТ.ЗаполнитьКурсДокументаПоУмолчанию(
				СтрокаРасшифровки.КурсЧислительВзаиморасчетов,
				СтрокаРасшифровки.КурсЗнаменательВзаиморасчетов,
				ДокументОбъект.Валюта,
				СтрокаРасшифровки.ВалютаВзаиморасчетов,
				ДокументОбъект.Организация,
				?(ЗначениеЗаполнено(СтрокаДокумента.ДатаПроведения), СтрокаДокумента.ДатаПроведения, ДокументОбъект.Дата));
		
		Иначе
			
			Реквизиты = Новый Структура;
			Реквизиты.Вставить("Дата", ДокументОбъект.Дата);
			Реквизиты.Вставить("Организация", ДокументОбъект.Организация);
			Реквизиты.Вставить("СуммаДокумента", 0);
			Реквизиты.Вставить("Валюта", ДокументОбъект.Валюта);
			
			Если ЗначениеЗаполнено(ДокументОбъект.Контрагент) Тогда
				Реквизиты.Вставить("Контрагент", ДокументОбъект.Контрагент);
			Иначе
				Реквизиты.Вставить("Контрагент", ДокументОбъект.ИмяКонтрагента);
			КонецЕсли;
			
			Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации") Тогда
				Реквизиты.Вставить("Партнер", Справочники.Партнеры.НашеПредприятие);
			Иначе
				Реквизиты.Вставить("Партнер", Неопределено);
			КонецЕсли;
			
			Реквизиты.Вставить("ХозяйственнаяОперация", ДокументОбъект.ХозяйственнаяОперация);
			
			Если ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика
				Или ДокументОбъект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации Тогда
				Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком);
			Иначе
				Реквизиты.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
			КонецЕсли;
			
			Реквизиты.Вставить("ПартнерПрочиеОтношения", Ложь);
			Реквизиты.Вставить("ПодборДебиторскойЗадолженности", Истина);
			Реквизиты.Вставить("ПодборТолькоБезусловнойЗадолженности", Ложь);
			Реквизиты.Вставить("ПоДатамПлатежей", Истина);
			Реквизиты.Вставить("НаДатуВозникновения", ДокументОбъект.Дата);
			
			//++ Локализация

			//++ НЕ УТ
			Реквизиты.Вставить("ПлатежиПо275ФЗ", Ложь);
			Реквизиты.Вставить("ДоговорСУчастникомГОЗ", Ложь);
			//-- НЕ УТ

			//-- Локализация
			
			АдресПлатежейВХранилище = "";
			ТаблицаОстатковРасчетов = Неопределено;
			
			ВзаиморасчетыСервер.ЗаполнитьТаблицуОстатковРасчетов(Реквизиты, АдресПлатежейВХранилище, ТаблицаОстатковРасчетов);
			КолонкиТаблицыОстатковВзаиморасчетов = ТаблицаОстатковРасчетов.Колонки; // КоллекцияКолонокТаблицыЗначений
			КолонкиТаблицыОстатковВзаиморасчетов.Добавить("Обработано", Новый ОписаниеТипов("Булево"));
			КолонкиТаблицыОстатковВзаиморасчетов.Объект.Имя = "ОснованиеПлатежа";
			
			РаспределяемаяСумма = ДокументОбъект.СуммаДокумента;
			ДоступныеСтатьиДДС = ДенежныеСредстваПовтИсп.СтатьиХозяйственнойОперации(ДокументОбъект.ХозяйственнаяОперация);
			
			ПараметрыДобавленияСтроки = Новый Структура;
			ПараметрыДобавленияСтроки.Вставить("РаспределяемаяСумма", РаспределяемаяСумма);
			ПараметрыДобавленияСтроки.Вставить("ДоступныеСтатьиДДС", ДоступныеСтатьиДДС);
			
			НомераОбъектовПоНазначению = КлиентБанкЛокализация.ОбъектыРасчетовНазначенияПлатежа(СтрокаДокумента.НазначениеПлатежа);
			
			Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
				
				Если ТипЗнч(СтрокаТаблицы.ОбъектРасчетов) = Тип("СправочникСсылка.ОбъектыРасчетов") Тогда
					
					НомерДокументаРасчетов = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(СтрокаТаблицы.Номер, Истина, Истина);
					
					Если КлиентБанкЛокализация.ЕстьОбъектРасчетаВНазначении(СтрокаДокумента.НазначениеПлатежа, НомераОбъектовПоНазначению, НомерДокументаРасчетов) Тогда
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						ЗаполнитьСтрокуРасшифровкиПлатежаПоОбъектуРасчета(
							СтрокаРасшифровки, СтрокаДокумента, ДокументОбъект, СтрокаТаблицы, ПараметрыДобавленияСтроки);
						КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если ПараметрыДобавленияСтроки.РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЦикла;
			
			Если ПроводитьДокументы И ПараметрыДобавленияСтроки.РаспределяемаяСумма > 0 Тогда // Подбор объектов расчетов
				
				Для Каждого СтрокаТаблицы Из ТаблицаОстатковРасчетов Цикл
					
					Если ЗначениеЗаполнено(СтрокаТаблицы.ОбъектРасчетов) Тогда
						
						Если СтрокаТаблицы.Обработано Тогда
							Продолжить;
						КонецЕсли;
						
						СтрокаРасшифровки = ДокументОбъект.РасшифровкаПлатежа.Добавить();
						ЗаполнитьСтрокуРасшифровкиПлатежаПоОбъектуРасчета(
							СтрокаРасшифровки, СтрокаДокумента, ДокументОбъект, СтрокаТаблицы, ПараметрыДобавленияСтроки);
						КлиентБанкЛокализация.ЗаполнитьСтатьюЦелевыхСредствРасшифровкиПлатежа(СтрокаДокумента, СтрокаРасшифровки);
						
						СтрокаТаблицы.Обработано = Истина;
						
						Если ПараметрыДобавленияСтроки.РаспределяемаяСумма <= 0 Тогда
							Прервать;
						КонецЕсли;
						
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			
			Если ПараметрыДобавленияСтроки.РаспределяемаяСумма > 0 Или Не ДокументОбъект.РасшифровкаПлатежа.Количество() Тогда
				
				ЗаполняемыеРеквизиты = "Партнер, ОснованиеПлатежа, ОбъектРасчетов, СтатьяДвиженияДенежныхСредств";
				ПараметрыДобавленияСтроки.Вставить("ЗаполняемыеРеквизиты", ЗаполняемыеРеквизиты);
				КлиентБанкЛокализация.ДобавитьСтрокуРасшифровкиПлатежаПоПлатежу(
					СтрокаДокумента, ДокументОбъект, ПараметрыДобавленияСтроки);
				
			КонецЕсли;
			
			ДокументОбъект.РасшифровкаПлатежа.Свернуть(
				"ОснованиеПлатежа, Партнер, СтатьяДвиженияДенежныхСредств, ВалютаВзаиморасчетов, Подразделение, Организация,
				|ОбъектРасчетов, КурсЧислительВзаиморасчетов, КурсЗнаменательВзаиморасчетов, СтатьяЦелевыхСредств, ДатаПогашения,
				|НаправлениеДеятельности, СтавкаНДС, НастройкаСчетовУчета",
				"Сумма, СуммаВзаиморасчетов, СуммаНДС");
			
			ДенежныеСредстваСервер.ЗаполнитьНДСВРасшифровке(
				ДокументОбъект.РасшифровкаПлатежа,
				ДенежныеСредстваСервер.РасшифровкаПлатежаНДС(ДокументОбъект.Организация,
					ДокументОбъект.Дата, ДокументОбъект.Валюта, ДокументОбъект.РасшифровкаПлатежа.ВыгрузитьКолонку("ОбъектРасчетов"), Истина));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция РеквизитыЗаполненияКонтрагента()

	РеквизитыКонтрагента = Новый Структура;
	РеквизитыКонтрагента.Вставить("Наименование", "");
	РеквизитыКонтрагента.Вставить("НаименованиеПолное", "");
	РеквизитыКонтрагента.Вставить("НаименованиеМеждународное", "");
	РеквизитыКонтрагента.Вставить("ИНН", "");
	РеквизитыКонтрагента.Вставить("КПП", "");
	РеквизитыКонтрагента.Вставить("СтранаРегистрации", Неопределено);
	РеквизитыКонтрагента.Вставить("ЮрФизЛицо", Неопределено);
	РеквизитыКонтрагента.Вставить("Партнер", Неопределено);
	
	Возврат РеквизитыКонтрагента;

КонецФункции

Функция РеквизитыЗаполненияПартнера()

	РеквизитыПартнера = Новый Структура;
	РеквизитыПартнера.Вставить("Наименование", "");
	РеквизитыПартнера.Вставить("НаименованиеПолное", "");
	РеквизитыПартнера.Вставить("Клиент", Истина);
	РеквизитыПартнера.Вставить("Поставщик", Истина);
	РеквизитыПартнера.Вставить("ПрочиеОтношения", Истина);
	РеквизитыПартнера.Вставить("ЮрФизЛицо", Перечисления.КомпанияЧастноеЛицо.Компания);
	
	Возврат РеквизитыПартнера;

КонецФункции

Функция СоздатьПартнера(Отбор)

	НовыйПартнер = Справочники.Партнеры.СоздатьЭлемент();
	ЗаполнитьЗначенияСвойств(НовыйПартнер, Отбор);
	НовыйПартнер.ДатаРегистрации = ТекущаяДатаСеанса();
	НовыйПартнер.Комментарий = НСтр("ru = '#Создан автоматически при импорте банковских документов';
									|en = '#Created automatically while importing bank documents'");
	НовыйПартнер.Записать();
	
	Результат = НовыйПартнер.Ссылка;
	
	Возврат Результат;

КонецФункции

Функция НайтиСоздатьКонтрагента(ИсходныеДанные, Отбор)

	Результат = СсылкаИзТаблицыСОтбором("Справочник.Контрагенты", Отбор);
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
		
		НовыйКонтрагент = Справочники.Контрагенты.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НовыйКонтрагент, Отбор);
		НовыйКонтрагент.Записать();
		
		Результат = НовыйКонтрагент.Ссылка;
		
		ДатаДействия = ?(ЗначениеЗаполнено(ИсходныеДанные.ДатаПроведения), ИсходныеДанные.ДатаПроведения, ИсходныеДанные.Дата);
		ЗаполнитьАдресКонтрагента(ДатаДействия, Результат, Отбор.ЮрФизЛицо, ИсходныеДанные.АдресКонтрагента);
		
		КлиентБанкЛокализация.ЗапуститьЗаполнениеДанныхКонтрагента(Результат, Отбор, ДатаДействия);
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Процедура ЗаполнитьКонтрагентаВДокументахЗагрузки(Контрагент, Партнер, ДокументыКЗагрузке, Отбор)

	ЗаписиКонтрагента = ДокументыКЗагрузке.НайтиСтроки(Отбор);
	
	Для каждого ЗаписьКонтрагента Из ЗаписиКонтрагента Цикл
		ЗаписьКонтрагента.Контрагент = Контрагент;
		ЗаписьКонтрагента.Партнер = Партнер;
	КонецЦикла;

КонецПроцедуры

Функция РеквизитыБанковскогоСчета()

	РеквизитыСчета = Новый Структура;
	РеквизитыСчета.Вставить("Владелец", Неопределено);
	РеквизитыСчета.Вставить("ВалютаДенежныхСредств", Неопределено);
	РеквизитыСчета.Вставить("НомерСчета", "");
	РеквизитыСчета.Вставить("ИностранныйБанк", Ложь);
	РеквизитыСчета.Вставить("СтранаБанка", Справочники.СтраныМира.ПустаяСсылка());
	РеквизитыСчета.Вставить("Банк", Неопределено);
	РеквизитыСчета.Вставить("РучноеИзменениеРеквизитовБанка", Ложь);
	РеквизитыСчета.Вставить("БИКБанка", "");
	РеквизитыСчета.Вставить("СВИФТБанка", "");
	РеквизитыСчета.Вставить("НаименованиеБанка", "");
	РеквизитыСчета.Вставить("ГородБанка", "");
	РеквизитыСчета.Вставить("КоррСчетБанка", "");
	РеквизитыСчета.Вставить("БанкДляРасчетов", "");
	РеквизитыСчета.Вставить("РучноеИзменениеРеквизитовБанкаДляРасчетов", Ложь);
	РеквизитыСчета.Вставить("НаименованиеБанкаДляРасчетов", "");
	РеквизитыСчета.Вставить("ГородБанкаДляРасчетов", "");
	РеквизитыСчета.Вставить("БИКБанкаДляРасчетов", "");
	РеквизитыСчета.Вставить("КоррСчетБанкаДляРасчетов", "");
	РеквизитыСчета.Вставить("Наименование", "");
	
	Возврат РеквизитыСчета;

КонецФункции

Функция СсылкаИзТаблицыСОтбором(ИмяТаблицы, ПараметрыЗапроса)

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Таблица.Ссылка КАК Ссылка
		|ИЗ
		|	&ИмяТаблицы КАК Таблица
		|ГДЕ
		|	НЕ Таблица.ПометкаУдаления
		|	И &Условия";
	
	СписокУсловий = Новый Массив;
	
	Для Каждого ТекущийПараметр Из ПараметрыЗапроса Цикл
		
		СписокУсловий.Добавить(СтрШаблон("Таблица.%1 = &%1", ТекущийПараметр.Ключ));
		Запрос.УстановитьПараметр(ТекущийПараметр.Ключ, ТекущийПараметр.Значение);
		
	КонецЦикла;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ИмяТаблицы);
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условия", СтрСоединить(СписокУсловий, " И ")); //@Query-part
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

Функция НайтиСоздатьБанковскийСчетКонтрагента(Отбор)

	Результат = СсылкаИзТаблицыСОтбором("Справочник.БанковскиеСчетаКонтрагентов", Отбор);
	
	Если Не ЗначениеЗаполнено(Результат) Тогда
	
		НовыйСчет = Справочники.БанковскиеСчетаКонтрагентов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(НовыйСчет, Отбор);
		
		НовыйСчет.Записать();
		Результат = НовыйСчет.Ссылка;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

Функция ТекстОшибкиЗаписиДокумента()

	Возврат НСтр("ru = 'Произошли ошибки при записи документа!';
				|en = 'Errors occurred when saving the document.'");

КонецФункции

Процедура ЗаписатьОшибкуИзмененияДокументаВЖурнал(Объект, ДокументСсылка, Комментарий)

	Событие = НСтр("ru = 'Данные';
					|en = 'Data'", ОбщегоНазначения.КодОсновногоЯзыка())
		+ "." + НСтр("ru = 'Изменение';
					|en = 'Change'", ОбщегоНазначения.КодОсновногоЯзыка());
	ЗаписьЖурналаРегистрации(
		Событие,
		УровеньЖурналаРегистрации.Ошибка,
		Объект.Метаданные(),
		ДокументСсылка,
		Комментарий);

КонецПроцедуры

Процедура ВывестиОшибкуЗаписи(Объект, ОписаниеОшибки, ОшибкиЗагрузки)

	НазваниеОбъекта = Строка(Объект);
	ЭтоНовыйДокумент = Объект.ЭтоНовый();
	
	ТекстСообщения  = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1%2. %3';
			|en = '%1%2. %3'"),
		Врег(Лев(НазваниеОбъекта, 1)) + Сред(НазваниеОбъекта, 2),
		?(ЭтоНовыйДокумент, " " + НСтр("ru = 'не создан';
										|en = 'not created'"), " " + НСтр("ru = 'не записан';
																		|en = 'not saved'")),
		ТекстОшибкиЗаписиДокумента());
	
	ДокументСсылка = ?(ЭтоНовыйДокумент, "", Объект.Ссылка);
	
	ПодробноеОписаниеОшибки = СформироватьОписаниеОшибкиЗаписиДокумента(ОписаниеОшибки);
	
	ТекстОшибки = НСтр("ru = '%1
						|%2';
						|en = '%1
						|%2'");
	
	Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		ТекстОшибки,
		ПодробноеОписаниеОшибки,
		?(ЭтоНовыйДокумент, ТекстСообщения, ТекстОшибкиЗаписиДокумента()));
	
	ЗаписатьОшибкуИзмененияДокументаВЖурнал(Объект, ДокументСсылка, Комментарий);
	
	Если ПодробноеОписаниеОшибки <> "" Тогда
		ДобавитьЗамечание(ОшибкиЗагрузки, ПодробноеОписаниеОшибки);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьОбъект(ОбъектДляЗаписи, РежимЗаписи, СтрокаДокумента)
	
	ОбъектМодифицирован = ОбъектДляЗаписи.Модифицированность();
	ОбъектПроведен      = ОбъектДляЗаписи.Проведен;
	Проводить = (РежимЗаписи = РежимЗаписиДокумента.Проведение Или ОбъектПроведен И РежимЗаписи = РежимЗаписиДокумента.Запись);
	
	Если ОбъектМодифицирован
		И Не Проводить Тогда
		Попытка
			ОбъектДляЗаписи.Записать(РежимЗаписи);
		Исключение
			
			ОписаниеОшибки = ИнформацияОбОшибке();
			ВывестиОшибкуЗаписи(ОбъектДляЗаписи, ОписаниеОшибки, СтрокаДокумента.ОшибкиЗагрузки);
			Возврат;
			
		КонецПопытки;
	КонецЕсли;
	
	Если Проводить И (ОбъектМодифицирован Или НЕ ОбъектПроведен) Тогда
		
		// Сохранение расшифровки платежа до проведения
		КопияРасшифровкиПлатежа = Неопределено;
		
		Если ОбъектДляЗаписи.РасшифровкаПлатежа.Количество() > 0 Тогда
			КопияРасшифровкиПлатежа = ОбъектДляЗаписи.РасшифровкаПлатежа.Выгрузить();
		КонецЕсли;
		
		Попытка
			ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			
			ОписаниеОшибки = ИнформацияОбОшибке();
			ТекстОшибки = НСтр("ru = '%1
				|Произошли ошибки при проведении!';
				|en = '%1 
				|Errors occurred when posting.'");
			
			ПодробноеОписаниеОшибки = СформироватьОписаниеОшибкиЗаписиДокумента(ОписаниеОшибки);
			Комментарий = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				ТекстОшибки,
				ПодробноеОписаниеОшибки);
			ЗаписатьОшибкуИзмененияДокументаВЖурнал(ОбъектДляЗаписи, ОбъектДляЗаписи.Ссылка, Комментарий);
			
			Если ОписаниеОшибки.Причина <> Неопределено Тогда
				ДобавитьЗамечание(СтрокаДокумента.ОшибкиЗагрузки, ПодробноеОписаниеОшибки);
			КонецЕсли;
			
		КонецПопытки;
		
		Если Не ОбъектДляЗаписи.Проведен Тогда
			
			// Восстановление расшифровки платежа до проведения в случае ошибки проведения,
			// т.к. во время записи объекта создается и заполняется объект расчетов в табличную часть,
			// а после отката транзакции остается ссылка, не существующего объекта расчетов ("битая ссылка").
			Если КопияРасшифровкиПлатежа <> Неопределено Тогда
				ОбъектДляЗаписи.РасшифровкаПлатежа.Загрузить(КопияРасшифровкиПлатежа);
			КонецЕсли;
			
			Попытка
				ОбъектДляЗаписи.Записать(РежимЗаписиДокумента.Запись);
			Исключение
				ОписаниеОшибки = ИнформацияОбОшибке();
				ВывестиОшибкуЗаписи(ОбъектДляЗаписи, ОписаниеОшибки, СтрокаДокумента.ОшибкиЗагрузки);
			КонецПопытки;
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСсылкиНаДокументы(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет)
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ТаблицаДокументов.ДатаДок,
	|	ТаблицаДокументов.Поступило,
	|	ТаблицаДокументов.Списано,
	|	ТаблицаДокументов.Номер,
	|	ТаблицаДокументов.НомерСокр,
	|	ТаблицаДокументов.Исходящий,
	|	ТаблицаДокументов.ТипПлатежногоДокумента,
	|	ТаблицаДокументов.ПолучательСчет,
	|	ТаблицаДокументов.ПлательщикСчет,
	|	ТаблицаДокументов.ПолучательИНН,
	|	ТаблицаДокументов.ПлательщикИНН,
	|	ТаблицаДокументов.ПолучательКПП,
	|	ТаблицаДокументов.ПлательщикКПП,
	|	ТаблицаДокументов.ПроверятьИНН,
	|	ТаблицаДокументов.ПроверятьКПП,
	|	ВЫРАЗИТЬ(ТаблицаДокументов.Код КАК Строка(25)) КАК Код,
	|	ВЫРАЗИТЬ(ТаблицаДокументов.ДанныеВыписки КАК Строка(1024)) КАК ДанныеВыписки,
	|	ВЫРАЗИТЬ(ТаблицаДокументов.Операция КАК Строка(128)) КАК Операция,
	|	ТаблицаДокументов.ЭтоЗагрузкаBankFeeds 
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Выполнить();
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|		
	|		И (НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаВходящегоДокумента, ДЕНЬ) = ТаблицаДокументов.ДатаДок
	|			И НЕ ДанныеДокумента.ПроведеноБанком
	|			ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаВходящегоДокумента, ДЕНЬ) = ТаблицаДокументов.ДатаДок
	|				И ДанныеДокумента.ДатаПроведенияБанком = ТаблицаДокументов.Списано
	|				И ДанныеДокумента.ПроведеноБанком)
	|		
	|		И (ДанныеДокумента.НомерВходящегоДокумента = ТаблицаДокументов.Номер
	|			ИЛИ ДанныеДокумента.НомерВходящегоДокумента = ТаблицаДокументов.НомерСокр)
	|		И ДанныеДокумента.ТипПлатежногоДокумента = ТаблицаДокументов.ТипПлатежногоДокумента
	|	
	|		И (ДанныеДокумента.БанковскийСчетПолучатель.НомерСчета = ТаблицаДокументов.ПолучательСчет
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.НомерСчета = ТаблицаДокументов.ПолучательСчет
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетПолучатель.НомерСчета, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.НомерСчета, """") = """")
	|			ИЛИ ТаблицаДокументов.ПолучательСчет = """"
	|			)
	//++ Локализация
	|		И (НЕ ТаблицаДокументов.ПроверятьИНН
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетПолучатель.Владелец.ИНН, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.ИНН, """") = """")
	|			ИЛИ ДанныеДокумента.БанковскийСчетПолучатель.Владелец.ИНН = ТаблицаДокументов.ПолучательИНН
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.ИНН = ТаблицаДокументов.ПолучательИНН
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.ИННКорреспондента = ТаблицаДокументов.ПолучательИНН
	|			)
	|		И (
	|			(НЕ ТаблицаДокументов.ПроверятьКПП
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетПолучатель.Владелец.КПП, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.КПП, """") = """")
	|			ИЛИ ДанныеДокумента.БанковскийСчетПолучатель.Владелец.КПП = ТаблицаДокументов.ПолучательКПП
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.КПП = ТаблицаДокументов.ПолучательКПП
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.КППКорреспондента = ТаблицаДокументов.ПолучательКПП
	|			)
	|			ИЛИ 
	|			(ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) = ТаблицаДокументов.ДанныеВыписки
	|				И ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) <> """"))
	//-- Локализация
	|		
	|ГДЕ
	|	ТаблицаДокументов.Исходящий
	|	И НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
	|	И НЕ ДанныеДокумента.ПометкаУдаления
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	//++ Локализация
	|		И НЕ ДанныеДокумента.ДанныеВыписки ПОДОБНО (&ТекстСекцияДокументОперацияПоЯндексКассе)
	//-- Локализация
	|		
	|		И (НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаВходящегоДокумента, ДЕНЬ) = ТаблицаДокументов.ДатаДок
	|			И НЕ ДанныеДокумента.ПроведеноБанком
	|			ИЛИ НАЧАЛОПЕРИОДА(ДанныеДокумента.ДатаВходящегоДокумента, ДЕНЬ) = ТаблицаДокументов.ДатаДок
	|				И ДанныеДокумента.ДатаПроведенияБанком = ТаблицаДокументов.Поступило
	|				И ДанныеДокумента.ПроведеноБанком)
	|		
	|		И (ДанныеДокумента.НомерВходящегоДокумента = ТаблицаДокументов.Номер
	|			ИЛИ ДанныеДокумента.НомерВходящегоДокумента = ТаблицаДокументов.НомерСокр)
	|		И ДанныеДокумента.ТипПлатежногоДокумента = ТаблицаДокументов.ТипПлатежногоДокумента
	|		
	|		И (ДанныеДокумента.БанковскийСчетОтправитель.НомерСчета = ТаблицаДокументов.ПлательщикСчет
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.НомерСчета = ТаблицаДокументов.ПлательщикСчет
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетОтправитель.НомерСчета, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.НомерСчета, """") = """")
	|			ИЛИ ТаблицаДокументов.ПлательщикСчет = """"
	|			)
	//++ Локализация
	|		И (НЕ ТаблицаДокументов.ПроверятьИНН
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетОтправитель.Владелец.ИНН, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.ИНН, """") = """")
	|			ИЛИ ДанныеДокумента.БанковскийСчетОтправитель.Владелец.ИНН = ТаблицаДокументов.ПлательщикИНН
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.ИНН = ТаблицаДокументов.ПлательщикИНН
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.ИННКорреспондента = ТаблицаДокументов.ПолучательИНН
	|			)
	|		
	|		И (НЕ ТаблицаДокументов.ПроверятьИНН
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.Контрагент.ИНН, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.ПодотчетноеЛицо.ИНН, """") = """")
	|			ИЛИ ДанныеДокумента.Контрагент.ИНН = ТаблицаДокументов.ПлательщикИНН
	|			ИЛИ ДанныеДокумента.ПодотчетноеЛицо.ИНН = ТаблицаДокументов.ПлательщикИНН)
	|		
	|		И ((НЕ ТаблицаДокументов.ПроверятьКПП
	|			ИЛИ ЕСТЬNULL(ДанныеДокумента.Контрагент.КПП, """") = """"
	|			ИЛИ ДанныеДокумента.Контрагент.КПП = ТаблицаДокументов.ПлательщикКПП)
	|			
	|			ИЛИ (ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) = ТаблицаДокументов.ДанныеВыписки
	|				И ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) <> """"))
	//-- Локализация
	|		И (
	//++ Локализация
	|			(НЕ ТаблицаДокументов.ПроверятьКПП
	|			ИЛИ (ЕСТЬNULL(ДанныеДокумента.БанковскийСчетОтправитель.Владелец.КПП, """") = """"
	|				И ЕСТЬNULL(ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.КПП, """") = """")
	|			ИЛИ ДанныеДокумента.БанковскийСчетОтправитель.Владелец.КПП = ТаблицаДокументов.ПлательщикКПП
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.Владелец.КПП = ТаблицаДокументов.ПлательщикКПП
	|			ИЛИ ДанныеДокумента.БанковскийСчетКонтрагента.КППКорреспондента = ТаблицаДокументов.ПолучательКПП
	|			)
	|			ИЛИ 
	//-- Локализация
	|			(ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) = ТаблицаДокументов.ДанныеВыписки
	|				И ВЫРАЗИТЬ(ДанныеДокумента.ДанныеВыписки КАК Строка(1024)) <> """")
	|			ИЛИ ТаблицаДокументов.ЭтоЗагрузкаBankFeeds)
	|		
	|ГДЕ
	|	НЕ ТаблицаДокументов.Исходящий
	|	И НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
	|	И НЕ ДанныеДокумента.ПометкаУдаления
	//++ Локализация
	|	И НЕ ТаблицаДокументов.Операция ПОДОБНО (&ТекстОперацияПоЯндексКассе)
	//-- Локализация
	|	
	//++ Локализация
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ // Проведение банком платежа ЯндексКассы
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|		И ДанныеДокумента.ИдентификаторПлатежа = ТаблицаДокументов.Код
	|		И ДанныеДокумента.ДанныеВыписки ПОДОБНО (&ТекстСекцияДокументОперацияПоЯндексКассе)
	|		
	|ГДЕ
	|	НЕ ТаблицаДокументов.Исходящий
	|	И НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
	|	И НЕ ДанныеДокумента.ПометкаУдаления
	|	И ТаблицаДокументов.Код <> """"
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ // Платеж ЯндексКассы
	|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
	|	ДанныеДокумента.Ссылка КАК Ссылка,
	|	ДанныеДокумента.Проведен КАК Проведен,
	|	ДанныеДокумента.ХозяйственнаяОперация КАК ХозяйственнаяОперация
	|
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	ПО
	|		ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|		И ДанныеДокумента.ИдентификаторПлатежа = ТаблицаДокументов.Код
	|		
	|ГДЕ
	|	НЕ ТаблицаДокументов.Исходящий
	|	И НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
	|	И НЕ ДанныеДокумента.ПометкаУдаления
	|	И ТаблицаДокументов.Код <> """"
	|	И ТаблицаДокументов.Операция ПОДОБНО (&ТекстОперацияПоЯндексКассе)
	//-- Локализация
	|
	|УПОРЯДОЧИТЬ ПО
	|	Проведен УБЫВ";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	//++ Локализация
	Запрос.УстановитьПараметр("ТекстОперацияПоЯндексКассе", "Операция по Яндекс.Кассе%");
	Запрос.УстановитьПараметр("ТекстСекцияДокументОперацияПоЯндексКассе", "СЕКЦИЯДОКУМЕНТ=Операция по Яндекс.Кассе%");
	//-- Локализация
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаДокумента = ДокументыКЗагрузке.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаДокумента <> Неопределено Тогда
			
			// Если документ найден, создавать новый не будем. Если найдено несколько документов,
			// то дубли документа пропускаем, обновляем только первый найденный документ.
			Если ЗначениеЗаполнено(СтрокаДокумента.Документ) Тогда
				Продолжить;
			КонецЕсли;
			СтрокаДокумента.Документ = Выборка.Ссылка;
			СтрокаДокумента.НайденДокументВБазе = Истина;
			СтрокаДокумента.ВидОперации = Выборка.ХозяйственнаяОперация;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция РазделенныйСписокУИП(ТаблицаДокументов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаУИП.Код КАК Код,
		|	ТаблицаУИП.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУИП.Исходящий КАК Исходящий
		|ПОМЕСТИТЬ СписокУИП
		|ИЗ
		|	&ТаблицаУИП КАК ТаблицаУИП
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУИП.НомерСтроки КАК НомерСтроки,
		|	СписокУИП.Код КАК Код,
		|	СписокУИП.Исходящий КАК Исходящий
		|ПОМЕСТИТЬ СписокУИПКонвертацииВалюты
		|ИЗ
		|	СписокУИП КАК СписокУИП
		|ГДЕ
		|	СписокУИП.Код ПОДОБНО &ПрефиксКонвертации
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУИП.Код КАК Код,
		|	СписокУИП.НомерСтроки КАК НомерСтроки,
		|	СписокУИП.Исходящий КАК Исходящий
		|ПОМЕСТИТЬ СписокУИПРасчетыСКонтрагентами
		|ИЗ
		|	СписокУИП КАК СписокУИП
		|ГДЕ
		|	НЕ СписокУИП.НомерСтроки В
		|				(ВЫБРАТЬ
		|					СписокУИПКонвертацииВалюты.НомерСтроки КАК НомерСтроки
		|				ИЗ
		|					СписокУИПКонвертацииВалюты КАК СписокУИПКонвертацииВалюты)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУИПРасчетыСКонтрагентами.Код КАК Код,
		|	СписокУИПРасчетыСКонтрагентами.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	СписокУИПРасчетыСКонтрагентами КАК СписокУИПРасчетыСКонтрагентами
		|ГДЕ
		|	СписокУИПРасчетыСКонтрагентами.Исходящий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУИПРасчетыСКонтрагентами.Код КАК Код,
		|	СписокУИПРасчетыСКонтрагентами.НомерСтроки КАК НомерСтроки
		|ИЗ
		|	СписокУИПРасчетыСКонтрагентами КАК СписокУИПРасчетыСКонтрагентами
		|ГДЕ
		|	НЕ СписокУИПРасчетыСКонтрагентами.Исходящий
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СписокУИПКонвертацииВалюты.НомерСтроки КАК НомерСтроки,
		|	СписокУИПКонвертацииВалюты.Код КАК Код
		|ИЗ
		|	СписокУИПКонвертацииВалюты КАК СписокУИПКонвертацииВалюты
		|ГДЕ
		|	НЕ СписокУИПКонвертацииВалюты.Исходящий";
	
	Запрос.УстановитьПараметр("ТаблицаУИП", ТаблицаДокументов);
	Запрос.УстановитьПараметр("ПрефиксКонвертации", НСтр("ru = 'ПП%';
														|en = '#%'", ОбщегоНазначения.КодОсновногоЯзыка()));
	
	МассивЗапросов = Запрос.ВыполнитьПакет();
	УИПРасчетовСПоставщиками = МассивЗапросов[МассивЗапросов.ВГраница() - 2].Выгрузить();
	УИПРасчетовСКлиентами = МассивЗапросов[МассивЗапросов.ВГраница() - 1].Выгрузить();
	УИПКонвертации = МассивЗапросов[МассивЗапросов.ВГраница()].Выгрузить();
	
	СпискиУИП = Новый Структура;
	СпискиУИП.Вставить("УИПРасчетовСПоставщиками", УИПРасчетовСПоставщиками);
	СпискиУИП.Вставить("УИПРасчетовСКлиентами", УИПРасчетовСКлиентами);
	СпискиУИП.Вставить("УИПКонвертации", УИПКонвертации);
	
	Возврат СпискиУИП;
	
КонецФункции

Процедура ЗаполнитьОснованиеПлатежейПоУИП(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет)
	
	СпискиУИП = РазделенныйСписокУИП(ТаблицаДокументов);
	
	Если СпискиУИП.УИПРасчетовСКлиентами.Количество() <> 0 Тогда
		ЗаполнитьПлатежиКЗагрузкеПоУИП(Ложь, БанковскийСчет, ДокументыКЗагрузке, СпискиУИП.УИПРасчетовСКлиентами);
	КонецЕсли;
	
	Если СпискиУИП.УИПРасчетовСПоставщиками.Количество() <> 0 Тогда
		
		ЗаполнитьПлатежиКЗагрузкеПоУИП(Истина, БанковскийСчет, ДокументыКЗагрузке, СпискиУИП.УИПРасчетовСПоставщиками);
		КлиентБанкЛокализация.УстановитьПризнакРасчетаССамозанятымПоОбъектуРасчета(ДокументыКЗагрузке);
		
	КонецЕсли;
	
	Если СпискиУИП.УИПКонвертации.Количество() <> 0 Тогда
	
		ДанныеДокументов = ДенежныеСредстваСервер.ОснованияОперацииКонвертацииПоУИП(
			СпискиУИП.УИПКонвертации.ВыгрузитьКолонку("Код"), ТекущаяДатаСеанса());
	
		Для Каждого ТекущиеДанные Из ДанныеДокументов Цикл
			
			ОтборИдентификатора = Новый Структура;
			ОтборИдентификатора.Вставить("Код", ТекущиеДанные.ИдентификаторПлатежа);
			СтрокиДокументов = ДокументыКЗагрузке.НайтиСтроки(ОтборИдентификатора);
			
			Для Каждого СтрокаДокумента Из СтрокиДокументов Цикл
				
				СтрокаДокумента.НайденоОснованиеПлатежа = Истина;
				СтрокаДокумента.НайденКонтрагент = Истина;
				СтрокаДокумента.СоздаватьКонтрагента = Ложь;
			
				ЗаполнитьЗначенияСвойств(
					СтрокаДокумента,
					ТекущиеДанные, 
					"Контрагент, Партнер, ОснованиеПлатежа, Подразделение, СтатьяДвиженияДенежныхСредств, ВалютаКонвертации, СуммаКонвертации");
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
			
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыХозОперацийПоступления()
	
	РеквизитыХозОпераций = Новый Соответствие;
	
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте,
		Новый Структура("ДоговорЭквайринга, Подразделение, СтатьяРасходов, АналитикаРасходов, СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств,
		Новый Структура("СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствСДругогоСчета,
		Новый Структура(""));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствВБанк,
		Новый Структура("Касса"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоКредитам,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоДепозитам,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствПоЗаймамВыданным,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.КонвертацияВалюты,
		Новый Структура("СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств, РасшифровкаПлатежаСтатьяДоходов, РасшифровкаПлатежаАналитикаДоходов, РасшифровкаПлатежаАналитикаАктивовПассивов, РасшифровкаПлатежаПодразделение"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника,
		Новый Структура("СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратНеперечисленныхДС,
		Новый Структура(""));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратНалогов,
		Новый Структура("ТипНалога, НастройкаСчетовУчета, РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	
	Возврат РеквизитыХозОпераций;
	
КонецФункции

Функция РеквизитыХозОперацийСписания()
	
	РеквизитыХозОпераций = Новый Соответствие;
	
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ОплатаПоставщику,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику,
		Новый Структура("ПодотчетноеЛицо, Подразделение, РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПеречислениеВБюджет,
		Новый Структура("ТипНалога, НастройкаСчетовУчета, РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПеречислениеТаможне,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств,
		Новый Структура("СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыНаЛицевыеСчета,
		Новый Структура("СтатьяДвиженияДенежныхСредств, Подразделение"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВыплатаЗарплатыПоЗарплатномуПроекту,
		Новый Структура("СтатьяДвиженияДенежныхСредств, Подразделение"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПеречислениеДенежныхСредствНаДругойСчет,
		Новый Структура(""));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ИнкассацияДенежныхСредствИзБанка,
		Новый Структура("КассаПолучатель"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ОплатаПоКредитам,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПеречислениеНаДепозиты,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВыдачаЗаймов,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.КонвертацияВалюты,
		Новый Структура("СтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств, РасшифровкаПлатежаСтатьяРасходов, РасшифровкаПлатежаАналитикаРасходов, РасшифровкаПлатежаАналитикаАктивовПассивов, РасшифровкаПлатежаПодразделение"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию,
		Новый Структура("РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств"));
	РеквизитыХозОпераций.Вставить(Перечисления.ХозяйственныеОперации.ВозвратОплатыНаПлатежнуюКарту,
		Новый Структура("ДоговорЭквайринга, Подразделение, СтатьяРасходов, АналитикаРасходов, СтатьяДвиженияДенежныхСредств"));
	
	Возврат РеквизитыХозОпераций;
	
КонецФункции

Функция ПоискКорректен(СтрокаДокумента, Выборка, НаборРеквизитов, ТекстУсловия)
	
	Запрос = Новый Запрос;
	
	Если СтрокаДокумента.Исходящий Тогда
		
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТабЧасть
		|	ПО
		|		ТабЧасть.Ссылка = ДанныеДокумента.Ссылка
		|ГДЕ
		|	&Условие
		|	
		|";
		
	Иначе
		
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	1
		|ИЗ
		|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТабЧасть
		|	ПО
		|		ТабЧасть.Ссылка = ДанныеДокумента.Ссылка
		|ГДЕ
		|	&Условие
		|	
		|";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
	
	Запрос.УстановитьПараметр("БанковскийСчет", СтрокаДокумента.БанковскийСчет);
	Запрос.УстановитьПараметр("СчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
	
	Если Запрос.НайтиПараметры().Найти("Контрагент") <> Неопределено Тогда
		Запрос.УстановитьПараметр("Контрагент", СтрокаДокумента.Контрагент);
	КонецЕсли;
	Если Запрос.НайтиПараметры().Найти("ФорматированноеНазначениеПлатежа") <> Неопределено Тогда
		Запрос.УстановитьПараметр("ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа);
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|	И ДанныеДокумента.Проведен
	|	И ДанныеДокумента.Дата > &ПериодПроверки";
	Запрос.УстановитьПараметр("ПериодПроверки", ТекущаяДатаСеанса() - 60 * 60 * 24 * 15); // Проверяются документы за последние 15 дней
	
	Если ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
		Запрос.Текст = Запрос.Текст + " И ДанныеДокумента.ХозяйственнаяОперация = &ХозяйственнаяОперация И (ИСТИНА";
		Запрос.УстановитьПараметр("ХозяйственнаяОперация", СтрокаДокумента.ВидОперации);
	Иначе
		Запрос.Текст = Запрос.Текст + " И (ДанныеДокумента.ХозяйственнаяОперация <> &ХозяйственнаяОперация";
		Запрос.УстановитьПараметр("ХозяйственнаяОперация", Выборка.ХозяйственнаяОперация);
	КонецЕсли;
	
	СтрокаУсловия = "";
	Если НаборРеквизитов <> Неопределено Тогда
		Для каждого Реквизит Из НаборРеквизитов Цикл
			
			Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
				Продолжить;
			КонецЕсли;
			
			Если СтрНайти(Реквизит.Ключ, "РасшифровкаПлатежа") Тогда
				ИмяРеквизита = Сред(Реквизит.Ключ, СтрДлина("РасшифровкаПлатежа") + 1);
				СтрокаУсловия = " ИЛИ ТабЧасть.ИмяРеквизита <> &ИмяРеквизита";
			Иначе
				ИмяРеквизита = Реквизит.Ключ;
				СтрокаУсловия = " ИЛИ ДанныеДокумента.ИмяРеквизита <> &ИмяРеквизита";
			КонецЕсли;
			
			СтрокаУсловия = СтрЗаменить(СтрокаУсловия, "ИмяРеквизита", ИмяРеквизита);
			Запрос.Текст = Запрос.Текст + СтрокаУсловия;
			Запрос.УстановитьПараметр(ИмяРеквизита, Реквизит.Значение);
		КонецЦикла;
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + ")";
	
	Результат = Запрос.Выполнить();
	
	Возврат Результат.Пустой();
	
КонецФункции

Процедура ЗаполнитьОбразцы(СтрокиКЗагрузке, БанковскийСчет)
	
	РеквизитыХозОперацийПоступления = РеквизитыХозОперацийПоступления();
	РеквизитыХозОперацийСписания = РеквизитыХозОперацийСписания();
	
	Запрос = Новый Запрос;
	
	ТекстЗапросаВходящие = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ТабЧасть.СтатьяДвиженияДенежныхСредств          КАК РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств   КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ПодотчетноеЛицо                 КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.КассаОтправитель                КАК КассаОтправитель,
	|	ТабЧасть.Подразделение                          КАК РасшифровкаПлатежаПодразделение,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ТабЧасть.СтатьяДоходов                          КАК РасшифровкаПлатежаСтатьяДоходов,
	|	ТабЧасть.АналитикаДоходов                       КАК РасшифровкаПлатежаАналитикаДоходов,
	|	ТабЧасть.АналитикаАктивовПассивов               КАК РасшифровкаПлатежаАналитикаАктивовПассивов,
	|	ДанныеДокумента.ДоговорЭквайринга               КАК ДоговорЭквайринга,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	ДанныеДокумента.ТипНалога                       КАК ТипНалога,
	|	ДанныеДокумента.НастройкаСчетовУчета            КАК НастройкаСчетовУчета,
	|	ДанныеДокумента.ИдентификаторПлатежа            КАК ИдентификаторПлатежа
	|	
	|ИЗ
	|	Документ.ПоступлениеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.ПоступлениеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТабЧасть
	|	ПО
	|		ТабЧасть.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	&Условие
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Дата УБЫВ
	|";
	
	ТекстЗапросаИсходящие = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ДанныеДокумента.Ссылка                          КАК Ссылка,
	|	ДанныеДокумента.ХозяйственнаяОперация           КАК ХозяйственнаяОперация,
	|	ТабЧасть.СтатьяДвиженияДенежныхСредств          КАК РасшифровкаПлатежаСтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.СтатьяДвиженияДенежныхСредств   КАК СтатьяДвиженияДенежныхСредств,
	|	ДанныеДокумента.ПодотчетноеЛицо                 КАК ПодотчетноеЛицо,
	|	ДанныеДокумента.КассаПолучатель                 КАК КассаПолучатель,
	|	ТабЧасть.Подразделение                          КАК РасшифровкаПлатежаПодразделение,
	|	ДанныеДокумента.Подразделение                   КАК Подразделение,
	|	ТабЧасть.СтатьяРасходов                         КАК РасшифровкаПлатежаСтатьяРасходов,
	|	ТабЧасть.АналитикаРасходов                      КАК РасшифровкаПлатежаАналитикаРасходов,
	|	ТабЧасть.АналитикаАктивовПассивов               КАК РасшифровкаПлатежаАналитикаАктивовПассивов,
	|	ДанныеДокумента.ДоговорЭквайринга               КАК ДоговорЭквайринга,
	|	ДанныеДокумента.СтатьяРасходов                  КАК СтатьяРасходов,
	|	ДанныеДокумента.АналитикаРасходов               КАК АналитикаРасходов,
	|	ДанныеДокумента.ТипНалога                       КАК ТипНалога,
	|	ДанныеДокумента.НастройкаСчетовУчета            КАК НастройкаСчетовУчета,
	|	ДанныеДокумента.ИдентификаторПлатежа            КАК ИдентификаторПлатежа
	|	
	|ИЗ
	|	Документ.СписаниеБезналичныхДенежныхСредств КАК ДанныеДокумента
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ
	|		Документ.СписаниеБезналичныхДенежныхСредств.РасшифровкаПлатежа КАК ТабЧасть
	|	ПО
	|		ТабЧасть.Ссылка = ДанныеДокумента.Ссылка
	|	
	|ГДЕ
	|	&Условие
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ДанныеДокумента.Дата УБЫВ
	|";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	// Шаг 1. Поиск по счету отправителя, получателя, назначению платежа
	УсловиеИсходящие = "
	|	ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &СчетКонтрагента
	|		ИЛИ ДанныеДокумента.БанковскийСчетПолучатель = &СчетКонтрагента)
	|	И ДанныеДокумента.ФорматированноеНазначениеПлатежа = &ФорматированноеНазначениеПлатежа
	|	И ДанныеДокумента.Проведен
	|";

	УсловиеВходящие = "
	|	ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &СчетКонтрагента
	|		ИЛИ ДанныеДокумента.БанковскийСчетОтправитель = &СчетКонтрагента
	|		ИЛИ (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте)
	|			И ДанныеДокумента.Контрагент = &Контрагент))
	|	И ДанныеДокумента.ФорматированноеНазначениеПлатежа = &ФорматированноеНазначениеПлатежа
	|	И ДанныеДокумента.Проведен
	|";
	
	СтрокиПоискаШаг2 = Новый Массив;
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			Запрос.Текст = ТекстЗапросаИсходящие;
			ТекстУсловия = УсловиеИсходящие;
			Реквизиты = РеквизитыХозОперацийСписания;
		Иначе
			Запрос.Текст = ТекстЗапросаВходящие;
			ТекстУсловия = УсловиеВходящие;
			Реквизиты = РеквизитыХозОперацийПоступления;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
			ТекстУсловия = ТекстУсловия + "
			|	И ДанныеДокумента.ХозяйственнаяОперация = &ХозяйственнаяОперация";
			Запрос.УстановитьПараметр("ХозяйственнаяОперация", СтрокаДокумента.ВидОперации);
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
		
		Если Запрос.НайтиПараметры().Найти("Контрагент") <> Неопределено Тогда
			Запрос.УстановитьПараметр("Контрагент", СтрокаДокумента.Контрагент);
		КонецЕсли;
		
		Запрос.УстановитьПараметр("СчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		Запрос.УстановитьПараметр("ФорматированноеНазначениеПлатежа", СтрокаДокумента.ФорматированноеНазначениеПлатежа);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			НаборРеквизитов = Реквизиты.Получить(Выборка.ХозяйственнаяОперация);
			Если НаборРеквизитов <> Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(НаборРеквизитов, Выборка);
				
				Если ПоискКорректен(СтрокаДокумента, Выборка, НаборРеквизитов, ТекстУсловия) Тогда
					
					СтрокаДокумента.НайденОбразец = Истина;
					СтрокаДокумента.Образец = Выборка.Ссылка;
					СтрокаДокумента.ВидОперации = Выборка.ХозяйственнаяОперация;
					
					Для каждого Реквизит Из НаборРеквизитов Цикл
						Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
							Продолжить;
						КонецЕсли;
						Если СтрНайти(Реквизит.Ключ, "РасшифровкаПлатежа") Тогда
							ИмяРеквизита = Сред(Реквизит.Ключ, СтрДлина("РасшифровкаПлатежа") + 1);
						Иначе
							ИмяРеквизита = Реквизит.Ключ;
						КонецЕсли;
						СтрокаДокумента[ИмяРеквизита] = Реквизит.Значение;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокиПоискаШаг2.Добавить(СтрокаДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если Не СтрокиПоискаШаг2.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	// Шаг 2. Поиск по счету отправителя, получателя, без учета назначения платежа
	УсловиеИсходящие = "
	|	ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &СчетКонтрагента
	|		ИЛИ ДанныеДокумента.БанковскийСчетПолучатель = &СчетКонтрагента)
	|	И ДанныеДокумента.Проведен
	|";

	УсловиеВходящие = "
	|	ДанныеДокумента.БанковскийСчет = &БанковскийСчет
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &СчетКонтрагента
	|		ИЛИ ДанныеДокумента.БанковскийСчетОтправитель = &СчетКонтрагента
	|		ИЛИ (ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыПоПлатежнойКарте)
	|			И ДанныеДокумента.Контрагент = &Контрагент))
	|	И ДанныеДокумента.Проведен
	|";
	
	СтрокиПоискаШаг3 = Новый Массив;
	Для каждого СтрокаДокумента Из СтрокиПоискаШаг2 Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			Запрос.Текст = ТекстЗапросаИсходящие;
			ТекстУсловия = УсловиеИсходящие;
			Реквизиты = РеквизитыХозОперацийСписания;
		Иначе
			Запрос.Текст = ТекстЗапросаВходящие;
			ТекстУсловия = УсловиеВходящие;
			Реквизиты = РеквизитыХозОперацийПоступления;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
			ТекстУсловия = ТекстУсловия + "
			|	И ДанныеДокумента.ХозяйственнаяОперация = &ХозяйственнаяОперация";
			Запрос.УстановитьПараметр("ХозяйственнаяОперация", СтрокаДокумента.ВидОперации);
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
		
		Запрос.УстановитьПараметр("СчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		Если Запрос.НайтиПараметры().Найти("Контрагент") <> Неопределено Тогда
			Запрос.УстановитьПараметр("Контрагент", СтрокаДокумента.Контрагент);
		КонецЕсли;
	
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			НаборРеквизитов = Реквизиты.Получить(Выборка.ХозяйственнаяОперация);
			Если НаборРеквизитов <> Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(НаборРеквизитов, Выборка);
				
				Если ПоискКорректен(СтрокаДокумента, Выборка, НаборРеквизитов, ТекстУсловия) Тогда
					
					СтрокаДокумента.НайденОбразец = Истина;
					СтрокаДокумента.Образец = Выборка.Ссылка;
					СтрокаДокумента.ВидОперации = Выборка.ХозяйственнаяОперация;
					
					Для каждого Реквизит Из НаборРеквизитов Цикл
						Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
							Продолжить;
						КонецЕсли;
						Если СтрНайти(Реквизит.Ключ, "РасшифровкаПлатежа") Тогда
							ИмяРеквизита = Сред(Реквизит.Ключ, СтрДлина("РасшифровкаПлатежа") + 1);
						Иначе
							ИмяРеквизита = Реквизит.Ключ;
						КонецЕсли;
						СтрокаДокумента[ИмяРеквизита] = Реквизит.Значение;
					КонецЦикла;
				КонецЕсли;
			КонецЕсли;
		Иначе
			СтрокиПоискаШаг3.Добавить(СтрокаДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если Не СтрокиПоискаШаг3.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	// Шаг 3. Зеркальный платеж
	УсловиеИсходящие = "
	|	ДанныеДокумента.БанковскийСчет = &СчетКонтрагента
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &БанковскийСчет
	|		ИЛИ ДанныеДокумента.БанковскийСчетПолучатель = &БанковскийСчет)
	|	И ДанныеДокумента.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаПоставщику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВыдачаДенежныхСредствПодотчетнику),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию)
	|	)
	|	И ДанныеДокумента.Проведен
	|";
	
	УсловиеВходящие = "
	|	ДанныеДокумента.БанковскийСчет = &СчетКонтрагента
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &БанковскийСчет
	|		ИЛИ ДанныеДокумента.БанковскийСчетОтправитель = &БанковскийСчет)
	|	И ДанныеДокумента.ХозяйственнаяОперация В (
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента),
	|		ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации)
	|	)
	|	И ДанныеДокумента.Проведен
	|";
	
	СтрокиПоискаШаг4 = Новый Массив;
	
	Для каждого СтрокаДокумента Из СтрокиПоискаШаг3 Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			Запрос.Текст = ТекстЗапросаВходящие;
			ТекстУсловия = УсловиеВходящие;
			Реквизиты = РеквизитыХозОперацийПоступления;
		Иначе
			Запрос.Текст = ТекстЗапросаИсходящие;
			ТекстУсловия = УсловиеИсходящие;
			Реквизиты = РеквизитыХозОперацийСписания;
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
		Запрос.УстановитьПараметр("СчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			НаборРеквизитов = Реквизиты.Получить(Выборка.ХозяйственнаяОперация);
			Если НаборРеквизитов <> Неопределено Тогда
				
				ЗаполнитьЗначенияСвойств(НаборРеквизитов, Выборка);
				
				СтрокаДокумента.НайденОбразец = Истина;
				СтрокаДокумента.Образец = Выборка.Ссылка;
				
				Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаПоставщику Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПоставщика;
				ИначеЕсли Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтДругойОрганизации;
				ИначеЕсли Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ВыдачаДенежныхДокументовПодотчетнику Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствОтПодотчетника;
				КонецЕсли;
				
				Если Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратОплатыКлиенту;
				ИначеЕсли Выборка.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВозвратДенежныхСредствВДругуюОрганизацию;
				КонецЕсли;
				
				Для каждого Реквизит Из НаборРеквизитов Цикл
					Если Не ЗначениеЗаполнено(Реквизит.Значение) Тогда
						Продолжить;
					КонецЕсли;
					Если СтрНайти(Реквизит.Ключ, "РасшифровкаПлатежа") Тогда
						ИмяРеквизита = Сред(Реквизит.Ключ, СтрДлина("РасшифровкаПлатежа") + 1);
					Иначе
						ИмяРеквизита = Реквизит.Ключ;
					КонецЕсли;
					СтрокаДокумента[ИмяРеквизита] = Реквизит.Значение;
				КонецЦикла;
			КонецЕсли;
		Иначе
			СтрокиПоискаШаг4.Добавить(СтрокаДокумента);
		КонецЕсли;
	КонецЦикла;
	
	Если Не СтрокиПоискаШаг4.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	// Шаг 4. Зеркальный платеж: поступление сконвертированной валюты
	
	УсловиеИсходящие = "
	|	ДанныеДокумента.БанковскийСчет = &СчетКонтрагента
	|	И (ДанныеДокумента.БанковскийСчетКонтрагента = &БанковскийСчет
	|		ИЛИ ДанныеДокумента.БанковскийСчетПолучатель = &БанковскийСчет)
	|	И ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КонвертацияВалюты)
	|	И ДанныеДокумента.ИдентификаторПлатежа <> """"
	|	И ДанныеДокумента.Проведен
	|";
	
	Для каждого СтрокаДокумента Из СтрокиПоискаШаг4 Цикл
		
		Если СтрокаДокумента.Исходящий Тогда
			Продолжить;
		Иначе
			Запрос.Текст = ТекстЗапросаИсходящие;
			ТекстУсловия = УсловиеИсходящие;
		КонецЕсли;
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", ТекстУсловия);
		Запрос.УстановитьПараметр("СчетКонтрагента", СтрокаДокумента.СчетКонтрагента);
		
		ДокументыКонвертации = Запрос.Выполнить().Выгрузить();
		
		Если ДокументыКонвертации.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СписокУИП = ДокументыКонвертации.ВыгрузитьКолонку("ИдентификаторПлатежа");
		ДанныеДокументов = ДенежныеСредстваСервер.ОснованияОперацииКонвертацииПоУИП(СписокУИП, ТекущаяДатаСеанса());
		
		Для Каждого ДанныеДокумента Из ДанныеДокументов Цикл
			
			Если ДанныеДокумента.СуммаКонвертации > 0 Тогда
				
				СтрокаДокумента.НайденоОснованиеПлатежа = Истина;
				СтрокаДокумента.НайденКонтрагент = Истина;
				СтрокаДокумента.СоздаватьКонтрагента = Ложь;
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.КонвертацияВалюты;
				СтрокаДокумента.НайденОбразец = Истина;
				СтрокаДокумента.Образец = ДанныеДокумента.ОснованиеПлатежа;
				
				ЗаполнитьЗначенияСвойств(
					СтрокаДокумента,
					ДанныеДокумента, 
					"ИдентификаторПлатежа, Контрагент, Партнер, ОснованиеПлатежа, Подразделение, СтатьяДвиженияДенежныхСредств, ВалютаКонвертации, СуммаКонвертации");
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьПартнеров(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет)
	
	Организация = Справочники.БанковскиеСчетаОрганизаций.ПолучитьРеквизитыБанковскогоСчетаОрганизации(БанковскийСчет).Организация;
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ТаблицаДокументов.Контрагент КАК Контрагент
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|ГДЕ
	|	ТаблицаДокументов.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Выполнить();
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ВЫБОР КОГДА ДанныеСправочника.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) ТОГДА
	|		ДанныеСправочника.Партнер
	|	КОГДА НЕ АналитикаУчетаПоПартнерам.Партнер ЕСТЬ NULL ТОГДА
	|		АналитикаУчетаПоПартнерам.Партнер
	|	ИНАЧЕ
	|		ЗНАЧЕНИЕ(Справочник.Партнеры.НеизвестныйПартнер)
	|	КОНЕЦ КАК Партнер
	|
	|ПОМЕСТИТЬ НайденныеПартнеры
	|ИЗ
	|	ТаблицаДокументов
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.Контрагенты КАК ДанныеСправочника
	|	ПО
	|		ДанныеСправочника.Ссылка = ТаблицаДокументов.Контрагент
	|		
	|		ЛЕВОЕ СОЕДИНЕНИЕ
	|			РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ПО
	|			АналитикаУчетаПоПартнерам.Контрагент = ДанныеСправочника.Ссылка
	|			И АналитикаУчетаПоПартнерам.Организация = &Организация
	|			И АналитикаУчетаПоПартнерам.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НайденныеПартнеры.НомерСтроки КАК НомерСтроки,
	|	НайденныеПартнеры.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(Партнеры.ПрочиеОтношения, ЛОЖЬ) = ИСТИНА
	|				И ЕСТЬNULL(Партнеры.Клиент, ЛОЖЬ) = ЛОЖЬ
	|				И ЕСТЬNULL(Партнеры.Поставщик, ЛОЖЬ) = ЛОЖЬ
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПрочиеОтношения
	|ИЗ
	|	НайденныеПартнеры КАК НайденныеПартнеры
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Партнеры КАК Партнеры
	|		ПО НайденныеПартнеры.Партнер = Партнеры.Ссылка";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтрокаДокумента = ДокументыКЗагрузке.Найти(Выборка.НомерСтроки, "НомерСтроки");
		
		Если СтрокаДокумента <> Неопределено Тогда
			СтрокаДокумента.Партнер = Выборка.Партнер;
			СтрокаДокумента.ТолькоПрочиеОтношения = Выборка.ПрочиеОтношения;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает таблицу с объектами расчетов по договорам контрагентов.
// 
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчетаОрганизаций -  Банковский счет организации.
//  Организация - СправочникСсылка.Организации -  Организация.
//  ИсходнаяТаблица - ТаблицаЗначений - таблица, содержащая список контрагентов и их счетов:
//   *Контрагент - СправочникСсылка.Организации, СправочникСсылка.Контрагенты, Строка, СправочникСсылка.ФизическиеЛица - 
//                контрагент, по которому выполняется поиск договоров контрагентов.
//   *СчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов, СправочникСсылка.БанковскиеСчетаОрганизаций, Строка - 
//                     банковский счет контрагента, по которому выполняется поиск договоров контрагентов.
// 
// Возвращаемое значение:
//  ТаблицаЗначений -  Список объектов расчетов по найденным договорам контрагентов:
//   *Контрагент - СправочникСсылка.Организации, СправочникСсылка.Контрагенты, Строка, СправочникСсылка.ФизическиеЛица - 
//                контрагент, по которому выполнялся поиск договоров контрагентов.
//   *СчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов, СправочникСсылка.БанковскиеСчетаОрганизаций, Строка - 
//                     банковский счет контрагента, по которому выполнялся поиск договоров контрагентов.
//   *Договор - СправочникСсылка.ДоговорыКонтрагентов - найденный договор контрагента.
//   *СтатьяДвиженияДенежныхСредств - СправочникСсылка.СтатьиДвиженияДенежныхСредств - статья ДДС по договору.
//   *ОбъектРасчетов - СправочникСсылка.ОбъектыРасчетов - объект расчетов по договору.
//
Функция СписокОбъектовРасчетовПоДоговорам(БанковскийСчет, Организация, ИсходнаяТаблица)

	ИсходнаяТаблица.Индексы.Добавить("Контрагент, СчетКонтрагента");
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Таблица.Контрагент КАК Контрагент,
		|	Таблица.СчетКонтрагента КАК СчетКонтрагента
		|ПОМЕСТИТЬ КонтрагентыИСчета
		|ИЗ
		|	&Таблица КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтрагентыИСчета.Контрагент КАК Контрагент,
		|	КонтрагентыИСчета.СчетКонтрагента КАК СчетКонтрагента,
		|	ДоговорыКонтрагентов.Ссылка КАК Договор,
		|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента КАК БанковскийСчетКонтрагента,
		|	ДоговорыКонтрагентов.БанковскийСчет КАК БанковскийСчет,
		|	ДоговорыКонтрагентов.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДоговорыКонтрагентов.ДоговорССамозанятым КАК ДоговорССамозанятым,
		|	ДоговорыКонтрагентов.Статус КАК Статус,
		|	НЕОПРЕДЕЛЕНО КАК ОбъектРасчетов,
		|	ВЫБОР
		|		КОГДА ДоговорыКонтрагентов.ТипДоговора В (
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СПокупателем),
		//++ НЕ УТКА

		//++ Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СДавальцем),
		//-- Устарело_Переработка24
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СДавальцем2_5),

		//-- НЕ УТКА
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СКомиссионером),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СХранителем),

		//++ НЕ УТ
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.ПередачаВАренду),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.Субаренда),
		//-- НЕ УТ
		
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.СКомитентомНаЗакупку),
		|			ЗНАЧЕНИЕ(Перечисление.ТипыДоговоров.РеализацияЧерезКомиссионера))
		|		ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСКлиентом)
		|	ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетовСПартнерами.РасчетыСПоставщиком)
		|КОНЕЦ КАК ТипРасчетов
		|ПОМЕСТИТЬ ДоговорыСКонтрагентами
		|ИЗ
		|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыИСчета КАК КонтрагентыИСчета
		|		ПО ДоговорыКонтрагентов.Контрагент = КонтрагентыИСчета.Контрагент
		|			И ДоговорыКонтрагентов.Организация = &Организация
		|			И (ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|				ИЛИ ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.НеСогласован))
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	БанковскийСчетКонтрагента,
		|	БанковскийСчет,
		|	Статус
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Контрагент КАК Контрагент,
		|	ДоговорыКонтрагентов.СчетКонтрагента КАК СчетКонтрагента,
		|	ДоговорыКонтрагентов.Договор КАК Договор,
		|	ДоговорыКонтрагентов.СтатьяДвиженияДенежныхСредств КАК СтатьяДвиженияДенежныхСредств,
		|	ДоговорыКонтрагентов.ДоговорССамозанятым КАК ДоговорССамозанятым,
		|	ДоговорыКонтрагентов.ОбъектРасчетов КАК ОбъектРасчетов,
		|	ДоговорыКонтрагентов.ТипРасчетов КАК ТипРасчетов,
		|	ВЫБОР
		|		КОГДА ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|			ТОГДА 1
		|		ИНАЧЕ 2
		|	КОНЕЦ КАК Приоритет
		|ИЗ
		|	ДоговорыСКонтрагентами КАК ДоговорыКонтрагентов
		|ГДЕ
		|	(ДоговорыКонтрагентов.БанковскийСчетКонтрагента = ДоговорыКонтрагентов.СчетКонтрагента
		|		ИЛИ ДоговорыКонтрагентов.БанковскийСчетКонтрагента = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка))
		|	И (ДоговорыКонтрагентов.БанковскийСчет = &БанковскийСчет
		|		ИЛИ ДоговорыКонтрагентов.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Контрагент,
		|	ДоговорыКонтрагентов.СчетКонтрагента,
		|	ДоговорыКонтрагентов.Договор,
		|	ДоговорыКонтрагентов.СтатьяДвиженияДенежныхСредств,
		|	ДоговорыКонтрагентов.ДоговорССамозанятым,
		|	ДоговорыКонтрагентов.ОбъектРасчетов,
		|	ДоговорыКонтрагентов.ТипРасчетов,
		|	ВЫБОР
		|		КОГДА ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ
		|ИЗ
		|	ДоговорыСКонтрагентами КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента <> ДоговорыКонтрагентов.СчетКонтрагента
		|	И ДоговорыКонтрагентов.БанковскийСчетКонтрагента <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
		|	И (ДоговорыКонтрагентов.БанковскийСчет = &БанковскийСчет
		|		ИЛИ ДоговорыКонтрагентов.БанковскийСчет = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Контрагент,
		|	ДоговорыКонтрагентов.СчетКонтрагента,
		|	ДоговорыКонтрагентов.Договор,
		|	ДоговорыКонтрагентов.СтатьяДвиженияДенежныхСредств,
		|	ДоговорыКонтрагентов.ДоговорССамозанятым,
		|	ДоговорыКонтрагентов.ОбъектРасчетов,
		|	ДоговорыКонтрагентов.ТипРасчетов,
		|	ВЫБОР
		|		КОГДА ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ
		|ИЗ
		|	ДоговорыСКонтрагентами КАК ДоговорыКонтрагентов
		|ГДЕ
		|	(ДоговорыКонтрагентов.БанковскийСчетКонтрагента = ДоговорыКонтрагентов.СчетКонтрагента
		|		ИЛИ ДоговорыКонтрагентов.БанковскийСчетКонтрагента = ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка))
		|	И ДоговорыКонтрагентов.БанковскийСчет <> &БанковскийСчет
		|	И ДоговорыКонтрагентов.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоговорыКонтрагентов.Контрагент,
		|	ДоговорыКонтрагентов.СчетКонтрагента,
		|	ДоговорыКонтрагентов.Договор,
		|	ДоговорыКонтрагентов.СтатьяДвиженияДенежныхСредств,
		|	ДоговорыКонтрагентов.ДоговорССамозанятым,
		|	ДоговорыКонтрагентов.ОбъектРасчетов,
		|	ДоговорыКонтрагентов.ТипРасчетов,
		|	ВЫБОР
		|		КОГДА ДоговорыКонтрагентов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует)
		|			ТОГДА 3
		|		ИНАЧЕ 4
		|	КОНЕЦ
		|ИЗ
		|	ДоговорыСКонтрагентами КАК ДоговорыКонтрагентов
		|ГДЕ
		|	ДоговорыКонтрагентов.БанковскийСчетКонтрагента <> ДоговорыКонтрагентов.СчетКонтрагента
		|	И ДоговорыКонтрагентов.БанковскийСчетКонтрагента <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаКонтрагентов.ПустаяСсылка)
		|	И ДоговорыКонтрагентов.БанковскийСчет <> &БанковскийСчет
		|	И ДоговорыКонтрагентов.БанковскийСчет <> ЗНАЧЕНИЕ(Справочник.БанковскиеСчетаОрганизаций.ПустаяСсылка)
		|";
	
	Запрос.УстановитьПараметр("Таблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Если Результат.Количество() <> 0 Тогда
		
		Отбор = ОбъектыРасчетовСервер.ДополнительныеКритерииПоиска();
		Отбор.Вставить("ВернутьПервый", Истина);
		
		ДоговорыИОбъектыРасчетов = ОбъектыРасчетовСервер.ПолучитьОбъектыРасчетовПоСсылкам(
			Результат.ВыгрузитьКолонку("Договор"),
			Организация, ,
			Отбор);
		
		Для Каждого СтрокаДоговора Из Результат Цикл
			СтрокаДоговора.ОбъектРасчетов = ДоговорыИОбъектыРасчетов[СтрокаДоговора.Договор];
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Возвращает таблицу с договорами кредитов и депозитов.
// 
// Параметры:
//  БанковскийСчет - СправочникСсылка.БанковскиеСчетаОрганизаций -  Банковский счет организации.
//  Организация - СправочникСсылка.Организации -  Организация.
//  ИсходнаяТаблица - ТаблицаЗначений - таблица, содержащая список контрагентов и их счетов:
//   *Контрагент - СправочникСсылка.Организации, СправочникСсылка.Контрагенты, Строка, СправочникСсылка.ФизическиеЛица - 
//                контрагент, по которому выполняется поиск договоров.
//   *СчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов, СправочникСсылка.БанковскиеСчетаОрганизаций, Строка - 
//                     банковский счет контрагента, по которому выполняется поиск договоров.
// 
// Возвращаемое значение:
//  ТаблицаЗначений -  Список объектов расчетов по найденным договорам:
//   *Контрагент - СправочникСсылка.Организации, СправочникСсылка.Контрагенты, Строка, СправочникСсылка.ФизическиеЛица - 
//                контрагент, по которому выполнялся поиск договоров.
//   *СчетКонтрагента - СправочникСсылка.БанковскиеСчетаКонтрагентов, СправочникСсылка.БанковскиеСчетаОрганизаций, Строка - 
//                     банковский счет контрагента, по которому выполнялся поиск договоров.
//   *Ссылка - СправочникСсылка.ДоговорыКредитовИДепозитов - найденный договор кредита и депозита.
//   *ХарактерДоговора - ПеречислениеСсылка.ХарактерыДоговоровФинансовыхИнструментов - характер договора.
//   *СтатьяДДСПоступленияВыдачи - СправочникСсылка.СтатьиДвиженияДенежныхСредств - статья движения денежных средств 
//                                 поступления (выдачи) по договору.
//   *СтатьяДДСОсновногоДолга - СправочникСсылка.ОбъектыРасчетов - Статья движения денежных средств основного долга по 
//                              договору.
//
Функция СписокДоговоровКредитовДепозитов(БанковскийСчет, ИсходнаяТаблица)

	ИсходнаяТаблица.Индексы.Добавить("Контрагент, СчетКонтрагента");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Таблица.Контрагент КАК Контрагент,
		|	Таблица.СчетКонтрагента КАК СчетКонтрагента
		|ПОМЕСТИТЬ КонтрагентыИСчета
		|ИЗ
		|	&Таблица КАК Таблица
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтрагентыИСчета.Контрагент КАК Контрагент,
		|	КонтрагентыИСчета.СчетКонтрагента КАК СчетКонтрагента,
		|	ДоговорыКредитовИДепозитов.Ссылка КАК Ссылка,
		|	ДоговорыКредитовИДепозитов.ХарактерДоговора КАК ХарактерДоговора,
		|	ДоговорыКредитовИДепозитов.СтатьяДДСПоступленияВыдачи КАК СтатьяДДСПоступленияВыдачи,
		|	ДоговорыКредитовИДепозитов.СтатьяДДСОсновногоДолга КАК СтатьяДДС,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.ОсновнойДолг) КАК ТипСуммыКредитаДепозита,
		|	ДоговорыКредитовИДепозитов.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
		|ИЗ
		|	Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыИСчета КАК КонтрагентыИСчета
		|		ПО ДоговорыКредитовИДепозитов.Контрагент = КонтрагентыИСчета.Контрагент
		|			И ДоговорыКредитовИДепозитов.БанковскийСчетКонтрагента = КонтрагентыИСчета.СчетКонтрагента
		|			И (ДоговорыКредитовИДепозитов.БанковскийСчет = &БанковскийСчет)
		|			И (ДоговорыКредитовИДепозитов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КонтрагентыИСчета.Контрагент,
		|	КонтрагентыИСчета.СчетКонтрагента,
		|	ДоговорыКредитовИДепозитов.Ссылка,
		|	ДоговорыКредитовИДепозитов.ХарактерДоговора,
		|	НЕОПРЕДЕЛЕНО,
		|	ДоговорыКредитовИДепозитов.СтатьяДДСПроцентов,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Проценты),
		|	ДоговорыКредитовИДепозитов.ВалютаВзаиморасчетов
		|ИЗ
		|	Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыИСчета КАК КонтрагентыИСчета
		|		ПО ДоговорыКредитовИДепозитов.Контрагент = КонтрагентыИСчета.Контрагент
		|			И ДоговорыКредитовИДепозитов.БанковскийСчетПроцентов = КонтрагентыИСчета.СчетКонтрагента
		|			И (ДоговорыКредитовИДепозитов.БанковскийСчет = &БанковскийСчет)
		|			И (ДоговорыКредитовИДепозитов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует))
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КонтрагентыИСчета.Контрагент,
		|	КонтрагентыИСчета.СчетКонтрагента,
		|	ДоговорыКредитовИДепозитов.Ссылка,
		|	ДоговорыКредитовИДепозитов.ХарактерДоговора,
		|	НЕОПРЕДЕЛЕНО,
		|	ДоговорыКредитовИДепозитов.СтатьяДДСКомиссии,
		|	ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИДепозитов.Комиссия),
		|	ДоговорыКредитовИДепозитов.ВалютаВзаиморасчетов
		|ИЗ
		|	Справочник.ДоговорыКредитовИДепозитов КАК ДоговорыКредитовИДепозитов
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КонтрагентыИСчета КАК КонтрагентыИСчета
		|		ПО ДоговорыКредитовИДепозитов.Контрагент = КонтрагентыИСчета.Контрагент
		|			И ДоговорыКредитовИДепозитов.БанковскийСчетКомиссии = КонтрагентыИСчета.СчетКонтрагента
		|			И (ДоговорыКредитовИДепозитов.БанковскийСчет = &БанковскийСчет)
		|			И (ДоговорыКредитовИДепозитов.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыДоговоровКонтрагентов.Действует))";
	
	Запрос.УстановитьПараметр("Таблица", ИсходнаяТаблица);
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Процедура ЗаполнитьПоКосвеннымДанным(СтрокиКЗагрузке, БанковскийСчет)
	
	Если СтрокиКЗагрузке.Количество() <> 0 Тогда
		
		Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
		МассивВзаимосвязанныхОрганизаций = Справочники.Организации.МассивВзаимосвязанныхОрганизаций(Организация);
		
		СтрокаТаблицы = СтрокиКЗагрузке.Получить(0);
		ТаблицаКЗагрузке = СтрокаТаблицы.Владелец();
		КонтрагентыИСчета = ТаблицаКЗагрузке.Скопировать(СтрокиКЗагрузке, "Контрагент, СчетКонтрагента");
		КонтрагентыИСчета.Свернуть("Контрагент, СчетКонтрагента");
		
		ОбъектыРасчетовПоДоговорам = СписокОбъектовРасчетовПоДоговорам(БанковскийСчет, Организация, КонтрагентыИСчета);
		ОбъектыРасчетовПоДоговорам.Индексы.Добавить("Контрагент, СчетКонтрагента");
		
		ДоговорыКредитовДепозитов = СписокДоговоровКредитовДепозитов(БанковскийСчет, КонтрагентыИСчета);
		ДоговорыКредитовДепозитов.Индексы.Добавить("Контрагент, СчетКонтрагента");
		
	КонецЕсли;
	
	Для каждого СтрокаДокумента Из СтрокиКЗагрузке Цикл
		
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) И ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации") Тогда // Собственная организация
			
			Если МассивВзаимосвязанныхОрганизаций.Найти(СтрокаДокумента.Контрагент) <> Неопределено Тогда
				СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ВнутренняяПередачаДенежныхСредств;
			Иначе
				
				Если СтрокаДокумента.Исходящий Тогда
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию;
				Иначе
					СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации;
				КонецЕсли;
				
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура ("Контрагент, СчетКонтрагента");
		ЗаполнитьЗначенияСвойств(ПараметрыОтбора, СтрокаДокумента);
		
		// Договор кредита/депозита
		НайденныеДоговорыКредитовДепозитов = ДоговорыКредитовДепозитов.НайтиСтроки(ПараметрыОтбора);
		
		Если НайденныеДоговорыКредитовДепозитов.Количество() = 1 Тогда
			
			Выборка = НайденныеДоговорыКредитовДепозитов.Получить(0);
			СтрокаДокумента.ДоговорКредитаДепозита = Выборка.Ссылка;
			СтрокаДокумента.ВидОперации = Справочники.ДоговорыКредитовИДепозитов.ОперацияПоХарактеруДоговора(
				Выборка.ХарактерДоговора,
				Не СтрокаДокумента.Исходящий);
			СтрокаДокумента.ВалютаВзаиморасчетов = Выборка.ВалютаВзаиморасчетов;
			Если Выборка.ХарактерДоговора = Перечисления.ХарактерыДоговоровФинансовыхИнструментов.КредитИлиЗайм Тогда
				
				Если СтрокаДокумента.Исходящий Тогда
					СтрокаДокумента.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДС;
					СтрокаДокумента.ТипСуммыКредитаДепозита = Выборка.ТипСуммыКредитаДепозита;
				Иначе
					СтрокаДокумента.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДСПоступленияВыдачи;
				КонецЕсли;
				
			Иначе
				
				Если СтрокаДокумента.Исходящий Тогда
					СтрокаДокумента.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДСПоступленияВыдачи;
				Иначе
					СтрокаДокумента.СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДДС;
					СтрокаДокумента.ТипСуммыКредитаДепозита = Выборка.ТипСуммыКредитаДепозита;
				КонецЕсли;
				
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
		// Договор с контрагентом
		Если Не ЗначениеЗаполнено(СтрокаДокумента.ОбъектРасчетов) Тогда
			
			ДополнитьПараметрыОтбораДоговора(ПараметрыОтбора);
			
			Если СтрокаДокумента.Исходящий Тогда
				ПараметрыОтбора.ТипРасчетов = Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком;
			КонецЕсли;
				
			ЗаполнитьОбъектРасчетовПлатежаПоДоговору(СтрокаДокумента, ОбъектыРасчетовПоДоговорам, ПараметрыОтбора);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьКассовыйДокумент(ДокументыКЗагрузке, ТаблицаДокументов, БанковскийСчет, Исходящие)
	
	Если Не ТаблицаДокументов.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаДокументов.НомерСтроки,
	|	ТаблицаДокументов.ДатаПроведения,
	|	ТаблицаДокументов.СуммаДокумента
	|
	|ПОМЕСТИТЬ ТаблицаДокументов
	|
	|ИЗ
	|	&ТаблицаДокументов КАК ТаблицаДокументов
	|";
	
	Запрос.УстановитьПараметр("ТаблицаДокументов", ТаблицаДокументов);
	Запрос.Выполнить();
	
	Если Исходящие Тогда
	
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	ТаблицаДокументов
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.ПриходныйКассовыйОрдер КАК ДанныеДокумента
		|	ПО
		|		ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзБанка)
		|		И ДанныеДокумента.БанковскийСчет = &БанковскийСчет
		|		И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ДЕНЬ) = ТаблицаДокументов.ДатаПроведения
		|		И ДанныеДокумента.СуммаДокумента = ТаблицаДокументов.СуммаДокумента
		|		И ДанныеДокумента.Проведен
		|ГДЕ
		|	НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
		|";
		
	Иначе
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаДокументов.НомерСтроки КАК НомерСтроки,
		|	ДанныеДокумента.Ссылка КАК Ссылка
		|ИЗ
		|	ТаблицаДокументов
		|	
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|		Документ.РасходныйКассовыйОрдер КАК ДанныеДокумента
		|	ПО
		|		ДанныеДокумента.ХозяйственнаяОперация = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.СдачаДенежныхСредствВБанк)
		|		И ДанныеДокумента.БанковскийСчет = &БанковскийСчет
		|		И НАЧАЛОПЕРИОДА(ДанныеДокумента.Дата, ДЕНЬ) = ТаблицаДокументов.ДатаПроведения
		|		И ДанныеДокумента.СуммаДокумента = ТаблицаДокументов.СуммаДокумента
		|		И ДанныеДокумента.Проведен
		|ГДЕ
		|	НЕ ДанныеДокумента.Ссылка ЕСТЬ NULL
		|";
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаДокумента = ДокументыКЗагрузке.Найти(Выборка.НомерСтроки, "НомерСтроки");
		Если СтрокаДокумента <> Неопределено Тогда
			СтрокаДокумента.КассовыйДокумент = Выборка.Ссылка;
			СтрокаДокумента.Загружать = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Функция СобственныеСчета(БанковскийСчет)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	БанковскиеСчета.Ссылка КАК Ссылка,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета,
	|	БанковскиеСчета.ВалютаДенежныхСредств КАК Валюта,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|			БанковскиеСчета.БИКБанка
	|		ИНАЧЕ
	|			БанковскиеСчета.Банк.Код
	|	КОНЕЦ КАК БИК,
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.РучноеИзменениеРеквизитовБанка ТОГДА
	|			БанковскиеСчета.СВИФТБанка
	|		ИНАЧЕ
	|			БанковскиеСчета.Банк.СВИФТБИК
	|	КОНЕЦ КАК СВИФТ
	|ИЗ
	|	Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчета
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|		Справочник.БанковскиеСчетаОрганизаций КАК БанковскиеСчетаОрганизаций
	|	ПО
	|		БанковскиеСчетаОрганизаций.Владелец = БанковскиеСчета.Владелец
	|		И БанковскиеСчетаОрганизаций.Ссылка = &БанковскийСчет
	|	
	|ГДЕ
	|	БанковскиеСчета.Ссылка <> &БанковскийСчет
	|	И НЕ БанковскиеСчета.ПометкаУдаления
	|";
	
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ДобавитьЗамечание(Буфер, НоваяСтрока, Разделитель = "")
	
	Если ПустаяСтрока(Буфер) Тогда
		Буфер = НоваяСтрока;
	Иначе
		Шаблон = НСтр("ru = '%1%2%3';
						|en = '%1%2%3'");
		Буфер = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Шаблон,
			Буфер,
			?(ПустаяСтрока(Разделитель), Символы.ПС, Разделитель),
			НоваяСтрока);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьАдресКонтрагента(Дата, Контрагент, ВидКонтрагента, Адрес)

	Если ПустаяСтрока(Адрес) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВидКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицо
		Или ВидКонтрагента = Перечисления.ЮрФизЛицо.ЮрЛицоНеРезидент Тогда
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			Контрагент,
			Адрес,
			Справочники.ВидыКонтактнойИнформации.ЮрАдресКонтрагента,
			Дата,
			Ложь);
	Иначе
		УправлениеКонтактнойИнформацией.ДобавитьКонтактнуюИнформацию(
			Контрагент,
			Адрес,
			Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента,
			Дата,
			Ложь);
	КонецЕсли;

КонецПроцедуры

Функция СформироватьОписаниеОшибкиЗаписиДокумента(ОписаниеОшибки)

	Причина = ?(
		ОписаниеОшибки.Причина <> Неопределено, СформироватьОписаниеОшибкиЗаписиДокумента(ОписаниеОшибки.Причина), "");
	
	Если ОписаниеОшибки.ИмяМодуля <> "" Тогда
		Описание = СтрШаблон(НСтр("ru = '%1
						|{%2(%3)}:%4';
						|en = '%1
						|{%2(%3)}:%4'"),
				ОписаниеОшибки.Описание,
				ОписаниеОшибки.ИмяМодуля,
				ОписаниеОшибки.НомерСтроки,
				ОписаниеОшибки.ИсходнаяСтрока);
	Иначе
		Описание = СтрШаблон("%1", ОписаниеОшибки.Описание);
	КонецЕсли;
	
	Возврат ?(Причина <> "", Причина + Символы.ПС, Причина) + Описание;

КонецФункции

Процедура ЗаполнитьСтавкуНДСПоДоговору(Объект, СтрокаРасшифровки)

	Если Не ЗначениеЗаполнено(СтрокаРасшифровки.ОбъектРасчетов) Тогда
		
		Если ЗначениеЗаполнено(Объект.Договор)
			И Не ЗначениеЗаполнено(Объект.НалогообложениеНДС) Тогда
				
			НалогообложениеПоДоговору = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Договор, "НалогообложениеНДС");
			УстановитьСвойство(Объект, "НалогообложениеНДС", НалогообложениеПоДоговору);
			
		КонецЕсли;
		
		УчетНДСУП.ЗаполнитьСтавкуНДСДляПлатежей(
			СтрокаРасшифровки.СтавкаНДС,
			Объект.НалогообложениеНДС,
			Объект.Организация,
			Объект.Дата);
		
	КонецЕсли;

КонецПроцедуры

Процедура ДополнитьПараметрыОтбораДоговора(ПараметрыОтбора)

	ПараметрыОтбора.Вставить("Приоритет", 1);
	ПараметрыОтбора.Вставить("ТипРасчетов", Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);

КонецПроцедуры

Процедура ЗаполнитьОбъектРасчетовПлатежаПоДоговору(СтрокаПлатежа, ОбъектыРасчетовПоДоговорам, Отбор)

	Если Отбор.Приоритет > 4 Тогда
		Возврат;
	КонецЕсли;
	
	СписокОбъектовРасчетов = ОбъектыРасчетовПоДоговорам.НайтиСтроки(Отбор);
	
	Если СписокОбъектовРасчетов.Количество() = 1 Тогда
		
		СтрокаПлатежа.ОбъектРасчетов = СписокОбъектовРасчетов[0].ОбъектРасчетов;
		СтрокаПлатежа.ДоговорССамозанятым = СписокОбъектовРасчетов[0].ДоговорССамозанятым;
		СтрокаПлатежа.Договор = СписокОбъектовРасчетов[0].Договор;
		
		Если ЗначениеЗаполнено(СписокОбъектовРасчетов[0].СтатьяДвиженияДенежныхСредств) Тогда
			СтрокаПлатежа.СтатьяДвиженияДенежныхСредств = СписокОбъектовРасчетов[0].СтатьяДвиженияДенежныхСредств;
		КонецЕсли;
		
	ИначеЕсли СписокОбъектовРасчетов.Количество() = 0 Тогда
		
		Отбор.Вставить("Приоритет", Отбор.Приоритет + 1);
		ЗаполнитьОбъектРасчетовПлатежаПоДоговору(СтрокаПлатежа, ОбъектыРасчетовПоДоговорам, Отбор);
		
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПрочееПоступлениеРасход(СтрокиКЗагрузке)

	Для Каждого СтрокаПлатежа Из СтрокиКЗагрузке Цикл
		СтрокаПлатежа.ВидОперации = ?(СтрокаПлатежа.Исходящий,
						Перечисления.ХозяйственныеОперации.ПрочаяВыдачаДенежныхСредств,
						Перечисления.ХозяйственныеОперации.ПрочееПоступлениеДенежныхСредств);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПлатежиКЗагрузкеПоУИП(Исходящий, БанковскийСчет, ДокументыКЗагрузке, СписокУИП)

	ДанныеВыбора = Новый СписокЗначений;
	
	СтруктураОтбора = ВзаиморасчетыКлиентСервер.СтруктураОтбораДанныхВыбора();
	СтруктураОтбора.Организация = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(БанковскийСчет, "Владелец");
	СтруктураОтбора.Текст       = СписокУИП.ВыгрузитьКолонку("Код");
	СтруктураОтбора.ТипРасчетов = 
		?(Исходящий, Перечисления.ТипыРасчетовСПартнерами.РасчетыСПоставщиком, Перечисления.ТипыРасчетовСПартнерами.РасчетыСКлиентом);
	СтруктураОтбора.УчитыватьФилиалы = Истина;
	
	ВзаиморасчетыСервер.ЗаполнитьДанныеВыбораУИП(
		ДанныеВыбора,
		СтруктураОтбора,
		Ложь);
	
	Для Каждого ЭлементДанных Из ДанныеВыбора Цикл
		
		Выборка = ЭлементДанных.Значение.Значение;
		
		Отбор = Новый Структура("Код", Выборка.ИдентификаторПлатежа);
		СтрокиТаблицы = СписокУИП.НайтиСтроки(Отбор);
		
		Для Каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
			
			СтрокаДокумента = ДокументыКЗагрузке.Найти(СтрокаТаблицы["НомерСтроки"], "НомерСтроки");
			
			Если СтрокаДокумента <> Неопределено Тогда
				
				СтрокаДокумента.НайденоОснованиеПлатежа = Истина;
				СтрокаДокумента.НайденКонтрагент = Истина;
				СтрокаДокумента.СоздаватьКонтрагента = Ложь;
				
				ЗаполнитьЗначенияСвойств(СтрокаДокумента, Выборка, "Контрагент, Партнер, ОснованиеПлатежа, ОбъектРасчетов");
				
				Если Не ЗначениеЗаполнено(СтрокаДокумента.ВидОперации) Тогда
					
					Если ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Контрагенты")
						И Не Исходящий Тогда
						СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
					ИначеЕсли ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации")
						И Не Исходящий Тогда
						СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеДенежныхСредствИзДругойОрганизации;
					ИначеЕсли ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Контрагенты")
						И Исходящий Тогда
						СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
					ИначеЕсли ТипЗнч(СтрокаДокумента.Контрагент) = Тип("СправочникСсылка.Организации")
						И Исходящий Тогда
						СтрокаДокумента.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаДенежныхСредствВДругуюОрганизацию;
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(СтрокаДокумента.ОбъектРасчетов) Тогда
					СтрокаДокумента.СтатьяДвиженияДенежныхСредств =
						ДенежныеСредстваСервер.СтатьяДвиженияДенежныхСредствОбъектаРасчетов(СтрокаДокумента.ОбъектРасчетов, СтрокаДокумента.ВидОперации);
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьВидОперацииПоУмолчанию(СтрокиКЗагрузке)

	Если СтрокиКЗагрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из СтрокиКЗагрузке Цикл
		
		Если СтрокаТаблицы.Исходящий Тогда
			СтрокаТаблицы.ВидОперации = Перечисления.ХозяйственныеОперации.ОплатаПоставщику;
		Иначе
			СтрокаТаблицы.ВидОперации = Перечисления.ХозяйственныеОперации.ПоступлениеОплатыОтКлиента;
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьСтатьюДвиженияДенежныхСредств(СтрокиКЗагрузке)

	Если СтрокиКЗагрузке.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = СтрокиКЗагрузке.Получить(0);
	ТаблицаКЗагрузке = СтрокаТаблицы.Владелец();
	
	ВидыОпераций = ТаблицаКЗагрузке.Скопировать(, "ВидОперации");
	ВидыОпераций.Свернуть("ВидОперации");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОперацииПлатежей.ВидОперации КАК ВидОперации
		|ПОМЕСТИТЬ ОперацииПлатежей
		|ИЗ
		|	&ВидыОпераций КАК ОперацииПлатежей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Статьи.Ссылка КАК СтатьяДвиженияДенежныхСредств,
		|	ОперацииПлатежей.ВидОперации КАК ХозяйственнаяОперация
		|ИЗ
		|	Справочник.СтатьиДвиженияДенежныхСредств КАК Статьи
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.СтатьиДвиженияДенежныхСредств.ХозяйственныеОперации КАК ОперацииСтатей
		|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОперацииПлатежей КАК ОперацииПлатежей
		|			ПО ОперацииСтатей.ХозяйственнаяОперация = ОперацииПлатежей.ВидОперации
		|		ПО Статьи.Ссылка = ОперацииСтатей.Ссылка
		|			И (НЕ Статьи.ПометкаУдаления)";
	
	Запрос.УстановитьПараметр("ВидыОпераций", ВидыОпераций);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Для Каждого СтрокаТаблицы Из СтрокиКЗагрузке Цикл
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ХозяйственнаяОперация", СтрокаТаблицы.ВидОперации);
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.СтатьяДвиженияДенежныхСредств) Тогда
			ПараметрыОтбора.Вставить("СтатьяДвиженияДенежныхСредств", СтрокаТаблицы.СтатьяДвиженияДенежныхСредств);
		КонецЕсли;
		
		Итерация = 0;
		СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
		
		Пока Выборка.НайтиСледующий(ПараметрыОтбора) Цикл
			
			Итерация = Итерация + 1;
			СтатьяДвиженияДенежныхСредств = Выборка.СтатьяДвиженияДенежныхСредств;
			
			Если Итерация > 1 Тогда
				СтатьяДвиженияДенежныхСредств = Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
		Выборка.Сбросить();
		
		СтрокаТаблицы.СтатьяДвиженияДенежныхСредств = СтатьяДвиженияДенежныхСредств;
		
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПоДоговоруПодключения(СтрокаПлатежа, ДокументОбъект)

	Если Не ЗначениеЗаполнено(СтрокаПлатежа.ДоговорЭквайринга) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСвойство(ДокументОбъект, "ДоговорЭквайринга", СтрокаПлатежа.ДоговорЭквайринга);
	РеквизитыДоговора = Справочники.ДоговорыЭквайринга.РеквизитыДоговора(ДокументОбъект.ДоговорЭквайринга);
	
	УстановитьСвойство(ДокументОбъект, "ОтражатьКомиссию",
		РеквизитыДоговора.СпособОтраженияКомиссии = Перечисления.СпособыОтраженияЭквайринговойКомиссии.ПриЗачислении);
	
	Если ДокументОбъект.ОтражатьКомиссию Тогда
		
		УстановитьСвойство(ДокументОбъект, "ВестиУчетКомиссииВоВзаиморасчетах", РеквизитыДоговора.ВестиУчетКомиссииВоВзаиморасчетах);
	
		Если РеквизитыДоговора.ВестиУчетКомиссииВоВзаиморасчетах Тогда
			УстановитьСвойство(ДокументОбъект, "ОбъектРасчетовКомиссии", РеквизитыДоговора.ОбъектРасчетовКомиссии);
		Иначе
			УстановитьСвойство(ДокументОбъект, "Подразделение", РеквизитыДоговора.ПодразделениеРасходов);
			УстановитьСвойство(ДокументОбъект, "СтатьяРасходов", РеквизитыДоговора.СтатьяРасходов);
			УстановитьСвойство(ДокументОбъект, "АналитикаРасходов", РеквизитыДоговора.АналитикаРасходов);
		КонецЕсли;
		
		УстановитьСвойство(ДокументОбъект, "СтатьяДвиженияДенежныхСредств", РеквизитыДоговора.СтатьяДвиженияДенежныхСредствПоступлениеОплаты);
		
		КлиентБанкЛокализация.ЗаполнитьСуммуКомиссии(ДокументОбъект, РеквизитыДоговора, ДокументОбъект.ОтражатьКомиссию);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
