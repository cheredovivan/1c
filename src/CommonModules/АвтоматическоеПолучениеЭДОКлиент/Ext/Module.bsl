// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// См. ОбщегоНазначенияКлиентПереопределяемый.ПередПериодическойОтправкойДанныхКлиентаНаСервер
//
Процедура ПередПериодическойОтправкойДанныхКлиентаНаСервер(Параметры) Экспорт
	
	Если Не ОбщегоНазначенияКлиент.ДоступноИспользованиеРазделенныхДанных() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ПараметрыОжидания();
	
	Если ПараметрыОжидания.Включено = Ложь
		Или Не ПериодОжиданияЗавершен(ПараметрыОжидания.ДатаПолучения) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОжидания.Включено = Истина Тогда
		
		ПараметрыСобытия = НовыеПараметрыСобытияПередПолучением();
		Оповестить(ИмяСобытияПередПолучением(), ПараметрыСобытия);
		
		Если Не ПараметрыСобытия.ЕстьОткрытыеФормы Тогда
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПараметрыОжидания.Включено = Неопределено
		И ИнтеграцияБСПБЭДКлиентСервер.УведомленияКлиентаДоступны() Тогда
		МенеджерУведомлений = Вычислить("УведомленияКлиента");
		Обработчик = Новый ОписаниеОповещения("ОповеститьОПолученииЭДО", АвтоматическоеПолучениеЭДОКлиент);
		//@skip-check dynamic-access-method-not-found
		МенеджерУведомлений.ПодключитьОбработчик(
			АвтоматическоеПолучениеЭДОКлиентСервер.КлючУведомленийКлиента(), Обработчик);
		ПараметрыОжидания.ИспользоватьУведомленияКлиента = Истина;
	КонецЕсли;
	
	ПараметрыОжидания.ДатаПолучения = ОбщегоНазначенияКлиент.ДатаСеанса();
	ПараметрыОтправки = НовыеПараметрыПериодическойОтправкиДанных();
	ПараметрыОтправки.Включено = ПараметрыОжидания.Включено;
	ПараметрыОтправки.ИспользоватьУведомленияКлиента = ПараметрыОжидания.ИспользоватьУведомленияКлиента;
	Параметры.Вставить(АвтоматическоеПолучениеЭДОКлиентСервер.КлючПараметраПериодическойОтправкиДанных(),
		ПараметрыОтправки);
	
КонецПроцедуры

// См. ОбщегоНазначенияКлиентПереопределяемый.ПослеПериодическогоПолученияДанныхКлиентаНаСервере
//
Процедура ПослеПериодическогоПолученияДанныхКлиентаНаСервере(Результаты) Экспорт
	
	Результат = Результаты[АвтоматическоеПолучениеЭДОКлиентСервер.КлючРезультатаПериодическойОтправкиДанных()]; // См. АвтоматическоеПолучениеЭДО.НовыйРезультатПериодическойОтправкиДанных
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ПараметрыОжидания();
	
	Если ПараметрыОжидания.ИспользоватьУведомленияКлиента
		И Не Результат.Включено И Результат.Включено <> ПараметрыОжидания.Включено Тогда
		МенеджерУведомлений = Вычислить("УведомленияКлиента");
		//@skip-check dynamic-access-method-not-found
		МенеджерУведомлений.ОтключитьОбработчик(АвтоматическоеПолучениеЭДОКлиентСервер.КлючУведомленийКлиента());
	КонецЕсли;
	
	ПараметрыОжидания.Включено = Результат.Включено;
	РезультатПолучения = Результат.РезультатПолучения; // См. АвтоматическоеПолучениеЭДО.ПолучитьЧерезИнтервал
	
	Если ПараметрыОжидания.ИспользоватьУведомленияКлиента Тогда
		Возврат;
	ИначеЕсли ЗначениеЗаполнено(РезультатПолучения) Тогда
		ОповеститьОПолученииЭДО(РезультатПолучения);
	ИначеЕсли ЗначениеЗаполнено(Результат.ДлительнаяОперация) Тогда
		ДлительнаяОперация = Результат.ДлительнаяОперация; // См. ДлительныеОперации.ВыполнитьФункцию
		Оповещение = Новый ОписаниеОповещения("ОповеститьОПолученииЭДОПослеДлительнойОперации",
			АвтоматическоеПолучениеЭДОКлиент);
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
		ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Использование

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат см. ВключитьЗавершение
//  ПараметрыАвторизацииВСервисеЭДО - см. УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО
Процедура ВключитьПоУчетнымЗаписямЭДО(ОповещениеОЗавершении, ПараметрыАвторизацииВСервисеЭДО) Экспорт
	
	КонтекстВключения = НовыйКонтекстВключенияПоУчетнымЗаписямЭДО();
	КонтекстВключения.ОповещениеОЗавершении = ОповещениеОЗавершении;
	КонтекстВключения.ПараметрыАвторизации = ПараметрыАвторизацииВСервисеЭДО;
	КонтекстВключения.КонтекстДиагностики.ЗаголовокОперации = ВидОперацииВключение();
	
	Оповещение = Новый ОписаниеОповещения("ВключитьПоУчетнымЗаписямЭДОПослеАвторизацииВСервисеЭДО",
		АвтоматическоеПолучениеЭДОКлиент, КонтекстВключения);
	УчетныеЗаписиЭДОКлиент.АвторизоватьсяВСервисеЭДО(Оповещение, ПараметрыАвторизацииВСервисеЭДО);
	
КонецПроцедуры

// Параметры:
//  ОповещениеОЗавершении - ОписаниеОповещения - результат см. ВключитьИспользованиеТокеновДоступаЗавершение
//  ФормаВладелец - Неопределено,ФормаКлиентскогоПриложения
Процедура ВключитьИспользованиеТокеновДоступа(ОповещениеОЗавершении, ФормаВладелец = Неопределено) Экспорт
	
	КонтекстВключения = НовыйКонтекстВключенияИспользованияТокеновДоступа();
	КонтекстВключения.ОповещениеОЗавершении = ОповещениеОЗавершении;
	Результат = КонтекстВключения.Результат;
	Результат.КонтекстДиагностики.ЗаголовокОперации = ВидОперацииВключение();
	
	Результат.Использовать = АвтоматическоеПолучениеЭДОВызовСервера.ВключитьИспользованиеТокеновДоступа(
		Результат.КонтекстДиагностики);
	Если Не Результат.Использовать Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВключитьИспользованиеТокеновДоступаЗавершение",
		АвтоматическоеПолучениеЭДОКлиент, КонтекстВключения);
	
	ПараметрыАвторизации = УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО();
	ПараметрыАвторизации.ФормаВладелец = ФормаВладелец;
	ПараметрыАвторизации.ВыводитьОкноОжидания = Истина;
	
	ВключитьПоУчетнымЗаписямЭДО(Оповещение, ПараметрыАвторизации);
	
КонецПроцедуры

Процедура Отключить() Экспорт
	
	АвтоматическоеПолучениеЭДОВызовСервера.Отключить();
	
	ПараметрыОжидания().Включено = Ложь;
	
	ОповеститьОбОтключении();
	
КонецПроцедуры

#КонецОбласти

#Область СобытияОповещения

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяСобытия - Строка
//  Параметр - Неопределено,Структура
Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр) Экспорт
	
	Если ИмяСобытия = ИмяСобытияПередПолучением() Тогда
		ОбработкаСобытияПередПолучением(Форма, Параметр);
	ИначеЕсли ИмяСобытия = ИмяСобытияПослеПолучения() Тогда 
		ОбработкаСобытияПослеПолучения(Форма, Параметр);
	ИначеЕсли ИмяСобытия = ИмяСобытияОтключение() Тогда
		ОбработкаСобытияОтключение(Форма, Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Предупреждение

// Параметры:
//  Форма                - ФормаКлиентскогоПриложения
//  Элемент              - Надпись
//  НавигационнаяСсылка  - Строка
//  СтандартнаяОбработка - Булево
Процедура ОбработкаНавигационнойСсылки(Форма, Элемент, НавигационнаяСсылка, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	Параметры  = ПараметрыРеквизитаФормы(Форма);
	Если Не ЗначениеЗаполнено(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если НавигационнаяСсылка = "ОткрытьУчетныеЗаписиЭДО" Тогда
		УчетныеЗаписиЭДОКлиент.ОткрытьСписокУчетныхЗаписей();
	ИначеЕсли Не ЗначениеЗаполнено(Параметры.ИдентификаторЭДО) Тогда
		Возврат;
	ИначеЕсли НавигационнаяСсылка = "ОткрытьЖурналРегистрации" Тогда
		Отбор = АвтоматическоеПолучениеЭДОВызовСервера.ОтборЖурналаРегистрации(Параметры.ИдентификаторЭДО);
		ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(Отбор, Форма);
	ИначеЕсли НавигационнаяСсылка = "ВключитьАвтоматическоеПолучениеЭДО" Тогда
		Контекст = Новый Структура;
		Контекст.Вставить("Форма", Форма);
		Контекст.Вставить("ИдентификаторЭДО", Параметры.ИдентификаторЭДО);
		Оповещение = Новый ОписаниеОповещения("ОбработкаНавигационнойСсылкиВключитьЗавершение",
			АвтоматическоеПолучениеЭДОКлиент, Контекст);
		ПараметрыАвторизации = УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО();
		ПараметрыАвторизации.ИдентификаторыЭДО = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(
			Параметры.ИдентификаторЭДО);
		ПараметрыАвторизации.ФормаВладелец = Форма;
		ВключитьПоУчетнымЗаписямЭДО(Оповещение, ПараметрыАвторизации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СостоянияПолучения

Процедура ОткрытьФормуСостоянийПолучения() Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ТолькоПросмотр", Истина);
	
	ОткрытьФорму("РегистрСведений.СостоянияАвтоматическогоПолученияЭДО.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

Процедура СброситьОтображениеСостояний() Экспорт
	
	ИзменениеСостояния = АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния();
	ОповеститьОСобытииПослеПолучения(ИзменениеСостояния);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Использование

// Возвращаемое значение:
//  Строка
Функция ВидОперацииВключение()
	
	Возврат НСтр("ru = 'Включение автоматического получения ЭДО';
				|en = 'Enable automatic EDI receipt'");
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. ВключитьПоУчетнымЗаписямЭДО.ОповещениеОЗавершении
//  * ПараметрыАвторизации - см. УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция НовыйКонтекстВключенияПоУчетнымЗаписямЭДО()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("ПараметрыАвторизации", УчетныеЗаписиЭДОКлиент.НовыеПараметрыАвторизацииВСервисеЭДО());
	Контекст.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики());
	Возврат Контекст;
КонецФункции

// Параметры:
//  РезультатАвторизации - см. УчетныеЗаписиЭДОКлиент.НовыйРезультатАвторизацииВСервисеЭДО
//  КонтекстВключения - см. НовыйКонтекстВключенияПоУчетнымЗаписямЭДО
Процедура ВключитьПоУчетнымЗаписямЭДОПослеАвторизацииВСервисеЭДО(РезультатАвторизации, КонтекстВключения) Экспорт
	
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(
		КонтекстВключения.КонтекстДиагностики, РезультатАвторизации.КонтекстДиагностики);
	
	ПараметрыАвторизации = КонтекстВключения.ПараметрыАвторизации;
	
	ИдентификаторФормы = Неопределено;
	Если ПараметрыАвторизации.ФормаВладелец <> Неопределено Тогда
		ИдентификаторФормы = ПараметрыАвторизации.ФормаВладелец.УникальныйИдентификатор;
	КонецЕсли;
	
	РасшифрованныеМаркеры = РезультатАвторизации.РасшифрованныеМаркеры;
	
	Если ПараметрыАвторизации.ИдентификаторыЭДО = Неопределено Тогда
		ИдентификаторыЭДО = ОбщегоНазначенияБЭДКлиент.ВыгрузитьКолонку(РасшифрованныеМаркеры, "Ключ"); // Массив из Строка
	Иначе
		ИдентификаторыЭДО = ПараметрыАвторизации.ИдентификаторыЭДО;
	КонецЕсли;
	
	ДлительнаяОперация = АвтоматическоеПолучениеЭДОВызовСервера.ВключитьПоУчетнымЗаписямЭДОВФоне(
		ИдентификаторыЭДО, РасшифрованныеМаркеры, ИдентификаторФормы);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ПараметрыАвторизации.ФормаВладелец);
	ПараметрыОжидания.ВыводитьОкноОжидания = ПараметрыАвторизации.ВыводитьОкноОжидания;
	
	Оповещение = Новый ОписаниеОповещения("ВключитьПоУчетнымЗаписямЭДОЗавершение", АвтоматическоеПолучениеЭДОКлиент, КонтекстВключения);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение, ПараметрыОжидания);
	
КонецПроцедуры

// Возвращаемое значение:
//  Структура:
//  * ИзменениеСостояния - Неопределено
//                       - см. АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция НовыйРезультатВключенияПоУчетнымЗаписямЭДО()
	
	Результат = Новый Структура;
	Результат.Вставить("ИзменениеСостояния", Неопределено);
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики());
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ДлительнаяОперация - Неопределено
//                     - см. ДлительныеОперации.ВыполнитьФункцию
//  КонтекстВключения - см. НовыйКонтекстВключенияПоУчетнымЗаписямЭДО
//
// Возвращаемое значение:
//  См. НовыйРезультатВключенияПоУчетнымЗаписямЭДО
Функция ВключитьПоУчетнымЗаписямЭДОЗавершение(ДлительнаяОперация, КонтекстВключения) Экспорт
	
	ИзменениеСостояния = АвтоматическоеПолучениеЭДОВызовСервера.ИзменениеСостоянияПослеВключенияПоУчетнымЗаписямЭДО(
		ДлительнаяОперация, КонтекстВключения.КонтекстДиагностики);
	
	Результат = НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	Результат.ИзменениеСостояния = ИзменениеСостояния;
	Результат.КонтекстДиагностики = КонтекстВключения.КонтекстДиагностики;
	
	Если ЗначениеЗаполнено(ИзменениеСостояния) Тогда
		ОповеститьОСобытииПослеПолучения(ИзменениеСостояния);
		ПодключитьПолучениеЧерезИнтервал();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(КонтекстВключения.ОповещениеОЗавершении, Результат);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ИзменениеСостояния - Неопределено
//                       - см. АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния
//  * КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  * Использовать - Булево
Функция НовыйРезультатВключенияИспользованияТокеновДоступа()
	
	Результат = НовыйРезультатВключенияПоУчетнымЗаписямЭДО();
	Результат.Вставить("Использовать", Ложь);
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ОповещениеОЗавершении - см. ВключитьПоУчетнымЗаписямЭДО.ОповещениеОЗавершении
//  * Результат - см. НовыйРезультатВключенияИспользованияТокеновДоступа
Функция НовыйКонтекстВключенияИспользованияТокеновДоступа()
	Контекст = Новый Структура;
	Контекст.Вставить("ОповещениеОЗавершении", Новый ОписаниеОповещения);
	Контекст.Вставить("Результат", НовыйРезультатВключенияИспользованияТокеновДоступа());
	Возврат Контекст;
КонецФункции

// Параметры:
//  РезультатВключенияПоУчетнымЗаписямЭДО - см. ВключитьПоУчетнымЗаписямЭДОЗавершение
//  КонтекстВключения - см. НовыйКонтекстВключенияИспользованияТокеновДоступа
// 
// Возвращаемое значение:
//  См. НовыйРезультатВключенияИспользованияТокеновДоступа
Функция ВключитьИспользованиеТокеновДоступаЗавершение(РезультатВключенияПоУчетнымЗаписямЭДО, КонтекстВключения) Экспорт
	
	Результат = КонтекстВключения.Результат;
	Результат.ИзменениеСостояния = РезультатВключенияПоУчетнымЗаписямЭДО.ИзменениеСостояния;
	ОбработкаНеисправностейБЭДКлиентСервер.ДополнитьКонтекстДиагностики(Результат.КонтекстДиагностики,
		РезультатВключенияПоУчетнымЗаписямЭДО.КонтекстДиагностики);
	
	ВыполнитьОбработкуОповещения(КонтекстВключения.ОповещениеОЗавершении, Результат);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область ПроверкаДоступности

// Параметры:
//  КонтекстДиагностики - см. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  ДополнительныеПараметры - Структура:
//  * ИдентификаторВидаОшибки - Строка
Процедура ОбработчикОшибкиПроверкаПодписейНедоступна(КонтекстДиагностики, ДополнительныеПараметры) Экспорт
	
	АвтоматическоеПолучениеЭДОВызовСервера.ВключитьПроверкуПодписейНаСервере();
	ОбновитьИнтерфейс();
	
	ВидОшибки = ОбработкаНеисправностейБЭДКлиентСервер.НовоеОписаниеВидаОшибки();
	ВидОшибки.Идентификатор = ДополнительныеПараметры.ИдентификаторВидаОшибки;
	ВидыОшибок = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ВидОшибки);
	ОбработкаНеисправностейБЭДКлиент.ОповеститьОбИсправленииОшибок(ВидыОшибок);
	
КонецПроцедуры

#КонецОбласти

#Область СобытияОповещения

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияПередПолучением()
	Возврат АвтоматическоеПолучениеЭДОКлиентСервер.ПолноеИмяПодсистемы() + ".ПередПолучением";
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ЕстьОткрытыеФормы - Булево
Функция НовыеПараметрыСобытияПередПолучением()
	Параметры = Новый Структура;
	Параметры.Вставить("ЕстьОткрытыеФормы", Ложь);
	Возврат Параметры;
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  Параметр - См. НовыеПараметрыСобытияПередПолучением
Процедура ОбработкаСобытияПередПолучением(Форма, Параметр)
	Если Форма.Открыта() Тогда
		Параметр.ЕстьОткрытыеФормы = Истина;
	КонецЕсли;
КонецПроцедуры

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияПослеПолучения()
	Возврат АвтоматическоеПолучениеЭДОКлиентСервер.ПолноеИмяПодсистемы() + ".ПослеПолучения";
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ИзменениеСостояния - см. АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния
Функция НовыеПараметрыСобытияПослеПолучения()
	Параметры = Новый Структура;
	Параметры.Вставить("ИзменениеСостояния", АвтоматическоеПолучениеЭДОКлиентСервер.НовоеИзменениеСостояния());
	Возврат Параметры;
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Параметр - См. НовыеПараметрыСобытияПослеПолучения
Процедура ОбработкаСобытияПослеПолучения(Форма, Параметр)
	УстановитьВидимостьПредупреждения(Форма, Параметр.ИзменениеСостояния);
КонецПроцедуры

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияОтключение()
	Возврат АвтоматическоеПолучениеЭДОКлиентСервер.ПолноеИмяПодсистемы() + ".Отключение";
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  Параметр - Неопределено
Процедура ОбработкаСобытияОтключение(Форма, Параметр)
	
	Параметры = ПараметрыРеквизитаФормы(Форма);
	Если Не ЗначениеЗаполнено(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Элементы = Форма.Элементы;
	
	Если ЗначениеЗаполнено(Параметры.ИмяГруппыПредупреждение) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
			Элементы, Параметры.ИмяГруппыПредупреждение, "Видимость", Ложь);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИмяСписка) Тогда
		Элементы[Параметры.ИмяСписка].Обновить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Получение

Процедура ПодключитьПолучениеЧерезИнтервал()
	ПараметрыОжидания().Включено = Неопределено;
КонецПроцедуры

// Возвращаемое значение:
//  Строка
Функция КлючПараметровОжидания()
	Возврат АвтоматическоеПолучениеЭДОКлиентСервер.ПолноеИмяПодсистемы();
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Включено - Неопределено,Булево
//  * ДатаПолучения - Дата
//  * ИспользоватьУведомленияКлиента - Булево
Функция НовыеПараметрыОжидания()
	Параметры = Новый Структура;
	Параметры.Вставить("Включено", Неопределено);
	Параметры.Вставить("ДатаПолучения", '00010101');
	Параметры.Вставить("ИспользоватьУведомленияКлиента", Ложь);
	Возврат Параметры;
КонецФункции

// Возвращаемое значение:
//  См. НовыеПараметрыОжидания
Функция ПараметрыОжидания()
	ПараметрыПолучения = ПараметрыПриложения[КлючПараметровОжидания()]; // См. НовыеПараметрыОжидания
	Если ПараметрыПолучения = Неопределено Тогда
		ПараметрыПолучения = НовыеПараметрыОжидания();
		//@skip-check dynamic-access-method-not-found
		ПараметрыПриложения.Вставить(КлючПараметровОжидания(), ПараметрыПолучения);
	КонецЕсли;
	Возврат ПараметрыПолучения;
КонецФункции

// Параметры:
//  Дата - Дата
// 
// Возвращаемое значение:
//  Булево
Функция ПериодОжиданияЗавершен(Дата)
	ТекущаяДата = ОбщегоНазначенияКлиент.ДатаСеанса();
	Интервал = АвтоматическоеПолучениеЭДОКлиентСервер.Интервал();
	Если ЗначениеЗаполнено(Дата)
		И Интервал > ТекущаяДата - Дата Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * Включено - Неопределено,Булево
//  * ИспользоватьУведомленияКлиента - Булево
Функция НовыеПараметрыПериодическойОтправкиДанных() Экспорт
	Параметры = Новый Структура;
	Параметры.Вставить("Включено", Неопределено);
	Параметры.Вставить("ИспользоватьУведомленияКлиента", Ложь);
	Возврат Параметры;
КонецФункции

// Параметры:
//  РезультатПолучения - см. АвтоматическоеПолучениеЭДО.НовыйРезультатПолучения
//  Контекст - Неопределено
Процедура ОповеститьОПолученииЭДО(РезультатПолучения, Контекст = Неопределено) Экспорт
	
	Если Не ЗначениеЗаполнено(РезультатПолучения) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыОжидания = ПараметрыОжидания();
	
	Если ЗначениеЗаполнено(РезультатПолучения.ИзменениеСостояния) Тогда
		ОповеститьОСобытииПослеПолучения(РезультатПолучения.ИзменениеСостояния);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РезультатПолучения.ИтогДействийПоЭДО.ОбработанныеДокументы) Тогда
		Результат = Новый Структура;
		Результат.Вставить("Итог", РезультатПолучения.ИтогДействийПоЭДО);
		Результат.Вставить("ОшибкиФормирования", Новый Массив);
		Результат.Вставить("КонтекстДиагностики", РезультатПолучения.КонтекстДиагностики);
		ИнтерфейсДокументовЭДОКлиент.ЗавершитьОтправкуПолучениеДокументовЭДО(Результат, Новый ОписаниеОповещения);
	ИначеЕсли Не ПараметрыОжидания.ИспользоватьУведомленияКлиента Тогда 
		Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ДлительнаяОперация - См. ДлительныеОперации.ВыполнитьФункцию
//  Контекст - Неопределено
Процедура ОповеститьОПолученииЭДОПослеДлительнойОперации(ДлительнаяОперация, Контекст = Неопределено) Экспорт
	
	Если ДлительнаяОперация = Неопределено
		Или ДлительнаяОперация.Статус <> "Выполнено" Тогда
		Возврат;
	КонецЕсли;
	
	РезультатПолучения = АвтоматическоеПолучениеЭДОВызовСервера.РезультатПолученияЧерезИнтервал(
		ДлительнаяОперация.АдресРезультата);
	
	ОповеститьОПолученииЭДО(РезультатПолучения);
	
КонецПроцедуры

// Параметры:
//  ИзменениеСостояния - Структура:
//  * ЕстьПредупреждение - Булево
//  * СостояниеПоИдентификаторамЭДО - см. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
Процедура ОповеститьОСобытииПослеПолучения(ИзменениеСостояния)
	
	ПараметрыСобытия = НовыеПараметрыСобытияПослеПолучения();
	ПараметрыСобытия.ИзменениеСостояния = ИзменениеСостояния;
	Оповестить(ИмяСобытияПослеПолучения(), ПараметрыСобытия);
	
КонецПроцедуры

#КонецОбласти

#Область Предупреждение

// Возвращаемое значение:
//  Строка
Функция ИмяРеквизитаПараметровФормы()
	Возврат "ПараметрыАвтоматическогоПолученияЭДО";
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  - Неопределено
//  - См. АвтоматическоеПолучениеЭДО.НовыеПараметрыРеквизитаФормы
Функция ПараметрыРеквизитаФормы(Форма)
	Реквизиты = Новый Структура(ИмяРеквизитаПараметровФормы());
	ЗаполнитьЗначенияСвойств(Реквизиты, Форма);
	Возврат Реквизиты[ИмяРеквизитаПараметровФормы()];
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИзменениеСостояния - Структура:
//  * ЕстьПредупреждения - См. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.ЕстьПредупреждения
//  * СостояниеПоИдентификаторамЭДО - См. РегистрыСведений.СостоянияАвтоматическогоПолученияЭДО.СостояниеПоИдентификаторамЭДО
Процедура УстановитьВидимостьПредупреждения(Форма, ИзменениеСостояния)
	
	Параметры = ПараметрыРеквизитаФормы(Форма);
	Если Не ЗначениеЗаполнено(Параметры) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.ИдентификаторЭДО) Тогда
		Состояние = ИзменениеСостояния.СостояниеПоИдентификаторамЭДО[Параметры.ИдентификаторЭДО];
		АвтоматическоеПолучениеЭДОКлиентСервер.УстановитьВидимостьПредупрежденияВФормеЭлемента(
			Форма, Состояние, Параметры);
	Иначе
		АвтоматическоеПолучениеЭДОКлиентСервер.УстановитьВидимостьПредупрежденияВФормеСписка(
			Форма, ИзменениеСостояния.ЕстьПредупреждения, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  РезультатВключения - см. ВключитьПоУчетнымЗаписямЭДОЗавершение
//  Контекст - Структура:
//  * Форма - ФормаКлиентскогоПриложения
//  * ИдентификаторЭДО - Строка
Процедура ОбработкаНавигационнойСсылкиВключитьЗавершение(РезультатВключения, Контекст) Экспорт
	
	Если РезультатВключения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатВключения.КонтекстДиагностики);
	
КонецПроцедуры

Процедура ОповеститьОбОтключении();
	
	Оповестить(ИмяСобытияОтключение());
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
