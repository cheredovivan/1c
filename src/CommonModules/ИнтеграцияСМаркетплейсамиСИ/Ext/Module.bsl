// @strict-types
#Область ПрограммныйИнтерфейс

// Дополняет параметры открытия формы.
// 
// Параметры:
//  ПараметрыФормы - См. ИнтеграцияСМаркетплейсамиСИКлиент.НовыеПараметрыФормыСервисаМаркетплейсы
Процедура ПараметрыСервисаМаркетплейсы(ПараметрыФормы) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		Если ОбщегоНазначения.РазделениеВключено() Тогда
			Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
				ВызватьИсключение(НСтр("ru = 'Операция в неразделенном сеансе не поддерживается.';
										|en = 'The operation is not supported in a shared session.'"));
			КонецЕсли;
			ПараметрыФормы.ПараметрыСервиса.ИнтернетПоддержкаПодключена = Истина;
		Иначе
			ПараметрыФормы.ПараметрыСервиса.ИнтернетПоддержкаПодключена =
				МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
		КонецЕсли;
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
								|en = 'Online support service is disabled.'");
	КонецЕсли;
	
	ПараметрыФормы.ПараметрыСервиса.ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	
КонецПроцедуры

// См. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.СинхронизацияССервисомИнтеграции.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы.ИмяМетода);
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы.ИмяМетода);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

#Область ДлительныеОперации

// Запустить выполнение фонового задания.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, из которой произведен вызов
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
Процедура ВыполнитьДлительныеОперации(Форма, ПараметрыОперации) Экспорт
	
	ПолноеИмяМетода = СтрШаблон("ИнтеграцияСМаркетплейсамиСИ.%1", ПараметрыОперации.Метод);
	
	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Форма.УникальныйИдентификатор);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(Форма.УникальныйИдентификатор);
	ЗаполнитьЗначенияСвойств(ПараметрыВыполнения, ПараметрыОперации);
	ПараметрыВыполнения.АдресРезультата = АдресРезультата;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = СтрШаблон("%1. %2",
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеВидаМаркетплейса(ПараметрыОперации.ВидМаркетплейса),
		ПараметрыОперации.ТекстСообщения);
	
	ПараметрыОперации.Вставить("ИдентификаторНазначения", Форма.УникальныйИдентификатор);
	
	Если ПараметрыОперации.ЭтоФункция Тогда
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, ПолноеИмяМетода, ПараметрыОперации, АдресРезультата);
	Иначе
		ФоновоеЗадание = ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыВыполнения, ПолноеИмяМетода, ПараметрыОперации, АдресРезультата);
	КонецЕсли;
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		Операция.Значение = Неопределено;
	КонецЦикла;
	
	Если ТипЗнч(ФоновоеЗадание) = Тип("Структура") Тогда
		ФоновоеЗадание.Вставить("Очередь", ПараметрыОперации.Очередь);
		ПараметрыОперации.ФоновоеЗадание = ФоновоеЗадание;
	КонецЕсли;
	
КонецПроцедуры

// Проверяет возможность запуска фонового задания. Проверяет подключения к интернет поддержке,
// заполненность очереди выполняемых методов и существование активного фонового задания с переданными параметрами.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, из которой произведен вызов
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
//  ИнтернетПоддержкаПодключена - Булево - Признак подключения к интернет поддержке
// 
// Возвращаемое значение:
//  Булево - Признак возможности запуска фонового задания
Функция ВозможенЗапускФоновогоЗадания(Форма, ПараметрыОперации, ИнтернетПоддержкаПодключена) Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		ИнтернетПоддержкаПодключена = МодульИнтернетПоддержкаПользователей.ЗаполненыДанныеАутентификацииПользователяИнтернетПоддержки();
		Если Не ИнтернетПоддержкаПодключена Тогда
			Возврат Ложь;
		КонецЕсли;
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
								|en = 'Online support service is disabled.'");
	КонецЕсли;
	
	Если ПараметрыОперации.Очередь.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыОперации.КлючФоновогоЗадания) Тогда
		ПараметрыОперации.КлючФоновогоЗадания = КлючФоновогоЗадания(Форма, ПараметрыОперации);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ПараметрыОперации.КлючВыполнения) Тогда
		ПараметрыОперации.КлючВыполнения = Форма.ИмяФормы;
	КонецЕсли;
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Ключ", ПараметрыОперации.КлючФоновогоЗадания);
	ПараметрыЗадания.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	АктивныеФоновыеЗадания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПараметрыЗадания);
	
	Если ПараметрыОперации.ОтменитьАктивные И АктивныеФоновыеЗадания.Количество() > 0 Тогда
		Для Каждого АктивноеФоновоеЗадание Из АктивныеФоновыеЗадания Цикл
			ДлительныеОперации.ОтменитьВыполнениеЗадания(АктивноеФоновоеЗадание.УникальныйИдентификатор);
		КонецЦикла;
		// Формирование нового ключа фонового задания, чтобы произошел запуск актуального фонового задания,
		// потому что отмена активных заданий происходит с задержкой
		ПараметрыОперации.КлючФоновогоЗадания = Новый УникальныйИдентификатор;
		Возврат Истина;
	КонецЕсли;
	
	Возврат АктивныеФоновыеЗадания.Количество() = 0;
	
КонецФункции

// Формирует ключ фонового задания.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, из которой произведен вызов:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
// 
// Возвращаемое значение:
//  Строка - Ключ фонового задания
Функция КлючФоновогоЗадания(Форма, ПараметрыОперации) Экспорт
	
	МассивДанных = Новый Массив; // Массив из Структура, ЛюбаяСсылка, ПеречислениеСсылка.ВидыМаркетплейсов, Строка
	МассивДанных.Добавить(Форма.ИмяФормы);
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		МассивДанных.Добавить(Операция.Представление);
	КонецЦикла;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ВидМаркетплейса") Тогда
		МассивДанных.Добавить(Форма.ВидМаркетплейса);
	КонецЕсли;
	
	МассивДанных.Добавить(Пользователи.ТекущийПользователь());
	
	Возврат ОбщегоНазначения.КонтрольнаяСуммаСтрокой(МассивДанных);
	
КонецФункции

// Менеджер длительных операций.
// 
// Параметры:
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
//  АдресРезультата - Строка - Адрес временного хранилища, в который помещаются результаты выполненных методов
//                             типов СписокЗначений из Произвольный, Неопределено.
Процедура МенеджерДлительныхОпераций(Знач ПараметрыОперации, Знач АдресРезультата) Экспорт
	
	// Коды состояний, указывающие на наличие проблем со связью с сервисом интеграции
	КодыСостоянияОтказ = Новый Массив; // Массив из Число
	КодыСостоянияОтказ.Добавить(404); // Неудачная попытка соединения
	КодыСостоянияОтказ.Добавить(500); // Внутренняя ошибка
	КодыСостоянияОтказ.Добавить(503); // Сервис остановлен
	
	ПередВыполнениемДлительныхОпераций(ПараметрыОперации);
	
	Очередь = Новый СписокЗначений; // СписокЗначений из Структура
	
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		
		РезультатОперации = ВыполнитьДлительнуюОперацию(Операция, ПараметрыОперации, Очередь);
		Очередь.Добавить(РезультатОперации, Операция.Представление);
		
		Если КодыСостоянияОтказ.Найти(РезультатОперации.КодСостояния) <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Результат = Новый Структура;
	Результат.Вставить("Очередь", Очередь);
	
	ПослеВыполненияДлительныхОпераций(ПараметрыОперации, Результат);
	
	ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	
	Если ЗначениеЗаполнено(ПараметрыОперации.СеансыУведомленияКлиента) Тогда
		
		Если ПараметрыОперации.КлючФоновогоЗадания <> Неопределено Тогда
			ПараметрыОтбора = Новый Структура("Ключ", ПараметрыОперации.КлючФоновогоЗадания);
			НайденныеФЗ = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПараметрыОтбора);
		
			Если НайденныеФЗ.Количество() = 1 Тогда

				ФоновоеЗадание = Новый Структура;
				ФоновоеЗадание.Вставить("Статус", "Выполняется");
				ФоновоеЗадание.Вставить("АдресРезультата", АдресРезультата);
				ФоновоеЗадание.Вставить("ИдентификаторЗадания", НайденныеФЗ[0].УникальныйИдентификатор);
				
				ПараметрыОперации.ФоновоеЗадание = ФоновоеЗадание;
				
				УведомленияКлиента.ОтправитьУведомление("Маркетплейсы_ЗавершениеМенеджерДлительныхОпераций", ПараметрыОперации,
					ПараметрыОперации.СеансыУведомленияКлиента);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССервисом

#Область МетодыСервиса

// Установить соединение.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеПараметрыУстановитьСоединение
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция УстановитьСоединение(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУстановитьСоединение(ПараметрыМетода);
	
	НовыеДанныеИнтеграции = Новый Структура;
	НовыеДанныеИнтеграции.Вставить("id_token", "");
	НовыеДанныеИнтеграции.Вставить("client_id", "");
	НовыеДанныеИнтеграции.Вставить("client_secret", "");
	НовыеДанныеИнтеграции.Вставить("ДатаОбновленияТокена", Дата(1, 1, 1));
	НовыеДанныеИнтеграции.Вставить("Логин", ПараметрыМетода.Логин);
	
	Если Результат.КодСостояния = 200 Тогда
		ЗаполнитьЗначенияСвойств(НовыеДанныеИнтеграции, Результат.Ответ);
	КонецЕсли;
	
	ПараметрыМетода.client_id = НовыеДанныеИнтеграции.client_id;
	
	ЗаписатьДанныеВБезопасноеХранилище( , НовыеДанныеИнтеграции, ПараметрыМетода.Логин);
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Обновление токена доступа.
// 
// Параметры:
//  ДанныеИнтеграции - См. НовыеДанныеИнтеграции
//  Ошибки - Массив из Строка - Ошибки
// 
// Возвращаемое значение:
//  См. НовыеПараметрыОбновитьТокенДоступа
Функция ОбновитьТокенДоступа(ДанныеИнтеграции, Ошибки) Экспорт
	
	ПараметрыМетода = НовыеПараметрыОбновитьТокенДоступа();
	ЗаполнитьЗначенияСвойств(ПараметрыМетода, ДанныеИнтеграции);
	
	Результат = ЗапросОбновитьТокенДоступа(ПараметрыМетода);
	
	Если Результат.КодСостояния = 200 Тогда
		//@skip-check property-return-type
		ПараметрыМетода.id_token = СтрШаблон("Bearer %1", Результат.Ответ.id_token);
	Иначе
		ПараметрыМетода.id_token = "";
		Ошибки.Добавить(НСтр("ru = 'Ошибка подключения к сервису интеграции, выполните подключение повторно.';
							|en = 'Error connecting to the integration service. Please reconnect.'"));
	КонецЕсли;
	ПараметрыМетода.ДатаОбновленияТокена = ТекущаяДатаСеанса();
	ДанныеИнтеграции = ЗаписатьДанныеВБезопасноеХранилище( , ПараметрыМетода, ДанныеИнтеграции.Логин);
	
	Возврат ПараметрыМетода;
	
КонецФункции

// Получить идентификатор ИТС.
// 
// Параметры:
//  ДанныеИнтеграции - См. НовыеДанныеИнтеграции
// 
// Возвращаемое значение:
//  Строка - Идентификатор ИТС
Функция ПолучитьИдентификаторИТС(ДанныеИнтеграции = Неопределено) Экспорт
	
	ИдентификаторИТС = "";
	
	Если ДанныеИнтеграции = Неопределено Тогда
		ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Если Не ПустаяСтрока(ДанныеИнтеграции.ИдентификаторИТС) Тогда
		Возврат ДанныеИнтеграции.ИдентификаторИТС;
	КонецЕсли;
	
	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Тикет", ТикетАутентификацииНаПорталеПоддержки());
	ПараметрыМетода.Вставить("ИмяМетода", ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьДанныеПользователяИТС());
	ПараметрыМетода.Вставить("ВидМаркетплейса", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	
	Результат = ЗапросПолучитьДанныеПользователяИТС(ПараметрыМетода); //см. НовыйРезультатМетодаСервиса
	
	Если Результат.КодСостояния = 200 Тогда
		
		ИдентификаторИТС = ПолучитьЗначение(Результат.Ответ, "subscriberUeid", "", "");
		ИдентификаторИТС = ?(ПустаяСтрока(ИдентификаторИТС), ПолучитьЗначение(Результат.Ответ, "uin", , ""), ИдентификаторИТС);
		
		Если Не ПустаяСтрока(ИдентификаторИТС) Тогда
			ЗаписатьДанныеВБезопасноеХранилище( , Новый Структура("ИдентификаторИТС", ИдентификаторИТС), ЛогинПользователяИТС());
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ИдентификаторИТС;
	
КонецФункции

// @skip-check statement-type-change
// @skip-check invocation-parameter-type-intersect
// 
// Параметры:
//  ПараметрыМетода - см. НовыеПараметрыПолучитьПодключения
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//   См. НовыйРезультатМетодаСервиса
Функция ПолучитьПодключения(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросПолучитьПодключения(ПараметрыМетода);
	
	Ответ = Новый Соответствие;
	
	Если Результат.КодСостояния = 200 Тогда
		
		Подключения = ПолучитьЗначение(Результат, "Ответ", "Массив", Новый Массив); // Массив из Соответствие
		Для Каждого Элемент Из Подключения Цикл
			ПараметрыПодключения = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыПодключения();
			ПараметрыПодключения.client_id = Элемент["clientId"];
			ПараметрыПодключения.КлючПодключения = Элемент["keyConnect"];
			ПараметрыПодключения.ИдентификаторИнформационнойБазы = Элемент["baseId"];
			ПараметрыПодключения.Представление = Элемент["name"];
			ПараметрыПодключения.ДатаПодключенияСИ = СтрокаВДату(Элемент["date"]);
			Ответ.Вставить(ПараметрыПодключения.КлючПодключения, ПараметрыПодключения);
		КонецЦикла;
		
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// @skip-check statement-type-change
// @skip-check invocation-parameter-type-intersect
// 
// Параметры:
//  ПараметрыМетода - см. НовыеПараметрыУстановитьСоединение
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса 
Функция УстановитьПодключение(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУстановитьПодключение(ПараметрыМетода);
	
	Ответ = Новый Структура("ДатаПодключенияСИ, КлючПодключения", Дата(1, 1, 1), "");
	
	Если Результат.КодСостояния = 200 Тогда
		Ответ.ДатаПодключенияСИ = СтрокаВДату(Результат.Ответ["date"]);
		Ответ.КлючПодключения = Результат.Ответ["keyConnect"];
		ЗаписатьДанныеВБезопасноеХранилище(ПараметрыМетода.ВидМаркетплейса, Ответ, ПараметрыМетода.Логин);
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Удалить подключение.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеПараметрыУдалитьПодключение
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция УдалитьПодключение(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУдалитьПодключение(ПараметрыМетода);
	
	Ответ = Новый Структура;
	Ответ.Вставить("КлючПодключения", ПараметрыМетода.КлючПодключения);
	Ответ.Вставить("ЭтоТекущийКлючПодключения", ПараметрыМетода.ЭтоТекущийКлючПодключения);
	
	Если Результат.КодСостояния = 200 Тогда
		Если ПараметрыМетода.ЭтоТекущийКлючПодключения Тогда
			ЗаписатьДанныеВБезопасноеХранилище(ПараметрыМетода.ВидМаркетплейса,
				Новый Структура("КлючПодключения, ДатаПодключенияСИ", "", Дата(1, 1, 1)), ПараметрыМетода.Логин);
		КонецЕсли;
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Проверка доступности сервиса интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПроверитьДоступностьСервисаИнтеграции
//  АдресРезультата - Строка - адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ПроверитьДоступностьСервисаИнтеграции(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросПроверитьДоступностьСервисаИнтеграции(ПараметрыМетода); // см. НовыйРезультатМетодаСервиса
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Проверка доступности сервиса маркетплейса
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПроверитьДоступностьСервисаМаркетплейса
//  АдресРезультата - Строка - адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ПроверитьДоступностьСервисаМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросПроверитьДоступностьСервисаМаркетплейса(ПараметрыМетода);
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Получение списка учетных записей, зарегистрированных в сервисе интеграции для пользователя ИТС
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьУчетныеЗаписи
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ПолучитьУчетныеЗаписи(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросПолучитьУчетныеЗаписи(ПараметрыМетода);
	
	Ответ = Новый Структура;
	
	УчетныеЗаписи = Новый Массив; // Массив из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
	
	Если Результат.КодСостояния = 200 Тогда
		
		//@skip-check invocation-parameter-type-intersect
		Для Каждого Элемент Из Результат.Ответ Цикл
			
			Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи();
			
			Состояние = ПолучитьЗначение(Элемент, "state", "Число", 0); // Число
			Карточка.Состояние = Состояние;
			Карточка.Ошибки = ПолучитьЗначение(Элемент, "error", "Строка", "");
			Карточка.Представление = ПолучитьЗначение(Элемент, "name", "Строка", "");
			Карточка.УчетнаяЗаписьИдентификатор = ПолучитьЗначение(Элемент, "id", "Строка", "");
			Карточка.ХешКлючейАвторизации = ПолучитьЗначение(Элемент, "hashAuth", "Строка", "");
			Токены = ПолучитьЗначение(Элемент, "tokens", "Массив", Новый Массив);
			Для Каждого ОписаниеТокена Из Токены Цикл
				СтруктураТокена = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйОписаниеТокена();
				СтруктураТокена.ХешТокена = ПолучитьЗначение(ОписаниеТокена, "hashToken", "Строка", "");
				СтруктураТокена.Категории = ПолучитьЗначение(ОписаниеТокена, "categories", "Строка", "");
				СтруктураТокена.ДействуетДо = ПолучитьЗначение(ОписаниеТокена, "dateEnd", "Дата", Дата(1,1,1));
				СтруктураТокена.Ошибка = ПолучитьЗначение(ОписаниеТокена, "error", "Строка", "");
				Карточка.Токены.Добавить(СтруктураТокена);
			КонецЦикла;
			Карточка.Функциональность = ПолучитьЗначение(Элемент, "functionality", "Массив", Новый Массив);
			Карточка.АвтоКоррекцияОстатков = ПолучитьЗначение(Элемент, "stockAutoCorrection", "Булево", Ложь);
			Карточка.ИспользоватьИнтеграциюПоОстаткам = ПолучитьЗначение(Элемент, "stockUpload", "Булево", Ложь);
			Карточка.АвтоКоррекцияЦен = ПолучитьЗначение(Элемент, "priceAutoCorrection", "Булево", Ложь);
			Карточка.ИспользоватьИнтеграциюПоЦенам = ПолучитьЗначение(Элемент, "priceUpload", "Булево", Ложь);
			Карточка.РазрешенаВыгрузкаЦенНаМаркетплейс = ПолучитьЗначение(Элемент, "priceUploadExchange", "Булево", Ложь);
			
			ИдентификаторОрганизации = ПолучитьЗначение(Элемент, "organizationId", "Строка", "");
			Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторОрганизации) Тогда
				Организация = XMLЗначение(Тип("СправочникСсылка.Организации"), ИдентификаторОрганизации); // СправочникСсылка.Организации
				Если ОбщегоНазначения.СсылкаСуществует(Организация) Тогда
					Карточка.Организация = Организация;
				КонецЕсли;
			КонецЕсли;
			
			ИдентификаторПартнера = ПолучитьЗначение(Элемент, "partnerId", "Строка", "");
			Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторПартнера) Тогда
				Партнер = XMLЗначение(Тип("СправочникСсылка.Партнеры"), ИдентификаторПартнера); // СправочникСсылка.Партнеры
				Если ОбщегоНазначения.СсылкаСуществует(Партнер) Тогда
					Карточка.Партнер = Партнер;
				КонецЕсли;
			КонецЕсли;
			
			Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(Карточка.УчетнаяЗаписьИдентификатор) Тогда
				
				УчетнаяЗаписьСсылка = XMLЗначение(Тип("СправочникСсылка.УчетныеЗаписиМаркетплейсов"),
					Карточка.УчетнаяЗаписьИдентификатор); // СправочникСсылка.УчетныеЗаписиМаркетплейсов
				
				УстановитьПривилегированныйРежим(Истина);
				Если ОбщегоНазначения.СсылкаСуществует(УчетнаяЗаписьСсылка) Тогда
					
					Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗаписьСсылка, "ПометкаУдаления") Тогда
						
						НачатьТранзакцию();
						
						Попытка
							
							Блокировка = Новый БлокировкаДанных;
							ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
							ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", УчетнаяЗаписьСсылка);
							ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
							Блокировка.Заблокировать();
							
							УчетнаяЗаписьОбъект = УчетнаяЗаписьСсылка.ПолучитьОбъект();
							УчетнаяЗаписьОбъект.ПометкаУдаления = Ложь;
							УчетнаяЗаписьОбъект.Записать();
							
							ЗафиксироватьТранзакцию();
							
						Исключение
							
							ОтменитьТранзакцию();
							Результат.КодСостояния = 500;
							ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
							ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
							Результат.Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
								НСтр("ru = 'Ошибка при восстановлении ""%1"". Возможно, объект уже редактируется.';
									|en = 'An error occurred when restoring ""%1"". The object might be locked for editing.'"),
								ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыМетода.УчетнаяЗапись, "Наименование")));
							
						КонецПопытки;
						
					КонецЕсли;
					
				Иначе
					
					НачатьТранзакцию();
					
					Попытка
						
						СсылкаНового = Справочники.УчетныеЗаписиМаркетплейсов.ПолучитьСсылку(
							Новый УникальныйИдентификатор(Карточка.УчетнаяЗаписьИдентификатор));
						
						Блокировка = Новый БлокировкаДанных;
						ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
						ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", СсылкаНового);
						ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
						Блокировка.Заблокировать();
						
						УчетнаяЗаписьОбъект = Справочники.УчетныеЗаписиМаркетплейсов.СоздатьЭлемент();
						УчетнаяЗаписьОбъект.УстановитьСсылкуНового(СсылкаНового);
						УчетнаяЗаписьОбъект.ВидМаркетплейса = ПараметрыМетода.ВидМаркетплейса;
						УчетнаяЗаписьОбъект.Наименование = Карточка.Представление;
						УчетнаяЗаписьОбъект.Организация = Карточка.Организация;
						УчетнаяЗаписьОбъект.Партнер = Карточка.Партнер;
						УчетнаяЗаписьОбъект.Записать();
						
						ЗафиксироватьТранзакцию();
						
					Исключение
						
						ОтменитьТранзакцию();
						Результат.КодСостояния = 500;
						ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
							ТекстОшибки);
						Результат.Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Ошибка при записи ""%1"". Возможно, объект уже редактируется.';
								|en = 'An error occurred when saving ""%1"". The object might be locked for editing.'"), Карточка.Представление));
						Продолжить;
						
					КонецПопытки;
					
				КонецЕсли;
				УстановитьПривилегированныйРежим(Ложь);
				
				Карточка.УчетнаяЗапись = УчетнаяЗаписьСсылка;
				
			КонецЕсли;
			
			УчетныеЗаписи.Добавить(Карточка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	Ответ.Вставить("УчетныеЗаписи", УчетныеЗаписи);
	Ответ.Вставить("УчетнаяЗапись", ПараметрыМетода.УчетнаяЗапись); // Источник
	Ответ.Вставить("ОбновлениеСостояния", ПараметрыМетода.ОбновлениеСостояния); // Источник
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Удалить учетную запись.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаУдалитьУчетнуюЗапись
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция УдалитьУчетнуюЗапись(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросУдалитьУчетнуюЗапись(ПараметрыМетода);
	
	Ответ = Новый Структура("УчетнаяЗапись", Неопределено);
	
	Если Результат.КодСостояния = 200 Тогда
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировкиДанных = Блокировка.Добавить("Справочник.УчетныеЗаписиМаркетплейсов");
			ЭлементБлокировкиДанных.УстановитьЗначение("Ссылка", ПараметрыМетода.УчетнаяЗапись);
			ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			Ответ.УчетнаяЗапись = ПараметрыМетода.УчетнаяЗапись;
			УчетнаяЗаписьОбъект = ПараметрыМетода.УчетнаяЗапись.ПолучитьОбъект();
			УчетнаяЗаписьОбъект.ПометкаУдаления = Истина;
			УчетнаяЗаписьОбъект.Записать();
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			Результат.КодСостояния = 500;
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(ПараметрыМетода.ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			Результат.Ошибки.Добавить(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при удалении ""%1"". Возможно, объект уже редактируется.';
					|en = 'An error occurred when deleting ""%1"". The object might be locked for editing.'"),
				ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыМетода.УчетнаяЗапись, "Наименование")));
			
		КонецПопытки;
		
	КонецЕсли;
	
	Результат.Ответ = Ответ;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Создание или обновление учетной записи в сервисе интеграции для пользователя ИТС
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаСоздатьОбновитьУчетнуюЗапись
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция СоздатьОбновитьУчетнуюЗапись(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = ЗапросСоздатьОбновитьУчетнуюЗапись(ПараметрыМетода);
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Обновление настроек учетной записи, зарегистрированной в сервисе интеграции для пользователя ИТС
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ОбновитьНастройкиУчетнойЗаписи(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	РезультатЗапроса = ЗапросОбновитьНастройкиУчетнойЗаписи(ПараметрыМетода);
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, РезультатЗапроса, АдресРезультата);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Получение списка складов маркетплейсов
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP.
//  * СкладыМаркетплейса - Массив из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса.
Функция ПолучитьСкладыМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("СкладыМаркетплейса", Новый Массив);
	СкладыМаркетплейса = Результат.СкладыМаркетплейса;	// Массив из Структура
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросПолучитьСкладыМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда

		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Ответ = РезультатЗапроса.Ответ; // Массив из структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") Тогда
			
			Для Каждого ДанныеСервиса Из Ответ Цикл

				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса();

				Карточка.ИдентификаторСкладаСервиса = ПолучитьЗначение(
					ДанныеСервиса, "serviceOfficeId", "Строка", "");
				Карточка.НаименованиеСкладаСервиса = ПолучитьЗначение(
					ДанныеСервиса, "serviceOfficeName", "Строка", "");
					
				ИдентификаторСклада = ПолучитьЗначение(
					ДанныеСервиса, "baseOfficeId", "Строка", "");
				Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторСклада) Тогда
					//@skip-check statement-type-change
					Карточка.Склад = XMLЗначение(Тип("СправочникСсылка.Склады"), ИдентификаторСклада);
				КонецЕсли;
				
				ИдентификаторСхемыМаркетплейса = ПолучитьЗначение(
					ДанныеСервиса, "deliveryType", "Строка", "");
					
				Карточка.НаименованиеСкладаМаркетплейса = ПолучитьЗначение(
					ДанныеСервиса, "marketOfficeName", "Строка", "");
					
				Если Метаданные.Перечисления.СхемыРаботыТорговыхПлощадок.ЗначенияПеречисления.Найти(ИдентификаторСхемыМаркетплейса) <> Неопределено Тогда
					Карточка.СхемаРаботыСклада = Перечисления.СхемыРаботыТорговыхПлощадок[ИдентификаторСхемыМаркетплейса];
				КонецЕсли;
					
				СкладыМаркетплейса.Добавить(Карточка);

			КонецЦикла;
		КонецЕсли;

	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Обновление данных складов маркетплейсов
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьСкладыМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов -
//  * Склады - Массив из СправочникСсылка.Склады - Элементы, выгруженные в СИ
Функция ОбновитьСкладыМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("УчетнаяЗапись", ПараметрыМетода.УчетнаяЗапись);
	Результат.Вставить("Склады", Новый Массив);
	Склады = Результат.Склады;	// Массив из Структура

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросОбновитьСкладыМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Если Результат.Ошибки.Количество() = 0 Тогда
			Для Каждого ОписаниеСклада Из ПараметрыМетода.СкладыМаркетплейса Цикл
				Склады.Добавить(ОписаниеСклада);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Выгрузка складов в сервис интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьСклады
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - результат выполнения метода. Свойства:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов
Функция ВыгрузитьСклады(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("УчетнаяЗапись", ПараметрыМетода.УчетнаяЗапись);

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросВыгрузитьСклады(ПараметрыМетода); // см. НовыйРезультатМетодаСервиса
	
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Получение карточки склада маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьКарточкуСкладаМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//	* СкладМаркетплейса - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса
Функция ПолучитьКарточкуСкладаМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("СкладМаркетплейса", ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаСкладаМаркетплейса());
	
	Карточка = Результат.СкладМаркетплейса;

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросПолучитьКарточкуСкладаМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Ответ = РезультатЗапроса.Ответ; // Массив из структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") И Ответ.Количество() > 0 Тогда
			
			ДанныеСервиса = Ответ[0]; // Структура

			Карточка.ИдентификаторСкладаСервиса = ПараметрыМетода.ИдентификаторСкладаСервиса;
			
			ИдСклада = ПолучитьЗначение(ДанныеСервиса, "baseOfficeId", "Строка", "");
			
			Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдСклада) Тогда
				Карточка.Склад = Справочники.Склады.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдСклада));
			КонецЕсли;
			
			Карточка.НаименованиеСкладаСервиса = ПолучитьЗначение(ДанныеСервиса, "serviceOfficeName", "Строка", "");
				
			Карточка.НаименованиеСкладаМаркетплейса = ПолучитьЗначение(ДанныеСервиса, "marketOfficeName", "Строка", "");
			
			ИдСхемыМаркетплейса = ПолучитьЗначение(ДанныеСервиса, "deliveryType", "Строка", "");
				
			Если Метаданные.Перечисления.СхемыРаботыТорговыхПлощадок.ЗначенияПеречисления.Найти(ИдСхемыМаркетплейса) <> Неопределено Тогда
				Карточка.СхемаРаботыСклада = Перечисления.СхемыРаботыТорговыхПлощадок[ИдСхемыМаркетплейса];
			КонецЕсли;
			
			Карточка.ДополнительныеСведения =
				ПолучитьЗначение(ДанныеСервиса, "info", "Соответствие", Новый Соответствие);
			
		КонецЕсли;
			
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;

КонецФункции

// Получение списка брендов, опубликованной на маркетплейсе номенклатуры
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьБренды
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * Бренды - Массив из Структура - Бренды:
//    ** Идентификатор - Строка - Идентификатор
//    ** Наименование - Строка - Наименование
Функция ПолучитьБренды(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("Бренды", Новый Массив);
	Бренды = Результат.Бренды;	// Массив Из Структура
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросПолучитьБренды(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Ответ = РезультатЗапроса.Ответ; // Массив из Структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") Тогда
		
			Для Каждого ДанныеСервиса Из Ответ Цикл

				Карточка = Новый Структура("Идентификатор, Наименование", "", "");
				Карточка.Идентификатор = ПолучитьЗначение(ДанныеСервиса, "rfId", "Строка", "");
				Карточка.Наименование = ПолучитьЗначение(ДанныеСервиса, "rfName", "Строка", "");
			
				Бренды.Добавить(Карточка);

			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Получение списка категорий, опубликованной на маркетплейсе номенклатуры
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьКатегории
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * Категории - Массив из Структура - Категории:
//    ** Идентификатор - Строка - Идентификатор
//    ** Наименование - Строка - Наименование
Функция ПолучитьКатегории(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("Категории", Новый Массив);
	Категории = Результат.Категории;	// Массив Из Структура
	
	РезультатЗапроса = ЗапросПолучитьКатегории(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Ответ = РезультатЗапроса.Ответ; // Массив из Структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") Тогда
		
			Для Каждого ДанныеСервиса Из Ответ Цикл

				Карточка = Новый Структура("Идентификатор, Наименование", "", "");
				Карточка.Идентификатор = ПолучитьЗначение(ДанныеСервиса, "rfId", "Строка", "");
				Карточка.Наименование = ПолучитьЗначение(ДанныеСервиса, "rfName", "Строка", "");
			
				Категории.Добавить(Карточка);

			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Получение списка ограничений остатков товаров для маркетплейса из сервиса интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьОграниченияОстатковТоваров
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - результат выполнения метода. Свойства:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * ОграниченияОстатковТоваров - массив из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров
Функция ПолучитьОграниченияОстатковТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("ОграниченияОстатковТоваров", Новый Массив);
	ОграниченияОстатковТоваров = Результат.ОграниченияОстатковТоваров;	// Массив из Структура

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросПолучитьОграниченияОстатковТоваров(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Если ТипЗнч(РезультатЗапроса.Ответ) = Тип("Массив") Тогда

			ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
			
			Ответ = РезультатЗапроса.Ответ; // массив из структура
	
			Синонимы = Новый Соответствие;
			Синонимы.Вставить("Номенклатура", "Номенклатура");
			Синонимы.Вставить("ТоварнаяКатегория", "ТоварныеКатегории");
			Синонимы.Вставить("ВидНоменклатуры", "ВидыНоменклатуры");
			
			Для Каждого ДанныеСервиса Из Ответ Цикл
	
				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНастройкиОграниченияОстаткаТоваров();
				
				ИмяТипа = Синонимы.Получить(ПолучитьЗначение(ДанныеСервиса, "typeItem", "Строка", ""));
					
				ИдентификаторОбъекта = ПолучитьЗначение(ДанныеСервиса, "itemId", "Строка", "");
					
				Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторОбъекта) Тогда
					Карточка.ОбластьДействия = XMLЗначение(Тип("СправочникСсылка." + ИмяТипа), ИдентификаторОбъекта);
				КонецЕсли;
				
				Карточка.Используется = ПолучитьЗначение(ДанныеСервиса, "isActive", "Булево", Ложь);
				Карточка.ПроцентОстатка = ПолучитьЗначение(ДанныеСервиса, "percent", "Число", 0);
				Карточка.СтраховойЗапас = ПолучитьЗначение(ДанныеСервиса, "insuranceReserve", "Число", 0);
				Карточка.МинимальныйОстаток = ПолучитьЗначение(ДанныеСервиса, "limitLow", "Число", 0);
				Карточка.МаксимальныйОстаток = ПолучитьЗначение(ДанныеСервиса, "limitUp", "Число", 0);
				Карточка.Код = ПолучитьЗначение(ДанныеСервиса, "id", "Число", 0);
				Карточка.Наименование = ПолучитьЗначение(ДанныеСервиса, "name", "Строка", "");
					
				ОграниченияОстатковТоваров.Добавить(Карточка);
	
			КонецЦикла;

		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Выгрузка настройки ограничений остатков товаров для маркетплейса в сервис интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция ВыгрузитьОграниченияОстатковТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросВыгрузитьОграниченияОстатковТоваров(ПараметрыМетода);
	
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Удаление настроек ограничений остатков товаров для маркетплейса в сервисе интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция УдалитьОграниченияОстатковТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();

	Если ПараметрыМетода.ОграниченияОстатковТоваров.Количество() > 0 Тогда

		ПередВыполнениемМетодаСервиса(ПараметрыМетода);

		РезультатЗапроса = ЗапросУдалитьОграниченияОстатковТоваров(ПараметрыМетода);
	
		Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
			ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		КонецЕсли;
		
	Иначе
		Результат.КодСостояния = 200;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Получение списка номенклатурных позиций маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьНоменклатуруМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Результат выполнения метода:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * Ответ - Структура - счетчики обработанных данных:
//   ** Ошибки - Массив из Строка - 
//   ** Получено - Число - количество обрабатываемых карточек.
//   ** Добавлено - Число - количество созданных элементов справочника "НоменклатураКонтрагентов".
//   ** Обновлено - Число - количество обновленных элементов справочника "НоменклатураКонтрагентов".
//   ** Отклонено - Число - количество карточек, при обработке которых возникла ошибка.
Функция ПолучитьНоменклатуруМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("Ответ", Новый Структура);
	Результат.Ответ.Вставить("Ошибки", Новый Массив);
	Результат.Ответ.Вставить("Получено", 0);
	Результат.Ответ.Вставить("Добавлено", 0);
	Результат.Ответ.Вставить("Обновлено", 0);
	Результат.Ответ.Вставить("Отклонено", 0);
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	НоменклатураМаркетплейса = Новый Массив; // Массив из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса
	
	Пока Истина Цикл
		
		РезультатЗапроса = ЗапросПолучитьНоменклатуруМаркетплейса(ПараметрыМетода);
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Если РезультатЗапроса.КодСостояния <> 200 Тогда
			Прервать;
		КонецЕсли;
		
		Заголовки = РезультатЗапроса.Заголовки; // Соответствие из Строка
		Ответ = РезультатЗапроса.Ответ; // Массив из Структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") Тогда
			Для Каждого ДанныеСервиса Из Ответ Цикл
				
				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса();
				Карточка.ИдентификаторНоменклатурыСервиса =
					ПолучитьЗначение(ДанныеСервиса, "serviceCardId", "Строка", "");
				
				Карточка.НаименованиеНоменклатурыСервиса =
					ПолучитьЗначение(ДанныеСервиса, "serviceCardName", "Строка", "");
				
				Карточка.ПолноеНаименованиеНоменклатурыСервиса =
					ПолучитьЗначение(ДанныеСервиса, "serviceCardFullName", "Строка", "");
				
				ИдентификаторНоменклатурыКонтрагента = ПолучитьЗначение(ДанныеСервиса, "baseOfficeId", "Строка", "");
				
				Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдентификаторНоменклатурыКонтрагента) Тогда
					Карточка.НоменклатураКонтрагента = Справочники.НоменклатураКонтрагентов.ПолучитьСсылку(
					Новый УникальныйИдентификатор(ИдентификаторНоменклатурыКонтрагента));
				КонецЕсли;
				
				Карточка.Штрихкод = ПолучитьЗначение(ДанныеСервиса, "barcode", "Строка", "");
				
				Карточка.Артикул = ПолучитьЗначение(ДанныеСервиса, "sku", "Строка", "");
				Карточка.АртикулМаркетплейса =
					Формат(ПолучитьЗначение(ДанныеСервиса, "marketSku", "Число", 0), "ЧВН=; ЧГ=");
				
				Карточка.НаименованиеХарактеристики = ПолучитьЗначение(ДанныеСервиса, "sizeName", "Строка", "");
				
				Карточка.ИдентификаторХарактеристики = ПолучитьЗначение(ДанныеСервиса, "sizeId", "Строка", "");
				
				Если ПустаяСтрока(Карточка.ИдентификаторХарактеристики) Тогда
					Карточка.ИдентификаторХарактеристики = Карточка.НаименованиеХарактеристики;
				КонецЕсли;
				
				НоменклатураМаркетплейса.Добавить(Карточка);
				
			КонецЦикла;
		КонецЕсли;

		ОбновитьКарточкиНоменклатурыКонтрагентов(ПараметрыМетода.УчетнаяЗапись,
			ПараметрыМетода.Партнер, НоменклатураМаркетплейса, Результат.Ответ); // Структура
		
		Курсор = ПолучитьЗначение(Заголовки, "Cursor", "Строка", "");
		Если Курсор = "" Или Курсор = "null" Тогда
			Прервать;
		КонецЕсли;
		ПараметрыМетода.Курсор = Курсор;
		ПараметрыМетода.ЗапроситьДанныеСМаркетплейса = Ложь;
		НоменклатураМаркетплейса.Очистить();
	КонецЦикла;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Обновить номенклатуру маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьНоменклатуруМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
// Структура - Обновить номенклатуру маркетплейса:
//  * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//  * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//  * НоменклатураКонтрагентов - Массив из СправочникСсылка.НоменклатураКонтрагентов - Элементы, выгруженные в СИ
//  * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
Функция ОбновитьНоменклатуруМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("УчетнаяЗапись", ПараметрыМетода.УчетнаяЗапись);
	Результат.Вставить("НоменклатураКонтрагентов", Новый Массив);
	НоменклатураКонтрагентов = Результат.НоменклатураКонтрагентов;	// Массив из СправочникСсылка.НоменклатураКонтрагентов
	
	Если ПараметрыМетода.НоменклатураМаркетплейса.Количество() = 0 Тогда
		Результат.КодСостояния = 200;
	КонецЕсли;
	
	УзлыОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиУзлы(ПараметрыМетода.УчетнаяЗапись, Ложь);
	
	РазмерПорции = 1000;
	
	НоменклатураКВыгрузке = ПараметрыМетода.НоменклатураМаркетплейса;
	ПараметрыМетода.НоменклатураМаркетплейса = Новый Массив;
	
	КоличествоКВыгрузке = 0;
	ВсегоКВыгрузке = НоменклатураКВыгрузке.Количество();
	
	//@skip-check query-in-loop
	Для Каждого ТекущаяНоменклатура Из НоменклатураКВыгрузке Цикл
		
		КоличествоКВыгрузке = КоличествоКВыгрузке + 1;
		Если КоличествоКВыгрузке = ВсегоКВыгрузке Или КоличествоКВыгрузке % РазмерПорции = 0 Тогда
			
			ПараметрыМетода.НоменклатураМаркетплейса.Добавить(ТекущаяНоменклатура);
			
			ПередВыполнениемМетодаСервиса(ПараметрыМетода);

			РезультатЗапроса = ЗапросОбновитьНоменклатуруМаркетплейса(ПараметрыМетода);
			
			Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
				
				ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
			
				Если РезультатЗапроса.КодСостояния = 200 Тогда
					
					ДатаАктуальности = ТекущаяДатаСеанса();
					
					// Читаем данные регистра, чтобы они не затерлись при перезаписи
					ЗапросСоответствия = Новый Запрос;
					ЗапросСоответствия.Текст =
					"ВЫБРАТЬ
					|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
					|	СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
					|	СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
					|	СоответствияОбъектовМаркетплейсов.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
					|	СоответствияОбъектовМаркетплейсов.Объект1С КАК Объект1С,
					|	СоответствияОбъектовМаркетплейсов.НаименованиеОбъектаМаркетплейса КАК НаименованиеОбъектаМаркетплейса,
					|	СоответствияОбъектовМаркетплейсов.ДополнительныеСведения КАК ДополнительныеСведения
					|ИЗ
					|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
					|ГДЕ
					|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
					|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
					|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В(&ИдентификаторыМП)";
					
					ЗапросСоответствия.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", ПараметрыМетода.УчетнаяЗапись);
					
					ИдентификаторыМП = Новый Массив;	// Массив из Строка
					Для Каждого ОписаниеНоменклатурыМП Из ПараметрыМетода.НоменклатураМаркетплейса Цикл
						ИдентификаторыМП.Добавить(ОписаниеНоменклатурыМП.ИдентификаторНоменклатурыСервиса);
						НоменклатураКонтрагентов.Добавить(ОписаниеНоменклатурыМП.НоменклатураКонтрагента);
					КонецЦикла;
					
					ЗапросСоответствия.УстановитьПараметр("ИдентификаторыМП", ИдентификаторыМП);
					
					РезультатЗапросаСоответствия = ЗапросСоответствия.Выполнить();
					
					НаборОбновления = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
					
					// Установка даты актуальности
					Выборка = РезультатЗапросаСоответствия.Выбрать();
					Пока Выборка.Следующий() Цикл
						Запись = НаборОбновления.Добавить();
						ЗаполнитьЗначенияСвойств(Запись, Выборка);
						Запись.ДатаАктуальности = ДатаАктуальности;
					КонецЦикла;
					
					БлокировкаДанных = Новый БлокировкаДанных;
					ЭлементБлокировкиДанных = БлокировкаДанных.Добавить("РегистрСведений.СоответствияОбъектовМаркетплейсов");
					ЭлементБлокировкиДанных.ИсточникДанных = РезультатЗапросаСоответствия;
					ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("УчетнаяЗаписьМаркетплейса", "УчетнаяЗаписьМаркетплейса");
					ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ВидОбъектаМаркетплейса", "ВидОбъектаМаркетплейса");
					ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ИдентификаторОбъектаМаркетплейса", "ИдентификаторОбъектаМаркетплейса");
					ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("ИдентификаторВладельцаОбъектаМаркетплейса", "ИдентификаторВладельцаОбъектаМаркетплейса");
					ЭлементБлокировкиДанных.ИспользоватьИзИсточникаДанных("Объект1С", "Объект1С");
					ЭлементБлокировкиДанных.Режим = РежимБлокировкиДанных.Исключительный;

					НачатьТранзакцию();

					Попытка
						БлокировкаДанных.Заблокировать();
						
						// Проинформируем механизм расчета остатков, об изменениях в сопоставленной номенклатуре
						Для Каждого УзелОбмена Из УзлыОбмена Цикл
							НаборОбновления.ОбменДанными.Получатели.Добавить(УзелОбмена);
						КонецЦикла;
						
						НаборОбновления.Записать(РежимЗамещения.Обновление);
						ЗафиксироватьТранзакцию();
					Исключение
						ОтменитьТранзакцию();
						
						ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
						ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
					КонецПопытки;
					
				КонецЕсли;
				
				ПараметрыМетода.НоменклатураМаркетплейса.Очистить();
				
			КонецЕсли;
		Иначе
			ПараметрыМетода.НоменклатураМаркетплейса.Добавить(ТекущаяНоменклатура);
		КонецЕсли;
	КонецЦикла;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Получение карточки номенклатуры маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьКарточкуНоменклатурыМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Структура - результат выполнения метода. Свойства:
//   * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//   * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//   * НоменклатураМаркетплейса - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса
Функция ПолучитьКарточкуНоменклатурыМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("НоменклатураМаркетплейса", ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса());
	
	Карточка = Результат.НоменклатураМаркетплейса;
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	РезультатЗапроса = ЗапросПолучитьКарточкуНоменклатурыМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) И ТипЗнч(РезультатЗапроса.Ответ) = Тип("Соответствие") Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		ДанныеСервиса = РезультатЗапроса.Ответ; // Структура

		Карточка.ИдентификаторНоменклатурыСервиса = ПолучитьЗначение(ДанныеСервиса, "serviceCardId", "Строка", "");
		
		Карточка.НаименованиеНоменклатурыСервиса = ПолучитьЗначение(ДанныеСервиса, "serviceCardName", "Строка", "");
		
		Карточка.ПолноеНаименованиеНоменклатурыСервиса =
			ПолучитьЗначение(ДанныеСервиса, "serviceCardFullName", "Строка", "");
		
		ИдНоменклатурыКонтрагента = ПолучитьЗначение(ДанныеСервиса, "baseCardId", "Строка", "");
		
		Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдНоменклатурыКонтрагента) Тогда
			Карточка.НоменклатураКонтрагента = Справочники.НоменклатураКонтрагентов.ПолучитьСсылку(
				Новый УникальныйИдентификатор(ИдНоменклатурыКонтрагента));
		КонецЕсли;
		
		Карточка.Штрихкод = ПолучитьЗначение(ДанныеСервиса, "barcode", "Строка", "");
		
		Карточка.Артикул = ПолучитьЗначение(ДанныеСервиса, "sku", "Строка", "");
		
		Карточка.ДополнительныеСведения = ПолучитьЗначение(ДанныеСервиса, "info", "Соответствие", Новый Соответствие);
		
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Получение остатков товаров маркетплейса из сервиса интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Структура - результат выполнения метода:
//   * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//   * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//   * ОстаткиТоваров - массив из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаОстаткаТовараМаркетплейса
Функция ПолучитьОстаткиТоваровМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("ОстаткиТоваров", Новый Массив);
	ОстаткиТоваров = Результат.ОстаткиТоваров;	// массив из Структура

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	РезультатЗапроса = ЗапросПолучитьОстаткиТоваровМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		Ответ = РезультатЗапроса.Ответ; // Массив из Структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") Тогда
		
			Для Каждого ДанныеСервиса Из Ответ Цикл

				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаОстаткаТовараМаркетплейса();
			
				ИдПараметра = ПолучитьЗначение(ДанныеСервиса, "baseCardId", "Строка", "");
				
				Если СтроковыеФункцииКлиентСервер.ЭтоУникальныйИдентификатор(ИдПараметра) Тогда
					Карточка.НоменклатураПартнера = Справочники.НоменклатураКонтрагентов.ПолучитьСсылку(Новый УникальныйИдентификатор(ИдПараметра));
				КонецЕсли;
			
				Карточка.ИдентификаторСклада = ПолучитьЗначение(ДанныеСервиса, "serviceOfficeId", "Строка", "");
				Карточка.НаименованиеСклада = ПолучитьЗначение(ДанныеСервиса, "serviceOfficeName", "Строка", "");
				
				Карточка.КоличествоМаркетплейса = ПолучитьЗначение(ДанныеСервиса, "marketQuantity", "Число", 0);
				Карточка.Количество = ПолучитьЗначение(ДанныеСервиса, "quantity", "Число", 0);
				
				Карточка.ИдентификаторОграничения = ПолучитьЗначение(ДанныеСервиса, "stockLimitId", "Строка", "");
				Карточка.ПредставлениеОграничения = ПолучитьЗначение(ДанныеСервиса, "stockLimitName", "Строка", "");
			
				Карточка.ДатаВыгрузки = ПолучитьЗначение(ДанныеСервиса, "datePub", "Дата", '00010101');
					
				ОстаткиТоваров.Добавить(Карточка);

			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Выгрузка остатков товаров для маркетплейса в сервис интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьОстаткиТоваров
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ВыгрузитьОстаткиТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	РезультатЗапроса = ЗапросВыгрузитьОстаткиТоваров(ПараметрыМетода);
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, РезультатЗапроса, АдресРезультата);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Перенос номенклатуры партнера
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПеренестиНоменклатуруПартнера
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция ПеренестиНоменклатуруПартнера(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();

	ПередВыполнениемМетодаСервиса(ПараметрыМетода);

	ПараметрыИзменения = ИнтеграцияСМаркетплейсамиКлиентСервер.ПараметрыИзмененияВладельцаТоварногоКаталога();
	ЗаполнитьЗначенияСвойств(ПараметрыИзменения, ПараметрыМетода);
	ПараметрыИзменения.ВидТорговойПлощадки = ПараметрыМетода.ВидМаркетплейса;
	РезультатЗапроса = ИнтеграцияСМаркетплейсамиСервер.ПеренестиНоменклатуруПартнера(ПараметрыИзменения, Ложь);
	
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		Если ЗначениеЗаполнено(РезультатЗапроса.КодОшибки) Тогда
			Результат.КодСостояния = 500;
			ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("%1: %2",
				РезультатЗапроса.КодОшибки, РезультатЗапроса.ОписаниеОшибки);
			Если ЗначениеЗаполнено(РезультатЗапроса.Детализация) Тогда
				ДетализацияСтрокой = СтрСоединить(РезультатЗапроса.Детализация, " ");
				ОписаниеОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1; детализация: %2';
																								|en = '%1; details: %2'"),
					ОписаниеОшибки, ДетализацияСтрокой);
			КонецЕсли;
			Результат.Ошибки.Добавить(ОписаниеОшибки);
		Иначе
			Результат.КодСостояния = 200;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

// Получение показателей номенклатуры маркетплейса из сервиса интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  Структура - результат выполнения метода:
//   * Ошибки - Массив из Строка - описание ошибок, возникших в ходе выполнения метода
//   * КодСостояния - Число - Код ответа. https://ru.wikipedia.org/wiki/Список_кодов_состояния_HTTP
//   * Показатели - Соответствие из См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаПоказателиНоменклатурыМаркетплейса
//   * ВерсияКэша - Строка
//   * Курсор - Строка - курсор страницы, содержит идентификатора номенклатуры маркетплейса
//   * СтраницаКэша - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйСтраницаКэша
Функция ПолучитьПоказателиНоменклатурыМаркетплейса(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
	Результат.Вставить("Показатели", Новый Соответствие);
	Результат.Вставить("ВерсияКэша", ПараметрыМетода.ВерсияКэша);
	Результат.Вставить("Курсор", ПараметрыМетода.Курсор);
	Результат.Вставить("СтраницаКэша", ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйСтраницаКэша());
	
	Показатели = Результат.Показатели;
	СтраницаКэша = Результат.СтраницаКэша;
	СтраницаКэша.ДатаАктуальности = ТекущаяДатаСеанса();
	СтраницаКэша.Курсор = ПараметрыМетода.Курсор;
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	РезультатЗапроса = ЗапросПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыМетода);
	
	// Обработка результата запроса
	Если ЗначениеЗаполнено(РезультатЗапроса) Тогда
		
		ЗаполнитьЗначенияСвойств(Результат, РезультатЗапроса, "КодСостояния, Ошибки");
		
		// Последняя, согласно показаний сервиса интеграции. В СИ зависит от направления сортировки.
		// В 1С список считанных данных всегда хранится с упорядочиванием "по возрастанию".
		// Далее, признак "Это последняя" при сортировке "по убыванию", в 1С превратится в "ЭтоПервая".
		ЭтоПоследняяСтраница = ПолучитьЗначение(РезультатЗапроса.Заголовки, "IsLast", "Булево", Ложь);
		
		Ответ = РезультатЗапроса.Ответ; // Массив из Структура
		
		Если ТипЗнч(Ответ) = Тип("Массив") И Ответ.Количество() > 0 Тогда
			
			Для Каждого ДанныеСервиса Из Ответ Цикл

				Карточка = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаПоказателиНоменклатурыМаркетплейса();
				
				Карточка.идНоменклатурыСИ = ПолучитьЗначение(ДанныеСервиса, "serviceCardId", "Строка", "");
				
				Карточка.ИдБрендаМП = ПолучитьЗначение(ДанныеСервиса, "marketBrandId", "Строка", 0);
				Карточка.ИдКатегорииМП = ПолучитьЗначение(ДанныеСервиса, "marketCategoryId", "Строка", 0);
				
				Если ПараметрыМетода.ПолучитьСводныйОстаток Тогда
					Карточка.КоличествоМП = ПолучитьЗначение(ДанныеСервиса, "marketQuantity", "Число", "");
				КонецЕсли;
				
				Если ПараметрыМетода.ПолучитьЦены Тогда
					Карточка.ЦенаБезСкидки = ПолучитьЗначение(ДанныеСервиса, "price", "Число", 0);
					Карточка.ЦенаБезСкидкиМП = ПолучитьЗначение(ДанныеСервиса, "marketPrice", "Число", 0);
				
					Карточка.ЦенаСоСкидкойПродавца = ПолучитьЗначение(ДанныеСервиса, "priceDiscounted", "Число", 0);
					Карточка.ЦенаСоСкидкойПродавцаМП =
						ПолучитьЗначение(ДанныеСервиса, "marketPriceDiscounted", "Число", 0);
				
					Карточка.СкидкаПродавца = ПолучитьЗначение(ДанныеСервиса, "discount", "Число", 0);
					Карточка.СкидкаПродавцаМП = ПолучитьЗначение(ДанныеСервиса, "marketDiscount", "Число", 0);
				
					Карточка.ЦенаСоВсемиСкидками = ПолучитьЗначение(ДанныеСервиса, "priceClubDiscounted", "Число", 0);
					Карточка.ЦенаСоВсемиСкидкамиМП = ПолучитьЗначение(ДанныеСервиса, "marketPriceClubDiscounted", "Число", 0);
				
					Карточка.СкидкаWB = ПолучитьЗначение(ДанныеСервиса, "clubDiscount", "Число", 0);
					Карточка.СкидкаМПWB = ПолучитьЗначение(ДанныеСервиса, "marketClubDiscount", "Число", 0);
				
					Карточка.НаКарантине = ПолучитьЗначение(ДанныеСервиса, "quarantine", "Булево", Ложь);
					Если Карточка.НаКарантине Тогда
						Карточка.МаркерСтатуса = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен().НаКарантине;
					Иначе
						Карточка.МаркерСтатуса = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен().БезОшибок;
					КонецЕсли;
					
					Карточка.ДатаВыгрузкиЦен = ПолучитьЗначение(ДанныеСервиса, "datePricePub", "Дата", '00010101');
					Если Карточка.ДатаВыгрузкиЦен = '20010101' Тогда
						Карточка.ДатаВыгрузкиЦен = '00010101';
					КонецЕсли;
				КонецЕсли;
				
				Карточка.ТекстОшибки = ПолучитьЗначение(ДанныеСервиса, "error", "Строка", "");
				Карточка.Основная = ПолучитьЗначение(ДанныеСервиса, "isGeneral", "Булево", Ложь);
				Карточка.ЦеныПоХарактеристикам = ПолучитьЗначение(ДанныеСервиса, "editableSizePrice", "Булево", Ложь);
				
				Показатели.Вставить(Карточка.идНоменклатурыСИ, Карточка);
			КонецЦикла;
			
			// Формирование страницы кэшированных данных
			Если Показатели.Количество() > 0 Тогда
			
				// Определим ключи страницы кэшированных данных
				Если ПараметрыМетода.НаправлениеСортировки = "asc" Тогда
					ПервыйИдентификатор = ПолучитьЗначение(Ответ[0], "serviceCardId", "Строка", "");
					ПоследнийИдентификатор = Карточка.идНоменклатурыСИ;
				Иначе
					ПервыйИдентификатор = Карточка.идНоменклатурыСИ;
					ПоследнийИдентификатор = ПолучитьЗначение(Ответ[0], "serviceCardId", "Строка", "");
				КонецЕсли;
			
				ЗапросНаименования = Новый Запрос;
				ЗапросНаименования.Текст =
				"ВЫБРАТЬ
				|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
				|		КОГДА СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса = &ПервыйИдентификатор
				|			ТОГДА СоответствияОбъектовМаркетплейсов.НаименованиеОбъектаМаркетплейса + ""_"" +
				|				СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса
				|		ИНАЧЕ &ПустаяСтрока
				|	КОНЕЦ), &ПустаяСтрока) КАК ПервыйКлюч,
				|	ЕСТЬNULL(МАКСИМУМ(ВЫБОР
				|		КОГДА СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса = &ПоследнийИдентификатор
				|			ТОГДА СоответствияОбъектовМаркетплейсов.НаименованиеОбъектаМаркетплейса + ""_"" +
				|				СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса
				|		ИНАЧЕ &ПустаяСтрока
				|	КОНЕЦ), &ПустаяСтрока) КАК ПоследнийКлюч
				|ИЗ
				|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
				|ГДЕ
				|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
				|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
				|	И СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса В (&ПервыйИдентификатор,
				|		&ПоследнийИдентификатор)";
				
				ЗапросНаименования.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", ПараметрыМетода.УчетнаяЗапись);
				ЗапросНаименования.УстановитьПараметр("ПервыйИдентификатор", ПервыйИдентификатор);
				ЗапросНаименования.УстановитьПараметр("ПоследнийИдентификатор", ПоследнийИдентификатор);
				ЗапросНаименования.УстановитьПараметр("ПустаяСтрока", "");
			
				РезультатЗапросаНаименования = ЗапросНаименования.Выполнить();
				Если Не РезультатЗапросаНаименования.Пустой() Тогда
					Выборка = РезультатЗапросаНаименования.Выбрать();
					Выборка.Следующий();
					ЗаполнитьЗначенияСвойств(СтраницаКэша, Выборка, "ПервыйКлюч, ПоследнийКлюч");
				КонецЕсли;
				
				Если СтраницаКэша.ПервыйКлюч = "" Или СтраницаКэша.ПоследнийКлюч = "" Тогда
					Показатели.Очистить();
				КонецЕсли;

				ЭтоПервая = ПараметрыМетода.НаправлениеСортировки = "asc" И ПараметрыМетода.Курсор = "";
				ЭтоПоследняя = ПараметрыМетода.НаправлениеСортировки = "desc" И ПараметрыМетода.Курсор = "";
				
				СтраницаКэша.ПервыйИдентификатор = ПервыйИдентификатор;
				СтраницаКэша.ПоследнийИдентификатор = ПоследнийИдентификатор;
				Если ПараметрыМетода.НаправлениеСортировки = "asc" Тогда
					СтраницаКэша.Первая = ЭтоПервая;
					СтраницаКэша.Последняя = Показатели.Количество() < ПараметрыМетода.РазмерСтраницы Или ЭтоПоследняяСтраница;
				Иначе
					СтраницаКэша.Первая = Показатели.Количество() < ПараметрыМетода.РазмерСтраницы Или ЭтоПоследняяСтраница;
					СтраницаКэша.Последняя = ЭтоПоследняя;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;
	
КонецФункции

#Область МетодыВыгрузкиТоварногоКаталога

// Подготовка остатков товаров для выгрузки в сервис интеграции
// 
// Параметры:
//  ПараметрыМетода - см. НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция ПодготовитьОстаткиТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт

	Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();

	Результат.КодСостояния = 200;
	
	Если ОбновитьНоменклатуруВСервисеИнтеграции(ПараметрыМетода.УчетнаяЗапись, ПараметрыМетода.ВидМаркетплейса) Тогда
		СинхронизацияССервисомИнтеграции_Склады(ПараметрыМетода.УчетнаяЗапись, ПараметрыМетода.ВидМаркетплейса);
		СинхронизацияССервисомИнтеграции_РасчетОстатковТоваров(ПараметрыМетода.УчетнаяЗапись, ПараметрыМетода.ВидМаркетплейса);
	Иначе
		Результат.КодСостояния = 500;
	КонецЕсли;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);

	Возврат Результат;

КонецФункции

// Выгрузка остатков товаров, зарегистрированных на плане обмена
// 
// Параметры:
//  ПараметрыМетода - см. НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода
Функция ВыгрузитьПодготовленныеОстаткиТоваров(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт

	// Проверка на наличие фонового задания, запущенного в ручном режиме
	ПараметрыФЗ = Новый Структура;
	КлючЗадания = ИнтеграцияСМаркетплейсамиСИКлиентСервер.КлючЗаданияРасчетОстатковТоваров(
		ПараметрыМетода.ВидМаркетплейса,
		XMLСтрока(ПараметрыМетода.УчетнаяЗапись));
	ПараметрыФЗ.Вставить("Ключ", КлючЗадания);
	ПараметрыФЗ.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	КоличествоФЗ = ФоновыеЗадания.ПолучитьФоновыеЗадания(ПараметрыФЗ).Количество();

	Если КоличествоФЗ = 0 Тогда
		Результат = СинхронизацияССервисомИнтеграции_ВыгрузкаОстатковТоваров(ПараметрыМетода.УчетнаяЗапись, ПараметрыМетода.ВидМаркетплейса);
	Иначе
		Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
		Результат.Ошибки.Добавить(НСтр("ru = 'Уже выполняется регламентное задание по выгрузке остатков';
										|en = 'The scheduled job for exporting balances is already in progress'"));
	КонецЕсли;
	
	// Обработка ошибок и упаковка данных
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции

// Выгрузка цен для маркетплейса в сервис интеграции
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьЦены
//  АдресРезультата - Строка - Адрес результата
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ВыгрузитьЦены(Знач ПараметрыМетода, Знач АдресРезультата = "") Экспорт
	
	ПередВыполнениемМетодаСервиса(ПараметрыМетода);
	
	Результат = НовыйРезультатМетодаСервиса();
	
	Если ОбновитьНоменклатуруВСервисеИнтеграции(ПараметрыМетода.УчетнаяЗапись, ПараметрыМетода.ВидМаркетплейса) Тогда
		Результат = ЗапросВыгрузитьЦены(ПараметрыМетода);
	Иначе
		Результат.КодСостояния = 500;
	КонецЕсли;
	
	ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата);
	
	Возврат Результат;
	
КонецФункции
#КонецОбласти

#КонецОбласти

#Область КонструкторыЗапросов

// Новые служебные параметры запроса.
// 
// Возвращаемое значение:
//  Структура - Новые служебные параметры запроса:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
Функция НовыеСлужебныеПараметрыЗапроса() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("ВидМаркетплейса", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	Параметры.Вставить("УчетнаяЗапись", Справочники.УчетныеЗаписиМаркетплейсов.ПустаяСсылка());
	Параметры.Вставить("ИмяМетода", "");
	Параметры.Вставить("ВыводитьСообщения", Ложь);
	
	Параметры.Вставить("ЗависитОт", "");
	Параметры.Вставить("ЗависимыеПоля", "");
	
	Возврат Параметры
	
КонецФункции

// Описание параметров запроса для чтения списка данных с использованием пагинации.
// Параметры:
//  Параметры - См. НовыеСлужебныеПараметрыЗапроса
Процедура ДополнитьСлужебныеПараметрыЗапросаПолямиДляПагинации(Параметры) Экспорт
	
	Параметры.Вставить("ИспользоватьПагинацию", Ложь);
	Параметры.Вставить("ПолеСортировки", "id");
	Параметры.Вставить("НаправлениеСортировки", "asc");
	Параметры.Вставить("Курсор", "");
	Параметры.Вставить("РазмерСтраницы", 100);
	
КонецПроцедуры

// Инициализация параметров запроса "ОбновитьТокенДоступа".
// 
// Возвращаемое значение:
//  Структура - Новые параметры обновить токен доступа:
// * id_token - Строка - Токен доступа
// * client_id - Строка - Публичная часть ключа доступа
// * client_secret - Строка - Секретная часть ключа доступа
// * ДатаОбновленияТокена - Дата - Дата обновления токена доступа
Функция НовыеПараметрыОбновитьТокенДоступа() Экспорт
	
	Параметры = НовыеСлужебныеПараметрыЗапроса();
	Параметры.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьТокенДоступа();
	
	Параметры.Вставить("id_token", "");
	Параметры.Вставить("client_id", "");
	Параметры.Вставить("client_secret", "");
	Параметры.Вставить("ДатаОбновленияТокена", Дата(1,1,1));
	
	Возврат Параметры;
	
КонецФункции

// Инициализация параметров запроса "УстановитьСоединение".
// 
// Возвращаемое значение:
//  Структура - Новые параметры установить соединение:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - 
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
// * ИмяМетода - Строка - 
// * ВыводитьСообщения - Булево - 
// * Логин - Строка - 
// * Тикет - Строка - 
// * client_id - Строка - 
// * КлючПодключения - Строка - 
// * Представление - Строка - 
// * ИдентификаторИнформационнойБазы - Строка -
// * ВерсияКонфигурации -Строка
Функция НовыеПараметрыУстановитьСоединение() Экспорт
	
	Параметры = НовыеСлужебныеПараметрыЗапроса();
	Параметры.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение();
	
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("Тикет", "");
	Параметры.Вставить("client_id", "");
	Параметры.Вставить("КлючПодключения", "");
	Параметры.Вставить("Представление", "");
	Параметры.Вставить("ИдентификаторИнформационнойБазы", "");
	Параметры.Вставить("ВерсияКонфигурации", "");
	
	Возврат Параметры;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьПодключения".
// 
// Возвращаемое значение:
//  См. НовыеСлужебныеПараметрыЗапроса
Функция НовыеПараметрыПолучитьПодключения() Экспорт
	Возврат НовыеСлужебныеПараметрыЗапроса();
КонецФункции

// Новые параметры удалить подключение.
// 
// Возвращаемое значение:
//  Структура - Новые параметры удалить подключение:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * Логин - Строка -
// * КлючПодключения - Строка - 
// * ЭтоТекущийКлючПодключения - Булево - 
Функция НовыеПараметрыУдалитьПодключение() Экспорт
	
	Параметры = НовыеСлужебныеПараметрыЗапроса();
	
	Параметры.Вставить("Логин", "");
	Параметры.Вставить("КлючПодключения", "");
	Параметры.Вставить("ЭтоТекущийКлючПодключения", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Инициализация параметров запроса "ПроверитьДоступностьСервисаИнтеграции".
// 
// Возвращаемое значение:
//  См. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаПроверитьДоступностьСервисаИнтеграции() Экспорт
	Возврат НовыеСлужебныеПараметрыЗапроса();
КонецФункции

// Инициализация параметров запроса "ПроверитьДоступностьСервисаМаркетплейса".
// 
// Возвращаемое значение:
//  См. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаПроверитьДоступностьСервисаМаркетплейса() Экспорт
	Возврат НовыеСлужебныеПараметрыЗапроса();
КонецФункции

// Инициализация параметров запроса "СоздатьОбновитьУчетнуюЗапись".
// 
// Возвращаемое значение:
//  Структура - Новый параметры запроса создать обновить учетную запись:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * Логин - Строка - Имя пользователя ИТС
// * Организация - СправочникСсылка.Организации - Организация, связанная с учетной записью
// * ИНН - Строка - ИНН
// * КПП - Строка - КПП
// * Представление - Строка - Представление учетной записи
// * Токены - Массив из Строка - 
// * Функциональность - Массив из Строка - 
// * client_id - Строка -
// * client_secret - Строка -
// * ХешКлючейАвторизации - Строка - хэш ключей client_id и client_secret, сформированные в СИ
// * ВерсияКонфигурации - Строка
Функция НовыйПараметрыЗапросаСоздатьОбновитьУчетнуюЗапись() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("Логин", "");
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("ИНН", "");
	Результат.Вставить("КПП", "");
	Результат.Вставить("Представление", "");
	Результат.Вставить("Токены", Новый Массив);
	Результат.Вставить("Функциональность", Новый Массив);
	Результат.Вставить("client_id", "");
	Результат.Вставить("client_secret", "");
	Результат.Вставить("ХешКлючейАвторизации", "");
	Результат.Вставить("ВерсияКонфигурации", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьУчетныеЗаписи".
// 
// Возвращаемое значение:
//  Структура - Параметры запроса:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ВсеУчетныеЗаписи - Булево - Признак получения данных всех учетных записей
// * ПроверятьТокены - Булево - Признак проверки токенов
// * ОбновлениеСостояния - Булево - Признак обновления состояния
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса
Функция НовыйПараметрыЗапросаПолучитьУчетныеЗаписи() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ВсеУчетныеЗаписи", Истина);
	Результат.Вставить("ПроверятьТокены", Ложь);
	Результат.Вставить("ОбновлениеСостояния", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "УдалитьУчетнуюЗапись".
// 
// Возвращаемое значение:
//  См. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаУдалитьУчетнуюЗапись() Экспорт
	Возврат НовыеСлужебныеПараметрыЗапроса();
КонецФункции

// Инициализация параметров запроса "ОбновитьДанныеУчетнойЗаписи"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
//  * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
//  * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
//  * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
//  * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи
//  * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                         и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
//  * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
//  * Партнер - СправочникСсылка.Партнеры - Партнер, с которым заключен договор комиссионера для торговли на маркетплейсе, 
//                                          который является владельцем карточек номенклатуры маркетплейса в 1С.
//  * ИспользоватьИнтеграциюПоОстаткам - Булево - Признак работы с остатками.
//  * АвтоКоррекцияОстатков - Булево - признак автоматической корректировки остатков.
//  * ИспользоватьИнтеграциюПоЦенам - Булево - Признак работы с ценами.
//  * АвтоКоррекцияЦен - Булево - признак автоматической корректировки цен.
//  * РазрешенаВыгрузкаЦенНаМаркетплейс - Булево - признак автоматической выгрузки цен в маркетплейс
Функция НовыйПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("ИспользоватьИнтеграциюПоОстаткам", Ложь);
	Результат.Вставить("АвтоКоррекцияОстатков", Ложь);
	Результат.Вставить("ИспользоватьИнтеграциюПоЦенам", Ложь);
	Результат.Вставить("АвтоКоррекцияЦен", Ложь);
	Результат.Вставить("РазрешенаВыгрузкаЦенНаМаркетплейс", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьСкладыМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - Новые служебные параметры запроса:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * ЗапроситьДанныеСМаркетплейса - Булево - указание что сначала должна произойти внеочередная синхронизация
//                                           карточек складов маркетплейса и сервиса интеграции,
//                                           а затем должен быть сформирован ответ на данный запрос.
// * ТолькоНесопоставленные - Булево - получить только несопоставленные со складами 1С
// * Курсор - Строка - Позиция списка, с которой необходимо начать получение данных.
//                     Сама позиция в ответ включена не будет.
Функция НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ЗапроситьДанныеСМаркетплейса", Ложь);
	Результат.Вставить("ТолькоНесопоставленные", Ложь);
	Результат.Вставить("Курсор", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ВыгрузитьСклады"
// 
// Возвращаемое значение:
//  Структура - Новые служебные параметры запроса:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * Склады - Массив из СправочникСсылка.Склады - склады
Функция НовыйПараметрыЗапросаВыгрузитьСклады() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("Склады", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьБренды"
// 
// Возвращаемое значение:
//  см. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаПолучитьБренды() Экспорт
	
	Возврат НовыеСлужебныеПараметрыЗапроса();
	
КонецФункции

// Инициализация параметров запроса "ПолучитьКарточкуСкладаМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ИдентификаторСкладаСервиса - Строка - идентификатор склада сервиса интеграции.
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуСкладаМаркетплейса
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
Функция НовыйПараметрыЗапросаПолучитьКарточкуСкладаМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ИдентификаторСкладаСервиса", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьКатегории"
// 
// Возвращаемое значение:
//  см. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаПолучитьКатегории() Экспорт
	
	Возврат НовыеСлужебныеПараметрыЗапроса();
	
КонецФункции

// Инициализация параметров запроса "ОбновитьСкладыМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - Новые служебные параметры запроса:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * СкладыМаркетплейса - Массив из структура - значения обновляемых реквизитов складов маркетплейсов. Свойства:
//  ** ИдентификаторСкладаСервиса - Строка - идентификатор склада сервиса интеграции.
//  ** Склад - СправочникСсылка.Склады - склад 1С, связанный со складом сервиса интеграции.
Функция НовыйПараметрыЗапросаОбновитьСкладыМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("СкладыМаркетплейса", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьОграниченияОстатковТоваров"
// 
// Возвращаемое значение:
//  Структура - Параметры метода сервиса интеграции:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//   * ИмяМетода - Строка - Имя метода
//   * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
//   * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                          и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
//   * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
//   * Курсор - Строка - Позиция списка, с которой необходимо начать получение данных.
//                       Сама позиция в ответ включена не будет.
Функция НовыйПараметрыЗапросаПолучитьОграниченияОстатковТоваров() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("Курсор", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ВыгрузитьОграниченияОстатковТоваров"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * ОграниченияОстатковТоваров - Массив из Структура - ограничения, на остатки товаров. Свойства:
//   ** Объект - Произвольный - элемент фильтра, на который накладывается ограничение. Например: Номенклатура, Тип номенклатуры и т.д.
Функция НовыйПараметрыЗапросаВыгрузитьОграниченияОстатковТоваров() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ОграниченияОстатковТоваров", Новый массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "УдалитьОграниченияОстатковТоваров"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                         и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * ОграниченияОстатковТоваров - Массив из Структура - ограничения, на остатки товаров. Свойства:
//   ** Объект - Произвольный - элемент фильтра, на который накладывается ограничение. Например: Номенклатура, Тип номенклатуры и т.д.
Функция НовыйПараметрыЗапросаУдалитьОграниченияОстатковТоваров() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ОграниченияОстатковТоваров", Новый массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьНоменклатуруМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - Параметры метода сервиса интеграции:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//   * ИмяМетода - Строка - Имя метода
//   * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
//   * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                          и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
//   * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
//   * ЗапроситьДанныеСМаркетплейса - Булево - указание что сначала должна произойти внеочередная синхронизация
//                                             карточек номенклатуры маркетплейса и сервиса интеграции, а затем должен
//                                             быть сформирован ответ на данный запрос.
//   * Курсор - Строка - Позиция списка, с которой необходимо начать получение данных.
//                       Сама позиция в ответ включена не будет.
//   * ТолькоНесопоставленные - Булево - получить только несопоставленные с номенклатурой 1С
//   * Партнер - СправочникСсылка.Партнеры - Партнер
Функция НовыйПараметрыЗапросаПолучитьНоменклатуруМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	ДополнитьСлужебныеПараметрыЗапросаПолямиДляПагинации(Результат);
	
	Результат.Вставить("ЗапроситьДанныеСМаркетплейса", Ложь);
	Результат.Вставить("ТолькоНесопоставленные", Ложь);
	Результат.Вставить("Курсор", "");
	Результат.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ОбновитьНоменклатуруМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * НоменклатураМаркетплейса - Массив из структура - значения обновляемых реквизитов номенклатуры.Свойства:
//  ** ИдентификаторНоменклатурыСервиса - Строка - идентификатор номенклатуры сервиса интеграции.
//  ** НоменклатураКонтрагента - СправочникСсылка.НоменклатураКонтрагентов - соответствие номенклатуры 1С и маркетплейса
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса
Функция НовыйПараметрыЗапросаОбновитьНоменклатуруМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("НоменклатураМаркетплейса", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьКарточкуНоменклатурыМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * ИдентификаторНоменклатурыСервиса - Строка - идентификатор номенклатуры сервиса интеграции.
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуНоменклатурыМаркетплейса
Функция НовыйПараметрыЗапросаПолучитьКарточкуНоменклатурыМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ИдентификаторНоменклатурыСервиса", "");
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьОстаткиТоваров"
// 
// Возвращаемое значение:
//  Структура - Параметры метода сервиса интеграции:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//   * ИмяМетода - Строка - Имя метода
//   * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
//   * Курсор - Строка -	Позиция списка, с которой необходимо начать получение данных.
//						Сама позиция в ответ включена не будет.
//   * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
//   * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
Функция НовыйПараметрыЗапросаПолучитьОстаткиТоваровМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	ДополнитьСлужебныеПараметрыЗапросаПолямиДляПагинации(Результат);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ВыгрузитьОстаткиТоваров"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ВсеТовары - Булево - Истина - полная выгрузка данных из регистра "Остатки товаров для маркетплейсов"
// * Товары - Массив из Структура - ключевые параметры записи. Свойства:
//   ** Номенклатура - СправочникСсылка.Номенклатура - товар
//   ** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - характеристика
//   ** Склад - СправочникСсылка.Склады - склад
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
Функция НовыйПараметрыЗапросаВыгрузитьОстаткиТоваров() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ВсеТовары", Ложь);
	Результат.Вставить("Товары", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Новый параметры запроса выгрузить подготовленные остатки товаров.
// 
// Возвращаемое значение:
//  См. НовыеСлужебныеПараметрыЗапроса
Функция НовыйПараметрыЗапросаВыгрузитьПодготовленныеОстаткиТоваров() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПолучитьПоказателиНоменклатурыМаркетплейса"
// 
// Возвращаемое значение:
//  Структура - Параметры метода сервиса интеграции:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//   * ИмяМетода - Строка - Имя метода
//   * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
//   * ИспользоватьПагинацию - Булево - Использовать курсор-пагинацию при чтении списка данных
//   * ПолеСортировки - Строка - имя поля сортировки. "name" или "id"
//   * НаправлениеСортировки - Строка - направление сортировки списка. "desc" или "asc"
//   * Курсор - Строка - Позиция списка, с которой необходимо начать получение данных.
//						Сама позиция в ответ включена не будет.
//   * РазмерСтраницы - Число - Количество запрашиваемых записей. Если не задано, то возвращаются все записи.
//   * ПолучитьСводныйОстаток - Булево - признак получения сводного остатка по всем складам маркетплейса.
//   * ПолучитьЦены - Булево - признак получения цен.
//	 * НоменклатураМП - массив из СправочникСсылка.НоменклатураКонтрагентов - номенклатура, для которой необходимо получить значения показателей
//	 * БрендыМП - Массив из Строка - идентификаторы брендов опубликованных товаров.
//	 * КатегорииМП - Массив из Строка - идентификаторы категорий опубликованных товаров.
//	 * НаименованиеМП - Строка - подстрока поиска по наименованию товара.
//	 * ТолькоВНаличии - Булево - отбор по товарам, по которым есть остатки на складах, связанных с маркетплейсом.
//	 * МаркерСтатуса - Число - см. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен.
//	 * ВерсияКэша - Строка, УникальныйИдентификатор - Версия кэша
Функция НовыйПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	ДополнитьСлужебныеПараметрыЗапросаПолямиДляПагинации(Результат);
	
	Результат.Вставить("ПолучитьСводныйОстаток", Истина);
	Результат.Вставить("ПолучитьЦены", Истина);
	Результат.Вставить("НоменклатураМП", Новый Массив);
	Результат.Вставить("БрендыМП", Новый Массив);
	Результат.Вставить("КатегорииМП", Новый Массив);
	Результат.Вставить("НаименованиеМП", "");
	Результат.Вставить("Статус", 0);
	Результат.Вставить("ТолькоВНаличии", Неопределено);
	Результат.Вставить("МаркерСтатуса", -1);
	
	Результат.Вставить("ВерсияКэша", "");
	Результат.Вставить("СеансыУведомленияКлиента", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ПеренестиНоменклатуруПартнера"
// 
// Возвращаемое значение:
//  Структура - Параметры метода сервиса интеграции:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
// * ИмяМетода - Строка - Имя метода
// * ВыводитьСообщения - Булево - Признак вывода сообщений в ходе выполнения метода на экран
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
// * ТекущийВладелец - СправочникСсылка.Партнеры - предыдущий партнер.
// * НовыйВладелец      - СправочникСсылка.Партнеры - новый партнер.
Функция НовыйПараметрыЗапросаПеренестиНоменклатуруПартнера() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	Результат.Вставить("ТекущийВладелец", Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("НовыйВладелец", Справочники.Партнеры.ПустаяСсылка());
	
	Возврат Результат;
	
КонецФункции

// Инициализация параметров запроса "ВыгрузитьЦены"
// 
// Возвращаемое значение:
//  Структура - параметры запроса. Свойства:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - вид маркетплейса
// * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - учетная запись сервиса интеграции
// * ВсяНоменклатураМП - Булево - Истина - полная выгрузка данных из регистра "Цены номенклатуры маркетплейсов"
// * НоменклатураМП - Массив из СправочникСсылка.НоменклатураКонтрагентов - массив номенклатур маркетплейса
// * ВыводитьСообщения - Булево - выводить сообщения об ошибках, полученных в ходе выполнения метода
// * ИмяМетода - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены
// * ЗависитОт - Строка - Имя метода. Означает, что данный метод получит часть данных из метода, указанного в "ЗависитОт"
//                        и будет принят к выполнению только в случае успешного выполнения метода, указанного в "ЗависитОт".
// * ЗависимыеПоля - Строка - настройка зависимости полей. Пока реализован простейший механизм, когда имена полей полностью совпадают
Функция НовыйПараметрыЗапросаВыгрузитьЦены() Экспорт
	
	Результат = НовыеСлужебныеПараметрыЗапроса();
	
	Результат.Вставить("ВсяНоменклатураМП", Ложь);
	Результат.Вставить("НоменклатураМП", Новый Массив);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

Процедура ПередВыполнениемМетодаСервиса(ПараметрыМетода) Экспорт
	Возврат;
КонецПроцедуры

// Новые параметры запроса.
// 
// Параметры:
//  ПараметрыМетода - Структура - Параметры метода:
// * Метод - Строка - Имя процедуры 1С, которая отвечает за вызов метода запроса и обработку полученного результата
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// 
// Возвращаемое значение:
//  Структура - Параметры запроса:
// * Сервис - Строка - имя сервиса. "Доставка", "Маркетплейсы" и т.д.
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - раздел сервиса.
// * Сервер - Строка - адрес сервера (хоста) с которым осуществляется соединение. Например "1C.ru"
// * Приложение - Строка - Имя публикации сервиса. Например, "applications/IntegrationService"
// * КорневойURL - Строка - Путь к API сервиса
// * Метод - Строка - метод запроса. "GET", "POST" и т.п.
// * АдресМетода - Строка - Часть адрес метода запроса, включая параметры запроса. Например, "ping?p=1"
// * ПолныйАдресМетода - Строка - Полный адрес метода запроса, включая параметры запроса. Например, "applications/IntegrationService/api/ping?p=1"
// * Заголовки - Соответствие из строка - заголовки запроса
// * Авторизация - Булево - Признак необходимости получения токена доступа для доступа в сервис интеграции
// * ОбращениеКСервисуИнтеграции - Булево - Признак обращения к сервису интеграции
// * Данные - Строка, ДвоичныеДанные, Произвольный - Сначала данные для формирования тела запроса. А потом тело запроса
// * ДанныеИнтеграции - См. НовыеДанныеИнтеграции
// * ТипКонтента - Строка - Тип передаваемых данных. "application/json", "application/x-www-form-urlencoded"
// * Таймаут - Число - время ожидания ответа запроса
// * ИмяМетода - Строка - Имя метода 1С, который отвечает за вызов HTTP запроса и обработку полученного результата
// * ИдентификаторИТС - Строка - Идентификатор пользователя ИТС
// * УчетнаяЗапись - Строка - Идентификатор учетной записи (организации)
// * ВыводитьСообщения - Булево - признак вывода на экран сообщений, возникающих в ходе выполнения запроса
// * ПрочитатьВСоответствие - Булево - признак чтения тела ответа в соответствие или в структуру. Чтение в структуру не рекомендуется из-за ограничения на имена ключей
// * ИдентификаторНазначения - Неопределено, УникальныйИдентификатор - Уникальный идентификатор формы, из которой произведен вызов
// * АктивноеСоединение - HTTPСоединение, Неопределено - Активное соединение с сервером
Функция НовыеПараметрыЗапроса(ПараметрыМетода) Экспорт
	
	ПараметрыЗапроса = Новый Структура;
	
	// Основные параметры запроса
	ПараметрыЗапроса.Вставить("Сервер", "");
	ПараметрыЗапроса.Вставить("Приложение", "");
	ПараметрыЗапроса.Вставить("КорневойURL", "api/redirect/v1/%1/api/content");
	ПараметрыЗапроса.Вставить("Метод", "");
	ПараметрыЗапроса.Вставить("АдресМетода", "");
	ПараметрыЗапроса.Вставить("ПолныйАдресМетода", "");
	ПараметрыЗапроса.Вставить("Заголовки", Новый Соответствие);
	ПараметрыЗапроса.Вставить("Авторизация", Истина);
	ПараметрыЗапроса.Вставить("ОбращениеКСервисуИнтеграции", Истина);
	ПараметрыЗапроса.Вставить("Данные", "");
	ПараметрыЗапроса.Вставить("ТипКонтента", "application/json");
	ПараметрыЗапроса.Вставить("Таймаут", 20);
	
	// Служебные параметры, участвующие в формировании параметров запроса и в обработке результата ответа
	ПараметрыЗапроса.Вставить("Сервис", ИнтеграцияСМаркетплейсамиСИКлиентСервер.ИмяСервиса(ПараметрыМетода.ВидМаркетплейса));
	ПараметрыЗапроса.Вставить("ИмяМетода", "");
	ПараметрыЗапроса.Вставить("ВидМаркетплейса", Перечисления.ВидыМаркетплейсов.ПустаяСсылка());
	ПараметрыЗапроса.Вставить("ДанныеИнтеграции", НовыеДанныеИнтеграции());
	ПараметрыЗапроса.Вставить("ИдентификаторИТС", "");
	ПараметрыЗапроса.Вставить("УчетнаяЗапись", "");
	ПараметрыЗапроса.Вставить("ВыводитьСообщения", Истина);
	ПараметрыЗапроса.Вставить("ПрочитатьВСоответствие", Истина);
	ПараметрыЗапроса.Вставить("ИдентификаторНазначения", Неопределено);
	
	ПараметрыЗапроса.Вставить("АктивноеСоединение", Неопределено);
	
	ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ПараметрыМетода);
	ДополнитьПараметрыЗапроса(ПараметрыЗапроса, ПараметрыМетода);
	
	Возврат ПараметрыЗапроса;
	
КонецФункции

// Выполнить запрос к сервису.
// 
// Параметры:
// 	ПараметрыЗапроса - См. НовыеПараметрыЗапроса
// 	ПараметрыМетода - Структура - Параметры метода. Состав:
// 	 * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// 	Кэш - Неопределено, Структура - Кэшированные значения
// 	ПовторныйЗапрос - Булево - Признак ошибки обращения к основному серверу, либо признак последовательной передачи
//  	пакетов одного потока данных.
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода, Кэш = Неопределено, ПовторныйЗапрос = Ложь) Экспорт
	
	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	
	Если Не ПовторныйЗапрос Тогда
		ПараметрыЗапроса.ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Прокси = Неопределено;
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
		МодульПолучениеФайловИзИнтернета = ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернета");
		Прокси = МодульПолучениеФайловИзИнтернета.ПолучитьПрокси("https");
	КонецЕсли;
	
	УстановитьПолныйАдресМетода(ПараметрыЗапроса, ПовторныйЗапрос, РезультатЗапроса.Ошибки);
	Если РезультатЗапроса.Ошибки.Количество() > 0 Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ЗапросКСервису = Новый HTTPЗапрос(ПараметрыЗапроса.ПолныйАдресМетода);
	
	Если ЗначениеЗаполнено(ПараметрыЗапроса.Данные) Тогда
		Если ТипЗнч(ПараметрыЗапроса.Данные) = Тип("ДвоичныеДанные") Тогда
			ЗапросКСервису.УстановитьТелоИзДвоичныхДанных(ПараметрыЗапроса.Данные);
		ИначеЕсли ТипЗнч(ПараметрыЗапроса.Данные) = Тип("Строка") Тогда
			ЗапросКСервису.УстановитьТелоИзСтроки(ПараметрыЗапроса.Данные);
		Иначе
			Данные = ДанныеВJSON(ПараметрыЗапроса.Данные);
			ЗапросКСервису.УстановитьТелоИзСтроки(Данные);
		КонецЕсли;
	КонецЕсли;
	
	Если ПараметрыЗапроса.Метод = "GET" Тогда
		ЗапросКСервису.Заголовки.Вставить("Accept", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "POST" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "PUT" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "PATCH" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	ИначеЕсли ПараметрыЗапроса.Метод = "DELETE" Тогда
		ЗапросКСервису.Заголовки.Вставить("Content-Type", ПараметрыЗапроса.ТипКонтента);
	КонецЕсли;
	
	Если ПараметрыЗапроса.Авторизация Тогда
		ЗапросКСервису.Заголовки.Вставить("Authorization",
			УстановитьТокенДоступа(ПараметрыЗапроса.ДанныеИнтеграции, РезультатЗапроса.Ошибки));
		ЗапросКСервису.Заголовки.Вставить("KeyConnect", ПараметрыЗапроса.ДанныеИнтеграции.КлючПодключения);
		ЗапросКСервису.Заголовки.Вставить("MarketplaceID",
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркетплейсПоВиду(ПараметрыМетода.ВидМаркетплейса));
		ЗапросКСервису.Заголовки.Вставить("AccountID", XMLСтрока(ПараметрыЗапроса.УчетнаяЗапись));
		ЗапросКСервису.Заголовки.Вставить("Idempotency-Key", Новый УникальныйИдентификатор());
	КонецЕсли;
	
	ЗапросКСервису.Заголовки.Вставить("BaseID", ИдентификаторИнформационнойБазы());
	
	Для Каждого ПараметрыЗаголовка Из ПараметрыЗапроса.Заголовки Цикл
		//@skip-check property-return-type
		ЗапросКСервису.Заголовки.Вставить(ПараметрыЗаголовка.Ключ, ПараметрыЗаголовка.Значение);
	КонецЦикла;
	
	Соединение = УстановитьСоединениеССервером(ПараметрыЗапроса, Прокси, РезультатЗапроса, ПовторныйЗапрос);
	Если Соединение = Неопределено Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	Попытка
		
		Ответ = ВызватьHTTPМетод(Соединение, ЗапросКСервису, ПараметрыЗапроса);
		Если Ответ = Неопределено Тогда // Таймаут
			Данные = Неопределено;
		Иначе
			Данные = Ответ.ПолучитьТелоКакСтроку();
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = НСтр("ru = 'Ошибка выполнения запроса к сервису.';
							|en = 'An error occurred while executing a query to the service.'");
		
		РезультатЗапроса.КодСостояния = 500;
		РезультатЗапроса.Ошибки.Добавить(ТекстОшибки);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));

		Возврат РезультатЗапроса;
		
	КонецПопытки;
	
	ОбработатьОтветСервиса(РезультатЗапроса, ПараметрыЗапроса, ПараметрыМетода, Кэш, ЗапросКСервису, Ответ, Данные);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// После выполнения метода сервиса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыеСлужебныеПараметрыЗапроса
//  Результат - См. НовыйРезультатМетодаСервиса
//  АдресРезультата - Строка - Адрес результата в который необходимо упаковать результат
Процедура ПослеВыполненияМетодаСервиса(ПараметрыМетода, Результат, АдресРезультата) Экспорт
	
	Ошибки = Результат.Ошибки; // Массив из Строка
	
	СообщенияПользователю = ПолучитьСообщенияПользователю(Не ПараметрыМетода.ВыводитьСообщения);
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		Ошибки.Добавить(Сообщение.Текст);
	КонецЦикла;
	
	Если Ошибки.Количество() = 0
		И Результат.КодСостояния <> 0 // Запрос не выполнялся
		И Результат.КодСостояния <> 200 Тогда
		Ошибки.Добавить(ИнтеграцияСМаркетплейсамиСИКлиентСервер.ВнутренняяОшибкаСервиса());
	КонецЕсли;
	
	Если ЭтоАдресВременногоХранилища(АдресРезультата) Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСФормами

// Установить значения реквизитов по параметрам.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма, из которой вызвана процедура.
//  Параметры - Структура - Параметры
Процедура УстановитьЗначенияРеквизитовПоПараметрам(Форма, Параметры) Экспорт
	
	ИсключаемыеСвойства = Новый Массив; // Массив из Строка
	ИсключаемыеСвойства.Добавить("ТолькоПросмотр");
	ИсключаемыеСвойства.Добавить("ЗакрыватьПриВыборе");
	ИсключаемыеСвойства.Добавить("ЗакрыватьПриЗакрытииВладельца");

	ЗаполнитьЗначенияСвойств(Форма, Параметры, , СтрСоединить(ИсключаемыеСвойства, ", "));
	
КонецПроцедуры

#КонецОбласти

#Область КонвертацияДанных

//@skip-check invocation-parameter-type-intersect
// Получает значение реквизита структуры/соответствия.
// 
// Параметры:
//  Источник - Структура, Соответствие из Произвольный - источник данных.
//  ИмяРеквизита - Строка - Имя реквизита. Поддерживается разделение через "."
//  ТипЗначения - Неопределено, Строка - Тип значения, к которому нужно привести пустое значение реквизита
//  ЗначениеПоУмолчанию - Неопределено, Произвольный - Значение по умолчанию
// 
// Возвращаемое значение:
//  Неопределено, Произвольный, Строка, Дата, Булево, Структура, Массив из Произвольный - Полученное значение
Функция ПолучитьЗначение(Знач Источник, Знач ИмяРеквизита, ТипЗначения = "", ЗначениеПоУмолчанию = Неопределено) Экспорт
	
	Значение = ЗначениеПоУмолчанию;
	
	МассивПолей = СтрРазделить(ИмяРеквизита, ".");
	КоличествоПолей = МассивПолей.Количество()-1;
	
	Для Ид = 0 По КоличествоПолей-1 Цикл
		ИмяСвойства = МассивПолей[Ид];
		
		Если ТипЗнч(Источник) = Тип("Структура")
			И Источник.Свойство(ИмяСвойства) Тогда
			Источник = Источник[ИмяСвойства]; // Структура
		Иначе
			Возврат Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	ИмяРеквизита = МассивПолей[КоличествоПолей];
	
	ВыполнитьПреобразование = Ложь;
	
	Если ТипЗнч(Источник) = Тип("Структура") И Источник.Свойство(ИмяРеквизита) Тогда
		
		ИсходноеЗначение = Источник[ИмяРеквизита]; // Произвольный
		ВыполнитьПреобразование = Истина;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("Соответствие") Тогда
		
		ИсходноеЗначение = Источник.Получить(ИмяРеквизита);
		ВыполнитьПреобразование = Истина;
		
	КонецЕсли;
	
	Если ВыполнитьПреобразование Тогда
		
		Если ИсходноеЗначение = Неопределено Тогда
			Значение = ЗначениеПоУмолчанию;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Массив") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Структура") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Дата") Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗнч(ИсходноеЗначение) = Тип("Строка") И ВРег(ИсходноеЗначение) = "NONE" Тогда
			Значение = ЗначениеПоУмолчанию;
		ИначеЕсли ТипЗначения = "" Тогда
			Значение = ИсходноеЗначение;
		ИначеЕсли ТипЗначения = "Дата" Тогда
			Значение = СтрокаВДату(ИсходноеЗначение);
		ИначеЕсли ТипЗначения = "Число" Тогда
			Значение = СтрокаВЧисло(ИсходноеЗначение);
		ИначеЕсли ТипЗначения = "ХранилищеЗначения" Тогда
			Значение = ХранилищеЗначенияИзJSON(ИсходноеЗначение);
		Иначе
			ТребуемыйТип = Новый ОписаниеТипов(ТипЗначения);
			Значение = ТребуемыйТип.ПривестиЗначение(ИсходноеЗначение);
		КонецЕсли;
		
		Если Значение = Неопределено Тогда
			Значение = ЗначениеПоУмолчанию;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Значение;
	
КонецФункции

#КонецОбласти

//@skip-check property-return-type
//@skip-check statement-type-change
#Область РегламентныеЗадания

// Синхронизация с сервисом интеграции
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ИмяМетода - Строка, Неопределено - имя метода
Процедура СинхронизацияССервисомИнтеграции(УчетнаяЗапись, ИмяМетода = Неопределено) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.СинхронизацияССервисомИнтеграции);
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСWildberries")
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСLamoda") Тогда
		ВызватьИсключение НСтр("ru = 'Регламентное задание недоступно по функциональным опциям.';
								|en = 'The scheduled job is not available by functional options.'");
	КонецЕсли;
	
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидМаркетплейса");
	Если ИмяМетода = Неопределено Или ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады() Тогда
		СинхронизацияССервисомИнтеграции_Склады(УчетнаяЗапись, ВидМаркетплейса);
	КонецЕсли;
	Если ИмяМетода = Неопределено Или ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса() Тогда
		СинхронизацияССервисомИнтеграции_НоменклатураКонтрагентов(УчетнаяЗапись, ВидМаркетплейса);
	КонецЕсли;
	Если ИмяМетода = Неопределено Тогда
		СинхронизацияССервисомИнтеграции_ВыгрузкаОстатковТоваров(УчетнаяЗапись, ВидМаркетплейса);
	КонецЕсли;
	
КонецПроцедуры

// Загрузка или обновление номенклатуры контрагентов на основании данных сервиса интеграции.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  Партнер - СправочникСсылка.Партнеры - владелец создаваемых/обновляемых карточек номенклатуры контрагента
//  НоменклатураМаркетплейса - Массив из структура - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаНоменклатурыМаркетплейса
//  Результат - Структура - Результат обработки данных:
//  * Ошибки - Массив из строка - ошибки.
//  * Получено - Число - количество обрабатываемых карточек.
//  * Добавлено - Число - количество созданных элементов справочника "НоменклатураКонтрагентов".
//  * Обновлено - Число - количество обновленных элементов справочника "НоменклатураКонтрагентов".
//  * Отклонено - Число - количество карточек, при обработке которых возникла ошибка.
Процедура ОбновитьКарточкиНоменклатурыКонтрагентов(УчетнаяЗапись, Партнер, НоменклатураМаркетплейса, Результат) Экспорт
	
	Получено = НоменклатураМаркетплейса.Количество();
	Если Получено = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Результат.Получено = Результат.Получено + Получено;
	
	ДлинаНаименования = Метаданные.Справочники.НоменклатураКонтрагентов.ДлинаНаименования;
	
	ТаблицаКарточек = Новый ТаблицаЗначений;
	ТаблицаКарточек.Колонки.Добавить("ИдентификаторНоменклатурыСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(36)));
	ТаблицаКарточек.Колонки.Добавить("НаименованиеНоменклатурыСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
	ТаблицаКарточек.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(ДлинаНаименования)));
	ТаблицаКарточек.Колонки.Добавить("ПолноеНаименованиеНоменклатурыСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(1024)));
	ТаблицаКарточек.Колонки.Добавить("ИдентификаторХарактеристики", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(1024)));
	ТаблицаКарточек.Колонки.Добавить("НаименованиеХарактеристики", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(1024)));
	ТаблицаКарточек.Колонки.Добавить("НоменклатураКонтрагента", Новый ОписаниеТипов("СправочникСсылка.НоменклатураКонтрагентов"));
	ТаблицаКарточек.Колонки.Добавить("Штрихкод", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
	ТаблицаКарточек.Колонки.Добавить("Артикул", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
	ТаблицаКарточек.Колонки.Добавить("АртикулМаркетплейса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100)));
	
	Для Каждого Карточка Из НоменклатураМаркетплейса Цикл
		
		Если ПустаяСтрока(Карточка.ИдентификаторНоменклатурыСервиса) Тогда
			Результат.Ошибки.Добавить(НСтр("ru = 'Получена карточка номенклатуры без идентификатора.';
											|en = 'An item card without an ID is received.'"));
			Результат.Отклонено = Результат.Отклонено + 1;
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ТаблицаКарточек.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Карточка);
		
		// Формула для формирования наименования номенклатуры контрагентов
		НоваяСтрока.Наименование = НоваяСтрока.ПолноеНаименованиеНоменклатурыСервиса;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаКарточек.ИдентификаторНоменклатурыСервиса КАК ИдентификаторНоменклатурыСервиса,
	|	ТаблицаКарточек.НаименованиеНоменклатурыСервиса КАК НаименованиеНоменклатурыСервиса,
	|	ТаблицаКарточек.ПолноеНаименованиеНоменклатурыСервиса КАК ПолноеНаименованиеНоменклатурыСервиса,
	|	ТаблицаКарточек.Наименование КАК Наименование,
	|	ТаблицаКарточек.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
	|	ТаблицаКарточек.НаименованиеХарактеристики КАК НаименованиеХарактеристики,
	|	ТаблицаКарточек.НоменклатураКонтрагента КАК НоменклатураКонтрагента,
	|	ТаблицаКарточек.Штрихкод КАК Штрихкод,
	|	ТаблицаКарточек.Артикул КАК Артикул,
	|	ТаблицаКарточек.АртикулМаркетплейса КАК АртикулМаркетплейса
	|ПОМЕСТИТЬ врТаблицаКарточек
	|ИЗ
	|	&ТаблицаКарточек КАК ТаблицаКарточек
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врТаблицаКарточек.ИдентификаторНоменклатурыСервиса КАК Идентификатор,
	|	врТаблицаКарточек.НаименованиеНоменклатурыСервиса КАК НаименованиеНоменклатуры,
	|	врТаблицаКарточек.ПолноеНаименованиеНоменклатурыСервиса КАК НаименованиеПолное,
	|	врТаблицаКарточек.Наименование КАК Наименование,
	|	врТаблицаКарточек.ИдентификаторХарактеристики КАК ИдентификаторХарактеристики,
	|	врТаблицаКарточек.НаименованиеХарактеристики КАК НаименованиеХарактеристики,
	|	врТаблицаКарточек.Штрихкод КАК Штрихкод,
	|	врТаблицаКарточек.Артикул КАК Артикул,
	|	врТаблицаКарточек.АртикулМаркетплейса КАК КодНоменклатуры,
	|	НоменклатураКонтрагентов.Ссылка КАК НоменклатураКонтрагента,
	|	ВЫБОР
	|		КОГДА врТаблицаКарточек.НоменклатураКонтрагента = ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)
	|				И НоменклатураКонтрагентов.Ссылка ЕСТЬ NULL
	|			ТОГДА &КодСоздать
	|		КОГДА врТаблицаКарточек.НоменклатураКонтрагента <> ЗНАЧЕНИЕ(Справочник.НоменклатураКонтрагентов.ПустаяСсылка)
	|				И врТаблицаКарточек.НоменклатураКонтрагента <> НоменклатураКонтрагентов.Ссылка
	|			ТОГДА &КодОшибка
	|		КОГДА НоменклатураКонтрагентов.Ссылка ЕСТЬ НЕ NULL 
	|				И (НоменклатураКонтрагентов.Наименование <> врТаблицаКарточек.Наименование
	|					ИЛИ НоменклатураКонтрагентов.НаименованиеПолное <> врТаблицаКарточек.ПолноеНаименованиеНоменклатурыСервиса
	|					ИЛИ НоменклатураКонтрагентов.КодНоменклатуры <> врТаблицаКарточек.АртикулМаркетплейса
	|					ИЛИ НоменклатураКонтрагентов.Артикул <> врТаблицаКарточек.Артикул
	|					ИЛИ НоменклатураКонтрагентов.НаименованиеХарактеристики <> врТаблицаКарточек.НаименованиеХарактеристики
	|					ИЛИ НоменклатураКонтрагентов.ИдентификаторХарактеристики <> врТаблицаКарточек.ИдентификаторХарактеристики
	|					ИЛИ НоменклатураКонтрагентов.Штрихкод <> врТаблицаКарточек.Штрихкод)
	|			ТОГДА &КодОбновить
	|		КОГДА СоответствияМП.Объект1С ЕСТЬ НЕ NULL 
	|				И СоответствияМП.НаименованиеОбъектаМаркетплейса <> врТаблицаКарточек.Наименование
	|			ТОГДА &КодОбновить
	|		КОГДА СоответствияМП.Объект1С ЕСТЬ NULL И НоменклатураКонтрагентов.Ссылка ЕСТЬ НЕ NULL
	|			ТОГДА &КодСоздатьСоответствие
	|		ИНАЧЕ
	|			&КодПропустить
	|	КОНЕЦ КАК Действие,
	|	ЕстьNULL(СоответствияМП.ДатаАктуальности, ДатаВремя(1,1,1,0,0,0)) КАК ДатаАктуальности
	|ИЗ
	|	врТаблицаКарточек КАК врТаблицаКарточек
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Идентификатор = врТаблицаКарточек.ИдентификаторНоменклатурыСервиса)
	|			И (НоменклатураКонтрагентов.Владелец = &Партнер)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияМП
	|		ПО СоответствияМП.УчетнаяЗаписьМаркетплейса = &УчетнаяЗапись
	|		И СоответствияМП.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|		И СоответствияМП.ИдентификаторОбъектаМаркетплейса = врТаблицаКарточек.ИдентификаторНоменклатурыСервиса
	|";

	Запрос.УстановитьПараметр("КодПропустить", 0);
	Запрос.УстановитьПараметр("КодСоздать", 1);
	Запрос.УстановитьПараметр("КодОбновить", 2);
	Запрос.УстановитьПараметр("КодСоздатьСоответствие", 3);
	Запрос.УстановитьПараметр("КодОшибка", 99);
	Запрос.УстановитьПараметр("ТаблицаКарточек", ТаблицаКарточек);
	Запрос.УстановитьПараметр("Партнер", Партнер);
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;

	Выборка = РезультатЗапроса.Выбрать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Справочник.НоменклатураКонтрагентов");
	ЭлементБлокировки.УстановитьЗначение("ВладелецНоменклатуры", Партнер);
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	
	ЗаписиДляРегистрации = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();

	НачатьТранзакцию();
	Попытка
		
		Блокировка.Заблокировать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Действие = Запрос.Параметры.КодПропустить Тогда
				Продолжить;
			КонецЕсли;
			
			Если Выборка.Действие = Запрос.Параметры.КодОшибка Тогда
				
				ТекстОшибки = СтрШаблон(НСтр("ru = 'В карточке %1 должен быть идентификатор %2';
											|en = 'The ""%1"" card must contain ID %2'"), Выборка.НоменклатураКонтрагента, Выборка.ИдентификаторНоменклатурыСервиса);
				Результат.Ошибки.Добавить(ТекстОшибки);
				
				ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
						УровеньЖурналаРегистрации.Ошибка,,Выборка.НоменклатураКонтрагента,
						ТекстОшибки);
				
				Результат.Отклонено = Результат.Отклонено + 1;
				
				Продолжить;
			КонецЕсли;
			
			КарточкаСсылка = СоздатьОбновитьКарточкуНоменклатурыКонтрагентов(Выборка, Запрос.Параметры, Результат);
			Если КарточкаСсылка = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Запись = ЗаписиДляРегистрации.Добавить();
			Запись.УчетнаяЗаписьМаркетплейса = УчетнаяЗапись;
			Запись.ВидОбъектаМаркетплейса = Перечисления.ВидыОбъектовМаркетплейсов.Товар;
			Запись.ИдентификаторОбъектаМаркетплейса = Выборка.Идентификатор;
			Запись.Объект1С = КарточкаСсылка;
			Запись.НаименованиеОбъектаМаркетплейса = Выборка.Наименование;
			Запись.ДатаАктуальности = Выборка.ДатаАктуальности;
			
			Если Выборка.Действие = Запрос.Параметры.КодСоздать 
				Или Выборка.Действие = Запрос.Параметры.КодСоздатьСоответствие Тогда
				Результат.Добавлено = Результат.Добавлено + 1;
			ИначеЕсли Выборка.Действие = Запрос.Параметры.КодОбновить Тогда
				Результат.Обновлено = Результат.Обновлено + 1;
			КонецЕсли;
			
		КонецЦикла;
		
		ЗаписиДляРегистрации.Записать(РежимЗамещения.Слияние);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Результат.Ошибки.Добавить(ТекстОшибки);
		
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Ошибка,,,
			ТекстОшибки);
		
	КонецПопытки;
		
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат;
	
КонецПроцедуры

// Выгрузить остатки товаров регламентным заданием.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ДополнительныеПараметры - Структура - Дополнительные параметры метода, обязательно содержит:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
Процедура ВыгрузитьОстаткиТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры) Экспорт
	
	ВидМаркетплейса = ДополнительныеПараметры.ВидМаркетплейса;
	
	Если ИнтеграцияСМаркетплейсомОтключена(ВидМаркетплейса) Тогда
		ВызватьИсключение НСтр("ru = 'Регламентное задание недоступно по функциональным опциям.';
								|en = 'The scheduled job is not available by functional options.'");
	КонецЕсли;
	
	СинхронизацияССервисомИнтеграции_РасчетОстатковТоваров(УчетнаяЗапись, ВидМаркетплейса);
	
КонецПроцедуры

// Синхронизация карточек складов с сервисом интеграции.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
Процедура СинхронизацияССервисомИнтеграции_Склады(Знач УчетнаяЗапись, ВидМаркетплейса) Экспорт
	
	УзлыПланаОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиУзлы(УчетнаяЗапись);
	Если УзлыПланаОбмена.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = НовыйПараметрыЗапросаВыгрузитьСклады();
	ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса();
	
	ПараметрыМетода.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады();
	
	// Получим сопоставленные склады из сервиса интеграции
	ПараметрыЗапроса = НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса();

	ПараметрыЗапроса.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыЗапроса.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыЗапроса.ИмяМетода = ИмяМетода;
	
	РезультатМетода = ПолучитьСкладыМаркетплейса(ПараметрыЗапроса);
	
	// исключим склады
	ТаблицаИспользуемыхСкладов = Новый ТаблицаЗначений;
	ТаблицаИспользуемыхСкладов.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	ТаблицаИспользуемыхСкладов.Колонки.Добавить("ИдентификаторСкладаСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(50)));
	ТаблицаИспользуемыхСкладов.Колонки.Добавить("НаименованиеСкладаСервиса", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150)));
	
	ИспользуемыеСклады = Новый Массив; // Массив Из СправочникСсылка.Склады
	
	Для Каждого ОписаниеСклада Из РезультатМетода.СкладыМаркетплейса Цикл
		Если ЗначениеЗаполнено(ОписаниеСклада.Склад) Тогда
			НоваяСтрока = ТаблицаИспользуемыхСкладов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеСклада);
			
			ИспользуемыеСклады.Добавить(ОписаниеСклада.Склад);
		КонецЕсли;
	КонецЦикла;
		
	// Получим склады с плана обмена
	ЗапросСклады = Новый Запрос;
	ЗапросСклады.Текст =
	"ВЫБРАТЬ
	|	ТаблицаИспользуемыхСкладов.Склад КАК Склад,
	|	ТаблицаИспользуемыхСкладов.ИдентификаторСкладаСервиса КАК ИдентификаторСкладаСервиса,
	|	ТаблицаИспользуемыхСкладов.НаименованиеСкладаСервиса КАК НаименованиеСкладаСервиса
	|ПОМЕСТИТЬ врИспользуемыеСклады
	|ИЗ
	|	&ТаблицаИспользуемыхСкладов КАК ТаблицаИспользуемыхСкладов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеУзла.Ссылка КАК Склад,
	|	врИспользуемыеСклады.ИдентификаторСкладаСервиса КАК ИдентификаторСклада,
	|	врИспользуемыеСклады.НаименованиеСкладаСервиса КАК НаименованиеСклада,
	|	&КодВключить КАК Действие,
	|	ДанныеУзла.Ссылка.ЭтоГруппа КАК ЭтоГруппа
	|ИЗ
	|	Справочник.Склады.Изменения КАК ДанныеУзла
	|		ЛЕВОЕ СОЕДИНЕНИЕ врИспользуемыеСклады КАК врИспользуемыеСклады
	|		ПО (врИспользуемыеСклады.Склад = ДанныеУзла.Ссылка)
	|ГДЕ
	|	ДанныеУзла.Узел В(&УзлыПланаОбмена)
	|	И ДанныеУзла.Ссылка В ИЕРАРХИИ(&ИспользуемыеСклады)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеУзла.Ссылка,
	|	СоответствияСкладов.ИдентификаторОбъектаМаркетплейса,
	|	СоответствияСкладов.НаименованиеОбъектаМаркетплейса,
	|	&КодИсключить,
	|	ДанныеУзла.Ссылка.ЭтоГруппа
	|ИЗ
	|	Справочник.Склады.Изменения КАК ДанныеУзла
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияСкладов
	|		ПО СоответствияСкладов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|			И СоответствияСкладов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
	|			И (СоответствияСкладов.Объект1С = ДанныеУзла.Ссылка)
	|ГДЕ
	|	ДанныеУзла.Узел В(&УзлыПланаОбмена)
	|	И НЕ ДанныеУзла.Ссылка В ИЕРАРХИИ (&ИспользуемыеСклады)
	|ИТОГИ ПО
	|	Действие,
	|	Склад ИЕРАРХИЯ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫРАЗИТЬ(ДанныеУзла.Ссылка КАК Справочник.Склады) КАК Склад
	|ИЗ
	|	Справочник.Склады.Изменения КАК ДанныеУзла
	|ГДЕ
	|	ДанныеУзла.Узел В(&УзлыПланаОбмена)";
		
	ЗапросСклады.УстановитьПараметр("УзлыПланаОбмена", УзлыПланаОбмена);
	ЗапросСклады.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	ЗапросСклады.УстановитьПараметр("ТаблицаИспользуемыхСкладов", ТаблицаИспользуемыхСкладов);
	ЗапросСклады.УстановитьПараметр("ИспользуемыеСклады", ИспользуемыеСклады);
	ЗапросСклады.УстановитьПараметр("КодВключить", 1);
	ЗапросСклады.УстановитьПараметр("КодИсключить", 0);
	
	ПакетЗапросаСклады = ЗапросСклады.ВыполнитьПакет();
		
	Если Не ПакетЗапросаСклады[1].Пустой() Тогда
		
		НаборВключить = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		НаборИсключить = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.СоответствияОбъектовМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = ПакетЗапросаСклады[1];
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		ЭлементБлокировки.УстановитьЗначение("ВидОбъектаМаркетплейса", Перечисления.ВидыОбъектовМаркетплейсов.Склад);
		ЭлементБлокировки.УстановитьЗначение("Объект1С", "Склад");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		ИдентификаторСклада = "";
		НаименованиеСклада = "";
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			ВыборкаДействие = ПакетЗапросаСклады[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Действие");
			Пока ВыборкаДействие.Следующий() Цикл
			
				Выборка = ВыборкаДействие.Выбрать();
				Пока Выборка.Следующий() Цикл
					
					Если Выборка.ИдентификаторСклада <> NULL Тогда
						ИдентификаторСклада = Выборка.ИдентификаторСклада;
						НаименованиеСклада = Выборка.НаименованиеСклада;
					КонецЕсли;
					
					Если Выборка.ТипЗаписи() <> ТипЗаписиЗапроса.ДетальнаяЗапись Тогда
						Продолжить;
					КонецЕсли;
					
					Если Выборка.ЭтоГруппа Тогда
						Продолжить;
					КонецЕсли;
					
					Если ВыборкаДействие.Действие = ЗапросСклады.Параметры.КодВключить Тогда
						Запись = НаборВключить.Добавить();
						Запись.НаименованиеОбъектаМаркетплейса = НаименованиеСклада;
					ИначеЕсли ВыборкаДействие.Действие = ЗапросСклады.Параметры.КодИсключить Тогда
						Запись = НаборИсключить.Добавить();
					Иначе
						Продолжить;
					КонецЕсли;
					
					Запись.УчетнаяЗаписьМаркетплейса = УчетнаяЗапись;
					Запись.ВидОбъектаМаркетплейса = Перечисления.ВидыОбъектовМаркетплейсов.Склад;
					Запись.ИдентификаторОбъектаМаркетплейса = ИдентификаторСклада;
					Запись.Объект1С = Выборка.Склад;
					
				КонецЦикла;
			КонецЦикла;
			
			// Сначала очистим без отбора по идентификаторам объектов маркетплейса
			Если НаборИсключить.Количество() > 0 Тогда
				Для Каждого УзелОбмена Из УзлыПланаОбмена Цикл
					НаборИсключить.ОбменДанными.Получатели.Добавить(УзелОбмена);
				КонецЦикла;
				НаборИсключить.Записать(РежимЗамещения.Удаление);
			КонецЕсли;
			
			// Добавим записи по определенным идентификаторам объектов маркетплейса
			Если НаборВключить.Количество() > 0 Тогда
				Для Каждого УзелОбмена Из УзлыПланаОбмена Цикл
					НаборВключить.ОбменДанными.Получатели.Добавить(УзелОбмена);
				КонецЦикла;
				НаборВключить.Записать(РежимЗамещения.Слияние);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
			
			Возврат;
		КонецПопытки;
		
		// Очистка регистрации на плане обмена
		ВыборкаСклады = ПакетЗапросаСклады[2].Выбрать();
		СкладыУзла = Новый Массив;
		Пока ВыборкаСклады.Следующий() Цикл
			//@skip-check typed-value-adding-to-untyped-collection
			СкладыУзла.Добавить(ВыборкаСклады.Склад);
		КонецЦикла;
		Для Каждого УзелОбмена Из УзлыПланаОбмена Цикл
			ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, СкладыУзла);
		КонецЦикла;
		
	КонецЕсли;
		
КонецПроцедуры

// Синхронизация карточек товаров с сервисом интеграции
// 
// Параметры:
//  УчетнаяЗапись - Неопределено, СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов -
Процедура СинхронизацияССервисомИнтеграции_НоменклатураКонтрагентов(Знач УчетнаяЗапись, ВидМаркетплейса) Экспорт
	
	ОбновитьНоменклатуруВСервисеИнтеграции(УчетнаяЗапись, ВидМаркетплейса);
	
КонецПроцедуры

// Формирование остатков для выгрузки в маркетплейс.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
Процедура СинхронизацияССервисомИнтеграции_РасчетОстатковТоваров(Знач УчетнаяЗапись, ВидМаркетплейса) Экспорт
	
	Если Не ЗначениеЗаполнено(УчетнаяЗапись) 
		Или Не ЭтоМаркетплейсСИ(ВидМаркетплейса) Тогда
		Возврат;
	КонецЕсли;
	
	КлючеваяОперация = СтрШаблон("СинхронизацияССервисомИнтеграции.РасчетОстатковТоваров.%1",
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркетплейсПоВиду(ВидМаркетплейса));
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	КоличествоДанных = 0;
	
	ЕстьОшибки = Ложь;
	
	ДатаВыгрузкиОстатков = ТекущаяДатаСеанса();
	
	УзлыПланаОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиУзлы(УчетнаяЗапись);
	
	// Удалим записи по номенклатуре и характеристике, исключенным из "Номенклатуры контрагента"
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
	|	НоменклатураКонтрагентов.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ врИспользуемаяНоменклатура
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = СоответствияОбъектовМаркетплейсов.Объект1С)
	|ГДЕ
	|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 5000
	|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	ОстаткиТоваровМаркетплейсов.Склад КАК Склад,
	|	ОстаткиТоваровМаркетплейсов.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровМаркетплейсов.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваровМаркетплейсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ врИспользуемаяНоменклатура КАК врИспользуемаяНоменклатура
	|		ПО ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса = врИспользуемаяНоменклатура.УчетнаяЗаписьМаркетплейса
	|			И ОстаткиТоваровМаркетплейсов.Номенклатура = врИспользуемаяНоменклатура.Номенклатура
	|			И ОстаткиТоваровМаркетплейсов.Характеристика = врИспользуемаяНоменклатура.Характеристика
	|ГДЕ
	|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И врИспользуемаяНоменклатура.УчетнаяЗаписьМаркетплейса ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	КоличествоПопыток = 3;
	
	Пока Не РезультатЗапроса.Пустой() И КоличествоПопыток > 0 Цикл
		
		НаборЗаписейУдаление = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписейУдаление.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("УчетнаяЗаписьМаркетплейса", "УчетнаяЗаписьМаркетплейса");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
		
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Запись = НаборЗаписейУдаление.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
			КонецЦикла;
			
			Для Каждого УзелПланаОбмена Из УзлыПланаОбмена Цикл
				НаборЗаписейУдаление.ОбменДанными.Получатели.Добавить(УзелПланаОбмена);
			КонецЦикла;
			НаборЗаписейУдаление.Записать(РежимЗамещения.Удаление);
				
			ЗафиксироватьТранзакцию();
				
		Исключение
			ОтменитьТранзакцию();
			ЕстьОшибки = Истина;
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			КоличествоПопыток = КоличествоПопыток - 1;
		КонецПопытки;
		
		//@skip-warning порционное удаление записей
		РезультатЗапроса = Запрос.Выполнить();
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Удалим записи по складам, исключенным из "Соответствия складов"
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ врИспользуемыеСклады
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО (Склады.Ссылка = СоответствияОбъектовМаркетплейсов.Объект1С)
	|ГДЕ
	|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 5000
	|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	ОстаткиТоваровМаркетплейсов.Склад КАК Склад,
	|	ОстаткиТоваровМаркетплейсов.Номенклатура КАК Номенклатура,
	|	ОстаткиТоваровМаркетплейсов.Характеристика КАК Характеристика
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваровМаркетплейсов
	|		ЛЕВОЕ СОЕДИНЕНИЕ врИспользуемыеСклады КАК врИспользуемыеСклады
	|		ПО ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса = врИспользуемыеСклады.УчетнаяЗаписьМаркетплейса
	|			И ОстаткиТоваровМаркетплейсов.Склад = врИспользуемыеСклады.Склад
	|ГДЕ
	|	ОстаткиТоваровМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И врИспользуемыеСклады.УчетнаяЗаписьМаркетплейса ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();
	КоличествоПопыток = 3;
	
	Пока Не РезультатЗапроса.Пустой() Цикл
		
		НаборЗаписейУдаление = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписейУдаление.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);

		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("УчетнаяЗаписьМаркетплейса", "УчетнаяЗаписьМаркетплейса");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
		
			Выборка = РезультатЗапроса.Выбрать();
			Пока Выборка.Следующий() Цикл
				Запись = НаборЗаписейУдаление.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
			КонецЦикла;
			
			Для Каждого УзелПланаОбмена Из УзлыПланаОбмена Цикл
				НаборЗаписейУдаление.ОбменДанными.Получатели.Добавить(УзелПланаОбмена);
			КонецЦикла;
			НаборЗаписейУдаление.Записать(РежимЗамещения.Удаление);
				
			ЗафиксироватьТранзакцию();
				
		Исключение
			ОтменитьТранзакцию();
			ЕстьОшибки = Истина;
			ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЗаписьЖурналаРегистрации(
				СобытиеЖурналаРегистрации(ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка,,, ТекстОшибки);
			КоличествоПопыток = КоличествоПопыток - 1;
		КонецПопытки;
		
		//@skip-warning порционное удаление записей
		РезультатЗапроса = Запрос.Выполнить();
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
	
	// Разнесем новые / измененные номенклатуры по складам
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ИзмененнаяНК.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	ИзмененнаяНК.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
	|	ИзмененнаяНК.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
	|	ИзмененнаяНК.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
	|	ИзмененнаяНК.Объект1С КАК Объект1С,
	|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
	|	НоменклатураКонтрагентов.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА НоменклатураКонтрагентов.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удалено
	|ПОМЕСТИТЬ врНоменклатура
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов.Изменения КАК ИзмененнаяНК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = ИзмененнаяНК.Объект1С)
	|ГДЕ
	|	ИзмененнаяНК.Узел В(&УзлыПланаОбмена)
	|	И ИзмененнаяНК.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И ИзмененнаяНК.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Склады.Ссылка КАК Склад
	|ПОМЕСТИТЬ врСклады
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СопоставленныеСклады
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
	|		ПО (Склады.Ссылка = СопоставленныеСклады.Объект1С)
	|			И (НЕ Склады.ЭтоГруппа)
	|ГДЕ
	|	СопоставленныеСклады.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СопоставленныеСклады.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врНоменклатура.Номенклатура КАК Номенклатура,
	|	врНоменклатура.Характеристика КАК Характеристика,
	|	врСклады.Склад КАК Склад
	|ПОМЕСТИТЬ врДанные
	|ИЗ
	|	врСклады КАК врСклады
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врНоменклатура КАК врНоменклатура
	|		ПО (врНоменклатура.Удалено = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врДанные.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врДанные.Номенклатура КАК Номенклатура,
	|	врДанные.Характеристика КАК Характеристика,
	|	врДанные.Склад КАК Склад,
	|	ЕСТЬNULL(ОстаткиТоваров.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ОстаткиТоваров.ДатаВыгрузкиОстатков, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаВыгрузкиОстатков
	|ИЗ
	|	врДанные КАК врДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваров
	|		ПО врДанные.УчетнаяЗаписьМаркетплейса = ОстаткиТоваров.УчетнаяЗаписьМаркетплейса
	|			И врДанные.Номенклатура = ОстаткиТоваров.Номенклатура
	|			И врДанные.Характеристика = ОстаткиТоваров.Характеристика
	|			И врДанные.Склад = ОстаткиТоваров.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врНоменклатура.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врНоменклатура.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
	|	врНоменклатура.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
	|	врНоменклатура.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
	|	врНоменклатура.Объект1С КАК Объект1С
	|ИЗ
	|	врНоменклатура КАК врНоменклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(врСклады.Склад) КАК КоличествоЗаписей
	|ИЗ
	|	врСклады КАК врСклады
	|ГДЕ
	|	врСклады.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(врСклады.Склад) > 0";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	Запрос.УстановитьПараметр("УзлыПланаОбмена", УзлыПланаОбмена);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);
	
	ЗаписиКОбработке = РезультатЗапроса[3];
	Если Не ЗаписиКОбработке.Пустой() Тогда
		Выборка = ЗаписиКОбработке.Выбрать();
		КоличествоДанных = КоличествоДанных + Выборка.Количество();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = ЗаписиКОбработке;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("УчетнаяЗаписьМаркетплейса", "УчетнаяЗаписьМаркетплейса");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;

		НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();

			Пока Выборка.Следующий() Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
			КонецЦикла;
			Для Каждого УзелПланаОбмена Из УзлыПланаОбмена Цикл
				НаборЗаписей.ОбменДанными.Получатели.Добавить(УзелПланаОбмена);
			КонецЦикла;
			НаборЗаписей.Записать(РежимЗамещения.Слияние);

			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();

			ЕстьОшибки = Истина;
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
	
	// Номенклатуру удалять нельзя, пока не сопоставлен хотя бы один склад
	НоменклатураОбработана = Не РезультатЗапроса[5].Пустой();
	
	ЗаписиКУдалению = РезультатЗапроса[4];
	Если Не ЕстьОшибки И Не ЗаписиКУдалению.Пустой() И НоменклатураОбработана Тогда
		Выборка = ЗаписиКУдалению.Выбрать();
		КоличествоДанных = КоличествоДанных + Выборка.Количество();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
			Для Каждого Измерение Из Метаданные.РегистрыСведений.СоответствияОбъектовМаркетплейсов.Измерения Цикл
				НаборЗаписей.Отбор[Измерение.Имя].Установить(Выборка[Измерение.Имя]);
			КонецЦикла;
			
			Для Каждого УзелОбмена Из УзлыПланаОбмена Цикл
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, НаборЗаписей);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	// Разнесем новые склады по номенклатурам
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ИзмененныеСклады.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	ИзмененныеСклады.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
	|	ИзмененныеСклады.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
	|	ИзмененныеСклады.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
	|	ИзмененныеСклады.Объект1С КАК Объект1С,
	|	ИзмененныеСклады.Объект1С КАК Склад,
	|	ВЫБОР
	|		КОГДА СоответствияСкладов.УчетнаяЗаписьМаркетплейса ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Удалено
	|ПОМЕСТИТЬ врСклады
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов.Изменения КАК ИзмененныеСклады
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияСкладов
	|		ПО (СоответствияСкладов.УчетнаяЗаписьМаркетплейса = ИзмененныеСклады.УчетнаяЗаписьМаркетплейса)
	|			И (СоответствияСкладов.ВидОбъектаМаркетплейса = ИзмененныеСклады.ВидОбъектаМаркетплейса)
	|			И (СоответствияСкладов.ИдентификаторОбъектаМаркетплейса = ИзмененныеСклады.ИдентификаторОбъектаМаркетплейса)
	|			И (СоответствияСкладов.ИдентификаторВладельцаОбъектаМаркетплейса = ИзмененныеСклады.ИдентификаторВладельцаОбъектаМаркетплейса)
	|			И (СоответствияСкладов.Объект1С = ИзмененныеСклады.Объект1С)
	|ГДЕ
	|	ИзмененныеСклады.Узел В(&УзлыПланаОбмена)
	|	И ИзмененныеСклады.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И ИзмененныеСклады.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Склад)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НоменклатураКонтрагентов.Номенклатура КАК Номенклатура,
	|	НоменклатураКонтрагентов.Характеристика КАК Характеристика
	|ПОМЕСТИТЬ врНоменклатура
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СопоставленнаяНК
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = СопоставленнаяНК.Объект1С)
	|		И НоменклатураКонтрагентов.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|ГДЕ
	|	СопоставленнаяНК.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СопоставленнаяНК.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врНоменклатура.Номенклатура КАК Номенклатура,
	|	врНоменклатура.Характеристика КАК Характеристика,
	|	врСклады.Склад КАК Склад
	|ПОМЕСТИТЬ врДанные
	|ИЗ
	|	врНоменклатура КАК врНоменклатура
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врСклады КАК врСклады
	|		ПО (врСклады.Удалено = ЛОЖЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врДанные.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врДанные.Номенклатура КАК Номенклатура,
	|	врДанные.Характеристика КАК Характеристика,
	|	врДанные.Склад КАК Склад,
	|	ЕСТЬNULL(ОстаткиТоваров.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ОстаткиТоваров.ДатаВыгрузкиОстатков, ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)) КАК ДатаВыгрузкиОстатков
	|ИЗ
	|	врДанные КАК врДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваров
	|		ПО врДанные.УчетнаяЗаписьМаркетплейса = ОстаткиТоваров.УчетнаяЗаписьМаркетплейса
	|			И врДанные.Номенклатура = ОстаткиТоваров.Номенклатура
	|			И врДанные.Характеристика = ОстаткиТоваров.Характеристика
	|			И врДанные.Склад = ОстаткиТоваров.Склад
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врСклады.УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	врСклады.ВидОбъектаМаркетплейса КАК ВидОбъектаМаркетплейса,
	|	врСклады.ИдентификаторОбъектаМаркетплейса КАК ИдентификаторОбъектаМаркетплейса,
	|	врСклады.ИдентификаторВладельцаОбъектаМаркетплейса КАК ИдентификаторВладельцаОбъектаМаркетплейса,
	|	врСклады.Объект1С КАК Объект1С
	|ИЗ
	|	врСклады КАК врСклады
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(врНоменклатура.Номенклатура) КАК КоличествоЗаписей
	|ИЗ
	|	врНоменклатура КАК врНоменклатура
	|ГДЕ
	|	врНоменклатура.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|ИМЕЮЩИЕ
	|	КОЛИЧЕСТВО(врНоменклатура.Номенклатура) > 0";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	Запрос.УстановитьПараметр("УзлыПланаОбмена", УзлыПланаОбмена);
	
	ЕстьОшибки = Ложь;
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	УстановитьПривилегированныйРежим(Ложь);

	ЗаписиКОбработке = РезультатЗапроса[3];
	Если Не ЗаписиКОбработке.Пустой() Тогда
		Выборка = ЗаписиКОбработке.Выбрать();
		КоличествоДанных = КоличествоДанных + Выборка.Количество();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
		ЭлементБлокировки.ИсточникДанных = ЗаписиКОбработке;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("УчетнаяЗаписьМаркетплейса", "УчетнаяЗаписьМаркетплейса");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");

		НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);
		
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			Пока Выборка.Следующий() Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
			КонецЦикла;
			Для Каждого УзелПланаОбмена Из УзлыПланаОбмена Цикл
				НаборЗаписей.ОбменДанными.Получатели.Добавить(УзелПланаОбмена);
			КонецЦикла;
			НаборЗаписей.Записать(РежимЗамещения.Слияние);

			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();

			ЕстьОшибки = Истина;
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса),
				УровеньЖурналаРегистрации.Ошибка,,,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЕсли;
	
	// Склад удалять нельзя, пока не сопоставлена хотя бы одна номенклатура маркетплейса
	СкладыОбработаны = Не РезультатЗапроса[5].Пустой();
	
	ЗаписиКУдалению = РезультатЗапроса[4];
	Если Не ЕстьОшибки И Не ЗаписиКУдалению.Пустой() И СкладыОбработаны Тогда
		Выборка = ЗаписиКУдалению.Выбрать();
		КоличествоДанных = КоличествоДанных + Выборка.Количество();
		
		Пока Выборка.Следующий() Цикл
			НаборЗаписей = РегистрыСведений.СоответствияОбъектовМаркетплейсов.СоздатьНаборЗаписей();
			Для Каждого Измерение Из Метаданные.РегистрыСведений.СоответствияОбъектовМаркетплейсов.Измерения Цикл
				НаборЗаписей.Отбор[Измерение.Имя].Установить(Выборка[Измерение.Имя]);
			КонецЦикла;
			
			Для Каждого УзелОбмена Из УзлыПланаОбмена Цикл
				ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, НаборЗаписей);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	// Рассчитаем остатки
	ЧастиТекстаЗапроса = Новый Массив;	// Массив Из Строка
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Данные.Номенклатура КАК Номенклатура,
	|	Данные.Характеристика КАК Характеристика,
	|	Данные.Склад КАК Склад,
	|	ЗНАЧЕНИЕ(Справочник.Назначения.ПустаяСсылка) КАК Назначение,
	|	Данные.Количество КАК Количество
	|ПОМЕСТИТЬ ВТДанныеДляРасчета
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК Данные
	|ГДЕ
	|	Данные.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И Данные.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И Данные.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Склад,
	|	Назначение";
	
	ЧастиТекстаЗапроса.Добавить(ТекстЗапроса);
	
	НастройкиТекстаЗапроса = Новый Структура("ИсточникТоваров", "ВТДанныеДляРасчета");
	ЧастиТекстаЗапроса.Добавить(ТекстЗапросаВременнойТаблицыЗапасы(НастройкиТекстаЗапроса));
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТДанныеДляРасчета.Склад КАК Склад,
	|	ВТДанныеДляРасчета.Номенклатура КАК Номенклатура,
	|	ВТДанныеДляРасчета.Характеристика КАК Характеристика,
	|	ЕСТЬNULL(ВТСвободныеОстатки.Свободно, 0) КАК Количество
	|ИЗ
	|	ВТДанныеДляРасчета КАК ВТДанныеДляРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗапасы КАК ВТСвободныеОстатки
	|		ПО ВТСвободныеОстатки.Номенклатура = ВТДанныеДляРасчета.Номенклатура
	|		И ВТСвободныеОстатки.Характеристика = ВТДанныеДляРасчета.Характеристика
	|		И ВТСвободныеОстатки.Склад = ВТДанныеДляРасчета.Склад
	|ГДЕ
	|	ЕСТЬNULL(ВТСвободныеОстатки.Свободно, 0) <> ВТДанныеДляРасчета.Количество";
	
	ЧастиТекстаЗапроса.Добавить(ТекстЗапроса);
	
	Запрос = Новый Запрос;
	Запрос.Текст = СтрСоединить(ЧастиТекстаЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапроса = Запрос.Выполнить();

	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		КоличествоДанных = КоличествоДанных + Выборка.Количество();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ОстаткиТоваровМаркетплейсов");
		ЭлементБлокировки.УстановитьЗначение("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
		ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Склад", "Склад");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Номенклатура", "Номенклатура");
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Характеристика", "Характеристика");
		
		НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(УчетнаяЗапись);
			
		НачатьТранзакцию();
		Попытка
			Блокировка.Заблокировать();
			
			Пока Выборка.Следующий() Цикл
				Запись = НаборЗаписей.Добавить();
				ЗаполнитьЗначенияСвойств(Запись, Выборка);
				Запись.УчетнаяЗаписьМаркетплейса = УчетнаяЗапись;
				Запись.ДатаВыгрузкиОстатков = ДатаВыгрузкиОстатков;
			КонецЦикла;
			
			Для Каждого УзелПланаОбмена Из УзлыПланаОбмена Цикл
				НаборЗаписей.ОбменДанными.Получатели.Добавить(УзелПланаОбмена);
			КонецЦикла;
			НаборЗаписей.Записать(РежимЗамещения.Слияние);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса), 
				УровеньЖурналаРегистрации.Ошибка, , ,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КоличествоДанных / 1000);
	
КонецПроцедуры

// Выгрузка остатков товаров для маркетплейсов
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись сервиса интеграции
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция СинхронизацияССервисомИнтеграции_ВыгрузкаОстатковТоваров(Знач УчетнаяЗапись, ВидМаркетплейса) Экспорт
	
	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	РезультатЗапроса.КодСостояния = 200;
	
	УзлыПланаОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиУзлы(УчетнаяЗапись);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 5000
	|	ДанныеУзла.Номенклатура КАК Номенклатура,
	|	ДанныеУзла.Характеристика КАК Характеристика,
	|	ДанныеУзла.Склад КАК Склад,
	|	ДанныеУзла.Узел КАК Узел
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов.Изменения КАК ДанныеУзла
	|ГДЕ
	|	ДанныеУзла.Узел В(&УзлыПланаОбмена)
	|ИТОГИ ПО
	|	Узел";
	
	Запрос.УстановитьПараметр("УзлыПланаОбмена", УзлыПланаОбмена);
	
	ПараметрыМетода = НовыйПараметрыЗапросаВыгрузитьОстаткиТоваров();
	ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров();
	ПараметрыМетода.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыМетода.ВидМаркетплейса = ВидМаркетплейса;
	
	Пока Истина Цикл
		
		УстановитьПривилегированныйРежим(Истина);
		Результат = Запрос.Выполнить();
		УстановитьПривилегированныйРежим(Ложь);
		
		Если Результат.Пустой() Тогда
			Возврат РезультатЗапроса;
		КонецЕсли;
		
		ВыборкаУзел = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаУзел.Следующий() Цикл
			
			ПараметрыМетода.Товары.Очистить();
			ОбрабатываемыеЗаписи = Новый Массив; // Массив из Структура
			
			Выборка = ВыборкаУзел.Выбрать();
			Пока Выборка.Следующий() Цикл
				
				ОписаниеТовара = Новый Структура;
				ОписаниеТовара.Вставить("Номенклатура", Выборка.Номенклатура);
				ОписаниеТовара.Вставить("Характеристика", Выборка.Характеристика);
				ОписаниеТовара.Вставить("Склад", Выборка.Склад);
				
				ПараметрыМетода.Товары.Добавить(ОписаниеТовара);
				
				ОписаниеЗаписи = Новый Структура;
				ОписаниеЗаписи.Вставить("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
				ОписаниеЗаписи.Вставить("Склад", Выборка.Склад);
				ОписаниеЗаписи.Вставить("Номенклатура", Выборка.Номенклатура);
				ОписаниеЗаписи.Вставить("Характеристика", Выборка.Характеристика);
				
				ОбрабатываемыеЗаписи.Добавить(ОписаниеЗаписи);
				
			КонецЦикла;
			
			//@skip-check query-in-loop - Порционная выгрузка
			РезультатЗапроса = ВыгрузитьОстаткиТоваров(ПараметрыМетода);
			
			Если РезультатЗапроса.КодСостояния <> 200 Или РезультатЗапроса.Ошибки.Количество() > 0 Тогда
				Возврат РезультатЗапроса;
			КонецЕсли;
			
			Если ОбрабатываемыеЗаписи.Количество() = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Очистка регистрации на плане обмена
			Попытка
				Для Каждого ОписаниеЗаписи Из ОбрабатываемыеЗаписи Цикл
					НаборЗаписей = РегистрыСведений.ОстаткиТоваровМаркетплейсов.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.УчетнаяЗаписьМаркетплейса.Установить(ОписаниеЗаписи.УчетнаяЗаписьМаркетплейса);
					НаборЗаписей.Отбор.Склад.Установить(ОписаниеЗаписи.Склад);
					НаборЗаписей.Отбор.Номенклатура.Установить(ОписаниеЗаписи.Номенклатура);
					НаборЗаписей.Отбор.Характеристика.Установить(ОписаниеЗаписи.Характеристика);
					//@skip-check invocation-parameter-type-intersect
					ПланыОбмена.УдалитьРегистрациюИзменений(ВыборкаУзел.Узел, НаборЗаписей);
				КонецЦикла;
			Исключение
				ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЗаписьЖурналаРегистрации(
					СобытиеЖурналаРегистрации(ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Выгрузить цены товаров регламентным заданием.
// 
// Параметры:
//  УчетнаяЗапись - Неопределено, СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ДополнительныеПараметры - Структура - Дополнительные параметры метода, обязательно содержит:
//   * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
Процедура ВыгрузитьЦеныТоваровРегламентнымЗаданием(УчетнаяЗапись, ДополнительныеПараметры) Экспорт
	
	ВидМаркетплейса = ДополнительныеПараметры.ВидМаркетплейса;
	
	Если ИнтеграцияСМаркетплейсомОтключена(ВидМаркетплейса) Тогда
		ВызватьИсключение НСтр("ru = 'Регламентное задание недоступно по функциональным опциям.';
								|en = 'The scheduled job is not available by functional options.'");
	КонецЕсли;
	
	СинхронизацияССервисомИнтеграции_ВыгрузкаЦен(УчетнаяЗапись, ВидМаркетплейса);
	
КонецПроцедуры

Процедура СинхронизацияССервисомИнтеграции_ВыгрузкаЦен(УчетнаяЗапись, ВидМаркетплейса) Экспорт
	
	Если Не ОбновитьНоменклатуруВСервисеИнтеграции(УчетнаяЗапись, ВидМаркетплейса) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыМетода = НовыйПараметрыЗапросаВыгрузитьЦены();
	ПараметрыМетода.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыМетода.УчетнаяЗапись = УчетнаяЗапись;
	ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены();
	
	ЗапросВыгрузитьЦены(ПараметрыМетода);
	
КонецПроцедуры

// Определить состояние регламентного задания.
// 
// Параметры:
//  МетаОбъект - ОбъектМетаданныхРегламентноеЗадание
//  Ключ - Строка
// 
// Возвращаемое значение:
//  Булево - Определить состояние регламентного задания
Функция ОпределитьСостояниеРегламентногоЗадания(Знач МетаОбъект, Знач Ключ) Экспорт

	Результат = Ложь;

	Если ПустаяСтрока(Ключ) Тогда
		Возврат Результат;
	КонецЕсли;

	УстановитьПривилегированныйРежим(Истина);
	Отбор = Новый Структура("Метаданные, Ключ", МетаОбъект, Ключ);
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	УстановитьПривилегированныйРежим(Ложь);

	Если Задания.Количество() = 0 Тогда
		Возврат Результат;
	КонецЕсли;

	Для Каждого Задание Из Задания Цикл
		Результат = Результат Или Задание.Использование;
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Включает / Отключает обязательные регламентные задания. При включении создает отсутствующее задание.
// 
// Параметры:
//  ОписаниеУчетнойЗаписи - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйКарточкаУчетнойЗаписи
//  Префикс - Строка, Неопределено - Часть ключа, служит для определения имя конечной процедуры, выполняемой при старте регламентного задания
//  Использование - Булево - Использование
//  Создавать - Булево - Создать регламентное задание, если такое не найдено и Использование = Истина
Процедура ИзменитьИспользованиеРегламентногоЗадания(ОписаниеУчетнойЗаписи, Префикс, Использование, Создавать = Истина) Экспорт
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Метаданные", Неопределено);
	ПараметрыЗадания.Вставить("Ключ", "");
	ПараметрыЗадания.Вставить("Использование", Использование);
	ПараметрыЗадания.Вставить("Расписание", Неопределено);
	ПараметрыЗадания.Вставить("Параметры", Новый Массив);
	
	ПараметрыМетода = ПараметрыЗадания.Параметры; // Массив из Произвольный
	ВидМаркетплейса = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОписаниеУчетнойЗаписи.УчетнаяЗапись, "ВидМаркетплейса");
	
	//@skip-check typed-value-adding-to-untyped-collection
	Если Префикс = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияСинхронизацияССервисомИнтеграции(ВидМаркетплейса) Тогда
		
		ПараметрыЗадания.Метаданные = Метаданные.РегламентныеЗадания.СинхронизацияССервисомИнтеграции;
		ПараметрыЗадания.Ключ = Префикс;
		Наименование = ИнтеграцияСМаркетплейсамиКлиентСервер.СформироватьНаименованиеРегламентногоЗадания(
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеЗаданияСинхронизацияССервисомИнтеграции(ВидМаркетплейса),
			ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
		Расписание = Новый РасписаниеРегламентногоЗадания();
		Расписание.ДатаНачала = ТекущаяДатаСеанса();
		Расписание.ВремяНачала = '00010101000001';
		Расписание.ВремяКонца = '00010101060001';
		Расписание.ПериодПовтораДней = 1;
		
		ПараметрыМетода.Добавить(ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
	ИначеЕсли Префикс = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияРасчетОстатковТоваров(ВидМаркетплейса) Тогда
		
		ПараметрыЗадания.Метаданные = Метаданные.РегламентныеЗадания.ВыгрузкаОстатковНаМаркетплейсы;
		ПараметрыЗадания.Ключ = Префикс;
		Наименование = ИнтеграцияСМаркетплейсамиКлиентСервер.СформироватьНаименованиеРегламентногоЗадания(
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеЗаданияРасчетОстатковТоваров(ВидМаркетплейса),
			ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
		Расписание = Новый РасписаниеРегламентногоЗадания();
		Расписание.ДатаНачала = ТекущаяДатаСеанса();
		Расписание.ПериодПовтораДней = 1;
		Расписание.ПериодПовтораВТечениеДня = 900;
		
		ПараметрыМетода.Добавить(ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
	ИначеЕсли Префикс = ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПрефиксЗаданияВыгрузкаЦенНаМаркетплейсы(ВидМаркетплейса) Тогда
		
		ПараметрыЗадания.Метаданные = Метаданные.РегламентныеЗадания.ВыгрузкаЦенНаМаркетплейсы;
		ПараметрыЗадания.Ключ = Префикс;
		Наименование = ИнтеграцияСМаркетплейсамиКлиентСервер.СформироватьНаименованиеРегламентногоЗадания(
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПредставлениеЗаданияВыгрузкаЦенНаМаркетплейсы(ВидМаркетплейса),
			ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
		Расписание = Новый РасписаниеРегламентногоЗадания();
		Расписание.ДатаНачала = ТекущаяДатаСеанса();
		Расписание.ПериодПовтораДней = 1;
		Расписание.ПериодПовтораВТечениеДня = 300;
		
		ПараметрыМетода.Добавить(ОписаниеУчетнойЗаписи.УчетнаяЗапись);
		
	КонецЕсли;
	
	ПараметрыЗадания.Ключ = ПараметрыЗадания.Ключ + ОписаниеУчетнойЗаписи.УчетнаяЗаписьИдентификатор;
	ПараметрыЗадания.Расписание = Расписание;
	
	ИнтеграцияСМаркетплейсамиСервер.СоздатьИзменитьРегламентноеЗадание(ПараметрыЗадания, Наименование, Создавать);
	
КонецПроцедуры

#КонецОбласти

#Область Другое

#Область КонструкторыДанных

// Новые данные интеграции.
// 
// Возвращаемое значение:
//  Структура - Данные интеграции:
// * id_token - Строка - Токен доступа
// * client_id - Строка - Публичная часть ключа доступа
// * client_secret - Строка - Секретная часть ключа доступа
// * Логин - Строка - Логин пользователя ИТС
// * КлючПодключения - Строка - Ключ подключения ИБ
// * ИдентификаторИТС - Строка - Идентификатор пользователя ИТС
// * ДатаПодключенияСИ - Дата - Дата подключения к сервису интеграции
// * ДатаОбновленияТокена - Дата - Дата обновления токена доступа
// * ДоступныеАдреса - Массив из См. НовыеПараметрыДоступногоАдреса - Доступные адреса
Функция НовыеДанныеИнтеграции() Экспорт
	
	ДанныеИнтеграции = Новый Структура;
	
	ДанныеИнтеграции.Вставить("id_token", "");
	ДанныеИнтеграции.Вставить("client_id", "");
	ДанныеИнтеграции.Вставить("client_secret", "");
	ДанныеИнтеграции.Вставить("Логин", "");
	ДанныеИнтеграции.Вставить("КлючПодключения", "");
	ДанныеИнтеграции.Вставить("ИдентификаторИТС", "");
	ДанныеИнтеграции.Вставить("ДатаПодключенияСИ", Дата(1, 1, 1));
	ДанныеИнтеграции.Вставить("ДатаОбновленияТокена", Дата(1, 1, 1));
	ДанныеИнтеграции.Вставить("ДоступныеАдреса", Новый Массив);
	
	Возврат ДанныеИнтеграции;
	
КонецФункции

// Новые параметры доступного адреса.
// 
// Возвращаемое значение:
//  Структура - Новые параметры доступного адреса:
// * Адрес - Строка - 
// * ДатаОтказа - Дата - 
// * Текущий - Булево - 
Функция НовыеПараметрыДоступногоАдреса() Экспорт
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("Адрес", "");
	Параметры.Вставить("ДатаОтказа", Дата(1,1,1));
	Параметры.Вставить("Текущий", Ложь);
	
	Возврат Параметры;
	
КонецФункции

// Новый результат метода сервиса.
// 
// Возвращаемое значение:
//  Структура - Новый результат метода сервиса:
// * Ошибки - Массив из Строка - Тексты ошибок
// * КодСостояния - Число - Код ответа HTTP
// * ВыводитьСообщения - Булево - Признак вывода пользователю сообщений об ошибках
// * Ответ - Неопределено, Соответствие из Произвольный, Структура из Произвольный - Ответ HTTP запроса. Свойства:
// ** Ключ - Строка - Имя поля
// ** Значение - Произвольный - Значение поля
// * Заголовки - Неопределено, Соответствие из Строка - Заголовки ответа.
Функция НовыйРезультатМетодаСервиса() Экспорт
	
	НовыйРезультат = Новый Структура;
	
	НовыйРезультат.Вставить("Ошибки", Новый Массив);
	НовыйРезультат.Вставить("КодСостояния", 0);
	НовыйРезультат.Вставить("ВыводитьСообщения", Истина);
	НовыйРезультат.Вставить("Ответ", Неопределено);
	НовыйРезультат.Вставить("Заголовки", Неопределено);
	
	Возврат НовыйРезультат;
	
КонецФункции

// Конструктор таблицы для типов цен.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новый типы цен:
//     * Идентификатор          - Строка - строковое обозначение типа цен.
//     * ИдентификаторДляФормул - Строка - шаблон для формирования идентификатора вида цен.
//     * Наименование           - Строка - наименование типа цен.
//     * ДляУчетнойЗаписи       - Булево - признак отношения типа цен к учетной записи или к интеграции в целом.
//     * ОбязательноеЗаполнение - Булево - признак обязательного заполнения в формах.
//     * Значение               - СправочникСсылка.ВидыЦен - пустая ссылка вида цен.
//     * ИмяПараметраСИ         - Строка - имя параметра Вид цены в запросе к сервису интеграции
Функция НовыйТаблицаТипыЦен() Экспорт
	
	ОписаниеТиповСтрока50 = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50, ДопустимаяДлина.Переменная));
	ТипыЦен = Новый ТаблицаЗначений;
	ТипыЦен.Колонки.Добавить("Идентификатор", ОписаниеТиповСтрока50);
	ТипыЦен.Колонки.Добавить("ИдентификаторДляФормул", ОписаниеТиповСтрока50);
	ТипыЦен.Колонки.Добавить("Наименование", ОписаниеТиповСтрока50);
	ТипыЦен.Колонки.Добавить("ДляУчетнойЗаписи", Новый ОписаниеТипов("Булево"), НСтр("ru = 'Тип цен уникален для учетной записи';
																					|en = 'Price type is unique for the account'"));
	ТипыЦен.Колонки.Добавить("ОбязательноеЗаполнение", Новый ОписаниеТипов("Булево"));
	ТипыЦен.Колонки.Добавить("Значение", Новый ОписаниеТипов("СправочникСсылка.ВидыЦен"));
	ТипыЦен.Колонки.Добавить("ИмяПараметраСИ", ОписаниеТиповСтрока50);
	
	Возврат ТипыЦен;
	
КонецФункции

#КонецОбласти

// Идентификатор информационной базы.
// 
// Возвращаемое значение:
//  Строка - Идентификатор информационной базы
Функция ИдентификаторИнформационнойБазы() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	ИдентификаторИнформационнойБазы = Константы.ИдентификаторИнформационнойБазы.Получить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ИдентификаторИнформационнойБазы;
	
КонецФункции

// Данные аутентификации ИТС.
// 
// Возвращаемое значение:
//  См. ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки
Функция ДанныеАутентификацииИТС() Экспорт
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		УстановитьПривилегированныйРежим(Истина);
		ДанныеАутентификацииИТС = МодульИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
								|en = 'Online support service is disabled.'");
	КонецЕсли;
	
	Возврат ДанныеАутентификацииИТС;
	
КонецФункции

// Логин пользователя ИТС.
// 
// Возвращаемое значение:
//  Строка - Логин пользователя ИТС
Функция ЛогинПользователяИТС() Экспорт
	
	Если ОбщегоНазначения.РазделениеВключено() Тогда
		Если Не ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных() Тогда
			ВызватьИсключение(НСтр("ru = 'Операция в неразделенном сеансе не поддерживается.';
									|en = 'The operation is not supported in a shared session.'"));
		КонецЕсли;
		Возврат ИнтеграцияСМаркетплейсамиСИКлиентСервер.ПользовательМоделиСервиса();
	КонецЕсли;
	
	ДанныеАутентификацииИТС = ДанныеАутентификацииИТС();
	
	Если ТипЗнч(ДанныеАутентификацииИТС) = Тип("Структура") Тогда
		Возврат ДанныеАутентификацииИТС.Логин;
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Возвращает имя события журнала регистрации работы с торговой площадкой.
// 
// Параметры:
//  ВидМаркетплейса - Неопределено, ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
// 
// Возвращаемое значение:
//  Строка - Наименование события для записей в журнале регистрации.
Функция СобытиеЖурналаРегистрации(ВидМаркетплейса = Неопределено) Экспорт
	
	Если ЗначениеЗаполнено(ВидМаркетплейса) Тогда
		Возврат СтрШаблон(НСтр("ru = 'Интеграция с %1';
								|en = 'Integration with %1'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркетплейсПоВиду(ВидМаркетплейса));
	Иначе
		Возврат НСтр("ru = 'Интеграция с маркетплейсами';
					|en = 'Integration with marketplaces'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
КонецФункции

Процедура ДобавитьПараметрАдресаHTML(Адрес, Имя, Знач Значение) Экспорт
	
	Если Не ЗначениеЗаполнено(Значение) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		//@skip-check invocation-parameter-type-intersect
		Значение = Формат(Значение, "ЧГ=");
	КонецЕсли;
	
	Адрес = Адрес + ?(СтрНайти(Адрес, "?") = 0, "?", "&") + Имя + "=" + Значение;
	
КонецПроцедуры

// Данные ВJSON.
// 
// Параметры:
//  Данные - Произвольный, ДвоичныеДанные, Строка - Данные
// 
// Возвращаемое значение:
//  Строка - Данные ВJSON
Функция ДанныеВJSON(Данные) Экспорт
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.ПроверятьСтруктуру = Ложь;
	ПараметрыЗаписиJSON = Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Авто, , , ЭкранированиеСимволовJSON.СимволыВнеASCII);
	ЗаписьJSON.УстановитьСтроку(ПараметрыЗаписиJSON);
	ЗаписатьJSON(ЗаписьJSON, Данные, , "ПреобразованиеТиповЗначений", ИнтеграцияСМаркетплейсамиСИ);
	
	Возврат ЗаписьJSON.Закрыть();
	
КонецФункции

// Данные из JSON.
// 
// Параметры:
//  Данные - Строка, Неопределено - Данные
//  ЕстьОшибка - Неопределено, Булево - Есть ошибка
//  ТекстОшибки - Строка - Текст ошибки
//  ПрочитатьВСоответствие - Булево - Прочитать в соответствие
// 
// Возвращаемое значение:
//  Строка, Неопределено, Произвольный - Данные из JSON
Функция ДанныеИзJSON(Данные, ЕстьОшибка = Ложь, ТекстОшибки = "", ПрочитатьВСоответствие = Истина) Экспорт
	
	// Проверка типа JSON
	Если ТипЗнч(Данные) <> Тип("Строка") Тогда
		Возврат Данные;
	КонецЕсли;
	
	ПервыйСимвол = Лев(Данные, 1);
	Если ПервыйСимвол <> "[" И ПервыйСимвол <> "{" Тогда
		Возврат Данные;
	КонецЕсли;
	
	Результат = Неопределено;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Попытка
		ЧтениеJSON.УстановитьСтроку(Данные);
		Результат = ПрочитатьJSON(ЧтениеJSON, ПрочитатьВСоответствие);
		ЧтениеJSON.Закрыть();
	Исключение
		ЕстьОшибка = Истина;
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибки);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Преобразование типов значений, не поддерживаемых JSON
// 
// Параметры:
//  Свойство - Строка - Свойство
//  Значение - Произвольный - Значение свойства
//  ДополнительныеПараметры - Произвольный - Дополнительные параметры
//  Отказ - Булево - Отказ
// 
// Возвращаемое значение:
//  Строка - Преобразованное значение
Функция ПреобразованиеТиповЗначений(Знач Свойство, Значение, ДополнительныеПараметры, Отказ) Экспорт
	
	Если ТипЗнч(Значение) = Тип("Неопределено") Тогда
		
		Возврат "";
		
	ИначеЕсли ТипЗнч(Значение) = Тип("УникальныйИдентификатор") Тогда
		
		Возврат Строка(Значение);
		
	ИначеЕсли ТипЗнч(Значение) = Тип("ХранилищеЗначения") Тогда
		
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, Значение, НазначениеТипаXML.Явное);
		
		Возврат ЗаписьJSON.Закрыть();
		
	КонецЕсли;
	
КонецФункции

// Расшифровать токен JWT.
// 
// Параметры:
//  Токен - Строка - Токен
// 
// Возвращаемое значение:
//  Структура - Расшифровать токен JWT:
// * ТекстОшибки - Строка - 
// * Данные - Неопределено - 
Функция РасшифроватьТокенJWT(Токен) Экспорт
	
	ВозвращаемоеЗначение = Новый Структура("ТекстОшибки, Данные", "", Неопределено);
	
	ЭлементыТокена = СтрРазделить(Токен, ".");
	Если ЭлементыТокена.Количество() <> 3 Тогда
		ВозвращаемоеЗначение.ТекстОшибки = НСтр("ru = 'Токен не соответствует формату JWT.';
												|en = 'The token does not match the JWT format.'");
		Возврат ВозвращаемоеЗначение;
	КонецЕсли;
	
	Значение = СтрЗаменить(СтрЗаменить(ЭлементыТокена[1], "-", "+"), "_", "/");
	Остаток = СтрДлина(Значение) % 4;
	
	Если Остаток = 1 Тогда
		Возврат ВозвращаемоеЗначение;
	ИначеЕсли Остаток = 2 Тогда
		Значение = Значение + "==";
	ИначеЕсли Остаток = 3 Тогда
		Значение = Значение + "=";
	КонецЕсли;
	
	ВозвращаемоеЗначение.Данные = ДанныеИзJSON(ПолучитьСтрокуИзДвоичныхДанных(Base64Значение(Значение)), ,
		ВозвращаемоеЗначение.ТекстОшибки);
	
	Если Не ПустаяСтрока(ВозвращаемоеЗначение.ТекстОшибки) Тогда
		ВозвращаемоеЗначение.ТекстОшибки = НСтр("ru = 'Токен не соответствует формату JWT.';
												|en = 'The token does not match the JWT format.'");
	КонецЕсли;
	
	Возврат ВозвращаемоеЗначение;
	
КонецФункции

// Прочитать данные из безопасного хранилища.
// 
// Параметры:
//  ВидМаркетплейса - Строка, ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
//  Ключ - Строка - Ключ
// 
// Возвращаемое значение:
//  См. НовыеДанныеИнтеграции
Функция ПрочитатьДанныеИзБезопасногоХранилища(ВидМаркетплейса = "", Ключ = "") Экспорт
	
	НовыеДанныеИнтеграции = НовыеДанныеИнтеграции();
	
	Владелец = XMLСтрока(ВидМаркетплейса);
	Владелец = ?(ПустаяСтрока(Владелец), ИмяСервиса(), Владелец);
	Владелец = СтрШаблон("%1.%2", Владелец, ?(ПустаяСтрока(Ключ), ЛогинПользователяИТС(), Ключ));
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеИнтеграции = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец);
	УстановитьПривилегированныйРежим(Ложь);
	
	Если ТипЗнч(ДанныеИнтеграции) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(НовыеДанныеИнтеграции, ДанныеИнтеграции);
	КонецЕсли;
	
	Если НовыеДанныеИнтеграции.ДоступныеАдреса.Количество() = 0 Тогда
		НовыеДанныеИнтеграции.ДоступныеАдреса = ДоступныеАдреса()
	КонецЕсли;
	
	Возврат НовыеДанныеИнтеграции;
	
КонецФункции

// Записать данные в безопасное хранилище.
// 
// Параметры:
//  ВидМаркетплейса - Строка, ПеречислениеСсылка.ВидыМаркетплейсов, Неопределено - Вид маркетплейса
//  Источник - Неопределено, ФормаКлиентскогоПриложения, Структура - Источник:
// * id_token - Строка - 
// * client_id - Строка - 
// * client_secret - Строка - 
// * ДатаОбновленияТокена - Дата - 
// * Логин - Строка - 
//  Ключ - Строка - Ключ
// 
// Возвращаемое значение:
//  См. НовыеДанныеИнтеграции
Функция ЗаписатьДанныеВБезопасноеХранилище(ВидМаркетплейса = "", Источник = Неопределено, Ключ = "") Экспорт
	
	ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища(ВидМаркетплейса, Ключ);
	
	Если ТипЗнч(Источник) = Тип("ФормаКлиентскогоПриложения") Или ЗначениеЗаполнено(Источник) Тогда
		ЗаполнитьЗначенияСвойств(ДанныеИнтеграции, Источник);
	КонецЕсли;
	
	Владелец = XMLСтрока(ВидМаркетплейса);
	Владелец = ?(ПустаяСтрока(Владелец), ИмяСервиса(), Владелец);
	Владелец = СтрШаблон("%1.%2", Владелец, ?(ПустаяСтрока(Ключ), ЛогинПользователяИТС(), Ключ));
	
	УстановитьПривилегированныйРежим(Истина);
	ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(Владелец, ДанныеИнтеграции);
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ДанныеИнтеграции;
	
КонецФункции

// Проверяет существование сообщения пользователю, чтобы не дублировать их вывод.
// 
// Параметры:
//  Текст - Строка - Текст
// 
// Возвращаемое значение:
//  Булево - Сообщение пользователю существует
Функция СообщениеПользователюСуществует(Текст) Экспорт
	
	Результат = Ложь;
	
	СообщенияПользователю = ПолучитьСообщенияПользователю();
	Для Каждого Сообщение Из СообщенияПользователю Цикл
		Если Сообщение.Текст = Текст Тогда
			Результат = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Текст запроса временной таблицы содержащей информацию о запасах товаров на складах
// 
// Параметры:
//  Настройки - Структура - данные для подстановки в текст запроса:
//   * ИсточникТоваров - Строка - Имя временной таблицы
// 
// Возвращаемое значение:
//  Строка - Текст запроса временной таблицы
Функция ТекстЗапросаВременнойТаблицыЗапасы(Настройки) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Запасы.Склад КАК Склад,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток < 0
	|			ТОГДА
	|				Запасы.ВНаличииОстаток - Запасы.РезервироватьНаСкладеОстаток
	|		ИНАЧЕ
	|			ВЫБОР
	|				КОГДА Запасы.ВНаличииОстаток
	|						- Запасы.РезервироватьНаСкладеОстаток
	|						- Запасы.РезервироватьПоМереПоступленияОстаток > 0
	|					ТОГДА
	|						Запасы.ВНаличииОстаток
	|							- Запасы.РезервироватьНаСкладеОстаток
	|							- Запасы.РезервироватьПоМереПоступленияОстаток
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК Свободно
	|ПОМЕСТИТЬ ВТЗапасы
	|ИЗ
	|	РегистрНакопления.ЗапасыИПотребности.Остатки(, (Номенклатура, Характеристика, Склад, Назначение) В
	|		(ВЫБРАТЬ
	|			ИсточникТоваров.Номенклатура,
	|			ИсточникТоваров.Характеристика,
	|			ИсточникТоваров.Склад,
	|			ИсточникТоваров.Назначение
	|		ИЗ
	|			#ИсточникТоваров КАК ИсточникТоваров)) КАК Запасы";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#ИсточникТоваров", Настройки.ИсточникТоваров);
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Возвращает признак хранения данных маркетплейса в сервисе интеграции
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
// 
// Возвращаемое значение:
//  Булево - Истина, если данные маркетплейса хранятся в сервисе интеграции
Функция ЭтоМаркетплейсСИ(ВидМаркетплейса) Экспорт
	Возврат ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса)
		Или ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса);
КонецФункции

// Формирует строку с версией конфигурации для передачи в сервис интеграции.
// 
// Возвращаемое значение:
//  Строка - Версия конфигурации для передачи в сервис интеграции
Функция ВерсияКонфигурацииДляСИ() Экспорт
	Возврат СтрШаблон("%1 %2", Метаданные.Имя, Метаданные.Версия);
КонецФункции

// Удалить сопоставление для переданной номенклатуры контрагентов по владельцу из учетной записи.
// 
// Параметры:
//  СписокНоменклатуры - Массив из СправочникСсылка.НоменклатураКонтрагентов - 
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - 
// 
// Возвращаемое значение:
//  Строка - текст ошибки
Функция УдалитьСопоставлениеНоменклатурыКонтрагентов(СписокНоменклатуры, УчетнаяЗапись) Экспорт
	
	РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(УчетнаяЗапись, "Партнер, ВидМаркетплейса");
	ВладелецНоменклатуры = РеквизитыУчетнойЗаписи.Партнер;
	Если Не ЗначениеЗаполнено(ВладелецНоменклатуры) Тогда
		ТекстОшибки = НСтр("ru = 'Не заполнен владелец номенклатуры.';
							|en = 'The owner of items is blank.'");
		Возврат ТекстОшибки;
	КонецЕсли;
	
	ТекстОшибкиПользователю = "";
	Для Каждого НоменклатураКонтрагента Из СписокНоменклатуры Цикл
		
		НачатьТранзакцию();
		Попытка
			ОбъектНоменклатураКонтрагента = НоменклатураКонтрагента.ПолучитьОбъект();
			
			Если Не ЗначениеЗаполнено(ОбъектНоменклатураКонтрагента.Номенклатура) Тогда
				ОтменитьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ТекстСообщенияОшибки = СтрШаблон(НСтр("ru = 'Не удалось изменить номенклатуру %1 по владельцу %2';
													|en = 'Cannot change the %1 items by the %2 owner'",
				ОбщегоНазначения.КодОсновногоЯзыка()), ОбъектНоменклатураКонтрагента.Наименование, ВладелецНоменклатуры);
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить("Справочник.НоменклатураКонтрагентов");
			ЭлементБлокировки.УстановитьЗначение("ВладелецНоменклатуры", ВладелецНоменклатуры);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", НоменклатураКонтрагента);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
			
			ОбъектНоменклатураКонтрагента.Номенклатура = Неопределено;
			ОбъектНоменклатураКонтрагента.Характеристика = Неопределено;
			ОбъектНоменклатураКонтрагента.Упаковка = Неопределено;

			ОбъектНоменклатураКонтрагента.Записать();
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			КраткоеПредставлениеОшибки = ОбработкаОшибок.КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстОшибкиПодробный = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ТекстОшибкиИПричины = СтрШаблон(НСтр("ru = '%1 по причине:
							 					|%2';
							 					|en = '%1 due to:
							 					|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				ТекстСообщенияОшибки, КраткоеПредставлениеОшибки);
			ТекстОшибкиПользователю = 
				?(ПустаяСтрока(ТекстОшибкиПользователю), "", ТекстОшибкиПользователю + Символы.ПС) + ТекстОшибкиИПричины;
			
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(РеквизитыУчетнойЗаписи.ВидМаркетплейса), 
				УровеньЖурналаРегистрации.Ошибка, , , ТекстОшибкиПодробный);

		КонецПопытки;
		
	КонецЦикла;
	
	Возврат ТекстОшибкиПользователю;
	
КонецФункции

#КонецОбласти

#КонецОбласти

//@skip-check property-return-type
//@skip-check invocation-parameter-type-intersect
//@skip-check dynamic-access-method-not-found
//@skip-check statement-type-change
//@skip-check method-param-value-type
#Область СлужебныеПроцедурыИФункции

#Область ДлительныеОперацииСлужебныеМетоды

// Действия перед началом выполнения длительных операций.
// 
// Параметры:
//  ПараметрыОперации - См. ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций
Процедура ПередВыполнениемДлительныхОпераций(ПараметрыОперации)
	
	// В каждый метод очереди передадим значения глобальных реквизитов.
	// Значения глобальных реквизитов имеют больший приоритет, чем значения локальных реквизитов методов
	Для Каждого Операция Из ПараметрыОперации.Очередь Цикл
		Операция.Значение.Вставить("ВыводитьСообщения", ПараметрыОперации.ВыводитьСообщения);
		Операция.Значение.Вставить("ИмяМетода", Операция.Представление);
		Операция.Значение.Вставить("ВидМаркетплейса", ПараметрыОперации.ВидМаркетплейса);
	КонецЦикла;
	
КонецПроцедуры

// Выполнить очередную длительную операцию.
// 
// Параметры:
//  Операция - ЭлементСпискаЗначений - Данные операции
//  ОбщиеПараметры - Структура - Общие параметры для всех операций:
//   * Очередь - СписокЗначений из Структура - Очередь операций
//  ОчередьВыполненныхМетодов - СписокЗначений Из Структура -
// 
// Возвращаемое значение:
//  Произвольный - Результат выполнения операции
Функция ВыполнитьДлительнуюОперацию(Операция, ОбщиеПараметры, ОчередьВыполненныхМетодов)
	
	РезультатОперации = Неопределено;
	
	ИмяМетода = Операция.Представление;
	Параметры = Операция.Значение;
	Параметры.ИмяМетода = ИмяМетода; // Строка
	
	АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено);
	
	Если ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение() Тогда
		
		РезультатОперации = УстановитьСоединение(Параметры);
		
		Если РезультатОперации.КодСостояния = 200 Тогда
			ОбщиеПараметры.Очередь.Добавить(Параметры, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьПодключение());
			ОбщиеПараметры.Очередь.Добавить(Параметры, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения());
		КонецЕсли;
		
	ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьУчетнуюЗапись() Тогда
		
		РезультатОперации = УдалитьУчетнуюЗапись(Параметры);
		
		Если РезультатОперации.КодСостояния = 200 Тогда
			НовыеПараметры = НовыйПараметрыЗапросаПолучитьУчетныеЗаписи();
			ЗаполнитьЗначенияСвойств(НовыеПараметры, Параметры);
			ОбщиеПараметры.Очередь.Добавить(НовыеПараметры, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи());
		КонецЕсли;
	
	ИначеЕсли ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
		
		РезультатОперации = ПолучитьУчетныеЗаписи(Параметры);
		
		Если РезультатОперации.КодСостояния = 200 Тогда
			
			Если ОбщиеПараметры.КлючВыполнения = "Обработка.УправлениеПродажамиНаМаркетплейсах.Форма.НастройкиИнтеграции" Тогда
				
				Результат = РезультатОперации.Ответ;
				Если ЗначениеЗаполнено(Результат.УчетнаяЗапись) Или Результат.УчетныеЗаписи.Количество() = 1 Тогда
					
					Если Результат.УчетныеЗаписи.Количество() = 1 Тогда
						Параметры.УчетнаяЗапись = Результат.УчетныеЗаписи[0].УчетнаяЗапись;
					КонецЕсли;
					
					ПараметрыЗапроса = НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса();
					ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
					ОбщиеПараметры.Очередь.Добавить(ПараметрыЗапроса, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса());
					
					ПараметрыЗапроса = НовыйПараметрыЗапросаПолучитьОграниченияОстатковТоваров();
					ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, Параметры);
					ОбщиеПараметры.Очередь.Добавить(ПараметрыЗапроса, ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров());
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		МетодДоступенДляВыполнения = Истина;
		
		Если Не ПустаяСтрока(Операция.Значение.ЗависитОт) Тогда
			Для Каждого ЭлементОчереди Из ОчередьВыполненныхМетодов Цикл
				Если ЭлементОчереди.Представление = Операция.Значение.ЗависитОт Тогда
					
					Если ЭлементОчереди.Значение.КодСостояния = 200 Тогда
						Если СтрНайти(Параметры.ЗависимыеПоля, "УчетнаяЗапись") > 0 Тогда
							Если ЭлементОчереди.Значение.Ответ.УчетныеЗаписи.Количество() = 1 Тогда
								Параметры.УчетнаяЗапись = ЭлементОчереди.Значение.Ответ.УчетныеЗаписи[0].УчетнаяЗапись;
							Иначе
								МетодДоступенДляВыполнения = Ложь;
							КонецЕсли;
						КонецЕсли;
					Иначе
						МетодДоступенДляВыполнения = Ложь;
					КонецЕсли;
					
					Если Не МетодДоступенДляВыполнения Тогда
						РезультатОперации = НовыйРезультатМетодаСервиса();
						РезультатОперации.КодСостояния = 500;
					КонецЕсли;
					
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если МетодДоступенДляВыполнения Тогда
			ИмяМетода = СтрШаблон("ИнтеграцияСМаркетплейсамиСИ.%1", ИмяМетода);
			ПараметрыМетодаКонфигурации = Новый Массив; // Массив из Структура, Строка
			ПараметрыМетодаКонфигурации.Добавить(Параметры);
			ПараметрыМетодаКонфигурации.Добавить(АдресРезультата);
			ОбщегоНазначения.ВыполнитьМетодКонфигурации(ИмяМетода, ПараметрыМетодаКонфигурации);
			РезультатОперации = ПолучитьИзВременногоХранилища(АдресРезультата);
		КонецЕсли;
		
	КонецЕсли;
	
	УдалитьИзВременногоХранилища(АдресРезультата);
	
	Возврат РезультатОперации;
	
КонецФункции

// Действия после выполнения длительных операций.
// 
// Параметры:
//  Параметры - Структура - Общие параметры для всех операций
//  Результат - Структура, Неопределено - Результаты длительных операций
Процедура ПослеВыполненияДлительныхОпераций(Параметры, Результат)
	
	АдресРезультата = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АдресРезультата", ""); // Строка
	Если Не ПустаяСтрока(АдресРезультата) Тогда
		ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область РаботаССервисомСлужебныеМетоды

#Область ОбщиеМетодыДляРаботыССервисом

Процедура ДополнитьПараметрыЗапроса(ПараметрыЗапроса, ПараметрыМетода)
	
	Если ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьТокенДоступа() Тогда
		
		ПараметрыЗапроса.КорневойURL = "";
		ПараметрыЗапроса.АдресМетода = "sys/token/";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ТипКонтента = "application/x-www-form-urlencoded";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
		ДанныеАвторизации = СтрЗаменить(Base64Строка(ПолучитьДвоичныеДанныеИзСтроки(СтрШаблон("%1:%2",
			ПараметрыМетода.client_id, ПараметрыМетода.client_secret))), Символы.ВК + Символы.ПС, "");
		ПараметрыЗапроса.Заголовки.Вставить("Authorization", СтрШаблон("Basic %1", ДанныеАвторизации));
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПодключения() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьСоединение() Тогда
		
		ПараметрыЗапроса.КорневойURL = "api/direct/v1/%1";
		ПараметрыЗапроса.АдресМетода = "auth";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Таймаут = 60;
		
		ПараметрыЗапроса.Заголовки.Вставить("Ticket", ПараметрыМетода.Тикет);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУстановитьПодключение() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьПодключение() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/connect";
		ПараметрыЗапроса.Метод = "DELETE";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "keyConnect", ПараметрыМетода.КлючПодключения);
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьДанныеПользователяИТС() Тогда
		
		ПараметрыЗапроса.Сервер = "login.1c.ru";
		ПараметрыЗапроса.Приложение = "";
		ПараметрыЗапроса.КорневойURL = "rest/public";
		ПараметрыЗапроса.АдресМетода = "ticket/check";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.Авторизация = Ложь;
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.ОбращениеКСервисуИнтеграции = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьУчетныеЗаписи() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/auth";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ВыводитьСообщения = Ложь;
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодСоздатьОбновитьУчетнуюЗапись() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/auth";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьУчетнуюЗапись() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/auth";
		ПараметрыЗапроса.Метод = "DELETE";
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНастройкиУчетнойЗаписи() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/settings";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьСкладыМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/warehouses";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьСкладыМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/warehouses";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуСкладаМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/warehouses_info";
		ПараметрыЗапроса.Метод = "GET";
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьСклады() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/baseWarehouses";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьНоменклатуруМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/cards";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/cards";
		ПараметрыЗапроса.Метод = "PUT";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКарточкуНоменклатурыМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/cards_info";
		ПараметрыЗапроса.Метод = "GET";
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОграниченияОстатковТоваров() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/stockLimits";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОграниченияОстатковТоваров() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/stockLimits";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодУдалитьОграниченияОстатковТоваров() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/stockLimits";
		ПараметрыЗапроса.Метод = "DELETE";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьОстаткиТоваровМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/stockRemains";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьОстаткиТоваров() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/stockRemains";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПроверитьДоступностьСервисаИнтеграции() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/ping";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		ПараметрыЗапроса.Авторизация = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПроверитьДоступностьСервисаМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/ping_service";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;

	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодВыгрузитьЦены() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/goodsPrices";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/goodsList";
		ПараметрыЗапроса.Метод = "POST";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьБренды() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/brandList";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	ИначеЕсли ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьКатегории() Тогда
		
		ПараметрыЗапроса.АдресМетода = "v1/categoryList";
		ПараметрыЗапроса.Метод = "GET";
		ПараметрыЗапроса.ПрочитатьВСоответствие = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

// Установить соединение с сервером.
// 
// Параметры:
//  ПараметрыЗапроса - Структура - Параметры запроса:
// * Сервис - Строка - 
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - 
// * Сервер - Строка - 
// * Приложение - Строка - 
// * КорневойURL - Строка - 
// * Метод - Строка - 
// * АдресМетода - Строка - 
// * Заголовки - Соответствие из Строка - 
// * Авторизация - Булево - 
// * Данные - Строка, ДвоичныеДанные, Произвольный - 
// * ТипКонтента - Строка - 
// * Таймаут - Число - 
// * ИмяМетода - Строка - 
// * ИдентификаторИТС - Строка - 
// * УчетнаяЗапись - Строка - 
// * ВыводитьСообщения - Булево - 
// * ПрочитатьВСоответствие - Булево - 
// * ИдентификаторНазначения - Неопределено, УникальныйИдентификатор -
// * АктивноеСоединение - HTTPСоединение, Неопределено - ранее установленное соединение с сервером 
//  Прокси - Неопределено, ИнтернетПрокси - Прокси
//  РезультатЗапроса - См. НовыйРезультатМетодаСервиса
//  ПовторныйЗапрос - Булево - Признак ошибки обращения к основному серверу, либо признак последовательной передачи
//  	пакетов одного потока данных.
// 
// Возвращаемое значение:
//  Структура - HTTPСоединение, Неопределено - Установить соединение с сервером:
Функция УстановитьСоединениеССервером(ПараметрыЗапроса, Прокси, РезультатЗапроса, ПовторныйЗапрос = Ложь)
	
	Если Не ПовторныйЗапрос Тогда
		ПараметрыЗапроса.АктивноеСоединение = Неопределено;
	КонецЕсли;
	
	Если ПараметрыЗапроса.АктивноеСоединение = Неопределено Тогда
		Попытка
			ПараметрыЗапроса.АктивноеСоединение = Новый HTTPСоединение(ПараметрыЗапроса.Сервер, 443, , , Прокси, ПараметрыЗапроса.Таймаут,
				ОбщегоНазначенияКлиентСервер.НовоеЗащищенноеСоединение());
		Исключение
			ТекстОшибки = НСтр("ru = 'Отсутствует соединение с сервисом интеграции. Подробности см. в журнале регистрации.';
								|en = 'No connection to the integration service. For more information, see the event log.'");
			РезультатЗапроса.КодСостояния = 404;
			РезультатЗапроса.Ошибки.Добавить(ТекстОшибки);
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, , ,
					ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Возврат ПараметрыЗапроса.АктивноеСоединение;
	
КонецФункции

Процедура УстановитьПолныйАдресМетода(ПараметрыЗапроса, ПовторныйЗапрос, Ошибки)
	
	Если Не ПараметрыЗапроса.ОбращениеКСервисуИнтеграции Тогда
		ПараметрыЗапроса.ПолныйАдресМетода = СтрШаблон("/%1/%2/%3", ПараметрыЗапроса.Приложение,
			ПараметрыЗапроса.КорневойURL, ПараметрыЗапроса.АдресМетода);
		ПараметрыЗапроса.ПолныйАдресМетода = СтрЗаменить(ПараметрыЗапроса.ПолныйАдресМетода, "//", "/");
		Возврат;
	КонецЕсли;
	
	АдресЗапроса = "";
	
	Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
		Если ОписаниеДоступногоАдреса.Текущий Тогда
			АдресЗапроса = ОписаниеДоступногоАдреса.Адрес;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ПустаяСтрока(АдресЗапроса) Тогда
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если Не ЗначениеЗаполнено(ОписаниеДоступногоАдреса.ДатаОтказа)
				Или ТекущаяДатаСеанса() - ОписаниеДоступногоАдреса.ДатаОтказа > 60 * 60 * 12 Тогда
				АдресЗапроса = ОписаниеДоступногоАдреса.Адрес;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ПустаяСтрока(АдресЗапроса) Тогда
		АдресЗапроса = СтрЗаменить(АдресЗапроса, "https://", "");
		ИндексРазделителя = СтрНайти(АдресЗапроса, ".com/");
		ПараметрыЗапроса.Сервер = Сред(АдресЗапроса, 1, ИндексРазделителя + 3);
		ПараметрыЗапроса.Приложение = Сред(АдресЗапроса, ИндексРазделителя + 5);
	ИначеЕсли ПовторныйЗапрос Тогда
		Ошибки.Добавить(НСтр("ru = 'Нет доступных адресов для отправки запроса.';
							|en = 'No available addresses to send the request.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.ПолныйАдресМетода = СтрШаблон("/%1/%2/%3", ПараметрыЗапроса.Приложение, ПараметрыЗапроса.КорневойURL,
		ПараметрыЗапроса.АдресМетода);
	
	Если СтрНайти(ПараметрыЗапроса.ПолныйАдресМетода, "/%1") > 0 Тогда
		Если ПустаяСтрока(ПараметрыЗапроса.ИдентификаторИТС) Тогда
			ПараметрыЗапроса.ИдентификаторИТС = ПолучитьИдентификаторИТС(ПараметрыЗапроса.ДанныеИнтеграции);
			Если ПустаяСтрока(ПараметрыЗапроса.ИдентификаторИТС) Тогда
				Ошибки.Добавить(НСтр("ru = 'Ошибка валидации Интернет-поддержки пользователей.';
									|en = 'An error occurred when validating online user support.'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		ПараметрыЗапроса.ПолныйАдресМетода = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ПараметрыЗапроса.ПолныйАдресМетода, ПараметрыЗапроса.ИдентификаторИТС);
	КонецЕсли;
	
	ПараметрыЗапроса.ПолныйАдресМетода = СтрЗаменить(ПараметрыЗапроса.ПолныйАдресМетода, "//", "/");
	
КонецПроцедуры

// Вызвать HTTPМетод.
// 
// Параметры:
//  Соединение - HTTPСоединение - Соединение
//  ЗапросКСервису - HTTPЗапрос - Запрос к сервису
//  ПараметрыЗапроса - См. НовыеПараметрыЗапроса
// 
// Возвращаемое значение:
//  HTTPОтвет - Вызвать HTTPМетод
Функция ВызватьHTTPМетод(Соединение, ЗапросКСервису, ПараметрыЗапроса)
	
	Для ПопыткаПодключения = 1 По 3 Цикл
		
		Если ПопыткаПодключения = 2 Тогда
			ВызватьПаузу(5000);
		ИначеЕсли ПопыткаПодключения = 3 Тогда
			ВызватьПаузу(20000);
		КонецЕсли;
		
		Попытка
			Возврат Соединение.ВызватьHTTPМетод(ПараметрыЗапроса.Метод, ЗапросКСервису);
		Исключение
			Продолжить;
		КонецПопытки;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ОбработатьОтветСервиса(РезультатЗапроса, ПараметрыЗапроса, ПараметрыМетода, Кэш, ЗапросКСервису, Ответ, Данные)
	
	Если ТребуетсяПереадресация(ПараметрыЗапроса, Ответ) Тогда
		РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода, Кэш, Истина);
		Возврат;
	КонецЕсли;
	
	Ответ = ?(Ответ = Неопределено, HTTPОтветТаймаут(), Ответ); // Таймаут
	
	РезультатЗапроса.КодСостояния = Ответ.КодСостояния;
	РезультатЗапроса.ВыводитьСообщения = ПараметрыЗапроса.ВыводитьСообщения;
	
	ЕстьОшибка = Ложь;
	ТекстОшибки = "";
	ДанныеИзJSON = ДанныеИзJSON(Данные, ЕстьОшибка, ТекстОшибки, ПараметрыЗапроса.ПрочитатьВСоответствие);
	Если ЕстьОшибка И РезультатЗапроса.Ошибки.Найти(ТекстОшибки) = Неопределено Тогда
		РезультатЗапроса.Ошибки.Добавить(ТекстОшибки);
	КонецЕсли;
	
	Если РезультатЗапроса.КодСостояния <> 200 Тогда
		
		Информация = "";
		Если ТипЗнч(ДанныеИзJSON) = Тип("Соответствие") Тогда
			Информация = ДанныеИзJSON["Информация"]; // Строка
			ТехническаяИнформация = ДанныеИзJSON["ТехническаяИнформация"];
		ИначеЕсли ТипЗнч(ДанныеИзJSON) = Тип("Структура") Тогда
			Информация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИзJSON, "Информация", ""); // Строка
			ТехническаяИнформация = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(ДанныеИзJSON, "ТехническаяИнформация", "");
		ИначеЕсли ТипЗнч(ДанныеИзJSON) = Тип("Строка") Тогда
			ТехническаяИнформация = ДанныеИзJSON;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Информация) Тогда
			РезультатЗапроса.Ошибки.Добавить(Информация);
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТехническаяИнформация) Тогда
			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ПараметрыЗапроса.ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка, , , ТехническаяИнформация);
		КонецЕсли;
		
		Возврат;
		
	ИначеЕсли ПараметрыЗапроса.Авторизация Тогда
		
		ЕстьИзменения = Ложь;
		
		ДоступныеАдреса = Ответ.Заголовки["NextUrls"];
		Если ТипЗнч(ДоступныеАдреса) = Тип("Строка") Тогда
			
			ДоступныеАдресаНовые = СтрРазделить(ДоступныеАдреса, ",");
			
			КоличествоДоступныхАдресов = ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.ВГраница();
			Для Индекс = -КоличествоДоступныхАдресов По 0 Цикл
				ОписаниеДоступногоАдреса = ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Получить(-Индекс);
				РезультатПоиска = ДоступныеАдресаНовые.Найти(ОписаниеДоступногоАдреса.Адрес);
				Если РезультатПоиска = Неопределено Тогда
					ЕстьИзменения = Истина;
					ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Удалить(-Индекс);
				Иначе
					ДоступныеАдресаНовые.Удалить(РезультатПоиска);
				КонецЕсли;
			КонецЦикла;
			
			Для Каждого Адрес Из ДоступныеАдресаНовые Цикл
				ЕстьИзменения = Истина;
				ОписаниеДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
				ОписаниеДоступногоАдреса.Адрес = Адрес;
				ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса.Добавить(ОписаниеДоступногоАдреса);
			КонецЦикла;
			
		КонецЕсли;
		
		ТекущийАдрес = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("https://%1/%2",
			ПараметрыЗапроса.Сервер, ПараметрыЗапроса.Приложение);
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если ТекущийАдрес = ОписаниеДоступногоАдреса.Адрес И Не ОписаниеДоступногоАдреса.Текущий Тогда
				ОписаниеДоступногоАдреса.Текущий = Истина;
				ЕстьИзменения = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если ЕстьИзменения Тогда
			ЗаписатьДанныеВБезопасноеХранилище( , Новый Структура("ДоступныеАдреса, ДатаОбновленияТокена",
				ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса, Дата(1,1,1)));
		КонецЕсли;
		
	КонецЕсли;
	
	РезультатЗапроса.Ответ = ДанныеИзJSON;
	РезультатЗапроса.Заголовки = Ответ.Заголовки;
	
КонецПроцедуры

Функция ТребуетсяПереадресация(ПараметрыЗапроса, Ответ)
	
	Результат = Ложь;
	
	Если ПараметрыЗапроса.ОбращениеКСервисуИнтеграции
		И (Ответ = Неопределено
		Или Ответ.КодСостояния = 404
		Или Ответ.КодСостояния = 503) Тогда
		
		Результат = Истина;
		
		ТекущийАдрес = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("https://%1/%2",
			ПараметрыЗапроса.Сервер, ПараметрыЗапроса.Приложение);
		Для Каждого ОписаниеДоступногоАдреса Из ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса Цикл
			Если ТекущийАдрес = ОписаниеДоступногоАдреса.Адрес Тогда
				ОписаниеДоступногоАдреса.ДатаОтказа = ТекущаяДатаСеанса();
				ОписаниеДоступногоАдреса.Текущий = Ложь;
				ПараметрыЗапроса.ДанныеИнтеграции = ЗаписатьДанныеВБезопасноеХранилище( ,
					Новый Структура("ДоступныеАдреса, ДатаОбновленияТокена",
					ПараметрыЗапроса.ДанныеИнтеграции.ДоступныеАдреса, Дата(1,1,1)));
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// HTTPОтвет таймаут.
// 
// Возвращаемое значение:
//  Структура - HTTPОтвет таймаут:
// * КодСостояния - Число - 
// * Заголовки - Соответствие из Строка - 
Функция HTTPОтветТаймаут()
	
	Результат = Новый Структура;
	
	Результат.Вставить("КодСостояния", 408);
	Результат.Вставить("Заголовки", Новый Соответствие);
	
	Возврат Результат;
	
КонецФункции

// Имя сервиса.
// 
// Возвращаемое значение:
//  Строка - Имя сервиса
Функция ИмяСервиса()
	Возврат ИнтеграцияСМаркетплейсамиСИКлиентСервер.ИмяСервисаСервисИнтеграции();
КонецФункции

// Владелец тикета.
// 
// Возвращаемое значение:
//  Строка - Владелец тикета
Функция ВладелецТикета()
	Возврат "IntegrationService";
КонецФункции

// Тикет аутентификации на портале поддержки.
// 
// Возвращаемое значение:
//  Строка - Тикет аутентификации на портале поддержки
Функция ТикетАутентификацииНаПорталеПоддержки()
	
	Результат = "";
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей") Тогда
		МодульИнтернетПоддержкаПользователей = ОбщегоНазначения.ОбщийМодуль("ИнтернетПоддержкаПользователей");
		УстановитьПривилегированныйРежим(Истина);
		ДанныеТикета = МодульИнтернетПоддержкаПользователей.ТикетАутентификацииНаПорталеПоддержки(ВладелецТикета());
		УстановитьПривилегированныйРежим(Ложь);
	Иначе
		ВызватьИсключение НСтр("ru = 'Сервис интернет-поддержки пользователей не подключен.';
								|en = 'Online support service is disabled.'");
	КонецЕсли;
	
	Результат = ДанныеТикета.Тикет;
	
	Возврат Результат;
	
КонецФункции

// Установить токен доступа.
// 
// Параметры:
//  ДанныеИнтеграции - См. НовыеДанныеИнтеграции
//  Ошибки - Массив из Строка - Ошибки
// 
// Возвращаемое значение:
//  Строка - Установить токен доступа
Функция УстановитьТокенДоступа(ДанныеИнтеграции = Неопределено, Ошибки = Неопределено)
	
	ТокенДоступа = "";
	
	Если ДанныеИнтеграции = Неопределено Тогда
		ДанныеИнтеграции = ПрочитатьДанныеИзБезопасногоХранилища();
	КонецЕсли;
	
	Если ТекущаяДатаСеанса() - ДанныеИнтеграции.ДатаОбновленияТокена > 1800 Тогда
		ТокенДоступа = ОбновитьТокенДоступа(ДанныеИнтеграции, Ошибки).id_token;
	Иначе
		ТокенДоступа = ДанныеИнтеграции.id_token;
	КонецЕсли;
	
	Возврат ТокенДоступа;
	
КонецФункции

#КонецОбласти

#Область ЗапросыКСервису

Функция ЗапросПолучитьПодключения(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос установить соединение.
// 
// Параметры:
//  ПараметрыМетода - Структура, Произвольный - Параметры метода
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросУстановитьСоединение(ПараметрыМетода)
	
	ПараметрыМетода.Тикет = ТикетАутентификацииНаПорталеПоддержки();
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос установить подключение.
// 
// Параметры:
//  ПараметрыМетода - см. НовыеПараметрыУстановитьСоединение
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросУстановитьПодключение(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Соответствие;
	ПараметрыЗапроса.Данные.Вставить("login", ПараметрыМетода.Логин);
	ПараметрыЗапроса.Данные.Вставить("name", ПараметрыМетода.Представление);
	ПараметрыЗапроса.Данные.Вставить("keyConnect", ПараметрыМетода.КлючПодключения);
	ПараметрыЗапроса.Данные.Вставить("clientId", ПараметрыМетода.client_id);
	ПараметрыЗапроса.Данные.Вставить("configuration", ПараметрыМетода.ВерсияКонфигурации);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросУдалитьПодключение(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросОбновитьТокенДоступа(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	ПараметрыЗапроса.Данные = "grant_type=CLIENT_CREDENTIALS";
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросПолучитьДанныеПользователяИТС(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.ИдентификаторИТС = "?";
	
	ПараметрыЗапроса.Данные = Новый Соответствие;
	ПараметрыЗапроса.Данные.Вставить("ticket", ПараметрыМетода.Тикет);
	ПараметрыЗапроса.Данные.Вставить("serviceNick", ВладелецТикета());
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса проверки доступности сервиса интеграции.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПроверитьДоступностьСервисаИнтеграции
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросПроверитьДоступностьСервисаИнтеграции(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса проверки доступности сервиса маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПроверитьДоступностьСервисаМаркетплейса
// 
// Возвращаемое значение:
//  См. ВыполнитьЗапросКСервису
Функция ЗапросПроверитьДоступностьСервисаМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область РаботаССервисомОсновныеМетоды

//@skip-check pruned-dynamic-feature-access-translation-ambiguity
#Область ЗапросыКСервисуОсновныеМетоды

// Запрос получить учетные записи
// 
// Параметры:
//  ПараметрыМетода - Структура - Параметры метода:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - 
// * Логин - Строка - 
// * Метод - Строка - 
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросПолучитьУчетныеЗаписи(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Если ПараметрыМетода.ВсеУчетныеЗаписи Тогда
		ПараметрыЗапроса.УчетнаяЗапись = "";
	КонецЕсли;
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "all", XMLСтрока(ПараметрыМетода.ВсеУчетныеЗаписи));
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "checkTokens", XMLСтрока(ПараметрыМетода.ПроверятьТокены));
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос удалить учетную запись.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаУдалитьУчетнуюЗапись
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросУдалитьУчетнуюЗапись(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос создать обновить учетную запись.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаСоздатьОбновитьУчетнуюЗапись
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросСоздатьОбновитьУчетнуюЗапись(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Данные = Новый Соответствие;
	
	Данные.Вставить("inn", ПараметрыМетода.ИНН);
	Данные.Вставить("kpp", ПараметрыМетода.КПП);
	Данные.Вставить("organizationId", XMLСтрока(ПараметрыМетода.Организация));
	Данные.Вставить("name", Строка(ПараметрыМетода.Представление));
	Данные.Вставить("tokens", ПараметрыМетода.Токены);
	Данные.Вставить("functionality", ПараметрыМетода.Функциональность);
	Данные.Вставить("client_id", ПараметрыМетода.client_id);
	Данные.Вставить("client_secret", ПараметрыМетода.client_secret);
	Данные.Вставить("hashAuth", ПараметрыМетода.ХешКлючейАвторизации);
	Данные.Вставить("configuration", ПараметрыМетода.ВерсияКонфигурации);
	
	ПараметрыЗапроса.Данные = Данные;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса обновить настройки учетной записи.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьНастройкиУчетнойЗаписи
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросОбновитьНастройкиУчетнойЗаписи(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Структура;
	Данные = ПараметрыЗапроса.Данные; // Структура
	
	Данные.Вставить("partnerId", XMLСтрока(ПараметрыМетода.Партнер));
	Данные.Вставить("stockAutoCorrection", ПараметрыМетода.АвтоКоррекцияОстатков);
	Данные.Вставить("stockUpload", ПараметрыМетода.ИспользоватьИнтеграциюПоОстаткам);
	Данные.Вставить("priceAutoCorrection", ПараметрыМетода.АвтоКоррекцияЦен);
	Данные.Вставить("priceUpload", ПараметрыМетода.ИспользоватьИнтеграциюПоЦенам);
	Данные.Вставить("priceUploadExchange", ПараметрыМетода.РазрешенаВыгрузкаЦенНаМаркетплейс);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса получить склады маркетплейсов.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьСкладыМаркетплейса
// 
// Возвращаемое значение:
// См. ВыполнитьЗапросКСервису
Функция ЗапросПолучитьСкладыМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);

	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "all", XMLСтрока(Не ПараметрыМетода.ТолькоНесопоставленные));
	
	Если ПараметрыМетода.ЗапроситьДанныеСМаркетплейса Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "aggressive", XMLСтрока(Истина));
	КонецЕсли;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса обновить склады маркетплейсов.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьСкладыМаркетплейса
// 
// Возвращаемое значение:
// См. ВыполнитьЗапросКСервису
Функция ЗапросОбновитьСкладыМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	// Тело запроса
	ПараметрыЗапроса.Данные = Новый Массив;
	ДанныеТелаЗапроса = ПараметрыЗапроса.Данные;	// массив из структура
	
	Для Каждого ОписаниеСклада Из ПараметрыМетода.СкладыМаркетплейса Цикл
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("serviceOfficeId", ОписаниеСклада.ИдентификаторСкладаСервиса);
		
		Склад = ОписаниеСклада.Склад;
		Если ЗначениеЗаполнено(Склад) Тогда
			ОписаниеДанных.Вставить("baseOfficeId", XMLСтрока(Склад));
		Иначе
			ОписаниеДанных.Вставить("baseOfficeId", "");
		КонецЕсли;
		
		ДанныеТелаЗапроса.Добавить(ОписаниеДанных);
	КонецЦикла;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Получить результат запроса выгрузить склады маркетплейсов.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаВыгрузитьСкладыМаркетплейса
// 
// Возвращаемое значение:
// См. ВыполнитьЗапросКСервису
Функция ЗапросВыгрузитьСклады(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	// Тело запроса
	ПараметрыЗапроса.Данные = Новый Массив;
	Данные = ПараметрыЗапроса.Данные; // массив из структура
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Склады.Родитель = ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Склады.Родитель))
	|	КОНЕЦ КАК parentId,
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(Склады.Ссылка)) КАК baseOfficeId,
	|	Склады.Наименование КАК name,
	|	Склады.ЭтоГруппа КАК isFolder
	|ИЗ
	|	Справочник.Склады КАК Склады
	|ГДЕ
	|	Склады.Ссылка В(&Склады)";
	
	Запрос.УстановитьПараметр("Склады", ПараметрыМетода.Склады);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Результат = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыйРезультатМетода();
		Результат.КодСостояния = 200;
		Возврат Результат;
	КонецЕсли;
		
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОписаниеДанных = НовыйАтрибутыЭлементаТелаЗапросаВыгрузитьСклады();
		ЗаполнитьЗначенияСвойств(ОписаниеДанных, Выборка);
		Данные.Добавить(ОписаниеДанных);
	КонецЦикла;
		
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросПолучитьОграниченияОстатковТоваров(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросПолучитьКарточкуСкладаМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "officeId", ПараметрыМетода.ИдентификаторСкладаСервиса);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросВыгрузитьОграниченияОстатковТоваров(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Массив;
	
	Синонимы = Новый Соответствие;
	Синонимы.Вставить(НСтр("ru = 'Номенклатура';
							|en = 'Item'"), НСтр("ru = 'Номенклатура';
														|en = 'Item'"));
	Синонимы.Вставить(НСтр("ru = 'ТоварныеКатегории';
							|en = 'Product categories'"), НСтр("ru = 'ТоварнаяКатегория';
															|en = 'Product category'"));
	Синонимы.Вставить(НСтр("ru = 'ВидыНоменклатуры';
							|en = 'Item kinds'"), НСтр("ru = 'ВидНоменклатуры';
															|en = 'Item kind'"));
	
	Для Каждого ОписаниеОграничения Из ПараметрыМетода.ОграниченияОстатковТоваров Цикл
		
		ОписаниеДанных = Новый Структура;
		
		ОписаниеДанных.Вставить("typeItem", Синонимы.Получить(Метаданные.НайтиПоТипу(ТипЗнч(ОписаниеОграничения.ОбластьДействия)).Имя));
		ОписаниеДанных.Вставить("itemId", XMLСтрока(ОписаниеОграничения.ОбластьДействия));
		ОписаниеДанных.Вставить("isActive", ОписаниеОграничения.Используется);
		ОписаниеДанных.Вставить("percent", ОписаниеОграничения.ПроцентОстатка);
		ОписаниеДанных.Вставить("insuranceReserve", ОписаниеОграничения.СтраховойЗапас);
		ОписаниеДанных.Вставить("limitLow", ОписаниеОграничения.МинимальныйОстаток);
		ОписаниеДанных.Вставить("limitUp", ОписаниеОграничения.МаксимальныйОстаток);
		ОписаниеДанных.Вставить("name", ОписаниеОграничения.Наименование);
		ОписаниеДанных.Вставить("id", ОписаниеОграничения.Код);
		
		ПараметрыЗапроса.Данные.Добавить(ОписаниеДанных);
		
	КонецЦикла;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

Функция ЗапросУдалитьОграниченияОстатковТоваров(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ПараметрыЗапроса.Данные = Новый Массив;
	
	Для Каждого ОписаниеОграничения Из ПараметрыМетода.ОграниченияОстатковТоваров Цикл
		
		//@skip-check structure-consructor-value-type
		Карточка = Новый Структура("Id", ОписаниеОграничения.Код);
		ПараметрыЗапроса.Данные.Добавить(Карточка);
	КонецЦикла;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Возвращает результат запроса ПолучитьБренды к сервису
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьБренды
// 
// Возвращаемое значение:
//  см. НовыйРезультатМетодаСервиса
Функция ЗапросПолучитьБренды(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
КонецФункции

Функция ЗапросПолучитьНоменклатуруМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "all", XMLСтрока(Не ПараметрыМетода.ТолькоНесопоставленные));
	
	Если ПараметрыМетода.ЗапроситьДанныеСМаркетплейса Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "aggressive", XMLСтрока(Истина));
	КонецЕсли;
	Если ПараметрыМетода.Курсор <> "" Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "cursor", ПараметрыМетода.Курсор);
	КонецЕсли;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос обновить номенклатуру маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаОбновитьНоменклатуруМаркетплейса
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросОбновитьНоменклатуруМаркетплейса(ПараметрыМетода)
	
	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	РезультатЗапроса.КодСостояния = 200;
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	// Тело запроса
	ПараметрыЗапроса.Данные = Новый Массив;
	
	НоменклатураМаркетплейса = Новый ТаблицаЗначений;
	НоменклатураМаркетплейса.Колонки.Добавить("ИдентификаторНоменклатурыСервиса",
		Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(300)));
	НоменклатураМаркетплейса.Колонки.Добавить("НоменклатураКонтрагента",
		Новый ОписаниеТипов("СправочникСсылка.НоменклатураКонтрагентов"));
	
	Для Каждого Данные Из ПараметрыМетода.НоменклатураМаркетплейса Цикл
		НоваяСтрока = НоменклатураМаркетплейса.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Данные);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НоменклатураМаркетплейса.ИдентификаторНоменклатурыСервиса КАК ИдентификаторНоменклатурыСервиса,
	|	НоменклатураМаркетплейса.НоменклатураКонтрагента КАК НоменклатураКонтрагента
	|ПОМЕСТИТЬ врНоменклатураМаркетплейса
	|ИЗ
	|	&НоменклатураМаркетплейса КАК НоменклатураМаркетплейса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	врНоменклатураМаркетплейса.ИдентификаторНоменклатурыСервиса КАК ИдентификаторНоменклатурыСервиса,
	|	ЕСТЬNULL(НоменклатураКонтрагентов.Ссылка, &ПустаяСтрока) КАК НоменклатураКонтрагента,
	|	ЕСТЬNULL(Номенклатура1С.Ссылка, &ПустаяСтрока) КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА Характеристика1С.Ссылка ЕСТЬ NULL
	|			ТОГДА &ПустаяСтрока
	|		ИНАЧЕ Характеристика1С.Ссылка
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА Номенклатура1С.Ссылка ЕСТЬ NULL
	|			ТОГДА &ПустаяСтрока
	|		ИНАЧЕ Номенклатура1С.ВидНоменклатуры
	|	КОНЕЦ КАК ВидНоменклатуры,
	|	ВЫБОР
	|		КОГДА Номенклатура1С.Ссылка ЕСТЬ NULL
	|			ТОГДА &ПустаяСтрока
	|		ИНАЧЕ Номенклатура1С.ТоварнаяКатегория
	|	КОНЕЦ КАК ТоварнаяКатегория,
	|	ЕСТЬNULL(Окр(&ТекстЗапросаКоэффициентУпаковки, 2), 1) КАК baseRatioPackage
	|ИЗ
	|	врНоменклатураМаркетплейса КАК врНоменклатураМаркетплейса
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = врНоменклатураМаркетплейса.НоменклатураКонтрагента)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1С
	|		ПО (Номенклатура1С.Ссылка = НоменклатураКонтрагентов.Номенклатура)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ХарактеристикиНоменклатуры КАК Характеристика1С
	|		ПО (Характеристика1С.Ссылка = НоменклатураКонтрагентов.Характеристика)";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст,
		"&ТекстЗапросаКоэффициентУпаковки",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"(ВЫБОР
			|	КОГДА НоменклатураКонтрагентов.Упаковка = ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
			|		ТОГДА Номенклатура1С.ЕдиницаИзмерения
			|	ИНАЧЕ НоменклатураКонтрагентов.Упаковка
			|КОНЕЦ)",
			"НоменклатураКонтрагентов.Номенклатура"));
	
	Запрос.УстановитьПараметр("НоменклатураМаркетплейса", НоменклатураМаркетплейса);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ОписаниеДанных = Новый Структура;
		
		ОписаниеДанных.Вставить("serviceCardId", Выборка.ИдентификаторНоменклатурыСервиса);
		
		Если ЗначениеЗаполнено(Выборка.НоменклатураКонтрагента) Тогда
			ОписаниеДанных.Вставить("baseCardId", XMLСтрока(Выборка.НоменклатураКонтрагента));
		Иначе
			ОписаниеДанных.Вставить("baseCardId", "");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Номенклатура) Тогда
			ОписаниеДанных.Вставить("baseStockId", XMLСтрока(Выборка.Номенклатура));
		Иначе
			ОписаниеДанных.Вставить("baseStockId", "");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.Характеристика) Тогда
			ОписаниеДанных.Вставить("baseCharacteristicId", XMLСтрока(Выборка.Характеристика));
		Иначе
			ОписаниеДанных.Вставить("baseCharacteristicId", "");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ВидНоменклатуры) Тогда
			ОписаниеДанных.Вставить("baseStockType", XMLСтрока(Выборка.ВидНоменклатуры));
		Иначе
			ОписаниеДанных.Вставить("baseStockType", "");
		КонецЕсли;
		Если ЗначениеЗаполнено(Выборка.ТоварнаяКатегория) Тогда
			ОписаниеДанных.Вставить("baseStockCategory", XMLСтрока(Выборка.ТоварнаяКатегория));
		Иначе
			ОписаниеДанных.Вставить("baseStockCategory", "");
		КонецЕсли;
		
		ОписаниеДанных.Вставить("baseRatioPackage", Выборка.baseRatioPackage);
		
		ПараметрыЗапроса.Данные.Добавить(ОписаниеДанных);
		
		Если ПараметрыЗапроса.Данные.Количество() >= 50 Тогда
			
			РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
			
			Если Не ЗначениеЗаполнено(РезультатЗапроса) Тогда
				Возврат РезультатЗапроса;
			ИначеЕсли РезультатЗапроса.КодСостояния <> 200 Тогда
				Возврат РезультатЗапроса;
			КонецЕсли;
			
			ПараметрыЗапроса.Данные.Очистить();
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ПараметрыЗапроса.Данные.Количество() > 0 Тогда
		РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	КонецЕсли;
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ЗапросПолучитьКарточкуНоменклатурыМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "serviceCardId", ПараметрыМетода.ИдентификаторНоменклатурыСервиса);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);

КонецФункции

// Возвращает результат запроса ПолучитьКатегории к сервису 
// 
// Параметры:
//  ПараметрыМетода - см. НовыйПараметрыЗапросаПолучитьКатегории
// 
// Возвращаемое значение:
//  см. НовыйРезультатМетодаСервиса 
Функция ЗапросПолучитьКатегории(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
КонецФункции

Функция ЗапросПолучитьОстаткиТоваровМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	
	Если ПараметрыМетода.Курсор <> "" Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "cursor", ПараметрыМетода.Курсор);
	КонецЕсли;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

//@skip-check typed-value-adding-to-untyped-collection
// Запрос получить показатели номенклатуры маркетплейса.
// 
// Параметры:
//  ПараметрыМетода - См. НовыйПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса
// 
// Возвращаемое значение:
//  Структура - См. НовыйРезультатМетодаСервиса
Функция ЗапросПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыМетода)
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);

	Если ПараметрыМетода.Курсор <> "" Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "Cursor", ПараметрыМетода.Курсор);
	КонецЕсли;
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "SortName", ПараметрыМетода.ПолеСортировки);
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "SortDir", ПараметрыМетода.НаправлениеСортировки);
	
	Если ПараметрыМетода.РазмерСтраницы <> 0 Тогда
		ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "Limit", ПараметрыМетода.РазмерСтраницы);
	КонецЕсли;
	
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "Remains", XMLСтрока(ПараметрыМетода.ПолучитьСводныйОстаток));
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "Prices", XMLСтрока(ПараметрыМетода.ПолучитьЦены));
	
	// Тело запроса
	ПараметрыЗапроса.Данные = Новый Структура;
	
	ПараметрыЗапроса.Данные.Вставить("serviceCardId", Новый Массив);
	ИдентификаторыМП = ПараметрыЗапроса.Данные.serviceCardId; // Массив Из Произвольный
	Для Каждого ИдМП Из ПараметрыМетода.НоменклатураМП Цикл
		ИдентификаторыМП.Добавить(ИдМП);
	КонецЦикла;
	
	ПараметрыЗапроса.Данные.Вставить("marketBrandId", Новый Массив);
	ИдентификаторыМП = ПараметрыЗапроса.Данные.marketBrandId; // Массив Из Произвольный
	
	Для Каждого ИдМП Из ПараметрыМетода.БрендыМП Цикл
		ИдентификаторыМП.Добавить(ИдМП);
	КонецЦикла;
	
	ПараметрыЗапроса.Данные.Вставить("marketCategoryId", Новый Массив);
	ИдентификаторыМП = ПараметрыЗапроса.Данные.marketCategoryId; // Массив Из Произвольный
	
	Для Каждого ИдМП Из ПараметрыМетода.КатегорииМП Цикл
		ИдентификаторыМП.Добавить(ИдМП);
	КонецЦикла;
	
	Если ПараметрыМетода.ТолькоВНаличии <> Неопределено Тогда
		ПараметрыЗапроса.Данные.Вставить("availableInStock", ПараметрыМетода.ТолькоВНаличии);
	КонецЕсли;
	
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	Если ПараметрыМетода.МаркерСтатуса = МаркерыСтатусов.НаКарантине Тогда
		ПараметрыЗапроса.Данные.Вставить("quarantine", Истина);
	ИначеЕсли ПараметрыМетода.МаркерСтатуса = МаркерыСтатусов.Ошибка Тогда
		ПараметрыЗапроса.Данные.Вставить("status", "error");
	ИначеЕсли ПараметрыМетода.МаркерСтатуса = МаркерыСтатусов.БезОшибок Тогда
		ПараметрыЗапроса.Данные.Вставить("status", "ready");
	КонецЕсли;
	
	Если Не ПустаяСтрока(ПараметрыМетода.НаименованиеМП) Тогда
		ПараметрыЗапроса.Данные.Вставить("serviceCardName", ПараметрыМетода.НаименованиеМП);
	КонецЕсли;
	
	Возврат ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода);
	
КонецФункции

// Запрос выгрузить остатки товаров.
// 
// Параметры:
//  ПараметрыМетода - Структура - Параметры метода:
// * ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - 
// * УчетнаяЗапись - Строка - 
// * ВсеТовары - Булево - 
// * Товары - Массив из Структура - :
// ** Номенклатура - СправочникСсылка.Номенклатура - 
// ** Характеристика - СправочникСсылка.ХарактеристикиНоменклатуры - 
// ** Склад - СправочникСсылка.Склады - 
// * ВыводитьСообщения - Булево - 
// * ИмяМетода - Строка - 
// 
// Возвращаемое значение:
//  См. НовыйРезультатМетодаСервиса
Функция ЗапросВыгрузитьОстаткиТоваров(ПараметрыМетода)
	
	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	РезультатЗапроса.КодСостояния = 200;
	
	РазмерПорции = 500;
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	ПараметрыЗапроса.Заголовки.Вставить("Content-Encoding", "gzip");
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "StreamId", Строка(Новый УникальныйИдентификатор()));
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "IsLast", XMLСтрока(Ложь));
	
	ДанныеПагинации = НовыеАтрибутыПагинации();
	ДанныеПагинации.PageSize = РазмерПорции;
	
	Товары = Новый ТаблицаЗначений;
	Товары.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Товары.Колонки.Добавить("Характеристика", Новый ОписаниеТипов("СправочникСсылка.ХарактеристикиНоменклатуры"));
	Товары.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Для Каждого ОписаниеТовара Из ПараметрыМетода.Товары Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОписаниеТовара);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&УчетнаяЗаписьМаркетплейса КАК УчетнаяЗаписьМаркетплейса,
	|	ВЫРАЗИТЬ(Товары.Склад КАК Справочник.Склады) КАК Склад,
	|	ВЫРАЗИТЬ(Товары.Номенклатура КАК Справочник.Номенклатура) КАК Номенклатура,
	|	ВЫРАЗИТЬ(Товары.Характеристика КАК Справочник.ХарактеристикиНоменклатуры) КАК Характеристика
	|ПОМЕСТИТЬ врДанные
	|ИЗ
	|	&Товары КАК Товары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	УчетнаяЗаписьМаркетплейса,
	|	Склад,
	|	Номенклатура,
	|	Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	1 КАК КоличествоЗаписей,
	|	врДанные.Номенклатура КАК Номенклатура,
	|	врДанные.Характеристика КАК Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(врДанные.Склад)) КАК baseOfficeId,
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(врДанные.Номенклатура)) КАК baseStockId,
	|	ВЫБОР
	|		КОГДА врДанные.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(врДанные.Характеристика))
	|	КОНЕЦ КАК baseStockCharacteristicId,
	|	ЕСТЬNULL(ОстаткиТоваров.Количество, 0) КАК quantity,
	|	ВЫБОР
	|		КОГДА ОстаткиТоваров.УчетнаяЗаписьМаркетплейса ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК isDelete
	|ИЗ
	|	врДанные КАК врДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваров
	|		ПО (ОстаткиТоваров.УчетнаяЗаписьМаркетплейса = врДанные.УчетнаяЗаписьМаркетплейса)
	|			И (ОстаткиТоваров.Склад = врДанные.Склад)
	|			И (ОстаткиТоваров.Номенклатура = врДанные.Номенклатура)
	|			И (ОстаткиТоваров.Характеристика = врДанные.Характеристика)
	|ГДЕ
	|	НЕ &ВсеТовары
	|	И врДанные.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И врДанные.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ОстаткиТоваров.Номенклатура,
	|	ОстаткиТоваров.Характеристика,
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ОстаткиТоваров.Склад)),
	|	ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ОстаткиТоваров.Номенклатура)),
	|	ВЫБОР
	|		КОГДА ОстаткиТоваров.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|			ТОГДА """"
	|		ИНАЧЕ ПРЕДСТАВЛЕНИЕ(УНИКАЛЬНЫЙИДЕНТИФИКАТОР(ОстаткиТоваров.Характеристика))
	|	КОНЕЦ,
	|	ОстаткиТоваров.Количество,
	|	ЛОЖЬ
	|ИЗ
	|	РегистрСведений.ОстаткиТоваровМаркетплейсов КАК ОстаткиТоваров
	|ГДЕ
	|	&ВсеТовары
	|	И ОстаткиТоваров.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И ОстаткиТоваров.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|	И ОстаткиТоваров.Склад <> ЗНАЧЕНИЕ(Справочник.Склады.ПустаяСсылка)
	|ИТОГИ
	|	Количество(КоличествоЗаписей)
	|ПО
	|	ОБЩИЕ,
	|	Номенклатура,
	|	Характеристика";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", ПараметрыМетода.УчетнаяЗапись);
	Запрос.УстановитьПараметр("Товары", Товары);
	Запрос.УстановитьПараметр("ВсеТовары", ПараметрыМетода.ВсеТовары);
	
	Данные = Новый Массив; // Массив из Структура
	
	УстановитьПривилегированныйРежим(Истина);
	РезультатЗапросаДанные = Запрос.Выполнить();
	УстановитьПривилегированныйРежим(Ложь);
	
	Если РезультатЗапросаДанные.Пустой() Тогда
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ЭтоПоследняяПорция = Ложь;
	ПовторныйЗапрос = Ложь;
	
	ВыборкаКоличество = РезультатЗапросаДанные.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКоличество.Следующий() Цикл
		
		ОбработанноеКоличествоЗаписей = 0;
		
		ВыборкаНоменклатура = ВыборкаКоличество.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаНоменклатура.Следующий() Цикл
			
			ВыборкаХарактеристика = ВыборкаНоменклатура.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаХарактеристика.Следующий() Цикл
				
				Выборка = ВыборкаХарактеристика.Выбрать();
				
				ПлановоеКоличествоЗаписей = ОбработанноеКоличествоЗаписей + ВыборкаХарактеристика.КоличествоЗаписей;
				
				Если Не ЭтоПоследняяПорция Тогда
					Если ((ВыборкаКоличество.КоличествоЗаписей - ПлановоеКоличествоЗаписей) < РазмерПорции) Тогда
						ВыгрузитьПорцию = Истина;
						ЭтоПоследняяПорция = Истина;
					ИначеЕсли (Данные.Количество() + ВыборкаХарактеристика.КоличествоЗаписей) > РазмерПорции Тогда
						ВыгрузитьПорцию = Истина;
					Иначе
						ВыгрузитьПорцию = Ложь;
					КонецЕсли;
				
					Если ВыгрузитьПорцию И Данные.Количество() > 0 Тогда
						ПараметрыЗапроса.Данные = Данные;
						РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода,,ПовторныйЗапрос);
						ПовторныйЗапрос = Истина;
						
						Если РезультатЗапроса.КодСостояния <> 200 Тогда
							Возврат РезультатЗапроса;
						КонецЕсли;
						
						Данные.Очистить();
					КонецЕсли;
				КонецЕсли;
				
				Пока Выборка.Следующий() Цикл
				
					ОписаниеДанных = НовыйАтрибутыЭлементаТелаЗапросаВыгрузитьОстаткиТоваров();
					ЗаполнитьЗначенияСвойств(ОписаниеДанных, Выборка);
					Если Выборка.isDelete Тогда
						ОписаниеДанных.Вставить("isDelete", Истина);
					КонецЕсли;
					Данные.Добавить(ОписаниеДанных);
				
				КонецЦикла;
				
				ОбработанноеКоличествоЗаписей = ПлановоеКоличествоЗаписей;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	Если Данные.Количество() > 0 Тогда
		ПараметрыЗапроса.АдресМетода = СтрЗаменить(ПараметрыЗапроса.АдресМетода, "IsLast="+XMLСтрока(Ложь), "IsLast="+XMLСтрока(Истина));
		ПараметрыЗапроса.Данные = Данные;
		РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода,,ПовторныйЗапрос);
	КонецЕсли;
	
	Возврат РезультатЗапроса;
	
КонецФункции

Функция ЗапросВыгрузитьЦены(ПараметрыМетода)

	РезультатЗапроса = НовыйРезультатМетодаСервиса();
	РезультатЗапроса.КодСостояния = 200;
	
	ВидМаркетплейса = ПараметрыМетода.ВидМаркетплейса;
	Если Не ЭтоМаркетплейсСИ(ВидМаркетплейса) Тогда
		ТекстСообщения = СтрШаблон(НСтр("ru = 'Вид маркетплейса %1 не поддерживает обмен с сервисом интеграции.';
										|en = 'The %1 marketplace kind does not support exchange with the integration service.'", 
			ОбщегоНазначения.КодОсновногоЯзыка()), ВидМаркетплейса);
		ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(ВидМаркетплейса), 
			УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		РезультатЗапроса.КодСостояния = 400;
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	КлючеваяОперация = СтрШаблон("СинхронизацияССервисомИнтеграции.РасчетЦен.%1",
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркетплейсПоВиду(ВидМаркетплейса));
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(КлючеваяОперация);
	КоличествоДанныхДляЗамера = 0;
	
	РазмерПорции = 100;
	
	ПараметрыЗапроса = НовыеПараметрыЗапроса(ПараметрыМетода);
	ПараметрыЗапроса.Заголовки.Вставить("Content-Encoding", "gzip");
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "StreamId", Строка(Новый УникальныйИдентификатор()));
	ДобавитьПараметрАдресаHTML(ПараметрыЗапроса.АдресМетода, "IsLast", XMLСтрока(Ложь));
	
	ДанныеПагинации = НовыеАтрибутыПагинации();
	ДанныеПагинации.PageSize = РазмерПорции;
	
	НастройкиУчетнойЗаписи = Справочники.УчетныеЗаписиМаркетплейсов.НастройкиУчетнойЗаписи(ПараметрыМетода.УчетнаяЗапись);
	ТипыЦенМаркетплейса = ТипыЦенПоВидуМаркетплейса(ВидМаркетплейса);
	Если НастройкиВидаЦенНеЗаполнены(НастройкиУчетнойЗаписи, ТипыЦенМаркетплейса) Тогда
		ТекстСообщения = НСтр("ru = 'Не установлены виды цен для выгрузки.';
								|en = 'Price types to export are not set.'", ОбщегоНазначения.КодОсновногоЯзыка());
		ЗаписьЖурналаРегистрации(
			СобытиеЖурналаРегистрации(ВидМаркетплейса), УровеньЖурналаРегистрации.Ошибка, , , ТекстСообщения);
		РезультатЗапроса.КодСостояния = 400;
		Возврат РезультатЗапроса;
	КонецЕсли;
	
	ТекущаяДата = КонецДня(ТекущаяДатаСеанса());
	
	УзлыПланаОбмена = ПланыОбмена.МаркетплейсыСИ.НайтиУзлы(ПараметрыМетода.УчетнаяЗапись);
	
	Запрос = Новый Запрос;
	
	ИспользуетсяЦенообразование25 = ЦенообразованиеВызовСервера.ИспользуетсяЦенообразование25(ТекущаяДата);
	ИспользуетсяОтборПоНоменклатуре = ПараметрыМетода.НоменклатураМП.Количество() > 0;
	ПоляДляИндексирования = ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыИндексирование(ИспользуетсяЦенообразование25);
		
	ИменаПолей = СтрРазделить(ПоляДляИндексирования, ",");
	
	ИменаПолейБезВидаЦен = Новый Массив; // Массив Из Строка
	Для Каждого ИмяПоля Из ИменаПолей Цикл
		Если СтрНайти(ИмяПоля, "ВидЦены") = 0 Тогда
			ИменаПолейБезВидаЦен.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	ПоляДляИндексированияБезВидаЦен = СтрСоединить(ИменаПолейБезВидаЦен, ",");
	
	ЧастиЗапроса = Новый Массив; // Массив Из Строка
	// Подготовка таблицы товаров маркетплейса, для которых необходимо получить цены
	ТекстЗапроса = ТекстЗапросаТаблицаТоваровМП(ПоляДляИндексированияБезВидаЦен, ИспользуетсяЦенообразование25, ИспользуетсяОтборПоНоменклатуре);
	ЧастиЗапроса.Добавить(ТекстЗапроса);
	
	// Исключаем характеристики, по которым не было изменения цен в документах, попавших в план обмена.
	// Добавляем виды цен.
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВидыЦен.ВидЦены КАК ВидЦены,
	|	&ПоляТаблицыЦенБезВидаЦен
	|ПОМЕСТИТЬ ТаблицаДляОтбораЦен
	|ИЗ
	|	ТаблицаТоваровМП КАК ТаблицаНХУ
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК ВидыЦен
	|		ПО (ВидыЦен.Ссылка = &УчетнаяЗаписьМаркетплейса)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	&ПоляДляИндексирования
	|";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицыЦенБезВидаЦен", ПоляДляИндексированияБезВидаЦен);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляИндексирования", ПоляДляИндексирования);
	
	ЧастиЗапроса.Добавить(ТекстЗапроса);
	
	// Удаление временных таблиц
	ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ТаблицаНХУ");
	
	// Расчет цен для ранее подготовленной таблицы товаров
	ТекстЗапроса = ТекстЗапросаРасчетЦенПоТаблицеТоваровМП(ПоляДляИндексирования, ИспользуетсяЦенообразование25);
	ЧастиЗапроса.Добавить(ТекстЗапроса);
	
	// Удаление временных таблиц
	ЧастиЗапроса.Добавить("УНИЧТОЖИТЬ ТаблицаДляОтбораЦен");
	
	// Пересчет цен по упаковкам для ранее подготовленной таблицы товаров
	ТекстЗапроса = ТекстЗапросаПересчетЦенПоУпаковкамТаблицыТоваровМП(ПоляДляИндексированияБезВидаЦен, 
		ИспользуетсяЦенообразование25, ТипыЦенМаркетплейса.Количество());
	ЧастиЗапроса.Добавить(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("УзлыПланаОбмена", УзлыПланаОбмена);
	Запрос.УстановитьПараметр("Организация", НастройкиУчетнойЗаписи.Организация);
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", ПараметрыМетода.УчетнаяЗапись);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.БазоваяВалютаПоУмолчанию());
	Запрос.УстановитьПараметр("ВалютаУчета", НастройкиУчетнойЗаписи.ВалютаУчета);
	Запрос.УстановитьПараметр("ДатаЦен", ТекущаяДата);
	
	Для Каждого ТипЦены Из ТипыЦенМаркетплейса Цикл
		ИмяПараметра = СтрШаблон("ВидЦены%1", ТипыЦенМаркетплейса.Индекс(ТипЦены) + 1);
		Запрос.УстановитьПараметр(ИмяПараметра, НастройкиУчетнойЗаписи.ВидыЦен[ТипЦены.Идентификатор]);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("НоменклатураМП", ПараметрыМетода.НоменклатураМП);
	Запрос.УстановитьПараметр("ВсяНоменклатураМП", ПараметрыМетода.ВсяНоменклатураМП);
	
	Запрос.Текст = СтрСоединить(ЧастиЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	РезультатПакета = Запрос.ВыполнитьПакет();
	
	ЭтоПоследняяПорция = Ложь;
	ПовторныйЗапрос = Ложь;
	
	Данные = Новый Массив; // Массив из Структура
	
	ВыборкаКоличество = РезультатПакета[9].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаКоличество.Следующий() Цикл
		
		ОбработанноеКоличествоЗаписей = 0;
		КоличествоДанныхДляЗамера = ВыборкаКоличество.КоличествоЗаписей; // Число
	
		ВыборкаАртикулы = ВыборкаКоличество.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		// Для корректного расчета, все записи с одним артикулом должны попасть в один пакет данных
		Пока ВыборкаАртикулы.Следующий() Цикл
			
			Выборка = ВыборкаАртикулы.Выбрать();
			
			ПлановоеКоличествоЗаписей = ОбработанноеКоличествоЗаписей + ВыборкаАртикулы.КоличествоЗаписей;
			
			Если Не ЭтоПоследняяПорция Тогда
				Если ((ВыборкаКоличество.КоличествоЗаписей - ПлановоеКоличествоЗаписей) < РазмерПорции) Тогда
					ВыгрузитьПорцию = Истина;
					ЭтоПоследняяПорция = Истина;
				ИначеЕсли (Данные.Количество() + ВыборкаАртикулы.КоличествоЗаписей) > РазмерПорции Тогда
					ВыгрузитьПорцию = Истина;
				Иначе
					ВыгрузитьПорцию = Ложь;
				КонецЕсли;
			
				Если ВыгрузитьПорцию И Данные.Количество() > 0 Тогда
					ПараметрыЗапроса.Данные = Данные;
					
					РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода,,ПовторныйЗапрос);
					ПовторныйЗапрос = Истина;
					
					Если РезультатЗапроса.КодСостояния <> 200 Тогда
						Возврат РезультатЗапроса;
					КонецЕсли;
					
					Данные.Очистить();
				КонецЕсли;
			КонецЕсли;
			
			Пока Выборка.Следующий() Цикл
				ОписаниеДанных = Новый Структура;
				
				ОписаниеДанных.Вставить("serviceCardId", Выборка.ИдентификаторНоменклатурыМП);
				
				Для Каждого ТипЦены Из ТипыЦенМаркетплейса Цикл
					ИмяПоляЗапроса = СтрШаблон("Цена%1", ТипыЦенМаркетплейса.Индекс(ТипЦены) + 1);
					ОписаниеДанных.Вставить(ТипЦены.ИмяПараметраСИ, Выборка[ИмяПоляЗапроса]);
				КонецЦикла;
				
				Данные.Добавить(ОписаниеДанных);
			КонецЦикла;
			
			ОбработанноеКоличествоЗаписей = ПлановоеКоличествоЗаписей;
			
		КонецЦикла;
	КонецЦикла;
	
	Если Данные.Количество() > 0 Тогда
		ПараметрыЗапроса.АдресМетода = СтрЗаменить(ПараметрыЗапроса.АдресМетода, "IsLast="+XMLСтрока(Ложь), "IsLast="+XMLСтрока(Истина));
		ПараметрыЗапроса.Данные = Данные;
		
		РезультатЗапроса = ВыполнитьЗапросКСервису(ПараметрыЗапроса, ПараметрыМетода,,ПовторныйЗапрос);
		
		Если РезультатЗапроса.КодСостояния <> 200 Тогда
			Возврат РезультатЗапроса;
		КонецЕсли;
	КонецЕсли;
	
	// Очистим план обмена
	Выборка = РезультатПакета[10].Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ИспользуетсяЦенообразование25 Тогда
			НаборЗаписей = РегистрыСведений.ЦеныНоменклатуры25.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыСведений.ЦеныНоменклатуры.СоздатьНаборЗаписей();
		КонецЕсли;
		НаборЗаписей.Отбор.Регистратор.Установить(Выборка.Регистратор);
		ПланыОбмена.УдалитьРегистрациюИзменений(Выборка.Узел, НаборЗаписей);
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КоличествоДанныхДляЗамера / 10000);
	
	Возврат РезультатЗапроса;
	
КонецФункции

// Текст запроса таблицы товаров маркетплейса, для которых необходимо получить цены
// 
// Параметры:
//  ПоляДляИндексированияБезВидаЦен - Строка
//  ИспользуетсяЦенообразование25 - Булево
//  ИспользуетсяОтборПоНоменклатуре - Булево
// 
// Возвращаемое значение:
//  Строка
Функция ТекстЗапросаТаблицаТоваровМП(ПоляДляИндексированияБезВидаЦен, ИспользуетсяЦенообразование25, ИспользуетсяОтборПоНоменклатуре)
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	УстановкаЦен.Ссылка КАК Регистратор,
	|	ТаблицаИзмененияЦен.Узел КАК Узел
	|ПОМЕСТИТЬ врИсточникиИзмененияЦен
	|ИЗ
	|	&ТаблицаИзмененияЦен КАК ТаблицаИзмененияЦен
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаЦенНоменклатуры КАК УстановкаЦен
	|		ПО (УстановкаЦен.Ссылка = ТаблицаИзмененияЦен.Регистратор)
	|			И (УстановкаЦен.Дата <= &ДатаЦен)
	|ГДЕ
	|	ТаблицаИзмененияЦен.Узел В(&УзлыПланаОбмена)
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ПоляТаблицыЦенБезВидаЦен
	|ПОМЕСТИТЬ ТаблицаНХУ
	|ИЗ
	|	&ТаблицаЦен КАК ЦеныНоменклатуры
	|ГДЕ
	|	НЕ &ВсяНоменклатураМП
	|	И ЦеныНоменклатуры.Ссылка В
	|			(ВЫБРАТЬ
	|				врИсточникиИзмененияЦен.Регистратор
	|			ИЗ
	|				врИсточникиИзмененияЦен КАК врИсточникиИзмененияЦен)
	|ИНДЕКСИРОВАТЬ ПО
	|	&ПоляДляИндексированияБезВидаЦен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеНоменклатурыМП.Идентификатор КАК ИдентификаторНоменклатурыМП,
	|	ДанныеНоменклатурыМП.Номенклатура КАК Номенклатура,
	|	ДанныеНоменклатурыМП.Характеристика КАК Характеристика,
	|	ДанныеНоменклатурыМП.КодНоменклатуры КАК АртикулМП,
	|	ВЫБОР
	|		КОГДА ДанныеНоменклатурыМП.Упаковка = Номенклатура1С.ЕдиницаИзмерения
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|		ИНАЧЕ ДанныеНоменклатурыМП.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	Номенклатура1С.ВидНоменклатуры КАК ВидНоменклатуры,
	|	&ПоляДляЦенообразованияВыборка
	|ПОМЕСТИТЬ ДанныеНоменклатурыМП
	|ИЗ
	|	РегистрСведений.НоменклатураКонтрагентовБЭД КАК ДанныеНоменклатурыМП
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1С
	|		ПО Номенклатура1С.Ссылка = ДанныеНоменклатурыМП.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = Номенклатура1С.ВидНоменклатуры)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК Соответствия
	|		ПО Соответствия.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|			И Соответствия.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|			И Соответствия.ИдентификаторОбъектаМаркетплейса = ДанныеНоменклатурыМП.Идентификатор
	|ГДЕ
	|	НЕ &ВсяНоменклатураМП
	|	И ДанныеНоменклатурыМП.Номенклатура В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ТаблицаНХУ.Номенклатура
	|			ИЗ
	|				ТаблицаНХУ)
	|	И ЕстьNULL(Соответствия.ДатаАктуальности, ДАТАВРЕМЯ(1,1,1,0,0,0)) <> ДАТАВРЕМЯ(1,1,1,0,0,0)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеНоменклатурыМП.ИдентификаторНоменклатурыМП КАК ИдентификаторНоменклатурыМП,
	|	ДанныеНоменклатурыМП.Номенклатура КАК Номенклатура,
	|	ДанныеНоменклатурыМП.Характеристика КАК Характеристика,
	|	ДанныеНоменклатурыМП.АртикулМП КАК АртикулМП,
	|	ДанныеНоменклатурыМП.Упаковка КАК Упаковка,
	|	ДанныеНоменклатурыМП.ВидНоменклатуры КАК ВидНоменклатуры,
	|	&ПоляДляЦенообразованияТаблицаТоваровМП
	|ПОМЕСТИТЬ ТаблицаТоваровМП
	|ИЗ
	|	ДанныеНоменклатурыМП КАК ДанныеНоменклатурыМП
	|ГДЕ
	|	НЕ &ВсяНоменклатураМП
	|	И (&ПоляДляИндексированияБезВидаЦен) В (ВЫБРАТЬ &ПоляДляИндексированияБезВидаЦен ИЗ ТаблицаНХУ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДанныеНоменклатурыМП.Идентификатор КАК ИдентификаторНоменклатурыМП,
	|	ДанныеНоменклатурыМП.Номенклатура КАК Номенклатура,
	|	ДанныеНоменклатурыМП.Характеристика КАК Характеристика,
	|	ДанныеНоменклатурыМП.КодНоменклатуры КАК АртикулМП,
	|	ВЫБОР
	|		КОГДА ДанныеНоменклатурыМП.Упаковка = Номенклатура1С.ЕдиницаИзмерения
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)
	|		ИНАЧЕ ДанныеНоменклатурыМП.Упаковка
	|	КОНЕЦ КАК Упаковка,
	|	ДанныеНоменклатурыМП.Номенклатура.ВидНоменклатуры КАК ВидНоменклатуры,
	|	&ПоляДляЦенообразованияВыборка
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК Соответствия
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК ДанныеНоменклатурыМП
	|		ПО (ДанныеНоменклатурыМП.Ссылка = Соответствия.Объект1С)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1С
	|		ПО Номенклатура1С.Ссылка = ДанныеНоменклатурыМП.Номенклатура
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыНоменклатуры КАК ВидыНоменклатуры
	|		ПО (ВидыНоменклатуры.Ссылка = Номенклатура1С.ВидНоменклатуры)
	|ГДЕ
	|	&ВсяНоменклатураМП
	|	И &УсловиеОтбораПоНоменклатуре
	|	И Соответствия.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И Соответствия.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	&ПоляДляИндексированияБезВидаЦен
	|";
	
	Если ИспользуетсяЦенообразование25 Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИзмененияЦен", "РегистрСведений.ЦеныНоменклатуры25.Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаЦен", "Документ.УстановкаЦенНоменклатуры.Товары2_5");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИзмененияЦен", "РегистрСведений.ЦеныНоменклатуры.Изменения");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаЦен", "Документ.УстановкаЦенНоменклатуры.Товары");
	КонецЕсли;
	
	НастройкаЦенообразования = ЦенообразованиеКлиентСервер.НастройкиДляВременнойТаблицыСДополнениемДляЦенообразования();
	НастройкаЦенообразования.ИсточникТоваров = "ДанныеНоменклатурыМП";
	НастройкаЦенообразования.ПриемникТоваров = "";
	НастройкаЦенообразования.ПолеСерия = "";
	
	ТекстЗамены = ЦенообразованиеКлиентСервер.ТекстПолейДляЦенообразования(НастройкаЦенообразования,,
		ИспользуетсяЦенообразование25);
		
	Если ЗначениеЗаполнено(ТекстЗамены) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", ТекстЗамены);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
			"&ПоляДляЦенообразованияТаблицаТоваровМП",
			"	ДанныеНоменклатурыМП.ХарактеристикаЦО КАК ХарактеристикаЦО,
			|	ДанныеНоменклатурыМП.СерияЦО КАК СерияЦО,
			|	ДанныеНоменклатурыМП.УпаковкаЦО КАК УпаковкаЦО");
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", "ИСТИНА");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияТаблицаТоваровМП", "ИСТИНА");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицыЦенБезВидаЦен",
		СтрЗаменить(ПоляДляИндексированияБезВидаЦен, "СерияЦО", 
		"ЗНАЧЕНИЕ(Справочник.СерииНоменклатурыДляЦенообразования.ПустаяСсылка) КАК СерияЦО"));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляИндексированияБезВидаЦен", ПоляДляИндексированияБезВидаЦен);
	
	УсловиеОтбораПоНоменклатуре = "ДанныеНоменклатурыМП.Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
	Если ИспользуетсяОтборПоНоменклатуре Тогда
		УсловиеОтбораПоНоменклатуре = "ДанныеНоменклатурыМП.Ссылка В (&НоменклатураМП)";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоНоменклатуре", УсловиеОтбораПоНоменклатуре);
	
	Возврат ТекстЗапроса;

КонецФункции

// Текст запроса для расчета цен по таблице товаров маркетплейса
// 
// Параметры:
//  ПоляДляИндексирования - Строка
//  ИспользуетсяЦенообразование25 - Булево
// 
// Возвращаемое значение:
//  Строка
Функция ТекстЗапросаРасчетЦенПоТаблицеТоваровМП(ПоляДляИндексирования, ИспользуетсяЦенообразование25)
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ОтносительныеКурсыВСрезПоследних.Валюта КАК Валюта,
	|	ОтносительныеКурсыВСрезПоследних.КурсЧислитель КАК КурсЧислитель,
	|	ОтносительныеКурсыВСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&ДатаЦен, БазоваяВалюта = &БазоваяВалюта) КАК ОтносительныеКурсыВСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Валюта
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	&ПоляДляЦенообразованияВыборка,
	|	Цена,
	|	Упаковка,
	|	ЕСТЬNULL(КурсыВалют.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютУчета.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютУчета.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалют.КурсЗнаменатель, 1)) КАК КоэффициентПересчетаЦены
	|	
	|ПОМЕСТИТЬ ЦеныТоваров
	|ИЗ
	|	&ЦеныНоменклатуры КАК ЦеныНоменклатуры
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО ЦеныНоменклатуры.Валюта = КурсыВалют.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	&ПоляДляИндексирования
	|	,Цена
	|	,Упаковка
	|	,КоэффициентПересчетаЦены";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляЦенообразованияВыборка", ПоляДляИндексирования);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляДляИндексирования", ПоляДляИндексирования);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ЦеныНоменклатуры",
		ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатуры("ТаблицаДляОтбораЦен",
			"&ДатаЦен",
			Новый Структура("ВТаблице", "ВидЦены"),
			ИспользуетсяЦенообразование25));
	Возврат ТекстЗапроса;
КонецФункции

// Текст запроса для расчета цен по таблице товаров маркетплейса
// 
// Параметры:
//  ПоляДляИндексированияБезВидаЦен - Строка
//  ИспользуетсяЦенообразование25 - Булево
//  КоличествоВидовЦен - Число
// 
// Возвращаемое значение:
//  Строка
Функция ТекстЗапросаПересчетЦенПоУпаковкамТаблицыТоваровМП(ПоляДляИндексированияБезВидаЦен, 
														ИспользуетсяЦенообразование25, КоличествоВидовЦен)
	ЧастиЗапроса = Новый Массив; // Массив Из Строка
	ТекстВыборкаПолей = 
	"ВЫБРАТЬ
	|	1 КАК КоличествоЗаписей,
	|	ТаблицаТоваровМП.АртикулМП КАК АртикулМП,
	|	ТаблицаТоваровМП.ИдентификаторНоменклатурыМП КАК ИдентификаторНоменклатурыМП";
	ЧастиЗапроса.Добавить(ТекстВыборкаПолей);
	
	ЧастиЗапросаИсточники = Новый Массив; // Массив Из Строка
	ТекстИсточники = "
	|ИЗ
	|	ТаблицаТоваровМП КАК ТаблицаТоваровМП";
	ЧастиЗапросаИсточники.Добавить(ТекстИсточники);
	
	Для НомерВидаЦены = 1 По КоличествоВидовЦен Цикл
		ТекстВыборкаПолей = " 
		|	ВЫРАЗИТЬ(ЕстьNULL(ТоварыСЦенами%Номер%.Цена,0)
		|				* ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиТовара, 1)
		|				/ ВЫБОР
		|					КОГДА ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиЦены%Номер%, 1) = 0
		|						ТОГДА 1
		|						ИНАЧЕ ЕСТЬNULL(&ТекстЗапросаКоэффициентУпаковкиЦены%Номер%, 1)
		|					КОНЕЦ
		|				* ЕстьNULL(ТоварыСЦенами%Номер%.КоэффициентПересчетаЦены, 1) КАК ЧИСЛО(31, 2)) КАК Цена%Номер%";
		ТекстВыборкаПолей = СтрЗаменить(ТекстВыборкаПолей, "%Номер%", Строка(НомерВидаЦены));
		ЧастиЗапроса.Добавить(ТекстВыборкаПолей);
		
		ТекстИсточники = "
		|		ЛЕВОЕ СОЕДИНЕНИЕ ЦеныТоваров КАК ТоварыСЦенами%Номер%
		|		ПО &УсловиеСоединенияЦеныНоменклатуры%Номер%
		|		И ТоварыСЦенами%Номер%.ВидЦены = &ВидЦены%Номер%";
		ТекстИсточники = СтрЗаменить(ТекстИсточники, "%Номер%", Строка(НомерВидаЦены));
		ЧастиЗапросаИсточники.Добавить(ТекстИсточники);
	КонецЦикла;
	
	ТекстВыборкаПолей = СтрСоединить(ЧастиЗапроса, ",");
	ТекстИсточники = СтрСоединить(ЧастиЗапросаИсточники);
	
	ТекстУсловияИтоги = "
	|
	|ИТОГИ
	|	Количество(КоличествоЗаписей)
	|ПО
	|	ОБЩИЕ,
	|	АртикулМП";
	
	ЧастиЗапроса.Очистить();
	ЧастиЗапроса.Добавить(ТекстВыборкаПолей);
	ЧастиЗапроса.Добавить(ТекстИсточники);
	ЧастиЗапроса.Добавить(ТекстУсловияИтоги);
	ТекстЗапросаВыборкаЦен = СтрСоединить(ЧастиЗапроса);
	
	ТекстЗапросаВыборкаРегистраторов = 
	"ВЫБРАТЬ
	|	врИсточникиИзмененияЦен.Регистратор КАК Регистратор,
	|	врИсточникиИзмененияЦен.Узел КАК Узел
	|ИЗ
	|	врИсточникиИзмененияЦен КАК врИсточникиИзмененияЦен";
	
	ЧастиЗапроса.Очистить();
	ЧастиЗапроса.Добавить(ТекстЗапросаВыборкаЦен);
	ЧастиЗапроса.Добавить(ТекстЗапросаВыборкаРегистраторов);
	ТекстЗапроса = СтрСоединить(ЧастиЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
			
	ИменаПолейОсновнойТаблицы = Новый Массив; // Массив Из Строка
	Для Каждого ИмяПоля Из СтрРазделить(ПоляДляИндексированияБезВидаЦен, ",") Цикл
		ИменаПолейОсновнойТаблицы.Добавить("ТаблицаТоваровМП." + СокрЛП(СтрЗаменить(ИмяПоля, Символы.ПС, "")));
	КонецЦикла;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицыЦенОсновнойТаблицыБезВидаЦен", 
		СтрСоединить(ИменаПолейОсновнойТаблицы, ","));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТаблицыЦенБезВидаЦен", ПоляДляИндексированияБезВидаЦен);
	
	Для НомерВидаЦены = 1 По КоличествоВидовЦен Цикл
		ПодстрокаПоиска = СтрЗаменить("&ТекстЗапросаКоэффициентУпаковкиЦены%Номер%", "%Номер%", Строка(НомерВидаЦены));
		ИсточникУпаковки = СтрЗаменить("ТоварыСЦенами%Номер%.Упаковка", "%Номер%", Строка(НомерВидаЦены));
		ИсточникНоменклатуры = СтрЗаменить("ТоварыСЦенами%Номер%.Номенклатура", "%Номер%", Строка(НомерВидаЦены));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, ПодстрокаПоиска,
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(ИсточникУпаковки, ИсточникНоменклатуры));
	КонецЦикла;
		
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,
		"&ТекстЗапросаКоэффициентУпаковкиТовара",
		Справочники.УпаковкиЕдиницыИзмерения.ТекстЗапросаКоэффициентаУпаковки(
			"ТаблицаТоваровМП.Упаковка",
			"ТаблицаТоваровМП.Номенклатура"));
			
	Для НомерВидаЦены = 1 По КоличествоВидовЦен Цикл
		ПодстрокаПоиска = СтрЗаменить("&УсловиеСоединенияЦеныНоменклатуры%Номер%", "%Номер%", Строка(НомерВидаЦены));
		ИсточникЦен = СтрЗаменить("ТоварыСЦенами%Номер%", "%Номер%", Строка(НомерВидаЦены));
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, 
			ПодстрокаПоиска,
			ЦенообразованиеКлиентСервер.ТекстЗапросаРегистрСведенийЦеныНоменклатурыУсловиеСоединения(
			"ТаблицаТоваровМП",
			ИсточникЦен,
			Неопределено,
			ИспользуетсяЦенообразование25));
	КонецЦикла;
		
	Возврат ТекстЗапроса;
КонецФункции

#КонецОбласти

// Параметры запроса получить показатели номенклатуры маркетплейса.
// 
// Параметры:
//  ПараметрыОбъекта -Структура - параметры формы с динамическим списком цен:
//   * ИмяФормы - Строка - 
//   * УникальныйИдентификатор - Строка 
//   * УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов
//   * НастройкиСКД - НастройкиКомпоновкиДанных - настройки компоновки данных для динамического списка цен формы
//   * ЦеныОстаткиТоваровМП_КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек динамического списка цен формы
//   * ЦеныОстаткиТоваровМП_Параметры - ЗначенияПараметровМакетаКомпоновкиДанных - параметры динамического списка цен формы
//  ПараметрыЗапроса -Структура - входящие параметры для запроса, возможные ключи:
//   * Курсор - Строка - курсор страницы, содержит идентификатора номенклатуры маркетплейса
//   * ПолучитьСводныйОстаток - Булево 
//   * ПолучитьЦены - Булево
//   * ЗависитОт - Строка -
//   * ЗависимыеПоля - Строка -
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов -
Процедура ПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыОбъекта, ПараметрыЗапроса) Экспорт

	ВходящиеПараметры = ПараметрыЗапроса;
	
	ПараметрыЗапроса = НовыйПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса();
	ПараметрыЗапроса.ВидМаркетплейса = ПараметрыОбъекта.ВидМаркетплейса;
	
	НастройкиСКД = ПараметрыОбъекта.НастройкиСКД;
	Если НастройкиСКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса.УчетнаяЗапись = ПараметрыОбъекта.УчетнаяЗапись;
	
	ПараметрыЗапроса.РазмерСтраницы = НастройкиСКД.ДополнительныеСвойства.РазмерСтраницы; // Число
	ПараметрыЗапроса.ПолеСортировки = "name";
	
	ПолеБрендМП = Новый ПолеКомпоновкиДанных("БрендМП");
	ПолеИспользоватьБрендыМП = НастройкиСКД.ПараметрыДанных.Элементы.Найти("ИспользоватьБрендыМП");
	Если ПолеИспользоватьБрендыМП = Неопределено Тогда
		ИспользоватьБрендыМП = Ложь;
	Иначе
		ИспользоватьБрендыМП = ПолеИспользоватьБрендыМП.Значение; // Булево
	КонецЕсли;
	
	ПолеКатегорияМП = Новый ПолеКомпоновкиДанных("КатегорияМП");
	ПолеИспользоватьКатегорииМП = НастройкиСКД.ПараметрыДанных.Элементы.Найти("ИспользоватьКатегорииМП");
	Если ПолеИспользоватьКатегорииМП = Неопределено Тогда
		ИспользоватьКатегорииМП = Ложь;
	Иначе
		ИспользоватьКатегорииМП = ПолеИспользоватьКатегорииМП.Значение; // Булево
	КонецЕсли;
	
	ПолеТолькоВНаличии = Новый ПолеКомпоновкиДанных("ТолькоВНаличии");
	ПолеИспользоватьТолькоВНаличии = НастройкиСКД.ПараметрыДанных.Элементы.Найти("ИспользоватьТолькоВНаличии");
	Если ПолеИспользоватьТолькоВНаличии = Неопределено Тогда
		ИспользоватьТолькоВНаличии = Ложь;
	Иначе
		ИспользоватьТолькоВНаличии = ПолеИспользоватьТолькоВНаличии.Значение; // Булево
	КонецЕсли;
	
	ПолеНаименованиеМП = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	ПолеИспользоватьНаименованиеМП = НастройкиСКД.ПараметрыДанных.Элементы.Найти("ИспользоватьНаименованиеМП");
	Если ПолеИспользоватьНаименованиеМП = Неопределено Тогда
		ИспользоватьНаименованиеМП = Ложь;
	Иначе
		ИспользоватьНаименованиеМП = ПолеИспользоватьНаименованиеМП.Значение; // Булево
	КонецЕсли;
	
	ПолеМаркерСтатуса = Новый ПолеКомпоновкиДанных("МаркерСтатуса");
	ПолеИспользоватьМаркерСтатуса = НастройкиСКД.ПараметрыДанных.Элементы.Найти("ИспользоватьМаркерСтатуса");
	Если ПолеИспользоватьМаркерСтатуса = Неопределено Тогда
		ИспользоватьМаркерСтатуса = Ложь;
	Иначе
		ИспользоватьМаркерСтатуса = ПолеИспользоватьМаркерСтатуса.Значение; // Булево
	КонецЕсли;
	
	Для Каждого ЭлементОтбора Из НастройкиСКД.Отбор.Элементы Цикл
		Если ЭлементОтбора.ЛевоеЗначение = ПолеБрендМП Тогда
			Если ИспользоватьБрендыМП Тогда
				УстановитьПараметрМетодаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыЗапроса.БрендыМП, ЭлементОтбора);
			КонецЕсли;
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеКатегорияМП Тогда
			Если ИспользоватьКатегорииМП Тогда
				УстановитьПараметрМетодаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыЗапроса.КатегорииМП, ЭлементОтбора);
			КонецЕсли;
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеТолькоВНаличии Тогда
			Если ИспользоватьТолькоВНаличии Тогда
				ПараметрыЗапроса.ТолькоВНаличии = ЭлементОтбора.ПравоеЗначение;
			КонецЕсли;
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеНаименованиеМП Тогда
			Если ИспользоватьНаименованиеМП Тогда
				ПараметрыЗапроса.НаименованиеМП = ЭлементОтбора.ПравоеЗначение;
			КонецЕсли;
		ИначеЕсли ЭлементОтбора.ЛевоеЗначение = ПолеМаркерСтатуса Тогда
			Если ИспользоватьМаркерСтатуса Тогда
				ПараметрыЗапроса.МаркерСтатуса = ЭлементОтбора.ПравоеЗначение;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	СкорректироватьНастройкиСортировкиЦеныОстаткиТоваровМП(НастройкиСКД, ПараметрыЗапроса.НаправлениеСортировки);
	
	Если ВходящиеПараметры <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыЗапроса, ВходящиеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Установить параметр метода получить показатели номенклатуры маркетплейса.
// 
// Параметры:
//  ПараметрМетода - Массив Из Строка - Параметр метода
//  ЭлементОтбора - ЭлементОтбораКомпоновкиДанных, ГруппаЭлементовОтбораКомпоновкиДанных - Элемент отбора
Процедура УстановитьПараметрМетодаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрМетода, ЭлементОтбора)
	
	Если ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке Тогда
		Если ТипЗнч(ЭлементОтбора.ПравоеЗначение) = Тип("СписокЗначений") Тогда
			Для Каждого ЭлементЗначения Из ЭлементОтбора.ПравоеЗначение Цикл				
				ПараметрМетода.Добавить(ЭлементЗначения.Значение); // Строка
			КонецЦикла;
		КонецЕсли;
	ИначеЕсли ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно Тогда
		ПараметрМетода.Добавить(ЭлементОтбора.ПравоеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбщиеМетодыДляРаботыССервисом

// Описание атрибутов пагинации, указываемых в теле http-запроса
Функция НовыеАтрибутыПагинации()
	Результат = Новый Структура;
	Результат.Вставить("streamId", Строка(Новый УникальныйИдентификатор));
	Результат.Вставить("pageSize", 0);
	Результат.Вставить("rc", 0);
	Результат.Вставить("isLast", Ложь);
	
	Возврат Результат;
КонецФункции

#КонецОбласти

//@skip-check variable-value-type
#Область РаботаСФормой

// Добавить элементы состояния.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
//  МестоРасположения - ГруппаФормы - Элемент, в который разместится группа
Процедура ДобавитьЭлементыСостояния(Форма, МестоРасположения) Экспорт
	
	ГруппаСостояниеВСервисе = Форма.Элементы.Добавить("ГруппаСостояниеВСервисе", Тип("ГруппаФормы"), МестоРасположения);
	ГруппаСостояниеВСервисе.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСостояниеВСервисе.ОтображатьЗаголовок = Ложь;
	ГруппаСостояниеВСервисе.Группировка = ГруппировкаПодчиненныхЭлементовФормы.ГоризонтальнаяВсегда;
	ГруппаСостояниеВСервисе.Видимость = Ложь;
	
	НовыйЭлемент = Форма.Элементы.Добавить("СостояниеВСервисеКартинка", Тип("ДекорацияФормы"), ГруппаСостояниеВСервисе);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Картинка;
	НовыйЭлемент.Картинка = БиблиотекаКартинок.Предупреждение;
	НовыйЭлемент.ВертикальноеПоложениеВГруппе = ВертикальноеПоложениеЭлемента.Центр;
	
	НовыйЭлемент = Форма.Элементы.Добавить("СостояниеВСервисе", Тип("ДекорацияФормы"), ГруппаСостояниеВСервисе);
	НовыйЭлемент.Вид = ВидДекорацииФормы.Надпись;
	НовыйЭлемент.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_СостояниеВСервисеОбработкаНавигационнойСсылки");
	
КонецПроцедуры

// Заполнить элементы состояния.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - Форма
Процедура ЗаполнитьЭлементыСостояния(Форма) Экспорт
	
	Элементы = Форма.Элементы;
	
	Элементы.СостояниеВСервисе.Подсказка = "";
	
	Если Не ЗначениеЗаполнено(Форма.УчетнаяЗапись) Тогда
		Элементы.ГруппаСостояниеВСервисе.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	ОписаниеУчетнойЗаписи = Форма.Кэш.УчетныеЗаписи.Получить(Форма.УчетнаяЗапись);
	Если ОписаниеУчетнойЗаписи = Неопределено Тогда
		Элементы.ГруппаСостояниеВСервисе.Видимость = Ложь;
		Возврат;
	КонецЕсли;
	
	Ошибки = Новый Массив; // Массив Из Строка
	
	ЗаполнитьОшибкиСостоянияОбменаПриНеобходимости(Форма, Ошибки);
	
	Для Каждого ДанныеТокена Из ОписаниеУчетнойЗаписи.Токены Цикл
		Если Не ПустаяСтрока(ДанныеТокена.Ошибка) Тогда
			Ошибки.Добавить(ДанныеТокена.Ошибка)
		КонецЕсли;
	КонецЦикла;
	
	Элементы.ГруппаСостояниеВСервисе.Видимость = Ошибки.Количество() > 0;
	Если Не Элементы.ГруппаСостояниеВСервисе.Видимость Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.СостояниеВСервисе.Подсказка = СтрСоединить(ОбщегоНазначенияКлиентСервер.СвернутьМассив(Ошибки), Символы.ПС);
	Элементы.СостояниеВСервисе.Заголовок =
		СтроковыеФункции.ФорматированнаяСтрока(СтрШаблон("<a href = ""Показать"">%1</a>", НСтр("ru = 'Внимание';
																								|en = 'Warning'")));
	
КонецПроцедуры

Процедура ЗаполнитьОшибкиСостоянияОбменаПриНеобходимости(Форма, Ошибки)
	Если СтрНайти(Форма.ИмяФормы, "ВыгрузкаТоварногоКаталога")  = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ЕстьПраваНаОбмен = Форма.Кэш.ПравоНаОстатки Или Форма.Кэш.ПравоНаЦены;
	Если Не ЕстьПраваНаОбмен Тогда
		Ошибки.Добавить(
			НСтр("ru = 'Получить права на редактирование остатков товаров маркетплейса или изменение цен.';
				|en = 'Get the rights to edit the marketplace item balance or change prices.'"));
	КонецЕсли;

	ОписаниеУчетнойЗаписи = Форма.Кэш.УчетныеЗаписи.Получить(Форма.УчетнаяЗапись);
	ФункциональностьДоступна = 
		ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоОстаткам Или ОписаниеУчетнойЗаписи.ИспользоватьИнтеграциюПоЦенам;
	Если Не ФункциональностьДоступна Тогда
		Ошибки.Добавить(
			НСтр("ru = 'Включить обмен остатками или ценами.';
				|en = 'Enable balance or price exchange.'"));
	КонецЕсли;
	
	Если Ошибки.Количество() > 0 Тогда
		Ошибки.Вставить(0, НСтр("ru = 'Для работы необходимо:';
								|en = 'You need:'"));
	КонецЕсли;
	
КонецПроцедуры

#Область ЦеныОстаткиТоваровМП

// Скорректировать настройки сортировки цены остатки товаров МП.
// 
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных - 
//  НаправлениеСортировки - Строка - Направление сортировки, возможные значения "asc" и "desc"
Процедура СкорректироватьНастройкиСортировкиЦеныОстаткиТоваровМП(Настройки, НаправлениеСортировки)
	
	ПолеПорядка = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	
	ПравильноеПолеСортировки = Ложь;
	Для Каждого ЭлементПорядка Из Настройки.Порядок.Элементы Цикл
		Если ЭлементПорядка.Использование Тогда
			Если ЭлементПорядка.Поле = ПолеПорядка Тогда
				ПравильноеПолеСортировки = Истина;
				Если ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
					НаправлениеСортировки = "asc";
				Иначе
					НаправлениеСортировки = "desc";
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПравильноеПолеСортировки Тогда
		Настройки.Порядок.Элементы.Очистить();
		ЭлементПорядка = Настройки.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		ЭлементПорядка.Поле = ПолеПорядка;
		ЭлементПорядка.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
		ЭлементПорядка.Использование = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Синхронизировать цены.
// 
// Параметры:
//  КомпоновщикНастроек - КомпоновщикНастроекКомпоновкиДанных - компоновщик настроек динамического списка формы ЦеныОстаткиТоваровМП
//  КлючНоменклатурыМП - Строка - ключ вида <НаименованиеНоменклатурыМП_ИдентификаторНоменклатурыМП>
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
Процедура СинхронизироватьЦены(Знач КомпоновщикНастроек, КлючНоменклатурыМП, ВидМаркетплейса) Экспорт
	
	Настройки = КомпоновщикНастроек.ПолучитьНастройки();
	
	АдресаСтраницКэша = Настройки.ДополнительныеСвойства.АдресаСтраницКэша;
	ПредыдущаяСтраница = Неопределено;
	// Обновим/удалим страницы
	Для Каждого СтраницаКэша Из АдресаСтраницКэша Цикл
		
		Если КлючНоменклатурыМП >= СтраницаКэша.Значение.ПервыйКлюч
			И КлючНоменклатурыМП <= СтраницаКэша.Значение.ПоследнийКлюч
			Или СтраницаКэша.Значение.Первая
			Или СтраницаКэша.Значение.Последняя Тогда
			
			Порядок = "";
			Если Настройки.ДополнительныеСвойства.НаправлениеСортировкиКД = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
				Если СтраницаКэша.Значение.Первая Тогда
					Курсор = "";
					Порядок = "asc";
				ИначеЕсли СтраницаКэша.Значение.Последняя Тогда
					Курсор = "";
					Порядок = "desc";
				ИначеЕсли ПредыдущаяСтраница <> Неопределено Тогда
					Курсор = ПредыдущаяСтраница.Значение.ПоследнийИдентификатор;
					Порядок = "asc";
				КонецЕсли;
			Иначе
				Если СтраницаКэша.Значение.Последняя Тогда
					Курсор = "";
					Порядок = "desc";
				ИначеЕсли СтраницаКэша.Значение.Последняя Тогда
					Курсор = "";
					Порядок = "asc";
				ИначеЕсли ПредыдущаяСтраница <> Неопределено Тогда
					Курсор = ПредыдущаяСтраница.Значение.ПервыйИдентификатор;
					Порядок = "desc";
				КонецЕсли;
			КонецЕсли;
			
			ПараметрыСтраницы = Новый Структура("Курсор, Порядок", Курсор, Порядок);
			ДополнитьКэшСтраницЦеныОстаткиТоваровМП(Настройки, ПараметрыСтраницы, ВидМаркетплейса);
		КонецЕсли;
		
		ПредыдущаяСтраница = СтраницаКэша;
		
	КонецЦикла;
	
КонецПроцедуры

// Дополнить кэш страниц цены остатки товаров МП.
// 
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных - Настройки
//  ПараметрыСтраницы - Структура - Параметры страницы:
// * Курсор - Строка - 
// * Порядок - Строка - 
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов - Вид маркетплейса
Процедура ДополнитьКэшСтраницЦеныОстаткиТоваровМП(Настройки, ПараметрыСтраницы, ВидМаркетплейса) Экспорт
	
	ПараметрУчетнаяЗапись = Настройки.ПараметрыДанных.Элементы.Найти("УчетнаяЗаписьМаркетплейса").Значение;
	Если Не ЗначениеЗаполнено(ПараметрУчетнаяЗапись) Тогда
		Возврат;
	КонецЕсли;

	ПараметрыМетода = Новый Структура;
	ПараметрыМетода.Вставить("Курсор", ПараметрыСтраницы.Курсор);
	ПараметрыМетода.Вставить("ПолучитьСводныйОстаток", Истина);
	ПараметрыМетода.Вставить("ПолучитьЦены", Истина);
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("ИмяФормы", Настройки.ДополнительныеСвойства.ИмяФормы);
	ПараметрыОбъекта.Вставить("УникальныйИдентификатор", Настройки.ДополнительныеСвойства.УникальныйИдентификатор);
	ПараметрыОбъекта.Вставить("УчетнаяЗапись", ПараметрУчетнаяЗапись);
	ПараметрыОбъекта.Вставить("НастройкиСКД", Настройки);
	ПараметрыОбъекта.Вставить("ВидМаркетплейса", ВидМаркетплейса);
	
	ПараметрыЗапросаПолучитьПоказателиНоменклатурыМаркетплейса(ПараметрыОбъекта, ПараметрыМетода);
	
	ПараметрыМетода.Вставить("ИмяМетода", 
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	Если ПараметрыСтраницы.Порядок <> "" Тогда
		ПараметрыМетода.НаправлениеСортировки = ПараметрыСтраницы.Порядок;
	КонецЕсли;
	
	ПараметрыОперации = ИнтеграцияСМаркетплейсамиСИКлиентСервер.НовыеПараметрыМенеджераДлительныхОпераций();
	ПараметрыОперации.ОбработкаРезультатаНаСервереБезКонтекста = Истина;
	ПараметрыОперации.ВыводитьОкноОжидания = Ложь;
	ПараметрыОперации.ВыводитьСообщения = Ложь;
	ПараметрыОперации.РежимОткрытияОкнаОжидания = Неопределено;
	ПараметрыОперации.СеансыУведомленияКлиента.Добавить(Настройки.ДополнительныеСвойства.НомерСеансаИБ);
	ПараметрыОперации.ВидМаркетплейса = ВидМаркетплейса;
	ПараметрыОперации.КлючФоновогоЗадания = Новый УникальныйИдентификатор;
	
	ПараметрыМетода.ВерсияКэша = Настройки.ДополнительныеСвойства.ВерсияКэша;
	//@skip-check pruned-dynamic-feature-access-translation-ambiguity
	ПараметрыМетода.СеансыУведомленияКлиента.Добавить(Настройки.ДополнительныеСвойства.НомерСеансаИБ);
	ПараметрыОперации.Очередь.Добавить(ПараметрыМетода , 
		ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодПолучитьПоказателиНоменклатурыМаркетплейса());
	
	ВыполнитьДлительныеОперации(ПараметрыОбъекта, ПараметрыОперации);

КонецПроцедуры

// Данные в строках динамического списка заполняются по данным из кэша страниц на форме или Сервиса интеграции.
// 
// Параметры:
//  Настройки - НастройкиКомпоновкиДанных - настройки динамического списка ЦеныОстаткиТоваровМП
//  Строки - СтрокиДинамическогоСписка - коллекция строк динамического списка ЦеныОстаткиТоваровМП
Процедура ЦеныОстаткиТоваровМПЗаполнитьПоДаннымСИ(Настройки, Строки) Экспорт
	
	ДействующиеЗапросы = ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
	
	МаркерыСтатусов = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МаркерыСтатусовЦен();
	// по данным строк определяем первый и последний ключ
	ПервыйКлюч = "";
	ПервыйКлючИдентификатор = "";
	
	ПоследнийКлюч = "";
	ПоследнийКлючИдентификатор = "";
	
	Для Каждого СтрокаЦены Из Строки Цикл
		ЧастиКлюча = Новый Массив; // Массив Из Строка
		ЧастиКлюча.Добавить(СтрокаЦены.Значение.Данные.НаименованиеМП);
		ЧастиКлюча.Добавить(СтрокаЦены.Значение.Данные.ИдентификаторМП);
		
		ТекущийКлюч = СтрСоединить(ЧастиКлюча, "_");
		
		Если ПервыйКлюч = "" Или СтрСравнить(ПервыйКлюч, ТекущийКлюч) = 1 Тогда
			ПервыйКлюч = ТекущийКлюч;
			ПервыйКлючИдентификатор = СтрокаЦены.Значение.Данные.ИдентификаторМП;
		КонецЕсли;
		Если ПоследнийКлюч = "" Или СтрСравнить(ПоследнийКлюч, ТекущийКлюч) = -1 Тогда
			ПоследнийКлюч = ТекущийКлюч;
			ПоследнийКлючИдентификатор = СтрокаЦены.Значение.Данные.ИдентификаторМП;
		КонецЕсли;
		СтрокаЦены.Значение.Данные.МаркерСтатуса = МаркерыСтатусов.НетДанных;
	КонецЦикла;
	
	// Определим по установленным первому и последнему ключу страницы из кэша.
	// АдресаСтраницКэша - список значений из Структура, содержащая ключи ПервыйКлюч, ПоследнийКлюч
	ПредыдущаяСтраница = Неопределено;
	ТекущаяСтраница = Неопределено;
	СледующаяСтраница = Неопределено;
	
	НомерТекущейСтраницы = 0;
	НомерПоследнейСтраницы = Настройки.ДополнительныеСвойства.АдресаСтраницКэша.Количество()-1;
	
	Для Каждого КэшСтраницы Из Настройки.ДополнительныеСвойства.АдресаСтраницКэша Цикл
		
		Если ТекущаяСтраница = Неопределено 
			И ПервыйКлюч >= КэшСтраницы.Значение.ПервыйКлюч И ПервыйКлюч <= КэшСтраницы.Значение.ПоследнийКлюч Тогда
			
			ТекущаяСтраница = КэшСтраницы;
			
			Если НомерТекущейСтраницы > 0 Тогда 
				ПредыдущаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1];
			КонецЕсли;
			
			Если НомерТекущейСтраницы < НомерПоследнейСтраницы Тогда
				Если ПоследнийКлюч >= Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1].Значение.ПервыйКлюч
					И ПоследнийКлюч <= Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1].Значение.ПоследнийКлюч Тогда
						СледующаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1];
						
					ИначеЕсли Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1].Значение.Курсор = ТекущаяСтраница.Значение.ПоследнийИдентификатор Тогда
						СледующаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1];
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ТекущаяСтраница = Неопределено И ПоследнийКлюч >= КэшСтраницы.Значение.ПервыйКлюч И ПоследнийКлюч <= КэшСтраницы.Значение.ПоследнийКлюч Тогда
			
			ТекущаяСтраница = КэшСтраницы;
			Если НомерТекущейСтраницы > 0 Тогда
				Если ПервыйКлюч >= Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1].Значение.ПервыйКлюч
					И ПервыйКлюч <= Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1].Значение.ПоследнийКлюч Тогда
						ПредыдущаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1];
					
				ИначеЕсли Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1].Значение.Курсор = ТекущаяСтраница.Значение.ПервыйИдентификатор Тогда
					ПредыдущаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы-1];
				КонецЕсли;
			КонецЕсли;
			
			Если НомерТекущейСтраницы < НомерПоследнейСтраницы Тогда
				СледующаяСтраница = Настройки.ДополнительныеСвойства.АдресаСтраницКэша[НомерТекущейСтраницы+1];
			КонецЕсли;
			
		КонецЕсли;
	
		НомерТекущейСтраницы = НомерТекущейСтраницы + 1;
	КонецЦикла;
	
	СчитанныеДанныеКэша = Новый Соответствие;
	ДобавленДействующийЗапрос = Ложь;
	
	Если ТекущаяСтраница <> Неопределено Тогда
		ДанныеТекущейСтраницы = ПолучитьИзВременногоХранилища(ТекущаяСтраница.Значение.АдресСтраницы);
		Если ДанныеТекущейСтраницы = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПрочитатьПредыдущуюСтраницу = Ложь;
		ПрочитатьСледующуюСтраницу = Ложь;
		
		Если (ПредыдущаяСтраница = Неопределено И Не ТекущаяСтраница.Значение.Первая) Тогда 
			ПрочитатьПредыдущуюСтраницу = Истина;
			
		ИначеЕсли ПредыдущаяСтраница <> Неопределено Тогда
			
			Если ПредыдущаяСтраница.Значение.Первая Или ДанныеТекущейСтраницы.Получить(ПервыйКлючИдентификатор) = Неопределено Тогда
				ДанныеПредыдущейСтраницы = ПолучитьИзВременногоХранилища(ПредыдущаяСтраница.Значение.АдресСтраницы);
				СчитанныеДанныеКэша.Вставить(ПредыдущаяСтраница.Значение.ПервыйКлюч, ДанныеПредыдущейСтраницы);
			
				Если ДанныеПредыдущейСтраницы.Получить(ПервыйКлючИдентификатор) = Неопределено И ПредыдущаяСтраница.Значение.Курсор <> ТекущаяСтраница.Значение.ПервыйИдентификатор Тогда
					ПрочитатьПредыдущуюСтраницу = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если (СледующаяСтраница = Неопределено И Не ТекущаяСтраница.Значение.Последняя) Тогда 
			ПрочитатьСледующуюСтраницу = Истина;
			
		ИначеЕсли СледующаяСтраница <> Неопределено Тогда
			
			Если СледующаяСтраница.Значение.Последняя Или ДанныеТекущейСтраницы.Получить(ПоследнийКлючИдентификатор) = Неопределено Тогда
				ДанныеСледующейСтраницы = ПолучитьИзВременногоХранилища(СледующаяСтраница.Значение.АдресСтраницы);
				СчитанныеДанныеКэша.Вставить(СледующаяСтраница.Значение.ПервыйКлюч, ДанныеСледующейСтраницы);
			
				Если ДанныеСледующейСтраницы.Получить(ПоследнийКлючИдентификатор) = Неопределено И СледующаяСтраница.Значение.Курсор <> ТекущаяСтраница.Значение.ПоследнийИдентификатор Тогда
					ПрочитатьСледующуюСтраницу = Истина;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;

		Если ПрочитатьПредыдущуюСтраницу И ДействующиеЗапросы.Получить(ТекущаяСтраница.Значение.ПервыйИдентификатор) = Неопределено Тогда
			ДополнитьКэшСтраницЦеныОстаткиТоваровМП(Настройки, ТекущаяСтраница.Значение.ПервыйИдентификатор, "desc");
			ДействующиеЗапросы.Вставить(ТекущаяСтраница.Значение.ПервыйИдентификатор, ТекущаяСтраница.Значение.ПервыйКлюч);
			ДобавленДействующийЗапрос = Истина;
		КонецЕсли;
		Если ПрочитатьСледующуюСтраницу И ДействующиеЗапросы.Получить(ТекущаяСтраница.Значение.ПоследнийИдентификатор) = Неопределено Тогда
			ДополнитьКэшСтраницЦеныОстаткиТоваровМП(Настройки, ТекущаяСтраница.Значение.ПоследнийИдентификатор, "asc");
			ДействующиеЗапросы.Вставить(ТекущаяСтраница.Значение.ПоследнийИдентификатор, ТекущаяСтраница.Значение.ПоследнийКлюч);
			ДобавленДействующийЗапрос = Истина;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ДобавленДействующийЗапрос Тогда
		ПоместитьВоВременноеХранилище(ДействующиеЗапросы, Настройки.ДополнительныеСвойства.АдресДействующихЗапросов);
	КонецЕсли;
	
	БрендыМП = ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.БрендыМП); // СписокЗначений
	КатегорииМП = ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.КатегорииМП); // СписокЗначений
	
	ТекущаяДата = ТекущаяДатаСеанса();
	НачалоДня = НачалоДня(ТекущаяДата);
	НачалоГода = НачалоГода(ТекущаяДата);
	
	Для Каждого СтрокаЦены Из Строки Цикл
		ИдентификаторМП = СтрокаЦены.Значение.Данные.ИдентификаторМП;
		
		// 1. Ищем в данных текущей страницы
		ОписаниеЦеныМП = ДанныеТекущейСтраницы.Получить(ИдентификаторМП);
		
		// 2. Ищем в извлеченных из хранилища данных других страниц
		Если ОписаниеЦеныМП = Неопределено Тогда
			Для Каждого КлючЗначение Из СчитанныеДанныеКэша Цикл
				ОписаниеЦеныМП = КлючЗначение.Значение.Получить(ИдентификаторМП);
				Если ОписаниеЦеныМП <> Неопределено Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		// 3. Ищем среди хранилища адресов подходящую страницу и извлекаем из нее данные
		Если ОписаниеЦеныМП = Неопределено Тогда
			ЧастиКлюча = Новый Массив; // Массив Из Строка
			ЧастиКлюча.Добавить(СтрокаЦены.Значение.Данные.НаименованиеМП);
			ЧастиКлюча.Добавить(СтрокаЦены.Значение.Данные.ИдентификаторМП);
			
			ТекущийКлюч = СтрСоединить(ЧастиКлюча, "_");
			
			СтраницаДляТекущегоКлюча = Неопределено;
			
			Для Каждого КэшСтраницы Из Настройки.ДополнительныеСвойства.АдресаСтраницКэша Цикл
				Если Настройки.ДополнительныеСвойства.НаправлениеСортировкиКД = НаправлениеСортировкиКомпоновкиДанных.Возр Тогда
					Если ТекущийКлюч >= КэшСтраницы.Значение.ПервыйКлюч Тогда
						СтраницаДляТекущегоКлюча = КэшСтраницы;
					ИначеЕсли ТекущийКлюч < КэшСтраницы.Значение.ПервыйКлюч Тогда
						Прервать;
					КонецЕсли;
				Иначе
					Если ТекущийКлюч <= КэшСтраницы.Значение.ПоследнийКлюч Тогда
						СтраницаДляТекущегоКлюча = КэшСтраницы;
					ИначеЕсли ТекущийКлюч > КэшСтраницы.Значение.ПоследнийКлюч Тогда
						Прервать;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если ТекущаяСтраница <> СтраницаДляТекущегоКлюча И СтраницаДляТекущегоКлюча <> Неопределено Тогда
				Если СчитанныеДанныеКэша.Получить(СтраницаДляТекущегоКлюча.Значение.ПервыйКлюч) = Неопределено Тогда
					ДанныеКэшаСтраницаДляТекущегоКлюча = ПолучитьИзВременногоХранилища(СтраницаДляТекущегоКлюча.Значение.АдресСтраницы);
					СчитанныеДанныеКэша.Вставить(СтраницаДляТекущегоКлюча.Значение.ПервыйКлюч, ДанныеКэшаСтраницаДляТекущегоКлюча);
					ОписаниеЦеныМП = ДанныеКэшаСтраницаДляТекущегоКлюча.Получить(ИдентификаторМП);
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		// 4. В текущем кэше нет информации о номенклатуре
		Если ОписаниеЦеныМП = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		// 5. Выводим данные на экран
		ЗаполнитьЗначенияСвойств(СтрокаЦены.Значение.Данные, ОписаниеЦеныМП);
		
		Если ОписаниеЦеныМП.НаКарантине Тогда
			СтрокаЦены.Значение.Данные.МаркерСтатуса = МаркерыСтатусов.НаКарантине;
		ИначеЕсли Не ПустаяСтрока(ОписаниеЦеныМП.ТекстОшибки) Тогда
			СтрокаЦены.Значение.Данные.МаркерСтатуса = МаркерыСтатусов.Ошибка;
		Иначе
			СтрокаЦены.Значение.Данные.МаркерСтатуса = МаркерыСтатусов.БезОшибок;
		КонецЕсли;
		
		ЭлементБрендыМП = БрендыМП.НайтиПоЗначению(ОписаниеЦеныМП.ИдБрендаМП);
		Если ЭлементБрендыМП <> Неопределено Тогда
			СтрокаЦены.Значение.Данные.БрендМП = ЭлементБрендыМП.Представление;
		КонецЕсли;
		
		ЭлементКатегорииМП = КатегорииМП.НайтиПоЗначению(ОписаниеЦеныМП.ИдКатегорииМП);
		Если ЭлементКатегорииМП <> Неопределено Тогда
			СтрокаЦены.Значение.Данные.КатегорияМП = ЭлементКатегорииМП.Представление;
		КонецЕсли;
		
		Если СтрокаЦены.Значение.Данные.Основная Тогда
			Для Каждого ОформлениеЯчейки Из СтрокаЦены.Значение.Оформление Цикл
				ОформлениеЯчейки.Значение.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветТекстаФормы);
			КонецЦикла;
		КонецЕсли;
		
		Если СтрокаЦены.Значение.Данные.ДатаВыгрузкиЦен > Дата(1,1,1) Тогда
			
			Для Каждого ОформлениеЯчейки Из СтрокаЦены.Значение.Оформление Цикл
				Если ОформлениеЯчейки.Ключ = "ДатаВыгрузкиЦен" Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			Если СтрокаЦены.Значение.Данные.ДатаВыгрузкиЦен >= НачалоДня
				И НачалоГода(СтрокаЦены.Значение.Данные.ДатаВыгрузкиЦен) = НачалоГода Тогда
				ОформлениеЯчейки.Значение.УстановитьЗначениеПараметра("Формат", НСтр("ru = 'ДФ=ЧЧ:мм';
																					|en = 'DF=HH:mm tt'"));
			Иначе
				ОформлениеЯчейки.Значение.УстановитьЗначениеПараметра("Формат", "ДЛФ=D");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

// Получить настройки для отбора динамического списка.
// 
// Параметры:
//  НастройкиСКД - Структура:
//   * СхемаКомпоновкиДанных - СхемаКомпоновкиДанных
//   * Настройки - НастройкиКомпоновкиДанных
// 
// Возвращаемое значение:
//  Строка - Адрес временного хранилища с настройками
Функция ПолучитьНастройкиДляОтбораСпискаЦеныОстаткиТоваровМП(НастройкиСКД) Экспорт
	
	Настройки = НастройкиСКД.Настройки;
	
	// Настройка правил отбора для предопределенных полей динамического списка
	НаборДанных = НастройкиСКД.СхемаКомпоновкиДанных.НаборыДанных.НаборДанныхДинамическогоСписка;
	ИсключитьПолеИзДоступныхОтборов(НаборДанных, "ВидОбъектаМП");
	ИсключитьПолеИзДоступныхОтборов(НаборДанных, "Объект1С");
	ИсключитьПолеИзДоступныхОтборов(НаборДанных, "ИдентификаторВладельцаОбъектаМаркетплейса");
	ИсключитьПолеИзДоступныхОтборов(НаборДанных, "УчетнаяЗаписьМаркетплейса");
	ИсключитьПолеИзДоступныхОтборов(НаборДанных, "ИдентификаторМП");
	
	// Настройка правил отбора для полей, данные для которых берутся из сервиса интеграции
	Для Каждого ВычисляемоеПоле Из НастройкиСКД.СхемаКомпоновкиДанных.ВычисляемыеПоля Цикл
		Если ВычисляемоеПоле.ПутьКДанным = "БрендМП" Тогда
			ВычисляемоеПоле.УстановитьДоступныеЗначения(ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.БрендыМП));
		ИначеЕсли ВычисляемоеПоле.ПутьКДанным = "КатегорияМП" Тогда
			ВычисляемоеПоле.УстановитьДоступныеЗначения(ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.КатегорииМП));
		ИначеЕсли ВычисляемоеПоле.ПутьКДанным = "МаркерСтатуса" Тогда
			ВычисляемоеПоле.УстановитьДоступныеЗначения(ПолучитьИзВременногоХранилища(Настройки.ДополнительныеСвойства.МаркерыСтатусов));
		КонецЕсли;
	КонецЦикла;
	
	ПолеБрендМП = Новый ПолеКомпоновкиДанных("БрендМП");
	ПолеКатегорияМП = Новый ПолеКомпоновкиДанных("КатегорияМП");
	ПолеНаименованиеМП = Новый ПолеКомпоновкиДанных("НаименованиеМП");
	ПолеТолькоВНаличии = Новый ПолеКомпоновкиДанных("ТолькоВНаличии");
	ПолеМаркерСтатуса = Новый ПолеКомпоновкиДанных("МаркерСтатуса");
	
	ЭлементОтбораСКД_БрендМП = Неопределено;
	ЭлементОтбораСКД_КатегорияМП = Неопределено;
	ЭлементОтбораСКД_НаименованиеМП = Неопределено;
	ЭлементОтбораСКД_ТолькоВНаличии = Неопределено;
	ЭлементОтбораСКД_МаркерСтатуса = Неопределено;
	
	Для Каждого ЭлементОтбораСКД Из Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбораСКД) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбораСКД.ЛевоеЗначение = ПолеБрендМП Тогда
				ЭлементОтбораСКД_БрендМП = ЭлементОтбораСКД;
			ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеКатегорияМП Тогда
				ЭлементОтбораСКД_КатегорияМП = ЭлементОтбораСКД;
			ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеТолькоВНаличии Тогда
				ЭлементОтбораСКД_ТолькоВНаличии = ЭлементОтбораСКД;
			ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеНаименованиеМП Тогда
				ЭлементОтбораСКД_НаименованиеМП = ЭлементОтбораСКД;
			ИначеЕсли ЭлементОтбораСКД.ЛевоеЗначение = ПолеМаркерСтатуса Тогда
				ЭлементОтбораСКД_МаркерСтатуса = ЭлементОтбораСКД;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ЭлементОтбораСКД_БрендМП = Неопределено Тогда
		ЭлементОтбораСКД_БрендМП = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораСКД_БрендМП.ЛевоеЗначение = ПолеБрендМП;
		ЭлементОтбораСКД_БрендМП.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	КонецЕсли;
	ЭлементОтбораСКД_БрендМП.Использование = Настройки.ПараметрыДанных.Элементы.Найти("ИспользоватьБрендыМП").Значение;
	
	Если ЭлементОтбораСКД_КатегорияМП = Неопределено Тогда
		ЭлементОтбораСКД_КатегорияМП = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораСКД_КатегорияМП.ЛевоеЗначение = ПолеКатегорияМП;
		ЭлементОтбораСКД_КатегорияМП.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	КонецЕсли;
	ЭлементОтбораСКД_КатегорияМП.Использование = Настройки.ПараметрыДанных.Элементы.Найти("ИспользоватьКатегорииМП").Значение;
	
	Если ЭлементОтбораСКД_НаименованиеМП = Неопределено Тогда
		ЭлементОтбораСКД_НаименованиеМП = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораСКД_НаименованиеМП.ЛевоеЗначение = ПолеНаименованиеМП;
		ЭлементОтбораСКД_НаименованиеМП.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
	КонецЕсли;
	ЭлементОтбораСКД_НаименованиеМП.Использование = Настройки.ПараметрыДанных.Элементы.Найти("ИспользоватьНаименованиеМП").Значение;
	
	Если ЭлементОтбораСКД_ТолькоВНаличии = Неопределено Тогда
		ЭлементОтбораСКД_ТолькоВНаличии = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораСКД_ТолькоВНаличии.ЛевоеЗначение = ПолеТолькоВНаличии;
		ЭлементОтбораСКД_ТолькоВНаличии.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбораСКД_ТолькоВНаличии.ПравоеЗначение = Истина;
		ЭлементОтбораСКД_ТолькоВНаличии.Представление = НСтр("ru = 'Только в наличии';
															|en = 'Only in stock'");
	КонецЕсли;
	ЭлементОтбораСКД_ТолькоВНаличии.Использование = Настройки.ПараметрыДанных.Элементы.Найти("ИспользоватьТолькоВНаличии").Значение;
	
	Если ЭлементОтбораСКД_МаркерСтатуса = Неопределено Тогда
		ЭлементОтбораСКД_МаркерСтатуса = Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбораСКД_МаркерСтатуса.ЛевоеЗначение = ПолеМаркерСтатуса;
		ЭлементОтбораСКД_МаркерСтатуса.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	КонецЕсли;
	ЭлементОтбораСКД_МаркерСтатуса.Использование = Настройки.ПараметрыДанных.Элементы.Найти("ИспользоватьМаркерСтатуса").Значение;
	
	//@skip-check typed-value-adding-to-untyped-collection
	Для Каждого ДоступноеПолеОтбора Из Настройки.Отбор.ДоступныеПоляОтбора.Элементы Цикл
		Если ДоступноеПолеОтбора.Поле = ПолеБрендМП Тогда
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Очистить();
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
		ИначеЕсли ДоступноеПолеОтбора.Поле = ПолеКатегорияМП Тогда
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Очистить();
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.ВСписке);
		ИначеЕсли ДоступноеПолеОтбора.Поле = ПолеТолькоВНаличии Тогда
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Очистить();
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
		ИначеЕсли ДоступноеПолеОтбора.Поле = ПолеНаименованиеМП Тогда
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Очистить();
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Содержит);
		ИначеЕсли ДоступноеПолеОтбора.Поле = ПолеМаркерСтатуса Тогда
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Очистить();
			ДоступноеПолеОтбора.ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	КонецЦикла;
	
	АдресВХ = ПоместитьВоВременноеХранилище(НастройкиСКД);

	Возврат АдресВХ;
	
КонецФункции

Процедура ИсключитьПолеИзДоступныхОтборов(НаборДанных, ИмяПоля)
	
	ПолеНабораДанных = НаборДанных.Поля.Найти(ИмяПоля);
	Если ПолеНабораДанных = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ПолеНабораДанных.ОграничениеИспользования.Группировка = Истина;
	ПолеНабораДанных.ОграничениеИспользования.Поле = Истина;
	ПолеНабораДанных.ОграничениеИспользования.Порядок = Истина;
	ПолеНабораДанных.ОграничениеИспользования.Условие = Истина;
	
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Группировка = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Поле = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Порядок = Истина;
	ПолеНабораДанных.ОграничениеИспользованияРеквизитов.Условие = Истина;
	
КонецПроцедуры

// Сформировать параметры для открытия формы обработки ПрайсЛист.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись маркетплейса
//  ВыделенныеСтроки - Неопределено, Массив из Число - Выделенные строки
// 
// Возвращаемое значение:
//  Структура - Сформировать параметры формы прайс лист:
// * ВидыЦен - СписокЗначений из СправочникСсылка.ВидыЦен - список видов цен, используемых в маркетплейсе
// * Номенклатура - СписокЗначений из СправочникСсылка.Номенклатура - список номенклатуры, для которой необходимо рассчитать цены
Функция СформироватьПараметрыРедактированияПрайсЛиста(Знач УчетнаяЗапись, Знач ВыделенныеСтроки = Неопределено) Экспорт
	
	ПараметрыРедактированияПрайсЛиста = Новый Структура("ВидыЦен, Номенклатура", Новый СписокЗначений, Новый СписокЗначений);
	ВидыЦен = ПараметрыРедактированияПрайсЛиста.ВидыЦен; // СписокЗначений Из СправочникСсылка.ВидыЦен
	Номенклатура = ПараметрыРедактированияПрайсЛиста.Номенклатура; // СписокЗначений Из СправочникСсылка.Номенклатура
	
	// Определяем виды цен
	ВидыЦенУчетнойЗаписи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(УчетнаяЗапись, "ВидыЦен");	// РезультатЗапроса
	
	ВыборкаВидыЦен = ВидыЦенУчетнойЗаписи.Выбрать();
	Пока ВыборкаВидыЦен.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаВидыЦен.ВидЦены) Тогда
			ВидыЦен.Добавить(ВыборкаВидыЦен.ВидЦены);
		КонецЕсли;
	КонецЦикла;
	
	// Определяем номенклатуру
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Номенклатура1С.Ссылка КАК Номенклатура
	|ИЗ
	|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = СоответствияОбъектовМаркетплейсов.Объект1С)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура1С
	|		ПО (Номенклатура1С.Ссылка = НоменклатураКонтрагентов.Номенклатура)
	|ГДЕ
	|	СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса = &УчетнаяЗаписьМаркетплейса
	|	И СоответствияОбъектовМаркетплейсов.ВидОбъектаМаркетплейса = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовМаркетплейсов.Товар)
	|	И СоответствияОбъектовМаркетплейсов.ДатаАктуальности <> ДатаВремя(1,1,1,0,0,0)
	|	И (&ВсяНоменклатураМП
	|			ИЛИ СоответствияОбъектовМаркетплейсов.Объект1С В (&НоменклатураМП))";
	
	Запрос.УстановитьПараметр("УчетнаяЗаписьМаркетплейса", УчетнаяЗапись);
	
	НоменклатураМП = Новый Массив; // Массив Из СправочникСсылка.Номенклатура 
	
	Если ВыделенныеСтроки <> Неопределено Тогда
		Если ВыделенныеСтроки.Количество() < 5000 Тогда
			Для Каждого КлючЗаписи Из ВыделенныеСтроки Цикл
				НоменклатураМП.Добавить(КлючЗаписи.Объект1С);
			КонецЦикла;
		Иначе
			ВыделенныеСтроки = Неопределено;
			ТекстСообщения = НСтр("ru = 'Количество выделенных строк превысило 5000. Прайс-лист будет построен по всем номенклатурам, опубликованным на маркетплейсе';
									|en = 'The number of selected rows exceeded 5000. The price list will be based on all items published on the marketplace'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ВсяНоменклатураМП", ВыделенныеСтроки = Неопределено);
	Запрос.УстановитьПараметр("НоменклатураМП", НоменклатураМП);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			Номенклатура.Добавить(Выборка.Номенклатура);
		КонецЦикла;
	КонецЕсли;
	
	Возврат ПараметрыРедактированияПрайсЛиста;
	
КонецФункции

#КонецОбласти

#КонецОбласти

// Хранилище значения из JSON.
// 
// Параметры:
//  Значение - Строка - данные в формате json
// 
// Возвращаемое значение:
//  Строка, Произвольный - Хранилище значения из JSON
Функция ХранилищеЗначенияИзJSON(Знач Значение)
	
	Результат = "";
	Значение = СокрЛП(Значение);
	
	Если Значение <> "" Тогда
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(Значение);
		Попытка
			ЗначениеИзJSON = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON);
		Исключение
			Возврат Результат;
		КонецПопытки;
		
		ЧтениеJSON.Закрыть();
	
		Если ЗначениеЗаполнено(ЗначениеИзJSON) И ТипЗнч(ЗначениеИзJSON) = Тип("ХранилищеЗначения") Тогда
			Возврат ЗначениеИзJSON.Получить();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ПустаяДатаJSON()

	Возврат ЗаписатьДатуJSON('00010101', ФорматДатыJSON.ISO, ВариантЗаписиДатыJSON.УниверсальнаяДата);

КонецФункции

// Преобразует строку в дату.
// 
// Параметры:
//  СтрокаДанных - Строка - значение, преобразуемое в дату
// 
// Возвращаемое значение:
//  Дата - Строка в дату
Функция СтрокаВДату(СтрокаДанных)
	
	Результат = Дата(1,1,1);
	
	Если СтрокаДанных = ПустаяДатаJSON() Тогда
		Возврат Результат;
	КонецЕсли;
	
	Результат = ПрочитатьДатуJSON(СтрокаДанных, ФорматДатыJSON.ISO);
	
	Возврат Результат;
	
КонецФункции

// Преобразует строку в число
// 
// Параметры:
//  Значение - Произвольный - значение, преобразуемое в число
// 
// Возвращаемое значение:
//  Число, Неопределено, Произвольный - Строка в число
Функция СтрокаВЧисло(Значение)
	
	Если ТипЗнч(Значение) = Тип("Число") Тогда
		Результат = Значение;
	ИначеЕсли ТипЗнч(Значение) = Тип("Строка") Тогда
		НовоеЗначение = СтроковыеФункцииКлиентСервер.СтрокаВЧисло(Значение);
		Если НовоеЗначение = Неопределено Тогда
			Результат = 0;
		Иначе
			Результат = НовоеЗначение;
		КонецЕсли;
		
	Иначе
		Результат = 0;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Доступные адреса.
// 
// Возвращаемое значение:
//  Массив из См. НовыеПараметрыДоступногоАдреса - Доступные адреса
Функция ДоступныеАдреса()
	
	ДоступныеАдреса = Новый Массив; // Массив из См. НовыеПараметрыДоступногоАдреса
	
	ПараметрыДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
	ПараметрыДоступногоАдреса.Текущий = Истина;
	ПараметрыДоступногоАдреса.Адрес = "https://app-115216.1cmycloud.com/applications/mp-gate-prod-1";
	ДоступныеАдреса.Добавить(ПараметрыДоступногоАдреса);
	
	ПараметрыДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
	ПараметрыДоступногоАдреса.Адрес = "https://app-353566.1cmycloud.com/applications/mp-gate-prod-2";
	ДоступныеАдреса.Добавить(ПараметрыДоступногоАдреса);
	
	ПараметрыДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
	ПараметрыДоступногоАдреса.Адрес = "https://app-697825.1cmycloud.com/applications/mp-gate-prod-3";
	ДоступныеАдреса.Добавить(ПараметрыДоступногоАдреса);
	
	ПараметрыДоступногоАдреса = НовыеПараметрыДоступногоАдреса();
	ПараметрыДоступногоАдреса.Адрес = "https://app-775973.1cmycloud.com/applications/mp-gate-prod-4";
	ДоступныеАдреса.Добавить(ПараметрыДоступногоАдреса);
	
	Возврат ДоступныеАдреса;
	
КонецФункции

// Метод подписки на событие ПередЗаписьюМаркетплейсыСервисИнтеграции
// 
// Параметры:
//  Источник - СправочникОбъект - Источник события
//  Отказ - Булево - Отказ
Процедура ПередЗаписьюСервисИнтеграции(Источник, Отказ) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСWildberries")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСLamoda") Тогда
		ЗарегистрироватьИзменения(Источник, Отказ);
	КонецЕсли;
	
КонецПроцедуры

// Метод подписки на событие МаркетплейсыРегистрацияНабора
// 
// Параметры:
//  Источник - СправочникОбъект - Источник события
//  Отказ - Булево - Отказ
//  Замещение - Булево - используется режим замещения при записи регистра сведений
Процедура ПередЗаписьюРегистровМаркетплейсыСервисИнтеграцииПередЗаписью(Источник, Отказ, Замещение) Экспорт
	
	Если Источник.ОбменДанными.Загрузка = Истина Тогда
		Возврат;
	КонецЕсли;
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСWildberries")
		Или ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСLamoda") Тогда
		ЗарегистрироватьИзменения(Источник, Отказ, Замещение);
	КонецЕсли;
	
КонецПроцедуры

// Метод подписки на событие ПередЗаписьюМаркетплейсыСервисИнтеграции
// 
// Параметры:
//  Источник - СправочникОбъект - Источник события
//  Отказ - Булево - Отказ
//  Замещение - Булево - используется режим замещения при записи регистра сведений
Процедура ЗарегистрироватьИзменения(Источник, Отказ, Замещение = Ложь)

	Если ТипЗнч(Источник) = Тип("СправочникОбъект.Номенклатура") И Не Источник.Ссылка.Пустая() Тогда

		// При расчете остатков для выгрузки на торговую площадку учитываются ограничения по виду номенклатуры и товарной категории
		// Если остаток на складе по номенклатуре долго не менялся, а в карточке номенклатуры произошли изменения,
		// то понадобится пересчитать остаток  для выгрузки на торговую площадку.
		// Для этого сначала регистрируем номенклатуру контрагента на узле, а потом регламентным заданием скорректируем регистр сведений
		// "Остатки товаров для маркетплейсов"
		
		СтарыеЗначения =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка,
			"ВидНоменклатуры, ТоварнаяКатегория");	// Структура

		Если СтарыеЗначения.ВидНоменклатуры <> Источник.ВидНоменклатуры
			Или СтарыеЗначения.ТоварнаяКатегория <> Источник.ТоварнаяКатегория Тогда
			
			ВидыМаркетплейсов = Новый Массив; //  Массив Из ПеречислениеСсылка.ВидыМаркетплейсов
			ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсLamoda);
			ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries);
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	УчетныеЗаписи.Ссылка КАК УчетнаяЗапись,
				|	УзлыОбмена.Ссылка КАК Узел
				|ПОМЕСТИТЬ врУзлыОбмена
				|ИЗ
				|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК УзлыОбмена
				|		ПО (УзлыОбмена.УчетнаяЗапись = УчетныеЗаписи.Ссылка)
				|			И (НЕ УзлыОбмена.ЭтотУзел)
				|			И (НЕ УзлыОбмена.ПометкаУдаления)
				|ГДЕ
				|	УчетныеЗаписи.ВидМаркетплейса В (&ВидыМаркетплейсов)
				|	И НЕ УчетныеЗаписи.ПометкаУдаления
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	ВЫРАЗИТЬ(СоответствияОбъектовМаркетплейсов.Объект1С КАК Справочник.НоменклатураКонтрагентов) КАК НоменклатураКонтрагента,
				|	УзлыОбмена.Узел КАК Узел
				|ИЗ
				|	РегистрСведений.НоменклатураКонтрагентовБЭД КАК НоменклатураКонтрагентовБЭД
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
				|		ПО НоменклатураКонтрагентовБЭД.Идентификатор = СоответствияОбъектовМаркетплейсов.ИдентификаторОбъектаМаркетплейса
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врУзлыОбмена КАК УзлыОбмена
				|		ПО (УзлыОбмена.УчетнаяЗапись = СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса)
				|ГДЕ
				|	НоменклатураКонтрагентовБЭД.Номенклатура = &Номенклатура";
			
			Запрос.УстановитьПараметр("ВидыМаркетплейсов", ВидыМаркетплейсов);
			Запрос.УстановитьПараметр("Номенклатура", Источник.Ссылка);

			Выборка = Запрос.Выполнить().Выбрать();
			Пока Выборка.Следующий() Цикл
				ПланыОбмена.ЗарегистрироватьИзменения(Выборка.Узел, Выборка.НоменклатураКонтрагента);
			КонецЦикла;

		КонецЕсли;

	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.НоменклатураКонтрагентов") И Не Источник.Ссылка.Пустая() Тогда
		
		СтарыеЗначения =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка,
			"Номенклатура, Характеристика, Упаковка, КоличествоБазовойЕдиницыИзмерения, КоличествоУпаковок");

		Если СтарыеЗначения.Номенклатура <> Источник.Номенклатура
			Или СтарыеЗначения.Характеристика <> Источник.Характеристика
			Или СтарыеЗначения.Характеристика <> Источник.Характеристика
			Или СтарыеЗначения.Упаковка <> Источник.Упаковка
			Или СтарыеЗначения.КоличествоБазовойЕдиницыИзмерения <> Источник.КоличествоБазовойЕдиницыИзмерения
			Или СтарыеЗначения.КоличествоУпаковок <> Источник.КоличествоУпаковок Тогда

			ВидыМаркетплейсов = Новый Массив; // Массив Из ПеречислениеСсылка.ВидыМаркетплейсов
			Если Источник.ДополнительныеСвойства.Свойство("ВидМаркетплейса") Тогда
				ВидыМаркетплейсов.Добавить(Источник.ДополнительныеСвойства.ВидМаркетплейса);
			Иначе
				ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсLamoda);
				ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries);
			КонецЕсли;

			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	УчетныеЗаписи.Ссылка КАК УчетнаяЗапись,
				|	УзлыОбмена.Ссылка КАК Узел
				|ПОМЕСТИТЬ врУзлыОбмена
				|ИЗ
				|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК УзлыОбмена
				|		ПО (УзлыОбмена.УчетнаяЗапись = УчетныеЗаписи.Ссылка)
				|			И (НЕ УзлыОбмена.ЭтотУзел)
				|			И (НЕ УзлыОбмена.ПометкаУдаления)
				|ГДЕ
				|	УчетныеЗаписи.ВидМаркетплейса В (&ВидыМаркетплейсов)
				|	И НЕ УчетныеЗаписи.ПометкаУдаления
				|;
				|
				|////////////////////////////////////////////////////////////////////////////////
				|ВЫБРАТЬ
				|	УзлыОбмена.Узел КАК Узел
				|ИЗ
				|	РегистрСведений.СоответствияОбъектовМаркетплейсов КАК СоответствияОбъектовМаркетплейсов
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ врУзлыОбмена КАК УзлыОбмена
				|		ПО (УзлыОбмена.УчетнаяЗапись = СоответствияОбъектовМаркетплейсов.УчетнаяЗаписьМаркетплейса)
				|ГДЕ
				|	(ВЫРАЗИТЬ(СоответствияОбъектовМаркетплейсов.Объект1С КАК Справочник.НоменклатураКонтрагентов)) = &НоменклатураКонтрагента";

			Запрос.УстановитьПараметр("ВидыМаркетплейсов", ВидыМаркетплейсов);
			Запрос.УстановитьПараметр("НоменклатураКонтрагента", Источник.Ссылка);

			РезультатЗапроса = Запрос.Выполнить();

			Если Не РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Источник.ОбменДанными.Получатели.Добавить(Выборка.Узел);
				КонецЦикла;
			КонецЕсли;

		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("СправочникОбъект.Склады") Тогда
		
		Зарегистрировать = Ложь;
		Если Источник.Ссылка.Пустая() Тогда
			Зарегистрировать = Истина;
		Иначе
			СтарыеЗначения =  ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Источник.Ссылка, "Родитель");
			Зарегистрировать = СтарыеЗначения.Родитель <> Источник.Родитель;
		КонецЕсли;

		Если Зарегистрировать Тогда
			
			ВидыМаркетплейсов = Новый Массив; // Массив Из ПеречислениеСсылка.ВидыМаркетплейсов
			ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсLamoda);
			ВидыМаркетплейсов.Добавить(Перечисления.ВидыМаркетплейсов.МаркетплейсWildberries);
			
			Запрос = Новый Запрос;
			Запрос.Текст =
				"ВЫБРАТЬ
				|	УзлыОбмена.Ссылка КАК Узел
				|ИЗ
				|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
				|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК УзлыОбмена
				|		ПО (УзлыОбмена.УчетнаяЗапись = УчетныеЗаписи.Ссылка)
				|			И (НЕ УзлыОбмена.ЭтотУзел)
				|			И (НЕ УзлыОбмена.ПометкаУдаления)
				|ГДЕ
				|	УчетныеЗаписи.ВидМаркетплейса В (&ВидыМаркетплейсов)
				|	И НЕ УчетныеЗаписи.ПометкаУдаления";

			Запрос.УстановитьПараметр("ВидыМаркетплейсов", ВидыМаркетплейсов);

			РезультатЗапроса = Запрос.Выполнить();

			Если Не РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Источник.ОбменДанными.Получатели.Добавить(Выборка.Узел);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ЦеныНоменклатуры") 
		Или ТипЗнч(Источник) = Тип("РегистрСведенийНаборЗаписей.ЦеныНоменклатуры25") Тогда
		
			// Регистрируем на всех узлах планах обмена без разбора.
			Запрос = Новый Запрос;
			Запрос.Текст =
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	УчетныеЗаписиМаркетплейсов.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ ВТ_УчетныеЗаписиПоВидамЦен
			|ИЗ
			|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписиМаркетплейсов
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.УчетныеЗаписиМаркетплейсов.ВидыЦен КАК УчетныеЗаписиМаркетплейсовВидыЦен
			|		ПО УчетныеЗаписиМаркетплейсовВидыЦен.Ссылка = УчетныеЗаписиМаркетплейсов.Ссылка
			|		И УчетныеЗаписиМаркетплейсовВидыЦен.ВидЦены В (&ВидыЦенИсточника)
			|		И НЕ УчетныеЗаписиМаркетплейсов.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	УзлыОбмена.Ссылка КАК Узел
			|ИЗ
			|	ВТ_УчетныеЗаписиПоВидамЦен КАК УчетныеЗаписи
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК УзлыОбмена
			|		ПО УзлыОбмена.УчетнаяЗапись = УчетныеЗаписи.Ссылка
			|		И НЕ УзлыОбмена.ЭтотУзел
			|		И НЕ УзлыОбмена.ПометкаУдаления";

			Запрос.УстановитьПараметр("ВидыЦенИсточника", Источник.ВыгрузитьКолонку("ВидЦены"));

			РезультатЗапроса = Запрос.Выполнить();

			Если Не РезультатЗапроса.Пустой() Тогда
				Выборка = РезультатЗапроса.Выбрать();
				Пока Выборка.Следующий() Цикл
					Источник.ОбменДанными.Получатели.Добавить(Выборка.Узел);
				КонецЦикла;
			КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Функция ИнтеграцияСМаркетплейсомОтключена(ВидМаркетплейса)
	Возврат ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) 
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСWildberries") 
		Или ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса)
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьИнтеграциюСLamoda");
КонецФункции

// 
// Параметры:
//  НастройкиУчетнойЗаписи - Структура - Настройки учетной записи:
//  ТипыЦен - ТаблицаЗначений - см. ИнтеграцияСМаркетплейсомLamoda.ТипыЦенLamoda
// 
// Возвращаемое значение:
//  Булево - Настройки вида цен не заполнены
Функция НастройкиВидаЦенНеЗаполнены(НастройкиУчетнойЗаписи, ТипыЦен)

	Результат = Истина;
	Для Каждого ТипЦены Из ТипыЦен Цикл
		Результат = Результат И НастройкиУчетнойЗаписи.ВидыЦен[ТипЦены.Идентификатор].Пустая();	
	КонецЦикла;

	Возврат Результат;

КонецФункции

// Типы цен по виду маркетплейса.
// 
// Параметры:
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
// 
// Возвращаемое значение:
//  ТаблицаЗначений - см. ИнтеграцияСМаркетплейсомLamoda.ТипыЦенLamoda 
Функция ТипыЦенПоВидуМаркетплейса(ВидМаркетплейса)
	Результат = НовыйТаблицаТипыЦен();
	
	Если ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоLamoda(ВидМаркетплейса) Тогда
		Результат = ИнтеграцияСМаркетплейсомLamoda.ТипыЦенLamoda();
	ИначеЕсли ИнтеграцияСМаркетплейсамиКлиентСервер.ЭтоWildberries(ВидМаркетплейса) Тогда
		Результат = ИнтеграцияСМаркетплейсомWildberries.ТипыЦенWildberries();
	Иначе
		Возврат Результат;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

// Обновить номенклатуру в сервисе интеграции.
// 
// Параметры:
//  УчетнаяЗапись - СправочникСсылка.УчетныеЗаписиМаркетплейсов - Учетная запись
//  ВидМаркетплейса - ПеречислениеСсылка.ВидыМаркетплейсов
// 
// Возвращаемое значение:
//  Булево - Обновить номенклатуру в сервисе интеграции
Функция ОбновитьНоменклатуруВСервисеИнтеграции(УчетнаяЗапись, ВидМаркетплейса)
	
	РезультатОбновления = Истина;
	
	Запрос1 = Новый Запрос;
	Запрос1.Текст =
	"ВЫБРАТЬ
	|	Узлы.Ссылка КАК Узел
	|ПОМЕСТИТЬ врУзлы
	|ИЗ
	|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК Узлы
	|		ПО УчетныеЗаписи.Ссылка = Узлы.УчетнаяЗапись
	|			И (НЕ Узлы.ЭтотУзел)
	|			И (НЕ Узлы.ПометкаУдаления)
	|ГДЕ
	|	&ВсеУчетныеЗаписи
	|	И УчетныеЗаписи.ВидМаркетплейса = &ВидМаркетплейса
	|	И НЕ УчетныеЗаписи.ПометкаУдаления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Узлы.Ссылка
	|ИЗ
	|	Справочник.УчетныеЗаписиМаркетплейсов КАК УчетныеЗаписи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПланОбмена.МаркетплейсыСИ КАК Узлы
	|		ПО УчетныеЗаписи.Ссылка = Узлы.УчетнаяЗапись
	|ГДЕ
	|	УчетныеЗаписи.Ссылка = &УчетнаяЗапись
	|	И УчетныеЗаписи.ВидМаркетплейса = &ВидМаркетплейса
	|	И НЕ УчетныеЗаписи.ПометкаУдаления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ДанныеУзла.Ссылка КАК НоменклатураКонтрагента,
	|	ДанныеУзла.Ссылка.Идентификатор КАК ИдентификаторНоменклатурыСервиса,
	|	ДанныеУзла.Узел КАК Узел,
	|	ВЫБОР
	|		КОГДА НоменклатураКонтрагентов.Ссылка ЕСТЬ NULL
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Выгрузить
	|ИЗ
	|	Справочник.НоменклатураКонтрагентов.Изменения КАК ДанныеУзла
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.НоменклатураКонтрагентов КАК НоменклатураКонтрагентов
	|		ПО (НоменклатураКонтрагентов.Ссылка = ДанныеУзла.Ссылка)
	|ГДЕ
	|	ДанныеУзла.Узел В
	|			(ВЫБРАТЬ
	|				врУзлы.Узел
	|			ИЗ
	|				врУзлы КАК врУзлы)
	|ИТОГИ ПО
	|	Узел";
	
	Запрос1.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос1.УстановитьПараметр("ВидМаркетплейса", ВидМаркетплейса);
	Запрос1.УстановитьПараметр("ВсеУчетныеЗаписи", УчетнаяЗапись = Неопределено);
	
	РезультатЗапроса1 = Запрос1.Выполнить();
	
	//@skip-check query-in-loop - Порционная выгрузка
	Пока Не РезультатЗапроса1.Пустой() Цикл
		
		ПараметрыМетода = НовыйПараметрыЗапросаОбновитьНоменклатуруМаркетплейса();
		
		ПараметрыМетода.УчетнаяЗапись = УчетнаяЗапись;
		ПараметрыМетода.ВидМаркетплейса = ВидМаркетплейса;
		ПараметрыМетода.ИмяМетода = ИнтеграцияСМаркетплейсамиСИКлиентСервер.МетодОбновитьНоменклатуруМаркетплейса();
		НоменклатураМаркетплейса = ПараметрыМетода.НоменклатураМаркетплейса;
		
		ВыборкаУзел = РезультатЗапроса1.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		Пока ВыборкаУзел.Следующий() Цикл
			
			НоменклатураМаркетплейса.Очистить();
			ОбрабатываемыеЗаписи = Новый Массив; // Массив из СправочникСсылка.НоменклатураКонтрагентов
			
			Выборка = ВыборкаУзел.Выбрать();
			Пока Выборка.Следующий() Цикл
				Если Выборка.Выгрузить Тогда
					Карточка = Новый Структура("ИдентификаторНоменклатурыСервиса, НоменклатураКонтрагента", "","");
					ЗаполнитьЗначенияСвойств(Карточка, Выборка);
					НоменклатураМаркетплейса.Добавить(Карточка);
				КонецЕсли;
				ОбрабатываемыеЗаписи.Добавить(Выборка.НоменклатураКонтрагента);
			КонецЦикла;
			
			РезультатОперации = ОбновитьНоменклатуруМаркетплейса(ПараметрыМетода);
			
			Если РезультатОперации = Неопределено Тогда
				Возврат РезультатОбновления;
			КонецЕсли;
			
			Если РезультатОперации.КодСостояния <> 200 Тогда
				Для Каждого Ошибка Из РезультатОперации.Ошибки Цикл
					ОбщегоНазначения.СообщитьПользователю(Ошибка);
				КонецЦикла;
				Возврат Ложь;
			КонецЕсли;
			
			// Очистка регистрации на плане обмена
			ПланыОбмена.УдалитьРегистрациюИзменений(ВыборкаУзел.Узел, РезультатОперации.НоменклатураКонтрагентов);
		КонецЦикла;
		
		РезультатЗапроса1 = Запрос1.Выполнить();
		
	КонецЦикла;
	
	Возврат РезультатОбновления;
	
КонецФункции

// Создает или обновляет элемент справочника НоменклатураКонтрагентов по данным выборки запроса к СИ и 1С.
// 
// Параметры:
//  Выборка - ВыборкаИзРезультатаЗапроса - данные выборки 
//  ПараметрыВыборки - Структура - параметры запроса, по которым получена выборка:
//   * КодСоздать - Число
//   * КодОбновить - Число
//   * КодСоздатьСоответствие - Число
//   * Партнер - СправочникСсылка.Партнеры
//  РезультатОбработки - Структура - результат обработки, содержит ключ:
// * Ошибки - Массив из Строка - 
// 
// Возвращаемое значение:
//  Неопределено, СправочникСсылка.НоменклатураКонтрагентов - Неопределено, если не удалось заблокировать существующую карточку
Функция СоздатьОбновитьКарточкуНоменклатурыКонтрагентов(Выборка, ПараметрыВыборки, РезультатОбработки)
	
	Результат = Неопределено;
	
	Если Выборка.Действие = ПараметрыВыборки.КодСоздать Тогда

		Карточка = Справочники.НоменклатураКонтрагентов.СоздатьЭлемент();
		Карточка.Владелец = ПараметрыВыборки.Партнер;
		Карточка.ВладелецНоменклатуры = ПараметрыВыборки.Партнер;
		Карточка.КоличествоБазовойЕдиницыИзмерения = 1;

	ИначеЕсли Выборка.Действие = ПараметрыВыборки.КодОбновить Тогда

		Карточка = Выборка.НоменклатураКонтрагента.ПолучитьОбъект(); // СправочникОбъект.НоменклатураКонтрагентов
		Попытка
			Карточка.Заблокировать();
		Исключение
			ТекстОшибки = НСтр("ru = 'Не удалось обновить данные.';
								|en = 'Cannot update the data.'");
			РезультатОбработки.Ошибки.Добавить(ТекстОшибки);

			ЗаписьЖурналаРегистрации(СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка, ,
				Выборка.НоменклатураКонтрагента, ТекстОшибки);
			Возврат Результат;
		КонецПопытки;
	КонецЕсли;

	Если Выборка.Действие = ПараметрыВыборки.КодСоздатьСоответствие Тогда
		Результат = Выборка.НоменклатураКонтрагента; // СправочникСсылка.НоменклатураКонтрагентов
	Иначе
		ЗаполнитьЗначенияСвойств(Карточка, Выборка);
		Карточка.Записать();
		Результат = Карточка.Ссылка;
	КонецЕсли;

	Возврат Результат;
КонецФункции

#Область КонструкторыТелЗапросов

// Конструктор элемента тела запроса выгрузить остатки товаров.
// 
// Возвращаемое значение:
//  Структура - атрибуты элемента тела запроса:
// * baseOfficeId - Строка - Идентификатор склада 1С
// * baseStockId - Строка - Идентификатор товара 1С
// * baseStockCharacteristicId - Строка - Идентификатор характеристики товара 1С
// * quantity - Число - Остаток товара на складе 1С
Функция НовыйАтрибутыЭлементаТелаЗапросаВыгрузитьОстаткиТоваров()
	
	Результат = Новый Структура;
	Результат.Вставить("baseOfficeId", "");
	Результат.Вставить("baseStockId", "");
	Результат.Вставить("baseStockCharacteristicId", "");
	Результат.Вставить("quantity", 0);
		
	Возврат Результат;
	
КонецФункции

// Новый параметры тела запроса выгрузить склады.
// 
// Возвращаемое значение:
//  Структура - Новый параметры тела запроса выгрузить склады:
// * baseOfficeId - Строка - Идентификатор склада 1С
// * parentId - Строка - Идентификатор родителя склада 1С
// * name - Строка - Наименование склада 1С
// * isFolder - Булево - признак того, что склад является группой
Функция НовыйАтрибутыЭлементаТелаЗапросаВыгрузитьСклады()
	
	Результат = Новый Структура;
	Результат.Вставить("baseOfficeId", "");
	Результат.Вставить("parentId", "");
	Результат.Вставить("name", "");
	Результат.Вставить("isFolder", Ложь);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти