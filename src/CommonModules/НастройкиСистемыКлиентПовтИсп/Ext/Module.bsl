////////////////////////////////////////////////////////////////////////////////
// Клиентские процедуры и функции (кэшируемые), управляющие настройками системы.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Свойства формы.
// 
// Параметры:
//  ИмяФормы - Строка - Имя формы
// 
// Возвращаемое значение:
//  см. НастройкиСистемыКлиент.КонструкторСвойстваФормы
Функция СвойстваФормы(ИмяФормы) Экспорт
	
	//@skip-check use-non-recommended-method
	Форма = ПолучитьФорму(ИмяФормы);
	СвойстваФормы = НастройкиСистемыКлиент.КонструкторСвойстваФормы();
	
	Если Форма = Неопределено Тогда
		Возврат СвойстваФормы;
	КонецЕсли;
	
	ЦветПоясняюшийТекст = ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("ПоясняющийТекст");
	
	Элементы = СвойстваФормы.Элементы;
	СвойстваФормы.ИмяФормыСтрокой = Новый ФорматированнаяСтрока(Форма.ИмяФормы,, ОбщегоНазначенияКлиентПовтИсп.ЦветСтиля("НедоступныеДанныеЦвет"));
	
	Для каждого ЭлементФормы Из Форма.Элементы Цикл
		НастройкиСистемыКлиент.ДобавитьЭлементФормы(Элементы, ЭлементФормы, ЦветПоясняюшийТекст);
	КонецЦикла;
	
	Возврат СвойстваФормы
КонецФункции

// Элементы формы по строке.
// 
// Параметры:
//  СтрокаПоиска - Строка - Строка поиска
//  ИмяФормы - Строка -Имя формы
// 
// Возвращаемое значение:
//  см. НастройкиСистемыКлиент.НайтиЭлементыПоСтроке
Функция ЭлементыФормыПоСтроке(СтрокаПоиска, ИмяФормы) Экспорт
	
	Форма = НастройкиСистемыКлиентПовтИсп.СвойстваФормы(ИмяФормы);
	Результат = Новый Массив();
	
	Если НЕ ЗначениеЗаполнено(Форма.ИмяФормыСтрокой) Тогда
		Возврат Результат;
	КонецЕсли;
	
	ИндексПоиска = НастройкиСистемыКлиент.ДополнительныйИндексПоискаНастроек().Получить(ИмяФормы);
	Результат = НастройкиСистемыКлиент.НайтиЭлементыПоСтроке(СтрокаПоиска, Форма.Элементы, Форма.ИмяФормыСтрокой, ИндексПоиска);
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти
