
#Область ПрограммныйИнтерфейс

// Распределение срочных бонусных баллов регламентное задание.
// 
Процедура РаспределениеСрочныхБонусныхБалловРегламентноеЗадание() Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.РаспределениеСрочныхБонусныхБаллов);

	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") Тогда
		Возврат
	КонецЕсли;
		
	УстановитьПривилегированныйРежим(Истина);
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
									|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Начато регламентное распределение срочных бонусных баллов.';
			|en = 'Scheduled allocation of fixed-term bonus points has started.'"));
	
	ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5 = Константы.ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5.Получить();
	
	РаспределениеУспешно = ОбработатьВозвратыНачисленийБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5);
	
	Если РаспределениеУспешно Тогда
		РаспределениеУспешно = ОбработатьВозвратыОплатыБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5);
	КонецЕсли;
	
	Если РаспределениеУспешно Тогда
		РаспределениеУспешно = ОбработатьОплатыИНачисленияСрочныхБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5);
	КонецЕсли;
	
	Если РаспределениеУспешно Тогда
		ОбработатьПрекратившиеДействияНачисленияСрочныхБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5);		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
									|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
		УровеньЖурналаРегистрации.Информация, , ,
		НСтр("ru = 'Закончено регламентное распределение срочных бонусных баллов.';
			|en = 'Scheduled allocation of fixed-term bonus points is completed.'"));
	
КонецПроцедуры

// Формирует список шаблонов заданий очереди.
//
// Параметры:
//  СоответствиеИменПсевдонимам - см. ОчередьЗаданийПереопределяемый.ПриОпределенииПсевдонимовОбработчиков
//
Процедура ПриОпределенииПсевдонимовОбработчиков(СоответствиеИменПсевдонимам) Экспорт
	
	СоответствиеИменПсевдонимам.Вставить(Метаданные.РегламентныеЗадания.РаспределениеСрочныхБонусныхБаллов.ИмяМетода);
	
КонецПроцедуры

// Требуется добавить движения по регистру Бонусные баллы
// Параметры:
//	ДокументСсылка - ДокументСсылка - ссылка, значения реквизитов которого необходимо получить.
//
// Возвращаемое значение:
// Булево - признак добавить движения по регистру Бонусные баллы.
//
Функция ТребуетсяДобавитьДвиженияПоРегиструБонусныеБаллы(ДокументСсылка) Экспорт
	
	ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5 = Константы.ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5.Получить();
	
	РеквизитДокументаДата = "Дата";
	ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, РеквизитДокументаДата);
	
	ЭтоМеханизмБонусныхБаллов_2_5 = Ложь;
	Если НачалоДня(ДатаДокумента) >= НачалоДня(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5) Тогда
		ЭтоМеханизмБонусныхБаллов_2_5 = Истина;
	КонецЕсли;
	
	Если (ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ВозвратТоваровОтКлиента") 
		ИЛИ ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.ЗаявкаНаВозвратТоваровОтКлиента"))
			И ЭтоМеханизмБонусныхБаллов_2_5 Тогда
		РТУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументРеализации");
		Если ЗначениеЗаполнено(РТУ) Тогда
			ДатаДокументаРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РТУ, "Дата");
			Если НачалоДня(ДатаДокументаРеализации)< НачалоДня(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5) Тогда
				ЭтоМеханизмБонусныхБаллов_2_5 = Ложь;
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ДокументСсылка) = Тип("ДокументСсылка.КорректировкаРеализации") 
			И ЭтоМеханизмБонусныхБаллов_2_5 Тогда
		РТУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылка, "ДокументОснование");
		Если ЗначениеЗаполнено(РТУ) Тогда
			ДатаДокументаРеализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РТУ, "Дата");
			Если НачалоДня(ДатаДокументаРеализации)< НачалоДня(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5) Тогда
				ЭтоМеханизмБонусныхБаллов_2_5 = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат ЭтоМеханизмБонусныхБаллов_2_5;
	
КонецФункции

// Запускает автоматическое начисление и списание баллов и контролирует результат.
// 
// Параметры:
//  ПравилоНачисления - СправочникСсылка.ПравилаНачисленияИСписанияБонусныхБаллов - правило начисления баллов.
//
Процедура ВыполнитьАвтоматическоеНачислениеИСписаниеРегламентноеЗадание(ПравилоНачисления) Экспорт
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.АвтоматическоеНачислениеИСписаниеБонусныхБаллов);
	
	Если ПустаяСтрока(ИмяПользователя()) Тогда
		УстановитьПривилегированныйРежим(Истина);
	КонецЕсли;
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьБонусныеПрограммыЛояльности") 
		Или Не ЗначениеЗаполнено(ПравилоНачисления) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЖурнала = Новый Структура("ГруппаСобытий, Метаданные, Данные");
	ПараметрыЖурнала.ГруппаСобытий = НСтр("ru = 'Автоматическое начисление и списание бонусных баллов. Запуск по расписанию';
											|en = 'Automatic accumulation and write-off of bonus points. Run on schedule'");
	ПараметрыЖурнала.Метаданные    = ПравилоНачисления.Метаданные();
	ПараметрыЖурнала.Данные        = ПравилоНачисления;
	
	СегментыСервер.ЗаписьЖурнала(ПараметрыЖурнала, УровеньЖурналаРегистрации.Примечание, "", НСтр("ru = 'Запуск';
																									|en = 'Start'"));
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		ПравилоНачисления,
		"ПометкаУдаления, Наименование");
	
	// Проверки
	Если Реквизиты.ПометкаУдаления Тогда
		СегментыСервер.ЗаписьЖурнала(ПараметрыЖурнала, УровеньЖурналаРегистрации.Ошибка, "",
			НСтр("ru = 'Завершение';
				|en = 'End'"), НСтр("ru = 'Элемент автоматического начисления и списания бонусных баллов помечен на удаление';
											|en = 'Item of auto accumulation or write-off of bonus points is marked for deletion'"));
		Возврат;
	КонецЕсли;
	
	БонусныеБаллыСервер.ВыполнитьАвтоматическоеНачислениеИСписание(ПравилоНачисления);
	СегментыСервер.ЗаписьЖурнала(ПараметрыЖурнала, УровеньЖурналаРегистрации.Примечание, "",НСтр("ru = 'Завершение';
																								|en = 'End'"));
	
КонецПроцедуры

// Выполняет автоматическое начисление и списание бонусных
// баллов по правилу начисления бонусных баллов.
// 
// Параметры:
//  ПравилоНачисления - СправочникСсылка.ПравилаНачисленияИСписанияБонусныхБаллов - правило начисления.
//
Процедура ВыполнитьАвтоматическоеНачислениеИСписание(ПравилоНачисления) Экспорт
	
	ДатаНачисления = ТекущаяДатаСеанса();
	
	ТаблицаНачислениеИСписание = ТаблицаНачислениеИСписание(ПравилоНачисления, ДатаНачисления);

	Если ТаблицаНачислениеИСписание.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ПравилаНачисленияИСписанияБонусныхБаллов.ПериодДействия КАК ПериодДействия,
	|	ПравилаНачисленияИСписанияБонусныхБаллов.КоличествоПериодовДействия КАК КоличествоПериодовДействия,
	|	ПравилаНачисленияИСписанияБонусныхБаллов.КоличествоПериодовОтсрочкиНачалаДействия КАК КоличествоПериодовОтсрочкиНачалаДействия,
	|	ПравилаНачисленияИСписанияБонусныхБаллов.ПериодОтсрочкиНачалаДействия КАК ПериодОтсрочкиНачалаДействия,
	|	ПравилаНачисленияИСписанияБонусныхБаллов.ВидПравила КАК ВидПравила,
	|	ПравилаНачисленияИСписанияБонусныхБаллов.Владелец КАК БонуснаяПрограммаЛояльности
	|ИЗ
	|	Справочник.ПравилаНачисленияИСписанияБонусныхБаллов КАК ПравилаНачисленияИСписанияБонусныхБаллов
	|ГДЕ
	|	ПравилаНачисленияИСписанияБонусныхБаллов.Ссылка = &ПравилоНачисления");
	
	Запрос.УстановитьПараметр("ПравилоНачисления", ПравилоНачисления);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		НачислениеИСписаниеБонусныхБаллов = Документы.НачислениеИСписаниеБонусныхБаллов.СоздатьДокумент();
		НачислениеИСписаниеБонусныхБаллов.ПравилоНачисления = ПравилоНачисления;
		НачислениеИСписаниеБонусныхБаллов.Дата              = ДатаНачисления;
		НачислениеИСписаниеБонусныхБаллов.БонуснаяПрограммаЛояльности              = Выборка.БонуснаяПрограммаЛояльности;
		НачислениеИСписаниеБонусныхБаллов.ВидПравила                               = Выборка.ВидПравила;
		НачислениеИСписаниеБонусныхБаллов.КоличествоПериодовДействия               = Выборка.КоличествоПериодовДействия;
		НачислениеИСписаниеБонусныхБаллов.КоличествоПериодовОтсрочкиНачалаДействия = Выборка.КоличествоПериодовОтсрочкиНачалаДействия;
		НачислениеИСписаниеБонусныхБаллов.ПериодОтсрочкиНачалаДействия             = Выборка.ПериодОтсрочкиНачалаДействия;
		НачислениеИСписаниеБонусныхБаллов.ПериодДействия                           = Выборка.ПериодДействия;
		
		Для Каждого СтрокаТЧ Из ТаблицаНачислениеИСписание Цикл
		
			Если НачислениеИСписаниеБонусныхБаллов.ВидПравила = Перечисления.ВидыПравилНачисленияБонусныхБаллов.Начисление Тогда
				НоваяСтрока = НачислениеИСписаниеБонусныхБаллов.Начисление.Добавить();
			Иначе
				НоваяСтрока = НачислениеИСписаниеБонусныхБаллов.Списание.Добавить();
			КонецЕсли;
			
			НоваяСтрока.Партнер = СтрокаТЧ.Партнер;
			НоваяСтрока.Баллы   = СтрокаТЧ.СуммаНачисления;
			
		КонецЦикла;
		
		НачислениеИСписаниеБонусныхБаллов.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		
	КонецЕсли;
	
КонецПроцедуры

// Получает данные об остатках бонусных баллов партнера по бонусной программе лояльности.
//
// Параметры:
//  БонуснаяПрограммаЛояльности - СправочникСсылка.БонусныеПрограммыЛояльности - Бонусная программа лояльности.
//  Партнер - СправочникСсылка.Партнеры - Партнер.
//
// Возвращаемое значение:
//  ТаблицаЗначений - с колонками:
//   * Период - Дата - Период.
//   * Сумма - Число - Сумма.
//   * Изменение - Число - Сумма изменения.
//   * ТекущийОстаток - Число - Текущий остаток.
//
Функция ОстаткиИДвиженияБонусныхБаллов(БонуснаяПрограммаЛояльности, Партнер) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОстаткиБонусныхБаллов = Новый ТаблицаЗначений;
	ОстаткиБонусныхБаллов.Колонки.Добавить("Период");
	ОстаткиБонусныхБаллов.Колонки.Добавить("Сумма");
	ОстаткиБонусныхБаллов.Колонки.Добавить("Изменение");
	ОстаткиБонусныхБаллов.Колонки.Добавить("ТекущийОстаток");
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	&БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	&Партнер КАК Партнер
	|ПОМЕСТИТЬ ИсходныеДанныеПредварительные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанныеПредварительные.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ИсходныеДанныеПредварительные.Партнер КАК Партнер,
	|	НЕ СостояниеБонусовСерверЛояльности.ДисконтнаяКарта ЕСТЬ NULL КАК КартаЗаблокирована
	|ПОМЕСТИТЬ СостояниеБонусовСерверЛояльности
	|ИЗ
	|	ИсходныеДанныеПредварительные КАК ИсходныеДанныеПредварительные
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СостояниеБонусовСерверЛояльности КАК СостояниеБонусовСерверЛояльности
	|		ПО СостояниеБонусовСерверЛояльности.ДисконтнаяКарта.Партнер = ИсходныеДанныеПредварительные.Партнер
	|		И СостояниеБонусовСерверЛояльности.ДисконтнаяКарта.Владелец.БонуснаяПрограммаЛояльности = ИсходныеДанныеПредварительные.БонуснаяПрограммаЛояльности
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИсходныеДанныеПредварительные.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ИсходныеДанныеПредварительные.Партнер КАК Партнер,
	|	СУММА(ЕСТЬNULL(БонусныеБаллыКСписанию.КСписанию, 0)) КАК КСписанию
	|ПОМЕСТИТЬ БонусныеБаллыКСписаниюСерверЛояльности
	|ИЗ
	|	ИсходныеДанныеПредварительные КАК ИсходныеДанныеПредварительные
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.БонусныеБаллыКСписаниюИНачислению КАК БонусныеБаллыКСписанию
	|		ПО БонусныеБаллыКСписанию.ДисконтнаяКарта.Партнер = ИсходныеДанныеПредварительные.Партнер
	|		И БонусныеБаллыКСписанию.БонуснаяПрограммаЛояльности = ИсходныеДанныеПредварительные.БонуснаяПрограммаЛояльности
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанныеПредварительные.БонуснаяПрограммаЛояльности,
	|	ИсходныеДанныеПредварительные.Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""Сейчас"" КАК Период,
	|	ВЫБОР
	|		КОГДА БонусныеБаллыОстаткиИОбороты.НачисленоОстаток - 
	|				ВЫБОР КОГДА (БонусныеБаллыОстаткиИОбороты.КСписаниюОстаток + БонусныеБаллыКСписанию.КСписанию) > 0 ТОГДА (БонусныеБаллыОстаткиИОбороты.КСписаниюОстаток + БонусныеБаллыКСписанию.КСписанию) ИНАЧЕ 0 КОНЕЦ >= 0
	|			ТОГДА БонусныеБаллыОстаткиИОбороты.НачисленоОстаток - 
	|					ВЫБОР КОГДА (БонусныеБаллыОстаткиИОбороты.КСписаниюОстаток + БонусныеБаллыКСписанию.КСписанию) > 0 ТОГДА (БонусныеБаллыОстаткиИОбороты.КСписаниюОстаток + БонусныеБаллыКСписанию.КСписанию) ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма
	|ПОМЕСТИТЬ НачальныйОстаток
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.Остатки(
	|			&ДатаНачала,
	|			(БонуснаяПрограммаЛояльности, Партнер) В
	|				(ВЫБРАТЬ
	|					СостояниеБонусовСерверЛояльности.БонуснаяПрограммаЛояльности,
	|					СостояниеБонусовСерверЛояльности.Партнер
	|				ИЗ
	|					СостояниеБонусовСерверЛояльности
	|				ГДЕ НЕ СостояниеБонусовСерверЛояльности.КартаЗаблокирована)) КАК БонусныеБаллыОстаткиИОбороты
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ БонусныеБаллыКСписаниюСерверЛояльности КАК БонусныеБаллыКСписанию
	|		ПО БонусныеБаллыОстаткиИОбороты.Партнер = БонусныеБаллыКСписанию.Партнер
	|		И БонусныеБаллыОстаткиИОбороты.БонуснаяПрограммаЛояльности = БонусныеБаллыКСписанию.БонуснаяПрограммаЛояльности
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""Сейчас"",
	|	0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НачальныйОстаток.Период КАК Период,
	|	СУММА(НачальныйОстаток.Сумма) КАК Сумма
	|ИЗ
	|	НачальныйОстаток КАК НачальныйОстаток
	|
	|СГРУППИРОВАТЬ ПО
	|	НачальныйОстаток.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллыОстаткиИОбороты.ПериодДень КАК Период,
	|	ВЫБОР
	|		КОГДА БонусныеБаллыОстаткиИОбороты.НачисленоКонечныйОстаток - ВЫБОР КОГДА БонусныеБаллыОстаткиИОбороты.КСписаниюКонечныйОстаток > 0 ТОГДА БонусныеБаллыОстаткиИОбороты.КСписаниюКонечныйОстаток ИНАЧЕ 0 КОНЕЦ >= 0
	|			ТОГДА БонусныеБаллыОстаткиИОбороты.НачисленоКонечныйОстаток - ВЫБОР КОГДА БонусныеБаллыОстаткиИОбороты.КСписаниюКонечныйОстаток > 0 ТОГДА БонусныеБаллыОстаткиИОбороты.КСписаниюКонечныйОстаток ИНАЧЕ 0 КОНЕЦ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы.ОстаткиИОбороты(
	|			&ДатаНачала,
	|			,
	|			Авто,
	|			Движения,
	|			(БонуснаяПрограммаЛояльности, Партнер) В
	|				(ВЫБРАТЬ
	|					СостояниеБонусовСерверЛояльности.БонуснаяПрограммаЛояльности,
	|					СостояниеБонусовСерверЛояльности.Партнер
	|				ИЗ
	|					СостояниеБонусовСерверЛояльности
	|				ГДЕ НЕ СостояниеБонусовСерверЛояльности.КартаЗаблокирована)) КАК БонусныеБаллыОстаткиИОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период");

	Запрос.УстановитьПараметр("ДатаНачала",                  ТекущаяДатаСеанса());
	Запрос.УстановитьПараметр("БонуснаяПрограммаЛояльности", БонуснаяПрограммаЛояльности);
	Запрос.УстановитьПараметр("Партнер",                     Партнер);

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Текущий остаток
	РезультатТекущийОстаток = МассивРезультатов[4]; // РезультатЗапроса
	Выборка = РезультатТекущийОстаток.Выбрать();
	ТекущийОстаток = 0;
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ОстаткиБонусныхБаллов.Добавить();
		НоваяСтрока.Период = Выборка.Период;
		НоваяСтрока.Сумма = Выборка.Сумма;
		ТекущийОстаток = НоваяСтрока.Сумма;
		НоваяСтрока.ТекущийОстаток = Истина;
		
	КонецЦикла;
	
	// Списания баллов
	РезультатСписанияБаллов = МассивРезультатов[5]; // РезультатЗапроса
	Выборка = РезультатСписанияБаллов.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Изменение = Выборка.Сумма - ТекущийОстаток;
		
		Если Изменение = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ОстаткиБонусныхБаллов.Добавить();
		Если Изменение > 0 Тогда
			НоваяСтрока.Период = НСтр("ru = 'Начисление через';
										|en = 'Accrual in'")
			                   + " " + СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Цел((Выборка.Период - ТекущаяДатаСеанса())/(24*60*60)), НСтр("ru = 'день, дня, дней';
																																										|en = 'day, day, days'"))
			                   + " " + "("+Формат(Выборка.Период,"ДЛФ=D")+"):"
			                   + " " + СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Изменение, НСтр("ru = 'балл, балла, баллов';
																															|en = 'point, point, points'"))
		Иначе
			НоваяСтрока.Период = НСтр("ru = 'Списание через';
										|en = 'Write-off in'")
			                   + " " + СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(Цел((Выборка.Период - ТекущаяДатаСеанса())/(24*60*60)), НСтр("ru = 'день, дня, дней';
																																										|en = 'day, day, days'"))
			                   + " " + "("+Формат(Выборка.Период,"ДЛФ=D")+"):"
			                   + " " + СтроковыеФункцииКлиентСервер.ЧислоЦифрамиПредметИсчисленияПрописью(-Изменение, НСтр("ru = 'балл, балла, баллов';
																															|en = 'point, point, points'"))
		КонецЕсли;
		
		НоваяСтрока.Сумма = Выборка.Сумма;
		НоваяСтрока.Изменение = Изменение;
		ТекущийОстаток = Выборка.Сумма; 
		
	КонецЦикла;
	
	Возврат ОстаткиБонусныхБаллов;
	
КонецФункции

// Получает данные бонусной программы по карте лояльности.
//
// Параметры:
//  КартаЛояльности - СправочникСсылка.КартыЛояльности - карта лояльности.
//
// Возвращаемое значение:
//  СправочникСсылка.БонусныеПрограммыЛояльности - Бонусная программа лояльности.
//
Функция БонуснаяПрограммаКартыЛояльности(КартаЛояльности) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КартыЛояльности.Владелец                                                               КАК ВидКартыЛояльности,
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности                                   КАК БонуснаяПрограммаЛояльности,
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов          КАК Валюта,
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности.КурсКонвертацииБонусовВВалюту     КАК КурсКонвертацииБонусовВВалюту,
	|	КартыЛояльности.Партнер                                                                КАК Партнер,
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности.НеНачислятьБаллыПриОплатеБонусами КАК НеНачислятьБаллыПриОплатеБонусами,
	|	КартыЛояльности.Владелец.БонуснаяПрограммаЛояльности.МаксимальныйПроцентОплатыБонусами КАК МаксимальныйПроцентОплатыБонусами
	|ИЗ
	|	Справочник.КартыЛояльности КАК КартыЛояльности
	|ГДЕ
	|	КартыЛояльности.Ссылка = &КартаЛояльности");
	Запрос.УстановитьПараметр("КартаЛояльности", КартаЛояльности);
	
	ВыборкаБонуснаяПрограммаЛояльности = Запрос.Выполнить().Выбрать();
	ВыборкаБонуснаяПрограммаЛояльности.Следующий();
	
	Возврат ВыборкаБонуснаяПрограммаЛояльности;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработкаВозвратаНачисленийБонусныхБаллов

Функция ОбработатьВозвратыНачисленийБонусныхБаллов(ДатаСреза)
	
	РаспределениеУспешно = Истина;
	КоличествоОбработанныхДанных = 0;
	ПовторитьОбработку = Истина;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегламентноеЗадание.РаспределениеСрочныхБонусныхБаллов.ОбработатьВозвратыНачисленийБонусныхБаллов_х100");
	
	Пока ПовторитьОбработку Цикл
		ТекстОшибки = "";
		
		КоличествоОбработанныхДокументов = СнятьКОбработкеВозвратыНачисленийБонусныхБаллов(ДатаСреза,,ТекстОшибки);
		ПовторитьОбработку = Не (КоличествоОбработанныхДокументов = 0);
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			РаспределениеУспешно = Ложь;
			ТекстСообщения = НСтр("ru = 'При выполнении распределения возвратов начислений срочных баллов произошла ошибка: %1';
									|en = 'При выполнении распределения возвратов начислений срочных баллов произошла ошибка: %1'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1", ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
											|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , ,
				ТекстСообщения);
			Прервать;
		КонецЕсли;
		
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + КоличествоОбработанныхДокументов;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Окр(КоличествоОбработанныхДанных/100));
	
	Возврат РаспределениеУспешно;
	
КонецФункции

Функция СнятьКОбработкеВозвратыНачисленийБонусныхБаллов(ДатаСреза, РазмерПорции = Неопределено, ТекстОшибки = "")
	
	РезультатЗапроса = РезультатЗапросаВозвратыНачисленийБонусныхБалловКОбработке(ДатаСреза, РазмерПорции);
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	РезультатЗапросаВозвратыНачисленийКОбработке = РезультатЗапроса[КоличествоПакетов-3];
	
	СписанияВозвратовНачисленийБонусныхБаллов = РезультатЗапроса[КоличествоПакетов-2].Выгрузить();
	СписанияСгоревшихБонусныхБаллов = РезультатЗапроса[КоличествоПакетов-1].Выгрузить();
		
	ВыборкаВозвратыНачисленийКОбработкеПрограммаЛояльности = РезультатЗапросаВозвратыНачисленийКОбработке.Выбрать(
		ОбходРезультатаЗапроса.ПоГруппировкам, "БонуснаяПрограммаЛояльности");
	
	КоличествоОбработанныхДокументов = 0;
	Пока ВыборкаВозвратыНачисленийКОбработкеПрограммаЛояльности.Следующий() Цикл
		
		ВыборкаВозвратыНачисленийКОбработкеПартнер = ВыборкаВозвратыНачисленийКОбработкеПрограммаЛояльности.Выбрать(
			ОбходРезультатаЗапроса.ПоГруппировкам, "Партнер");
		
		Пока ВыборкаВозвратыНачисленийКОбработкеПартнер.Следующий() Цикл
			
			ВыборкаВозвратыНачисленийКОбработкеДатаНачалаДействия = ВыборкаВозвратыНачисленийКОбработкеПартнер.Выбрать(
				ОбходРезультатаЗапроса.ПоГруппировкам, "ДатаНачалаДействия");
			
			Пока ВыборкаВозвратыНачисленийКОбработкеДатаНачалаДействия.Следующий() Цикл
					
				ВыборкаВозвратыНачисленийКОбработкеДатаКСписанию = ВыборкаВозвратыНачисленийКОбработкеДатаНачалаДействия.Выбрать(
					ОбходРезультатаЗапроса.ПоГруппировкам, "ДатаКСписанию");
				
				Пока ВыборкаВозвратыНачисленийКОбработкеДатаКСписанию.Следующий() Цикл
					
					СтруктураПоиска = Новый Структура;
					СтруктураПоиска.Вставить("БонуснаяПрограммаЛояльности");
					СтруктураПоиска.Вставить("Партнер");
					СтруктураПоиска.Вставить("ДатаНачалаДействия");
					СтруктураПоиска.Вставить("ДатаКСписанию");
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаВозвратыНачисленийКОбработкеДатаКСписанию);
					
					СуммаНеиспользованныхБонусныхБаллов = ПолучитьСуммуНеиспользованныхСрочныхБонусныхБаллов(
						СтруктураПоиска,
						СписанияВозвратовНачисленийБонусныхБаллов,
						СписанияСгоревшихБонусныхБаллов);
					
					НачатьТранзакцию();
					
					Попытка
						
						ВыборкаВозвратыНачисленийКОбработкеДетали = ВыборкаВозвратыНачисленийКОбработкеДатаКСписанию.Выбрать();
						
						Пока ВыборкаВозвратыНачисленийКОбработкеДетали.Следующий() Цикл
							
							ДанныеИзменены = Ложь;
							
							// распределяем возврат начисления по регистратору
							СуммаВозврата = ВыборкаВозвратыНачисленийКОбработкеДетали.СуммаВозврата;
							
							РаспределитьВозвратыНачисленийПоРегистраторам(
								ВыборкаВозвратыНачисленийКОбработкеДетали,
								СуммаНеиспользованныхБонусныхБаллов,
								СуммаВозврата,
								ДанныеИзменены);
							
							СуммаНеиспользованныхБонусныхБаллов = СуммаНеиспользованныхБонусныхБаллов - СуммаВозврата;
							
							Если ДанныеИзменены Тогда
								КоличествоОбработанныхДокументов = КоличествоОбработанныхДокументов + 1;
							КонецЕсли;
							
						КонецЦикла;
						
						ЗафиксироватьТранзакцию();
						
					Исключение
					
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ИнформацияОбОшибке = ИнформацияОбОшибке();
						ТекстОшибки = ИнформацияОбОшибке.Описание;
						Прервать;
						
					КонецПопытки;
					
				КонецЦикла;
					
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхДокументов;
	
КонецФункции

Функция РезультатЗапросаВозвратыНачисленийБонусныхБалловКОбработке(ДатаСреза, РазмерПорции)
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ЗапросДвиженияБонусныхБалловВозвратыНачислений(РазмерПорции));
	ТекстыЗапроса.Добавить(ЗапросПериодыДействияВозвратовБонусныхБаллов());
	ТекстыЗапроса.Добавить(ЗапросДвиженияБонусныхБалловНачисления());
	ТекстыЗапроса.Добавить(ЗапросДвиженияБонусныхБалловВозвратыНачисленийКОбработке());
	ТекстыЗапроса.Добавить(ЗапросДвиженияСписанияСгоревшихБонусныхБаллов());
	ТекстыЗапроса.Добавить(ЗапросДвиженияСписанияВозвратовНачисленийБонусныхБаллов());
	
	ОсновнойТекстЗапроса = 
	"ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.СуммаВозврата КАК СуммаВозврата,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.Регистратор.Дата КАК РегистраторДата
	|ИЗ
	|	ДвиженияБонусныхБалловВозвратыНачисленийКОбработке КАК БонусныеБаллы
	|
	|УПОРЯДОЧИТЬ ПО
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию
	|ИТОГИ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер,
	|	ДатаНачалаДействия,
	|	ДатаКСписанию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.СуммаСписанияВозврата КАК СуммаСписанияВозврата
	|ИЗ
	|	ДвиженияСписанияВозвратовНачисленийБонусныхБаллов КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.СуммаСписания КАК СуммаСписания
	|ИЗ
	|	ДвиженияСписанияСгоревшихБонусныхБаллов КАК БонусныеБаллы";
	ТекстыЗапроса.Добавить(ОсновнойТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
	Если РазмерПорции <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция ЗапросДвиженияБонусныхБалловВозвратыНачислений(РазмерПорции)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	БонусныеБаллы.Период КАК Период
	|ПОМЕСТИТЬ НачалоВыборки
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ДатаКОбработке >= &ДатаСреза
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено < 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""ВозвратНачисления""
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию < 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""ВозвратНачисленияСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.ВидДвижения КАК ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию,
	|	СУММА(БонусныеБаллы.Зарезервировано) КАК Зарезервировано,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	МАКСИМУМ(БонусныеБаллы.ДатаКОбработке) КАК ДатаКОбработке
	|ПОМЕСТИТЬ ДвиженияБонусныхБалловВозвратыНачисленийРаздельно
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачалоВыборки КАК НачалоВыборки
	|	ПО БонусныеБаллы.Период >= НачалоВыборки.Период
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И (БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено < 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ИЛИ БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию < 0
	|				И БонусныеБаллы.Начислено = 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено < 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""ВозвратНачисления""
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию < 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""ВозвратНачисленияСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ВидДвижения,
	|	Начислено,
	|	КСписанию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллыСписание.Период КАК ДатаКСписанию,
	|	-БонусныеБаллы.Начислено КАК СуммаВозврата,
	|	БонусныеБаллы.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ДвиженияБонусныхБалловВозвратыНачислений
	|ИЗ
	|	ДвиженияБонусныхБалловВозвратыНачисленийРаздельно КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияБонусныхБалловВозвратыНачисленийРаздельно КАК БонусныеБаллыСписание
	|		ПО БонусныеБаллы.Период = БонусныеБаллыСписание.ДатаНачалаДействия
	|			И БонусныеБаллы.Регистратор = БонусныеБаллыСписание.Регистратор
	|			И БонусныеБаллы.ВидДвижения = БонусныеБаллыСписание.ВидДвижения
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = БонусныеБаллыСписание.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = БонусныеБаллыСписание.Партнер
	|			И БонусныеБаллы.Период = БонусныеБаллыСписание.ДатаНачалаДействия
	|			И (БонусныеБаллы.ВидОперации = ""ВозвратНачисления"")
	|			И (БонусныеБаллыСписание.ВидОперации = ""ВозвратНачисленияСписание"")
	|ГДЕ
	|	БонусныеБаллыСписание.Период <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ДатаНачалаДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|";
	
	Если РазмерПорции <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросПериодыДействияВозвратовБонусныхБаллов()
	
	Возврат "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер
	|ПОМЕСТИТЬ ПериодыДействияВозвратовБонусныхБаллов
	|ИЗ
	|	ДвиженияБонусныхБалловВозвратыНачислений КАК БонусныеБаллы
	|";
	 
КонецФункции

Функция ЗапросДвиженияБонусныхБалловНачисления()
	
	Возврат "
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено > 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""Начисление""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.ВидДвижения КАК ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию,
	|	СУММА(БонусныеБаллы.Зарезервировано) КАК Зарезервировано,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	МАКСИМУМ(БонусныеБаллы.ДатаКОбработке) КАК ДатаКОбработке
	|ПОМЕСТИТЬ ДвиженияБонусныхБалловНачисленияРаздельно
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыДействияВозвратовБонусныхБаллов КАК ПериодыДействияВозвратовБонусныхБаллов
	|		ПО БонусныеБаллы.ДатаНачалаДействия = ПериодыДействияВозвратовБонусныхБаллов.ДатаНачалаДействия
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыДействияВозвратовБонусныхБаллов.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыДействияВозвратовБонусныхБаллов.Партнер
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И БонусныеБаллы.Начислено > 0
	|	И БонусныеБаллы.КСписанию = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено > 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""Начисление""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию > 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""НачислениеСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	СУММА(БонусныеБаллы.Начислено),
	|	СУММА(БонусныеБаллы.КСписанию),
	|	СУММА(БонусныеБаллы.Зарезервировано),
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор,
	|	МАКСИМУМ(БонусныеБаллы.ДатаКОбработке)
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыДействияВозвратовБонусныхБаллов КАК ПериодыДействияВозвратовБонусныхБаллов
	|		ПО БонусныеБаллы.Период = ПериодыДействияВозвратовБонусныхБаллов.ДатаКСписанию
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыДействияВозвратовБонусныхБаллов.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыДействияВозвратовБонусныхБаллов.Партнер
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И БонусныеБаллы.КСписанию > 0
	|	И БонусныеБаллы.Начислено = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию > 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""НачислениеСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Регистратор,
	|	ВидДвижения,
	|	Начислено,
	|	КСписанию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Распределено,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.Начислено КАК СуммаНачисления,
	|	БонусныеБаллы.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ДвиженияБонусныхБалловНачисления
	|ИЗ
	|	(ВЫБРАТЬ
	|		БонусныеБаллы.ВидОперации КАК ВидОперации,
	|		БонусныеБаллы.Период КАК Период,
	|		БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|		БонусныеБаллыСписание.Период КАК ДатаКСписанию,
	|		БонусныеБаллы.ВидДвижения КАК ВидДвижения,
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер КАК Партнер,
	|		БонусныеБаллы.Начислено КАК Начислено,
	|		БонусныеБаллыСписание.КСписанию КАК КСписанию,
	|		БонусныеБаллы.Зарезервировано КАК Зарезервировано,
	|		БонусныеБаллы.Регистратор КАК Регистратор,
	|		БонусныеБаллы.ДатаКОбработке КАК ДатаКОбработке
	|	ИЗ
	|		ДвиженияБонусныхБалловНачисленияРаздельно КАК БонусныеБаллы
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияБонусныхБалловНачисленияРаздельно КАК БонусныеБаллыСписание
	|			ПО БонусныеБаллы.Регистратор = БонусныеБаллыСписание.Регистратор
	|				И БонусныеБаллы.БонуснаяПрограммаЛояльности = БонусныеБаллыСписание.БонуснаяПрограммаЛояльности
	|				И БонусныеБаллы.Партнер = БонусныеБаллыСписание.Партнер
	|				И БонусныеБаллы.Период = БонусныеБаллыСписание.ДатаНачалаДействия
	|				И БонусныеБаллы.ВидДвижения = БонусныеБаллыСписание.ВидДвижения
	|				И (БонусныеБаллы.ВидОперации = ""Начисление"")
	|				И (БонусныеБаллыСписание.Период <> БонусныеБаллыСписание.ДатаНачалаДействия)) КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыДействияВозвратовБонусныхБаллов КАК ПериодыДействияВозвратовБонусныхБаллов
	|		ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыДействияВозвратовБонусныхБаллов.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыДействияВозвратовБонусныхБаллов.Партнер
	|			И БонусныеБаллы.ДатаНачалаДействия = ПериодыДействияВозвратовБонусныхБаллов.ДатаНачалаДействия
	|			И БонусныеБаллы.ДатаКСписанию = ПериодыДействияВозвратовБонусныхБаллов.ДатаКСписанию
	|";
	
КонецФункции

Функция ЗапросДвиженияБонусныхБалловВозвратыНачисленийКОбработке()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	СУММА(-БонусныеБаллы.Сумма) КАК СуммаВозврата
	|ПОМЕСТИТЬ ПериодыВозвратовНачисленийКОбработке
	|ИЗ
	|	(ВЫБРАТЬ
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер КАК Партнер,
	|		БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|		БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|		-БонусныеБаллы.СуммаВозврата КАК Сумма
	|	ИЗ
	|		ДвиженияБонусныхБалловВозвратыНачислений КАК БонусныеБаллы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер,
	|		БонусныеБаллы.ДатаНачалаДействия,
	|		БонусныеБаллы.ДатаКСписанию,
	|		БонусныеБаллы.СуммаНачисления
	|	ИЗ
	|		ДвиженияБонусныхБалловНачисления КАК БонусныеБаллы
	|	ГДЕ
	|		БонусныеБаллы.Распределено = ЛОЖЬ) КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию
	|
	|ИМЕЮЩИЕ
	|	СУММА(-БонусныеБаллы.Сумма) > 0
	|;
    |
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.СуммаВозврата КАК СуммаВозврата,
	|	БонусныеБаллы.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ДвиженияБонусныхБалловВозвратыНачисленийКОбработке
	|ИЗ
	|	ДвиженияБонусныхБалловВозвратыНачислений КАК БонусныеБаллы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовНачисленийКОбработке КАК ПериодыВозвратовНачисленийКОбработке
	|	ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовНачисленийКОбработке.БонуснаяПрограммаЛояльности
	|		И БонусныеБаллы.Партнер = ПериодыВозвратовНачисленийКОбработке.Партнер
	|		И БонусныеБаллы.ДатаНачалаДействия = ПериодыВозвратовНачисленийКОбработке.ДатаНачалаДействия
	|		И БонусныеБаллы.ДатаКСписанию = ПериодыВозвратовНачисленийКОбработке.ДатаКСписанию
	|";
	
КонецФункции

Функция ЗапросДвиженияСписанияСгоревшихБонусныхБаллов()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Период КАК ДатаКСписанию,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(-БонусныеБаллы.Начислено) КАК СуммаСписания
	|ПОМЕСТИТЬ ДвиженияСписанияСгоревшихБонусныхБаллов
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыДействияВозвратовБонусныхБаллов КАК ПериодыДействияВозвратовБонусныхБаллов
	|		ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыДействияВозвратовБонусныхБаллов.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыДействияВозвратовБонусныхБаллов.Партнер
	|			И БонусныеБаллы.Период = ПериодыДействияВозвратовБонусныхБаллов.ДатаКСписанию
	|			И БонусныеБаллы.ДатаНачалаДействия = ПериодыДействияВозвратовБонусныхБаллов.ДатаНачалаДействия
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И БонусныеБаллы.Начислено < 0
	|	И БонусныеБаллы.КСписанию < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|";
	
КонецФункции

Функция ЗапросДвиженияСписанияВозвратовНачисленийБонусныхБаллов()
	
	Возврат 
	"ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Период КАК ДатаКСписанию,
	|	СУММА(БонусныеБаллы.Начислено) КАК СуммаСписанияВозврата
	|ПОМЕСТИТЬ ДвиженияСписанияВозвратовНачисленийБонусныхБаллов
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыДействияВозвратовБонусныхБаллов КАК ПериодыДействияВозвратовБонусныхБаллов
	|		ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыДействияВозвратовБонусныхБаллов.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыДействияВозвратовБонусныхБаллов.Партнер
	|			И БонусныеБаллы.ДатаНачалаДействия = ПериодыДействияВозвратовБонусныхБаллов.ДатаНачалаДействия
	|			И БонусныеБаллы.Период = ПериодыДействияВозвратовБонусныхБаллов.ДатаКСписанию
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И БонусныеБаллы.Начислено > 0
	|	И БонусныеБаллы.КСписанию > 0
	|	И БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.Период";
	
КонецФункции

Процедура РаспределитьВозвратыНачисленийПоРегистраторам(ВозвратНачисленияКОбработкеДетали,
	СуммаНеиспользованныхБонусныхБаллов, СуммаВозврата, ДанныеИзменены)
	
	СуммаВОплату = 0;
	Если СуммаВозврата > СуммаНеиспользованныхБонусныхБаллов Тогда
		Если СуммаНеиспользованныхБонусныхБаллов <= 0 Тогда
			СуммаВОплату = СуммаВозврата;
		Иначе
			СуммаВОплату = СуммаВозврата - СуммаНеиспользованныхБонусныхБаллов;
		КонецЕсли;
	КонецЕсли;
	
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
	ЭлементБлокировки.УстановитьЗначение("Регистратор", ВозвратНачисленияКОбработкеДетали.Регистратор);
	
	Блокировка.Заблокировать();
	
	НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = ВозвратНачисленияКОбработкеДетали.Регистратор;
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
	НаборЗаписейРНБонусныеБаллы.Прочитать();
	
	// Очистить в существующих движениях регистратора реквизит "Дата к обработке", так как возврат
	// по окончании операции распределяется (списывается или преобразуется в расход)
	Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
		
		Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = ВозвратНачисленияКОбработкеДетали.БонуснаяПрограммаЛояльности
			И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = ВозвратНачисленияКОбработкеДетали.Партнер
			И ((СтрокаНаборЗаписейРНБонусныеБаллы.Период = ВозвратНачисленияКОбработкеДетали.ДатаНачалаДействия
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ВозвратНачисленияКОбработкеДетали.ДатаНачалаДействия)
				Или (СтрокаНаборЗаписейРНБонусныеБаллы.Период = ВозвратНачисленияКОбработкеДетали.ДатаКСписанию
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ВозвратНачисленияКОбработкеДетали.ДатаНачалаДействия)) Тогда
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
			ДанныеИзменены = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	// Регистрируем списание возвращенных бонусных баллов, не использованных покупателем ранее
	СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
	ЗаполнитьЗначенияСвойств(СтрокаНаборЗаписейРНБонусныеБаллы, ВозвратНачисленияКОбработкеДетали);
	
	СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Приход;
	СтрокаНаборЗаписейРНБонусныеБаллы.Период = ВозвратНачисленияКОбработкеДетали.ДатаКСписанию;
	СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СуммаВозврата;
	СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = СуммаВозврата;
	
	// Регистрируем преобразование возвращенных бонусных баллов в оплату (преобразуем в расход), так как
	// ранее бонусные баллы были использованы
	Если СуммаВОплату > 0 Тогда
		СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаНаборЗаписейРНБонусныеБаллы, ВозвратНачисленияКОбработкеДетали);
		
		СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаНаборЗаписейРНБонусныеБаллы.Период = ВозвратНачисленияКОбработкеДетали.РегистраторДата;
		СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СуммаВОплату;
		СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = Дата(1, 1, 1);
		СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
		
		ДанныеИзменены = Истина;
	КонецЕсли;
	
	НаборЗаписейРНБонусныеБаллы.Записать();
	
КонецПроцедуры

Функция ПолучитьСуммуНеиспользованныхСрочныхБонусныхБаллов(СтруктураПоиска,
	СписанияВозвратовНачисленийБонусныхБаллов, СписанияСгоревшихБонусныхБаллов)
	
	СписанияВозвратовНачисленийБонусныхБалловПоПериоду
		= СписанияВозвратовНачисленийБонусныхБаллов.Скопировать(СтруктураПоиска);
	СуммаСписанияВозвратовНачислений
		= СписанияВозвратовНачисленийБонусныхБалловПоПериоду.Итог("СуммаСписанияВозврата");
	
	СписанияСгоревшихБонусныхБалловПоПериоду
		= СписанияСгоревшихБонусныхБаллов.Скопировать(СтруктураПоиска);
	СуммаСписанияСгоревшихБонусныхБаллов = СписанияСгоревшихБонусныхБалловПоПериоду.Итог("СуммаСписания");
	
	СуммаНеиспользованныхБонусныхБаллов = СуммаСписанияСгоревшихБонусныхБаллов - СуммаСписанияВозвратовНачислений;
	
	Возврат СуммаНеиспользованныхБонусныхБаллов;
	
КонецФункции

#КонецОбласти

#Область ОбработкаВозвратаОплатыБонусныхБаллов

Функция ОбработатьВозвратыОплатыБонусныхБаллов(ДатаСреза)
	
	РаспределениеУспешно = Истина;
	КоличествоОбработанныхДанных = 0;
	ПовторитьОбработку = Истина;
	
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегламентноеЗадание.РаспределениеСрочныхБонусныхБаллов.ОбработатьВозвратыНачисленийБонусныхБаллов_х100");
	
	Пока ПовторитьОбработку Цикл
		ТекстОшибки = "";
		
		КоличествоОбработанныхДокументов = СнятьКОбработкеВозвратыОплатБонуснымиБаллами(ДатаСреза,,ТекстОшибки);
		ПовторитьОбработку = Не (КоличествоОбработанныхДокументов = 0);
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			РаспределениеУспешно = Ложь;
			ТекстСообщения = НСтр("ru = 'При выполнении распределения возвратов оплат бонусными баллами произошла ошибка: %1';
									|en = 'При выполнении распределения возвратов оплат бонусными баллами произошла ошибка: %1'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1", ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
											|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , ,
				ТекстСообщения);
			Прервать;
		КонецЕсли;
		
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + КоличествоОбработанныхДокументов;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Окр(КоличествоОбработанныхДанных/100));
	
	Возврат РаспределениеУспешно;
	
КонецФункции

Функция СнятьКОбработкеВозвратыОплатБонуснымиБаллами(ДатаСреза, РазмерПорции = Неопределено, ТекстОшибки = "")
	
	РезультатЗапроса = РезультатЗапросаВозвратыОплатБонуснымиБалламиКОбработке(ДатаСреза, РазмерПорции);
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	РезультатЗапросаПериодыВозвратовОплатыБонуснымиБалламиКОбработке = РезультатЗапроса[КоличествоПакетов-6];
	
	ВозвратыОплатБонуснымиБалламиКОбработке = РезультатЗапроса[КоличествоПакетов-5].Выгрузить();
	РаспределенныеОплатыБонуснымиБаллами = РезультатЗапроса[КоличествоПакетов-4].Выгрузить();
	РаспределенныеОплатыБессрочнымиБонуснымиБаллами = РезультатЗапроса[КоличествоПакетов-3].Выгрузить();
	СписанияСгоревшихБонусныхБаллов = РезультатЗапроса[КоличествоПакетов-2].Выгрузить();
	ЧастичноРаспределеныеОплатыБонусныхБаллов = РезультатЗапроса[КоличествоПакетов-1].Выгрузить();
	
	ВыборкаПериодыВозвратовОплатыКОбработкеПрограммаЛояльности = РезультатЗапросаПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Выбрать(
		ОбходРезультатаЗапроса.ПоГруппировкам, "БонуснаяПрограммаЛояльности");
	
	КоличествоОбработанныхДокументов = 0;
	Пока ВыборкаПериодыВозвратовОплатыКОбработкеПрограммаЛояльности.Следующий() Цикл
		
		ВыборкаПериодыВозвратовОплатыКОбработкеПартнер = ВыборкаПериодыВозвратовОплатыКОбработкеПрограммаЛояльности.Выбрать(
			ОбходРезультатаЗапроса.ПоГруппировкам, "Партнер");
		
		Пока ВыборкаПериодыВозвратовОплатыКОбработкеПартнер.Следующий() Цикл
			
			ВыборкаПериодыВозвратовОплатКОбработкеДетали = ВыборкаПериодыВозвратовОплатыКОбработкеПартнер.Выбрать();
			
			Пока ВыборкаПериодыВозвратовОплатКОбработкеДетали.Следующий() Цикл
					
					СуммаВозвратаКРаспределению = ВыборкаПериодыВозвратовОплатКОбработкеДетали.СуммаВозврата;
					
					НачатьТранзакцию();
					
					Попытка
						
						ДанныеИзменены = Ложь;
						
						СтруктураПоиска = Новый Структура;
						СтруктураПоиска.Вставить("БонуснаяПрограммаЛояльности");
						СтруктураПоиска.Вставить("Партнер");
						СтруктураПоиска.Вставить("Период");
						ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаПериодыВозвратовОплатКОбработкеДетали);
						
						// Получить регистраторы по периоду и сумму возврата к распределению
						РегистраторыКРаспределению = ВозвратыОплатБонуснымиБалламиКОбработке.Скопировать(СтруктураПоиска);
						
						// Определить, потратил бы их покупатель, если бы возврат произошел до распределения оплаты на
						// начисление срочных бонусных баллов. Если бы не потратил, не возвращать ему баллы
						НеВозвращатьБонусныеБаллы = Ложь;
						Если СписанияСгоревшихБонусныхБаллов.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
							НеВозвращатьБонусныеБаллы = Истина;
						КонецЕсли;
						
						// Получить бессрочные бонусные баллы, которые были использованы для оплаты
						РаспределеныеБессрочныеОплатыПоВозврату = РаспределенныеОплатыБессрочнымиБонуснымиБаллами.Скопировать(СтруктураПоиска);
						СуммаОплатыБессрочными = Мин(РаспределеныеБессрочныеОплатыПоВозврату.Итог("СуммаОплаты"), СуммаВозвратаКРаспределению);
						
						// Получить срочные бонусные баллы, которые были использованы для оплаты
						РаспределеныеОплатыПоВозврату = РаспределенныеОплатыБонуснымиБаллами.Скопировать(СтруктураПоиска);
						РаспределеныеОплатыПоВозврату.Сортировать("ДатаКСписанию");
						
						// Определиться, нужно ли снимать с обработки возврат оплаты или возврат оплаты должен сняться после
						// распределения оплаты и начисления
						ВозвратОплатыСнятьСОбработки = Истина;
						Если ЧастичноРаспределеныеОплатыБонусныхБаллов.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
							ВозвратОплатыСнятьСОбработки = Ложь;
						КонецЕсли;
						
						// Получить регистратор возврата оплаты для распределения
						Для Каждого РегистраторВозвратаОплаты Из РегистраторыКРаспределению Цикл
							
							Если НеВозвращатьБонусныеБаллы Тогда
								
								РаспределитьВозвратыОплатПоРегистраторамСписание(РегистраторВозвратаОплаты, ДанныеИзменены);
								
							Иначе
								
								ПродолжитьРаспределение = РаспределитьВозвратыОплатПоРегистраторамПреобразованиеВОплату(
									РегистраторВозвратаОплаты,
									РаспределеныеОплатыПоВозврату,
									ВозвратОплатыСнятьСОбработки,
									СуммаВозвратаКРаспределению,
									СуммаОплатыБессрочными,
									ДанныеИзменены);
								
								Если Не ПродолжитьРаспределение Тогда
									Прервать;
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЦикла;
						
						Если ДанныеИзменены Тогда
							КоличествоОбработанныхДокументов = КоличествоОбработанныхДокументов + 1;
						КонецЕсли;
						
						ЗафиксироватьТранзакцию();
						
					Исключение
					
						Если ТранзакцияАктивна() Тогда
							ОтменитьТранзакцию();
						КонецЕсли;
						ИнформацияОбОшибке = ИнформацияОбОшибке();
						ТекстОшибки = ИнформацияОбОшибке.Описание;
						Прервать;
						
					КонецПопытки;
					
			КонецЦикла;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхДокументов;
	
КонецФункции

Функция РезультатЗапросаВозвратыОплатБонуснымиБалламиКОбработке(ДатаСреза, РазмерПорции)
	
	ТекстыЗапроса = Новый Массив;
	ТекстыЗапроса.Добавить(ЗапросДвиженияВозвратовОплатыБонуснымиБаллами(РазмерПорции));
	ТекстыЗапроса.Добавить(ЗапросПериодыВозвратовОплатыБонуснымиБаллами());
	ТекстыЗапроса.Добавить(ЗапросДвиженияОплатБонуснымиБаллами());
	ТекстыЗапроса.Добавить(ПериодыВозвратовОплатыБонуснымиБалламиКОбработке());
	ТекстыЗапроса.Добавить(ЗапросДвиженияВозвратовОплатыБонуснымиБалламиКОбработке());
	ТекстыЗапроса.Добавить(ЗапросДвиженияРаспределенныхОплатБонуснымиБаллами());
	ТекстыЗапроса.Добавить(ЗапросДвиженияЧастичноРаспределеныхОплатБонуснымиБаллами());
	ТекстыЗапроса.Добавить(ЗапросДвиженияРаспределенныхОплатБессрочнымиБонуснымиБаллами());
	ТекстыЗапроса.Добавить(ЗапросДвиженияСписанияСгоревшихБонусныхБалловВозвратОплаты());
	
	ОсновнойТекстЗапроса = "
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.СуммаВозврата КАК СуммаВозврата
	|ИЗ
	|	ПериодыВозвратовОплатыБонуснымиБалламиКОбработке КАК БонусныеБаллы
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|ИТОГИ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.ДатаВозврата КАК ДатаВозврата,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.СуммаВозврата КАК СуммаВозврата
	|ИЗ
	|	ДвиженияВозвратовОплатыБонуснымиБалламиКОбработке КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКСписанию КАК ДатаКСписанию,
	|	БонусныеБаллы.ПериодДействия КАК ПериодДействия,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.СуммаВозврата КАК СуммаВозврата
	|ИЗ
	|	ДвиженияРаспределенныхОплатБонуснымиБаллами КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.СуммаОплаты КАК СуммаОплаты
	|ИЗ
	|	ДвиженияРаспределенныхОплатБессрочнымиБонуснымиБаллами КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.СуммаСписанияНачисления КАК СуммаСписанияНачисления
	|ИЗ
	|	ДвиженияСписанияСгоревшихБонусныхБаллов КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.КСписанию КАК КСписанию
	|ИЗ
	|	ДвиженияЧастичноРаспределеныхОплатБонуснымиБаллами КАК БонусныеБаллы
	|";
	ТекстыЗапроса.Добавить(ОсновнойТекстЗапроса);
	
	ТекстЗапроса = СтрСоединить(ТекстыЗапроса, ОбщегоНазначения.РазделительПакетаЗапросов());
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
	Если РазмерПорции <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Функция ЗапросДвиженияВозвратовОплатыБонуснымиБаллами(РазмерПорции)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	БонусныеБаллы.Период КАК Период
	|ПОМЕСТИТЬ НачалоВыборки
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ДатаКОбработке >= &ДатаСреза
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1000
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|				И БонусныеБаллы.Начислено < 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""ВозвратОплаты""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.ВидДвижения КАК ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию,
	|	СУММА(БонусныеБаллы.Зарезервировано) КАК Зарезервировано,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	МАКСИМУМ(БонусныеБаллы.ДатаКОбработке) КАК ДатаКОбработке
	|ПОМЕСТИТЬ ДвиженияВозвратовОплатыБонуснымиБаллами
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачалоВыборки КАК НачалоВыборки
	|		ПО БонусныеБаллы.Период >= НачалоВыборки.Период
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.Начислено < 0
	|	И БонусныеБаллы.КСписанию = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|				И БонусныеБаллы.Начислено < 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""ВозвратОплаты""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.ВидДвижения,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|";
	
	Если РазмерПорции <> Неопределено Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ЗапросПериодыВозвратовОплатыБонуснымиБаллами()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.Период КАК Период
	|ПОМЕСТИТЬ ПериодыВозвратовОплатыБонуснымиБаллами
	|ИЗ
	|	ДвиженияВозвратовОплатыБонуснымиБаллами КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.Период
	|";
	
КонецФункции

Функция ЗапросДвиженияОплатБонуснымиБаллами()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК СуммаОплаты,
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Распределено
	|ПОМЕСТИТЬ ДвиженияОплатБонуснымиБаллами
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовОплатыБонуснымиБаллами КАК ПериодыВозвратовОплатыБонуснымиБаллами
	|		ПО БонусныеБаллы.Период = ПериодыВозвратовОплатыБонуснымиБаллами.Период
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовОплатыБонуснымиБаллами.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыВозвратовОплатыБонуснымиБаллами.Партнер
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.Начислено > 0
	|	И БонусныеБаллы.КСписанию = 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ
	|";
	
КонецФункции

Функция ПериодыВозвратовОплатыБонуснымиБалламиКОбработке()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(-БонусныеБаллы.СуммаВозврата) КАК СуммаВозврата
	|ПОМЕСТИТЬ ПериодыВозвратовОплатыБонуснымиБалламиКОбработке
	|ИЗ
	|	(ВЫБРАТЬ
	|		БонусныеБаллы.Период КАК Период,
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер КАК Партнер,
	|		БонусныеБаллы.Начислено КАК СуммаВозврата
	|	ИЗ
	|		ДвиженияВозвратовОплатыБонуснымиБаллами КАК БонусныеБаллы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		БонусныеБаллы.Период,
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер,
	|		БонусныеБаллы.СуммаОплаты
	|	ИЗ
	|		ДвиженияОплатБонуснымиБаллами КАК БонусныеБаллы
	|	ГДЕ
	|		БонусныеБаллы.Распределено = ЛОЖЬ) КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|
	|ИМЕЮЩИЕ
	|	СУММА(-БонусныеБаллы.СуммаВозврата) > 0
	|";
	
КонецФункции

Функция ЗапросДвиженияВозвратовОплатыБонуснымиБалламиКОбработке()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ) КАК ДатаВозврата,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(-БонусныеБаллы.Начислено) КАК СуммаВозврата
	|ПОМЕСТИТЬ ДвиженияВозвратовОплатыБонуснымиБалламиКОбработке
	|ИЗ
	|	ДвиженияВозвратовОплатыБонуснымиБаллами КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовОплатыБонуснымиБалламиКОбработке КАК ПериодыВозвратовОплатыБонуснымиБалламиКОбработке
	|		ПО БонусныеБаллы.Период = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Период
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Партнер
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ),
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|";
	
КонецФункции

Функция ЗапросДвиженияРаспределенныхОплатБонуснымиБаллами()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Период КАК ДатаКСписанию,
	|	РАЗНОСТЬДАТ(БонусныеБаллы.ДатаНачалаДействия, БонусныеБаллы.Период, ДЕНЬ) КАК ПериодДействия,
	|	ДвиженияОплатБонуснымиБаллами.Период,
	|	СУММА(БонусныеБаллы.КСписанию) КАК СуммаВозврата
	|ПОМЕСТИТЬ ДвиженияРаспределенныхОплатБонуснымиБаллами
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОплатБонуснымиБаллами КАК ДвиженияОплатБонуснымиБаллами
	|		ПО БонусныеБаллы.Регистратор = ДвиженияОплатБонуснымиБаллами.Регистратор
	|			И БонусныеБаллы.БонуснаяПрограммаЛояльности = ДвиженияОплатБонуснымиБаллами.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ДвиженияОплатБонуснымиБаллами.Партнер
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовОплатыБонуснымиБалламиКОбработке КАК ПериодыВозвратовОплатыБонуснымиБалламиКОбработке
	|		ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Партнер
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.Начислено = 0
	|	И БонусныеБаллы.КСписанию > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	ДвиженияОплатБонуснымиБаллами.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	РАЗНОСТЬДАТ(БонусныеБаллы.ДатаНачалаДействия, БонусныеБаллы.Период, ДЕНЬ)
	|";
	
КонецФункции

Функция ЗапросДвиженияЧастичноРаспределеныхОплатБонуснымиБаллами()
	
	Возврат "
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ) КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию
	|ПОМЕСТИТЬ ДвиженияЧастичноРаспределеныхОплатБонуснымиБаллами
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	(БонусныеБаллы.Регистратор, БонусныеБаллы.БонуснаяПрограммаЛояльности, БонусныеБаллы.Партнер) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				БонусныеБаллы.Регистратор КАК Регистратор,
	|				БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|				БонусныеБаллы.Партнер КАК Партнер
	|			ИЗ
	|				ДвиженияОплатБонуснымиБаллами КАК БонусныеБаллы
	|			ГДЕ
	|				БонусныеБаллы.Распределено = ЛОЖЬ)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ),
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|
	|ИМЕЮЩИЕ
	|	СУММА(БонусныеБаллы.КСписанию) > 0
	|";
	
КонецФункции

Функция ЗапросДвиженияРаспределенныхОплатБессрочнымиБонуснымиБаллами()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ) КАК Период,
	|	СУММА(БонусныеБаллы.СуммаОплаты) КАК СуммаОплаты
	|ПОМЕСТИТЬ ДвиженияРаспределенныхОплатБессрочнымиБонуснымиБаллами
	|ИЗ
	|	(ВЫБРАТЬ
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер КАК Партнер,
	|		БонусныеБаллы.Регистратор КАК Регистратор,
	|		СУММА(БонусныеБаллы.СуммаОплаты) КАК СуммаОплаты
	|	ИЗ
	|		(ВЫБРАТЬ
	|			БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|			БонусныеБаллы.Партнер КАК Партнер,
	|			БонусныеБаллы.Регистратор КАК Регистратор,
	|			БонусныеБаллы.Начислено - БонусныеБаллы.КСписанию КАК СуммаОплаты
	|		ИЗ
	|			РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОплатБонуснымиБаллами КАК ДвиженияОплатБонуснымиБаллами
	|				ПО БонусныеБаллы.Регистратор = ДвиженияОплатБонуснымиБаллами.Регистратор
	|					И БонусныеБаллы.БонуснаяПрограммаЛояльности = ДвиженияОплатБонуснымиБаллами.БонуснаяПрограммаЛояльности
	|					И БонусныеБаллы.Партнер = ДвиженияОплатБонуснымиБаллами.Партнер
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовОплатыБонуснымиБалламиКОбработке КАК ПериодыВозвратовОплатыБонуснымиБалламиКОбработке
	|				ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.БонуснаяПрограммаЛояльности
	|					И БонусныеБаллы.Партнер = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Партнер
	|		ГДЕ
	|			БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|			И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|			БонусныеБаллы.Партнер КАК Партнер,
	|			БонусныеБаллы.Регистратор КАК Регистратор,
	|			-БонусныеБаллы.КСписанию КАК СуммаОплаты
	|		ИЗ
	|			РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияОплатБонуснымиБаллами КАК ДвиженияОплатБонуснымиБаллами
	|				ПО БонусныеБаллы.Регистратор = ДвиженияОплатБонуснымиБаллами.Регистратор
	|					И БонусныеБаллы.БонуснаяПрограммаЛояльности = ДвиженияОплатБонуснымиБаллами.БонуснаяПрограммаЛояльности
	|					И БонусныеБаллы.Партнер = ДвиженияОплатБонуснымиБаллами.Партнер
	|				ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПериодыВозвратовОплатыБонуснымиБалламиКОбработке КАК ПериодыВозвратовОплатыБонуснымиБалламиКОбработке
	|				ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.БонуснаяПрограммаЛояльности
	|					И БонусныеБаллы.Партнер = ПериодыВозвратовОплатыБонуснымиБалламиКОбработке.Партнер
	|		ГДЕ
	|			БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|			И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|			И БонусныеБаллы.КСписанию > 0) КАК БонусныеБаллы
	|	
	|	СГРУППИРОВАТЬ ПО
	|		БонусныеБаллы.Регистратор,
	|		БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		БонусныеБаллы.Партнер
	|	
	|	ИМЕЮЩИЕ
	|		СУММА(БонусныеБаллы.СуммаОплаты) > 0) КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Регистратор.Дата, ДЕНЬ)
	|";
	
КонецФункции

Функция ЗапросДвиженияСписанияСгоревшихБонусныхБалловВозвратОплаты()
	
	Возврат "
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	ОплатыБонуснымиБаллами.Период КАК Период,
	|	СУММА(-БонусныеБаллы.Начислено) КАК СуммаСписанияНачисления
	|ПОМЕСТИТЬ ДвиженияСписанияСгоревшихБонусныхБаллов
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияРаспределенныхОплатБонуснымиБаллами КАК ОплатыБонуснымиБаллами
	|		ПО БонусныеБаллы.БонуснаяПрограммаЛояльности = ОплатыБонуснымиБаллами.БонуснаяПрограммаЛояльности
	|			И БонусныеБаллы.Партнер = ОплатыБонуснымиБаллами.Партнер
	|			И БонусныеБаллы.ДатаНачалаДействия = ОплатыБонуснымиБаллами.ДатаНачалаДействия
	|			И БонусныеБаллы.Период = ОплатыБонуснымиБаллами.ДатаКСписанию
	|ГДЕ
	|	БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И БонусныеБаллы.Начислено < 0
	|	И БонусныеБаллы.КСписанию < 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	ОплатыБонуснымиБаллами.Период
	|";
	
КонецФункции

Процедура РаспределитьВозвратыОплатПоРегистраторамСписание(РегистраторВозвратаОплаты, ДанныеИзменены)
	
	Блокировка = Новый БлокировкаДанных;
							
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
	ЭлементБлокировки.УстановитьЗначение("Регистратор", РегистраторВозвратаОплаты.Регистратор);
	
	Блокировка.Заблокировать();
	
	НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = РегистраторВозвратаОплаты.Регистратор;
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
	НаборЗаписейРНБонусныеБаллы.Прочитать();
	
	Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
		
		Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = РегистраторВозвратаОплаты.БонуснаяПрограммаЛояльности
			И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = РегистраторВозвратаОплаты.Партнер
			И СтрокаНаборЗаписейРНБонусныеБаллы.Период = РегистраторВозвратаОплаты.Период Тогда
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
			ДанныеИзменены = Истина;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ДвижениеСторноВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
	ЗаполнитьЗначенияСвойств(ДвижениеСторноВозвратаОплаты, РегистраторВозвратаОплаты);
	ДвижениеСторноВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Расход;
	ДвижениеСторноВозвратаОплаты.Начислено = РегистраторВозвратаОплаты.СуммаВозврата;
	
	НаборЗаписейРНБонусныеБаллы.Записать();
	
КонецПроцедуры

Функция РаспределитьВозвратыОплатПоРегистраторамПреобразованиеВОплату(РегистраторВозвратаОплаты,
	РаспределеныеОплатыПоВозврату, ВозвратОплатыСнятьСОбработки, СуммаВозвратаКРаспределению, СуммаОплатыБессрочными, ДанныеИзменены)
	
	СуммаРегистратораКРаспределению = Мин(СуммаВозвратаКРаспределению, РегистраторВозвратаОплаты.СуммаВозврата);
	СуммаРегистратораКРаспределениюБессрочными = Мин(СуммаРегистратораКРаспределению, СуммаОплатыБессрочными);
	СуммаРегистратораКРаспределениюСрочными = СуммаРегистратораКРаспределению - СуммаОплатыБессрочными;
	
	Блокировка = Новый БлокировкаДанных;
							
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
	ЭлементБлокировки.УстановитьЗначение("Регистратор", РегистраторВозвратаОплаты.Регистратор);
	
	Блокировка.Заблокировать();
	
	НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = РегистраторВозвратаОплаты.Регистратор;
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
	НаборЗаписейРНБонусныеБаллы.Прочитать();
	
	Если ВозвратОплатыСнятьСОбработки Тогда
		
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = РегистраторВозвратаОплаты.БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = РегистраторВозвратаОплаты.Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = РегистраторВозвратаОплаты.Период Тогда
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				ДанныеИзменены = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Если возвращаемые оплаты были распределены на бессрочные бонусные баллы, в первую очередь
	// покупателю нужно вернуть бонусные баллы как бессрочны
	Если СуммаРегистратораКРаспределениюБессрочными > 0 Тогда
		// Сторно возврата оплаты
		ДвижениеСторноВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеСторноВозвратаОплаты, РегистраторВозвратаОплаты);
		ДвижениеСторноВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Расход;
		ДвижениеСторноВозвратаОплаты.Начислено = СуммаРегистратораКРаспределениюБессрочными;
		Если Не ВозвратОплатыСнятьСОбработки Тогда
			ДвижениеСторноВозвратаОплаты.ДатаКОбработке = ТекущаяДатаСеанса();
		КонецЕсли;
		
		// Преобразование суммы вовзрата оплаты в начисление бессрочных бонусных баллов
		ДвижениеНачислениеВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(ДвижениеНачислениеВозвратаОплаты, РегистраторВозвратаОплаты);
		ДвижениеНачислениеВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Приход;
		ДвижениеНачислениеВозвратаОплаты.Период = НачалоДня(РегистраторВозвратаОплаты.ДатаВозврата);
		ДвижениеНачислениеВозвратаОплаты.Начислено = СуммаРегистратораКРаспределениюБессрочными;
		
		СуммаОплатыБессрочными = СуммаОплатыБессрочными - СуммаРегистратораКРаспределениюБессрочными;
		
		ДанныеИзменены = Истина;
	КонецЕсли;
	
	// Если осталась сумма возврата оплаты к распределению и возвращаемые оплаты были распределены на
	// срочные бонусные баллы, покупателю возвращаются бонусыне баллы как срочные с периодом действия
	// бонусных баллов, на которые возвращаемая оплата была распределена. Приоритет обработки соответствуют
	// распределению оплат - приоритет по дате окочания действия.
	Если СуммаРегистратораКРаспределениюСрочными > 0 Тогда
		
		//вот это расходные данные, они уменьшаются по мере распределения
		Для Каждого ДанныеРаспределеннойОплаты Из РаспределеныеОплатыПоВозврату Цикл
			Если ДанныеРаспределеннойОплаты.СуммаВозврата = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СуммаРаспределеннойОплаты = Мин(СуммаРегистратораКРаспределениюСрочными, ДанныеРаспределеннойОплаты.СуммаВозврата);
			
			// Сторно возврата оплаты
			ДвижениеСторноВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеСторноВозвратаОплаты, РегистраторВозвратаОплаты);
			ДвижениеСторноВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Расход;
			ДвижениеСторноВозвратаОплаты.Начислено = СуммаРаспределеннойОплаты;
			Если Не ВозвратОплатыСнятьСОбработки Тогда
				ДвижениеСторноВозвратаОплаты.ДатаКОбработке = ТекущаяДатаСеанса();
			КонецЕсли;
			
			// Преобразование суммы вовзрата оплаты в начисление бессрочных бонусных баллов
			ДвижениеНачислениеВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеНачислениеВозвратаОплаты, РегистраторВозвратаОплаты);
			ДвижениеНачислениеВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеНачислениеВозвратаОплаты.Период = НачалоДня(РегистраторВозвратаОплаты.ДатаВозврата);
			ДвижениеНачислениеВозвратаОплаты.Начислено = СуммаРаспределеннойОплаты;
			ДвижениеНачислениеВозвратаОплаты.ДатаНачалаДействия = НачалоДня(РегистраторВозвратаОплаты.ДатаВозврата);
			ДвижениеНачислениеВозвратаОплаты.ДатаКОбработке = ТекущаяДатаСеанса();
			
			ДвижениеНачислениеВозвратаОплаты = НаборЗаписейРНБонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(ДвижениеНачислениеВозвратаОплаты, РегистраторВозвратаОплаты);
			ДвижениеНачислениеВозвратаОплаты.ВидДвижения = ВидДвиженияНакопления.Приход;
			ДвижениеНачислениеВозвратаОплаты.Период = НачалоДня(РегистраторВозвратаОплаты.ДатаВозврата)
				+ ДанныеРаспределеннойОплаты.ПериодДействия * 24*60*60;
			ДвижениеНачислениеВозвратаОплаты.КСписанию = СуммаРаспределеннойОплаты;
			ДвижениеНачислениеВозвратаОплаты.ДатаНачалаДействия = НачалоДня(РегистраторВозвратаОплаты.ДатаВозврата);
			ДвижениеНачислениеВозвратаОплаты.ДатаКОбработке = ТекущаяДатаСеанса();
			
			СуммаРегистратораКРаспределениюСрочными = СуммаРегистратораКРаспределениюСрочными - СуммаРаспределеннойОплаты;
			ДанныеРаспределеннойОплаты.СуммаВозврата = ДанныеРаспределеннойОплаты.СуммаВозврата - СуммаРаспределеннойОплаты;
			
			ДанныеИзменены = Истина;
			
			Если СуммаРегистратораКРаспределениюСрочными = 0 Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	НаборЗаписейРНБонусныеБаллы.Записать();
	
	СуммаВозвратаКРаспределению = СуммаВозвратаКРаспределению
		- СуммаРегистратораКРаспределениюБессрочными
		- СуммаРегистратораКРаспределениюСрочными;
		
	ПродолжитьРаспределение = Истина;
	Если СуммаВозвратаКРаспределению = 0 Тогда
		ПродолжитьРаспределение = Ложь;
	КонецЕсли;
	
	Возврат ПродолжитьРаспределение;
	
КонецФункции

#КонецОбласти

#Область РаспределениеСписанныхСрочныхБонусныхБалловВСтатусеКОбработке

Функция ОбработатьОплатыИНачисленияСрочныхБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5)
	
	РаспределениеУспешно = Истина;
	
	КоличествоОбработанныхДанных = 0;
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегламентноеЗадание.РаспределениеСрочныхБонусныхБаллов.РаспределитьСписаниеСрочныхБонусныхБалловВСтатусеКОбработке_х100");
	
	ПовторитьРаспределение = Истина;
	Пока ПовторитьРаспределение Цикл
		ТекстОшибки = "";
		
		КоличествоОбработанныхДокументов = РаспределитьСписаниеСрочныхБонусныхБалловВСтатусеКОбработке(
			ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,,
			ТекстОшибки);
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + КоличествоОбработанныхДокументов;
		
		ПовторитьРаспределение = НЕ (КоличествоОбработанныхДокументов = 0);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			РаспределениеУспешно = Ложь;
			ТекстСообщения = НСтр("ru = 'При выполнении распределения списания срочных баллов произошла ошибка: %1';
									|en = 'An error occurred when allocating the write-off of fixed-term bonus points: %1'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1", ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
											|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , ,
				ТекстСообщения);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, Окр(КоличествоОбработанныхДанных/100));
	
	Возврат РаспределениеУспешно;
	
КонецФункции

// Распределить списание срочных бонусных баллов в статусе к обработке.
// 
// Параметры:
//  ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5 - Дата - Дата начала работы механизма бонусных баллов 2_5
//  РазмерПорции - Число - Количество записей к обработке
//  ТекстОшибки - Строка - Текст сообщения об ошибке
// 
// Возвращаемое значение:
//  Число - Количество обработнных документов, к которым было применено распределение.
Функция РаспределитьСписаниеСрочныхБонусныхБалловВСтатусеКОбработке(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
	РазмерПорции = Неопределено, ТекстОшибки = "")
	
	РезультатЗапроса = РезультатЗапросаСписаниеБонусныхБалловВСтатусеКОбработке(
		ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
		РазмерПорции);
	КоличествоПакетов = РезультатЗапроса.Количество();
	
	РезультатЗапросаСписание = РезультатЗапроса[КоличествоПакетов - 6];
	РезультатЗапросаОстатки = РезультатЗапроса[КоличествоПакетов - 5];
	РезультатЗапросаВозвратыНачислений = РезультатЗапроса[КоличествоПакетов - 4];
	РезультатЗапросаВозвратыСписаний = РезультатЗапроса[КоличествоПакетов - 3];
	РезультатЗапросаРаспределенныеСписания = РезультатЗапроса[КоличествоПакетов - 2];
	РезультатЗапросаНачальныйОстаток = РезультатЗапроса[КоличествоПакетов - 1];
	
	ТаблицаОстатковПоПартнеру = Новый ТаблицаЗначений;
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("Регистратор");
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("Период");
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("ОстатокСрочныхБонусныхБаллов");
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("ДатаНачалаДействия");
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("БонуснаяПрограммаЛояльности");
	ТаблицаОстатковПоПартнеру.Колонки.Добавить("Партнер");
	
	ТаблицаВозвратовНачислений = РезультатЗапросаВозвратыНачислений.Выгрузить();
	ТаблицаСписаний = РезультатЗапросаСписание.Выгрузить();
	ТаблицаВозвратов = РезультатЗапросаВозвратыСписаний.Выгрузить();
	ТаблицаРаспределенныхСписаний = РезультатЗапросаРаспределенныеСписания.Выгрузить();
	ТаблицаНачальныхОстатков = РезультатЗапросаНачальныйОстаток.Выгрузить();
	
	ВыборкаОстаткиБонуснаяПрограммаЛояльности = РезультатЗапросаОстатки.Выбрать(
		ОбходРезультатаЗапроса.ПоГруппировкам,
		"БонуснаяПрограммаЛояльности");
	
	КоличествоОбработанныхДокументов = 0;
	Пока ВыборкаОстаткиБонуснаяПрограммаЛояльности.Следующий() Цикл
		
		ВыборкаОстаткиПартнер = ВыборкаОстаткиБонуснаяПрограммаЛояльности.Выбрать(
			ОбходРезультатаЗапроса.ПоГруппировкам,
			"Партнер");
		
		Пока ВыборкаОстаткиПартнер.Следующий() Цикл
			
			СтруктураПоиска = Новый Структура("БонуснаяПрограммаЛояльности, Партнер");
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, ВыборкаОстаткиПартнер);
			
			// Получить таблицу остатков действующих срочных бонусных баллов
			ВыборкаОстаткиДетали = ВыборкаОстаткиПартнер.Выбрать();
			
			ТаблицаОстатковПоПартнеру.Очистить();
			Пока ВыборкаОстаткиДетали.Следующий() Цикл
				СтрокаТаблицаОстатковПоПартнеру = ТаблицаОстатковПоПартнеру.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаОстатковПоПартнеру, ВыборкаОстаткиДетали);
			КонецЦикла;
			
			// Получить сумму долга покупателя
			ТаблицаНачальныхОстатковПоПартнеру = ТаблицаНачальныхОстатков.Скопировать(СтруктураПоиска);
			СуммаДолгаПокупателя = ТаблицаНачальныхОстатковПоПартнеру.Итог("НачисленоОстаток");
			Если ТаблицаНачальныхОстатковПоПартнеру.Итог("КСписаниюОстаток") <> 0 Тогда
				СуммаДолгаПокупателя = 0;
			КонецЕсли;
			ЕстьДолгПокупателя = ?(СуммаДолгаПокупателя = 0, Ложь, Истина);
			
			// Если начисление еще действует, не распределять догл и остальные расходы
			// и ожидать окончания действия начисления
			Если ЕстьДолгПокупателя
				И ТаблицаОстатковПоПартнеру.Количество() > 0 Тогда
				
				ТаблицаОстатковПоПартнеру.Сортировать("Период");
				Если ТаблицаОстатковПоПартнеру[0].Период > НачалоДня(ТекущаяДатаСеанса()) Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			
			// Получить таблица возвратов начислений бллов к обработке
			ТаблицаВозвратовНачисленийПоПартнеру = ТаблицаВозвратовНачислений.Скопировать(СтруктураПоиска);
			ТаблицаВозвратовНачисленийПоПартнеру.Сортировать("Период");
			
			// Получить таблицу списаний бонусных баллов к обработке
			ТаблицаСписанийПоПартнеру = ТаблицаСписаний.Скопировать(СтруктураПоиска);
			ТаблицаСписанийПоПартнеру.Сортировать("ДатаСписанияБонусныхБаллов");
			
			// Получить таблицу возвратов бонусных баллов к обработке
			ТаблицаВозвратовПоПартнеру = ТаблицаВозвратов.Скопировать(СтруктураПоиска);
			СуммаВозвратовПоПартнеру = ТаблицаВозвратовПоПартнеру.Итог("СуммаВозвратаБонусныхБаллов");
			
			НачатьТранзакцию();
				
			Попытка
				
				ДанныеИзменены = Ложь;
				
				УменьшитьСуммыНачисленийБонусныхБалловНаСуммуВозврата(
					ТаблицаОстатковПоПартнеру,
					ТаблицаВозвратовНачисленийПоПартнеру);
				
				РаспределитьДолгПокупателяПоОстаткамНачисленийБонусныхБаллов(
					СуммаДолгаПокупателя,
					ТаблицаОстатковПоПартнеру,
					ДанныеИзменены);
				
				// Распределить списание срочных бонусных баллов
				Для Каждого СтрокаТаблицаСписанийПоПартнеру Из ТаблицаСписанийПоПартнеру Цикл
					
					// Получить таблицу сумм списаний, которые были распределены на начисления срочных бонусных баллов,
					// срок действия которых завершен
					СтруктураПоиска = Новый Структура("БонуснаяПрограммаЛояльности, Партнер, ДокументСписанияБонусныхБаллов");
					ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаСписанийПоПартнеру);
					
					ТаблицаРаспределенныхСписанийПоРегистратору = ТаблицаРаспределенныхСписаний.Скопировать(СтруктураПоиска);
					СуммаРаспределенныхБалловПоРегистратору = ТаблицаРаспределенныхСписанийПоРегистратору.Итог("СуммаСписанияБонусныхБаллов");
					
					СуммаСписанияПоРегистраторуОстаток
						= СтрокаТаблицаСписанийПоПартнеру.СуммаСписанияБонусныхБаллов
						- СуммаРаспределенныхБалловПоРегистратору;
						
					// Снять с распределения возвращенные возвраты оплат
					СуммаВозвратаПоРегистраторуСписания = Мин(
						СуммаСписанияПоРегистраторуОстаток,
						СуммаВозвратовПоПартнеру);
					СуммаВозвратовПоПартнеру = СуммаВозвратовПоПартнеру - СуммаВозвратаПоРегистраторуСписания;
					
					ЗачестьСуммуВозвратаВСуммеСписанияБонусныхБаллов(
						СтрокаТаблицаСписанийПоПартнеру,
						ТаблицаВозвратовПоПартнеру,
						СуммаВозвратаПоРегистраторуСписания,
						СуммаСписанияПоРегистраторуОстаток,
						ДанныеИзменены);
						
					СнятьРегистраторыДолгаПокупателяСОбраотки(
						ЕстьДолгПокупателя,
						СуммаДолгаПокупателя,
						СтрокаТаблицаСписанийПоПартнеру,
						ТаблицаОстатковПоПартнеру,
						ДанныеИзменены);
					
					ТаблицаОстатковПоПартнеру.Сортировать("Период");
					РаспределитьСуммуСписанияПоСрокамДействияБонусныхБаллов(
						СтрокаТаблицаСписанийПоПартнеру,
						ТаблицаОстатковПоПартнеру,
						ДанныеИзменены);
					
					Если ДанныеИзменены Тогда
						КоличествоОбработанныхДокументов = КоличествоОбработанныхДокументов + 1;
						ДанныеИзменены = Ложь;
					КонецЕсли;
				
				КонецЦикла;
				
				// Снять обработанные документы с регистрации
				СнятьОбработанныеДокументыСРегистрации(
					ТаблицаВозвратовНачисленийПоПартнеру,
					ТаблицаОстатковПоПартнеру,
					ТаблицаСписанийПоПартнеру);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				Если ТранзакцияАктивна() Тогда
					ОтменитьТранзакцию();
				КонецЕсли;
				ИнформацияОбОшибке = ИнформацияОбОшибке();
				ТекстОшибки = ИнформацияОбОшибке.Описание;
				Прервать;
				
			КонецПопытки;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат КоличествоОбработанныхДокументов;
	
КонецФункции

// Возвращает результат запроса, содержащий списанные бонусные баллы в статусе к обработке.
// 
// Параметры:
//  ДатаСреза - Дата - Дата начала работы механизма бонусных баллов 2_5
//  РазмерПорции - Число - Количество записей к обработке. По умолчанию = 1000
// 
// Возвращаемое значение:
//  РезультатЗапроса - Результат запроса, содержащий списанные бонусные баллы к обработке
//
Функция РезультатЗапросаСписаниеБонусныхБалловВСтатусеКОбработке(ДатаСреза, РазмерПорции = Неопределено)
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	БонусныеБаллы.Регистратор.МоментВремени КАК РегистраторМоментВремени,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Период, ДЕНЬ) КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.Регистратор КАК ДокументСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК СуммаСписанияБонусныхБаллов
	|ПОМЕСТИТЬ СписаниеБонусныхБалловКОбработке
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.Начислено > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Регистратор.МоментВремени,
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Период, ДЕНЬ),
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БонусныеБаллы.ДатаСписанияБонусныхБаллов КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер
	|ПОМЕСТИТЬ ПериодыВозвратовБонусныхБаллов
	|ИЗ
	|	СписаниеБонусныхБалловКОбработке КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Период, ДЕНЬ) КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.Регистратор КАК ДокументВозвратаБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(-БонусныеБаллы.Начислено) КАК СуммаВозвратаБонусныхБаллов
	|ПОМЕСТИТЬ ВозвратыБонусныхБалловКОбработкеНовый
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.Начислено < 0
	|	И (НАЧАЛОПЕРИОДА(БонусныеБаллы.Период, ДЕНЬ), БонуснаяПрограммаЛояльности, Партнер) В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			БонусныеБаллы.ДатаСписанияБонусныхБаллов,
	|			БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|			БонусныеБаллы.Партнер
	|		ИЗ ПериодыВозвратовБонусныхБаллов КАК БонусныеБаллы
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(БонусныеБаллы.Период, ДЕНЬ),
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаСписанияБонусныхБаллов КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.ДокументСписанияБонусныхБаллов КАК ДокументСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.СуммаСписанияБонусныхБаллов) КАК СуммаСписанияБонусныхБаллов
	|ПОМЕСТИТЬ ПериодыСписанийБонусныхБаллов
	|ИЗ
	|	СписаниеБонусныхБалловКОбработке КАК БонусныеБаллы
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.ДокументСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.Регистратор КАК ДокументСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.КСписанию) КАК СуммаСписанияБонусныхБаллов
	|ПОМЕСТИТЬ СписанияНаЗавершенныеНачисления
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке = ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ВидДвижения = &ВидДвиженияРасход
	|	И БонусныеБаллы.КСписанию <> 0
	|	И (БонусныеБаллы.Регистратор, БонусныеБаллы.БонуснаяПрограммаЛояльности, БонусныеБаллы.Партнер) В (
	|		ВЫБРАТЬ
	|			БонусныеБаллы.ДокументСписанияБонусныхБаллов КАК ДокументСписанияБонусныхБаллов,
	|			БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|			БонусныеБаллы.Партнер КАК Партнер
	|		ИЗ
	|			ПериодыСписанийБонусныхБаллов КАК БонусныеБаллы
	|	)
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	БонусныеБаллы.Период КАК Период
	|ПОМЕСТИТЬ НачалоВыборки
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|
	|УПОРЯДОЧИТЬ ПО
	|	Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено > 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""Начисление""
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию > 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""НачислениеСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ВидОперации,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(БонусныеБаллы.Начислено) КАК Начислено,
	|	СУММА(БонусныеБаллы.КСписанию) КАК КСписанию,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ДвиженияНачисленийБонусныхБаллов
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачалоВыборки КАК НачалоВыборки
	|	ПО БонусныеБаллы.Период >= НачалоВыборки.Период
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И ((БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено > 0
	|				И БонусныеБаллы.КСписанию = 0)
	|		ИЛИ (БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию > 0
	|				И БонусныеБаллы.Начислено = 0)
	|	И (БонуснаяПрограммаЛояльности, Партнер) В (
	|		ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|			БонусныеБаллы.Партнер
	|		ИЗ ПериодыВозвратовБонусныхБаллов КАК БонусныеБаллы))
	|				
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.Начислено > 0
	|				И БонусныеБаллы.КСписанию = 0
	|			ТОГДА ""Начисление""
	|		КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				И БонусныеБаллы.КСписанию > 0
	|				И БонусныеБаллы.Начислено = 0
	|			ТОГДА ""НачислениеСписание""
	|		ИНАЧЕ """"
	|	КОНЕЦ,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.Начислено КАК Начислено,
	|	БонусныеБаллыСписание.КСписанию КАК КСписанию,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллыСписание.Период КАК ДатаКСписанию,
	|	БонусныеБаллы.Регистратор КАК Регистратор
	|ПОМЕСТИТЬ ДвиженияНачисленийБонусныхБалловКОбработке
	|ИЗ
	|	ДвиженияНачисленийБонусныхБаллов КАК БонусныеБаллы
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДвиженияНачисленийБонусныхБаллов КАК БонусныеБаллыСписание
	|	ПО БонусныеБаллы.Период = БонусныеБаллыСписание.ДатаНачалаДействия
	|		И БонусныеБаллы.Регистратор = БонусныеБаллыСписание.Регистратор
	|		И БонусныеБаллы.БонуснаяПрограммаЛояльности = БонусныеБаллыСписание.БонуснаяПрограммаЛояльности
	|		И БонусныеБаллы.Партнер = БонусныеБаллыСписание.Партнер
	|		И БонусныеБаллы.ВидОперации = ""Начисление""
	|		И БонусныеБаллыСписание.ВидОперации = ""НачислениеСписание""
	|	
	|ГДЕ
	|	БонусныеБаллыСписание.Период <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ДатаНачалаДействия <> ДАТАВРЕМЯ(1, 1, 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 	
	|	БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	Партнер КАК Партнер,
	|	МИНИМУМ(ДатаНачалаДействия) КАК ДатаНачалаДействия
	|ПОМЕСТИТЬ ПервыеСрезыОстатков
	|ИЗ
	|	ДвиженияНачисленийБонусныхБалловКОбработке КАК БонусныеБаллы
	|	
	|СГРУППИРОВАТЬ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ДатаНачалаДействия
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	МИНИМУМ(ДатаНачалаДействия) КАК МинимальныйПериодОстатка
	|ИЗ
	|	ПервыеСрезыОстатков
	|
	|ИМЕЮЩИЕ
	|	НЕ МИНИМУМ(ДатаНачалаДействия) ЕСТЬ NULL
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияНачисленийБонусныхБалловКОбработке
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДвиженияНачисленийБонусныхБаллов
	|;";
	Запрос.УстановитьПараметр("ДатаНачалаРаботыМеханизмаБонусныхБаллов", ДатаСреза);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ВидДвиженияРасход", ВидДвиженияНакопления.Расход);
	
	Если РазмерПорции <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	ЗапросПредварительныхДанных = Запрос.ВыполнитьПакет();
	КоличествоЗапросов = ЗапросПредварительныхДанных.Количество();
	
	ТребуетсяРассчитатьНачальныйОстаток = Ложь;
	ПериодРаннегоОстатка = ТекущаяДатаСеанса();
	ВыборкаЗапроса = ЗапросПредварительныхДанных[КоличествоЗапросов - 3].Выбрать();
	Если ВыборкаЗапроса.Следующий() Тогда
		Если ЗначениеЗаполнено(ВыборкаЗапроса.МинимальныйПериодОстатка) Тогда
			ПериодРаннегоОстатка = ВыборкаЗапроса.МинимальныйПериодОстатка;
			ТребуетсяРассчитатьНачальныйОстаток = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ОстаткиНаНачалоПериодов.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ОстаткиНаНачалоПериодов.Партнер КАК Партнер,
	|	МАКСИМУМ(ОстаткиНаНачалоПериодов.ПериодОстатка) КАК ПериодОстатка,
	|	СУММА(ОстаткиНаНачалоПериодов.НачисленоОстаток) КАК НачисленоОстаток,
	|	СУММА(ОстаткиНаНачалоПериодов.КСписаниюОстаток) КАК КСписаниюОстаток
	|ПОМЕСТИТЬ ОстаткиНаНачалаПериодов 
	|ИЗ 
	|	(ВЫБРАТЬ
	|		ОстаткиБалловОбщие.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ОстаткиБалловОбщие.Партнер КАК Партнер,
	|		Неопределено КАК Период,
	|		Неопределено КАК ДатаНачалаДействия,
	|		НАЧАЛОПЕРИОДА(&ПериодРаннегоОстатка, ДЕНЬ) КАК ПериодОстатка,
	|		ОстаткиБалловОбщие.НачисленоОстаток КАК НачисленоОстаток,
	|		ОстаткиБалловОбщие.КСписаниюОстаток КАК КСписаниюОстаток
	|	ИЗ РегистрНакопления.БонусныеБаллы.Остатки(&ПериодРаннегоОстатка, 
	|		(БонуснаяПрограммаЛояльности, Партнер) В (
	|			ВЫБРАТЬ
	|				ПервыеСрезыОстатков.БонуснаяПрограммаЛояльности,
	|				ПервыеСрезыОстатков.Партнер
	|			ИЗ
	|				ПервыеСрезыОстатков)) КАК ОстаткиБалловОбщие
	|		ГДЕ
	|			&ТребуетсяРассчитатьНачальныйОстаток
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ДельтыОстатков.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ДельтыОстатков.Партнер КАК Партнер,
	|		ДельтыОстатков.Период КАК Период,
	|		ПериодыОстатков.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|		ПериодыОстатков.ДатаНачалаДействия КАК ПериодОстатка,
	|		ВЫБОР
	|			КОГДА ДельтыОстатков.ВидДвижения = &ВидДвиженияПриход
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ДельтыОстатков.Начислено КАК НачисленоОстаток,
	|		ВЫБОР
	|			КОГДА ДельтыОстатков.ВидДвижения = &ВидДвиженияПриход 
	|				ТОГДА 1
	|			ИНАЧЕ -1
	|		КОНЕЦ * ДельтыОстатков.КСписанию КАК КСписаниюОстаток
	|	ИЗ
	|		РегистрНакопления.БонусныеБаллы КАК ДельтыОстатков
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПервыеСрезыОстатков КАК ПериодыОстатков
	|		ПО ПериодыОстатков.БонуснаяПрограммаЛояльности = ДельтыОстатков.БонуснаяПрограммаЛояльности
	|			И ПериодыОстатков.Партнер = ДельтыОстатков.Партнер
	|			И ПериодыОстатков.ДатаНачалаДействия > ДельтыОстатков.Период
	|	ГДЕ
	|		&ТребуетсяРассчитатьНачальныйОстаток
	|		И ДельтыОстатков.Период > &ПериодРаннегоОстатка) КАК ОстаткиНаНачалоПериодов
	|
	|СГРУППИРОВАТЬ ПО
	|	ОстаткиНаНачалоПериодов.БонуснаяПрограммаЛояльности,
	|	ОстаткиНаНачалоПериодов.Партнер
	|
	|ИМЕЮЩИЕ 
	|	СУММА(ОстаткиНаНачалоПериодов.НачисленоОстаток) < 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер,
	|	ПериодОстатка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СписаниеБонусныхБалловКОбработке.ДатаСписанияБонусныхБаллов КАК ДатаСписанияБонусныхБаллов,
	|	СписаниеБонусныхБалловКОбработке.ДокументСписанияБонусныхБаллов КАК ДокументСписанияБонусныхБаллов,
	|	СписаниеБонусныхБалловКОбработке.ДокументСписанияБонусныхБаллов.Дата КАК ДокументСписанияБонусныхБалловДата,
	|	СписаниеБонусныхБалловКОбработке.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	СписаниеБонусныхБалловКОбработке.БонуснаяПрограммаЛояльности.Наименование КАК БонуснаяПрограммаЛояльностиНаименование,
	|	СписаниеБонусныхБалловКОбработке.Партнер КАК Партнер,
	|	СписаниеБонусныхБалловКОбработке.Партнер.Наименование КАК ПартнерНаименование,
	|	СписаниеБонусныхБалловКОбработке.СуммаСписанияБонусныхБаллов КАК СуммаСписанияБонусныхБаллов
	|ИЗ
	|	СписаниеБонусныхБалловКОбработке КАК СписаниеБонусныхБалловКОбработке
	|
	|УПОРЯДОЧИТЬ ПО
	|	СписаниеБонусныхБалловКОбработке.РегистраторМоментВремени,
	|	ДокументСписанияБонусныхБалловДата,
	|	ПартнерНаименование,
	|	БонуснаяПрограммаЛояльностиНаименование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(ВЫБОР
	|			КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				ТОГДА БонусныеБаллы.КСписанию
	|			ИНАЧЕ -БонусныеБаллы.КСписанию
	|		КОНЕЦ) КАК ОстатокСрочныхБонусныхБаллов,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.КСписанию <> 0
	|	И (БонусныеБаллы.Партнер, БонусныеБаллы.БонуснаяПрограммаЛояльности) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СписаниеБонусныхБалловКОбработке.Партнер,
	|				СписаниеБонусныхБалловКОбработке.БонуснаяПрограммаЛояльности
	|			ИЗ
	|				СписаниеБонусныхБалловКОбработке КАК СписаниеБонусныхБалловКОбработке
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ОстаткиНаНачалаПериодов.Партнер,
	|				ОстаткиНаНачалаПериодов.БонуснаяПрограммаЛояльности
	|			ИЗ
	|				ОстаткиНаНачалаПериодов КАК ОстаткиНаНачалаПериодов)
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				ТОГДА БонусныеБаллы.КСписанию
	|			ИНАЧЕ -БонусныеБаллы.КСписанию
	|		КОНЕЦ) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	БонусныеБаллы.Период
	|ИТОГИ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(-БонусныеБаллы.КСписанию) КАК СуммаВозвратаНачисления,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.КСписанию < 0
	|	И (БонусныеБаллы.Партнер, БонусныеБаллы.БонуснаяПрограммаЛояльности) В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				СписаниеБонусныхБалловКОбработке.Партнер,
	|				СписаниеБонусныхБалловКОбработке.БонуснаяПрограммаЛояльности
	|			ИЗ
	|				СписаниеБонусныхБалловКОбработке КАК СписаниеБонусныхБалловКОбработке
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				ОстаткиНаНачалаПериодов.Партнер,
	|				ОстаткиНаНачалаПериодов.БонуснаяПрограммаЛояльности
	|			ИЗ
	|				ОстаткиНаНачалаПериодов КАК ОстаткиНаНачалаПериодов)
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|УПОРЯДОЧИТЬ ПО
	|	БонусныеБаллы.Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаСписанияБонусныхБаллов КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.ДокументВозвратаБонусныхБаллов КАК ДокументВозвратаБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.СуммаВозвратаБонусныхБаллов КАК СуммаВозвратаБонусныхБаллов
	|ИЗ
	|	ВозвратыБонусныхБалловКОбработкеНовый КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.ДатаСписанияБонусныхБаллов КАК ДатаСписанияБонусныхБаллов,
	|	БонусныеБаллы.ДокументСписанияБонусныхБаллов КАК ДокументСписанияБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.СуммаСписанияБонусныхБаллов КАК СуммаСписанияБонусныхБаллов
	|ИЗ
	|	СписанияНаЗавершенныеНачисления КАК БонусныеБаллы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиНаНачалаПериодов.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ОстаткиНаНачалаПериодов.Партнер КАК Партнер,
	|	ОстаткиНаНачалаПериодов.ПериодОстатка КАК ПериодОстатка,
	|	-ОстаткиНаНачалаПериодов.НачисленоОстаток КАК НачисленоОстаток,
	|	ОстаткиНаНачалаПериодов.КСписаниюОстаток КАК КСписаниюОстаток
	|ИЗ
	|	ОстаткиНаНачалаПериодов КАК ОстаткиНаНачалаПериодов
	|";
	Запрос.УстановитьПараметр("ДатаНачалаРаботыМеханизмаБонусныхБаллов", ДатаСреза);
	Запрос.УстановитьПараметр("ПериодРаннегоОстатка", ПериодРаннегоОстатка);
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	Запрос.УстановитьПараметр("ТребуетсяРассчитатьНачальныйОстаток", ТребуетсяРассчитатьНачальныйОстаток);
	
	Если РазмерПорции <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

Процедура ЗачестьСуммуВозвратаВСуммеСписанияБонусныхБаллов(СтрокаТаблицыСписаний,
	ТаблицаВозвратов, СуммаВозврата, СуммаСписания, ДанныеИзменены)
	
	Для Каждого СтрокаТаблицаВозвратов Из ТаблицаВозвратов Цикл
		
		Если СуммаВозврата = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаВозвратаПоРегистраторуВозврата = Мин(
			СуммаВозврата,
			СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов);
			
		// Снять регистратор возврата оплаты с обработки, если он полностью зачтен
		Если СуммаВозвратаПоРегистраторуВозврата = СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов Тогда
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаВозвратов.ДокументВозвратаБонусныхБаллов);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаВозвратов.ДокументВозвратаБонусныхБаллов;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
				
				Если Не (СтрокаТаблицаВозвратов.ДатаСписанияБонусныхБаллов = СтрокаНаборЗаписейРНБонусныеБаллы.Период
					И СтрокаТаблицаВозвратов.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
					И СтрокаТаблицаВозвратов.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер) Тогда
					Продолжить;
				КонецЕсли; 
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
			КонецЦикла;
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов
				= СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов - СуммаВозвратаПоРегистраторуВозврата;
			
			ДанныеИзменены = Истина;
			
		Иначе
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаВозвратов.ДокументВозвратаБонусныхБаллов);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаВозвратов.ДокументВозвратаБонусныхБаллов;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
				
				Если Не (СтрокаТаблицаВозвратов.ДатаСписанияБонусныхБаллов = СтрокаНаборЗаписейРНБонусныеБаллы.Период
					И СтрокаТаблицаВозвратов.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
					И СтрокаТаблицаВозвратов.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер) Тогда
					Продолжить;
				КонецЕсли; 
				
				Если СуммаВозвратаПоРегистраторуВозврата < СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов Тогда
					СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = - СуммаВозвратаПоРегистраторуВозврата;
				КонецЕсли;
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
			КонецЦикла;
			
			СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
			СтрокаНаборЗаписейРНБонусныеБаллы.Период = СтрокаТаблицаВозвратов.ДатаСписанияБонусныхБаллов;
			СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход;
			СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
				= СтрокаТаблицаВозвратов.БонуснаяПрограммаЛояльности;
			СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
				= СтрокаТаблицаВозвратов.Партнер;
			СтрокаНаборЗаписейРНБонусныеБаллы.Начислено
				= -(СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов - СуммаВозвратаПоРегистраторуВозврата);
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов
				= СтрокаТаблицаВозвратов.СуммаВозвратаБонусныхБаллов - СуммаВозвратаПоРегистраторуВозврата;
			
			ДанныеИзменены = Истина;
			
		КонецЕсли;
		
		// Снять регистратор списания (оплаты) с обработки, если он полностью зачтен
		МассивСтрокРаспределенныхСписанийКУдалению = Новый Массив;
		Если СуммаВозвратаПоРегистраторуВозврата = СуммаСписания Тогда
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицыСписаний.ДокументСписанияБонусныхБаллов);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицыСписаний.ДокументСписанияБонусныхБаллов;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
				
				Если Не (СтрокаТаблицыСписаний.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
					И СтрокаТаблицыСписаний.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаНаборЗаписейРНБонусныеБаллы.Начислено > 0
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1) Тогда
					
					СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				КонецЕсли;
				
				Если СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию > 0
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1) Тогда
					
					МассивСтрокРаспределенныхСписанийКУдалению.Добавить(СтрокаНаборЗаписейРНБонусныеБаллы);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого СтрокаМассиваСтрокКУдалению Из МассивСтрокРаспределенныхСписанийКУдалению Цикл
				НаборЗаписейРНБонусныеБаллы.Удалить(СтрокаМассиваСтрокКУдалению);
			КонецЦикла;
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			СтрокаТаблицыСписаний.СуммаСписанияБонусныхБаллов
				= СуммаСписания - СуммаВозвратаПоРегистраторуВозврата;
			
			ДанныеИзменены = Истина;
			
		Иначе
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицыСписаний.ДокументСписанияБонусныхБаллов);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицыСписаний.ДокументСписанияБонусныхБаллов;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			СуммаОстаткаНачисленоКРаспределению = СуммаСписания - СуммаВозвратаПоРегистраторуВозврата;
			Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
				
				Если Не (СтрокаТаблицыСписаний.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
					И СтрокаТаблицыСписаний.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
					Продолжить;
				КонецЕсли; 
				
				// Снимаем с распределения сумму оплаты, которая была ранее распределена и возвращена в текущем периоде распределения
				Если СтрокаНаборЗаписейРНБонусныеБаллы.Начислено > 0
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1) Тогда
					
					Если СуммаОстаткаНачисленоКРаспределению < СтрокаНаборЗаписейРНБонусныеБаллы.Начислено Тогда
						СтрокаНаборЗаписейРНБонусныеБаллы.Начислено
							= СтрокаНаборЗаписейРНБонусныеБаллы.Начислено
							- (СуммаСписания - СуммаВозвратаПоРегистраторуВозврата);
						СуммаОстаткаНачисленоКРаспределению = 0;
					Иначе
						СуммаОстаткаНачисленоКРаспределению
							= СуммаОстаткаНачисленоКРаспределению - СтрокаНаборЗаписейРНБонусныеБаллы.Начислено;
					КонецЕсли;
					
					СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				КонецЕсли;
				
				Если СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию > 0
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1) Тогда
					
					МассивСтрокРаспределенныхСписанийКУдалению.Добавить(СтрокаНаборЗаписейРНБонусныеБаллы);
					
				КонецЕсли;
				
			КонецЦикла;
			
			Для Каждого СтрокаМассиваСтрокКУдалению Из МассивСтрокРаспределенныхСписанийКУдалению Цикл
				НаборЗаписейРНБонусныеБаллы.Удалить(СтрокаМассиваСтрокКУдалению);
			КонецЦикла;
			
			СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
			СтрокаНаборЗаписейРНБонусныеБаллы.Период = СтрокаТаблицыСписаний.ДатаСписанияБонусныхБаллов;
			СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход;
			СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
				= СтрокаТаблицаВозвратов.БонуснаяПрограммаЛояльности;
			СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
				= СтрокаТаблицаВозвратов.Партнер;
			СтрокаНаборЗаписейРНБонусныеБаллы.Начислено
				= СуммаСписания
				- СуммаВозвратаПоРегистраторуВозврата;
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			СтрокаТаблицыСписаний.СуммаСписанияБонусныхБаллов
				= СуммаСписания - СуммаВозвратаПоРегистраторуВозврата;
			
		КонецЕсли;
		
		ДанныеИзменены = Истина;
		
	КонецЦикла;
	
КонецПроцедуры

// Распределить сумму списания по срокам действия бонусных баллов (бессрочные или срочные бонусные баллы).
// 
// Параметры:
//  СтрокаТаблицаСписанийПоПартнеру - СтрокаТаблицыЗначений - Данные по списанию бонусных баллов
//  ТаблицаОстатковПоПартнеру - ТаблицаЗначений - Таблица остатков действующих срочных бонусных баллов
//  ДанныеИзменены - Булево - Флаг изменения данных в двиежниях для корректного подсчета количества обработнанных
//  документов.
// 
// Возвращаемое значение:
//  Массив Из СтрокаТаблицыЗначений - Массив строк с нулевыми остатками бонусных баллов к удалению
// 
Процедура РаспределитьСуммуСписанияПоСрокамДействияБонусныхБаллов(
	СтрокаТаблицаСписанийПоПартнеру,
	ТаблицаОстатковПоПартнеру,
	ДанныеИзменены)
	
	// Переменные метода
	ДокументСписания = СтрокаТаблицаСписанийПоПартнеру.ДокументСписанияБонусныхБаллов;
	ДатаСписания     = СтрокаТаблицаСписанийПоПартнеру.ДатаСписанияБонусныхБаллов;
	
	Партнер  = СтрокаТаблицаСписанийПоПартнеру.Партнер;
	БонуснаяПрограммаЛояльности  = СтрокаТаблицаСписанийПоПартнеру.БонуснаяПрограммаЛояльности;
	
	СуммаСписанияБонусныхБалловСУчетомРаспределения = СтрокаТаблицаСписанийПоПартнеру.СуммаСписанияБонусныхБаллов;
	
	// Установка блокировки
	Блокировка = Новый БлокировкаДанных;
	
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
	ЭлементБлокировки.УстановитьЗначение("Регистратор", ДокументСписания);
	
	Блокировка.Заблокировать();
	
	// Распределение списания бонусных баллов по остаткам действующих срочных бонусных баллов
	НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = ДокументСписания;
	НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
	НаборЗаписейРНБонусныеБаллы.Прочитать();
	
	Для Каждого СтрокаТаблицаОстатковПоПартнеру Из ТаблицаОстатковПоПартнеру Цикл
		
		Если СуммаСписанияБонусныхБалловСУчетомРаспределения = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если ДатаСписания < СтрокаТаблицаОстатковПоПартнеру.ДатаНачалаДействия
			ИЛИ ДатаСписания > СтрокаТаблицаОстатковПоПартнеру.Период
			ИЛИ СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ОстатокСрочныхБонусныхБаллов = СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов;
		СуммаКорректировкиЗначенияКСписанию = Мин(ОстатокСрочныхБонусныхБаллов, СуммаСписанияБонусныхБалловСУчетомРаспределения);
		ОстатокСрочныхБонусныхБаллов = ОстатокСрочныхБонусныхБаллов - СуммаКорректировкиЗначенияКСписанию;
		
		СуммаСписанияБонусныхБалловСУчетомРаспределения = СуммаСписанияБонусныхБалловСУчетомРаспределения - СуммаКорректировкиЗначенияКСписанию;
		
		СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(
			СтрокаНаборЗаписейРНБонусныеБаллы,
			СтрокаТаблицаОстатковПоПартнеру);
		
		СтрокаНаборЗаписейРНБонусныеБаллы.Регистратор = ДокументСписания;
		СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = СуммаКорректировкиЗначенияКСписанию;
		СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
		
		СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов = ОстатокСрочныхБонусныхБаллов;
		
		ДанныеИзменены = Истина;
		
	КонецЦикла;
	
	Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
		Если СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)
			И СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
			И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
			И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено > 0 Тогда
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеИзменены Тогда
		НаборЗаписейРНБонусныеБаллы.Записать();
	КонецЕсли;
	
КонецПроцедуры

Процедура УменьшитьСуммыНачисленийБонусныхБалловНаСуммуВозврата(ТаблицаОстатковПоПартнеру, ТаблицаВозвратовНачисленийПоПартнеру)
	
	СуммаВозвратаНачисленнийПоПартнеру = ТаблицаВозвратовНачисленийПоПартнеру.Итог("СуммаВозвратаНачисления");
	
	Если ТаблицаВозвратовНачисленийПоПартнеру.Количество() > 0 Тогда
		
		ТаблицаОстатковПоПартнеру.Сортировать("Период");
		Для Каждого СтрокаТаблицаОстатковПоПартнеру Из ТаблицаОстатковПоПартнеру Цикл
			
			Для Каждого СтрокаТаблицаВозвратовНачисленийПоПартнеру Из ТаблицаВозвратовНачисленийПоПартнеру Цикл
				
				Если Не (СтрокаТаблицаВозвратовНачисленийПоПартнеру.Период = СтрокаТаблицаОстатковПоПартнеру.Период
					И СтрокаТаблицаВозвратовНачисленийПоПартнеру.ДатаНачалаДействия = СтрокаТаблицаОстатковПоПартнеру.ДатаНачалаДействия) Тогда
					Продолжить;
				КонецЕсли;
				
				СуммаВозвратаНачисленияКОбработке = Мин(
					СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов,
					СтрокаТаблицаВозвратовНачисленийПоПартнеру.СуммаВозвратаНачисления);
				
				СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов
					= СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов
					- СуммаВозвратаНачисленияКОбработке;
					
				СтрокаТаблицаВозвратовНачисленийПоПартнеру.СуммаВозвратаНачисления
					= СтрокаТаблицаВозвратовНачисленийПоПартнеру.СуммаВозвратаНачисления
					- СуммаВозвратаНачисленияКОбработке;
				
				СуммаВозвратаНачисленнийПоПартнеру = СуммаВозвратаНачисленнийПоПартнеру - СуммаВозвратаНачисленияКОбработке;
				
				Если СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если СуммаВозвратаНачисленнийПоПартнеру = 0 Тогда
				Прервать;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура РаспределитьДолгПокупателяПоОстаткамНачисленийБонусныхБаллов(СуммаДолгаПокупателя,
	ТаблицаОстатковПоПартнеру, ДанныеИзменены)
	
	КоличествоОбработанныхДокументов = 0;
	
	Если СуммаДолгаПокупателя > 0 Тогда
		
		ТаблицаОстатковПоПартнеру.Сортировать("Период");
		Для Каждого СтрокаТаблицаОстатковПоПартнеру Из ТаблицаОстатковПоПартнеру Цикл
			
			Если СуммаДолгаПокупателя = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			// Теперь нужно понять, какая сумма списывается от
			СуммаЗачетаДолгаПоНачислению = Мин(
				СуммаДолгаПокупателя,
				СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов);
				
			// Подложить сумму зачета под регистратор начисления
			Блокировка = Новый БлокировкаДанных;

			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаОстатковПоПартнеру.Регистратор);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаОстатковПоПартнеру.Регистратор;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаНаборЗаписейРНБонусныеБаллы, СтрокаТаблицаОстатковПоПартнеру);
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход;
			СтрокаНаборЗаписейРНБонусныеБаллы.Период = СтрокаТаблицаОстатковПоПартнеру.Период;
			СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = СуммаЗачетаДолгаПоНачислению;
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = СтрокаТаблицаОстатковПоПартнеру.ДатаНачалаДействия;
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов
				= СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов
				- СуммаЗачетаДолгаПоНачислению;
			СуммаДолгаПокупателя = СуммаДолгаПокупателя - СуммаЗачетаДолгаПоНачислению;
			
			ДанныеИзменены = Истина;
			
		КонецЦикла;
		
		Если ДанныеИзменены Тогда
			КоличествоОбработанныхДокументов = КоличествоОбработанныхДокументов + 1;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СнятьРегистраторыДолгаПокупателяСОбраотки(ЕстьДолгПокупателя, СуммаДолгаПокупателя,
	СтрокаТаблицаСписанийПоПартнеру, ТаблицаОстатковПоПартнеру, ДанныеИзменены)
	
	Если ЕстьДолгПокупателя Тогда
		
		СнятьРегистраторСписанияСОбработки = Ложь;
		
		ТаблицаОстатковПоПартнеру.Сортировать("Период");
		Для Каждого СтрокаТаблицаОстатковПоПартнеру Из ТаблицаОстатковПоПартнеру Цикл
			
			Если СтрокаТаблицаОстатковПоПартнеру.ДатаНачалаДействия <= СтрокаТаблицаСписанийПоПартнеру.ДатаСписанияБонусныхБаллов Тогда
				Прервать;
			КонецЕсли;
			
			СнятьРегистраторСписанияСОбработки = Истина;
			Прервать;
			
		КонецЦикла;
		
		Если СнятьРегистраторСписанияСОбработки Тогда
			
			Блокировка = Новый БлокировкаДанных;
			
			ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
			ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаСписанийПоПартнеру.ДокументСписанияБонусныхБаллов);
			
			Блокировка.Заблокировать();
			
			НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаСписанийПоПартнеру.ДокументСписанияБонусныхБаллов;
			НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
			НаборЗаписейРНБонусныеБаллы.Прочитать();
			
			Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
				
				Если Не (СтрокаТаблицаСписанийПоПартнеру.ДатаСписанияБонусныхБаллов = СтрокаНаборЗаписейРНБонусныеБаллы.Период
					И СтрокаТаблицаСписанийПоПартнеру.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
					И СтрокаТаблицаСписанийПоПартнеру.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
					И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
					Продолжить;
				КонецЕсли; 
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
			КонецЦикла;
			
			НаборЗаписейРНБонусныеБаллы.Записать();
			
			ДанныеИзменены = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СнятьОбработанныеДокументыСРегистрации(ТаблицаВозвратовНачисленийПоПартнеру,
	ТаблицаОстатковПоПартнеру, ТаблицаСписанийПоПартнеру)
	
	// Снять возвраты начислений, период действия которых завершен
	Для Каждого ДанныеВозвратаНачислений Из ТаблицаВозвратовНачисленийПоПартнеру Цикл
		
		Блокировка = Новый БлокировкаДанных;

		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", ДанныеВозвратаНачислений.Регистратор);
		
		Блокировка.Заблокировать();
		
		НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = ДанныеВозвратаНачислений.Регистратор;
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
		НаборЗаписейРНБонусныеБаллы.Прочитать();
		
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			
			Если Не (ДанныеВозвратаНачислений.Период <= НачалоДня(ТекущаяДатаСеанса())
				И ДанныеВозвратаНачислений.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
				И ДанныеВозвратаНачислений.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
		КонецЦикла;
		
		НаборЗаписейРНБонусныеБаллы.Записать();
		
	КонецЦикла;
	
	// Снять начисления, период действия которых завершен
	Для Каждого СтрокаТаблицаОстатковПоПартнеру Из ТаблицаОстатковПоПартнеру Цикл
		
		Блокировка = Новый БлокировкаДанных;

		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаОстатковПоПартнеру.Регистратор);
		
		Блокировка.Заблокировать();
		
		НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаОстатковПоПартнеру.Регистратор;
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
		НаборЗаписейРНБонусныеБаллы.Прочитать();
		
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			
			Если Не (СтрокаТаблицаОстатковПоПартнеру.Период <= НачалоДня(ТекущаяДатаСеанса())
				И СтрокаТаблицаОстатковПоПартнеру.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
				И СтрокаТаблицаОстатковПоПартнеру.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
		КонецЦикла;
		
		Если СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов > 0
			И СтрокаТаблицаОстатковПоПартнеру.Период <= НачалоДня(ТекущаяДатаСеанса()) Тогда
			
			СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(
				СтрокаНаборЗаписейРНБонусныеБаллы,
				СтрокаТаблицаОстатковПоПартнеру);
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Приход;
			СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = -СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов;
			СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = -СтрокаТаблицаОстатковПоПартнеру.ОстатокСрочныхБонусныхБаллов;
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
		КонецЕсли;
		
		НаборЗаписейРНБонусныеБаллы.Записать();
		
	КонецЦикла;
	
	// Снять оплаты (списания оплат по начислениям, период действия которых завершен)
	Для Каждого СтрокаТаблицаСписанийПоПартнеру Из ТаблицаСписанийПоПартнеру Цикл
		
		Блокировка = Новый БлокировкаДанных;

		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", СтрокаТаблицаСписанийПоПартнеру.ДокументСписанияБонусныхБаллов);
		
		Блокировка.Заблокировать();
		
		НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = СтрокаТаблицаСписанийПоПартнеру.ДокументСписанияБонусныхБаллов;
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
		НаборЗаписейРНБонусныеБаллы.Прочитать();
		
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			
			Если Не (СтрокаНаборЗаписейРНБонусныеБаллы.Период <= НачалоДня(ТекущаяДатаСеанса())
				И СтрокаТаблицаСписанийПоПартнеру.БонуснаяПрограммаЛояльности = СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности
				И СтрокаТаблицаСписанийПоПартнеру.Партнер = СтрокаНаборЗаписейРНБонусныеБаллы.Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию <> 0
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке <> Дата(1, 1, 1)) Тогда
				Продолжить;
			КонецЕсли; 
			
			СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
			
		КонецЦикла;
		
		НаборЗаписейРНБонусныеБаллы.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры

#КонецОбласти

#Область СписаниеОстатковСрочныхБонусныхБалловСИстекшимСрокомДействия

Процедура ОбработатьПрекратившиеДействияНачисленияСрочныхБонусныхБаллов(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5)
	
	КоличествоОбработанныхДанных = 0;
	Замер = ОценкаПроизводительности.НачатьЗамерДлительнойОперации(
		"РегламентноеЗадание.РаспределениеСрочныхБонусныхБаллов.СписатьОстаткиСрочныхБонусныхБалловСИстекшимСрокомДействия");
	
	ПовторитьСписание = Истина;
	Пока ПовторитьСписание Цикл
		ТекстОшибки = "";
		
		КоличествоОбработанныхДокументов = СписатьОстаткиСрочныхБонусныхБалловСИстекшимСрокомДействия(
			ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
			Неопределено,
			ТекстОшибки);
			
		КоличествоОбработанныхДанных = КоличествоОбработанныхДанных + КоличествоОбработанныхДокументов;
		ПовторитьСписание = НЕ (КоличествоОбработанныхДокументов = 0);
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			
			ТекстСообщения = НСтр("ru = 'При выполнении списания остатков срочных баллов произошла ошибка: %1';
									|en = 'An error occurred when writing off the balance of fixed-term bonus points: %1'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения,"%1", ТекстОшибки);
			
			ЗаписьЖурналаРегистрации(НСтр("ru = 'Распределение срочных бонусных баллов';
											|en = 'Allocate fixed-term bonus points'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка, , ,
				ТекстСообщения);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерДлительнойОперации(Замер, КоличествоОбработанныхДанных);
	
КонецПроцедуры

// Списывает остатки срочных бонусных баллов с истекшим сроком действия.
// 
// Параметры:
//  ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5 - Дата - Дата начала работы механизма бонусных баллов 2_5
//  РазмерПорции - Число - Количество записей к обработке
//  ТекстОшибки - Строка - Текст сообщения об ошибке
//  
// Возвращаемое значение:
//  Число - Количество обработанных документов, по которым были списаны остатки.
//
Функция СписатьОстаткиСрочныхБонусныхБалловСИстекшимСрокомДействия(ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
	РазмерПорции = Неопределено, ТекстОшибки = "")
	
	РезультатЗапроса = РезультатЗапросаОстаткиПросроченныхСрочныхБонусныхБаллов(
		ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
		РазмерПорции);
	
	РезультатЗапросаОстатки = РезультатЗапроса[1];
	РезультатЗапросаОстаткиПоДокументам = РезультатЗапроса[2];
	РезультатЗапросаДокументыКСнятиюСОбработки = РезультатЗапроса[3];
	
	ТаблицаОстатковКСписанию = Новый ТаблицаЗначений;
	ТаблицаОстатковКСписанию.Колонки.Добавить("БонуснаяПрограммаЛояльности");
	ТаблицаОстатковКСписанию.Колонки.Добавить("Партнер");
	ТаблицаОстатковКСписанию.Колонки.Добавить("ДатаНачалаДействияСрочныхБонусныхБаллов");
	ТаблицаОстатковКСписанию.Колонки.Добавить("ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов");
	ТаблицаОстатковКСписанию.Колонки.Добавить("ОстатокСрочныхБонусныхБаллов");
	
	ТаблицаОстатковПоДокументам = Новый ТаблицаЗначений;
	ТаблицаОстатковПоДокументам.Колонки.Добавить("БонуснаяПрограммаЛояльности");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("Партнер");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("Регистратор");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("ДатаКОбработке");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("ДатаНачалаДействия");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("Период");
	ТаблицаОстатковПоДокументам.Колонки.Добавить("КСписанию");
	
	ТаблицаДокументовКСнятиюСОбработки = Новый ТаблицаЗначений;
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("БонуснаяПрограммаЛояльности");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("Партнер");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("Регистратор");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("ДатаНачалаДействия");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("Период");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("КСписанию");
	ТаблицаДокументовКСнятиюСОбработки.Колонки.Добавить("ДатаКОбработке");
	
	ВыборкаОстаткиБонуснаяПрограммаЛояльности = РезультатЗапросаОстатки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "БонуснаяПрограммаЛояльности");
	ВыборкаОстаткиПоДокументамБонуснаяПрограммаЛояльности = РезультатЗапросаОстаткиПоДокументам.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "БонуснаяПрограммаЛояльности");
	ВыборкаДокументыКСнятиюСОбработкиБонуснаяПрограммаЛояльности = РезультатЗапросаДокументыКСнятиюСОбработки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "БонуснаяПрограммаЛояльности");
	
	КоличествоОбработанныхДокументов = 0;	
	Пока ВыборкаОстаткиБонуснаяПрограммаЛояльности.Следующий() Цикл
		
		ВыборкаОстаткиПартнер = ВыборкаОстаткиБонуснаяПрограммаЛояльности.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Партнер");
		
		Пока ВыборкаОстаткиПартнер.Следующий() Цикл
			
			// Получить таблицу остатков просроченных срочных бонусных баллов
			ВыборкаОстаткиДетали = ВыборкаОстаткиПартнер.Выбрать();
			
			ТаблицаОстатковКСписанию.Очистить();
			Пока ВыборкаОстаткиДетали.Следующий() Цикл
				СтрокаТаблицаОстатковКСписанию = ТаблицаОстатковКСписанию.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаТаблицаОстатковКСписанию, ВыборкаОстаткиДетали);
			КонецЦикла;
			
			// Получить таблицу начислений срочных бонусных баллов для списания просроченных срочных бонусных баллов
			
			СтруктураПоискаБонуснаяПрограммаЛояльности = Новый Структура;
			СтруктураПоискаБонуснаяПрограммаЛояльности.Вставить(
				"БонуснаяПрограммаЛояльности",
				ВыборкаОстаткиБонуснаяПрограммаЛояльности.БонуснаяПрограммаЛояльности);
				
			ВыборкаОстаткиПоДокументамБонуснаяПрограммаЛояльности.Сбросить();
			Если ВыборкаОстаткиПоДокументамБонуснаяПрограммаЛояльности.НайтиСледующий(СтруктураПоискаБонуснаяПрограммаЛояльности) Тогда
				
				ВыборкаОстаткиПоДокументамПартнер = ВыборкаОстаткиПоДокументамБонуснаяПрограммаЛояльности.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Партнер");
				
				СтруктураПоискаПартнер = Новый Структура;
				СтруктураПоискаПартнер.Вставить(
					"Партнер",
					ВыборкаОстаткиПартнер.Партнер);
				
				Если ВыборкаОстаткиПоДокументамПартнер.НайтиСледующий(СтруктураПоискаПартнер) Тогда
					
					ВыборкаВыборкаОстаткиПоДокументамДетали = ВыборкаОстаткиПоДокументамПартнер.Выбрать();
					
					ТаблицаОстатковПоДокументам.Очистить();
					Пока ВыборкаВыборкаОстаткиПоДокументамДетали.Следующий() Цикл
						СтрокаТаблицаОстатковПоДокументам = ТаблицаОстатковПоДокументам.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицаОстатковПоДокументам, ВыборкаВыборкаОстаткиПоДокументамДетали);
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
			// Получить таблицу документов для снятия с дальнейшей обработки распределения срочных бонусных баллов
			
			СтруктураПоискаБонуснаяПрограммаЛояльности = Новый Структура;
			СтруктураПоискаБонуснаяПрограммаЛояльности.Вставить(
				"БонуснаяПрограммаЛояльности",
				ВыборкаОстаткиБонуснаяПрограммаЛояльности.БонуснаяПрограммаЛояльности);
				
			ВыборкаДокументыКСнятиюСОбработкиБонуснаяПрограммаЛояльности.Сбросить();
			Если ВыборкаДокументыКСнятиюСОбработкиБонуснаяПрограммаЛояльности.НайтиСледующий(СтруктураПоискаБонуснаяПрограммаЛояльности) Тогда
				
				ВыборкаДокументыКСнятиюСОбработкиПартнер = ВыборкаДокументыКСнятиюСОбработкиБонуснаяПрограммаЛояльности.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Партнер");
				
				СтруктураПоискаПартнер = Новый Структура;
				СтруктураПоискаПартнер.Вставить(
					"Партнер",
					ВыборкаОстаткиПартнер.Партнер);
				
				Если ВыборкаДокументыКСнятиюСОбработкиПартнер.НайтиСледующий(СтруктураПоискаПартнер) Тогда
					
					ВыборкаДокументыКСнятиюСОбработкиДетали = ВыборкаДокументыКСнятиюСОбработкиПартнер.Выбрать();
					
					ТаблицаДокументовКСнятиюСОбработки.Очистить();
					Пока ВыборкаДокументыКСнятиюСОбработкиДетали.Следующий() Цикл
						СтрокаТаблицаДокументовКСнятиюСОбработки = ТаблицаДокументовКСнятиюСОбработки.Добавить();
						ЗаполнитьЗначенияСвойств(СтрокаТаблицаДокументовКСнятиюСОбработки, ВыборкаДокументыКСнятиюСОбработкиДетали);
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Для Каждого СтрокаТаблицаОстатковКСписанию Из ТаблицаОстатковКСписанию Цикл
				
				НачатьТранзакцию();
				
				Попытка
					
					ДанныеИзменены = Ложь;
					
					// Списать просроченные срочные бонусные баллы с остатков по регистру
					НулевыеСтрокиОстатковКУдалению = СписатьОстаткиПросроченныхСрочныхБонусныхБаллов(
						СтрокаТаблицаОстатковКСписанию,
						ТаблицаОстатковПоДокументам,
						ДанныеИзменены);
					
					Для Каждого СтрокаКУдалению Из НулевыеСтрокиОстатковКУдалению Цикл
						ТаблицаОстатковПоДокументам.Удалить(СтрокаКУдалению);
					КонецЦикла;
					
					// Снять документы с дальнейшей обработки распределения срочных бонусных баллов
					НулевыеСтрокиОстатковКУдалению = СнятьДокументыСоСтатусаКОбработке(
						СтрокаТаблицаОстатковКСписанию,
						ТаблицаДокументовКСнятиюСОбработки,
						ДанныеИзменены);
					
					Для Каждого СтрокаКУдалению Из НулевыеСтрокиОстатковКУдалению Цикл
						ТаблицаДокументовКСнятиюСОбработки.Удалить(СтрокаКУдалению);
					КонецЦикла;
					
					Если ДанныеИзменены Тогда
						КоличествоОбработанныхДокументов = КоличествоОбработанныхДокументов + 1;
					КонецЕсли;
					
					ЗафиксироватьТранзакцию();
					
				Исключение
					
					Если ТранзакцияАктивна() Тогда
						ОтменитьТранзакцию();
					КонецЕсли;
					ИнформацияОбОшибке = ИнформацияОбОшибке();
					ТекстОшибки = ИнформацияОбОшибке.Описание;
					Прервать;
					
				КонецПопытки;
				
			КонецЦикла;
			
		КонецЦикла;
			
	КонецЦикла;
	
	Возврат КоличествоОбработанныхДокументов;
	
КонецФункции

// Возвращает результат запроса с остатками просроченных срочных бонусных баллов.
// 
// Параметры:
//  ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5 - Дата - Дата начала работы механизма бонусных баллов 2_5
//  РазмерПорции - Число - Количество записей к обработке
// 
// Возвращаемое значение:
//  РезультатЗапроса - Результат запроса с остатками просроченных срочных бонусных баллов
Функция РезультатЗапросаОстаткиПросроченныхСрочныхБонусныхБаллов(
	ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5,
	РазмерПорции = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействияСрочныхБонусныхБаллов,
	|	БонусныеБаллы.Период КАК ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	СУММА(ВЫБОР
	|			КОГДА БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|				ТОГДА БонусныеБаллы.КСписанию
	|			ИНАЧЕ -БонусныеБаллы.КСписанию
	|		КОНЕЦ) КАК ОстатокСрочныхБонусныхБаллов
	|ПОМЕСТИТЬ ОстаткиПросроченныхСрочныхБонусныхБаллов
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.Период <= &ТекущаяДата
	|	И БонусныеБаллы.КСписанию <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.ДатаНачалаДействия
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.БонуснаяПрограммаЛояльности.Наименование КАК БонуснаяПрограммаЛояльностиНаименование,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.Партнер КАК Партнер,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.Партнер.Наименование КАК ПартнерНаименование,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаНачалаДействияСрочныхБонусныхБаллов КАК ДатаНачалаДействияСрочныхБонусныхБаллов,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов КАК ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.ОстатокСрочныхБонусныхБаллов КАК ОстатокСрочныхБонусныхБаллов
	|ИЗ
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов КАК ОстаткиПросроченныхСрочныхБонусныхБаллов
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартнерНаименование,
	|	БонуснаяПрограммаЛояльностиНаименование,
	|	ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаНачалаДействияСрочныхБонусныхБаллов
	|ИТОГИ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер КАК Партнер,
	|	БонусныеБаллы.Регистратор КАК Регистратор,
	|	БонусныеБаллы.ДатаКОбработке КАК ДатаКОбработке,
	|	БонусныеБаллы.ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	БонусныеБаллы.Период КАК Период,
	|	БонусныеБаллы.КСписанию КАК КСписанию
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|	И БонусныеБаллы.ВидДвижения = &ВидДвиженияПриход
	|	И (БонусныеБаллы.ДатаНачалаДействия, БонусныеБаллы.Период, БонусныеБаллы.БонуснаяПрограммаЛояльности, БонусныеБаллы.Партнер) В
	|			(ВЫБРАТЬ
	|				ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаНачалаДействияСрочныхБонусныхБаллов,
	|				ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов,
	|				ОстаткиПросроченныхСрочныхБонусныхБаллов.БонуснаяПрограммаЛояльности,
	|				ОстаткиПросроченныхСрочныхБонусныхБаллов.Партнер
	|			ИЗ
	|				ОстаткиПросроченныхСрочныхБонусныхБаллов КАК ОстаткиПросроченныхСрочныхБонусныхБаллов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачалаДействия
	|ИТОГИ ПО
	|	БонуснаяПрограммаЛояльности,
	|	Партнер
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БонусныеБаллы.Период,
	|	БонусныеБаллы.Регистратор,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности.Наименование КАК БонуснаяПрограммаЛояльностиНаименование,
	|	БонусныеБаллы.Партнер,
	|	БонусныеБаллы.Партнер.Наименование КАК ПартнерНаименование,
	|	БонусныеБаллы.КСписанию,
	|	БонусныеБаллы.ДатаНачалаДействия,
	|	БонусныеБаллы.ДатаКОбработке
	|ИЗ
	|	РегистрНакопления.БонусныеБаллы КАК БонусныеБаллы
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ОстаткиПросроченныхСрочныхБонусныхБаллов КАК ОстаткиПросроченныхСрочныхБонусныхБаллов
	|	ПО БонусныеБаллы.Партнер = ОстаткиПросроченныхСрочныхБонусныхБаллов.Партнер
	|		И БонусныеБаллы.БонуснаяПрограммаЛояльности = ОстаткиПросроченныхСрочныхБонусныхБаллов.БонуснаяПрограммаЛояльности
	|		И БонусныеБаллы.ДатаНачалаДействия = ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаНачалаДействияСрочныхБонусныхБаллов
	|		И БонусныеБаллы.Период = ОстаткиПросроченныхСрочныхБонусныхБаллов.ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов
	|ГДЕ
	|	БонусныеБаллы.ДатаКОбработке >= &ДатаНачалаРаботыМеханизмаБонусныхБаллов
	|	И БонусныеБаллы.ДатаКОбработке <> ДАТАВРЕМЯ(1, 1, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПартнерНаименование,
	|	БонуснаяПрограммаЛояльностиНаименование,
	|	ДатаНачалаДействия
	|ИТОГИ ПО
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.Партнер";
	Запрос.УстановитьПараметр("ДатаНачалаРаботыМеханизмаБонусныхБаллов", ДатаНачалаРаботыМеханизмаБонусныхБаллов_2_5);
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("ВидДвиженияПриход", ВидДвиженияНакопления.Приход);
	
	Если РазмерПорции <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст , "ПЕРВЫЕ 1000", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=;"));
	КонецЕсли;
	
	Возврат Запрос.ВыполнитьПакет();
	
КонецФункции

// Списывает остатки просроченных срочных бонусных баллов по регистру РН БонусныеБаллы
// 
// Параметры:
//  СтрокаТаблицаОстатковКСписанию - СтрокаТаблицыЗначений - Данные остатков просроченных бонусных баллов к списанию
//  ТаблицаОстатковПоДокументам - ТаблицаЗначений - Таблица документов начисления просроченных бонусных баллов с остатками
//  ДанныеИзменены - Булево - Флаг изменения данных для корректного подсчета обработанных документов
// 
// Возвращаемое значение:
//  Массив Из СтрокаТаблицыЗначений - Массив строк с нулевыми остатками бонусных баллов к удалению
// 
Функция СписатьОстаткиПросроченныхСрочныхБонусныхБаллов(
			СтрокаТаблицаОстатковКСписанию,
			ТаблицаОстатковПоДокументам,
			ДанныеИзменены)
			
	ОстатокПросроченныхСрочныхБонусныхБаллов = СтрокаТаблицаОстатковКСписанию.ОстатокСрочныхБонусныхБаллов;
	
	ДатаНачалаДействияСрочныхБонусныхБаллов = СтрокаТаблицаОстатковКСписанию.ДатаНачалаДействияСрочныхБонусныхБаллов;
	ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов = СтрокаТаблицаОстатковКСписанию.ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов;
	
	НулевыеСтрокиОстатковКУдалению = Новый Массив;
	
	// Списать остаток просроченных срочных бонусных баллов
	Для Каждого СтрокаТаблицаОстатковПоДокументам Из ТаблицаОстатковПоДокументам Цикл
		
		Если ОстатокПросроченныхСрочныхБонусныхБаллов = 0
			ИЛИ (СтрокаТаблицаОстатковПоДокументам.ДатаНачалаДействия <> ДатаНачалаДействияСрочныхБонусныхБаллов
					И СтрокаТаблицаОстатковПоДокументам.Период <> ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов
					И СтрокаТаблицаОстатковПоДокументам.ДатаНачалаДействия > ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов) Тогда
			Прервать;
		КонецЕсли;
		
		Если СтрокаТаблицаОстатковПоДокументам.ДатаНачалаДействия <> ДатаНачалаДействияСрочныхБонусныхБаллов
			ИЛИ СтрокаТаблицаОстатковПоДокументам.Период <> ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов Тогда
			Продолжить;
		КонецЕсли;
		
		Регистратор = СтрокаТаблицаОстатковПоДокументам.Регистратор;
		СуммаКСписанию = СтрокаТаблицаОстатковПоДокументам.КСписанию;
		
		СуммаКСписаниюПросроченныхСрочныхБонусныхБаллов = Мин(ОстатокПросроченныхСрочныхБонусныхБаллов, СуммаКСписанию);
		ОстатокПросроченныхСрочныхБонусныхБаллов = 
			ОстатокПросроченныхСрочныхБонусныхБаллов - СуммаКСписаниюПросроченныхСрочныхБонусныхБаллов;
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
		
		Блокировка.Заблокировать();
		
		НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = Регистратор;
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
		НаборЗаписейРНБонусныеБаллы.Прочитать();
		
		СтрокаНаборЗаписейРНБонусныеБаллы = НаборЗаписейРНБонусныеБаллы.Добавить();
		ЗаполнитьЗначенияСвойств(
			СтрокаНаборЗаписейРНБонусныеБаллы,
			СтрокаТаблицаОстатковПоДокументам);
		
		СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Приход;
		СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = -СуммаКСписаниюПросроченныхСрочныхБонусныхБаллов;
		СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = -СуммаКСписаниюПросроченныхСрочныхБонусныхБаллов;
		СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = ТекущаяДатаСеанса();
		
		НаборЗаписейРНБонусныеБаллы.Записать();
		
		СтрокаТаблицаОстатковПоДокументам.КСписанию = СтрокаТаблицаОстатковПоДокументам.КСписанию - СуммаКСписаниюПросроченныхСрочныхБонусныхБаллов;
		Если СтрокаТаблицаОстатковПоДокументам.КСписанию = 0 Тогда
			НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаОстатковПоДокументам);
		КонецЕсли;
		
		ДанныеИзменены = Истина;
		
	КонецЦикла;
	
	Возврат НулевыеСтрокиОстатковКУдалению;
	
КонецФункции

// Снимает документы с дальнейшей обработки распределения срочных бонусных баллов
// 
// Параметры:
//  СтрокаТаблицаОстатковКСписанию - СтрокаТаблицыЗначений - Данные остатков просроченных бонусных баллов к списанию
//  ТаблицаДокументовКСнятиюСОбработки - ТаблицаЗначений - Таблица документов для отмены статуса КОбработке
//  ДанныеИзменены - Булево - Флаг изменения данных для корректного подсчета обработанных документов
// 
// Возвращаемое значение:
//  Массив Из СтрокаТаблицыЗначений - Массив строк с нулевыми остатками бонусных баллов к удалению
// 
Функция СнятьДокументыСоСтатусаКОбработке(
			СтрокаТаблицаОстатковКСписанию,
			ТаблицаДокументовКСнятиюСОбработки,
			ДанныеИзменены)
			
	ДатаНачалаДействияСрочныхБонусныхБаллов = СтрокаТаблицаОстатковКСписанию.ДатаНачалаДействияСрочныхБонусныхБаллов;
	ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов = СтрокаТаблицаОстатковКСписанию.ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов;
	
	Партнер  = СтрокаТаблицаОстатковКСписанию.Партнер;
	БонуснаяПрограммаЛояльности  = СтрокаТаблицаОстатковКСписанию.БонуснаяПрограммаЛояльности;
	
	НулевыеСтрокиОстатковКУдалению = Новый Массив;
	
	Для Каждого СтрокаТаблицаДокументовКСнятиюСОбработки Из ТаблицаДокументовКСнятиюСОбработки Цикл
		
		СуммаНачисленияКСнятиюСОбработки = СтрокаТаблицаДокументовКСнятиюСОбработки.КСписанию;
		СуммаКСписаниюКСнятиюСОбработки = СтрокаТаблицаДокументовКСнятиюСОбработки.КСписанию;
		
		Регистратор = СтрокаТаблицаДокументовКСнятиюСОбработки.Регистратор;
		
		Блокировка = Новый БлокировкаДанных;
		
		ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.БонусныеБаллы.НаборЗаписей");
		ЭлементБлокировки.УстановитьЗначение("Регистратор", Регистратор);
		
		Блокировка.Заблокировать();
		
		НаборЗаписейРНБонусныеБаллы = РегистрыНакопления.БонусныеБаллы.СоздатьНаборЗаписей();
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Значение = Регистратор;
		НаборЗаписейРНБонусныеБаллы.Отбор.Регистратор.Использование = Истина;
		НаборЗаписейРНБонусныеБаллы.Прочитать();
		
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			
			Если СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1) Тогда
				Продолжить;
			КонецЕсли;
			
			// Снять возврат расхода срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу Начислено
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход
				И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено < 0
				И СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = 0 Тогда
			
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
					НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
				КонецЕсли;
				
				ДанныеИзменены = Истина;
				
				Продолжить;
				
			КонецЕсли;
			
			// Снять расход срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу Начислено
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ВидДвижения = ВидДвиженияНакопления.Расход
				И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено > 0
				И СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = 0 Тогда
			
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
					НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
				КонецЕсли;
				
				ДанныеИзменены = Истина;
				
				Продолжить;
				
			КонецЕсли;
			
			// Снятие списания срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу КСписанию
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = СуммаКСписаниюКСнятиюСОбработки
				И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = 0 Тогда
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
					НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
				КонецЕсли;
				
				СуммаКСписаниюКСнятиюСОбработки = 0;
				ДанныеИзменены = Истина;
				
				Продолжить;
				
			КонецЕсли;
			
			// Снятие начисления срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу Начислено
			// Нужно списывать только сумму начисления (может быть несколько срок с одним периодом начала,
			// но разными периодами окончания и наоборот)
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СуммаНачисленияКСнятиюСОбработки
				И СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = 0 Тогда
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
					НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
				КонецЕсли;
				
				СуммаНачисленияКСнятиюСОбработки = 0;
				ДанныеИзменены = Истина;
				
				Продолжить;
				
			КонецЕсли;
			
			// Снять списание просроченных баллов с дальнейшей обработки
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Начислено <> 0
				И  СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию <> 0 Тогда
				
				СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
				
				Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
					НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
				КонецЕсли;
				
				ДанныеИзменены = Истина;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Снимем с обработки суммы, которые были распределены частично
		Для Каждого СтрокаНаборЗаписейРНБонусныеБаллы Из НаборЗаписейРНБонусныеБаллы Цикл
			Если СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1) Тогда
				Продолжить;
			КонецЕсли;
			
			// Снятие списания срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу КСписанию
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = ДатаОкончанияСрокаДействияСрочныхБонусныхБаллов
				И СуммаКСписаниюКСнятиюСОбработки <> 0 Тогда
				
				Если СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию = СуммаКСписаниюКСнятиюСОбработки Тогда
					СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
					
					Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
						НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
					КонецЕсли;
					
					СуммаКСписаниюКСнятиюСОбработки = 0;
					ДанныеИзменены = Истина;
					
				Иначе
					
					СуммаКСнятиюСОбработки = Мин(
						СуммаКСписаниюКСнятиюСОбработки,
						СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию);
						
					СтрокаНаборЗаписейРНБонусныеБаллыНовая = НаборЗаписейРНБонусныеБаллы.Добавить();
					ЗаполнитьЗначенияСвойств(
						СтрокаНаборЗаписейРНБонусныеБаллыНовая,
						СтрокаНаборЗаписейРНБонусныеБаллы);
					
					СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СтрокаНаборЗаписейРНБонусныеБаллы.КСписанию - СуммаКСнятиюСОбработки;
						
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.ВидДвижения = ВидДвиженияНакопления.Приход;
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.КСписанию = СуммаКСнятиюСОбработки;
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.ДатаКОбработке = Дата(1, 1, 1);
					
					Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
						НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
					КонецЕсли;
					
					СуммаКСписаниюКСнятиюСОбработки = СуммаКСписаниюКСнятиюСОбработки - СуммаКСнятиюСОбработки;
					ДанныеИзменены = Истина;
					
				КонецЕсли;
				
				Продолжить;
				
			КонецЕсли;
			
			// Снятие начисления срочных бонусных баллов с дальнейшей обработки
			// Движение по ресурсу Начислено
			Если СтрокаНаборЗаписейРНБонусныеБаллы.БонуснаяПрограммаЛояльности = БонуснаяПрограммаЛояльности
				И СтрокаНаборЗаписейРНБонусныеБаллы.Партнер = Партнер
				И СтрокаНаборЗаписейРНБонусныеБаллы.ДатаНачалаДействия = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СтрокаНаборЗаписейРНБонусныеБаллы.Период = ДатаНачалаДействияСрочныхБонусныхБаллов
				И СуммаНачисленияКСнятиюСОбработки <> 0 Тогда
				
				Если СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СуммаНачисленияКСнятиюСОбработки Тогда
					СтрокаНаборЗаписейРНБонусныеБаллы.ДатаКОбработке = Дата(1, 1, 1);
					
					Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
						НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
					КонецЕсли;
					
					СуммаНачисленияКСнятиюСОбработки = 0;
					ДанныеИзменены = Истина;
					
				Иначе
					
					СуммаКСнятиюСОбработки = Мин(
						СуммаНачисленияКСнятиюСОбработки,
						СтрокаНаборЗаписейРНБонусныеБаллы.Начислено);
						
					СтрокаНаборЗаписейРНБонусныеБаллыНовая = НаборЗаписейРНБонусныеБаллы.Добавить();
					ЗаполнитьЗначенияСвойств(
						СтрокаНаборЗаписейРНБонусныеБаллыНовая,
						СтрокаНаборЗаписейРНБонусныеБаллы);
					
					СтрокаНаборЗаписейРНБонусныеБаллы.Начислено = СтрокаНаборЗаписейРНБонусныеБаллы.Начислено - СуммаКСнятиюСОбработки;
						
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.ВидДвижения = ВидДвиженияНакопления.Приход;
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.Начислено = СуммаКСнятиюСОбработки;
					СтрокаНаборЗаписейРНБонусныеБаллыНовая.ДатаКОбработке = Дата(1, 1, 1);
					
					Если НулевыеСтрокиОстатковКУдалению.Найти(СтрокаТаблицаДокументовКСнятиюСОбработки) = Неопределено Тогда
						НулевыеСтрокиОстатковКУдалению.Добавить(СтрокаТаблицаДокументовКСнятиюСОбработки);
					КонецЕсли;
					
					СуммаНачисленияКСнятиюСОбработки = СуммаНачисленияКСнятиюСОбработки - СуммаКСнятиюСОбработки;
					ДанныеИзменены = Истина;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ДанныеИзменены Тогда
			НаборЗаписейРНБонусныеБаллы.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат НулевыеСтрокиОстатковКУдалению;
	
КонецФункции

#КонецОбласти

Процедура ЗаполнитьБонусныеБаллыВозвратТоваровОтКлиента(ТекущийОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТекущийОбъект.НачислениеБонусныхБаллов.Очистить();
	ТекущийОбъект.ОплатаБонуснымиБаллами.Очистить();
	
	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
		ДокументОснование = ТекущийОбъект.ДокументРеализации;
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
			ТекстЗапроса = ТекстЗапросаВозвратБонусныхБалловРозничныеПродажи();
		ИначеЕсли ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ТекстЗапроса = ТекстЗапросаВозвратБонусныхБалловОптовыеПродажи();
		Иначе
			Возврат;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.КорректировкаРеализации") Тогда
		ДокументОснование = ТекущийОбъект.ДокументОснование;
		
		Если ТипЗнч(ДокументОснование) = Тип("ДокументСсылка.РеализацияТоваровУслуг") Тогда
			ТекстЗапроса = ТекстЗапросаВозвратБонусныхБалловОптовыеПродажи();
		Иначе
			Возврат;
		КонецЕсли;
	Иначе
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;

	Если ТипЗнч(ТекущийОбъект) = Тип("ДокументОбъект.ВозвратТоваровОтКлиента") Тогда
		Запрос.УстановитьПараметр("СсылкаДокументаВозврат", ТекущийОбъект.Ссылка);
		Запрос.УстановитьПараметр("СсылкаДокументаКорректировка", Документы.КорректировкаРеализации.ПустаяСсылка());
		Запрос.УстановитьПараметр("Товары", ТекущийОбъект.Товары.Выгрузить(,"Номенклатура, Характеристика, Сумма"));
	Иначе
		Запрос.УстановитьПараметр("СсылкаДокументаКорректировка", ТекущийОбъект.Ссылка);
		Запрос.УстановитьПараметр("СсылкаДокументаВозврат", Документы.ВозвратТоваровОтКлиента.ПустаяСсылка());
		
		Товары = ТекущийОбъект.Расхождения.Выгрузить(,"Номенклатура, Характеристика, Сумма");
		ИндексСтроки = Товары.Количество()-1;
		
		Пока ИндексСтроки >= 0 Цикл
			
			ТекущаяСтрока = Товары[ИндексСтроки];
			
			Если ТекущаяСтрока.Сумма > 0 Тогда
				Товары.Удалить(ТекущаяСтрока);
			Иначе
				ТекущаяСтрока.Сумма = -ТекущаяСтрока.Сумма;
			КонецЕсли;
			
			ИндексСтроки = ИндексСтроки - 1;
			
		КонецЦикла;
		
		Если Товары.Количество() = 0 Тогда
			Возврат;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Товары", Товары);
	КонецЕсли;
		
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.УстановитьПараметр("ВалютаДокумента", ТекущийОбъект.Валюта);
	Запрос.УстановитьПараметр("Дата", ТекущийОбъект.Дата);
	Запрос.УстановитьПараметр("Партнер", ТекущийОбъект.Партнер);

	Результаты = Запрос.ВыполнитьПакет();
	
	ОстаткиНачисленныхБонусныхБаллов = Результаты[1].Выгрузить();
	ОстаткиСписываемыхБонусныхБаллов = Результаты[2].Выгрузить();
	
	СуммаНачисленныхБалловКВозврату = 0;
	СуммаСписанныхБонусныхБалловКВозврату = 0;
	
	ВыборкаТовары = Результаты[3].Выбрать();
	Если ВыборкаТовары.Следующий() Тогда
		
		Если ВыборкаТовары.Сумма <> 0 Тогда
			СуммаНачисленныхБалловКВозврату = ОстаткиНачисленныхБонусныхБаллов.Итог("СуммаБонусныхБаллов")
			                                * ВыборкаТовары.СуммаВДокументе / ВыборкаТовары.Сумма;
			
			СуммаСписанныхБонусныхБалловКВозврату = ОстаткиСписываемыхБонусныхБаллов.Итог("СуммаБонусныхБаллов")
			                                      * ВыборкаТовары.СуммаВДокументе / ВыборкаТовары.Сумма;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Для Каждого СтрокаТЧ Из ОстаткиНачисленныхБонусныхБаллов Цикл
		
		Если СуммаНачисленныхБалловКВозврату = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если СуммаНачисленныхБалловКВозврату >= СтрокаТЧ.СуммаБонусныхБаллов Тогда
			
			СуммаНачисленныхБалловКВозврату = СуммаНачисленныхБалловКВозврату - СтрокаТЧ.СуммаБонусныхБаллов;
			КСписанию = СтрокаТЧ.СуммаБонусныхБаллов;
			
		Иначе
			
			КСписанию = СуммаНачисленныхБалловКВозврату;
			СуммаНачисленныхБалловКВозврату = 0;
			
		КонецЕсли;
		
		НоваяСтрока = ТекущийОбъект.НачислениеБонусныхБаллов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, "БонуснаяПрограммаЛояльности, ДатаНачисления, ДатаСписания");
		НоваяСтрока.СуммаБонусныхБаллов = КСписанию;
		
		СтрокаТЧ.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов - КСписанию;
		
	КонецЦикла;
	
	Для Каждого СтрокаТЧ Из ОстаткиСписываемыхБонусныхБаллов Цикл
		
		Если СуммаСписанныхБонусныхБалловКВозврату = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если СуммаСписанныхБонусныхБалловКВозврату >= СтрокаТЧ.СуммаБонусныхБаллов Тогда
			
			СуммаСписанныхБонусныхБалловКВозврату = СуммаСписанныхБонусныхБалловКВозврату - СтрокаТЧ.СуммаБонусныхБаллов;
			КСписанию = СтрокаТЧ.СуммаБонусныхБаллов;
			
		Иначе
			
			КСписанию = СуммаСписанныхБонусныхБалловКВозврату;
			СуммаСписанныхБонусныхБалловКВозврату = 0;
			
		КонецЕсли;
		
		НоваяСтрока = ТекущийОбъект.ОплатаБонуснымиБаллами.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, "БонуснаяПрограммаЛояльности, ДатаОплаты");
		НоваяСтрока.СуммаБонусныхБаллов = -КСписанию;
		
		СтрокаТЧ.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов - КСписанию;
		
	КонецЦикла;
	
КонецПроцедуры

Функция ТекстЗапросаВозвратБонусныхБалловРозничныеПродажи()

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления КАК ДатаНачисления,
	|	Таблица.ДатаСписания КАК ДатаСписания,
	|	СУММА(Таблица.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаНачисления КАК ДатаНачисления,
	|		ТаблицаБонусныеБаллы.ДатаСписания КАК ДатаСписания,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.НачислениеБонусныхБаллов КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.СуммаБонусныхБаллов > 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаНачисления,
	|		ТаблицаБонусныеБаллы.ДатаСписания,
	|		-ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента.НачислениеБонусныхБаллов КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументРеализации = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.СуммаБонусныхБаллов > 0
	|		И ТаблицаБонусныеБаллы.Ссылка.Ссылка <> &СсылкаДокументаВозврат) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления,
	|	Таблица.ДатаСписания
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаБонусныхБаллов) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаОплаты КАК ДатаОплаты,
	|	СУММА(Таблица.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаОплаты КАК ДатаОплаты,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.ОплатаБонуснымиБаллами КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаОплаты,
	|		-ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента.ОплатаБонуснымиБаллами КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументРеализации = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.Ссылка.Ссылка <> &СсылкаДокументаВозврат) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаОплаты
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаБонусныхБаллов) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК Сумма,
	|	ЕСТЬNULL(СУММА(Товары.СуммаВДокументе), 0) КАК СуммаВДокументе
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Сумма КАК Сумма,
	|		0 КАК СуммаВДокументе
	|	ИЗ
	|		Документ.ОтчетОРозничныхПродажах.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &ДокументОснование
	|		И ТаблицаТовары.Ссылка.Проведен
	|		И ТаблицаТовары.Партнер = &Партнер
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		0,
	|		Товары.Сумма
	|	ИЗ
	|		Товары КАК Товары) КАК Товары
	|";
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаВозвратБонусныхБалловОптовыеПродажи()
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Сумма
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления КАК ДатаНачисления,
	|	Таблица.ДатаСписания КАК ДатаСписания,
	|	СУММА(Таблица.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаНачисления КАК ДатаНачисления,
	|		ТаблицаБонусныеБаллы.ДатаСписания КАК ДатаСписания,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.НачислениеБонусныхБаллов КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.СуммаБонусныхБаллов > 0
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаНачисления,
	|		ТаблицаБонусныеБаллы.ДатаСписания,
	|		-ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента.НачислениеБонусныхБаллов КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументРеализации = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.СуммаБонусныхБаллов > 0
	|		И ТаблицаБонусныеБаллы.Ссылка.Ссылка <> &СсылкаДокументаВозврат
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаНачисления,
	|		ТаблицаБонусныеБаллы.ДатаСписания,
	|		-ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.КорректировкаРеализации.НачислениеБонусныхБаллов КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументОснование = &ДокументОснование
	|		И (ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|			ИЛИ ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ИЛИ ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок))
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.СуммаБонусныхБаллов > 0
	|		И ТаблицаБонусныеБаллы.Ссылка.Ссылка <> &СсылкаДокументаКорректировка) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления,
	|	Таблица.ДатаСписания
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаБонусныхБаллов) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаОплаты КАК ДатаОплаты,
	|	СУММА(Таблица.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов
	|ИЗ
	|	(ВЫБРАТЬ
	|		РеализацияТоваровУслуг.КартаЛояльности.Владелец.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		НАЧАЛОПЕРИОДА(РеализацияТоваровУслуг.Дата, ДЕНЬ) КАК ДатаОплаты,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБалловКСписанию КАК СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаБонусныеБаллы
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|			ПО ТаблицаБонусныеБаллы.Ссылка = РеализацияТоваровУслуг.Ссылка
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка = &ДокументОснование
	|		И РеализацияТоваровУслуг.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И НЕ РеализацияТоваровУслуг.КартаЛояльности.Владелец.БонуснаяПрограммаЛояльности ЕСТЬ NULL
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаОплаты,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента.ОплатаБонуснымиБаллами КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументРеализации = &ДокументОснование
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.Ссылка <> &СсылкаДокументаВозврат
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		ТаблицаБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ТаблицаБонусныеБаллы.ДатаОплаты,
	|		ТаблицаБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.КорректировкаРеализации.ОплатаБонуснымиБаллами КАК ТаблицаБонусныеБаллы
	|	ГДЕ
	|		ТаблицаБонусныеБаллы.Ссылка.ДокументОснование = &ДокументОснование
	|		И (ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|			ИЛИ ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ИЛИ ТаблицаБонусныеБаллы.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок))
	|		И ТаблицаБонусныеБаллы.Ссылка.Партнер = &Партнер
	|		И ТаблицаБонусныеБаллы.Ссылка.Проведен
	|		И ТаблицаБонусныеБаллы.Ссылка <> &СсылкаДокументаКорректировка) КАК Таблица
	|СГРУППИРОВАТЬ ПО
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаОплаты
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаБонусныхБаллов) <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(Товары.Сумма), 0) КАК Сумма,
	|	ЕСТЬNULL(СУММА(Товары.СуммаВДокументе), 0) КАК СуммаВДокументе
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаТовары.Номенклатура КАК Номенклатура,
	|		ТаблицаТовары.Характеристика КАК Характеристика,
	|		ТаблицаТовары.Сумма КАК Сумма,
	|		0 КАК СуммаВДокументе
	|	ИЗ
	|		Документ.РеализацияТоваровУслуг.Товары КАК ТаблицаТовары
	|	ГДЕ
	|		ТаблицаТовары.Ссылка = &ДокументОснование
	|		И ТаблицаТовары.Ссылка.Проведен
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		0,
	|		Товары.Сумма
	|	ИЗ
	|		Товары КАК Товары
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		-Товары.Сумма,
	|		0
	|	ИЗ
	|		Документ.ВозвратТоваровОтКлиента.Товары КАК Товары
	|	ГДЕ
	|		Товары.Ссылка.ДокументРеализации = &ДокументОснование
	|		И Товары.Ссылка.Партнер = &Партнер
	|		И Товары.Ссылка.Проведен
	|		И Товары.Ссылка <> &СсылкаДокументаВозврат
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		Товары.Сумма,
	|		0
	|	ИЗ
	|		Документ.КорректировкаРеализации.Расхождения КАК Товары
	|	ГДЕ
	|		Товары.Ссылка.ДокументОснование = &ДокументОснование
	|		И (Товары.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ВозвратНедопоставленногоТовара)
	|			ИЛИ Товары.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.КорректировкаПоСогласованиюСторон)
	|			ИЛИ Товары.Ссылка.ВидКорректировки = ЗНАЧЕНИЕ(Перечисление.ХозяйственныеОперации.ИсправлениеОшибок))
	|		И Товары.Ссылка.Партнер = &Партнер
	|		И Товары.Ссылка.Проведен
	|		И Товары.Ссылка <> &СсылкаДокументаКорректировка
	|		И Товары.Сумма < 0 ) КАК Товары";
	
	Возврат ТекстЗапроса
	
КонецФункции

Процедура ЗаполнитьБонусныеБаллыЧекККМВозврат(ТекущийОбъект) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОтносительныеКурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	ОтносительныеКурсыВалютСрезПоследних.КурсЧислитель КАК КурсЧислитель,
	|	ОтносительныеКурсыВалютСрезПоследних.КурсЗнаменатель КАК КурсЗнаменатель
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.ОтносительныеКурсыВалют.СрезПоследних(&Дата, БазоваяВалюта = &БазоваяВалюта) КАК ОтносительныеКурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	&Товары КАК Товары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления КАК ДатаНачисления,
	|	Таблица.ДатаСписания,
	|	СУММА(Таблица.СуммаБонусныхБаллов) КАК СуммаБонусныхБаллов
	|ПОМЕСТИТЬ БонусныеБаллы
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЧекККМБонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ЧекККМБонусныеБаллы.ДатаНачисления КАК ДатаНачисления,
	|		ЧекККМБонусныеБаллы.ДатаСписания КАК ДатаСписания,
	|		ЧекККМБонусныеБаллы.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ЧекККМ.БонусныеБаллы КАК ЧекККМБонусныеБаллы
	|	ГДЕ
	|		ЧекККМБонусныеБаллы.Ссылка = &ЧекККМ
	|		И ЧекККМБонусныеБаллы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ЧекККМВозвратБонусныеБаллы.БонуснаяПрограммаЛояльности,
	|		ЧекККМВозвратБонусныеБаллы.ДатаНачисления,
	|		ЧекККМВозвратБонусныеБаллы.ДатаСписания,
	|		-ЧекККМВозвратБонусныеБаллы.СуммаБонусныхБаллов
	|	ИЗ
	|		Документ.ЧекККМВозврат.БонусныеБаллы КАК ЧекККМВозвратБонусныеБаллы
	|	ГДЕ
	|		ЧекККМВозвратБонусныеБаллы.Ссылка.ЧекККМ = &ЧекККМ
	|		И ЧекККМВозвратБонусныеБаллы.Ссылка.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧековККМ.Пробит)
	|		И ЧекККМВозвратБонусныеБаллы.Ссылка.Ссылка <> &Ссылка) КАК Таблица
	|
	|СГРУППИРОВАТЬ ПО
	|	Таблица.БонуснаяПрограммаЛояльности,
	|	Таблица.ДатаНачисления,
	|	Таблица.ДатаСписания
	|
	|ИМЕЮЩИЕ
	|	СУММА(Таблица.СуммаБонусныхБаллов) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	БонусныеБаллы.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	БонусныеБаллы.ДатаНачисления КАК ДатаНачисления,
	|	БонусныеБаллы.ДатаСписания КАК ДатаСписания,
	|	БонусныеБаллы.СуммаБонусныхБаллов КАК СуммаБонусныхБаллов,
	|	БонусныеБаллы.СуммаБонусныхБаллов * ЕСТЬNULL(КурсыВалютКонвертацииБонусов.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютДокумента.КурсЗнаменатель, 1) / (ЕСТЬNULL(КурсыВалютДокумента.КурсЧислитель, 1) * ЕСТЬNULL(КурсыВалютКонвертацииБонусов.КурсЗнаменатель, 1)) * БонусныеБаллы.БонуснаяПрограммаЛояльности.КурсКонвертацииБонусовВВалюту КАК СуммаВВалютеДокумента
	|ИЗ
	|	БонусныеБаллы КАК БонусныеБаллы
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютКонвертацииБонусов
	|		ПО (КурсыВалютКонвертацииБонусов.Валюта = БонусныеБаллы.БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалютДокумента
	|		ПО (КурсыВалютДокумента.Валюта = &ВалютаДокумента)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачисления
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.Номенклатура КАК Номенклатура,
	|	Товары.Характеристика КАК Характеристика,
	|	СУММА(Товары.СуммаБонусныхБалловКСписанию) КАК СуммаБонусныхБалловКСписанию,
	|	СУММА(Товары.СуммаНачисленныхБонусныхБалловВВалюте) КАК СуммаНачисленныхБонусныхБалловВВалюте,
	|	СУММА(Товары.Количество) КАК Количество,
	|	СУММА(Товары.КоличествоВДокументе) КАК КоличествоВДокументе
	|ИЗ
	|	(ВЫБРАТЬ
	|		ЧекККМТовары.Номенклатура КАК Номенклатура,
	|		ЧекККМТовары.Характеристика КАК Характеристика,
	|		ЧекККМТовары.СуммаБонусныхБалловКСписанию КАК СуммаБонусныхБалловКСписанию,
	|		ЧекККМТовары.СуммаНачисленныхБонусныхБалловВВалюте КАК СуммаНачисленныхБонусныхБалловВВалюте,
	|		ЧекККМТовары.Количество КАК Количество,
	|		0 КАК КоличествоВДокументе
	|	ИЗ
	|		Документ.ЧекККМ.Товары КАК ЧекККМТовары
	|	ГДЕ
	|		ЧекККМТовары.Ссылка = &ЧекККМ
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Товары.Номенклатура,
	|		Товары.Характеристика,
	|		0,
	|		0,
	|		0,
	|		Товары.Количество
	|	ИЗ
	|		Товары КАК Товары) КАК Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	Товары.Номенклатура,
	|	Товары.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМ.КартаЛояльности КАК КартаЛояльности,
	|	ЧекККМ.КартаЛояльности.Владелец.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ЧекККМ.Дата КАК Дата
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.Ссылка = &ЧекККМ;
	|";

	Запрос.УстановитьПараметр("Ссылка", ТекущийОбъект.Ссылка);
	Запрос.УстановитьПараметр("ЧекККМ", ТекущийОбъект.ЧекККМ);
	Запрос.УстановитьПараметр("Дата",   ТекущийОбъект.Дата);
	Запрос.УстановитьПараметр("ВалютаДокумента", ТекущийОбъект.Валюта);
	Запрос.УстановитьПараметр("БазоваяВалюта", ЗначениеНастроекПовтИсп.ВалютаРегламентированногоУчетаОрганизации(ТекущийОбъект.Организация));
	Запрос.УстановитьПараметр("Товары", ТекущийОбъект.Товары.Выгрузить());

	Результаты = Запрос.ВыполнитьПакет();
	
	ОстаткиБонусныхБаллов = Результаты[3].Выгрузить();
	
	СуммаНачисленныхБалловКВозврату = 0;
	СуммаСписанныхБонусныхБалловКВозврату = 0;
	
	ВыборкаТовары = Результаты[4].Выбрать();
	Пока ВыборкаТовары.Следующий() Цикл
		
		Если ВыборкаТовары.Количество <> 0 Тогда
		
			СуммаНачисленныхБалловКВозврату = СуммаНачисленныхБалловКВозврату
			                                + ВыборкаТовары.СуммаНачисленныхБонусныхБалловВВалюте
			                                * ВыборкаТовары.КоличествоВДокументе / ВыборкаТовары.Количество;
			
			СуммаСписанныхБонусныхБалловКВозврату = СуммаСписанныхБонусныхБалловКВозврату
			                                      + ВыборкаТовары.СуммаБонусныхБалловКСписанию
			                                      * ВыборкаТовары.КоличествоВДокументе / ВыборкаТовары.Количество;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТекущийОбъект.БонусныеБаллы.Очистить();
	
	Для Каждого СтрокаТЧ Из ОстаткиБонусныхБаллов Цикл
		
		Если СуммаНачисленныхБалловКВозврату = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Если СуммаНачисленныхБалловКВозврату >= СтрокаТЧ.СуммаВВалютеДокумента Тогда
			
			СуммаНачисленныхБалловКВозврату = СуммаНачисленныхБалловКВозврату - СтрокаТЧ.СуммаВВалютеДокумента;
			
			КСписанию = СтрокаТЧ.СуммаВВалютеДокумента;
			
			НоваяСтрока = ТекущийОбъект.БонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, "БонуснаяПрограммаЛояльности, ДатаНачисления, ДатаСписания");
			НоваяСтрока.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов *  КСписанию / СтрокаТЧ.СуммаВВалютеДокумента;
			
			СтрокаТЧ.СуммаВВалютеДокумента = СтрокаТЧ.СуммаВВалютеДокумента - КСписанию;
			СтрокаТЧ.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов - НоваяСтрока.СуммаБонусныхБаллов;
			
		Иначе
			
			КСписанию = СуммаНачисленныхБалловКВозврату;
			
			СуммаНачисленныхБалловКВозврату = 0;
			
			НоваяСтрока = ТекущийОбъект.БонусныеБаллы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ, "БонуснаяПрограммаЛояльности, ДатаНачисления, ДатаСписания");
			НоваяСтрока.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов *  КСписанию / СтрокаТЧ.СуммаВВалютеДокумента;
			
			СтрокаТЧ.СуммаВВалютеДокумента = СтрокаТЧ.СуммаВВалютеДокумента - КСписанию;
			СтрокаТЧ.СуммаБонусныхБаллов = СтрокаТЧ.СуммаБонусныхБаллов - НоваяСтрока.СуммаБонусныхБаллов;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Выборка = Результаты[5].Выбрать();
	Если Выборка.Следующий() Тогда
		ТекущийОбъект.КартаЛояльности              = Выборка.КартаЛояльности;
		ТекущийОбъект.СуммаБонусныхБалловКВозврату = СуммаСписанныхБонусныхБалловКВозврату;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает таблицу бонусных баллов по таблице скидок и наценок
// 
// Параметры:
//  СкидкиНаценки - ТаблицаЗначений - Таблица скидок и наценок
//  Дата - Дата - Дата начала расчета периода действия бонусных баллов
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица бонусные баллы:
//   * БонуснаяПрограммаЛояльности - СправочникСсылка.БонусныеПрограммыЛояльности - Бонусная программа лояльности
//   * ВалютаКонвертацииБонусов     - СправочникСсылка.Валюты - Валюта конвертации бонусов
//   * КурсКонвертацииБонусовВВалюту - Число - Курс конвертации бонусов в валюту
//   * КлючСвязи - Строка - Ключ связи
//   * ДатаСписания - Дата - Дата списания
//   * ДатаНачисления - Дата - Дата начисления
//   * СуммаБонусныхБаллов - Число - Сумма бонусных баллов
Функция ТаблицаБонусныеБаллы(СкидкиНаценки, Дата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.КлючСвязи КАК КлючСвязи,
	|	Таблица.СкидкаНаценка КАК СкидкаНаценка,
	|	Таблица.Сумма КАК Сумма
	|ПОМЕСТИТЬ СкидкиНаценки
	|ИЗ
	|	&СкидкиНаценки КАК Таблица
	|ГДЕ
	|	Таблица.СпособПримененияСкидки = ЗНАЧЕНИЕ(Перечисление.СпособыПримененияСкидокНаценок.НачислитьБонусныеБаллы)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТабличнаяЧасть.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|	ТабличнаяЧасть.БонуснаяПрограммаЛояльности.ВалютаКонвертацииБонусов КАК ВалютаКонвертацииБонусов,
	|	ТабличнаяЧасть.БонуснаяПрограммаЛояльности.КурсКонвертацииБонусовВВалюту КАК КурсКонвертацииБонусовВВалюту,
	|	ТабличнаяЧасть.КлючСвязи КАК КлючСвязи,
	|
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия > 0 ТОГДА
	|			ВЫБОР
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, ДЕНЬ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, НЕДЕЛЯ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, МЕСЯЦ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, КВАРТАЛ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, ГОД, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, ДЕКАДА, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|					ТОГДА ДОБАВИТЬКДАТЕ(ТабличнаяЧасть.Период, ПОЛУГОДИЕ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|				ИНАЧЕ ТабличнаяЧасть.Период
	|			КОНЕЦ
	|	ИНАЧЕ
	|		ДатаВремя(1,1,1)
	|	КОНЕЦ КАК ДатаСписания,
	|
	|	ВЫБОР
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, НЕДЕЛЯ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, ГОД, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, ДЕКАДА, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодОтсрочкиНачалаДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|			ТОГДА ДОБАВИТЬКДАТЕ(&Период, ПОЛУГОДИЕ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовОтсрочкиНачалаДействия)
	|		ИНАЧЕ &Период
	|	КОНЕЦ КАК ДатаНачисления,
	|
	|	ТабличнаяЧасть.Сумма КАК СуммаБонусныхБаллов
	|
	|ИЗ
	|	(
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.День)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, ДЕНЬ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|				КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Неделя)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, НЕДЕЛЯ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Месяц)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, МЕСЯЦ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Квартал)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, КВАРТАЛ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Год)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, ГОД, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Декада)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, ДЕКАДА, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			КОГДА ТабличнаяЧасть.СкидкаНаценка.ПериодДействия = ЗНАЧЕНИЕ(Перечисление.Периодичность.Полугодие)
	|				ТОГДА ДОБАВИТЬКДАТЕ(&Период, ПОЛУГОДИЕ, ТабличнаяЧасть.СкидкаНаценка.КоличествоПериодовДействия)
	|			ИНАЧЕ &Период
	|		КОНЕЦ                                                    КАК Период,
	|		ТабличнаяЧасть.СкидкаНаценка.БонуснаяПрограммаЛояльности КАК БонуснаяПрограммаЛояльности,
	|		ТабличнаяЧасть.Сумма                                     КАК Сумма,
	|		ТабличнаяЧасть.СкидкаНаценка                             КАК СкидкаНаценка,
	|		ТабличнаяЧасть.КлючСвязи                                 КАК КлючСвязи
	|	ИЗ
	|		СкидкиНаценки КАК ТабличнаяЧасть
	|	) КАК ТабличнаяЧасть
	|";
	
	Запрос.УстановитьПараметр("СкидкиНаценки", СкидкиНаценки);
	Запрос.УстановитьПараметр("Период",        Дата);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Возврат Результат;
	
КонецФункции

Функция ТаблицаНачислениеИСписание(ПравилоНачисления, Дата) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос("
	|ВЫБРАТЬ
	|	ПравилоНачисления.СхемаКомпоновкиДанных КАК СхемаКомпоновкиДанных,
	|	ПравилоНачисления.ХранилищеСхемыКомпоновкиДанных КАК ХранилищеСхемыКомпоновкиДанных,
	|	ПравилоНачисления.ХранилищеНастроекКомпоновкиДанных КАК ХранилищеНастроекКомпоновкиДанных,
	|	ПравилоНачисления.Владелец КАК БонуснаяПрограммаЛояльности
	|ИЗ
	|	Справочник.ПравилаНачисленияИСписанияБонусныхБаллов КАК ПравилоНачисления
	|ГДЕ
	|	ПравилоНачисления.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ПравилоНачисления);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Если ЗначениеЗаполнено(Выборка.СхемаКомпоновкиДанных) Тогда
		СхемаКомпоновкиДанных = Справочники.ПравилаНачисленияИСписанияБонусныхБаллов.ПолучитьМакет(Выборка.СхемаКомпоновкиДанных);
	Иначе
		ВыборкаХранилищеСхемыКомпоновкиДанных = Выборка.ХранилищеСхемыКомпоновкиДанных; // ХранилищеЗначения
		СхемаКомпоновкиДанных = ВыборкаХранилищеСхемыКомпоновкиДанных.Получить();
	КонецЕсли;
	
	ВыборкаХранилищеНастроекКомпоновкиДанных = Выборка.ХранилищеНастроекКомпоновкиДанных; // ХранилищеЗначения
	НастройкиКомпоновкиДанных = ВыборкаХранилищеНастроекКомпоновкиДанных.Получить();
	
	// Подготовка компоновщика макета компоновки данных, загрузка настроек
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));

	Если НастройкиКомпоновкиДанных <> Неопределено Тогда
		КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновкиДанных);
		КомпоновщикНастроек.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
	Иначе
		КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	КонецЕсли;

	СегментыСервер.ВключитьОтборПоСегментуНоменклатурыВСКД(КомпоновщикНастроек);
	СегментыСервер.ВключитьОтборПоСегментуПартнеровВСКД(КомпоновщикНастроек);

	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ТекущаяДата");
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = Истина;
		Параметр.Значение = Дата;
	КонецЕсли;

	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("АвтоматическоеНачисление");
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = Истина;
		Параметр.Значение = ПравилоНачисления;
	КонецЕсли;

	Параметр = КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("БонуснаяПрограммаЛояльности");
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = Истина;
		Параметр.Значение = Выборка.БонуснаяПрограммаЛояльности;
	КонецЕсли;
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		СхемаКомпоновкиДанных,
		КомпоновщикНастроек.ПолучитьНастройки(), , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"), Ложь);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,,,Истина);
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;

	ДанныеОтчета = Новый ТаблицаЗначений();
	ПроцессорВывода.УстановитьОбъект(ДанныеОтчета);
	ДанныеОтчета = ПроцессорВывода.Вывести(ПроцессорКомпоновки);

	Возврат ДанныеОтчета;
	
КонецФункции

#КонецОбласти
