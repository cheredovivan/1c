
#Область ПрограммныйИнтерфейс

#Область Фискализация

//++ Локализация

// Возвращает таблицу товаров для заполнения позиций строк в параметрах чека на оплату
// 
// Параметры:
// 	ДокументСсылка - ДокументСсылка - Документ для получения товарных позиций
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица с товарными позициями с количественными и суммовыми показателями
Функция ПозицииНоменклатурыПоДокументу(ДокументСсылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументСсылка, "НалогообложениеНДС");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПодарочныеСертификаты.Наименование КАК НоменклатураНаименование,
	|	""""                                                    КАК ХарактеристикаНаименование,
	|	""""                                                    КАК Упаковка,
	|	""""                                                    КАК УпаковкаНаименование,
	|
	|	1                                                       КАК Количество,
	|
	|	ПодарочныеСертификаты.Владелец.Номинал                  КАК Цена,
	|	ПодарочныеСертификаты.Владелец.Номинал                  КАК Сумма,
	|
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтавкиНДС.БезНДС)
	|		ИНАЧЕ &СтавкаНДС20_120
	|	КОНЕЦ                                                   КАК СтавкаНДС,
	|
	|	ВЫБОР
	|		КОГДА &НалогообложениеНДС В (ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаОблагаетсяЕНВД),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаНеОблагаетсяНДС),
	|				ЗНАЧЕНИЕ(Перечисление.ТипыНалогообложенияНДС.ПродажаПоПатенту))
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ПодарочныеСертификаты.Владелец.Номинал / 120 * 20 КАК ЧИСЛО(31,2))
	|	КОНЕЦ                                                   КАК СуммаНДС
	|	
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Справочник.ПодарочныеСертификаты КАК ПодарочныеСертификаты
	|ГДЕ
	|	ПодарочныеСертификаты.Ссылка = &Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Товары.НоменклатураНаименование,
	|	Товары.ХарактеристикаНаименование,
	|	
	|	Товары.Упаковка,
	|	Товары.УпаковкаНаименование,
	|	
	|	Товары.Количество,
	|	Товары.Цена,
	|	Товары.СтавкаНДС,
	|	Товары.СуммаНДС,
	|	Товары.Сумма
	|ИЗ
	|	ТаблицаТовары КАК Товары
	|";
		
	Запрос.УстановитьПараметр("Ссылка"			  , ДокументСсылка);
	Запрос.УстановитьПараметр("НалогообложениеНДС", РеквизитыДокумента.НалогообложениеНДС);
	Запрос.УстановитьПараметр("СтавкаНДС20_120"	  , УчетНДСРФКлиентСерверПовтИсп.СтавкаНДСПоЗначениюПеречисления(Перечисления.СтавкиНДС.НДС20_120));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

//-- Локализация

#КонецОбласти

//Дополняет переданный массив разрешенных типов для зачета оплат подарочными сертификатами.
//
// Параметры:
//  РазрешенныеТипы - Массив Из ДокументСсылка, СправочникСсылка - Типы разрешенных ссылок для проверки.
//
Процедура ДополнитьРазрешенныеТипыДляЗачетаОплатыПодарочнымиСертификатами(РазрешенныеТипы) Экспорт
	
	//++ НЕ УТКА
	РазрешенныеТипы.Добавить(Тип("ДокументСсылка.ЗаказДавальца2_5"));
	РазрешенныеТипы.Добавить(Тип("ДокументСсылка.ОтчетДавальцу2_5"));
	//-- НЕ УТКА
	
	//++ Локализация
	
	//++ НЕ УТКА
	РазрешенныеТипы.Добавить(Тип("ДокументСсылка.ЗаказДавальца"));
	РазрешенныеТипы.Добавить(Тип("ДокументСсылка.ОтчетДавальцу"));
	//-- НЕ УТКА

	//-- Локализация

КонецПроцедуры

//++ НЕ УТКА

// Проверяет, что переданный тип относится к типам документов давальческой схемы.
//
// Параметры:
//  ТипДокумента - Тип - Проверяемый тип.
//
// Возвращаемое значение:
//  Булево -
Функция ТипОтноситсяКДокументамДавальческойСхемы(ТипДокумента) Экспорт
	Результат = Ложь;
	
	Если ТипДокумента = Тип("ДокументСсылка.ЗаказДавальца2_5")
			Или ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцу2_5") Тогда
		Результат = Истина;
	КонецЕсли;
	
	//++ Локализация
	Если Не Результат
		И (ТипДокумента = Тип("ДокументСсылка.ЗаказДавальца")
			Или ТипДокумента = Тип("ДокументСсылка.ОтчетДавальцу")) Тогда
		
		Результат = Истина;
	КонецЕсли;
	//-- Локализация
	
	Возврат Результат;
КонецФункции

//-- НЕ УТКА

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

//++ Локализация

#Область ОбработчикиЭтаповЗакрытияМесяца

#Область АннулированиеСертификатов

// Обработчики этапа.

// Добавляет этап в таблицу этапов закрытия месяца.
// Элементы данной таблицы являются элементами второго уровня в дереве этапов в форме закрытия месяца.
// 
// Параметры:
// 	ТаблицаЭтапов - см. Обработки.ОперацииЗакрытияМесяца.ИнициализироватьТаблицуОписанияЭтапов
// 	ТекущийРодитель - Строка - идентификатор группы.
Процедура ДобавитьЭтап_АннулированиеПодарочныхСертификатов(ТаблицаЭтапов,ТекущийРодитель) Экспорт
	
	ПредшествующийЭтап = Неопределено;
	//++ НЕ УТ
	ПредшествующийЭтап = ИнтеграцияБЗК.ОперацияОтражениеЗарплатыВФинансовомУчете();
	//-- НЕ УТ
	
	НоваяСтрока = ЗакрытиеМесяцаСервер.ДобавитьЭтапВТаблицу(ТаблицаЭтапов, ТекущийРодитель,
		Справочники.ОперацииЗакрытияМесяца.АннулированиеПодарочныхСертификатов,,,, ПредшествующийЭтап);
	
	НоваяСтрока.МультиязычноеНаименование = "ru = 'Аннулирование подарочных сертификатов';
											|en = 'Void gift card'"; // @НСтр-1
	НоваяСтрока.ТекстВыполнить = НСтр("ru = 'Аннулировать';
										|en = 'Cancel'");
	НоваяСтрока.ДействиеИспользование = ЗакрытиеМесяцаСервер.ОписаниеДействия_СервернаяПроцедура(
		"ПодарочныеСертификатыЛокализация.Использование_АннулированиеПодарочныхСертификатов");
	НоваяСтрока.ДействиеВыполнить  = ЗакрытиеМесяцаСервер.ОписаниеДействия_ВыполнитьРасчет(
		"ПодарочныеСертификатыЛокализация.Выполнить_АннулированиеПодарочныхСертификатов");
	НоваяСтрока.ДействиеПодробнее = ЗакрытиеМесяцаСервер.ОписаниеДействия_ОткрытьФорму(
		Метаданные.Документы.АннулированиеПодарочныхСертификатов.Формы.ФормаСписка.ПолноеИмя());
		
КонецПроцедуры

Процедура Использование_АннулированиеПодарочныхСертификатов(ПараметрыОбработчика) Экспорт
	
	ЗакрытиеМесяцаСервер.УвеличитьКоличествоОбработанныхДанныхДляЗамера(ПараметрыОбработчика, 1);
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьПодарочныеСертификаты") Тогда
		ЗакрытиеМесяцаСервер.УстановитьСостояниеОтключено(
			ПараметрыОбработчика,
			НСтр("ru = 'Подарочные сертификаты не используются.';
				|en = 'Gift cards are not used.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	ЗакрытиеМесяцаСервер.ИнициализироватьЗапрос(Запрос, ПараметрыОбработчика);
	
	Документы.АннулированиеПодарочныхСертификатов.ПросроченныеПодарочныеСертификатыНаДату(
		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
		ПараметрыОбработчика.ПараметрыРасчета.ДатаОкончанияПериода + 1,
		,
		"СертификатыКАннулированию",
		ПараметрыОбработчика.МенеджерВременныхТаблиц);
	
	Запрос.Текст = "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аннулирование.Организация КАК Организация
	|ПОМЕСТИТЬ АннулированныеСертификаты
	|ИЗ
	|	Документ.АннулированиеПодарочныхСертификатов КАК Аннулирование
	|ГДЕ
	|	Аннулирование.Организация В(&МассивОрганизаций)
	|	И Аннулирование.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И Аннулирование.Проведен";
	
	Запрос.Выполнить();
	
	РазмерыВременныхТаблиц = ЗакрытиеМесяцаСервер.РазмерыВременныхТаблиц(Запрос, ПараметрыОбработчика);
	
	Если РазмерыВременныхТаблиц.СертификатыКАннулированию > 0 Тогда
			
		ЗакрытиеМесяцаСервер.ЗафиксироватьРезультатыОбработчикаИспользованияПоОрганизациям(
			ПараметрыОбработчика,
			НСтр("ru = 'По организации ""%1"" за период %2 есть сертификаты, которые необходимо аннулировать.';
				|en = 'There are certificates which should be canceled for the ""%1"" company for period %2.'", ОбщегоНазначения.КодОсновногоЯзыка()),
			Запрос,
			"СертификатыКАннулированию",
			Ложь,
			Истина);
		
	ИначеЕсли РазмерыВременныхТаблиц.АннулированныеСертификаты = 0 Тогда
		
		ЗакрытиеМесяцаСервер.УстановитьСостояниеНеТребуется(
			ПараметрыОбработчика,
			НСтр("ru = 'Нет сертификатов, которые необходимо аннулировать.';
				|en = 'No certificates to cancel.'", ОбщегоНазначения.КодОсновногоЯзыка()));
		
	КонецЕсли;
	
КонецПроцедуры

Процедура Выполнить_АннулированиеПодарочныхСертификатов(ПараметрыОбработчика) Экспорт
	
	Документы.АннулированиеПодарочныхСертификатов.АннулироватьПодарочныеСертификатыПриЗакрытииМесяца(
		ПараметрыОбработчика.ПараметрыРасчета.МассивОрганизаций,
		ПараметрыОбработчика.ПараметрыРасчета.ДатаОкончанияПериода);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//-- Локализация

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Код процедур и функций

#КонецОбласти
