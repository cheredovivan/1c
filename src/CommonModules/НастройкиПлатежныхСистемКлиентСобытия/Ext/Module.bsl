///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2025, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "ИнтернетПоддержкаПользователей.НастройкиПлатежныхСистем".
// ОбщийМодуль.НастройкиПлатежныхСистемКлиентСобытия.
// 
// Обработчики событий, возникающих при работе с платежными системами:
//  - выбор строки на форме общих настроек;
//  - обработка оповещения;
//  - копирование настройки;
//  - обработка нажатия кнопки команды на форме.
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Вызывается при выборе строки в общем списке настроек подключения к платежным системам.
//  Если необходимо обработать более сложную логику, то можно установить значение Истина для параметра
//  ПараметрыВыбора.ТребуетсяДополнительнаяОбработка. В этом случае обработка выбора будет прервана, а вместо нее будет 
//  вызвана процедура НастройкиПлатежныхСистемКлиентСобытия.ПриДополнительнойОбработкеВыбораНастройки.
//  Если для дополнительной обработки требуются какие-то параметры, то они могут быть указаны в параметре
//  ПараметрыВыбора.ПараметрыДополнительнойОбработки.
//  Пример вызова дополнительной обработки можно посмотреть в подсистеме СБП:
//   СистемаБыстрыхПлатежейКлиент.ПриОбработкеВыбораНастройки.
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки.
//  
Процедура ПриОбработкеВыбораНастройки(ПараметрыВыбора) Экспорт

	Если ПараметрыВыбора.ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейКлиент");
		МодульСистемаБыстрыхПлатежейКлиент.ПриОбработкеВыбораНастройки(ПараметрыВыбора);

	ИначеЕсли ПараметрыВыбора.ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		
		МодульИнтернетЭквайрингКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетЭквайрингКлиент");
		МодульИнтернетЭквайрингКлиент.ПриОбработкеВыбораНастройки(ПараметрыВыбора);
	
	Иначе
		
		ПараметрыВыбора.Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается в случае необходимости дополнительной обработки выбора значения.
//  Чтобы продолжить процесс обработки выбора значения после выполнения дополнительных действий
//  необходимо вызвать процедуру, описанную в параметре ОбработкаЗавершения.
//  В качестве результата в нее должно быть передано обработанное значение параметра ПараметрыВыбора;
// 
// Параметры:
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройки.
//  ОбработкаЗавершения - ОписаниеОповещения - описание оповещения.
Процедура ПриДополнительнойОбработкеВыбораНастройки(ПараметрыВыбора, ОбработкаЗавершения) Экспорт
	
	Если ПараметрыВыбора.ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейКлиент");
		МодульСистемаБыстрыхПлатежейКлиент.ПриДополнительнойОбработкеВыбораНастройки(
			ПараметрыВыбора,
			ОбработкаЗавершения);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при выполнении команды Изменить контекстного меню общей формы настроек платежных систем.
// 
// Параметры:
//  ИмяПодсистемы - Строка - имя подсистемы, к которой относится строка.
//  ПараметрыВыбора - см. НастройкиПлатежныхСистемКлиент.НовыйПараметрыОбработкиВыбораНастройкиДляИзменения.
//
Процедура ПриОбработкеВыбораНастройкиДляИзменения(
		ИмяПодсистемы,
		ПараметрыВыбора) Экспорт
		
	Если ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейКлиент");
		МодульСистемаБыстрыхПлатежейКлиент.ПриОбработкеВыбораНастройкиДляИзменения(ПараметрыВыбора);
		
	ИначеЕсли ИмяПодсистемы = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		
		МодульИнтернетЭквайрингКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетЭквайрингКлиент");
		МодульИнтернетЭквайрингКлиент.ПриОбработкеВыбораНастройкиДляИзменения(ПараметрыВыбора);
		
	Иначе
		
		ПараметрыВыбора.Отказ = Истина;
		
	КонецЕсли;

КонецПроцедуры

// Вызывается при срабатывании события ОбработкаОповещения общей формы настроек платежных систем.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма настроек.
//  ОбновитьСписок - Булево - необходимо установить в Истина, если при наступлении события требуется обновить дерево.
//  ИмяСобытия - Строка- Имя события. Может быть использовано для идентификации сообщений принимающими их формами.
//  Параметр - Произвольный - Параметр сообщения. Могут быть переданы любые необходимые данные.
//  Источник - Произвольный - Источник события. Например, в качестве источника может быть указана другая форма.
//
Процедура ПриОбработкеОповещения(
		Форма,
		ОбновитьСписок,
		ИмяСобытия,
		Параметр,
		Источник) Экспорт

	Если ИмяСобытия = "БИПСменаУчастникаСБП"
		И Форма.КешПараметров.ВидимостьПодсистемВСписке.Получить(НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП()) Тогда
		ОбновитьСписок = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при выполнении команды из подменю ГруппаДополнительно.
// 
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма настроек.
//  Команда - КомандаФормы - выполняемая команда.
//  
Процедура ПриВыполненииКомандыДополнительно(Форма, Команда) Экспорт
	
	Если Не НастройкиПлатежныхСистемКлиент.КомандаДоступна(Форма, Команда) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Форма.Элементы.Список.ТекущиеДанные;
	ТекущаяПодсистема = ТекущиеДанные.Подсистема;
	
	Если Команда.Имя = "ОткрытьШаблоныНазначения" Тогда
		
		Если ТекущиеДанные.ЭтоГруппа Тогда
			ПоказатьПредупреждение(
				Неопределено,
				НСтр("ru = 'Работа с шаблонами назначения недоступна для группы.';
					|en = 'Operations with assignment templates are unavailable for the group.'"));
			Возврат;
		КонецЕсли;
		
		НастройкиПлатежныхСистемКлиент.ОткрытьНастройкуШаблоновНазначения(
			ТекущиеДанные.Ссылка,
			Форма);

	ИначеЕсли Команда.Имя = "ОткрытьКассовыеСсылки"
		И ТекущаяПодсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейКлиент");
		МодульСистемаБыстрыхПлатежейКлиент.ОткрытьКассовыеСсылкиКоманда(Форма.Элементы.Список.ТекущиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

// Вызывается при нажатии кнопки копирования настройки.
// 
// Параметры:
//  ТекущиеДанные - Неопределено, Произвольный - выбранная строка или структура, с ее описанием.
//  ОписаниеОповещения - ОписаниеОповещения - описание оповещения метода, который должно быть вызвано после закрытия
//    формы создания новой настройки.
//
Процедура ПриКопированииНастройки(ТекущиеДанные, ОписаниеОповещения) Экспорт
	
	Если ТекущиеДанные.Подсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыСБП() Тогда
		
		МодульСистемаБыстрыхПлатежейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("СистемаБыстрыхПлатежейКлиент");
		МодульСистемаБыстрыхПлатежейКлиент.СкопироватьНастройку(
			ТекущиеДанные,
			ОписаниеОповещения);
		
	ИначеЕсли ТекущиеДанные.Подсистема = НастройкиПлатежныхСистемКлиентСервер.ИмяПодсистемыЭквайринг() Тогда
		
		МодульИнтернетЭквайрингКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтернетЭквайрингКлиент");
		МодульИнтернетЭквайрингКлиент.СкопироватьНастройку(
			ТекущиеДанные,
			ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
