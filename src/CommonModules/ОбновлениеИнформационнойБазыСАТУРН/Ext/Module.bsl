////////////////////////////////////////////////////////////////////////////////
// Обновление информационной базы библиотеки интеграции с МОТП.
// 
/////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Получение сведений о библиотеке (или конфигурации).

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииПодсистемы
Процедура ПриДобавленииПодсистемы(Описание) Экспорт
	
	Описание.Имя    = "БиблиотекаИнтеграцииСАТУРН";
	Описание.Версия = ОбщегоНазначенияИС.ВерсияПодсистемы("10");
	Описание.РежимВыполненияОтложенныхОбработчиков = "Параллельно";
	
	Описание.ТребуемыеПодсистемы.Добавить("СтандартныеПодсистемы");
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт

#Область Монопольно
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "10.1.23.1";
	Обработчик.Процедура = "ОбновлениеИнформационнойБазыСАТУРН.НачальноеЗаполнениеСАТУРН";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.РежимВыполнения = "Монопольно";
	Обработчик.Комментарий = НСтр("ru = 'Заполнение настроек интеграции с ФГИС ""Сатурн"".';
									|en = 'Заполнение настроек интеграции с ФГИС ""Сатурн"".'");

#КонецОбласти

#Область Отложенно
	
	РазделениеВключено                     = ОбщегоНазначения.РазделениеВключено();
	ДоступноИспользованиеРазделенныхДанных = ОбщегоНазначения.ДоступноИспользованиеРазделенныхДанных();
	
	Если (Не РазделениеВключено
		Или РазделениеВключено И ДоступноИспользованиеРазделенныхДанных)
		И Константы.ВестиУчетПестицидовАгрохимикатовТукосмесейСАТУРН.Получить() Тогда
		
		Документы.АктИнвентаризацииСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.АктПримененияСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.ЗапросОстатковПартийСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.ИмпортПродукцииСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.НакладнаяСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.ПланПримененияСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		Документы.ПроизводственнаяОперацияСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		РегистрыСведений.ОчередьСообщенийСАТУРН.ПриДобавленииОбработчиковОбновления(Обработчики);
		
	КонецЕсли;
	
#КонецОбласти

КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПередОбновлениемИнформационнойБазы
Процедура ПередОбновлениемИнформационнойБазы() Экспорт
	
	ВерсияКонфигурации = ОбновлениеИнформационнойБазы.ВерсияИБ(Метаданные.Имя);
	Если ВерсияКонфигурации <> "0.0.0.0" Тогда
		
		ИдентификаторБиблиотекаИнтеграцииСАТУРН = "БиблиотекаИнтеграцииСАТУРН";
		ВерсияБиблиотекаИнтеграцииСАТУРН = ОбновлениеИнформационнойБазы.ВерсияИБ(ИдентификаторБиблиотекаИнтеграцииСАТУРН);
		Если ВерсияБиблиотекаИнтеграцииСАТУРН = "0.0.0.0" Тогда
			
			ОбновлениеИнформационнойБазы.УстановитьВерсиюИБ("БиблиотекаИнтеграцииСАТУРН", "1.0.0.0", Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПослеОбновленияИнформационнойБазы
Процедура ПослеОбновленияИнформационнойБазы(Знач ПредыдущаяВерсия, Знач ТекущаяВерсия,
		Знач ВыполненныеОбработчики, ВыводитьОписаниеОбновлений, МонопольныйРежим) Экспорт
	Возврат;
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыПереопределяемый.ПриПодготовкеМакетаОписанияОбновлений
Процедура ПриПодготовкеМакетаОписанияОбновлений(Знач Макет) Экспорт
	Возврат;
КонецПроцедуры

// Позволяет переопределить режим обновления данных информационной базы.
// Для использования в редких (нештатных) случаях перехода, не предусмотренных в
// стандартной процедуре определения режима обновления.
//
// Параметры:
//   РежимОбновленияДанных - Строка - в обработчике можно присвоить одно из значений:
//              "НачальноеЗаполнение"     - если это первый запуск пустой базы (области данных);
//              "ОбновлениеВерсии"        - если выполняется первый запуск после обновление конфигурации базы данных;
//              "ПереходСДругойПрограммы" - если выполняется первый запуск после обновление конфигурации базы данных, 
//                                          в которой изменилось имя основной конфигурации.
//
//   СтандартнаяОбработка  - Булево - если присвоить Ложь, то стандартная процедура
//                                    определения режима обновления не выполняется, 
//                                    а используется значение РежимОбновленияДанных.
//
Процедура ПриОпределенииРежимаОбновленияДанных(РежимОбновленияДанных, СтандартнаяОбработка) Экспорт
	Возврат;
КонецПроцедуры

// Добавляет в список процедуры-обработчики перехода с другой программы (с другим именем конфигурации).
// Например, для перехода между разными, но родственными конфигурациями: базовая -> проф -> корп.
// Вызывается перед началом обновления данных ИБ.
//
// Параметры:
//  Обработчики - ТаблицаЗначений - с колонками:
//    * ПредыдущееИмяКонфигурации - Строка - имя конфигурации, с которой выполняется переход;
//                                           или "*", если нужно выполнять при переходе с любой конфигурации.
//    * Процедура                 - Строка - полное имя процедуры-обработчика перехода с программы ПредыдущееИмяКонфигурации. 
//                                  Например, "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику"
//                                  Обязательно должна быть экспортной.
//
// Пример добавления процедуры-обработчика в список:
//  Обработчик = Обработчики.Добавить();
//  Обработчик.ПредыдущееИмяКонфигурации  = "УправлениеТорговлей";
//  Обработчик.Процедура                  = "ОбновлениеИнформационнойБазыУПП.ЗаполнитьУчетнуюПолитику";
//
Процедура ПриДобавленииОбработчиковПереходаСДругойПрограммы(Обработчики) Экспорт
	Возврат;
КонецПроцедуры

// Вызывается после выполнения всех процедур-обработчиков перехода с другой программы (с другим именем конфигурации),
// и до начала выполнения обновления данных ИБ.
//
// Параметры:
//  ПредыдущееИмяКонфигурации    - Строка - имя конфигурации до перехода.
//  ПредыдущаяВерсияКонфигурации - Строка - имя предыдущей конфигурации (до перехода).
//  Параметры                    - Структура:
//    * ВыполнитьОбновлениеСВерсии   - Булево - по умолчанию Истина. Если установить Ложь, 
//        то будут выполнена только обязательные обработчики обновления (с версией "*").
//    * ВерсияКонфигурации           - Строка - номер версии после перехода. 
//        По умолчанию, равен значению версии конфигурации в свойствах метаданных.
//        Для того чтобы выполнить, например, все обработчики обновления с версии ПредыдущаяВерсияКонфигурации, 
//        следует установить значение параметра в ПредыдущаяВерсияКонфигурации.
//        Для того чтобы выполнить вообще все обработчики обновления, установить значение "0.0.0.1".
//    * ОчиститьСведенияОПредыдущейКонфигурации - Булево - по умолчанию Истина. 
//        Для случаев когда предыдущая конфигурация совпадает по имени с подсистемой текущей конфигурации, следует
//        указать Ложь.
//
Процедура ПриЗавершенииПереходаСДругойПрограммы(Знач ПредыдущееИмяКонфигурации, Знач ПредыдущаяВерсияКонфигурации, Параметры) Экспорт
	Возврат;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиОбновления

Процедура НачальноеЗаполнениеСАТУРН() Экспорт
	
	Если Не ЗначениеЗаполнено(ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияКилограмм()) Тогда
		ЕдиницаИзмерения = Неопределено;
		ИнтеграцияИСПереопределяемый.ЕдиницаИзмеренияКилограмм(ЕдиницаИзмерения);
		Если ЕдиницаИзмерения <> Неопределено Тогда
			Константы.ЕдиницаИзмеренияКилограммИС.Установить(ЕдиницаИзмерения);
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИнтеграцияИСВызовСервера.ЕдиницаИзмеренияЛитр()) Тогда
		ЕдиницаИзмерения = Неопределено;
		ИнтеграцияИСПереопределяемый.ЕдиницаИзмеренияЛитр(ЕдиницаИзмерения);
		Если ЕдиницаИзмерения <> Неопределено Тогда
			Константы.ЕдиницаИзмеренияЛитрИС.Установить(ЕдиницаИзмерения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОбработчикПриПереходеНаНовуюВерсиюAPI(ОбъектМетаданных, Идентификатор, Обработчики) Экспорт
	
	ВерсияОчисткиДанныхПриОбновленииAPI = "10.1.38.1";
	ШаблонКомментария = НСтр("ru = 'Очистка устаревших данных ""%1"" при переходне на обновленную версию API ФГИС ""Сатурн""';
							|en = 'Очистка устаревших данных ""%1"" при переходне на обновленную версию API ФГИС ""Сатурн""'");
	ПолноеИмя         = ОбъектМетаданных.ПолноеИмя();
	
	ИмяБазовогоТипа = ОбщегоНазначения.ИмяБазовогоТипаПоОбъектуМетаданных(ОбъектМетаданных);
	ИмяОбъекта      = СтрШаблон("%1.%2", ИмяБазовогоТипа, ОбъектМетаданных.Имя);
	ИменаОбъектов   = Новый Массив;
	ИменаОбъектов.Добавить(ПолноеИмя);
	Если ОбъектМетаданных <> Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН Тогда
		ИменаОбъектов.Добавить(Метаданные.РегистрыСведений.ОчередьСообщенийСАТУРН.ПолноеИмя());
		ИменаОбъектов.Добавить(Метаданные.РегистрыСведений.СтатусыДокументовСАТУРН.ПолноеИмя());
	КонецЕсли;
	ВсеИменаОбъектов = СтрСоединить(ИменаОбъектов, ",");
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия               = ВерсияОчисткиДанныхПриОбновленииAPI;
	Обработчик.РежимВыполнения      = "Отложенно";
	Обработчик.Идентификатор        = Идентификатор;
	Обработчик.ЧитаемыеОбъекты      = ВсеИменаОбъектов;
	Обработчик.ИзменяемыеОбъекты    = ВсеИменаОбъектов;
	Обработчик.БлокируемыеОбъекты   = ПолноеИмя;
	Обработчик.Многопоточный        = Истина;
	Обработчик.Комментарий          = СтрШаблон(ШаблонКомментария, ОбъектМетаданных.Синоним);
	Обработчик.ПриоритетыВыполнения = ОбновлениеИнформационнойБазы.ПриоритетыВыполненияОбработчика();
	Обработчик.ПроцедураПроверки    = "ОбновлениеИнформационнойБазы.ДанныеОбновленыНаНовуюВерсиюПрограммы";
	Обработчик.Процедура            = СтрШаблон("%1.ОбработатьДанныеДляПереходаНаНовуюВерсию", ИмяОбъекта);
	Обработчик.ПроцедураЗаполненияДанныхОбновления = СтрШаблон("%1.ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию", ИмяОбъекта);
	
КонецПроцедуры

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура ЗарегистрироватьДанныеДляАрхивированияПриПереходеНаНовоеAPI(ОбъектМетаданных, Параметры) Экспорт
	
	ПолноеИмяОбъекта                     = ОбъектМетаданных.ПолноеИмя();
	ПараметрыВыборки                     = Параметры.ПараметрыВыборки;
	ПараметрыВыборки.ПолныеИменаОбъектов = ПолноеИмяОбъекта;
	ПараметрыВыборки.СпособВыборки       = ОбновлениеИнформационнойБазы.СпособВыборкиСсылки();
	ПараметрыВыборки.ПоляУпорядочиванияПриРаботеПользователей.Добавить("Дата УБЫВ");
	ПараметрыВыборки.ПоляУпорядочиванияПриОбработкеДанных.Добавить("Дата УБЫВ");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДействиеНеТребуется", Перечисления.ДальнейшиеДействияПоВзаимодействиюСАТУРН.НеТребуется);
	Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Таблица.Ссылка КАК Ссылка
		|ИЗ
		|	&ИмяТаблицы КАК Таблица
		|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыДокументовСАТУРН КАК СтатусыДокументовСАТУРН
		|	ПО Таблица.Ссылка = СтатусыДокументовСАТУРН.Документ
		|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьСообщенийСАТУРН КАК ОчередьСообщенийСАТУРН
		|	ПО Таблица.Ссылка = ОчередьСообщенийСАТУРН.Документ
		|ГДЕ
		|	НЕ ОчередьСообщенийСАТУРН.Документ ЕСТЬ NULL
		|	ИЛИ СтатусыДокументовСАТУРН.ДальнейшееДействие1 <> &ДействиеНеТребуется";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ПолноеИмяОбъекта);
	
	//@skip-check query-in-loop
	ТабицаДанных    = Запрос.Выполнить().Выгрузить();
	КоличествоСтрок = ТабицаДанных.Количество();
	
	Если КоличествоСтрок > 0 Тогда
		
		ДанныеДляОбработки = ТабицаДанных.ВыгрузитьКолонку("Ссылка");
		ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ДанныеДляОбработки);
		
	КонецЕсли;
		
КонецПроцедуры

// Параметры:
//  ОбъектМетаданных - ОбъектМетаданных
//  Параметры - см. ОбновлениеИнформационнойБазы.ОсновныеПараметрыОтметкиКОбработке
Процедура АрхивироватьДанныеПриПереходеНаНовоеAPI(ОбъектМетаданных, Параметры) Экспорт
	
	ПолноеИмяОбъекта     = ОбъектМетаданных.ПолноеИмя();
	ОбработанныхОбъектов = 0;
	ПроблемныхОбъектов   = 0;
	ОбновляемыеДанные    = ОбновлениеИнформационнойБазы.ДанныеДляОбновленияВМногопоточномОбработчике(Параметры);
	ПроблемыСДанными     = Новый Соответствие();
	
	Если ОбновляемыеДанные.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
			Параметры.Очередь, ПолноеИмяОбъекта);
		Возврат;
	КонецЕсли;
	
	Для Каждого ДанныеКОбработке Из ОбновляемыеДанные Цикл
		
		НачатьТранзакцию();
		
		Попытка
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяОбъекта);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", ДанныеКОбработке.Ссылка);
			Блокировка.Заблокировать();
			
			ОбъектДанных = ДанныеКОбработке.Ссылка.ПолучитьОбъект();
			
			Если ОбъектДанных = Неопределено Тогда
				ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДанныеКОбработке.Ссылка);
				ЗафиксироватьТранзакцию();
				Продолжить;
			КонецЕсли;
			
			ДокументыКАрхивированию = Новый Массив;
			ДокументыКАрхивированию.Добавить(ДанныеКОбработке.Ссылка);
			
			ИнтеграцияСАТУРНВызовСервера.АрхивироватьДокументы(ДокументыКАрхивированию);
			
			ОбновлениеИнформационнойБазы.ОтметитьВыполнениеОбработки(ДанныеКОбработке.Ссылка);
			
			ОбработанныхОбъектов = ОбработанныхОбъектов + 1;
			
			ЗафиксироватьТранзакцию();
		
		Исключение
			
			ОтменитьТранзакцию();
			ПроблемныхОбъектов = ПроблемныхОбъектов + 1;
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать ""%1"" при переходе на новую версию API ФГИС ""Сатурн"" по причине:
					       |%2';
					       |en = 'Не удалось обработать ""%1"" при переходе на новую версию API ФГИС ""Сатурн"" по причине:
					       |%2'"),
				ДанныеКОбработке.Ссылка,
				ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Ошибка,
				ОбъектДанных,,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Для Каждого НеобработанныйОбъект Из ПроблемыСДанными Цикл
		ОбновлениеИнформационнойБазы.ЗарегистрироватьПроблемуСДанными(
			НеобработанныйОбъект.Ключ,
			НеобработанныйОбъект.Значение,
			Параметры);
	КонецЦикла;
	
	Если ОбработанныхОбъектов = 0 И ПроблемныхОбъектов <> 0 Тогда
		ШаблонСообщения = НСтр("ru = 'Не удалось обработать некоторые данные ""%1"": %2';
								|en = 'Не удалось обработать некоторые данные ""%1"": %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектМетаданных.Синоним, ПроблемныхОбъектов);
		ВызватьИсключение ТекстСообщения;
	Иначе
		ШаблонСообщения = НСтр("ru = 'Обработана очередная порция ""%1"": %2';
								|en = 'Обработана очередная порция ""%1"": %2'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, ОбъектМетаданных.Синоним, ОбработанныхОбъектов);
		ЗаписьЖурналаРегистрации(
			ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
			УровеньЖурналаРегистрации.Информация,
			ОбъектМетаданных,, ТекстСообщения);
	КонецЕсли;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов
		+ ОбработанныхОбъектов;
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь, ПолноеИмяОбъекта);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
