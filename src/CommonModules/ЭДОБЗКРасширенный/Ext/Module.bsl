
#Область СлужебныйПрограммныйИнтерфейс

// Добавляет в список Обработчики процедуры-обработчики обновления,
// необходимые данной подсистеме.
//
// Параметры:
//   Обработчики - ТаблицаЗначений - см. описание функции НоваяТаблицаОбработчиковОбновления
//                                   общего модуля ОбновлениеИнформационнойБазы.
// 
Процедура ЗарегистрироватьОбработчикиОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия = "3.1.34.97";
	Обработчик.Процедура = "ЭДОБЗКРасширенный.ОбновитьПредопределенныеШаблоны_3_1_0";
	Обработчик.РежимВыполнения = ОбновлениеИнформационнойБазыЗарплатаКадрыБазовый.ОсновнойРежимВыполненияОбновления();
	Обработчик.Идентификатор = Новый УникальныйИдентификатор("22501ca9-1c1b-494a-b1d2-976d996a228b");
	Обработчик.Комментарий = НСтр("ru = 'Инициализания новых шаблонов документов.';
									|en = 'Initialize new document templates.'");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипыОбъектовСоСтороннимиПечатнымиФормами() Экспорт
	
	ТипыОбъектов = ЭДОБЗКБазовый.ТипыОбъектовСоСтороннимиПечатнымиФормами();
	ТипыОбъектов.Добавить(Тип("ДокументСсылка.УвольнениеСписком"));
	
	Возврат ТипыОбъектов;
	
КонецФункции

Процедура ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек) Экспорт
	
	ЭДОБЗКБазовый.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	
	ЗарплатаКадрыРасширенный.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	ЗаймыСотрудникам.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	РазрядыКатегорииДолжностей.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	РасчетЗарплатыРасширенный.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.КадровыйУчет.ДистанционнаяРабота") Тогда
		МодульДистанционнаяРабота = ОбщегоНазначения.ОбщийМодуль("ДистанционнаяРабота");
		МодульДистанционнаяРабота.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыПриложения.УчетБюджетныхУчреждений") Тогда
		МодульОтчетыПечатныеФормыБюджетныхУчреждений = ОбщегоНазначения.ОбщийМодуль("ОтчетыПечатныеФормыБюджетныхУчреждений");
		МодульОтчетыПечатныеФормыБюджетныхУчреждений.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	КонецЕсли;
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ЗарплатаКадрыКорпоративнаяПодсистемы.ОхранаТруда.МедицинскиеОсмотры") Тогда
		МодульМедицинскиеОсмотры = ОбщегоНазначения.ОбщийМодуль("МедицинскиеОсмотры");
		МодульМедицинскиеОсмотры.ЗаполнитьНастройкиПечатныхФормПоУмолчанию(ОписанияНастроек);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДокументЭДОБЗКФормаСпискаПриСозданииНаСервере(УправляемаяФорма, Отказ, СтандартнаяОбработка) Экспорт
	
	ТекстЗапросаСписка = СокрЛП(УправляемаяФорма.Список.ТекстЗапроса);
	ПозицияСкобки = СтрНайти(ТекстЗапросаСписка, "}", НаправлениеПоиска.СКонца);
	Если ПозицияСкобки > 0 Тогда
		ТекстЗапросаСписка = Лев(ТекстЗапросаСписка, ПозицияСкобки - 1);
		ТекстЗапросаСписка = ТекстЗапросаСписка + "
			|,
			|	(ИСТИНА В
			|			(ВЫБРАТЬ ПЕРВЫЕ 1
			|				ИСТИНА
			|			ИЗ
			|				РегистрСведений.СоставДокументовЗарплатаКадры КАК СоставДокументовЗарплатаКадры
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбораСотрудников
			|					ПО
			|						СоставДокументовЗарплатаКадры.ФизическоеЛицо = ДанныеДляПодбораСотрудников.ФизическоеЛицо
			|			ГДЕ
			|				ДанныеДляПодбораСотрудников.Подразделение В
			|					(ВЫБРАТЬ
			|						РегистрСведений.ПодчиненностьПодразделенийОрганизаций.Подразделение
			|					ИЗ
			|						РегистрСведений.ПодчиненностьПодразделенийОрганизаций
			|					ГДЕ
			|						РегистрСведений.ПодчиненностьПодразделенийОрганизаций.ВышестоящееПодразделение = &Подразделение)
			|				И ДокументДокументЭДОБЗК.Дата МЕЖДУ ДанныеДляПодбораСотрудников.Начало И ДанныеДляПодбораСотрудников.Окончание
			|				И ДокументДокументЭДОБЗК.Ссылка = СоставДокументовЗарплатаКадры.ДокументФизическогоЛица)),
			|	(ИСТИНА В
			|			(ВЫБРАТЬ ПЕРВЫЕ 1
			|				ИСТИНА
			|			ИЗ
			|				РегистрСведений.СоставДокументовЗарплатаКадры КАК СоставДокументовЗарплатаКадры
			|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДанныеДляПодбораСотрудников КАК ДанныеДляПодбораСотрудников
			|					ПО
			|						СоставДокументовЗарплатаКадры.ФизическоеЛицо = ДанныеДляПодбораСотрудников.ФизическоеЛицо
			|			ГДЕ
			|				ДанныеДляПодбораСотрудников.МестоВСтруктуреПредприятия В
			|					(ВЫБРАТЬ
			|						РегистрСведений.ПодчиненностьСтруктурныхЕдиниц.СтруктурнаяЕдиница
			|					ИЗ
			|						РегистрСведений.ПодчиненностьСтруктурныхЕдиниц
			|					ГДЕ
			|						РегистрСведений.ПодчиненностьСтруктурныхЕдиниц.ВышестоящаяСтруктурнаяЕдиница = &МестоВСтруктуреПредприятия)
			|				И ДокументДокументЭДОБЗК.Дата МЕЖДУ ДанныеДляПодбораСотрудников.Начало И ДанныеДляПодбораСотрудников.Окончание
			|				И ДокументДокументЭДОБЗК.Ссылка = СоставДокументовЗарплатаКадры.ДокументФизическогоЛица))}";
		УправляемаяФорма.Список.ТекстЗапроса = ТекстЗапросаСписка;
	КонецЕсли;
	
КонецПроцедуры

#Область УправлениеПечатью

// См. УправлениеПечатьюМультиязычностьПереопределяемый.ПриОпределенииДоступныхДляПереводаМакетов
Процедура ПриОпределенииДоступныхДляПереводаМакетов(Макеты) Экспорт
	
	ЭДОБЗКБазовый.ПриОпределенииДоступныхДляПереводаМакетов(Макеты);
	
КонецПроцедуры

#КонецОбласти

Функция ВидДокументаПоОснованиюИРеквизитам(ОснованиеСсылка, РеквизитыОбъекта) Экспорт
	
	ВидДокумента = ЭДОБЗКБазовый.ВидДокументаПоОснованиюИРеквизитам(ОснованиеСсылка, РеквизитыОбъекта);
	Если Не ЗначениеЗаполнено(ВидДокумента) Тогда
		ШаблонДокумента = Неопределено;
		Если ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаСправкаОстаткиОтпусковКабинетСотрудника") Тогда
			ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗапросСправкиПоОстаткамОтпуска");
		ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаСправкаСМестаРаботыКабинетСотрудника") Тогда
			ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗапросСправкиСМестаРаботы");
		ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаКомпенсацияОтпускаКабинетСотрудника") Тогда
			ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаКомпенсациюОтпуска");
		ИначеЕсли ТипЗнч(ОснованиеСсылка) = Тип("ДокументСсылка.ЗаявкаДСВКабинетСотрудника") Тогда
			ШаблонДокумента = ОбщегоНазначения.ПредопределенныйЭлемент("Справочник.ШаблоныДокументов.ЗаявлениеНаУдержаниеДСВ");
		КонецЕсли;
		Если ЗначениеЗаполнено(ШаблонДокумента) Тогда
			ВидДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ШаблонДокумента, "ВидДокумента");
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВидДокумента;
	
КонецФункции

Процедура ОбновитьПредопределенныеШаблоны_3_1_0(ПараметрыОбновления = Неопределено) Экспорт

	Справочники.ШаблоныДокументов.ОбновитьПредопределенныйШаблонДокумента("ЗаявлениеНаУчебныйОтпуск", ПараметрыОбновления);
	Справочники.ШаблоныДокументов.ОбновитьПредопределенныйШаблонДокумента("ЗаявлениеНаОтпускПоУходуЗаРебенком", ПараметрыОбновления);
	Справочники.ШаблоныДокументов.ОбновитьПредопределенныйШаблонДокумента("ЗаявлениеНаВозвратИзОтпускаПоУходуЗаРебенком", ПараметрыОбновления);

КонецПроцедуры

#КонецОбласти

