// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

#Область СобытияБСП

// См. БлокировкаРаботыСВнешнимиРесурсамиПереопределяемый.ПриЗапретеРаботыСВнешнимиРесурсами
Процедура ПриЗапретеРаботыСВнешнимиРесурсами() Экспорт
	
	Отказ       = Ложь;
	Организации = БизнесСеть.ПодключенныеОрганизации().ВыгрузитьКолонку("Организация");
	
	БизнесСеть.ОтключитьОрганизации(Организации, Отказ);
	
	ОбщегоНазначенияКлиентСервер.Проверить(
		Не Отказ,
		Нстр(
			"ru = 'Ошибка при отключении организаций';
			|en = 'An error occurred when disconnecting companies'", 
			ОбщегоНазначения.КодОсновногоЯзыка()),
		"БизнесСетьСлужебный.ПриЗапретеРаботыСВнешнимиРесурсами");
	
КонецПроцедуры

// См. ОбновлениеИнформационнойБазыБСП.ПриДобавленииОбработчиковОбновления.
Процедура ПриДобавленииОбработчиковОбновления(Обработчики) Экспорт
	
	Обработчик = Обработчики.Добавить();
	Обработчик.Версия              = "1.9.15.75";
	Обработчик.Процедура           = "БизнесСетьСлужебный.УстановитьРасписаниеОбновленияИдентификаторовКонтрагентов";
	Обработчик.НачальноеЗаполнение = Истина;
	Обработчик.РежимВыполнения     = "Оперативно";
	
КонецПроцедуры

// Обработчик обновления.
// Устанавливает расписание регламетного задания ОбновлениеИдентификаторовКонтрагентовБизнесСети случайным образом 
// с целью распределения нагрузки к сервису 1С:Бизнес-сеть.
Процедура УстановитьРасписаниеОбновленияИдентификаторовКонтрагентов() Экспорт
	
	МетаданныеЗадания = Метаданные.РегламентныеЗадания.ОбновлениеИдентификаторовКонтрагентовБизнесСети;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Метаданные", МетаданныеЗадания);
	
	Задания = РегламентныеЗаданияСервер.НайтиЗадания(Отбор);
	
	Если Задания.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Генератор = Новый ГенераторСлучайныхЧисел;
	
	ДатаНачала = Дата("00010101");
	ДатаОкончания = КонецДня(ДатаНачала);
	
	Интервал = ДатаОкончания - ДатаНачала; // Число
	
	Расписание = Новый РасписаниеРегламентногоЗадания;
	Расписание.ВремяНачала       = ДатаНачала + Генератор.СлучайноеЧисло(0, Интервал);
	Расписание.ПериодПовтораДней = 1;
	
	// Изменяемые параметры
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("Расписание", Расписание);

	Для Каждого Задание Из Задания Цикл
		РегламентныеЗаданияСервер.ИзменитьЗадание(Задание.УникальныйИдентификатор, ПараметрыЗадания);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ИННКПП

// Проверяет, являются ли переданные ИНН и КПП сведениями валидными
// 
// Параметры:
//  ИНН - Строка
//  КПП - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны 
//           Ложь, в ином случае.
Функция ЭтоИННКПП(Знач ИНН, Знач КПП) Экспорт
	Возврат ЭтоИННКППЮЛ(ИНН, КПП)
		Или ЭтоИННКППФЛ(ИНН, КПП);
КонецФункции

// Проверяет, являются ли переданные ИНН и КПП сведениями об юр. лице
// 
// Параметры:
//  ИНН - Строка
//  КПП - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны для юр. лица
//           Ложь, в ином случае.
Функция ЭтоИННКППЮЛ(Знач ИНН, Знач КПП) Экспорт
	Возврат ЭтоИННЮЛ(ИНН)
		И ЭтоКПП(КПП);
КонецФункции

// Проверяет, являются ли переданные ИНН и КПП сведениями об физ. лице
// 
// Параметры:
//  ИНН - Строка
//  КПП - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны для физ. лица
//           Ложь, в ином случае.
Функция ЭтоИННКППФЛ(Знач ИНН, Знач КПП) Экспорт
	Возврат ЭтоИННФЛ(ИНН)
		И ПустаяСтрока(КПП);
КонецФункции

// Проверяет, являются ли переданные ИНН валидными
// 
// Параметры:
//  ИНН - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны
//           Ложь, в ином случае.
Функция ЭтоИНН(Знач ИНН) Экспорт
	Возврат ЭтоИННЮЛ(ИНН)
		Или ЭтоИННФЛ(ИНН);
КонецФункции

// Проверяет, являются ли переданные ИНН валидными для юр. лица
// 
// Параметры:
//  ИНН - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны
//           Ложь, в ином случае.
Функция ЭтоИННЮЛ(Знач ИНН) Экспорт
	Возврат РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Истина, "");
КонецФункции

// Проверяет, являются ли переданные ИНН валидными для физ. лица
// 
// Параметры:
//  ИНН - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны
//           Ложь, в ином случае.
Функция ЭтоИННФЛ(Знач ИНН) Экспорт
	Возврат РегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ИНН, Ложь, "");
КонецФункции

// Проверяет, являются ли переданные КПП валидными
// 
// Параметры:
//  КПП - Строка
// 
// Возвращаемое значение:
//  Булево - Истина, если данные валидны
//           Ложь, в ином случае.
Функция ЭтоКПП(Знач КПП) Экспорт
	Возврат РегламентированныеДанныеКлиентСервер.КППСоответствуетТребованиям(КПП, "");
КонецФункции

#КонецОбласти

#Область ПоискКонтрагентов

// Метод-конструктор таблицы кэширования контрагентов
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Новая таблица контрагентов:
// * Контрагент    - ОпределяемыйТип.ОрганизацияКонтрагентEDI - Ссылка на контрагента
// * Идентификатор - Строка - Идентификатор организации в сервисе 1С:Бизнес-сеть
// * ИНН           - Строка - ИНН контрагента
// * КПП           - Строка - КПП контрагента
// * Найден        - Булево - Признак успешного поиска 
Функция НоваяТаблицаПоискаКонтрагентов() Экспорт
	
	Результат = Новый ТаблицаЗначений;
	МетаданныеРегистра = Метаданные.РегистрыСведений.КонтрагентыБизнесСеть;
	
	Результат.Колонки.Добавить("Контрагент"    , МетаданныеРегистра.Измерения.Контрагент.Тип);
	Результат.Колонки.Добавить("Идентификатор" , МетаданныеРегистра.Ресурсы.Идентификатор.Тип);
	Результат.Колонки.Добавить("ИНН"           , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(12)));
	Результат.Колонки.Добавить("КПП"           , Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(9)));
	Результат.Колонки.Добавить("Найден"        , Новый ОписаниеТипов("Булево"));
	
	Возврат Результат;
	
КонецФункции

// Выполняет поиск контрагентов в сервисе 1С:Бизнес-сеть
// 
// Параметры:
//  ТаблицаПоискаКонтрагентов - См. БизнесСетьСлужебный.НоваяТаблицаПоискаКонтрагентов
Процедура ВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов) Экспорт
	
	Если ТаблицаПоискаКонтрагентов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если БизнесСетьПовтИсп.ИспользоватьПоставляемыеДанныеОрганизацийБизнесСеть() Тогда
		ВыполнитьПоискКонтрагентовВПоставляемыхДанных(ТаблицаПоискаКонтрагентов);
	Иначе
		ВыполнитьПоискКонтрагентовВСервисе(ТаблицаПоискаКонтрагентов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ТаблицыЗначений

//@skip-check function-return-types - Универсальный метод
// Разбивает таблицу значений на порции.
// 
// Параметры:
//  Таблица - ТаблицаЗначений - Таблица, которую требуется разбить на порции
//  РазмерПорции - Число - Размер порции
// 
// Возвращаемое значение:
//  Массив Из ТаблицаЗначений - Набор из порций исходной таблицы
Функция РазбитьТаблицуНаПорции(Знач Таблица, РазмерПорции = 1000) Экспорт
	
	Контекст = "БизнесСетьСлужебный.РазбитьТаблицуНаПорции";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(Контекст, "Таблица", Таблица, Тип("ТаблицаЗначений"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(Контекст, "РазмерПорции", РазмерПорции, Тип("Число"));
	ОбщегоНазначенияКлиентСервер.Проверить(
		РазмерПорции > 0,
		СтрШаблон(
			НСтр(
				"ru = 'Размер порции [%1] должен быть больше 0';
				|en = 'The batch size [%1] must be greater than 0'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			РазмерПорции),
		Контекст);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Цел(РазмерПорции) = РазмерПорции,
		СтрШаблон(
			НСтр(
				"ru = 'Размер порции [%1] должен быть целым числом';
				|en = 'The batch size [%1] must be an integer number'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			РазмерПорции),
		Контекст);
	
	Результат = Новый Массив; // Массив Из ТаблицаЗначений
	ЭталоннаяТаблица = Таблица.СкопироватьКолонки();
	ТекущаяПорция = ЭталоннаяТаблица.Скопировать();
	
	Для Каждого Строка Из Таблица Цикл
		
		ЗаполнитьЗначенияСвойств(ТекущаяПорция.Добавить(), Строка);
		
		Если ТекущаяПорция.Количество() >= РазмерПорции Тогда
			// Текущая порция заполнилась до максимального количества:
			// - Добавляем порцию в результат
			// - Создаем новую порцию
			Результат.Добавить(ТекущаяПорция);
			ТекущаяПорция = ЭталоннаяТаблица.Скопировать();
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущаяПорция.Количество() > 0 Тогда
		// Добавляем последнюю порцию, если в ней есть данные
		Результат.Добавить(ТекущаяПорция);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Разбивает массив значений на порции.
// 
// Параметры:
//  Массив - Массив Из Произвольный - Массив, который требуется разбить на порции
//  РазмерПорции - Число - Размер порции
// 
// Возвращаемое значение:
//  Массив Из См. РазбитьМассивНаПорции.Массив - Набор из порций исходного массива
Функция РазбитьМассивНаПорции(Знач Массив, РазмерПорции = 1000) Экспорт
	
	Контекст = "БизнесСетьСлужебный.РазбитьМассивНаПорции";
	
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(Контекст, "Таблица", Массив, Тип("Массив"));
	ОбщегоНазначенияКлиентСервер.ПроверитьПараметр(Контекст, "РазмерПорции", РазмерПорции, Тип("Число"));
	ОбщегоНазначенияКлиентСервер.Проверить(
		РазмерПорции > 0,
		СтрШаблон(
			НСтр(
				"ru = 'Размер порции [%1] должен быть больше 0';
				|en = 'The batch size [%1] must be greater than 0'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			РазмерПорции),
		Контекст);
	ОбщегоНазначенияКлиентСервер.Проверить(
		Цел(РазмерПорции) = РазмерПорции,
		СтрШаблон(
			НСтр(
				"ru = 'Размер порции [%1] должен быть целым числом';
				|en = 'The batch size [%1] must be an integer number'",
				ОбщегоНазначения.КодОсновногоЯзыка()),
			РазмерПорции),
		Контекст);
	
	Результат = Новый Массив;
	ТекущаяПорция = Новый Массив;
	
	Для Каждого Элемент Из Массив Цикл
		
		//@skip-check typed-value-adding-to-untyped-collection - Особенность строгой типизации
		ТекущаяПорция.Добавить(Элемент);
		
		Если ТекущаяПорция.Количество() >= РазмерПорции Тогда
			// Текущая порция заполнилась до максимального количества:
			// - Добавляем порцию в результат
			// - Создаем новую порцию
			//@skip-check typed-value-adding-to-untyped-collection - Особенность строгой типизации
			Результат.Добавить(ТекущаяПорция);
			ТекущаяПорция = Новый Массив;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ТекущаяПорция.Количество() > 0 Тогда
		// Добавляем последнюю порцию, если в ней есть данные
		//@skip-check typed-value-adding-to-untyped-collection - Особенность строгой типизации
		Результат.Добавить(ТекущаяПорция);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПоставляемыеДанные

// См. ВыполнитьПоискКонтрагентов
Процедура ВыполнитьПоискКонтрагентовВПоставляемыхДанных(ТаблицаПоискаКонтрагентов)
	БизнесСетьВМоделиСервиса.ВыполнитьПоискКонтрагентов(ТаблицаПоискаКонтрагентов);
КонецПроцедуры

#КонецОбласти

#Область Имя

// См. ВыполнитьПоискКонтрагентов
Процедура ВыполнитьПоискКонтрагентовВСервисе(ТаблицаПоискаКонтрагентов)
	
	НатуральныеИдентификаторы = Новый Массив; // Массив Из Строка
	
	Для Каждого СтрокаТаблицы Из ТаблицаПоискаКонтрагентов Цикл
		
		Отказ = Ложь;
		
		КортежИдентификаторов = БизнесСеть.ИдентификаторыУчастника(СтрокаТаблицы.ИНН, СтрокаТаблицы.КПП, , Отказ);
		
		Если Отказ Тогда
			Продолжить;
		КонецЕсли;
		
		НатуральныйИдентификатор = БизнесСеть.НатуральныйИдентификаторОрганизации(КортежИдентификаторов);
		НатуральныеИдентификаторы.Добавить(НатуральныйИдентификатор);
		
	КонецЦикла;
	
	Если НатуральныеИдентификаторы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	// Убираем дубли
	ОбщегоНазначенияКлиентСервер.СвернутьМассив(НатуральныеИдентификаторы);
	
	Отказ = Ложь;
	
	ДанныеСервиса = БизнесСеть.ДанныеОрганизацийПоНатуральнымИдентификаторам(НатуральныеИдентификаторы, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаПоискаКонтрагентов.Индексы.Добавить("ИНН, КПП");
	
	Для Каждого ЗаписьСервиса Из ДанныеСервиса Цикл
		Отбор = Новый Структура;
		Отбор.Вставить("ИНН", ЗаписьСервиса.ИНН); 
		Отбор.Вставить("КПП", ЗаписьСервиса.КПП); 
		
		НайденныеСтроки = ТаблицаПоискаКонтрагентов.НайтиСтроки(Отбор);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка.Идентификатор = ЗаписьСервиса.Идентификатор;
			Строка.Найден        = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти