//@strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ЗначенияРеквизитов - Структура:
//   * Проведен - Булево
//   * БезРасчета - Булево
//   * Статус - ПеречислениеСсылка.СтатусыДокументовРетроБонусов
//  Отказ - Булево
//  ВызыватьИсключение - Булево
//
Процедура ПроверитьРеквизитыУсловий(ЗначенияРеквизитов, Отказ, ВызыватьИсключение = Ложь) Экспорт
	
	ТекстСообщения = "";
	
	Если ЗначенияРеквизитов.Проведен <> Истина Тогда
		ТекстСообщения = НСтр("ru = 'Документ условий ретро-бонусов не проведен. Формирование отчета выполнено не будет';
								|en = 'The rebate condition document is not posted. The report will not be generated'");
	КонецЕсли;
	
	Если ЗначенияРеквизитов.БезРасчета = Истина Тогда
		ТекстСообщения = НСтр("ru = 'Формирование отчета не предусмотрено для ретро-бонусов с флагом ""Сумма бонуса рассчитывается вне системы""';
								|en = 'The report cannot be generated for rebates with the ""Rebate amount is calculated outside the system"" checkbox selected'");
	КонецЕсли;
	
	Если ЗначенияРеквизитов.Статус <> Перечисления.СтатусыДокументовРетроБонусов.Согласован Тогда
		ТекстСообщения = НСтр("ru = 'Документ условий ретро-бонусов не согласован. Формирование отчета выполнено не будет';
								|en = 'The rebate conditions document is not approved. The report will not be generated'");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		
		Если ВызыватьИсключение Тогда
			ВызватьИсключение ТекстСообщения;
		Иначе
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПредыдущиеПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
// 
// Возвращаемое значение:
//  Булево
//
Функция ИзменилисьПараметрыУсловийРетроБонусов(ПредыдущиеПараметрыУсловийРетроБонусов, ПараметрыУсловийРетроБонусов) Экспорт
	
	Для Каждого ПолеПараметров Из ПараметрыУсловийРетроБонусов Цикл
		
		ЗначениеНовое = ПараметрыУсловийРетроБонусов[ПолеПараметров.Ключ]; // Произвольный
		ЕстьСвойствоСтарое = ПредыдущиеПараметрыУсловийРетроБонусов.Свойство(ПолеПараметров.Ключ);
		Если НЕ ЕстьСвойствоСтарое Тогда
			Возврат Истина;
		КонецЕсли;
		ЗначениеСтарое = ПредыдущиеПараметрыУсловийРетроБонусов[ПолеПараметров.Ключ]; // Произвольный
		Если ЗначениеНовое <> ЗначениеСтарое Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ДополнительныеСвойства - Структура
//
Процедура СохранитьПараметрыУсловийРетроБонусов(ПараметрыУсловийРетроБонусов, ДополнительныеСвойства) Экспорт
	
	ДополнительныеСвойства.Вставить("ПараметрыУсловийРетроБонусов", ПараметрыУсловийРетроБонусов);
	
КонецПроцедуры

// Параметры:
//  НастройкиПриемник - НастройкиКомпоновкиДанных
//  НастройкиИсточник - НастройкиКомпоновкиДанных
//
Процедура ПеренестиИспользуемыеОтборы(НастройкиПриемник, НастройкиИсточник) Экспорт
	
	Для Каждого ЭлементИсточника Из НастройкиИсточник.Отбор.Элементы Цикл
		
		Если НЕ ЭлементИсточника.Использование Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ЭлементИсточника) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			
			ЭлементПриемника = Неопределено;
			
			Для каждого Элемент Из НастройкиПриемник.Отбор.Элементы Цикл
				
				Если ТипЗнч(Элемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
					Продолжить;
				КонецЕсли;
				
				Если Элемент.ЛевоеЗначение = ЭлементИсточника.ЛевоеЗначение Тогда
					ЭлементПриемника = Элемент;
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
			
			Если ЭлементПриемника = Неопределено Тогда
				
				ЭлементПриемника = НастройкиПриемник.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				ЭлементПриемника.ЛевоеЗначение = ЭлементИсточника.ЛевоеЗначение;
				
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ЭлементПриемника, ЭлементИсточника, "ВидСравнения, ПравоеЗначение, Использование");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  НастройкиПриемник - НастройкиКомпоновкиДанных
//  НастройкиИсточник - НастройкиКомпоновкиДанных
//
Процедура ПеренестиСтруктуруНастроекКомпоновки(НастройкиПриемник, НастройкиИсточник) Экспорт
	
	НастройкиПриемник.Структура.Очистить();
	
	Для Каждого ГруппировкаИсточник Из НастройкиИсточник.Структура Цикл
		
		Если ТипЗнч(ГруппировкаИсточник) = Тип("ГруппировкаКомпоновкиДанных") Тогда
			ГруппировкаПриемник = КомпоновкаДанныхКлиентСервер.ДобавитьГруппировку(НастройкиПриемник);
		ИначеЕсли ТипЗнч(ГруппировкаИсточник) = Тип("ТаблицаКомпоновкиДанных") Тогда
			ГруппировкаПриемник = НастройкиПриемник.Структура.Добавить(Тип("ТаблицаКомпоновкиДанных"));
		Иначе
			Продолжить;
		КонецЕсли;
		
		КомпоновкаДанныхКлиентСервер.СкопироватьНастройкиКомпоновкиДанных(ГруппировкаПриемник, ГруппировкаИсточник);
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ПериодОтчета - СтандартныйПериод
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//
Процедура ПроверитьИсправитьПериодОтчета(ПериодОтчета, ПараметрыУсловийРетроБонусов) Экспорт
	
	ПустаяДата = '00010101';
	
	// проверка границ периода действия по условиям
	
	Если ПериодОтчета.ДатаНачала = ПустаяДата
	 ИЛИ ПериодОтчета.ДатаНачала < ПараметрыУсловийРетроБонусов.НачалоДействия
	 ИЛИ ПериодОтчета.ДатаНачала > ПараметрыУсловийРетроБонусов.ОкончаниеДействия Тогда
		
		Если ПериодОтчета.ДатаНачала <> ПустаяДата Тогда
			
			ТекстСообщения = НСтр("ru = 'Выбранная дата начала не соответствует периоду действия по условиям ретро-бонуса.
				|Дата начала будет приведена к дате начала по условиям ретро-бонуса.';
				|en = 'The selected start date does not correspond to the validity period according to the rebate conditions.
				|The start date will be adjusted to the start date according to the rebate conditions.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
		ПериодОтчета.ДатаНачала = ПараметрыУсловийРетроБонусов.НачалоДействия;
		
	КонецЕсли;
	
	Если ПериодОтчета.ДатаОкончания = ПустаяДата
	 ИЛИ ПериодОтчета.ДатаОкончания < ПараметрыУсловийРетроБонусов.НачалоДействия
	 ИЛИ ПериодОтчета.ДатаОкончания > КонецДня(ПараметрыУсловийРетроБонусов.ОкончаниеДействия) Тогда
		
		Если ПериодОтчета.ДатаОкончания <> ПустаяДата Тогда
			
			ТекстСообщения = НСтр("ru = 'Выбранная дата окончания не соответствует периоду действия по условиям ретро-бонуса.
				|Дата окончания будет приведена к дате окончания по условиям ретро-бонуса.';
				|en = 'The selected end date does not correspond to the validity period according to the rebate conditions.
				|The end date will be adjusted to the end date according to the rebate conditions.'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
		ПериодОтчета.ДатаОкончания = ПараметрыУсловийРетроБонусов.ОкончаниеДействия;
		
	КонецЕсли;
	
	// проверка кратности периодичности начисления по условиям
	
	РезультатПроверкиПериодичность = РетроБонусыСервер.ПроверитьПериодыПоПериодичности(
		ПараметрыУсловийРетроБонусов.ПериодичностьУсловий,
		ПериодОтчета.ДатаНачала,
		ПериодОтчета.ДатаОкончания,
		Истина);
	
	Если РезультатПроверкиПериодичность.ЕстьНарушениеНачала Тогда
		
		ВызватьИсключение РезультатПроверкиПериодичность.ТекстОшибкиНачала;
		
	КонецЕсли;
		
	Если РезультатПроверкиПериодичность.ЕстьНарушениеОкончания Тогда
		
		ВызватьИсключение РезультатПроверкиПериодичность.ТекстОшибкиОкончания;
		
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ПолеКомпоновки - ПолеКомпоновкиДанных
// 
// Возвращаемое значение:
//  Булево
//
Функция ЭтоПолеРасшифровки(ПараметрыВарианта, ПолеКомпоновки) Экспорт
	
	Для Каждого ПолеРасшифровки Из ПараметрыВарианта.ПоляРасшифровки Цикл
		
		Если ПолеРасшифровки = ПолеКомпоновки Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

// Параметры:
//  СтруктураПодчиненная - ГруппировкаКомпоновкиДанных, ГруппировкаТаблицыКомпоновкиДанных -
//  ПолеКомпоновки - ПолеКомпоновкиДанных
//
Процедура ОтключитьИспользованиеПоля(СтруктураПодчиненная, ПолеКомпоновки) Экспорт
	
	ОтключитьИспользованиеВыбранногоПоля(СтруктураПодчиненная.Выбор.Элементы, ПолеКомпоновки);
	
	Если ТипЗнч(СтруктураПодчиненная) = Тип("ГруппировкаКомпоновкиДанных")
	 ИЛИ ТипЗнч(СтруктураПодчиненная) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		Для Каждого ПолеГруппировки Из СтруктураПодчиненная.ПоляГруппировки.Элементы Цикл
			Если ПолеГруппировки.Поле = ПолеКомпоновки Тогда
				ПолеГруппировки.Использование = Ложь;
			КонецЕсли;
			
		КонецЦикла;
	
	КонецЕсли;
	
	Если ТипЗнч(СтруктураПодчиненная) = Тип("ТаблицаКомпоновкиДанных") Тогда
		
		Для Каждого СтрокаТаблицы Из СтруктураПодчиненная.Строки Цикл
			ОтключитьИспользованиеПоля(СтрокаТаблицы, ПолеКомпоновки);
		КонецЦикла;
		
		Для Каждого КолонкаТаблицы Из СтруктураПодчиненная.Колонки Цикл
			ОтключитьИспользованиеПоля(КолонкаТаблицы, ПолеКомпоновки);
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(СтруктураПодчиненная) = Тип("ГруппировкаКомпоновкиДанных")
	      ИЛИ ТипЗнч(СтруктураПодчиненная) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
		
		Для Каждого СтруктураПодчиненная Из СтруктураПодчиненная.Структура Цикл
			ОтключитьИспользованиеПоля(СтруктураПодчиненная, ПолеКомпоновки);
		КонецЦикла;
		
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Элементы - КоллекцияВыбранныхПолейКомпоновкиДанных
//  ПолеКомпоновки - ПолеКомпоновкиДанных
//
Процедура ОтключитьИспользованиеВыбранногоПоля(Элементы, ПолеКомпоновки) Экспорт
	
	Для Каждого ПолеВыбора Из Элементы Цикл
		
		Если ТипЗнч(ПолеВыбора) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ТипЗнч(ПолеВыбора) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			ОтключитьИспользованиеВыбранногоПоля(ПолеВыбора.Элементы, ПолеКомпоновки);
		Иначе
			
			Если ПолеВыбора.Поле = ПолеКомпоновки Тогда
				ПолеВыбора.Использование = Ложь;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ИсходныйДокумент - ДокументСсылка.УсловияРетроБонусовПоставщика
// 
// Возвращаемое значение:
//  См. НовыеПараметрыУсловийРетроБонусов
//
Функция ПараметрыУсловийРетроБонусовПоставщиков(ИсходныйДокумент) Экспорт
	
	АктуальныеДанные = Документы.УсловияРетроБонусовПоставщика.АктуальныеДанные(ИсходныйДокумент);
	Результат = НовыеПараметрыУсловийРетроБонусов();
	ЗаполнитьЗначенияСвойств(Результат, АктуальныеДанные);
	
	Результат.РасчетБонусовПоставщиков = Истина;
	Результат.ПоказательТоваровКоличественный = ПоказательТоваровКоличественный(Результат.ПоказательТоваров);
	
	Если АктуальныеДанные.ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Продажи Тогда
		
		Результат.ПропорциональноеРаспределение = Истина;
		
	Иначе
		
		Если АктуальныеДанные.ПорядокРаспределения = Перечисления.ПорядкиРаспределенияРетроБонусов.ПропорциональноВыручке
		 ИЛИ АктуальныеДанные.ПорядокРаспределения = Перечисления.ПорядкиРаспределенияРетроБонусов.ПропорциональноСуммеЗакупки Тогда
			Результат.ПропорциональноеРаспределение = Истина;
		Иначе
			Результат.ПропорциональноеРаспределение = Ложь;
		КонецЕсли;
		
		Если АктуальныеДанные.ПорядокРаспределения = Перечисления.ПорядкиРаспределенияРетроБонусов.ПустаяСсылка() Тогда
			Результат.ПропорциональноеРаспределение = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат.РаспределениеПоFIFO = (АктуальныеДанные.ПорядокРаспределения
		= Перечисления.ПорядкиРаспределенияРетроБонусов.FIFO);
	
	Результат.ПрогрессивнаяШкалаРасчета = РасчетПоПрогрессивнойШкале(Результат);
	Результат.ШкалаРасчетаПоДиапазонам = РасчетПоДиапазонам(Результат);
	
	Результат.РасчетВБазовыхЦенах = (АктуальныеДанные.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены
		ИЛИ АктуальныеДанные.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены);
	
	Результат.РасчетПоСвойствам = РасчетПоСвойствам(Результат);
	ПараметрыРеквизитаНоменклатуры = ПараметрыРеквизитаНоменклатуры(Результат);
	ЗаполнитьЗначенияСвойств(Результат, ПараметрыРеквизитаНоменклатуры);
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  ИсходныйДокумент - ДокументСсылка.УсловияРетроБонусовКлиентов
// 
// Возвращаемое значение:
//  См. НовыеПараметрыУсловийРетроБонусов
//
Функция ПараметрыУсловийРетроБонусовКлиентов(ИсходныйДокумент) Экспорт
	
	АктуальныеДанные = Документы.УсловияРетроБонусовКлиентов.АктуальныеДанные(ИсходныйДокумент);
	Результат = НовыеПараметрыУсловийРетроБонусов();
	ЗаполнитьЗначенияСвойств(Результат, АктуальныеДанные);
	
	Результат.ПоказательТоваровКоличественный = ПоказательТоваровКоличественный(Результат.ПоказательТоваров);
	Результат.БазаРасчета = АктуальныеДанные.БазаРасчетаПродаж;
	Результат.РасчетВБазовыхЦенах = (АктуальныеДанные.БазаРасчетаПродаж
		= Перечисления.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены);
	Результат.РасчетБонусовКлиентов = Истина;
	Результат.ПропорциональноеРаспределение = (АктуальныеДанные.ПорядокРаспределения
		= Перечисления.ПорядкиРаспределенияРетроБонусов.ПропорциональноВыручке);
	Результат.РаспределениеПоFIFO = (АктуальныеДанные.ПорядокРаспределения
		= Перечисления.ПорядкиРаспределенияРетроБонусов.FIFO);
	
	Результат.ПрогрессивнаяШкалаРасчета = РасчетПоПрогрессивнойШкале(Результат);
	Результат.ШкалаРасчетаПоДиапазонам = РасчетПоДиапазонам(Результат);
	
	Результат.РасчетПоСвойствам = РасчетПоСвойствам(Результат);
	ПараметрыРеквизитаНоменклатуры = ПараметрыРеквизитаНоменклатуры(Результат);
	ЗаполнитьЗначенияСвойств(Результат, ПараметрыРеквизитаНоменклатуры);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура - Актуальные параметры условий ретро-бонусов:
//  * РасчетБонусовКлиентов - Булево -
//  * РасчетБонусовПоставщиков - Булево -
//	* БезРасчета - Булево -
//	* НачалоДействия - Дата -
//	* ОкончаниеДействия - Дата -
//	* ДатаОстатков - Дата -
//	* Организация - СправочникСсылка.Организации -
//	* Валюта - СправочникСсылка.Валюты -
//	* ДетализацияРасчетаУчастников - ПеречислениеСсылка.ДетализацияРасчетаУчастниковРетроБонусов -
//	* ДетализацияРасчетаУчастниковПредставление - Строка -
//	* ПериодичностьНачислений - ПеречислениеСсылка.ПериодичностиРетроБонусов -
//	* ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов -
//	* ПериодичностьУсловийПредставление - Строка -
//	* ФиксацияВыполнена - Булево -
//	* СоставТоваров - ПеречислениеСсылка.СоставыТоваровРетроБонусов -
//	* СоставУчастников - ПеречислениеСсылка.СоставыУчастниковРетроБонусов -
//	* Участники - ПеречислениеСсылка.СоставыСписковРетроБонусов -
//	* Товары - ПеречислениеСсылка.СоставыСписковРетроБонусов -
//	* РасчетПоСвойствам - Булево -
//	* СвойствоНоменклатуры - Строка -
//	* СвойствоНоменклатурыДополнительное - Булево -
//	* СвойствоНоменклатурыСсылка - ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения -
//	* СвойствоНоменклатурыТипЗначения - ОписаниеТипов -
//	* СвойствоНоменклатурыЗаголовок - Строка -
//	* Склады - ПеречислениеСсылка.СоставыСписковРетроБонусов -
//	* ЗапретНачисленияСверхПлана - Булево -
//	* ПоказательТоваров - ПеречислениеСсылка.ЦелевыеПоказателиРетроБонусовПоТоварам -
//	* ПоказательТоваровПредставление - Строка -
//	* ПоказательТоваровКоличественный - Булево -
//	* БазаРасчета - ПеречислениеСсылка.БазыРасчетаРетроБонусов -
//	* РасчетВБазовыхЦенах - Булево -
//	* УчитыватьНДС - Булево -
//	* ПорядокРаспределения - ПеречислениеСсылка.ПорядкиРаспределенияРетроБонусов -
//	* ПропорциональноеРаспределение - Булево -
//	* РаспределениеПоFIFO - Булево -
//	* ДокументУсловийРетроБонусовПредставление - Строка -
//	* Контрагент - СправочникСсылка.Контрагенты -
//	* Партнер - СправочникСсылка.Партнеры -
//	* ТипБонуса - ПеречислениеСсылка.ТипыРетроБонусовПоставщиков -
//	* ВидШкалыРасчета - Число -
//	* ПрогрессивнаяШкалаРасчета - Булево -
//	* ШкалаРасчетаПоДиапазонам - Булево -
//
Функция НовыеПараметрыУсловийРетроБонусов() Экспорт
	
	Результат = Новый Структура;
	
	ПустаяДата = '00010101';
	
	Результат.Вставить("РасчетБонусовКлиентов", Ложь);
	Результат.Вставить("РасчетБонусовПоставщиков", Ложь);
	Результат.Вставить("БезРасчета", Ложь);
	Результат.Вставить("НачалоДействия", ПустаяДата);
	Результат.Вставить("ОкончаниеДействия", ПустаяДата);
	Результат.Вставить("ДатаОстатков", ПустаяДата);
	Результат.Вставить("Организация", Справочники.Организации.ПустаяСсылка());
	Результат.Вставить("Валюта", Справочники.Валюты.ПустаяСсылка());
	Результат.Вставить("ДетализацияРасчетаУчастников", Перечисления.ДетализацияРасчетаУчастниковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ДетализацияРасчетаУчастниковПредставление", "");
	Результат.Вставить("ПериодичностьНачислений", Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ПериодичностьУсловий", Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ПериодичностьУсловийПредставление", "");
	Результат.Вставить("ФиксацияВыполнена", Ложь);
	Результат.Вставить("СоставТоваров", Перечисления.СоставыТоваровРетроБонусов.ПустаяСсылка());
	Результат.Вставить("СоставУчастников", Перечисления.СоставыУчастниковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("Участники", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("Товары", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("РасчетПоСвойствам", Ложь);
	Результат.Вставить("СвойствоНоменклатуры", "");
	Результат.Вставить("СвойствоНоменклатурыДополнительное", Ложь);
	Результат.Вставить("СвойствоНоменклатурыСсылка", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	Результат.Вставить("СвойствоНоменклатурыТипЗначения", Новый ОписаниеТипов(Неопределено));
	Результат.Вставить("СвойствоНоменклатурыЗаголовок", "");
	Результат.Вставить("Склады", Перечисления.СоставыСписковРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ЗапретНачисленияСверхПлана", Ложь);
	Результат.Вставить("ПоказательТоваров", Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.ПустаяСсылка());
	Результат.Вставить("ПоказательТоваровПредставление", "");
	Результат.Вставить("ПоказательТоваровКоличественный", Ложь);
	Результат.Вставить("БазаРасчета", Перечисления.БазыРасчетаРетроБонусов.ПустаяСсылка());
	Результат.Вставить("РасчетВБазовыхЦенах", Ложь);
	Результат.Вставить("УчитыватьНДС", Ложь);
	Результат.Вставить("ПорядокРаспределения", Перечисления.ПорядкиРаспределенияРетроБонусов.ПустаяСсылка());
	Результат.Вставить("ПропорциональноеРаспределение", Ложь);
	Результат.Вставить("РаспределениеПоFIFO", Ложь);
	Результат.Вставить("ДокументУсловийРетроБонусовПредставление", "");
	Результат.Вставить("Контрагент", Справочники.Контрагенты.ПустаяСсылка());
	Результат.Вставить("Партнер", Справочники.Партнеры.ПустаяСсылка());
	Результат.Вставить("ТипБонуса", Перечисления.ТипыРетроБонусовПоставщиков.ПустаяСсылка());
	Результат.Вставить("ВидШкалыРасчета", 0);
	Результат.Вставить("ПрогрессивнаяШкалаРасчета", Ложь);
	Результат.Вставить("ШкалаРасчетаПоДиапазонам", Ложь);
	
	Возврат Результат;
	
КонецФункции

// Возвращаемое значение:
//  Структура - Параметры варианта отчета:
// * КлючВарианта - Строка - Имя варианта в СКД
// * ЭтоРасшифровкаПоДокументам - Булево 
// * ЭтоРасшифровкаПоНоменклатуре - Булево
// * ЭтоРасшифровка - Булево
// * РежимРасшифровки - Булево - Значение флага ДополнительныеСвойства.РежимРасшифровки в настройках компоновщика
// * ПоляРасшифровки - Массив Из ПолеКомпоновкиДанных
// * ВариантМодифицированПользователем - Булево
// * ДетализацияДоНоменклатуры - Булево
// 
Функция НовыеПараметрыВарианта() Экспорт
	
	ПараметрыВарианта = Новый Структура;
	ПараметрыВарианта.Вставить("КлючВарианта", "");
	ПараметрыВарианта.Вставить("ЭтоРасшифровкаПоДокументам", Ложь);
	ПараметрыВарианта.Вставить("ЭтоРасшифровкаПоНоменклатуре", Ложь);
	ПараметрыВарианта.Вставить("ЭтоРасшифровка", Ложь);
	ПараметрыВарианта.Вставить("РежимРасшифровки", Ложь);
	ПараметрыВарианта.Вставить("ПоляРасшифровки", Новый Массив);
	ПараметрыВарианта.Вставить("ВариантМодифицированПользователем", Ложь);
	ПараметрыВарианта.Вставить("ДетализацияДоНоменклатуры", Ложь);
	
	Возврат ПараметрыВарианта;
	
КонецФункции

// Параметры:
//  ДатаНачала - Дата
//  ДатаОкончания - Дата
//  ПериодичностьУсловий - ПеречислениеСсылка.ПериодичностиРетроБонусов
// 
// Возвращаемое значение:
//  Число - Количество целых периодов расчета между ДатаНачала и ДатаОкончания
//
Функция КоличествоПериодовРасчета(Знач ДатаНачала, Знач ДатаОкончания, Знач ПериодичностьУсловий) Экспорт
	
	ДатаОкончания = КонецДня(ДатаОкончания) + 1;
	СекундВДне = 24 * 60 * 60;
	ПериодичностиРетроБонусов = Перечисления.ПериодичностиРетроБонусов;
	
	Если ПериодичностьУсловий = ПериодичностиРетроБонусов.День Тогда
		
		КоличествоПериодов = Цел((ДатаОкончания - ДатаНачала) / СекундВДне);
		
	ИначеЕсли ПериодичностьУсловий = ПериодичностиРетроБонусов.Неделя Тогда
		
		КоличествоПериодов = Цел((ДатаОкончания - ДатаНачала) / (7 * СекундВДне));
		
	ИначеЕсли ПериодичностьУсловий = ПериодичностиРетроБонусов.Месяц Тогда
		
		КоличествоПериодов = Месяц(ДатаОкончания) - Месяц(ДатаНачала) + 12 * (Год(ДатаОкончания) - Год(ДатаНачала));
		
	ИначеЕсли ПериодичностьУсловий = ПериодичностиРетроБонусов.Квартал Тогда
		
		КоличествоПериодов = Цел(Месяц(НачалоКвартала(ДатаОкончания)) / 3) - Цел(Месяц(НачалоКвартала(ДатаНачала)) / 3)
			+ 4 * (Год(ДатаОкончания) - Год(ДатаНачала));
		
	Иначе
		
		КоличествоПериодов = 1;
		
	КонецЕсли;
	
	Возврат КоличествоПериодов;
	
КонецФункции

// Параметры:
//  Вариант - ВариантНастроекКомпоновкиДанных -
// 
// Возвращаемое значение:
//  Число - Индекс уровня группировок с полем НомерДиапазона
//
Функция УровеньГруппировкиПоДиапазонам(Вариант) Экспорт
	
	УровниГруппировок = КомпоновкаДанныхКлиентСервер.ПолучитьГруппировки(Вариант.Настройки);
	Результат = Неопределено;
	ПолеДиапазон = Новый ПолеКомпоновкиДанных("НомерДиапазона");
	Для ИндексУровня = 0 По УровниГруппировок.Количество() Цикл
		
		Группировка = УровниГруппировок[ИндексУровня].Значение; // ГруппировкаКомпоновкиДанных
		ПоляГруппировки = Группировка.ПоляГруппировки;
		Группировки = ПоляГруппировки.Элементы;
		Для Каждого ПолеГруппировки Из Группировки Цикл
			
			Если ПолеГруппировки.Поле = ПолеДиапазон Тогда
				
				Результат = ИндексУровня;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если Результат <> Неопределено Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  УсловноеОформление - УсловноеОформлениеКомпоновкиДанных
//
Процедура НастроитьУсловноеОформлениеОтчета(УсловноеОформление) Экспорт
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных("Характеристика");
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Характеристика");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ХарактеристикиИспользуются");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстПредопределенногоЗначения);
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Все>';
																|en = '<All>'"));
	
КонецПроцедуры

// Параметры:
//  Настройки - НастройкиКомпоновкиДанных
//  ЗаголовкиКолонок - Структура
//
Процедура НастроитьЗаголовкиВыбранныхКолонок(Настройки, ЗаголовкиКолонок) Экспорт
	
	Для Каждого ЭлементЗаголовкаКолонки Из ЗаголовкиКолонок Цикл
		
		Заголовок = ЭлементЗаголовкаКолонки.Значение; // Строка
		ПолеКомпоновки = Новый ПолеКомпоновкиДанных(ЭлементЗаголовкаКолонки.Ключ);
		
		Для Каждого ВыбранноеПоле Из Настройки.Выбор.Элементы Цикл
			
			Если ТипЗнч(ВыбранноеПоле) = Тип("АвтоВыбранноеПолеКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ВыбранноеПоле.Поле = ПолеКомпоновки Тогда
				ВыбранноеПоле.Заголовок = Заголовок;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ПоляНабораДанных - ПоляНабораДанныхСхемыКомпоновкиДанных 
//  ЗаголовкиКолонок - Структура
//
Процедура НастроитьЗаголовкиКолонокНабораДанных(ПоляНабораДанных, ЗаголовкиКолонок) Экспорт
	
	Для Каждого ЭлементЗаголовкаКолонки Из ЗаголовкиКолонок Цикл
		
		Заголовок = ЭлементЗаголовкаКолонки.Значение; // Строка
		ПолеНабораДанных = ПоляНабораДанных.Найти(ЭлементЗаголовкаКолонки.Ключ);
		ПолеНабораДанных.Заголовок = Заголовок;
		
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  ПоляНабораДанных - ПоляНабораДанныхСхемыКомпоновкиДанных 
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//
Процедура НастроитьФорматЗначенияСвойства(ПоляНабораДанных, ПараметрыУсловийРетроБонусов) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ПараметрыУсловийРетроБонусов.СвойствоНоменклатуры) Тогда
		Возврат;
	КонецЕсли;
	
	ПолеНабораДанных = ПоляНабораДанных.Найти("ЗначениеСвойства");
	ПолеНабораДанных.ТипЗначения = ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения;
	
	ПараметрыФормата = Новый Массив; // Массив Из Строка
	
	Если ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения.СодержитТип(Тип("Число")) Тогда
		
		Разрядность = ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения.КвалификаторыЧисла.Разрядность;
		ПараметрФорматаДлина = "ЧЦ=" + Формат(Разрядность, "ЧДЦ=0; ЧГ=;");
		ПараметрыФормата.Добавить(ПараметрФорматаДлина);
		
		Точность = ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
		ПараметрФорматаТочность = "ЧДЦ=" + Формат(Точность, "ЧДЦ=0; ЧГ=;");
		ПараметрыФормата.Добавить(ПараметрФорматаТочность);
		
	КонецЕсли;
	
	Если ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения.СодержитТип(Тип("Дата")) Тогда
		
		СоставДаты = ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения.КвалификаторыДаты.ЧастиДаты;
		Если СоставДаты = ЧастиДаты.Дата Тогда
			
			ПараметрФорматаДаты = "ДЛФ=D";
			
		ИначеЕсли СоставДаты = ЧастиДаты.Время Тогда
			
			ПараметрФорматаДаты = "ДЛФ=T";
			
		Иначе
			
			ПараметрФорматаДаты = "ДЛФ=DT";
			
		КонецЕсли;
		
		ПараметрыФормата.Добавить(ПараметрФорматаДаты);
		
	КонецЕсли;
	
	Если ПараметрыФормата.Количество() > 0 Тогда
		
		ФорматнаяСтрока = СтрСоединить(ПараметрыФормата, "; ");
		ПолеНабораДанных.Оформление.УстановитьЗначениеПараметра("Формат", ФорматнаяСтрока);
		
	КонецЕсли;
	
КонецПроцедуры

#Область ТекстЗапроса

// Возвращаемое значение:
//  Строка - Выражение "ОБЪЕДИНИТЬ ВСЕ"
//
Функция РазделительПодзапросов() Экспорт
	
	РазделительПодзапросов = Символы.ПС + Символы.ПС + "ОБЪЕДИНИТЬ ВСЕ" + Символы.ПС + Символы.ПС;
	Возврат РазделительПодзапросов;
	
КонецФункции

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПоРегиструЗакупки - Булево -
// 
// Возвращаемое значение:
//  Строка - Выражение для поля ПериодРасчета
//
Функция ВыражениеПериодРасчета(ПараметрыУсловийРетроБонусов, ПоРегиструЗакупки = Ложь) Экспорт
	
	ПериодичностьУсловий = ПараметрыУсловийРетроБонусов.ПериодичностьУсловий;
	Периодичности = Перечисления.ПериодичностиРетроБонусов;
	ВыражениеПериод = "";
	
	Если ПоРегиструЗакупки Тогда
		ТаблицаИсточник = "Закупки";
	Иначе
		ТаблицаИсточник = "ВыручкаИСебестоимостьПродаж";
	КонецЕсли;
	
	Если ПериодичностьУсловий = Периодичности.День Тогда
		
		ВыражениеПериод = "НАЧАЛОПЕРИОДА(&ТаблицаИсточник.Период, ДЕНЬ)";
		
	ИначеЕсли ПериодичностьУсловий = Периодичности.Неделя Тогда
		
		ВыражениеПериод = "НАЧАЛОПЕРИОДА(&ТаблицаИсточник.Период, НЕДЕЛЯ)";
		
	ИначеЕсли ПериодичностьУсловий = Периодичности.Месяц Тогда
		
		ВыражениеПериод = "НАЧАЛОПЕРИОДА(&ТаблицаИсточник.Период, МЕСЯЦ)";
		
	ИначеЕсли ПериодичностьУсловий = Периодичности.Квартал Тогда
		
		ВыражениеПериод = "НАЧАЛОПЕРИОДА(&ТаблицаИсточник.Период, КВАРТАЛ)";
		
	ИначеЕсли ПериодичностьУсловий = Периодичности.ВесьПериод
		  ИЛИ ПериодичностьУсловий = Перечисления.ПериодичностиРетроБонусов.ПустаяСсылка() Тогда
		
		ВыражениеПериод = "&ДатаНачала";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Не удалось определить периодичность условий (""%1"") для документа ""%2""';
							|en = 'Cannot determine the condition frequency (""%1"") for the ""%2"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПараметрыУсловийРетроБонусов.ПериодичностьУсловийПредставление,
			ПараметрыУсловийРетроБонусов.ДокументУсловийРетроБонусовПредставление);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ВыражениеПериод = СтрЗаменить(ВыражениеПериод, "&ТаблицаИсточник", ТаблицаИсточник);
	
	Возврат ВыражениеПериод;
	
КонецФункции

// Возвращаемое значение:
//  Строка - Размер порции расчета нарастающего итога
//
Функция ПорцияНарастающегоИтога() Экспорт
	
	Возврат "100";
	
КонецФункции

// Возвращаемое значение:
//  Строка - Размер субпорции расчета нарастающего итога
//
Функция СубпорцияНарастающегоИтога() Экспорт
	
	Возврат "10";
	
КонецФункции

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаШкалаРасчета(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АВТОНОМЕРЗАПИСИ() КАК НомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ШкалаРасчета
	|ИЗ
	|	РегистрСведений.РетроБонусыШкалыРасчета КАК ТаблицаИсточник
	|ГДЕ
	|	ТаблицаИсточник.ДокументУсловий = &ИсходныйДокумент";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаШкалаРасчетаМинимальныйПлан(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ШкалаРасчета.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ШкалаРасчета.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ШкалаРасчета.БонусПроцент КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ШкалаРасчетаМинимальныйПлан
	|ИЗ
	|	(ВЫБРАТЬ
	|		МИНИМУМ(ШкалаРасчета.ДиапазонОт) КАК ДиапазонОт
	|	ИЗ
	|		ВТ_ШкалаРасчета КАК ШкалаРасчета) КАК МинимальныйДиапазон
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО (МинимальныйДиапазон.ДиапазонОт = ВТ_ШкалаРасчета.ДиапазонОт)";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаТовары(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.ДокументУсловий КАК УсловияРетроБонусов,
	|	ТаблицаИсточник.НачалоДействия КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ТаблицаИсточник.ОкончаниеДействия, ДЕНЬ) КАК ДатаОкончания,
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА
	|			ТаблицаИсточник.Номенклатура.ИспользованиеХарактеристик В (
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеДляВидаНоменклатуры),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ОбщиеСДругимВидомНоменклатуры),
	|				ЗНАЧЕНИЕ(Перечисление.ВариантыИспользованияХарактеристикНоменклатуры.ИндивидуальныеДляНоменклатуры))
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ХарактеристикиИспользуются,
	|	ТаблицаИсточник.Характеристика КАК Характеристика,
	|	&ВыражениеЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	&ВыражениеКоличествоПланПо КАК КоличествоПланПо,
	|	&ВыражениеБонусЗаЕдиницу КАК БонусЗаЕдиницу
	|ПОМЕСТИТЬ ВТ_Товары
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|ГДЕ
	|	&УсловиеПоДокументу
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика";
	
	Если ПараметрыУсловийРетроБонусов.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СегментыНоменклатуры
	   И НЕ ПараметрыУсловийРетроБонусов.ФиксацияВыполнена Тогда
		
		// Таблица ВТ_СоставСегментовТоваров будет передана в ПроцессорКомпоновкиДанных через МенеджерВременныхТаблиц
		ТаблицаИсточник = "ВТ_СоставСегментовТоваров";
		УсловиеПоДокументу = "ИСТИНА";
		ВыражениеБонусЗаЕдиницу = "0";
		ВыражениеКоличествоПланПо = "0";
		ВыражениеЗначениеСвойства = "НЕОПРЕДЕЛЕНО";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетПоСвойствам
	        И НЕ ПараметрыУсловийРетроБонусов.ФиксацияВыполнена Тогда
		
		// Таблица ВТ_ТоварыПоСвойству будет передана в ПроцессорКомпоновкиДанных через МенеджерВременныхТаблиц
		ТаблицаИсточник = "ВТ_ТоварыПоСвойству";
		УсловиеПоДокументу = "ИСТИНА";
		ВыражениеБонусЗаЕдиницу = "0";
		ВыражениеКоличествоПланПо = "ТаблицаИсточник.КоличествоПланГраницаДиапазона";
		
		ВыражениеЗначениеСвойства = ВыражениеТипизацииПоляСоставногоТипа(
			ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения, "ТаблицаИсточник.ЗначениеСвойства");
		
	Иначе
		
		Если ПараметрыУсловийРетроБонусов.РасчетБонусовКлиентов Тогда
			
			ТаблицаИсточник = "РегистрСведений.РетроБонусыКлиентовТовары";
			ВыражениеБонусЗаЕдиницу = "0";
			
		ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетБонусовПоставщиков Тогда
			
			ТаблицаИсточник = "РегистрСведений.РетроБонусыПоставщиковТовары";
			ВыражениеБонусЗаЕдиницу = "ТаблицаИсточник.БонусЗаЕдиницу";
			
		Иначе
			ВызватьИсключение НСтр("ru = 'Неизвестный тип ретро-бонуса';
									|en = 'Unknown rebate type'");
		КонецЕсли;
		
		ВыражениеКоличествоПланПо = "ТаблицаИсточник.КоличествоПланГраницаДиапазона";
		
		Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
			ВыражениеЗначениеСвойства = "ТаблицаИсточник.ЗначениеСвойства";
		Иначе
			ВыражениеЗначениеСвойства = "НЕОПРЕДЕЛЕНО";
		КонецЕсли;
		
		УсловиеПоДокументу =
		"ТаблицаИсточник.ДокументУсловий В
		|			(ВЫБРАТЬ
		|				ВТ_ПараметрыРБ.УсловиеРетроБонуса
		|			ИЗ
		|				ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоДокументу", УсловиеПоДокументу);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусЗаЕдиницу", ВыражениеБонусЗаЕдиницу);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПланПо", ВыражениеКоличествоПланПо);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеЗначениеСвойства", ВыражениеЗначениеСвойства);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаТоварыМинимальныйПланПоУпаковкам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_Товары.УсловияРетроБонусов КАК УсловияРетроБонусов,
	|	ВТ_Товары.ДатаНачала КАК ДатаНачала,
	|	ВТ_Товары.ДатаОкончания КАК ДатаОкончания,
	|	&ПоляТоваров,
	|	МИНИМУМ(ВТ_Товары.КоличествоПлан) КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_ТоварыМинимальныйПланПоУпаковкам
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Товары.УсловияРетроБонусов,
	|	ВТ_Товары.ДатаНачала,
	|	ВТ_Товары.ДатаОкончания,
	|	&ПоляГруппировкиПоПолямТоваров";
	
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_Товары");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаМинимальныеПараметрыПоПозиции(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ТоварыМинимальныйПланПоУпаковкам.УсловияРетроБонусов КАК УсловияРетроБонусов,
	|	ВТ_ТоварыМинимальныйПланПоУпаковкам.ДатаНачала КАК ДатаНачала,
	|	ВТ_ТоварыМинимальныйПланПоУпаковкам.ДатаОкончания КАК ДатаОкончания,
	|	&ПоляТоваров,
	|	ВТ_ТоварыМинимальныйПланПоУпаковкам.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_Товары.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_Товары.БонусПроцент КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ТоварыМинимальныеПараметрыПоПозиции
	|ИЗ
	|	ВТ_ТоварыМинимальныйПланПоУпаковкам КАК ВТ_ТоварыМинимальныйПланПоУпаковкам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО ВТ_ТоварыМинимальныйПланПоУпаковкам.УсловияРетроБонусов = ВТ_Товары.УсловияРетроБонусов
	|			И ВТ_ТоварыМинимальныйПланПоУпаковкам.ДатаНачала = ВТ_Товары.ДатаНачала
	|			И ВТ_ТоварыМинимальныйПланПоУпаковкам.ДатаОкончания = ВТ_Товары.ДатаОкончания
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ТоварыМинимальныйПланПоУпаковкам.КоличествоПлан = ВТ_Товары.КоличествоПлан";
	
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ТоварыМинимальныйПланПоУпаковкам", "ВТ_Товары");
	
	Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ РАЗЛИЧНЫЕ"); // @query-part-1, @query-part-2
		
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДиапазоны(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000000
	|	АВТОНОМЕРЗАПИСИ() КАК НомерДиапазона,
	|	ВТ_Товары.КоличествоПлан КАК ДиапазонОт,
	|	ВТ_Товары.КоличествоПланПо КАК ДиапазонПо
	|ПОМЕСТИТЬ ВТ_Диапазоны
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Товары.КоличествоПлан,
	|	ВТ_Товары.КоличествоПланПо
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДиапазонОт";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаТоварыДиапазоны(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_Товары.ДатаНачала КАК ДатаНачала,
	|	ВТ_Товары.ДатаНачала КАК ДатаОкончания,
	|	&ПоляТоваров,
	|	ВТ_Товары.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_Товары.БонусЗаЕдиницу КАК БонусЗаЕдиницу,
	|	ВТ_Товары.БонусПроцент КАК БонусПроцент,
	|	ВТ_Товары.КоличествоПлан КАК ДиапазонОт,
	|	ВТ_Товары.КоличествоПланПо КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_Диапазоны.НомерДиапазона, -1) КАК НомерДиапазона
	|ПОМЕСТИТЬ ВТ_ТоварыДиапазоны
	|ИЗ
	|	ВТ_Товары КАК ВТ_Товары
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Диапазоны КАК ВТ_Диапазоны
	|	ПО ВТ_Товары.КоличествоПлан = ВТ_Диапазоны.ДиапазонОт
	|		И ВТ_Товары.КоличествоПланПо = ВТ_Диапазоны.ДиапазонПо";
	
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_Товары", "ВТ_Диапазоны");
	
	Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ РАЗЛИЧНЫЕ"); // @query-part-1, @query-part-2
		
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаУчастникиПредварительно(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	СоставыУчастников = Перечисления.СоставыУчастниковРетроБонусов;
	СоставыСписков = Перечисления.СоставыСписковРетроБонусов;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИННКлиентов.ДокументУсловий КАК УсловияРетроБонусов,
	|	Контрагенты.Ссылка КАК Контрагент,
	|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер,
	|	ИННКлиентов.НачалоДействия КАК ДатаНачала,
	|	ИННКлиентов.ОкончаниеДействия КАК ДатаОкончания,
	|	&ВыражениеСуммаПланИНН КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_УчастникиПредварительно
	|ИЗ
	|	&ТаблицаИсточникИНН КАК ИННКлиентов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
	|		ПО ИННКлиентов.ИНН = Контрагенты.ИНН
	|ГДЕ
	|	ИННКлиентов.ДокументУсловий В
	|			(ВЫБРАТЬ
	|				ВТ_ПараметрыРБ.УсловиеРетроБонуса
	|			ИЗ
	|				ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ
	|			ГДЕ
	|				ВТ_ПараметрыРБ.ОтборПоИНН)
	|	И &УсловиеОтбораПоКонтрагентуНаСправочник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КонтрагентыКлиентов.ДокументУсловий,
	|	КонтрагентыКлиентов.Контрагент,
	|	&ВыражениеКонтрагентыПартнер,
	|	КонтрагентыКлиентов.НачалоДействия,
	|	КонтрагентыКлиентов.ОкончаниеДействия,
	|	&ВыражениеСуммаПланКонтрагенты КАК СуммаПлан
	|ИЗ
	|	&ТаблицаИсточникКонтрагенты КАК КонтрагентыКлиентов
	|ГДЕ
	|	КонтрагентыКлиентов.ДокументУсловий В
	|			(ВЫБРАТЬ
	|				ВТ_ПараметрыРБ.УсловиеРетроБонуса
	|			ИЗ
	|				ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ
	|			ГДЕ
	|				ВТ_ПараметрыРБ.ОтборПоКонтрагентамПартнерам)";
	
	Если ПараметрыУсловийРетроБонусов.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.СегментыПартнеров
	   И НЕ ПараметрыУсловийРетроБонусов.ФиксацияВыполнена Тогда
		
		// Таблица ВТ_СоставСегментовКлиентов будет передана в ПроцессорКомпоновкиДанных через МенеджерВременныхТаблиц
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТ_СоставСегментовКлиентов.ДокументУсловий КАК УсловияРетроБонусов,
		|	ВТ_СоставСегментовКлиентов.Контрагент КАК Контрагент,
		|	ВТ_СоставСегментовКлиентов.Партнер КАК Партнер,
		|	ВТ_СоставСегментовКлиентов.НачалоДействия КАК ДатаНачала,
		|	ВТ_СоставСегментовКлиентов.ОкончаниеДействия КАК ДатаОкончания,
		|	ВТ_СоставСегментовКлиентов.СуммаПлан КАК СуммаПлан
		|ПОМЕСТИТЬ ВТ_УчастникиПредварительно
		|ИЗ
		|	ВТ_СоставСегментовКлиентов КАК ВТ_СоставСегментовКлиентов
		|ГДЕ
		|	&УсловиеОтбораПоПартнеруНаСоставСегментов";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.СоставУчастников = СоставыУчастников.ИНН
	   И ПараметрыУсловийРетроБонусов.Участники = СоставыСписков.КромеВыбранных Тогда
		
		ТекстПодзапросаПустыеИНН =
		"ВЫБРАТЬ
		|	&ИсходныйДокумент КАК УсловияРетроБонусов,
		|	Контрагенты.Ссылка КАК Контрагент,
		|	ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер,
		|	&ДатаНачала КАК ДатаНачала,
		|	&ДатаОкончания КАК ДатаОкончания,
		|	0 КАК СуммаПлан
		|ИЗ
		|	Справочник.Контрагенты КАК Контрагенты
		|ГДЕ
		|	СОКРЛ(Контрагенты.ИНН) = """"";
		
		ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении() + ТекстПодзапросаПустыеИНН;
		
	КонецЕсли;
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагентуНаСправочник =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ Контрагенты.Ссылка = &Контрагент
		|КОНЕЦ";
		
		УсловиеОтбораПоПартнеруНаСоставСегментов =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ВТ_СоставСегментовКлиентов.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагентуНаСправочник = "ИСТИНА";
		УсловиеОтбораПоПартнеруНаСоставСегментов = "ИСТИНА";
		
	КонецЕсли;
	
	Если ПараметрыУсловийРетроБонусов.РасчетБонусовКлиентов Тогда
		
		ТаблицаИсточникИНН = "РегистрСведений.РетроБонусыКлиентовИНН";
		ТаблицаИсточникКонтрагенты = "РегистрСведений.РетроБонусыКлиентовКонтрагенты";
		ВыражениеСуммаПланИНН = "ИННКлиентов.СуммаПлан";
		ВыражениеСуммаПланКонтрагенты = "КонтрагентыКлиентов.СуммаПлан";
		ВыражениеКонтрагентыПартнер = "КонтрагентыКлиентов.Партнер";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетБонусовПоставщиков Тогда
		
		ТаблицаИсточникИНН = "РегистрСведений.РетроБонусыПоставщиковИНН";
		ТаблицаИсточникКонтрагенты = "РегистрСведений.РетроБонусыПоставщиковКонтрагенты";
		ВыражениеСуммаПланИНН = "0";
		ВыражениеСуммаПланКонтрагенты = "0";
		ВыражениеКонтрагентыПартнер = "ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)";
		
	Иначе
		ВызватьИсключение НСтр("ru = 'Неизвестный тип ретро-бонуса';
								|en = 'Unknown rebate type'");
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПланИНН", ВыражениеСуммаПланИНН);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПланКонтрагенты", ВыражениеСуммаПланКонтрагенты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКонтрагентыПартнер", ВыражениеКонтрагентыПартнер);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточникИНН", ТаблицаИсточникИНН);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточникКонтрагенты", ТаблицаИсточникКонтрагенты);
	
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, "&УсловиеОтбораПоКонтрагентуНаСправочник", УсловиеОтбораПоКонтрагентуНаСправочник);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, "&УсловиеОтбораПоПартнеруНаСоставСегментов", УсловиеОтбораПоПартнеруНаСоставСегментов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаКонтрагенты(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_УчастникиПредварительно.УсловияРетроБонусов КАК УсловияРетроБонусов,
	|	ВТ_УчастникиПредварительно.Контрагент КАК Контрагент,
	|	ВТ_УчастникиПредварительно.Партнер КАК Партнер,
	|	ВТ_УчастникиПредварительно.ДатаНачала КАК ДатаНачала,
	|	ВТ_УчастникиПредварительно.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_УчастникиПредварительно.СуммаПлан КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_Контрагенты
	|ИЗ
	|	ВТ_УчастникиПредварительно КАК ВТ_УчастникиПредварительно
	|ГДЕ
	|	ВТ_УчастникиПредварительно.Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И &УсловиеОтбораПоКонтрагенту";
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагенту =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ВТ_УчастникиПредварительно.Контрагент = &Контрагент
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагенту = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоКонтрагенту", УсловиеОтбораПоКонтрагенту);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПартнеры(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_УчастникиПредварительно.УсловияРетроБонусов КАК УсловияРетроБонусов,
	|	ВТ_УчастникиПредварительно.Контрагент КАК Контрагент,
	|	ВТ_УчастникиПредварительно.Партнер КАК Партнер,
	|	ВТ_УчастникиПредварительно.ДатаНачала КАК ДатаНачала,
	|	ВТ_УчастникиПредварительно.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_УчастникиПредварительно.СуммаПлан КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_Партнеры
	|ИЗ
	|	ВТ_УчастникиПредварительно КАК ВТ_УчастникиПредварительно
	|ГДЕ
	|	ВТ_УчастникиПредварительно.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И ВТ_УчастникиПредварительно.Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И &УсловиеОтбораПоПартнеру";
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоПартнеру =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ВТ_УчастникиПредварительно.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоПартнеру = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоПартнеру", УсловиеОтбораПоПартнеру);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПартнерыКонтрагенты(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_УчастникиПредварительно.УсловияРетроБонусов КАК УсловияРетроБонусов,
	|	ВТ_УчастникиПредварительно.Контрагент КАК Контрагент,
	|	ВТ_УчастникиПредварительно.Партнер КАК Партнер,
	|	ВТ_УчастникиПредварительно.ДатаНачала КАК ДатаНачала,
	|	ВТ_УчастникиПредварительно.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_УчастникиПредварительно.СуммаПлан КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_ПартнерыКонтрагенты
	|ИЗ
	|	ВТ_УчастникиПредварительно КАК ВТ_УчастникиПредварительно
	|ГДЕ
	|	ВТ_УчастникиПредварительно.Партнер <> ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
	|	И ВТ_УчастникиПредварительно.Контрагент <> ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
	|	И &УсловиеОтбораПоКонтрагенту
	|	И &УсловиеОтбораПоПартнеру";
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагенту =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ВТ_УчастникиПредварительно.Контрагент = &Контрагент
		|КОНЕЦ";
		УсловиеОтбораПоПартнеру =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ ВТ_УчастникиПредварительно.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагенту = "ИСТИНА";
		УсловиеОтбораПоПартнеру = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоКонтрагенту", УсловиеОтбораПоКонтрагенту);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоПартнеру", УсловиеОтбораПоПартнеру);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаАналитикаПоПартнерам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	ТекстПодзапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаНачала,
	|	ДАТАВРЕМЯ(2999, 12, 31) КАК ДатаОкончания,
	|	0 КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_АналитикаПоПартнерам
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВТ_ПараметрыРБ.ОтборУчастниковБезОграничений
	|	И АналитикаУчетаПоПартнерам.Организация В(&Организация)
	|	И &УсловиеОтбораПоКонтрагенту
	|	И &УсловиеОтбораПоПартнеру";
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ВТ_Контрагенты.ДатаНачала КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ВТ_Контрагенты.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
	|	0 КАК СуммаПлан
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Контрагенты КАК ВТ_Контрагенты
	|		ПО АналитикаУчетаПоПартнерам.Контрагент = ВТ_Контрагенты.Контрагент
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Организация В (&Организация)
	|	И НЕ (АналитикаУчетаПоПартнерам.Контрагент, АналитикаУчетаПоПартнерам.Партнер) В
	|		(ВЫБРАТЬ
	|			ВТ_ПартнерыКонтрагенты.Контрагент КАК Контрагент,
	|			ВТ_ПартнерыКонтрагенты.Партнер КАК Партнер
	|		ИЗ
	|			ВТ_ПартнерыКонтрагенты КАК ВТ_ПартнерыКонтрагенты)";
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ВТ_Партнеры.ДатаНачала КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ВТ_Партнеры.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
	|	0 КАК СуммаПлан
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Партнеры КАК ВТ_Партнеры
	|		ПО АналитикаУчетаПоПартнерам.Партнер = ВТ_Партнеры.Партнер
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Организация В (&Организация)
	|	И НЕ (АналитикаУчетаПоПартнерам.Контрагент, АналитикаУчетаПоПартнерам.Партнер) В
	|		(ВЫБРАТЬ
	|			ВТ_ПартнерыКонтрагенты.Контрагент КАК Контрагент,
	|			ВТ_ПартнерыКонтрагенты.Партнер КАК Партнер
	|		ИЗ
	|			ВТ_ПартнерыКонтрагенты КАК ВТ_ПартнерыКонтрагенты)";
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
	|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ВТ_ПартнерыКонтрагенты.ДатаНачала КАК ДатаНачала,
	|	КОНЕЦПЕРИОДА(ВТ_ПартнерыКонтрагенты.ДатаОкончания, ДЕНЬ) КАК ДатаОкончания,
	|	0 КАК СуммаПлан
	|ИЗ
	|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПартнерыКонтрагенты КАК ВТ_ПартнерыКонтрагенты
	|		ПО АналитикаУчетаПоПартнерам.Партнер = ВТ_ПартнерыКонтрагенты.Партнер
	|		И АналитикаУчетаПоПартнерам.Контрагент = ВТ_ПартнерыКонтрагенты.Контрагент
	|ГДЕ
	|	АналитикаУчетаПоПартнерам.Организация В (&Организация)";
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	Если ПараметрыУсловийРетроБонусов.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Договоры Тогда
		
		ТекстПодзапроса =
		"ВЫБРАТЬ
		|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
		|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
		|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|	РетроБонусыКлиентовДоговорыСоглашения.НачалоДействия КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(РетроБонусыКлиентовДоговорыСоглашения.ОкончаниеДействия, ДЕНЬ) КАК ДатаОкончания,
		|	РетроБонусыКлиентовДоговорыСоглашения.СуммаПлан КАК СуммаПлан
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РетроБонусыКлиентовДоговорыСоглашения КАК РетроБонусыКлиентовДоговорыСоглашения
		|		ПО АналитикаУчетаПоПартнерам.Договор = РетроБонусыКлиентовДоговорыСоглашения.Договор
		|ГДЕ
		|	АналитикаУчетаПоПартнерам.Организация В (&Организация)
		|	И РетроБонусыКлиентовДоговорыСоглашения.ДокументУсловий = &ИсходныйДокумент
		|	И РетроБонусыКлиентовДоговорыСоглашения.Договор <> ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка)
		|	И &УсловиеОтбораПоКонтрагенту
		|	И &УсловиеОтбораПоПартнеру";
		ТекстыПодзапросов.Добавить(ТекстПодзапроса);
		
	КонецЕсли;
	
	Если ПараметрыУсловийРетроБонусов.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		
		ТекстПодзапроса =
		"ВЫБРАТЬ
		|	АналитикаУчетаПоПартнерам.КлючАналитики КАК КлючАналитики,
		|	АналитикаУчетаПоПартнерам.Организация КАК Организация,
		|	АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
		|	АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
		|	ВТ_Соглашения.НачалоДействия КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ВТ_Соглашения.ОкончаниеДействия, ДЕНЬ) КАК ДатаОкончания,
		|	ВТ_Соглашения.СуммаПлан КАК СуммаПлан
		|ИЗ
		|	РегистрСведений.АналитикаУчетаПоПартнерам КАК АналитикаУчетаПоПартнерам
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Соглашения КАК ВТ_Соглашения
		|		ПО АналитикаУчетаПоПартнерам.Партнер = ВТ_Соглашения.Партнер
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АналитикаПоПартнерамПоТиповымСоглашениям КАК ВТ_АналитикаПоПартнерамПоТиповымСоглашениям
		|		ПО АналитикаУчетаПоПартнерам.КлючАналитики = ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.КлючАналитики
		|ГДЕ
		|	ВТ_Соглашения.ТиповоеСоглашение = ЛОЖЬ
		|	И ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.КлючАналитики ЕСТЬ NULL
		|	И АналитикаУчетаПоПартнерам.Организация В (&Организация)
		|	И &УсловиеОтбораПоКонтрагенту
		|	И &УсловиеОтбораПоПартнеру";
		ТекстыПодзапросов.Добавить(ТекстПодзапроса);
		
		ТекстПодзапроса =
		"ВЫБРАТЬ
		|	ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.КлючАналитики КАК КлючАналитики,
		|	ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.Организация КАК Организация,
		|	ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.Контрагент КАК Контрагент,
		|	ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.Партнер КАК Партнер,
		|	ВТ_Соглашения.НачалоДействия КАК ДатаНачала,
		|	КОНЕЦПЕРИОДА(ВТ_Соглашения.ОкончаниеДействия, ДЕНЬ) КАК ДатаОкончания,
		|	ВТ_Соглашения.СуммаПлан КАК СуммаПлан
		|ИЗ
		|	ВТ_АналитикаПоПартнерамПоТиповымСоглашениям КАК ВТ_АналитикаПоПартнерамПоТиповымСоглашениям
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Соглашения КАК ВТ_Соглашения
		|		ПО ВТ_АналитикаПоПартнерамПоТиповымСоглашениям.Соглашение = ВТ_Соглашения.Соглашение";
		ТекстыПодзапросов.Добавить(ТекстПодзапроса);
		
	КонецЕсли;
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагенту =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
		|КОНЕЦ";
		УсловиеОтбораПоПартнеру =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ АналитикаУчетаПоПартнерам.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагенту = "ИСТИНА";
		УсловиеОтбораПоПартнеру = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоКонтрагенту", УсловиеОтбораПоКонтрагенту);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоПартнеру", УсловиеОтбораПоПартнеру);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаАналитикаУчетаНоменклатуры(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.Все Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	АналитикаУчетаНоменклатуры.КлючАналитики КАК КлючАналитики
	|ПОМЕСТИТЬ ВТ_АналитикаУчетаНоменклатуры
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ВТ_Товары.Номенклатура
	|			И АналитикаУчетаНоменклатуры.Характеристика = ВТ_Товары.Характеристика
	|ГДЕ
	|	ВТ_Товары.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АналитикаУчетаНоменклатуры.КлючАналитики КАК КлючАналитики
	|ИЗ
	|	РегистрСведений.АналитикаУчетаНоменклатуры КАК АналитикаУчетаНоменклатуры
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО АналитикаУчетаНоменклатуры.Номенклатура = ВТ_Товары.Номенклатура
	|ГДЕ
	|	ВТ_Товары.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПродажи(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ШаблонЗапроса =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыручкаИСебестоимостьПродаж.Период КАК Период,
	|	&ВыражениеПериод КАК ПериодРасчета,
	|	&ВыражениеДокументРегистратор КАК ДокументРегистратор,
	|	РС_АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РС_АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РС_АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ВыручкаИСебестоимостьПродаж.Договор КАК Договор,
	|	ВыручкаИСебестоимостьПродаж.Соглашение КАК Соглашение,
	|	ВыручкаИСебестоимостьПродаж.Склад КАК Склад,
	|	РС_АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	РС_АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	СУММА(ВыручкаИСебестоимостьПродаж.Количество) КАК Количество,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручки) КАК СуммаОборотУпрСНДС,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиСНДСРегл) КАК СуммаОборотРеглСНДС,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВВалютеВзаиморасчетов) КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиБезНДС) КАК СуммаОборотУпрБезНДС,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл) КАК СуммаОборотРеглБезНДС,
	|	СУММА(ВыручкаИСебестоимостьПродаж.СуммаБезНДСВВалютеВзаиморасчетов) КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА ВТ_ПараметрыРБ.УчитыватьНДС
	|			ТОГДА ВыручкаИСебестоимостьПродаж.СуммаВыручки
	|		ИНАЧЕ ВыручкаИСебестоимостьПродаж.СуммаВыручкиБезНДС
	|	КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА ВТ_ПараметрыРБ.УчитыватьНДС
	|			ТОГДА ВыручкаИСебестоимостьПродаж.СуммаВыручкиСНДСРегл
	|		ИНАЧЕ ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл
	|	КОНЕЦ) КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ВТ_ПараметрыРБ.УчитыватьНДС
	|			ТОГДА ВыручкаИСебестоимостьПродаж.СуммаВВалютеВзаиморасчетов
	|		ИНАЧЕ ВыручкаИСебестоимостьПродаж.СуммаБезНДСВВалютеВзаиморасчетов
	|	КОНЕЦ) КАК СуммаВВалютеВзаиморасчетов,
	|	ВТ_ПараметрыРБ.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПараметрыРБ.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПараметрыРБ.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПараметрыРБ.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПараметрыРБ.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПараметрыРБ.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПараметрыРБ.УчитыватьНДС КАК УчитыватьНДС,
	|	ВТ_ПараметрыРБ.Валюта КАК ВалютаУсловий,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВТ_ПараметрыРБ.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПараметрыРБ.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПараметрыРБ.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПараметрыРБ.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПараметрыРБ.ОтборТоваровВыбранные КАК ОтборТоваровВыбранные,
	|	ВТ_ПараметрыРБ.ОтборТоваровКромеВыбранных КАК ОтборТоваровКромеВыбранных,
	|	ВТ_ПараметрыРБ.ОтборТоваровБезОграничений КАК ОтборТоваровБезОграничений,
	|	ВТ_ПараметрыРБ.ОтборУчастниковВыбранные КАК ОтборУчастниковВыбранные,
	|	ВТ_ПараметрыРБ.ОтборУчастниковКромеВыбранных КАК ОтборУчастниковКромеВыбранных
	|ПОМЕСТИТЬ ВТ_ПродажиЗакупки
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК РС_АналитикаУчетаНоменклатуры
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры = РС_АналитикаУчетаНоменклатуры.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АналитикаПоПартнерам КАК ВТ_АналитикаПоПартнерам
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = ВТ_АналитикаПоПартнерам.КлючАналитики
	|			И (ВыручкаИСебестоимостьПродаж.Период МЕЖДУ ВТ_АналитикаПоПартнерам.ДатаНачала И ВТ_АналитикаПоПартнерам.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РС_АналитикаУчетаПоПартнерам
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ
	|			ПО ИСТИНА
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = РС_АналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	&УсловиеОтбораПоТипуДокумента
	|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВЫБОР
	|		КОГДА ВТ_ПараметрыРБ.ОтборУчастниковВыбранные
	|			ТОГДА НЕ ВТ_АналитикаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|		КОГДА ВТ_ПараметрыРБ.ОтборУчастниковКромеВыбранных
	|			ТОГДА ВТ_АналитикаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ
	|	И &УсловиеОтбораТоваров
	|	И РС_АналитикаУчетаПоПартнерам.Организация В (&Организация)
	|	И &УсловиеОтбораПоКонтрагенту
	|	И &УсловиеОтбораПоПартнеру
	|	И НЕ ВТ_ПараметрыРБ.УсловиеРетроБонуса ЕСТЬ NULL
	|	И ВыручкаИСебестоимостьПродаж.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация НЕ В (&ИсключаемыеХозяйственныеОперации)
	|	И ВыручкаИСебестоимостьПродаж.НастройкаХозяйственнойОперации НЕ В (&ИсключаемыеНастройкиХозяйственныхОпераций)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыручкаИСебестоимостьПродаж.Период,
	|	&ВыражениеПериод,
	|	&ВыражениеДокументРегистратор,
	|	РС_АналитикаУчетаПоПартнерам.Организация,
	|	РС_АналитикаУчетаПоПартнерам.Контрагент,
	|	РС_АналитикаУчетаПоПартнерам.Партнер,
	|	ВыручкаИСебестоимостьПродаж.Договор,
	|	ВыручкаИСебестоимостьПродаж.Соглашение,
	|	ВыручкаИСебестоимостьПродаж.Склад,
	|	РС_АналитикаУчетаНоменклатуры.Номенклатура,
	|	РС_АналитикаУчетаНоменклатуры.Характеристика,
	|	ВТ_ПараметрыРБ.УсловиеРетроБонуса,
	|	ВТ_ПараметрыРБ.БонусПроцент,
	|	ВТ_ПараметрыРБ.ПоказательТоваров,
	|	ВТ_ПараметрыРБ.ДетализацияРасчетаУчастников,
	|	ВТ_ПараметрыРБ.ПериодичностьУсловий,
	|	ВТ_ПараметрыРБ.ПериодичностьНачислений,
	|	ВТ_ПараметрыРБ.УчитыватьНДС,
	|	ВТ_ПараметрыРБ.Валюта,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов,
	|	ВТ_ПараметрыРБ.ЗапретНачисленияСверхПлана,
	|	ВТ_ПараметрыРБ.РаспределениеПоФИФО,
	|	ВТ_ПараметрыРБ.СуммаПлан,
	|	ВТ_ПараметрыРБ.БазаРасчета,
	|	ВТ_ПараметрыРБ.ОтборТоваровВыбранные,
	|	ВТ_ПараметрыРБ.ОтборТоваровКромеВыбранных,
	|	ВТ_ПараметрыРБ.ОтборТоваровБезОграничений,
	|	ВТ_ПараметрыРБ.ОтборУчастниковВыбранные,
	|	ВТ_ПараметрыРБ.ОтборУчастниковКромеВыбранных";
	
	ВыраженияДокументРегистратор = Новый Массив; // Массив Из Строка
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.РеализацияТоваровУслуг)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.КорректировкаРеализации)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.АктВыполненныхРабот)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.ВозвратТоваровОтКлиента)");
	
	ОтборыПоТипамДокументов = Новый Массив; // Массив Из Строка
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.КорректировкаРеализации");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.АктВыполненныхРабот");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента");
	
	Если ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		УсловиеОтбораТоваров =
		"ВТ_ПараметрыРБ.ОтборТоваровВыбранные
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры В
		|			(ВЫБРАТЬ
		|				ВТ_АналитикаУчетаНоменклатуры.КлючАналитики
		|			ИЗ
		|				ВТ_АналитикаУчетаНоменклатуры КАК ВТ_АналитикаУчетаНоменклатуры)";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		УсловиеОтбораТоваров =
		"ВТ_ПараметрыРБ.ОтборТоваровКромеВыбранных
		|	И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры В
		|			(ВЫБРАТЬ
		|				ВТ_АналитикаУчетаНоменклатуры.КлючАналитики
		|			ИЗ
		|				ВТ_АналитикаУчетаНоменклатуры КАК ВТ_АналитикаУчетаНоменклатуры)";
		
	Иначе
		
		УсловиеОтбораТоваров = "ВТ_ПараметрыРБ.ОтборТоваровБезОграничений";
		
	КонецЕсли;
	
	ВыражениеПериод = ВыражениеПериодРасчета(ПараметрыУсловийРетроБонусов);
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагенту =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ РС_АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
		|КОНЕЦ";
		УсловиеОтбораПоПартнеру =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ РС_АналитикаУчетаПоПартнерам.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагенту = "ИСТИНА";
		УсловиеОтбораПоПартнеру = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	НомерПодзапроса = 1;
	Для ИндексТипаДокумента = 0 По ОтборыПоТипамДокументов.ВГраница() Цикл
		
		ВыражениеДокументРегистратор = ВыраженияДокументРегистратор[ИндексТипаДокумента];
		УсловиеОтбораПоТипуДокумента = ОтборыПоТипамДокументов[ИндексТипаДокумента];
		
		ТекстЗапроса = ШаблонЗапроса;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПериод", ВыражениеПериод);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДокументРегистратор", ВыражениеДокументРегистратор);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоТипуДокумента", УсловиеОтбораПоТипуДокумента);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораТоваров", УсловиеОтбораТоваров);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоКонтрагенту", УсловиеОтбораПоКонтрагенту);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоПартнеру", УсловиеОтбораПоПартнеру);
		
		Если НомерПодзапроса > 1 Тогда
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "РАЗРЕШЕННЫЕ", "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТ_ПродажиЗакупки", "");
			
		КонецЕсли;
		
		ТекстыПодзапросов.Добавить(ТекстЗапроса);
		НомерПодзапроса = НомерПодзапроса + 1;
		
	КонецЦикла;
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПродажиБезОтбораПоКонтрагентам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ШаблонЗапроса =
	"ВЫБРАТЬ
	|	ВыручкаИСебестоимостьПродаж.Период КАК Период,
	|	ВыручкаИСебестоимостьПродаж.НомерСтроки КАК НомерСтроки,
	|	&ВыражениеПериод КАК ПериодРасчета,
	|	&ВыражениеДокументРегистратор КАК ДокументРегистратор,
	|	РС_АналитикаУчетаПоПартнерам.Организация КАК Организация,
	|	РС_АналитикаУчетаПоПартнерам.Контрагент КАК Контрагент,
	|	РС_АналитикаУчетаПоПартнерам.Партнер КАК Партнер,
	|	ВыручкаИСебестоимостьПродаж.Договор КАК Договор,
	|	ВыручкаИСебестоимостьПродаж.Соглашение КАК Соглашение,
	|	ВыручкаИСебестоимостьПродаж.Склад КАК Склад,
	|	РС_АналитикаУчетаНоменклатуры.Номенклатура КАК Номенклатура,
	|	РС_АналитикаУчетаНоменклатуры.Характеристика КАК Характеристика,
	|	ВыручкаИСебестоимостьПродаж.Количество КАК Количество,
	|	ВыручкаИСебестоимостьПродаж.СуммаВыручки КАК СуммаОборотУпрСНДС,
	|	ВыручкаИСебестоимостьПродаж.СуммаВыручкиСНДСРегл КАК СуммаОборотРеглСНДС,
	|	ВыручкаИСебестоимостьПродаж.СуммаВВалютеВзаиморасчетов КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	ВыручкаИСебестоимостьПродаж.СуммаВыручкиБезНДС КАК СуммаОборотУпрБезНДС,
	|	ВыручкаИСебестоимостьПродаж.СуммаВыручкиРегл КАК СуммаОборотРеглБезНДС,
	|	ВыручкаИСебестоимостьПродаж.СуммаБезНДСВВалютеВзаиморасчетов КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	ВТ_ПараметрыРБ.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПараметрыРБ.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПараметрыРБ.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПараметрыРБ.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПараметрыРБ.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПараметрыРБ.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПараметрыРБ.УчитыватьНДС КАК УчитыватьНДС,
	|	ВТ_ПараметрыРБ.Валюта КАК ВалютаУсловий,
	|	ВТ_ПараметрыРБ.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПараметрыРБ.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПараметрыРБ.СуммаПлан КАК СуммаПланОбщая,
	|	ВТ_АналитикаПоПартнерам.СуммаПлан КАК СуммаПланПоСоглашению,
	|	ВыручкаИСебестоимостьПродаж.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ВТ_ПараметрыРБ.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПараметрыРБ.ОтборТоваровВыбранные КАК ОтборТоваровВыбранные,
	|	ВТ_ПараметрыРБ.ОтборТоваровКромеВыбранных КАК ОтборТоваровКромеВыбранных,
	|	ВТ_ПараметрыРБ.ОтборТоваровБезОграничений КАК ОтборТоваровБезОграничений,
	|	ВТ_ПараметрыРБ.ОтборУчастниковВыбранные КАК ОтборУчастниковВыбранные,
	|	ВТ_ПараметрыРБ.ОтборУчастниковКромеВыбранных КАК ОтборУчастниковКромеВыбранных
	|ПОМЕСТИТЬ ВТ_ПродажиЗакупкиБезОтбораПоКонтрагентам
	|ИЗ
	|	РегистрНакопления.ВыручкаИСебестоимостьПродаж КАК ВыручкаИСебестоимостьПродаж
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаНоменклатуры КАК РС_АналитикаУчетаНоменклатуры
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры = РС_АналитикаУчетаНоменклатуры.КлючАналитики
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_АналитикаПоПартнерам КАК ВТ_АналитикаПоПартнерам
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = ВТ_АналитикаПоПартнерам.КлючАналитики
	|			И (ВыручкаИСебестоимостьПродаж.Период МЕЖДУ ВТ_АналитикаПоПартнерам.ДатаНачала И ВТ_АналитикаПоПартнерам.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.АналитикаУчетаПоПартнерам КАК РС_АналитикаУчетаПоПартнерам
	|			ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПараметрыРБ КАК ВТ_ПараметрыРБ
	|			ПО ИСТИНА
	|		ПО ВыручкаИСебестоимостьПродаж.АналитикаУчетаПоПартнерам = РС_АналитикаУчетаПоПартнерам.КлючАналитики
	|ГДЕ
	|	&УсловиеОтбораПоТипуДокумента
	|	И ВыручкаИСебестоимостьПродаж.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И ВЫБОР
	|			КОГДА ВТ_ПараметрыРБ.ОтборУчастниковВыбранные
	|				ТОГДА НЕ ВТ_АналитикаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|			КОГДА ВТ_ПараметрыРБ.ОтборУчастниковКромеВыбранных
	|				ТОГДА ВТ_АналитикаПоПартнерам.КлючАналитики ЕСТЬ NULL
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И &УсловиеОтбораТоваров
	|	И РС_АналитикаУчетаПоПартнерам.Организация В(&Организация)
	|	И &УсловиеОтбораПоКонтрагенту
	|	И &УсловиеОтбораПоПартнеру
	|	И НЕ ВТ_ПараметрыРБ.УсловиеРетроБонуса ЕСТЬ NULL
	|	И ВыручкаИСебестоимостьПродаж.РазделУчета <> ЗНАЧЕНИЕ(Перечисление.РазделыУчетаСебестоимостиТоваров.ТоварыПереданныеНаКомиссию)
	|	И ВыручкаИСебестоимостьПродаж.НастройкаХозяйственнойОперации НЕ В (&ИсключаемыеНастройкиХозяйственныхОпераций)
	|	И ВыручкаИСебестоимостьПродаж.ХозяйственнаяОперация НЕ В (&ИсключаемыеХозяйственныеОперации)";
	
	ВыраженияДокументРегистратор = Новый Массив; // Массив Из Строка
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.РеализацияТоваровУслуг)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.КорректировкаРеализации)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.АктВыполненныхРабот)");
	ВыраженияДокументРегистратор.Добавить("ВЫРАЗИТЬ(ВыручкаИСебестоимостьПродаж.Регистратор КАК Документ.ВозвратТоваровОтКлиента)");
	
	ОтборыПоТипамДокументов = Новый Массив; // Массив Из Строка
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.РеализацияТоваровУслуг");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.КорректировкаРеализации");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.АктВыполненныхРабот");
	ОтборыПоТипамДокументов.Добавить("ВыручкаИСебестоимостьПродаж.Регистратор ССЫЛКА Документ.ВозвратТоваровОтКлиента");
	
	Если ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		УсловиеОтбораТоваров =
		"ВТ_ПараметрыРБ.ОтборТоваровВыбранные
		|	И ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры В
		|			(ВЫБРАТЬ
		|				ВТ_АналитикаУчетаНоменклатуры.КлючАналитики
		|			ИЗ
		|				ВТ_АналитикаУчетаНоменклатуры КАК ВТ_АналитикаУчетаНоменклатуры)";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		УсловиеОтбораТоваров =
		"ВТ_ПараметрыРБ.ОтборТоваровКромеВыбранных
		|	И НЕ ВыручкаИСебестоимостьПродаж.АналитикаУчетаНоменклатуры В
		|			(ВЫБРАТЬ
		|				ВТ_АналитикаУчетаНоменклатуры.КлючАналитики
		|			ИЗ
		|				ВТ_АналитикаУчетаНоменклатуры КАК ВТ_АналитикаУчетаНоменклатуры)";
		
	Иначе
		
		УсловиеОтбораТоваров = "ВТ_ПараметрыРБ.ОтборТоваровБезОграничений";
		
	КонецЕсли;
	
	ВыражениеПериод = ВыражениеПериодРасчета(ПараметрыУсловийРетроБонусов);
	
	Если ПараметрыВарианта.ЭтоРасшифровка Тогда
		
		УсловиеОтбораПоКонтрагенту =
		"ВЫБОР
		|	КОГДА &Контрагент = ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)
		|		ИЛИ &Контрагент = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ РС_АналитикаУчетаПоПартнерам.Контрагент = &Контрагент
		|КОНЕЦ";
		УсловиеОтбораПоПартнеру =
		"ВЫБОР
		|	КОГДА &Партнер = ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка)
		|		ИЛИ &Партнер = НЕОПРЕДЕЛЕНО
		|	ТОГДА ИСТИНА
		|	ИНАЧЕ РС_АналитикаУчетаПоПартнерам.Партнер = &Партнер
		|КОНЕЦ";
		
	Иначе
		
		УсловиеОтбораПоКонтрагенту = "ИСТИНА";
		УсловиеОтбораПоПартнеру = "ИСТИНА";
		
	КонецЕсли;
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	НомерПодзапроса = 1;
	Для ИндексТипаДокумента = 0 По ОтборыПоТипамДокументов.ВГраница() Цикл
		
		ВыражениеДокументРегистратор = ВыраженияДокументРегистратор[ИндексТипаДокумента];
		УсловиеОтбораПоТипуДокумента = ОтборыПоТипамДокументов[ИндексТипаДокумента];
		
		ТекстЗапроса = ШаблонЗапроса;
		
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПериод", ВыражениеПериод);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДокументРегистратор", ВыражениеДокументРегистратор);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоТипуДокумента", УсловиеОтбораПоТипуДокумента);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораТоваров", УсловиеОтбораТоваров);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоКонтрагенту", УсловиеОтбораПоКонтрагенту);
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоПартнеру", УсловиеОтбораПоПартнеру);
		
		Если НомерПодзапроса > 1 Тогда
			
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ВТ_ПродажиЗакупкиБезОтбораПоКонтрагентам", "");
			
		КонецЕсли;
		
		ТекстыПодзапросов.Добавить(ТекстЗапроса);
		НомерПодзапроса = НомерПодзапроса + 1;
		
	КонецЦикла;
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПродажиСОтборомПоКонтрагентам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ИсходныеДанные.Период КАК Период,
	|	ИсходныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	ИсходныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ИсходныеДанные.Организация КАК Организация,
	|	ИсходныеДанные.Контрагент КАК Контрагент,
	|	ИсходныеДанные.Партнер КАК Партнер,
	|	ИсходныеДанные.Договор КАК Договор,
	|	ИсходныеДанные.Соглашение КАК Соглашение,
	|	ИсходныеДанные.Склад КАК Склад,
	|	ИсходныеДанные.Номенклатура КАК Номенклатура,
	|	ИсходныеДанные.Характеристика КАК Характеристика,
	|	СУММА(ИсходныеДанные.Количество) КАК Количество,
	|	СУММА(ИсходныеДанные.СуммаОборотУпрСНДС) КАК СуммаОборотУпрСНДС,
	|	СУММА(ИсходныеДанные.СуммаОборотРеглСНДС) КАК СуммаОборотРеглСНДС,
	|	СУММА(ИсходныеДанные.СуммаОборотВВалютеВзаиморасчетовСНДС) КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	СУММА(ИсходныеДанные.СуммаОборотУпрБезНДС) КАК СуммаОборотУпрБезНДС,
	|	СУММА(ИсходныеДанные.СуммаОборотРеглБезНДС) КАК СуммаОборотРеглБезНДС,
	|	СУММА(ИсходныеДанные.СуммаОборотВВалютеВзаиморасчетовБезНДС) КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	СУММА(ВЫБОР
	|		КОГДА ИсходныеДанные.УчитыватьНДС
	|			ТОГДА ИсходныеДанные.СуммаОборотУпрСНДС
	|		ИНАЧЕ ИсходныеДанные.СуммаОборотУпрБезНДС
	|	КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|		КОГДА ИсходныеДанные.УчитыватьНДС
	|			ТОГДА ИсходныеДанные.СуммаОборотРеглСНДС
	|		ИНАЧЕ ИсходныеДанные.СуммаОборотРеглБезНДС
	|	КОНЕЦ) КАК СуммаРегл,
	|	СУММА(ВЫБОР
	|		КОГДА ИсходныеДанные.УчитыватьНДС
	|			ТОГДА ИсходныеДанные.СуммаОборотВВалютеВзаиморасчетовСНДС
	|		ИНАЧЕ ИсходныеДанные.СуммаОборотВВалютеВзаиморасчетовБезНДС
	|	КОНЕЦ) КАК СуммаВВалютеВзаиморасчетов,
	|	ИсходныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ИсходныеДанные.БонусПроцент КАК БонусПроцент,
	|	ИсходныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ИсходныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ИсходныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ИсходныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ИсходныеДанные.УчитыватьНДС КАК УчитыватьНДС,
	|	ИсходныеДанные.ВалютаУсловий КАК ВалютаУсловий,
	|	ИсходныеДанные.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	ИсходныеДанные.БазаРасчета КАК БазаРасчета,
	|	ИсходныеДанные.ОтборТоваровВыбранные КАК ОтборТоваровВыбранные,
	|	ИсходныеДанные.ОтборТоваровКромеВыбранных КАК ОтборТоваровКромеВыбранных,
	|	ИсходныеДанные.ОтборТоваровБезОграничений КАК ОтборТоваровБезОграничений,
	|	ИсходныеДанные.ОтборУчастниковВыбранные КАК ОтборУчастниковВыбранные,
	|	ИсходныеДанные.ОтборУчастниковКромеВыбранных КАК ОтборУчастниковКромеВыбранных,
	|	ИсходныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ИсходныеДанные.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ПартнерыКонтрагенты.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_ПартнерыКонтрагенты.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ВТ_Партнеры.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_Партнеры.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ВТ_Контрагенты.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_Контрагенты.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ИсходныеДанные.СуммаПланПоСоглашению, 0) <> 0
	|			ТОГДА ИсходныеДанные.СуммаПланПоСоглашению
	|		ИНАЧЕ ЕСТЬNULL(ИсходныеДанные.СуммаПланОбщая, 0)
	|	КОНЕЦ КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_ПродажиЗакупки
	|ИЗ
	|	ВТ_ПродажиЗакупкиБезОтбораПоКонтрагентам КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Контрагенты КАК ВТ_Контрагенты
	|		ПО ИсходныеДанные.УсловиеРетроБонуса = ВТ_Контрагенты.УсловияРетроБонусов
	|			И ИсходныеДанные.Контрагент = ВТ_Контрагенты.Контрагент
	|			И (ИсходныеДанные.Период МЕЖДУ ВТ_Контрагенты.ДатаНачала И ВТ_Контрагенты.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Партнеры КАК ВТ_Партнеры
	|		ПО ИсходныеДанные.УсловиеРетроБонуса = ВТ_Партнеры.УсловияРетроБонусов
	|			И ИсходныеДанные.Партнер = ВТ_Партнеры.Партнер
	|			И (ИсходныеДанные.Период МЕЖДУ ВТ_Партнеры.ДатаНачала И ВТ_Партнеры.ДатаОкончания)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПартнерыКонтрагенты КАК ВТ_ПартнерыКонтрагенты
	|		ПО ИсходныеДанные.УсловиеРетроБонуса = ВТ_ПартнерыКонтрагенты.УсловияРетроБонусов
	|			И ИсходныеДанные.Контрагент = ВТ_ПартнерыКонтрагенты.Контрагент
	|			И ИсходныеДанные.Партнер = ВТ_ПартнерыКонтрагенты.Партнер
	|			И (ИсходныеДанные.Период МЕЖДУ ВТ_ПартнерыКонтрагенты.ДатаНачала И ВТ_ПартнерыКонтрагенты.ДатаОкончания)
	|СГРУППИРОВАТЬ ПО
	|	ИсходныеДанные.Период,
	|	ИсходныеДанные.ПериодРасчета,
	|	ИсходныеДанные.ДокументРегистратор,
	|	ИсходныеДанные.Организация,
	|	ИсходныеДанные.Контрагент,
	|	ИсходныеДанные.Партнер,
	|	ИсходныеДанные.Договор,
	|	ИсходныеДанные.Соглашение,
	|	ИсходныеДанные.Склад,
	|	ИсходныеДанные.Номенклатура,
	|	ИсходныеДанные.Характеристика,
	|	ИсходныеДанные.ЗапретНачисленияСверхПлана,
	|	ИсходныеДанные.РаспределениеПоФИФО,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_ПартнерыКонтрагенты.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_ПартнерыКонтрагенты.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ВТ_Партнеры.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_Партнеры.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ВТ_Контрагенты.СуммаПлан, 0) <> 0
	|			ТОГДА ЕСТЬNULL(ВТ_Контрагенты.СуммаПлан, 0)
	|		КОГДА ЕСТЬNULL(ИсходныеДанные.СуммаПланПоСоглашению, 0) <> 0
	|			ТОГДА ИсходныеДанные.СуммаПланПоСоглашению
	|		ИНАЧЕ ЕСТЬNULL(ИсходныеДанные.СуммаПланОбщая, 0)
	|	КОНЕЦ,
	|	ИсходныеДанные.УсловиеРетроБонуса,
	|	ИсходныеДанные.БонусПроцент,
	|	ИсходныеДанные.ПоказательТоваров,
	|	ИсходныеДанные.ДетализацияРасчетаУчастников,
	|	ИсходныеДанные.ПериодичностьУсловий,
	|	ИсходныеДанные.ПериодичностьНачислений,
	|	ИсходныеДанные.УчитыватьНДС,
	|	ИсходныеДанные.ВалютаУсловий,
	|	ИсходныеДанные.ВалютаВзаиморасчетов,
	|	ИсходныеДанные.БазаРасчета,
	|	ИсходныеДанные.ОтборТоваровВыбранные,
	|	ИсходныеДанные.ОтборТоваровКромеВыбранных,
	|	ИсходныеДанные.ОтборТоваровБезОграничений,
	|	ИсходныеДанные.ОтборУчастниковВыбранные,
	|	ИсходныеДанные.ОтборУчастниковКромеВыбранных";
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПроцентСкидкиБезГруппировок(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказательТоваров = ПараметрыУсловийРетроБонусов.ПоказательТоваров;
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ПустоеЗначениеСвойства = ПустоеЗначениеПоляСоставногоТипа(ПараметрыУсловийРетроБонусов.СвойствоНоменклатурыТипЗначения);
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПродажиЗакупки.Период КАК Период,
	|	АВТОНОМЕРЗАПИСИ() КАК НомерСтроки,
	|	ВТ_ПродажиЗакупки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПродажиЗакупки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПродажиЗакупки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПродажиЗакупки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПродажиЗакупки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	&ВыражениеПериодичностьУсловий КАК ПериодичностьУсловий,
	|	&ВыражениеЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	&ВыражениеРаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	&ВыражениеСуммаПлан КАК СуммаПлан,
	|	&ВыражениеКоличествоПлан КАК КоличествоПлан,
	|	ВТ_ПродажиЗакупки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПродажиЗакупки.Организация КАК Организация,
	|	ВТ_ПродажиЗакупки.Контрагент КАК Контрагент,
	|	ВТ_ПродажиЗакупки.Партнер КАК Партнер,
	|	ВТ_ПродажиЗакупки.Договор КАК Договор,
	|	ВТ_ПродажиЗакупки.Соглашение КАК Соглашение,
	|	ВТ_ПродажиЗакупки.Номенклатура КАК Номенклатура,
	|	ВЫРАЗИТЬ(ВТ_ПродажиЗакупки.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	&ВыражениеХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	&ВыражениеХарактеристикаРасчета КАК Характеристика,
	|	ВТ_ПродажиЗакупки.Характеристика КАК ХарактеристикаПродажЗакупок,
	|	&ВыражениеЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПродажиЗакупки.Количество КАК Количество,
	|	ВТ_ПродажиЗакупки.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПродажиЗакупки.ВалютаУсловий КАК ВалютаУсловий,
	|	ВТ_ПродажиЗакупки.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	&ВыражениеВалютаРасчета КАК Валюта,
	|	ВЫБОР
	|		КОГДА &ВыражениеОбороты < 0
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЗнакОборотов,
	|	ВТ_ПродажиЗакупки.Сумма КАК Сумма,
	|	ВТ_ПродажиЗакупки.СуммаРегл КАК СуммаРегл,
	|	ВТ_ПродажиЗакупки.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ВТ_ПродажиЗакупки.СуммаОборотУпрСНДС КАК СуммаОборотУпрСНДС,
	|	ВТ_ПродажиЗакупки.СуммаОборотРеглСНДС КАК СуммаОборотРеглСНДС,
	|	ВТ_ПродажиЗакупки.СуммаОборотВВалютеВзаиморасчетовСНДС КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	ВТ_ПродажиЗакупки.СуммаОборотУпрБезНДС КАК СуммаОборотУпрБезНДС,
	|	ВТ_ПродажиЗакупки.СуммаОборотРеглБезНДС КАК СуммаОборотРеглБезНДС,
	|	ВТ_ПродажиЗакупки.СуммаОборотВВалютеВзаиморасчетовБезНДС КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	&ВыражениеБонусПроцент КАК БонусПроцент,
	|	&ВыражениеДатаНачала КАК ДатаНачала,
	|	&ВыражениеДатаОкончания КАК ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ПроцентСкидки
	|ИЗ
	|	&ТаблицыИсточники КАК ВТ_ПродажиЗакупки
	|ГДЕ
	|	&УсловиеОтбораТоваров
	|	И &УсловиеОтбораПоСоглашениям
	|	И &УсловиеПоляСвертки
	|
	|ИНДЕКСИРОВАТЬ ПО 1";
	
	Если ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.Выбранные Тогда
		
		ТаблицыИсточники =
		"ВТ_ПродажиЗакупки КАК ВТ_ПродажиЗакупки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_ТоварыСХарактеристиками
		|		ПО ВТ_ПродажиЗакупки.Номенклатура = ВТ_ТоварыСХарактеристиками.Номенклатура
		|			И ВТ_ПродажиЗакупки.Характеристика = ВТ_ТоварыСХарактеристиками.Характеристика
		|			И (ВТ_ПродажиЗакупки.Период МЕЖДУ ВТ_ТоварыСХарактеристиками.ДатаНачала И ВТ_ТоварыСХарактеристиками.ДатаОкончания)
		|			И ВТ_ТоварыСХарактеристиками.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|			И &УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам
		|			И &УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_ТоварыБезХарактеристик
		|		ПО ВТ_ПродажиЗакупки.Номенклатура = ВТ_ТоварыБезХарактеристик.Номенклатура
		|			И (ВТ_ПродажиЗакупки.Период МЕЖДУ ВТ_ТоварыБезХарактеристик.ДатаНачала И ВТ_ТоварыБезХарактеристик.ДатаОкончания)
		|			И &УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам
		|			И ВТ_ТоварыБезХарактеристик.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
		
		УсловиеОтбораТоваров = "((ВТ_ТоварыСХарактеристиками.Номенклатура ЕСТЬ НЕ NULL)
		|	ИЛИ (ВТ_ТоварыБезХарактеристик.Номенклатура ЕСТЬ НЕ NULL))";
		
		Если ПоказательТоваров = ВидыПоказателей.Количество Тогда
			
			// Для ЦПП Количество процент бонуса по товарам определяется далее в ВТ_ПодготовленныеДанныеОпределениеПлана
			ВыражениеБазоваяЦена = "0";
			ВыражениеБонусПроцент = "0";
			ВыражениеКоличествоПлан = "0";
			
			Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
				
				УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ВТ_ТоварыСХарактеристиками.КоличествоПлан = 0";
				УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ВТ_ТоварыБезХарактеристик.КоличествоПлан = 0";
				
			Иначе
				
				УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ИСТИНА";
				УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ИСТИНА";
				
			КонецЕсли;
			
		Иначе
			
			ВыражениеБазоваяЦена = "ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.БазоваяЦена, ВТ_ТоварыБезХарактеристик.БазоваяЦена)";
			
			ВыражениеБонусПроцент =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.БонусПроцент, ВТ_ТоварыБезХарактеристик.БонусПроцент) <> 0
			|		ТОГДА ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.БонусПроцент, ВТ_ТоварыБезХарактеристик.БонусПроцент)
			|	ИНАЧЕ ВТ_ПродажиЗакупки.БонусПроцент
			|КОНЕЦ";
			
			ВыражениеКоличествоПлан =
				"ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.КоличествоПлан, ВТ_ТоварыБезХарактеристик.КоличествоПлан)";
			
			УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ИСТИНА";
			УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ИСТИНА";
			
		КонецЕсли;
		
		ВыражениеДатаНачала = "ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.ДатаНачала, ВТ_ТоварыБезХарактеристик.ДатаНачала)";
		ВыражениеДатаОкончания = "ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.ДатаОкончания, ВТ_ТоварыБезХарактеристик.ДатаОкончания)";
		
		Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
			
			ВыражениеХарактеристикиИспользуются = "ЛОЖЬ";
			ВыражениеХарактеристикаРасчета = "ВТ_ПродажиЗакупки.Характеристика";
			УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам = "ЛОЖЬ";
			
		Иначе
			
			ВыражениеХарактеристикиИспользуются =
				"ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.ХарактеристикиИспользуются, ВТ_ТоварыБезХарактеристик.ХарактеристикиИспользуются)";
			
			ВыражениеХарактеристикаРасчета =
				"ВЫРАЗИТЬ(ЕСТЬNULL(ВТ_ТоварыСХарактеристиками.Характеристика,
				|	ВТ_ТоварыБезХарактеристик.Характеристика) КАК Справочник.ХарактеристикиНоменклатуры)";
			
			УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам = "ИСТИНА";
			
		КонецЕсли;
		
		ВыражениеЗначениеСвойства = СтрШаблон("ЕСТЬNULL(ВТ_ТоварыБезХарактеристик.ЗначениеСвойства, %1)", ПустоеЗначениеСвойства);
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.Товары = Перечисления.СоставыСписковРетроБонусов.КромеВыбранных Тогда
		
		ТаблицыИсточники =
		"ВТ_ПродажиЗакупки КАК ВТ_ПродажиЗакупки
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_ТоварыСХарактеристиками
		|		ПО ВТ_ПродажиЗакупки.Номенклатура = ВТ_ТоварыСХарактеристиками.Номенклатура
		|			И ВТ_ПродажиЗакупки.Характеристика = ВТ_ТоварыСХарактеристиками.Характеристика
		|			И (ВТ_ПродажиЗакупки.Период МЕЖДУ ВТ_ТоварыСХарактеристиками.ДатаНачала И ВТ_ТоварыСХарактеристиками.ДатаОкончания)
		|			И &УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам
		|			И &УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам
		|			И ВТ_ТоварыСХарактеристиками.Характеристика <> ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_ТоварыБезХарактеристик
		|		ПО ВТ_ПродажиЗакупки.Номенклатура = ВТ_ТоварыБезХарактеристик.Номенклатура
		|			И (ВТ_ПродажиЗакупки.Период МЕЖДУ ВТ_ТоварыБезХарактеристик.ДатаНачала И ВТ_ТоварыБезХарактеристик.ДатаОкончания)
		|			И &УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам
		|			И ВТ_ТоварыСХарактеристиками.Характеристика = ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
		
		УсловиеОтбораТоваров = "ВТ_ТоварыСХарактеристиками.Номенклатура ЕСТЬ NULL
		|	И ВТ_ТоварыБезХарактеристик.Номенклатура ЕСТЬ NULL";
		
		ВыражениеБазоваяЦена = "0";
		ВыражениеБонусПроцент = "ВТ_ПродажиЗакупки.БонусПроцент";
		ВыражениеДатаНачала = "ВТ_ПродажиЗакупки.Период";
		ВыражениеДатаОкончания = "ВТ_ПродажиЗакупки.Период";
		ВыражениеКоличествоПлан = "0";
		ВыражениеХарактеристикиИспользуются = "ЛОЖЬ";
		ВыражениеХарактеристикаРасчета = "ВТ_ПродажиЗакупки.Характеристика";
		ВыражениеЗначениеСвойства = СтрШаблон("ЕСТЬNULL(ВТ_ТоварыБезХарактеристик.ЗначениеСвойства, %1)", ПустоеЗначениеСвойства);
		
		Если ПоказательТоваров = ВидыПоказателей.Количество
		   И ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
			
			УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ВТ_ТоварыСХарактеристиками.КоличествоПлан = 0";
			УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ВТ_ТоварыБезХарактеристик.КоличествоПлан = 0";
			
		Иначе
			
			УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ИСТИНА";
			УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ИСТИНА";
			
		КонецЕсли;
		
		Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
			УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам = "ЛОЖЬ";
		Иначе
			УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам = "ИСТИНА";
		КонецЕсли;
		
	Иначе
		
		ТаблицыИсточники = "ВТ_ПродажиЗакупки КАК ВТ_ПродажиЗакупки";
		УсловиеОтбораТоваров = "ВТ_ПродажиЗакупки.ОтборТоваровБезОграничений";
		ВыражениеБазоваяЦена = "0";
		ВыражениеБонусПроцент = "ВТ_ПродажиЗакупки.БонусПроцент";
		ВыражениеДатаНачала = "ВТ_ПродажиЗакупки.Период";
		ВыражениеДатаОкончания = "ВТ_ПродажиЗакупки.Период";
		ВыражениеКоличествоПлан = "0";
		ВыражениеХарактеристикиИспользуются = "ЛОЖЬ";
		ВыражениеХарактеристикаРасчета = "ВТ_ПродажиЗакупки.Характеристика";
		ВыражениеЗначениеСвойства = "НЕОПРЕДЕЛЕНО";
		УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам = "ИСТИНА";
		УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам = "ИСТИНА";
		УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам = "ИСТИНА";
		
	КонецЕсли;
	
	Если ПоказательТоваров = ВидыПоказателей.НеИспользуется Тогда
		
		ВыражениеПериодичностьУсловий = "ЗНАЧЕНИЕ(Перечисление.ПериодичностиРетроБонусов.ПустаяСсылка)";
		ВыражениеЗапретНачисленияСверхПлана = "ЛОЖЬ";
		ВыражениеСуммаПлан = "0";
		ВыражениеРаспределениеПоФИФО = "ИСТИНА";
		
	Иначе
		
		ВыражениеПериодичностьУсловий = "ВТ_ПродажиЗакупки.ПериодичностьУсловий";
		ВыражениеЗапретНачисленияСверхПлана = "ВТ_ПродажиЗакупки.ЗапретНачисленияСверхПлана";
		ВыражениеСуммаПлан = "ВТ_ПродажиЗакупки.СуммаПлан";
		ВыражениеРаспределениеПоФИФО = "ВТ_ПродажиЗакупки.РаспределениеПоФИФО";
		
	КонецЕсли;
	
	Если ПоказательТоваров = ВидыПоказателей.Количество Тогда
		
		// Для ЦПП Количество может быть несколько строк товара с разным планом и процентом (прогрессивная шкала)
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВЫБРАТЬ", "ВЫБРАТЬ РАЗЛИЧНЫЕ"); // @query-part-1, @query-part-2
		
	КонецЕсли;
	
	// для предотвращения удаления полей свертки из предыдущей таблицы
	УсловиеПоляСвертки = "ВТ_ПродажиЗакупки.Номенклатура ЕСТЬ НЕ NULL
	|	И ВТ_ПродажиЗакупки.Характеристика ЕСТЬ НЕ NULL";
	
	Если ПараметрыУсловийРетроБонусов.СоставУчастников = Перечисления.СоставыУчастниковРетроБонусов.Соглашения Тогда
		
		УсловиеОтбораПоСоглашениям =
		"ВТ_ПродажиЗакупки.Соглашение В
		|	(ВЫБРАТЬ
		|		ВТ_Соглашения.Соглашение
		|	ИЗ
		|		ВТ_Соглашения КАК ВТ_Соглашения)";
		
	Иначе
		
		УсловиеОтбораПоСоглашениям = "ИСТИНА";
		
	КонецЕсли;
	
	НужныИндексыДляЦПСумма = (ПоказательТоваров = ВидыПоказателей.Сумма
		И (ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
			ИЛИ ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам)
		И НЕ ПараметрыУсловийРетроБонусов.ПропорциональноеРаспределение);
	
	Если ПоказательТоваров = ВидыПоказателей.НеИспользуется
	 ИЛИ НЕ НужныИндексыДляЦПСумма Тогда
		
		ПоляИндексирования = "";
	
	Иначе
		
		ПоляИндексирования =
			"ИНДЕКСИРОВАТЬ ПО
			|	ДокументРегистратор,
			|	НомерСтроки";
		
	КонецЕсли;
	
	ПодставитьВыражениеВалютаРасчета(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПродажиЗакупки");
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	   И НЕ ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
		
		ВыражениеОбороты = "&ВыражениеСуммаБезУточнения";
		ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ВыражениеОбороты, "ВТ_ПродажиЗакупки");
		
	Иначе
		
		ВыражениеОбороты = "ВТ_ПродажиЗакупки.Количество";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицыИсточники КАК ВТ_ПродажиЗакупки", ТаблицыИсточники);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораТоваров", УсловиеОтбораТоваров);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБазоваяЦена", ВыражениеБазоваяЦена);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусПроцент", ВыражениеБонусПроцент);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДатаНачала", ВыражениеДатаНачала);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДатаОкончания", ВыражениеДатаОкончания);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПериодичностьУсловий", ВыражениеПериодичностьУсловий);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеЗапретНачисленияСверхПлана", ВыражениеЗапретНачисленияСверхПлана);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПлан", ВыражениеСуммаПлан);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРаспределениеПоФИФО", ВыражениеРаспределениеПоФИФО);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПлан", ВыражениеКоличествоПлан);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеХарактеристикиИспользуются", ВыражениеХарактеристикиИспользуются);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеХарактеристикаРасчета", ВыражениеХарактеристикаРасчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеЗначениеСвойства", ВыражениеЗначениеСвойства);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеОбороты", ВыражениеОбороты);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПоСоглашениям", УсловиеОтбораПоСоглашениям);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПоляСвертки", УсловиеПоляСвертки);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам",
		УсловиеСоединенияТоварыСХарактеристикамиПоДиапазонам);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам",
		УсловиеСоединенияТоварыБезХарактеристикПоДиапазонам);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса,
		"&УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам",
		УсловиеСоединенияТоварыСХарактеристикамиПоСвойствам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИНДЕКСИРОВАТЬ ПО 1", ПоляИндексирования);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаУчастникиПериодыДокументы(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта
	|ПОМЕСТИТЬ ВТ_УчастникиПериодыДокументы
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаУчастникиПериодыДокументыВсеТовары(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_УчастникиПериодыДокументы.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_УчастникиПериодыДокументы.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_УчастникиПериодыДокументы.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_УчастникиПериодыДокументы.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_УчастникиПериодыДокументы.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_УчастникиПериодыДокументы.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_УчастникиПериодыДокументы.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_УчастникиПериодыДокументы.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_УчастникиПериодыДокументы.СуммаПлан КАК СуммаПлан,
	|	ВТ_УчастникиПериодыДокументы.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_УчастникиПериодыДокументы.БазаРасчета КАК БазаРасчета,
	|	ВТ_УчастникиПериодыДокументы.Валюта КАК Валюта,
	|	&ПоляТоваров,
	|	ЕСТЬNULL(ВТ_Товары.ХарактеристикиИспользуются, ИСТИНА) КАК ХарактеристикиИспользуются,
	|	ЕСТЬNULL(ВТ_Товары.БазоваяЦена, 0) КАК БазоваяЦена,
	|	ЕСТЬNULL(ВТ_Товары.БонусПроцент, 0) КАК БонусПроцент,
	|	ЕСТЬNULL(ВТ_Товары.КоличествоПлан, 0) КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_УчастникиПериодыДокументыВсеТовары
	|ИЗ
	|	ВТ_УчастникиПериодыДокументы КАК ВТ_УчастникиПериодыДокументы
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО ИСТИНА";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_УчастникиПериодыДокументы");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_Товары");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаУчастникиПериодыДокументыПроданныеТовары(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	&ПоляТоваров
	|ПОМЕСТИТЬ ВТ_УчастникиПериодыДокументыПроданныеТовары
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаНедостающийСоставПакета(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ПериодРасчета КАК ПериодРасчета,
	|	&ПустаяСсылкаРегистратора КАК ДокументРегистратор,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.СуммаПлан КАК СуммаПлан,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ЗНАЧЕНИЕ(Справочник.ДоговорыКонтрагентов.ПустаяСсылка) КАК Договор,
	|	ЗНАЧЕНИЕ(Справочник.СоглашенияСКлиентами.ПустаяСсылка) КАК Соглашение,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.БазаРасчета КАК БазаРасчета,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.Валюта КАК Валюта,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	&ПоляТоваров,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.БонусПроцент КАК БонусПроцент,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.КоличествоПлан КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_НедостающийСоставПакета
	|ИЗ
	|	ВТ_УчастникиПериодыДокументыВсеТовары КАК ВТ_УчастникиПериодыДокументыВсеТовары
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УчастникиПериодыДокументыПроданныеТовары КАК ВТ_УчастникиПериодыДокументыПроданныеТовары
	|	ПО ВТ_УчастникиПериодыДокументыВсеТовары.УсловиеРетроБонуса = ВТ_УчастникиПериодыДокументыПроданныеТовары.УсловиеРетроБонуса
	|		И ВТ_УчастникиПериодыДокументыВсеТовары.Организация = ВТ_УчастникиПериодыДокументыПроданныеТовары.Организация
	|		И ВТ_УчастникиПериодыДокументыВсеТовары.Валюта = ВТ_УчастникиПериодыДокументыПроданныеТовары.Валюта
	|		И &ПоляСоединенияПоУчастникам
	|		И &ПоляСоединенияПоТоварам
	|		И ВТ_УчастникиПериодыДокументыВсеТовары.ПериодРасчета = ВТ_УчастникиПериодыДокументыПроданныеТовары.ПериодРасчета
	|ГДЕ
	|	&УсловиеТоварНеПродан";
	
	ПустаяСсылкаРегистратора = ЗначениеПустойСсылкиРегистратора(ПараметрыУсловийРетроБонусов);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПустаяСсылкаРегистратора", ПустаяСсылкаРегистратора);
	
	Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		УсловиеТоварНеПродан = "ВТ_УчастникиПериодыДокументыПроданныеТовары.ЗначениеСвойства ЕСТЬ NULL";
	Иначе
		УсловиеТоварНеПродан = "ВТ_УчастникиПериодыДокументыПроданныеТовары.Номенклатура ЕСТЬ NULL";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеТоварНеПродан", УсловиеТоварНеПродан);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_УчастникиПериодыДокументыВсеТовары",
		"ВТ_УчастникиПериодыДокументыПроданныеТовары");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_УчастникиПериодыДокументыВсеТовары",
		"ВТ_УчастникиПериодыДокументыПроданныеТовары");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПроцентСкидкиСДаннымиПоПакету(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	ВТ_ПроцентСкидки.Сумма КАК Сумма,
	|	ВТ_ПроцентСкидки.СуммаРегл КАК СуммаРегл,
	|	ВТ_ПроцентСкидки.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрСНДС КАК СуммаОборотУпрСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглСНДС КАК СуммаОборотРеглСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовСНДС КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрБезНДС КАК СуммаОборотУпрБезНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглБезНДС КАК СуммаОборотРеглБезНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовБезНДС КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	ВТ_ПроцентСкидки.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПроцентСкидки.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПроцентСкидки.ДатаНачала КАК ДатаНачала,
	|	ВТ_ПроцентСкидки.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_ПроцентСкидки.КоличествоПлан КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_ПроцентСкидкиСДаннымиПоПакету
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПроцентСкидки");
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_НедостающийСоставПакета.ПериодРасчета КАК Период,
	|	0 КАК НомерСтроки,
	|	ВТ_НедостающийСоставПакета.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_НедостающийСоставПакета.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_НедостающийСоставПакета.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_НедостающийСоставПакета.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_НедостающийСоставПакета.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_НедостающийСоставПакета.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_НедостающийСоставПакета.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_НедостающийСоставПакета.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_НедостающийСоставПакета.СуммаПлан КАК СуммаПлан,
	|	ВТ_НедостающийСоставПакета.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_НедостающийСоставПакета.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_НедостающийСоставПакета.Договор КАК Договор,
	|	ВТ_НедостающийСоставПакета.Соглашение КАК Соглашение,
	|	ВТ_НедостающийСоставПакета.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	&ВыражениеХарактеристика КАК ХарактеристикаПродажЗакупок,
	|	&ВыражениеНоменклатура КАК Номенклатура,
	|	&ВыражениеЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	&ВыражениеХарактеристика КАК Характеристика,
	|	&ВыражениеЗначениеСвойства КАК ЗначениеСвойства,
	|	0 КАК Количество,
	|	ВТ_НедостающийСоставПакета.БазаРасчета КАК БазаРасчета,
	|	ВТ_НедостающийСоставПакета.Валюта КАК Валюта,
	|	0 КАК Сумма,
	|	0 КАК СуммаРегл,
	|	0 КАК СуммаВВалютеВзаиморасчетов,
	|	0 КАК СуммаОборотУпрСНДС,
	|	0 КАК СуммаОборотРеглСНДС,
	|	0 КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	0 КАК СуммаОборотУпрБезНДС,
	|	0 КАК СуммаОборотРеглБезНДС,
	|	0 КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	ВТ_НедостающийСоставПакета.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_НедостающийСоставПакета.БонусПроцент КАК БонусПроцент,
	|	ВТ_НедостающийСоставПакета.ПериодРасчета КАК ДатаНачала,
	|	ВТ_НедостающийСоставПакета.ПериодРасчета КАК ДатаОкончания,
	|	ВТ_НедостающийСоставПакета.КоличествоПлан КАК КоличествоПлан
	|ИЗ
	|	ВТ_НедостающийСоставПакета КАК ВТ_НедостающийСоставПакета";
	
	Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		ВыражениеНоменклатура = "ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)";
		ВыражениеЕдиницаИзмерения = "ЗНАЧЕНИЕ(Справочник.УпаковкиЕдиницыИзмерения.ПустаяСсылка)";
		ВыражениеХарактеристика = "ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)";
		ВыражениеЗначениеСвойства = "ВТ_НедостающийСоставПакета.ЗначениеСвойства";
		
	Иначе
		
		ВыражениеНоменклатура = "ВТ_НедостающийСоставПакета.Номенклатура";
		ВыражениеЕдиницаИзмерения = "ВЫРАЗИТЬ(ВТ_НедостающийСоставПакета.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения";
		ВыражениеХарактеристика = "ВТ_НедостающийСоставПакета.Характеристика";
		ВыражениеЗначениеСвойства = "НЕОПРЕДЕЛЕНО";
		
	КонецЕсли;
	
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ВыражениеНоменклатура", ВыражениеНоменклатура);
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ВыражениеЕдиницаИзмерения", ВыражениеЕдиницаИзмерения);
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ВыражениеХарактеристика", ВыражениеХарактеристика);
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ВыражениеЗначениеСвойства", ВыражениеЗначениеСвойства);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_НедостающийСоставПакета");
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаКратностьПакетаТоваров(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация КАК Организация,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.КоличествоПлан КАК КоличествоПлан,
	|	СУММА(ВТ_ПроцентСкидкиСДаннымиПоПакету.Количество) КАК Количество
	|ПОМЕСТИТЬ ВТ_КратностьПакетаТоваров
	|ИЗ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету КАК ВТ_ПроцентСкидкиСДаннымиПоПакету
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.КоличествоПлан";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаКоличествоПакетовТоваров(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	// Условия на поля товаров необходимы для предотвращения их удаления оптимизатором СКД из предыдущей ВТ
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_КратностьПакетаТоваров.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_КратностьПакетаТоваров.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_КратностьПакетаТоваров.Организация КАК Организация,
	|	ВТ_КратностьПакетаТоваров.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	МИНИМУМ(ВЫБОР
	|		КОГДА ВТ_КратностьПакетаТоваров.Количество <= 0
	|			ТОГДА 0
	|		КОГДА ВТ_КратностьПакетаТоваров.КоличествоПлан = 0
	|			ТОГДА ВТ_КратностьПакетаТоваров.Количество
	|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_КратностьПакетаТоваров.Количество / ВТ_КратностьПакетаТоваров.КоличествоПлан
	|			- 0.5 КАК ЧИСЛО(15, 0))
	|	КОНЕЦ) КАК КоличествоПакетов
	|ПОМЕСТИТЬ ВТ_КоличествоПакетовТоваров
	|ИЗ
	|	ВТ_КратностьПакетаТоваров КАК ВТ_КратностьПакетаТоваров
	|ГДЕ
	|	&УсловиеНаПоляТоваров
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_КратностьПакетаТоваров.УсловиеРетроБонуса,
	|	ВТ_КратностьПакетаТоваров.ПериодРасчета,
	|	ВТ_КратностьПакетаТоваров.Организация,
	|	ВТ_КратностьПакетаТоваров.Валюта,
	|	&ПоляГруппировкиПоУчастникам";
	
	Если ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		УсловиеНаПоляТоваров = "ВТ_КратностьПакетаТоваров.ЗначениеСвойства ЕСТЬ НЕ NULL";
		
	Иначе
		
		УсловиеНаПоляТоваров =
		"ВТ_КратностьПакетаТоваров.Номенклатура ЕСТЬ НЕ NULL
		|	И ВТ_КратностьПакетаТоваров.Характеристика ЕСТЬ НЕ NULL";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеНаПоляТоваров", УсловиеНаПоляТоваров);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_КратностьПакетаТоваров");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПревышениеЛимитаОбщая(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	//@skip-check bsl-ql-hub - поля индексирования уточняются путем замены текста запроса
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1000000000
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.СуммаПлан КАК СуммаПлан,
	|	АВТОНОМЕРЗАПИСИ() КАК Приоритет,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	&ВыражениеСуммаБезУточнения КАК Сумма
	|ПОМЕСТИТЬ ВТ_ПревышениеЛимитаОбщая
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|
	|УПОРЯДОЧИТЬ ПО
	|	&ВыражениеПоляУпорядочивания
	|
	|ИНДЕКСИРОВАТЬ ПО ПериодРасчета";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	   И ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	   И ПараметрыУсловийРетроБонусов.ПропорциональноеРаспределение Тогда
		
		ПоляИндексирования = "";
		
	Иначе
		
		ПоляИндексирования = "ИНДЕКСИРОВАТЬ ПО
		|	ПериодРасчета,
		|	УсловиеРетроБонуса,
		|	Организация,
		|	&ПоляИндексированияПоУчастникам,
		|	НомерСтроки,
		|	Номенклатура,
		|	Характеристика,
		|	Валюта";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ИНДЕКСИРОВАТЬ ПО ПериодРасчета", ПоляИндексирования);
	
	ПоляУпорядочивания = ПоляУпорядочиванияПоХронологии(ПараметрыУсловийРетроБонусов, "ВТ_ПроцентСкидки");
	ВыражениеПоляУпорядочивания = СтрСоединить(ПоляУпорядочивания, "," + Символы.ПС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПоляУпорядочивания", ВыражениеПоляУпорядочивания);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПорциям(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	//@skip-check bsl-ql-hub - поля группировки и индексирования уточняются путем замены текста запроса
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ВЫРАЗИТЬ(ТаблицаИсточник.Приоритет / &ПорцияНарастающегоИтога - 0.5 КАК ЧИСЛО(15, 0))
	|		* &ПорцияНарастающегоИтога КАК ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	&ВыражениеКоличествоПлан КАК КоличествоПлан,
	|	0 КАК КоличествоДоПорции,
	|	0 КАК СуммаДоПорции,
	|	СУММА(ТаблицаИсточник.Количество) КАК Количество,
	|	СУММА(ТаблицаИсточник.РасчетнаяБаза) КАК Сумма,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ТаблицаИсточник.РасчетнаяБаза > 0
	|				ТОГДА ТаблицаИсточник.РасчетнаяБаза
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПриход,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ТаблицаИсточник.Количество > 0
	|				ТОГДА ТаблицаИсточник.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПриход
	|ПОМЕСТИТЬ ВТ_ОборотыПоПорциям
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ТаблицаИсточник
	|ГДЕ
	|	&ВыражениеУсловие
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.ПериодРасчета,
	|	ВЫРАЗИТЬ(ТаблицаИсточник.Приоритет / &ПорцияНарастающегоИтога - 0.5 КАК ЧИСЛО(15, 0)) * &ПорцияНарастающегоИтога,
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация,
	|	ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.СуммаПлан,
	|	&ВыражениеКоличествоПлан";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.Сумма Тогда
		
		ВыражениеУсловие = "(ТаблицаИсточник.СуммаПроверки >= ТаблицаИсточник.СуммаПлан)";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
		
		ВыражениеУсловие = "(ТаблицаИсточник.КоличествоПакетов > 0)";
		
	Иначе
		
		ВыражениеУсловие = "(ТаблицаИсточник.КоличествоПроверки >= ТаблицаИсточник.КоличествоПлан)";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеУсловие", ВыражениеУсловие);
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
		
		ВыражениеКоличествоПлан = "ТаблицаИсточник.КоличествоПакетов * ТаблицаИсточник.КоличествоПлан";
		
	Иначе
		
		ВыражениеКоличествоПлан = "ТаблицаИсточник.КоличествоПлан";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПлан", ВыражениеКоличествоПлан);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПорцияНарастающегоИтога", ПорцияНарастающегоИтога());
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//  ТаблицаИсточник - Строка
//  ТаблицаНазначение - Строка
//  ПервыйПроход - Булево
//
Процедура ДобавитьТекстЗапросаОборотыПоПорциямНарастающийИтог(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов, ТаблицаИсточник, ТаблицаНазначение, ПервыйПроход = Ложь) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.ПриоритетПорция КАК ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаИсточник.КоличествоДоПорции КАК КоличествоДоПорции,
	|	ТаблицаИсточник.СуммаДоПорции КАК СуммаДоПорции,
	|	ТаблицаИсточник.СуммаПриход КАК СуммаПриход,
	|	ТаблицаИсточник.Сумма КАК СуммаПорции,
	|	ТаблицаИсточник.КоличествоПриход КАК КоличествоПриход,
	|	ТаблицаИсточник.Количество КАК КоличествоПорции,
	|	СУММА(ТаблицаИсточникЗеркало.Количество) КАК Количество,
	|	СУММА(ТаблицаИсточникЗеркало.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ТаблицаНазначение
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаИсточник КАК ТаблицаИсточникЗеркало
	|		ПО ТаблицаИсточник.ПриоритетПорция >= ТаблицаИсточникЗеркало.ПриоритетПорция
	|			И &УсловиеРодительскаяПорция
	|			И ТаблицаИсточник.ПериодРасчета = ТаблицаИсточникЗеркало.ПериодРасчета
	|			И ТаблицаИсточник.УсловиеРетроБонуса = ТаблицаИсточникЗеркало.УсловиеРетроБонуса
	|			И ТаблицаИсточник.Организация = ТаблицаИсточникЗеркало.Организация
	|			И ТаблицаИсточник.Валюта = ТаблицаИсточникЗеркало.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.ПериодРасчета,
	|	ТаблицаИсточник.ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация,
	|	ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан,
	|	ТаблицаИсточник.КоличествоДоПорции,
	|	ТаблицаИсточник.Сумма,
	|	ТаблицаИсточник.СуммаПриход,
	|	ТаблицаИсточник.СуммаДоПорции,
	|	ТаблицаИсточник.КоличествоПриход,
	|	ТаблицаИсточник.Количество";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_ТаблицаНазначение", ТаблицаНазначение);
	
	Если ПервыйПроход Тогда
		УсловиеРодительскаяПорция = "ИСТИНА";
	Иначе
		УсловиеРодительскаяПорция = "ТаблицаИсточник.ПриоритетРодительскаяПорция <= ТаблицаИсточникЗеркало.ПриоритетПорция";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеРодительскаяПорция", УсловиеРодительскаяПорция);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ТаблицаИсточникЗеркало");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ТаблицаИсточникЗеркало");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//  ТаблицаИсточник - Строка
//  ТаблицаНазначение - Строка
//
Процедура ДобавитьТекстЗапросаПорцияПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов, ТаблицаИсточник, ТаблицаНазначение) Экспорт
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	Если НЕ ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	   И ПараметрыУсловийРетроБонусов.ПоказательТоваров <> ПоказателиПродаж.ПакетноеПредложение Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПараметрыУсловийРетроБонусов.РаспределениеПоFIFO Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	МИНИМУМ(ТаблицаИсточник.ПриоритетПорция) КАК ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_ТаблицаНазначение
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|ГДЕ
	|	&УсловиеПревышениеПлана
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.ПериодРасчета,
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация,
	|	ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.ПриоритетПорция КАК ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|ГДЕ
	|	&УсловиеПорцииСКорректировками";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.Сумма Тогда
		
		УсловиеПревышениеПлана = "ТаблицаИсточник.Сумма + ТаблицаИсточник.СуммаДоПорции >= ТаблицаИсточник.СуммаПлан";
		УсловиеПорцииСКорректировками = "ТаблицаИсточник.Сумма + ТаблицаИсточник.СуммаДоПорции < ТаблицаИсточник.СуммаПлан
		|	И ТаблицаИсточник.СуммаДоПорции + ТаблицаИсточник.Сумма - ТаблицаИсточник.СуммаПорции + ТаблицаИсточник.СуммаПриход > ТаблицаИсточник.СуммаПлан";
		
	Иначе
		
		УсловиеПревышениеПлана = "ТаблицаИсточник.Количество + ТаблицаИсточник.КоличествоДоПорции >= ТаблицаИсточник.КоличествоПлан";
		УсловиеПорцииСКорректировками = "ТаблицаИсточник.Количество + ТаблицаИсточник.КоличествоДоПорции < ТаблицаИсточник.КоличествоПлан
		|	И ТаблицаИсточник.КоличествоДоПорции + ТаблицаИсточник.Количество - ТаблицаИсточник.КоличествоПорции + ТаблицаИсточник.КоличествоПриход > ТаблицаИсточник.КоличествоПлан";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПревышениеПлана", УсловиеПревышениеПлана);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеПорцииСКорректировками", УсловиеПорцииСКорректировками);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_ТаблицаНазначение", ТаблицаНазначение);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//  ТаблицаИсточник - Строка
//  ТаблицаНазначение - Строка
//
Процедура ДобавитьТекстЗапросаПорцияПревышенияЛимитаПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов, ТаблицаИсточник, ТаблицаНазначение) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.ПриоритетПорция КАК ПриоритетПорция,
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_ТаблицаНазначение
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаШкалаРасчета КАК ТаблицаШкалаРасчета
	|		ПО ТаблицаШкалаРасчета.ДиапазонОт МЕЖДУ &УсловиеИтогДоПорции И &УсловиеИтогПослеПорции
	|			И ТаблицаШкалаРасчета.ДиапазонОт > 0
	|			И &ПоляСоединенияПоТоварам";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		ТаблицаШкалаРасчета = "ВТ_ШкалаРасчета";
		УсловиеИтогДоПорции = "ТаблицаИсточник.СуммаДоПорции + ТаблицаИсточник.Сумма - ТаблицаИсточник.СуммаПорции";
		УсловиеИтогПослеПорции = " ТаблицаИсточник.СуммаДоПорции + ТаблицаИсточник.Сумма";
		
	Иначе
		
		ТаблицаШкалаРасчета = "ВТ_ТоварыДиапазоны";
		УсловиеИтогДоПорции = "ТаблицаИсточник.КоличествоДоПорции + ТаблицаИсточник.Количество - ТаблицаИсточник.КоличествоПорции";
		УсловиеИтогПослеПорции = " ТаблицаИсточник.КоличествоДоПорции + ТаблицаИсточник.Количество";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИтогДоПорции", УсловиеИтогДоПорции);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеИтогПослеПорции", УсловиеИтогПослеПорции);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаШкалаРасчета", ТаблицаШкалаРасчета);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_ТаблицаНазначение", ТаблицаНазначение);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ТаблицаШкалаРасчета");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПредыдущаяПорцияДоПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПорцияПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПорцияПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПорцияПревышенияЛимита.Организация КАК Организация,
	|	ВТ_ПорцияПревышенияЛимита.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПорцияПревышенияЛимита.ПриоритетПорция КАК ПриоритетПорция,
	|	ВТ_ПорцияПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПорцияПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_ОборотыПоПорциямНарастающийИтог.ПриоритетПорция), -1) КАК ПриоритетПредыдущаяПорция
	|ПОМЕСТИТЬ ВТ_ПредыдущаяПорцияДоПревышенияЛимита
	|ИЗ
	|	ВТ_ПорцияПревышенияЛимита КАК ВТ_ПорцияПревышенияЛимита
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПорциямНарастающийИтог КАК ВТ_ОборотыПоПорциямНарастающийИтог
	|		ПО ВТ_ПорцияПревышенияЛимита.ПериодРасчета = ВТ_ОборотыПоПорциямНарастающийИтог.ПериодРасчета
	|			И ВТ_ПорцияПревышенияЛимита.УсловиеРетроБонуса = ВТ_ОборотыПоПорциямНарастающийИтог.УсловиеРетроБонуса
	|			И ВТ_ПорцияПревышенияЛимита.Организация = ВТ_ОборотыПоПорциямНарастающийИтог.Организация
	|			И ВТ_ПорцияПревышенияЛимита.Валюта = ВТ_ОборотыПоПорциямНарастающийИтог.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПорцияПревышенияЛимита.ПриоритетПорция > ВТ_ОборотыПоПорциямНарастающийИтог.ПриоритетПорция
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПорцияПревышенияЛимита.ПериодРасчета,
	|	ВТ_ПорцияПревышенияЛимита.УсловиеРетроБонуса,
	|	ВТ_ПорцияПревышенияЛимита.Организация,
	|	ВТ_ПорцияПревышенияЛимита.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ПорцияПревышенияЛимита.ПриоритетПорция,
	|	ВТ_ПорцияПревышенияЛимита.СуммаПлан,
	|	ВТ_ПорцияПревышенияЛимита.КоличествоПлан";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПорцияПревышенияЛимита", "ВТ_ОборотыПоПорциямНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПорцияПревышенияЛимита", "ВТ_ОборотыПоПорциямНарастающийИтог");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыДоПорцииПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.Организация КАК Организация,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.ПриоритетПорция КАК ПриоритетПорция,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	0 КАК КоличествоДоСубпорции,
	|	0 КАК СуммаДоСубпорции,
	|	ЕСТЬNULL(ВТ_ОборотыПоПорциямНарастающийИтог.Сумма, 0) КАК СуммаДоПорции,
	|	ЕСТЬNULL(ВТ_ОборотыПоПорциямНарастающийИтог.Количество, 0) КАК КоличествоДоПорции
	|ПОМЕСТИТЬ ВТ_ОборотыДоПорцииПревышенияЛимита
	|ИЗ
	|	ВТ_ПредыдущаяПорцияДоПревышенияЛимита КАК ВТ_ПредыдущаяПорцияДоПревышенияЛимита
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПорциямНарастающийИтог КАК ВТ_ОборотыПоПорциямНарастающийИтог
	|		ПО ВТ_ПредыдущаяПорцияДоПревышенияЛимита.ПериодРасчета = ВТ_ОборотыПоПорциямНарастающийИтог.ПериодРасчета
	|			И ВТ_ПредыдущаяПорцияДоПревышенияЛимита.УсловиеРетроБонуса = ВТ_ОборотыПоПорциямНарастающийИтог.УсловиеРетроБонуса
	|			И ВТ_ПредыдущаяПорцияДоПревышенияЛимита.Организация = ВТ_ОборотыПоПорциямНарастающийИтог.Организация
	|			И ВТ_ПредыдущаяПорцияДоПревышенияЛимита.Валюта = ВТ_ОборотыПоПорциямНарастающийИтог.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПредыдущаяПорцияДоПревышенияЛимита.ПриоритетПредыдущаяПорция = ВТ_ОборотыПоПорциямНарастающийИтог.ПриоритетПорция";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПредыдущаяПорцияДоПревышенияЛимита", "ВТ_ОборотыПоПорциямНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПредыдущаяПорцияДоПревышенияЛимита", "ВТ_ОборотыПоПорциямНарастающийИтог");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПорцияНарастающегоИтога", ПорцияНарастающегоИтога());
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаСубпорцияПревышенияЛимитаОборотыДоПорции(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_СубпорцияПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_СубпорцияПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_СубпорцияПревышенияЛимита.Организация КАК Организация,
	|	ВТ_СубпорцияПревышенияЛимита.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_СубпорцияПревышенияЛимита.ПриоритетПорция КАК ПриоритетПорция,
	|	ВЫРАЗИТЬ((ВТ_СубпорцияПревышенияЛимита.ПриоритетПорция + 1) / &ПорцияНарастающегоИтога - 0.5 КАК ЧИСЛО(15, 0))
	|		* &ПорцияНарастающегоИтога КАК ПриоритетРодительскаяПорция,
	|	ВТ_СубпорцияПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_СубпорцияПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ЕСТЬNULL(ВТ_ОборотыПоСубпорциямНарастающийИтог.СуммаДоПорции, 0) КАК СуммаДоПорции,
	|	ЕСТЬNULL(ВТ_ОборотыПоСубпорциямНарастающийИтог.КоличествоДоПорции, 0) КАК КоличествоДоПорции
	|ПОМЕСТИТЬ ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции
	|ИЗ
	|	ВТ_СубпорцияПревышенияЛимита КАК ВТ_СубпорцияПревышенияЛимита
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоСубпорциямНарастающийИтог КАК ВТ_ОборотыПоСубпорциямНарастающийИтог
	|		ПО ВТ_СубпорцияПревышенияЛимита.ПериодРасчета = ВТ_ОборотыПоСубпорциямНарастающийИтог.ПериодРасчета
	|			И ВТ_СубпорцияПревышенияЛимита.УсловиеРетроБонуса = ВТ_ОборотыПоСубпорциямНарастающийИтог.УсловиеРетроБонуса
	|			И ВТ_СубпорцияПревышенияЛимита.Организация = ВТ_ОборотыПоСубпорциямНарастающийИтог.Организация
	|			И ВТ_СубпорцияПревышенияЛимита.Валюта = ВТ_ОборотыПоСубпорциямНарастающийИтог.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_СубпорцияПревышенияЛимита.ПриоритетПорция = ВТ_ОборотыПоСубпорциямНарастающийИтог.ПриоритетПорция";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПорцияНарастающегоИтога", ПорцияНарастающегоИтога());
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СубпорцияПревышенияЛимита", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СубпорцияПревышенияЛимита", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПредыдущаяСубпорцияДоПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Организация КАК Организация,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПриоритетПорция КАК ПриоритетПорция,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.СуммаПлан КАК СуммаПлан,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.СуммаДоПорции КАК СуммаДоПорции,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.КоличествоДоПорции КАК КоличествоДоПорции,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_ОборотыПоСубпорциямНарастающийИтог.ПриоритетПорция), -1) КАК ПриоритетПредыдущаяСубпорция
	|ПОМЕСТИТЬ ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита
	|ИЗ
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции КАК ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоСубпорциямНарастающийИтог КАК ВТ_ОборотыПоСубпорциямНарастающийИтог
	|		ПО ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПериодРасчета = ВТ_ОборотыПоСубпорциямНарастающийИтог.ПериодРасчета
	|			И ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.УсловиеРетроБонуса = ВТ_ОборотыПоСубпорциямНарастающийИтог.УсловиеРетроБонуса
	|			И ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Организация = ВТ_ОборотыПоСубпорциямНарастающийИтог.Организация
	|			И ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Валюта = ВТ_ОборотыПоСубпорциямНарастающийИтог.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПриоритетПорция > ВТ_ОборотыПоСубпорциямНарастающийИтог.ПриоритетПорция
	|			И ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПриоритетРодительскаяПорция <= ВТ_ОборотыПоСубпорциямНарастающийИтог.ПриоритетПорция
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПериодРасчета,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.УсловиеРетроБонуса,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Организация,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.ПриоритетПорция,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.СуммаПлан,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.КоличествоПлан,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.СуммаДоПорции,
	|	ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции.КоличествоДоПорции";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СубпорцияПревышенияЛимитаОборотыДоПорции", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаСубпорцияПревышенияЛимитаОборотыДоСубпорции(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.Организация КАК Организация,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.ПриоритетПорция КАК ПриоритетПорция,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.СуммаДоПорции КАК СуммаДоПорции,
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.КоличествоДоПорции КАК КоличествоДоПорции,
	|	ЕСТЬNULL(ВТ_ОборотыПоСубпорциямНарастающийИтог.Сумма, 0) КАК СуммаДоСубпорции,
	|	ЕСТЬNULL(ВТ_ОборотыПоСубпорциямНарастающийИтог.Количество, 0) КАК КоличествоДоСубпорции
	|ПОМЕСТИТЬ ВТ_СубпорцияПревышенияЛимитаОборотыДоСубпорции
	|ИЗ
	|	ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита КАК ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоСубпорциямНарастающийИтог КАК ВТ_ОборотыПоСубпорциямНарастающийИтог
	|		ПО ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.ПериодРасчета = ВТ_ОборотыПоСубпорциямНарастающийИтог.ПериодРасчета
	|			И ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.УсловиеРетроБонуса = ВТ_ОборотыПоСубпорциямНарастающийИтог.УсловиеРетроБонуса
	|			И ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.Организация = ВТ_ОборотыПоСубпорциямНарастающийИтог.Организация
	|			И ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.Валюта = ВТ_ОборотыПоСубпорциямНарастающийИтог.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита.ПриоритетПредыдущаяСубпорция = ВТ_ОборотыПоСубпорциямНарастающийИтог.ПриоритетПорция";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПредыдущаяСубпорцияДоПревышенияЛимита", "ВТ_ОборотыПоСубпорциямНарастающийИтог");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СубпорцияНарастающегоИтога", СубпорцияНарастающегоИтога());
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//  ТаблицаИсточник - Строка
//  ТаблицаНазначение - Строка
//  Порция - Строка
//
Процедура ДобавитьТекстЗапросаОборотыЗаПорциюПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов, ТаблицаИсточник, ТаблицаНазначение, Порция) Экспорт
	
	//@skip-check bsl-ql-hub - выбранные поля сочетаются с группировкой путем замены текста запроса
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	&ПоляТоваров,
	|	ВТ_ПодготовленныеДанные.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ТаблицаИсточник.ПриоритетПорция КАК ПриоритетПорция,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.КоличествоПакетов КАК КоличествоПакетов,
	|	ВТ_ПодготовленныеДанные.КоличествоПланСУчетомКратностиПродаж КАК КоличествоПланСУчетомКратностиПродаж,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаИсточник.КоличествоДоПорции КАК КоличествоДоПорции,
	|	ТаблицаИсточник.КоличествоДоСубпорции КАК КоличествоДоСубпорции,
	|	ВТ_ПодготовленныеДанные.Количество КАК Количество,
	|	ТаблицаИсточник.СуммаДоПорции КАК СуммаДоПорции,
	|	ТаблицаИсточник.СуммаДоСубпорции КАК СуммаДоСубпорции,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК Сумма
	|ПОМЕСТИТЬ ВТ_ТаблицаНазначение
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ТаблицаИсточник.ПериодРасчета = ВТ_ПодготовленныеДанные.ПериодРасчета
	|			И ТаблицаИсточник.УсловиеРетроБонуса = ВТ_ПодготовленныеДанные.УсловиеРетроБонуса
	|			И ТаблицаИсточник.Организация = ВТ_ПодготовленныеДанные.Организация
	|			И ТаблицаИсточник.Валюта = ВТ_ПодготовленныеДанные.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ТаблицаИсточник.ПриоритетПорция <= ВТ_ПодготовленныеДанные.Приоритет
	|			И ТаблицаИсточник.ПриоритетПорция + &ПорцияНарастающегоИтога > ВТ_ПодготовленныеДанные.Приоритет";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ПодготовленныеДанные");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ПодготовленныеДанные");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВТ_ТаблицаНазначение", ТаблицаНазначение);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПорцияНарастающегоИтога", Порция);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоСубпорциям(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	//@skip-check bsl-ql-hub - поля группировки и индексирования уточняются путем замены текста запроса
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.Приоритет / &СубпорцияНарастающегоИтога - 0.5 КАК ЧИСЛО(15, 0))
	|		* &СубпорцияНарастающегоИтога КАК ПриоритетПорция,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.ПриоритетПорция КАК ПриоритетРодительскаяПорция,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.КоличествоДоПорции КАК КоличествоДоПорции,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.СуммаДоПорции КАК СуммаДоПорции,
	|	СУММА(ВТ_ПодготовленныеДанные.Количество) КАК Количество,
	|	СУММА(ВТ_ПодготовленныеДанные.РасчетнаяБаза) КАК Сумма,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ВТ_ПодготовленныеДанные.РасчетнаяБаза > 0
	|				ТОГДА ВТ_ПодготовленныеДанные.РасчетнаяБаза
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК СуммаПриход,
	|	СУММА(
	|		ВЫБОР
	|			КОГДА ВТ_ПодготовленныеДанные.Количество > 0
	|				ТОГДА ВТ_ПодготовленныеДанные.Количество
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК КоличествоПриход
	|ПОМЕСТИТЬ ВТ_ОборотыПоСубпорциям
	|ИЗ
	|	ВТ_ОборотыДоПорцииПревышенияЛимита КАК ВТ_ОборотыДоПорцииПревышенияЛимита
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ВТ_ОборотыДоПорцииПревышенияЛимита.ПериодРасчета = ВТ_ПодготовленныеДанные.ПериодРасчета
	|			И ВТ_ОборотыДоПорцииПревышенияЛимита.УсловиеРетроБонуса = ВТ_ПодготовленныеДанные.УсловиеРетроБонуса
	|			И ВТ_ОборотыДоПорцииПревышенияЛимита.Организация = ВТ_ПодготовленныеДанные.Организация
	|			И ВТ_ОборотыДоПорцииПревышенияЛимита.Валюта = ВТ_ПодготовленныеДанные.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ОборотыДоПорцииПревышенияЛимита.ПриоритетПорция <= ВТ_ПодготовленныеДанные.Приоритет
	|			И ВТ_ОборотыДоПорцииПревышенияЛимита.ПриоритетПорция + &ПорцияНарастающегоИтога > ВТ_ПодготовленныеДанные.Приоритет
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПодготовленныеДанные.ПериодРасчета,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.Организация,
	|	ВТ_ПодготовленныеДанные.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.Приоритет / &СубпорцияНарастающегоИтога - 0.5 КАК ЧИСЛО(15, 0)) * &СубпорцияНарастающегоИтога,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.ПриоритетПорция,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.СуммаПлан,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.КоличествоПлан,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.КоличествоДоПорции,
	|	ВТ_ОборотыДоПорцииПревышенияЛимита.СуммаДоПорции";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыДоПорцииПревышенияЛимита", "ВТ_ПодготовленныеДанные");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыДоПорцииПревышенияЛимита", "ВТ_ПодготовленныеДанные");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПорцияНарастающегоИтога", ПорцияНарастающегоИтога());
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СубпорцияНарастающегоИтога", СубпорцияНарастающегоИтога());
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПревышениеЛимитаНарастающийИтог(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Организация КАК Организация,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ДокументРегистратор КАК ДокументРегистратор,
	|	&ПоляТоваров,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.БонусПроцент КАК БонусПроцент,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Приоритет КАК Приоритет,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПакетов КАК КоличествоПакетов,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПланСУчетомКратностиПродаж КАК КоличествоПланСУчетомКратностиПродаж,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Сумма КАК СуммаСтроки,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Количество КАК КоличествоСтроки,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоДоПорции + ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоДоСубпорции
	|		+ СУММА(ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Количество) КАК Количество,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаДоПорции + ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаДоСубпорции
	|		+ СУММА(ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ПревышениеЛимитаНарастающийИтог
	|ИЗ
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита КАК ВТ_ОборотыЗаСубпорциюПревышенияЛимита
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОборотыЗаСубпорциюПревышенияЛимита КАК ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало
	|		ПО ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ПериодРасчета = ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.ПериодРасчета
	|			И ВТ_ОборотыЗаСубпорциюПревышенияЛимита.УсловиеРетроБонуса = ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.УсловиеРетроБонуса
	|			И ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Организация = ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Организация
	|			И ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Валюта = ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Приоритет >= ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Приоритет
	|			И ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ПриоритетПорция <= ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало.Приоритет
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ПериодРасчета,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.УсловиеРетроБонуса,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Организация,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.ДокументРегистратор,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаПлан,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПлан,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПакетов,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоПланСУчетомКратностиПродаж,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Сумма,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Количество,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.БонусПроцент,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.Приоритет,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаДоПорции,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.СуммаДоСубпорции,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоДоПорции,
	|	ВТ_ОборотыЗаСубпорциюПревышенияЛимита.КоличествоДоСубпорции";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыЗаСубпорциюПревышенияЛимита", "ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыЗаСубпорциюПревышенияЛимита", "ВТ_ОборотыЗаСубпорциюПревышенияЛимитаЗеркало");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СубпорцияНарастающегоИтога", СубпорцияНарастающегоИтога());
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	&ПоляПериодыТоваров,
	|	СУММА(ВТ_ПроцентСкидки.Количество) КАК Количество,
	|	СУММА(&ВыражениеСуммаБезУточнения) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодам
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПериодРасчета,
	|	ВТ_ПроцентСкидки.Организация,
	|	ВТ_ПроцентСкидки.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	&ПоляГруппировкиПоПериодамТоваров";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ПоказательТоваров = ПараметрыУсловийРетроБонусов.ПоказательТоваров;
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	ИмяТаблицы = "ВТ_ПроцентСкидки";
	
	Если ПоказательТоваров = ВидыПоказателей.Количество Тогда
		
		ПоляПериодыТоваров = СтрШаблон(
			"%1.ДатаНачала КАК ДатаНачала,
			|%1.ДатаОкончания КАК ДатаОкончания",
			ИмяТаблицы);
		
		ПоляГруппировкиПоПериодамТоваров = СтрШаблон(
			"%1.ДатаНачала,
			|%1.ДатаОкончания",
			ИмяТаблицы);
		
	Иначе
		
		ПоляПериодыТоваров = "NULL КАК ДатаНачала";
		ПоляГруппировкиПоПериодамТоваров = "NULL";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляПериодыТоваров", ПоляПериодыТоваров);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляГруппировкиПоПериодамТоваров", ПоляГруппировкиПоПериодамТоваров);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамУсловияПакетаНеВыполнены(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_УчастникиПериодыДокументыВсеТовары.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.Организация КАК Организация,
	|	ВТ_УчастникиПериодыДокументыВсеТовары.Валюта КАК Валюта,
	|	&ПоляУчастники
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены
	|ИЗ
	|	ВТ_УчастникиПериодыДокументыВсеТовары КАК ВТ_УчастникиПериодыДокументыВсеТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ПО ВТ_УчастникиПериодыДокументыВсеТовары.УсловиеРетроБонуса = ВТ_ОборотыПоПериодам.УсловиеРетроБонуса
	|			И ВТ_УчастникиПериодыДокументыВсеТовары.ПериодРасчета = ВТ_ОборотыПоПериодам.ПериодРасчета
	|			И ВТ_УчастникиПериодыДокументыВсеТовары.Организация = ВТ_ОборотыПоПериодам.Организация
	|			И ВТ_УчастникиПериодыДокументыВсеТовары.Валюта = ВТ_ОборотыПоПериодам.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|ГДЕ
	|	ВТ_УчастникиПериодыДокументыВсеТовары.КоличествоПлан > ЕСТЬNULL(ВТ_ОборотыПоПериодам.Количество, 0)";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_УчастникиПериодыДокументыВсеТовары", "ВТ_ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_УчастникиПериодыДокументыВсеТовары", "ВТ_ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамУсловияПакетаВыполнены(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	СУММА(ВТ_ОборотыПоПериодам.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамУсловияПакетаВыполнены
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены
	|	ПО ВТ_ОборотыПоПериодам.УсловиеРетроБонуса = ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены.УсловиеРетроБонуса
	|		И ВТ_ОборотыПоПериодам.ПериодРасчета = ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены.ПериодРасчета
	|		И ВТ_ОборотыПоПериодам.Организация = ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены.Организация
	|		И ВТ_ОборотыПоПериодам.Валюта = ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены.Валюта
	|		И &ПоляСоединенияПоУчастникам
	|ГДЕ
	|	ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены.УсловиеРетроБонуса ЕСТЬ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Организация,
	|	ВТ_ОборотыПоПериодам.Валюта,
	|	&ПоляГруппировкиПоУчастникам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам", "ВТ_ОборотыПоПериодамУсловияПакетаНеВыполнены");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамОпределениеПланаПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_ШкалаРасчета.ДиапазонОт), 0) КАК СуммаПлан
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамОпределениеПлана
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ОборотыПоПериодам.Сумма >= ВТ_ШкалаРасчета.ДиапазонОт
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	ВТ_ОборотыПоПериодам.Сумма";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамПроцентБонусаПоДиапазонамПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.НомерДиапазона, 0) КАК НомерДиапазона,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.БонусПроцент, 0) КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамПроцентБонусаПоДиапазонам
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ОборотыПоПериодам.Сумма > ВТ_ШкалаРасчета.ДиапазонОт";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамПроцентБонусаПоДиапазонамПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ВТ_ОборотыПоПериодам.Количество КАК Количество,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БазоваяЦена, 0) КАК БазоваяЦена,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.НомерДиапазона, 0) КАК НомерДиапазона,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БонусПроцент, 0) КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамПроцентБонусаПоДиапазонам
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
	|		ПО ВТ_ОборотыПоПериодам.Количество > ВТ_ТоварыДиапазоны.ДиапазонОт
	|			И &ПоляСоединенияПоТоварам
	|ГДЕ
	|	ВТ_ОборотыПоПериодам.Количество > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ВТ_ОборотыПоПериодам.Количество КАК Количество,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БазоваяЦена, 0) КАК БазоваяЦена,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.НомерДиапазона, 0) КАК НомерДиапазона,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БонусПроцент, 0) КАК БонусПроцент
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
	|		ПО ВТ_ТоварыДиапазоны.ДиапазонОт = 0
	|			И &ПоляСоединенияПоТоварам
	|ГДЕ
	|	ВТ_ОборотыПоПериодам.Количество <= 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам", "ВТ_ТоварыДиапазоны");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамПроцентБонусаПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.НомерДиапазона, 0) КАК НомерДиапазона,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.БонусПроцент, 0) КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамПроцентБонусаПоДиапазонам
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ОборотыПоПериодам.Сумма > ВТ_ШкалаРасчета.ДиапазонОт
	|ГДЕ
	|	ВТ_ОборотыПоПериодам.Сумма > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодам.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыПоПериодам.Сумма КАК Сумма,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.НомерДиапазона, 0) КАК НомерДиапазона,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.БонусПроцент, 0) КАК БонусПроцент
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ШкалаРасчета.ДиапазонОт = 0
	|ГДЕ
	|	ВТ_ОборотыПоПериодам.Сумма <= 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОборотыПоПериодамПроцентБонусаПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодамОпределениеПлана.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ОборотыПоПериодамОпределениеПлана.Организация КАК Организация,
	|	ВТ_ОборотыПоПериодамОпределениеПлана.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОборотыПоПериодамОпределениеПлана.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_ОборотыПоПериодамОпределениеПлана.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ВТ_ОборотыПоПериодамОпределениеПлана.СуммаПлан <> 0
	|			ТОГДА ВТ_ОборотыПоПериодамОпределениеПлана.СуммаПлан
	|		ИНАЧЕ ЕСТЬNULL(ВТ_ШкалаРасчетаМинимальныйПлан.ДиапазонОт, 0)
	|	КОНЕЦ КАК СуммаПлан,
	|	ЕСТЬNULL(ВТ_ШкалаРасчета.БонусПроцент, ЕСТЬNULL(ВТ_ШкалаРасчетаМинимальныйПлан.БонусПроцент, 0)) КАК БонусПроцент
	|ПОМЕСТИТЬ ВТ_ОборотыПоПериодамПроцентБонуса
	|ИЗ
	|	ВТ_ОборотыПоПериодамОпределениеПлана КАК ВТ_ОборотыПоПериодамОпределениеПлана
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ОборотыПоПериодамОпределениеПлана.СуммаПлан = ВТ_ШкалаРасчета.ДиапазонОт
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчетаМинимальныйПлан КАК ВТ_ШкалаРасчетаМинимальныйПлан
	|		ПО ИСТИНА";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ОборотыПоПериодамОпределениеПлана");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	&ВыражениеПриоритет КАК Приоритет,
	|	&ВыражениеВалютаРасчета КАК Валюта,
	|	&ВыражениеКоличествоИсходное КАК Количество,
	|	0 КАК КоличествоПакетов,
	|	&ВыражениеСуммаПродажи КАК СуммаПродажи,
	|	&ВыражениеСуммаОборотСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеБонусПроцент КАК БонусПроцент,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза,
	|	ВТ_ПроцентСкидки.БазоваяЦена КАК БазоваяЦена,
	|	&ВыражениеСуммаПлан КАК СуммаПлан,
	|	&ВыражениеДиапазонОт КАК ДиапазонОт,
	|	&ВыражениеДиапазонПо КАК ДиапазонПо,
	|	&ВыражениеНомерДиапазона КАК НомерДиапазона,
	|	0 КАК КоличествоПлан,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) КАК СуммаПроверки,
	|	ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) КАК РасчетнаяБазаВсего,
	|	0 КАК КоличествоПроверки,
	|	&ВыражениеПланВыполнен КАК ПланВыполнен
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ &ТаблицаОборотыПоПериодам КАК ОборотыПоПериодам
	|		ПО ВТ_ПроцентСкидки.УсловиеРетроБонуса = ОборотыПоПериодам.УсловиеРетроБонуса
	|			И ВТ_ПроцентСкидки.Организация = ОборотыПоПериодам.Организация
	|			И ВТ_ПроцентСкидки.ПериодРасчета = ОборотыПоПериодам.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|	И &ТаблицаПревышениеЛимита";
	
	Если ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	 ИЛИ ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам
	 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
		
		ТаблицаПревышениеЛимита =
		"ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
		|		ПО ВТ_ПроцентСкидки.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
		|			И ВТ_ПроцентСкидки.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
		
		ВыражениеПриоритет = "ВТ_ПревышениеЛимитаОбщая.Приоритет";
		
	Иначе
		
		ТаблицаПревышениеЛимита = "";
		ВыражениеПриоритет = "0";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТаблицаПревышениеЛимита", ТаблицаПревышениеЛимита);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПриоритет", ВыражениеПриоритет);
	
	Если ПараметрыУсловийРетроБонусов.ПрогрессивнаяШкалаРасчета Тогда
		
		ТаблицаОборотыПоПериодам = "ВТ_ОборотыПоПериодамПроцентБонуса";
		ВыражениеБонусПроцент = "ЕСТЬNULL(ОборотыПоПериодам.БонусПроцент, 0)";
		ВыражениеСуммаПлан = "ЕСТЬNULL(ОборотыПоПериодам.СуммаПлан, 0)";
		ВыражениеДиапазонОт = "ЕСТЬNULL(ОборотыПоПериодам.СуммаПлан, 0)";
		ВыражениеДиапазонПо = "0";
		ВыражениеНомерДиапазона = "0";
		ВыражениеКоличествоИсходное = "ВТ_ПроцентСкидки.Количество";
		ВыражениеСуммаПродажи = "&ВыражениеСуммаБезУточнения";
		ВыражениеСуммаОборотСНДС = "&ВыражениеСуммаСНДС";
		ВыражениеСуммаОборотБезНДС = "&ВыражениеСуммаБезНДС";
		ВыражениеПланВыполнен = "(ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) >= ЕСТЬNULL(ОборотыПоПериодам.СуммаПлан, 0))";
	
	ИначеЕсли ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам
	        И ПараметрыУсловийРетроБонусов.ПропорциональноеРаспределение Тогда
		
		ТаблицаОборотыПоПериодам = "ВТ_ОборотыПоПериодамПроцентБонусаПоДиапазонам";
		ВыражениеБонусПроцент = "ЕСТЬNULL(ОборотыПоПериодам.БонусПроцент, 0)";
		ВыражениеДиапазонОт = "ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0)";
		ВыражениеДиапазонПо = "ЕСТЬNULL(ОборотыПоПериодам.ДиапазонПо, 0)";
		ВыражениеНомерДиапазона = "ЕСТЬNULL(ОборотыПоПериодам.НомерДиапазона, 0)";
		
		Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.Сумма Тогда
			
			ВыражениеСуммаПлан =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.ДиапазонПо, 0) = 0
			|		ТОГДА ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) - ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0)
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) <= ЕСТЬNULL(ОборотыПоПериодам.ДиапазонПо, 0)
			|		ТОГДА ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) - ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0)
			|	ИНАЧЕ ЕСТЬNULL(ОборотыПоПериодам.ДиапазонПо, 0) - ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0)
			|КОНЕЦ";
			
			ВыражениеКоличествоИсходное =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) = 0
			|		ТОГДА ВТ_ПроцентСкидки.Количество
			|	ИНАЧЕ 0
			|КОНЕЦ";
			
			ВыражениеСуммаПродажи =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) = 0
			|		ТОГДА &ВыражениеСуммаБезУточнения
			|	ИНАЧЕ 0
			|КОНЕЦ";
			ВыражениеСуммаОборотСНДС =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) = 0
			|		ТОГДА &ВыражениеСуммаСНДС
			|	ИНАЧЕ 0
			|КОНЕЦ";
			ВыражениеСуммаОборотБезНДС =
			"ВЫБОР
			|	КОГДА ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) = 0
			|		ТОГДА &ВыражениеСуммаБезНДС
			|	ИНАЧЕ 0
			|КОНЕЦ";
			
			ВыражениеПланВыполнен = "(ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) >= ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0))";
			
		Иначе
			
			ВыражениеКоличествоИсходное = "ВТ_ПроцентСкидки.Количество";
			ВыражениеСуммаПлан = "ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0)";
			ВыражениеСуммаПродажи = "&ВыражениеСуммаБезУточнения";
			ВыражениеСуммаОборотСНДС = "&ВыражениеСуммаСНДС";
			ВыражениеСуммаОборотБезНДС = "&ВыражениеСуммаБезНДС";
			ВыражениеПланВыполнен = "(ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) >= ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0))";
			
		КонецЕсли;
		
	Иначе
		
		ТаблицаОборотыПоПериодам = "ВТ_ОборотыПоПериодам";
		ВыражениеБонусПроцент = "ВТ_ПроцентСкидки.БонусПроцент";
		ВыражениеСуммаПлан = "ВТ_ПроцентСкидки.СуммаПлан";
		ВыражениеДиапазонОт = "ВТ_ПроцентСкидки.СуммаПлан";
		ВыражениеДиапазонПо = "0";
		ВыражениеНомерДиапазона = "0";
		ВыражениеКоличествоИсходное = "ВТ_ПроцентСкидки.Количество";
		ВыражениеСуммаПродажи = "&ВыражениеСуммаБезУточнения";
		ВыражениеСуммаОборотСНДС = "&ВыражениеСуммаСНДС";
		ВыражениеСуммаОборотБезНДС = "&ВыражениеСуммаБезНДС";
		ВыражениеПланВыполнен = "(ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) >= ВТ_ПроцентСкидки.СуммаПлан)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаОборотыПоПериодам", ТаблицаОборотыПоПериодам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусПроцент", ВыражениеБонусПроцент);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПлан", ВыражениеСуммаПлан);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДиапазонОт", ВыражениеДиапазонОт);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеДиапазонПо", ВыражениеДиапазонПо);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеНомерДиапазона", ВыражениеНомерДиапазона);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПлан", ВыражениеСуммаПлан);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоИсходное", ВыражениеКоличествоИсходное);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПродажи", ВыражениеСуммаПродажи);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаОборотСНДС", ВыражениеСуммаОборотСНДС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаОборотБезНДС", ВыражениеСуммаОборотБезНДС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПланВыполнен", ВыражениеПланВыполнен);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьВыражениеВалютаРасчета(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеОпределениеПлана(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	ВТ_ПроцентСкидки.Сумма КАК Сумма,
	|	ВТ_ПроцентСкидки.СуммаРегл КАК СуммаРегл,
	|	ВТ_ПроцентСкидки.СуммаВВалютеВзаиморасчетов КАК СуммаВВалютеВзаиморасчетов,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрСНДС КАК СуммаОборотУпрСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглСНДС КАК СуммаОборотРеглСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовСНДС КАК СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрБезНДС КАК СуммаОборотУпрБезНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглБезНДС КАК СуммаОборотРеглБезНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовБезНДС КАК СуммаОборотВВалютеВзаиморасчетовБезНДС,
	|	ВТ_ПроцентСкидки.ДатаНачала КАК ДатаНачала,
	|	ВТ_ПроцентСкидки.ДатаОкончания КАК ДатаОкончания,
	|	ЕСТЬNULL(ВТ_ОборотыПоПериодам.Количество, 0) КАК КоличествоПроверки,
	|	ЕСТЬNULL(ВТ_ОборотыПоПериодам.Сумма, 0) КАК СуммаПродажиВсего,
	|	ЕСТЬNULL(МАКСИМУМ(ВТ_Товары.КоличествоПлан), 0) КАК КоличествоПлан
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанныеОпределениеПлана
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ПО ВТ_ПроцентСкидки.УсловиеРетроБонуса = ВТ_ОборотыПоПериодам.УсловиеРетроБонуса
	|			И ВТ_ПроцентСкидки.ПериодРасчета = ВТ_ОборотыПоПериодам.ПериодРасчета
	|			И ВТ_ПроцентСкидки.Организация = ВТ_ОборотыПоПериодам.Организация
	|			И ВТ_ПроцентСкидки.Валюта = ВТ_ОборотыПоПериодам.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПроцентСкидки.ДатаНачала = ВТ_ОборотыПоПериодам.ДатаНачала
	|			И ВТ_ПроцентСкидки.ДатаОкончания = ВТ_ОборотыПоПериодам.ДатаОкончания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО &УсловияСоединенияТовары
	|ГДЕ
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок ЕСТЬ НЕ NULL
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.Период,
	|	ВТ_ПроцентСкидки.ДокументРегистратор,
	|	ВТ_ПроцентСкидки.Организация,
	|	&ПоляГруппировкиПоУчастникам,
	|	ВТ_ПроцентСкидки.Договор,
	|	ВТ_ПроцентСкидки.Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.Номенклатура,
	|	ВТ_ПроцентСкидки.Характеристика,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.НомерСтроки,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.Количество,
	|	ВТ_ПроцентСкидки.Сумма,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотУпрБезНДС,
	|	ВТ_ПроцентСкидки.СуммаПлан,
	|	ВТ_ПроцентСкидки.ДатаНачала,
	|	ВТ_ПроцентСкидки.ДатаОкончания,
	|	ВТ_ОборотыПоПериодам.Количество,
	|	ВТ_ОборотыПоПериодам.Сумма,
	|	ВТ_ПроцентСкидки.ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ПериодРасчета,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.БазаРасчета,
	|	ВТ_ПроцентСкидки.Валюта,
	|	ВТ_ПроцентСкидки.СуммаРегл,
	|	ВТ_ПроцентСкидки.СуммаВВалютеВзаиморасчетов,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовСНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотРеглБезНДС,
	|	ВТ_ПроцентСкидки.СуммаОборотВВалютеВзаиморасчетовБезНДС";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ВТ_ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ВТ_ОборотыПоПериодам");
	
	УсловияСоединенияТовары =
	"&ПоляСоединенияПоТоварам
	|	И ВТ_ПроцентСкидки.ДатаНачала = ВТ_Товары.ДатаНачала
	|	И ВТ_ПроцентСкидки.ДатаОкончания = ВТ_Товары.ДатаОкончания
	|	И ЕСТЬNULL(ВТ_ОборотыПоПериодам.Количество, 0) >= ВТ_Товары.КоличествоПлан";
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, УсловияСоединенияТовары, "ВТ_ПроцентСкидки", "ВТ_Товары", Истина);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияСоединенияТовары", УсловияСоединенияТовары);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Период КАК Период,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.БазаРасчета КАК БазаРасчета,
	|	&ВыражениеПриоритет КАК Приоритет,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.Количество КАК Количество,
	|	0 КАК КоличествоПакетов,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза,
	|	ЕСТЬNULL(ВТ_Товары.БонусПроцент, ВТ_ТоварыМинимальныеПараметрыПоПозиции.БонусПроцент) КАК БонусПроцент,
	|	ЕСТЬNULL(ВТ_Товары.БазоваяЦена, ВТ_ТоварыМинимальныеПараметрыПоПозиции.БазоваяЦена) КАК БазоваяЦена,
	|	0 КАК СуммаПлан,
	|	ЕСТЬNULL(ВТ_Товары.КоличествоПлан, ВТ_ТоварыМинимальныеПараметрыПоПозиции.КоличествоПлан) КАК КоличествоПлан,
	|	ЕСТЬNULL(ВТ_Товары.КоличествоПлан, ВТ_ТоварыМинимальныеПараметрыПоПозиции.КоличествоПлан) КАК КоличествоПланСУчетомКратностиПродаж,
	|	0 КАК ДиапазонОт,
	|	0 КАК ДиапазонПо,
	|	0 КАК НомерДиапазона,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.СуммаПродажиВсего КАК СуммаПроверки,
	|	ВТ_ПодготовленныеДанныеОпределениеПлана.КоличествоПроверки КАК КоличествоПроверки,
	|	(ВТ_ПодготовленныеДанныеОпределениеПлана.КоличествоПроверки
	|		>= ЕСТЬNULL(ВТ_Товары.КоличествоПлан, ВТ_ТоварыМинимальныеПараметрыПоПозиции.КоличествоПлан)) КАК ПланВыполнен
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПодготовленныеДанныеОпределениеПлана КАК ВТ_ПодготовленныеДанныеОпределениеПлана
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Товары КАК ВТ_Товары
	|		ПО ВТ_ПодготовленныеДанныеОпределениеПлана.УсловиеРетроБонуса = ВТ_Товары.УсловияРетроБонусов
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПодготовленныеДанныеОпределениеПлана.ДатаНачала = ВТ_Товары.ДатаНачала
	|			И ВТ_ПодготовленныеДанныеОпределениеПлана.ДатаОкончания = ВТ_Товары.ДатаОкончания
	|			И ВТ_ПодготовленныеДанныеОпределениеПлана.КоличествоПлан = ВТ_Товары.КоличествоПлан
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыМинимальныеПараметрыПоПозиции КАК ВТ_ТоварыМинимальныеПараметрыПоПозиции
	|			ПО &УсловияСоединенияМинимальныеПараметры
	|			И &ТаблицаПревышениеЛимита";
	
	Если ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
		
		ТаблицаПревышениеЛимита =
		"ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
		|		ПО ВТ_ПодготовленныеДанныеОпределениеПлана.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
		|			И ВТ_ПодготовленныеДанныеОпределениеПлана.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
		
		ВыражениеПриоритет = "ВТ_ПревышениеЛимитаОбщая.Приоритет";
		
	Иначе
		
		ТаблицаПревышениеЛимита = "";
		ВыражениеПриоритет = "0";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТаблицаПревышениеЛимита", ТаблицаПревышениеЛимита);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПриоритет", ВыражениеПриоритет);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанныеОпределениеПлана");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанныеОпределениеПлана", "ВТ_Товары", Истина);
	ПодставитьПолеВыбораСуммаБезУточнения(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанныеОпределениеПлана", "ВТ_Товары");
	ПодставитьПолеВыбораСуммаСНДС(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанныеОпределениеПлана", "ВТ_Товары");
	ПодставитьПолеВыбораСуммаБезНДС(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанныеОпределениеПлана", "ВТ_Товары");
	
	УсловияСоединенияМинимальныеПараметры =
	"&ПоляСоединенияПоТоварам
	|	И ВТ_ПодготовленныеДанныеОпределениеПлана.ДатаНачала = ВТ_ТоварыМинимальныеПараметрыПоПозиции.ДатаНачала
	|	И ВТ_ПодготовленныеДанныеОпределениеПлана.ДатаОкончания = ВТ_ТоварыМинимальныеПараметрыПоПозиции.ДатаОкончания";
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		УсловияСоединенияМинимальныеПараметры,
		"ВТ_ПодготовленныеДанныеОпределениеПлана",
		"ВТ_ТоварыМинимальныеПараметрыПоПозиции");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловияСоединенияМинимальныеПараметры", УсловияСоединенияМинимальныеПараметры);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоКоличествуПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ЕСТЬNULL(ВТ_ПревышениеЛимитаОбщая.Приоритет, -1) КАК Приоритет,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	0 КАК КоличествоПакетов,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза,
	|	ЕСТЬNULL(ОборотыПоПериодам.БонусПроцент, 0) КАК БонусПроцент,
	|	ЕСТЬNULL(ОборотыПоПериодам.БазоваяЦена, 0) КАК БазоваяЦена,
	|	0 КАК СуммаПлан,
	|	ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) КАК КоличествоПлан,
	|	ЕСТЬNULL(ОборотыПоПериодам.ДиапазонОт, 0) КАК ДиапазонОт,
	|	ЕСТЬNULL(ОборотыПоПериодам.ДиапазонПо, 0) КАК ДиапазонПо,
	|	ЕСТЬNULL(ОборотыПоПериодам.НомерДиапазона, -1) КАК НомерДиапазона,
	|	ЕСТЬNULL(ОборотыПоПериодам.Сумма, 0) КАК СуммаПроверки,
	|	ЕСТЬNULL(ОборотыПоПериодам.Количество, 0) КАК КоличествоПроверки
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодамПроцентБонусаПоДиапазонам КАК ОборотыПоПериодам
	|		ПО ВТ_ПроцентСкидки.УсловиеРетроБонуса = ОборотыПоПериодам.УсловиеРетроБонуса
	|			И ВТ_ПроцентСкидки.Организация = ОборотыПоПериодам.Организация
	|			И ВТ_ПроцентСкидки.Валюта = ОборотыПоПериодам.Валюта
	|			И ВТ_ПроцентСкидки.ПериодРасчета = ОборотыПоПериодам.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
	|		ПО ВТ_ПроцентСкидки.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
	|			И ВТ_ПроцентСкидки.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоКоличествуПоДиапазонамФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидки.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидки.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидки.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидки.БазаРасчета КАК БазаРасчета,
	|	ЕСТЬNULL(ВТ_ПревышениеЛимитаОбщая.Приоритет, -1) КАК Приоритет,
	|	ВТ_ПроцентСкидки.Валюта КАК Валюта,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	0 КАК КоличествоПакетов,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза,
	|	0 КАК БонусПроцент,
	|	0 КАК БазоваяЦена,
	|	0 КАК СуммаПлан,
	|	0 КАК КоличествоПлан,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	0 КАК СуммаПроверки,
	|	ЕСТЬNULL(ОборотыПоПериодам.Количество, 0) КАК КоличествоПроверки
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодам КАК ОборотыПоПериодам
	|		ПО ВТ_ПроцентСкидки.УсловиеРетроБонуса = ОборотыПоПериодам.УсловиеРетроБонуса
	|			И ВТ_ПроцентСкидки.Организация = ОборотыПоПериодам.Организация
	|			И ВТ_ПроцентСкидки.Валюта = ОборотыПоПериодам.Валюта
	|			И ВТ_ПроцентСкидки.ПериодРасчета = ОборотыПоПериодам.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
	|		ПО ВТ_ПроцентСкидки.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
	|			И ВТ_ПроцентСкидки.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки", "ОборотыПоПериодам");
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоКоличествуСовокупно(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Период КАК Период,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Договор КАК Договор,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Количество КАК Количество,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДатаНачала КАК ДатаНачала,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДатаОкончания КАК ДатаОкончания,
	|	ЕСТЬNULL(ВТ_ОборотыПоПериодамУсловияПакетаВыполнены.Сумма, 0) КАК СуммаПроверки,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.КоличествоПлан КАК КоличествоПлан,
	|	0 КАК КоличествоПроверки,
	|	0 КАК ДиапазонОт,
	|	0 КАК ДиапазонПо,
	|	0 КАК НомерДиапазона,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта КАК Валюта,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету КАК ВТ_ПроцентСкидкиСДаннымиПоПакету
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодамУсловияПакетаВыполнены КАК ВТ_ОборотыПоПериодамУсловияПакетаВыполнены
	|	ПО  ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса = ВТ_ОборотыПоПериодамУсловияПакетаВыполнены.УсловиеРетроБонуса
	|		И ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета = ВТ_ОборотыПоПериодамУсловияПакетаВыполнены.ПериодРасчета
	|		И ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация = ВТ_ОборотыПоПериодамУсловияПакетаВыполнены.Организация
	|		И ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта = ВТ_ОборотыПоПериодамУсловияПакетаВыполнены.Валюта
	|		И &ПоляСоединенияПоУчастникам
	|ГДЕ
	|	&УсловиеОтбораПустогоДокумента";
	
	Если ПараметрыВарианта.ДетализацияДоНоменклатуры Тогда
		УсловиеОтбораПустогоДокумента = "ИСТИНА";
	Иначе
		УсловиеОтбораПустогоДокумента = "ДокументРегистратор <> " + ЗначениеПустойСсылкиРегистратора(ПараметрыУсловийРетроБонусов);
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПустогоДокумента", УсловиеОтбораПустогоДокумента);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ПроцентСкидкиСДаннымиПоПакету",
		"ВТ_ОборотыПоПериодамУсловияПакетаВыполнены");
	
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПодготовленныеДанныеПоПакетномуПредложению(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Период КАК Период,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДокументРегистратор КАК ДокументРегистратор,
	|	&ВыражениеПриоритет КАК Приоритет,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Договор КАК Договор,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Количество КАК Количество,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта КАК Валюта,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДатаНачала КАК ДатаНачала,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.ДатаОкончания КАК ДатаОкончания,
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету.КоличествоПлан КАК КоличествоПлан,
	|	ЕСТЬNULL(ВТ_КоличествоПакетовТоваров.КоличествоПакетов, 0) КАК КоличествоПакетов,
	|	ЕСТЬNULL(ВТ_КоличествоПакетовТоваров.КоличествоПакетов, 0) * ВТ_ПроцентСкидкиСДаннымиПоПакету.КоличествоПлан КАК КоличествоПланСУчетомКратностиПродаж,
	|	0 КАК СуммаПроверки,
	|	ЕСТЬNULL(ВТ_ПроцентСкидкиСДаннымиПоПакету.Количество, 0) КАК КоличествоПроверки,
	|	(ЕСТЬNULL(ВТ_КоличествоПакетовТоваров.КоличествоПакетов, 0) > 0) КАК ПланВыполнен,
	|	0 КАК ДиапазонОт,
	|	0 КАК ДиапазонПо,
	|	0 КАК НомерДиапазона,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза
	|ПОМЕСТИТЬ ВТ_ПодготовленныеДанные
	|ИЗ
	|	ВТ_ПроцентСкидкиСДаннымиПоПакету КАК ВТ_ПроцентСкидкиСДаннымиПоПакету
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КоличествоПакетовТоваров КАК ВТ_КоличествоПакетовТоваров
	|		ПО ВТ_ПроцентСкидкиСДаннымиПоПакету.УсловиеРетроБонуса = ВТ_КоличествоПакетовТоваров.УсловиеРетроБонуса
	|			И ВТ_ПроцентСкидкиСДаннымиПоПакету.ПериодРасчета = ВТ_КоличествоПакетовТоваров.ПериодРасчета
	|			И ВТ_ПроцентСкидкиСДаннымиПоПакету.Организация = ВТ_КоличествоПакетовТоваров.Организация
	|			И ВТ_ПроцентСкидкиСДаннымиПоПакету.Валюта = ВТ_КоличествоПакетовТоваров.Валюта
	|			И &ПоляСоединенияПоУчастникам
	|			И &ТаблицаПревышениеЛимита
	|ГДЕ
	|	&УсловиеОтбораПустогоДокумента";
	
	Если ПараметрыУсловийРетроБонусов.РаспределениеПоFIFO Тогда
		
		ТаблицаПревышениеЛимита =
		"ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
		|		ПО ВТ_ПроцентСкидкиСДаннымиПоПакету.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
		|			И ВТ_ПроцентСкидкиСДаннымиПоПакету.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
		ВыражениеПриоритет = "ЕСТЬNULL(ВТ_ПревышениеЛимитаОбщая.Приоритет, -1)";
		
	Иначе
		
		ТаблицаПревышениеЛимита = "";
		ВыражениеПриоритет = "-1";
		
	КонецЕсли;
	
	Если ПараметрыВарианта.ДетализацияДоНоменклатуры Тогда
		УсловиеОтбораПустогоДокумента = "ИСТИНА";
	Иначе
		УсловиеОтбораПустогоДокумента = "ВТ_ПроцентСкидкиСДаннымиПоПакету.ДокументРегистратор <> " + ЗначениеПустойСсылкиРегистратора(ПараметрыУсловийРетроБонусов);
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&УсловиеОтбораПустогоДокумента", УсловиеОтбораПустогоДокумента);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И &ТаблицаПревышениеЛимита", ТаблицаПревышениеЛимита);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПриоритет", ВыражениеПриоритет);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету", "ВТ_КоличествоПакетовТоваров");
	
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидкиСДаннымиПоПакету");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеСУчетомПревышенияЛимита(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказательТоваров = ПараметрыУсловийРетроБонусов.ПоказательТоваров;
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.РаспределениеПоФИФО КАК РаспределениеПоФИФО,
	|	ВТ_ПодготовленныеДанные.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	&ВыражениеКоличествоИсходное КАК Количество,
	|	&ВыражениеСуммаПродажи КАК СуммаПродажи,
	|	&ВыражениеСуммаОборотСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК РасчетнаяБаза,
	|	ВТ_ПодготовленныеДанные.СуммаПроверки КАК РасчетнаяБазаВсего,
	|	ВТ_ПодготовленныеДанные.КоличествоПроверки КАК КоличествоВсего,
	|	ВТ_ПодготовленныеДанные.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеСуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.КоличествоПлан КАК КоличествоПлан,
	|	&ВыражениеПриоритет КАК Приоритет,
	|	ВТ_ПодготовленныеДанные.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПодготовленныеДанные.ДиапазонПо КАК ДиапазонПо,
	|	&ВыражениеСуммаНарастающимИтогом КАК СуммаНарастающимИтогом,
	|	&ВыражениеКоличествоНарастающимИтогом КАК КоличествоНарастающимИтогом,
	|	&ВыражениеПланВыполнен КАК ПланВыполнен,
	|	&ВыражениеПроданоПакетов КАК КоличествоПакетов,
	|	&ВыражениеКоличествоПланСУчетомКратностиПродаж КАК КоличествоПланСУчетомКратностиПродаж
	|ПОМЕСТИТЬ ВТ_ДанныеСУчетомПревышенияЛимита
	|ИЗ
	|	&ТаблицыИсточники КАК ВТ_ПодготовленныеДанные";
	
	Если ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	 ИЛИ ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам
	 ИЛИ ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		
		ТаблицыИсточники =
		"ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
		|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаОбщая КАК ВТ_ПревышениеЛимитаОбщая
		|		ПО ВТ_ПодготовленныеДанные.ДокументРегистратор = ВТ_ПревышениеЛимитаОбщая.ДокументРегистратор
		|			И ВТ_ПодготовленныеДанные.НомерСтроки = ВТ_ПревышениеЛимитаОбщая.НомерСтроки";
		
		ПраваяТаблица = "ВТ_ПревышениеЛимитаОбщая";
		
		ВыражениеПриоритет = "ЕСТЬNULL(" + ПраваяТаблица + ".Приоритет, -1)";
		ВыражениеСуммаНарастающимИтогом = "ЕСТЬNULL(" + ПраваяТаблица + ".Сумма, 0)";
		ВыражениеКоличествоНарастающимИтогом = "ЕСТЬNULL(" + ПраваяТаблица + ".Количество, 0)";
		
		Если ПоказательТоваров = ВидыПоказателей.Сумма Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.СуммаПроверки >= ВТ_ПодготовленныеДанные.СуммаПлан)";
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.Количество Тогда
			
			Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам
			   И ПараметрыУсловийРетроБонусов.ПропорциональноеРаспределение Тогда
				ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.КоличествоПроверки > 0)";
			Иначе
				ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.КоличествоПроверки >= ВТ_ПодготовленныеДанные.КоличествоПлан)";
			КонецЕсли;
			
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.КоличествоСовокупно Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.СуммаПроверки > 0)";
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.КоличествоПакетов > 0)";
		Иначе
			ВыражениеПланВыполнен = "ИСТИНА";
		КонецЕсли;
		
	Иначе
		
		ТаблицыИсточники = "ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные";
		ПраваяТаблица = "";
		ВыражениеПриоритет = "0";
		ВыражениеСуммаНарастающимИтогом = "0";
		ВыражениеКоличествоНарастающимИтогом = "0";
		
		Если ПоказательТоваров = ВидыПоказателей.Сумма Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.СуммаПроверки >= ВТ_ПодготовленныеДанные.СуммаПлан)";
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.Количество Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.КоличествоПроверки >= ВТ_ПодготовленныеДанные.КоличествоПлан)";
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.КоличествоСовокупно Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.СуммаПроверки > 0)";
		ИначеЕсли ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
			ВыражениеПланВыполнен = "(ВТ_ПодготовленныеДанные.КоличествоПакетов > 0)";
		Иначе
			ВыражениеПланВыполнен = "ИСТИНА";
		КонецЕсли;
		
	КонецЕсли;
	
	Если ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		
		ВыражениеПроданоПакетов = "ВТ_ПодготовленныеДанные.КоличествоПакетов";
		ВыражениеКоличествоПланСУчетомКратностиПродаж =
			"ВТ_ПодготовленныеДанные.КоличествоПакетов * ВТ_ПодготовленныеДанные.КоличествоПлан";
		
	Иначе
		
		ВыражениеПроданоПакетов = "0";
		ВыражениеКоличествоПланСУчетомКратностиПродаж = "0";
		
	КонецЕсли;
	
	Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
		
		Если ПоказательТоваров = ВидыПоказателей.Сумма Тогда
			
			ВыражениеСуммаПлан =
			"ВЫБОР
			|	КОГДА ВТ_ПодготовленныеДанные.ДиапазонПо = 0
			|		ТОГДА ВТ_ПодготовленныеДанные.СуммаПроверки - ВТ_ПодготовленныеДанные.ДиапазонОт
			|	КОГДА ВТ_ПодготовленныеДанные.СуммаПроверки <= ВТ_ПодготовленныеДанные.ДиапазонПо
			|		ТОГДА ВТ_ПодготовленныеДанные.СуммаПроверки - ВТ_ПодготовленныеДанные.ДиапазонОт
			|	ИНАЧЕ ВТ_ПодготовленныеДанные.ДиапазонПо - ВТ_ПодготовленныеДанные.ДиапазонОт
			|КОНЕЦ";
			
			ВыражениеКоличествоИсходное =
			"ВЫБОР
			|	КОГДА ВТ_ПодготовленныеДанные.ДиапазонОт = 0
			|		ТОГДА ВТ_ПодготовленныеДанные.Количество
			|	ИНАЧЕ 0
			|КОНЕЦ";
			
		Иначе
			
			ВыражениеСуммаПлан = "ВТ_ПодготовленныеДанные.СуммаПлан";
			ВыражениеКоличествоИсходное = "ВТ_ПодготовленныеДанные.Количество";
			
		КонецЕсли;
		
		ВыражениеСуммаПродажи =
		"ВЫБОР
		|	КОГДА ВТ_ПодготовленныеДанные.ДиапазонОт = 0
		|		ТОГДА ВТ_ПодготовленныеДанные.СуммаПродажи
		|	ИНАЧЕ 0
		|КОНЕЦ";
		ВыражениеСуммаОборотСНДС =
		"ВЫБОР
		|	КОГДА ВТ_ПодготовленныеДанные.ДиапазонОт = 0
		|		ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотСНДС
		|	ИНАЧЕ 0
		|КОНЕЦ";
		ВыражениеСуммаОборотБезНДС =
		"ВЫБОР
		|	КОГДА ВТ_ПодготовленныеДанные.ДиапазонОт = 0
		|		ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотБезНДС
		|	ИНАЧЕ 0
		|КОНЕЦ";
		
	Иначе
		
		ВыражениеСуммаПлан = "ВТ_ПодготовленныеДанные.СуммаПлан";
		ВыражениеКоличествоИсходное = "ВТ_ПодготовленныеДанные.Количество";
		ВыражениеСуммаПродажи = "ВТ_ПодготовленныеДанные.СуммаПродажи";
		ВыражениеСуммаОборотСНДС = "ВТ_ПодготовленныеДанные.СуммаОборотСНДС";
		ВыражениеСуммаОборотБезНДС = "ВТ_ПодготовленныеДанные.СуммаОборотБезНДС";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицыИсточники КАК ВТ_ПодготовленныеДанные", ТаблицыИсточники);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПриоритет", ВыражениеПриоритет);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаНарастающимИтогом", ВыражениеСуммаНарастающимИтогом);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоНарастающимИтогом", ВыражениеКоличествоНарастающимИтогом);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПлан", ВыражениеСуммаПлан);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоИсходное", ВыражениеКоличествоИсходное);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаПродажи", ВыражениеСуммаПродажи);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаОборотСНДС", ВыражениеСуммаОборотСНДС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаОборотБезНДС", ВыражениеСуммаОборотБезНДС);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПланВыполнен", ВыражениеПланВыполнен);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПроданоПакетов", ВыражениеПроданоПакетов);
	ТекстЗапроса = СтрЗаменить(
		ТекстЗапроса, "&ВыражениеКоличествоПланСУчетомКратностиПродаж", ВыражениеКоличествоПланСУчетомКратностиПродаж);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанные", ПраваяТаблица);
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанные", ПраваяТаблица);
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПодготовленныеДанные");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеБезЗапретаПревышения(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеСУчетомПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Период КАК Период,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Договор КАК Договор,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Соглашение КАК Соглашение,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Характеристика КАК Характеристика,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.БазаРасчета КАК БазаРасчета,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Валюта КАК Валюта,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Количество КАК Количество,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.БонусПроцент КАК БонусПроцент,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен КАК ПланВыполнен,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен
	|			ТОГДА ВТ_ДанныеСУчетомПревышенияЛимита.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДляРасчетаБонуса,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен
	|			ТОГДА 
	|				ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПродажи
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫРАЗИТЬ(ВЫБОР
	|		КОГДА ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен
	|			ТОГДА 
	|				ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПродажи
	|		ИНАЧЕ 0
	|	КОНЕЦ * ВТ_ДанныеСУчетомПревышенияЛимита.БонусПроцент / 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_ДанныеБезЗапретаПревышения
	|ИЗ
	|	ВТ_ДанныеСУчетомПревышенияЛимита КАК ВТ_ДанныеСУчетомПревышенияЛимита";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеСУчетомПревышенияЛимита");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПриоритетОкругления(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ЦелевыеПоказатели = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	МАКСИМУМ(ТаблицаИсточник.Приоритет) КАК МаксимальныйПриоритет
	|ПОМЕСТИТЬ ВТ_ПриоритетОкругления
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|ГДЕ
	|	ТаблицаИсточник.ПланВыполнен
	|	И ТаблицаИсточник.РасчетнаяБаза > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.ПериодРасчета,
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	&ПолеГруппировкиНомерДиапазона,
	|	ТаблицаИсточник.Организация,
	|	ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров";
	
	Если ПараметрыУсловийРетроБонусов.РаспределениеПоFIFO Тогда
		
		Если (ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.Количество
		 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.ПакетноеПредложение) Тогда
			ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса";
		Иначе
			ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияПоФИФО";
		КонецЕсли;
		
	Иначе
		ТаблицаИсточник = "ВТ_ДанныеДляПропорциональногоРаспределения";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаМинимальныйПриоритетВыполненияПланаПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если НЕ ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	 ИЛИ НЕ ПараметрыУсловийРетроБонусов.РаспределениеПоFIFO Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	МИНИМУМ(ТаблицаИсточник.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВТ_МинимальныйПриоритетВыполненияПлана
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ТаблицаИсточник
	|ГДЕ
	|	ТаблицаИсточник.Сумма >= ТаблицаИсточник.СуммаПлан
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	ТаблицаИсточник.Организация,
	|	&ПоляГруппировкиПоУчастникам,
	|	ТаблицаИсточник.Валюта,
	|	ТаблицаИсточник.ПериодРасчета";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. ПараметрыУсловийРетроБонусовКлиентов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеДляРаспределенияПоФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ЦелевыеПоказатели = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	0 КАК НомерДиапазона,
	|	0 КАК ДиапазонОт,
	|	0 КАК ДиапазонПо,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.БазаРасчета КАК БазаРасчета,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.Количество КАК Количество,
	|	ВТ_ПодготовленныеДанные.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ПодготовленныеДанные.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ПодготовленныеДанные.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПодготовленныеДанные.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	&ВыражениеКоличествоПланИсходный КАК КоличествоПлан,
	|	&ВыражениеКоличествоПланСУчетомКратностиПродаж КАК КоличествоПланСУчетомКратностиПродаж,
	|	&ВыражениеКоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	&ВыражениеСуммаНарастающимИтогом КАК СуммаНарастающимИтогом,
	|	&ВыражениеКоличествоНарастающимИтогом КАК КоличествоНарастающимИтогом,
	|	ВТ_ПодготовленныеДанные.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ЕСТЬNULL(ВТ_МинимальныйПриоритетВыполненияПлана.Приоритет, -1) КАК МинимальныйПриоритет,
	|	&ВыражениеКоличествоПакетов КАК КоличествоПакетов,
	|	&ВыражениеРасчетнаяБаза КАК РасчетнаяБаза,
	|	&ВыражениеСуммаБонусПоСтроке КАК СуммаБонус,
	|	&ВыражениеСуммаБонусВсего КАК СуммаБонусВсего
	|ПОМЕСТИТЬ ВТ_ДанныеДляРаспределенияПоФИФО
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_МинимальныйПриоритетВыполненияПлана КАК ВТ_МинимальныйПриоритетВыполненияПлана
	|		ПО ВТ_ПодготовленныеДанные.Валюта = ВТ_МинимальныйПриоритетВыполненияПлана.Валюта
	|			И ВТ_ПодготовленныеДанные.ПериодРасчета = ВТ_МинимальныйПриоритетВыполненияПлана.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ПодготовленныеДанные",
		"ВТ_МинимальныйПриоритетВыполненияПлана");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ПодготовленныеДанные",
		"ВТ_МинимальныйПриоритетВыполненияПлана");
	
	ТекстСоединенияНарастающийИтог = "
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ПО ВТ_ПодготовленныеДанные.Валюта = ВТ_ПревышениеЛимитаНарастающийИтог.Валюта
	|			И ВТ_ПодготовленныеДанные.ПериодРасчета = ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета
	|			И ВТ_ПодготовленныеДанные.Приоритет = ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет
	|			И &ПоляСоединенияПоТоварам
	|			И &ПоляСоединенияПоУчастникам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстСоединенияНарастающийИтог,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПревышениеЛимитаНарастающийИтог");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстСоединенияНарастающийИтог,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПревышениеЛимитаНарастающийИтог");
	
	ТекстЗапроса = ТекстЗапроса + ТекстСоединенияНарастающийИтог;
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.Сумма Тогда
		
		ВыражениеКоличествоПланИсходный = "0";
		ВыражениеКоличествоПланСУчетомКратностиПродаж = "0";
		ВыражениеКоличествоДляРасчетаБонуса = "0";
		ВыражениеРасчетнаяБаза = "ВЫБОР
		|		КОГДА НЕ ВТ_ПодготовленныеДанные.ПланВыполнен
		|			ТОГДА 0
		|		КОГДА ВТ_ПодготовленныеДанные.Приоритет > ЕСТЬNULL(ВТ_МинимальныйПриоритетВыполненияПлана.Приоритет, -1)
		|			ТОГДА 0
		|		КОГДА ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Сумма, 0) <= ВТ_ПодготовленныеДанные.СуммаПлан
		|			ТОГДА ВТ_ПодготовленныеДанные.РасчетнаяБаза
		|		ИНАЧЕ ВТ_ПодготовленныеДанные.РасчетнаяБаза - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Сумма, 0) - ВТ_ПодготовленныеДанные.СуммаПлан)
		|	КОНЕЦ";
		ВыражениеСуммаБонусПоСтроке = "ВЫРАЗИТЬ(ВЫБОР
		|			КОГДА НЕ ВТ_ПодготовленныеДанные.ПланВыполнен
		|				ТОГДА 0
		|			КОГДА ВТ_ПодготовленныеДанные.Приоритет > ЕСТЬNULL(ВТ_МинимальныйПриоритетВыполненияПлана.Приоритет, -1)
		|				ТОГДА 0
		|			КОГДА ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Сумма, 0) <= ВТ_ПодготовленныеДанные.СуммаПлан
		|				ТОГДА ВТ_ПодготовленныеДанные.РасчетнаяБаза
		|			ИНАЧЕ ВТ_ПодготовленныеДанные.РасчетнаяБаза - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Сумма, 0) - ВТ_ПодготовленныеДанные.СуммаПлан)
		|		КОНЕЦ * ВТ_ПодготовленныеДанные.БонусПроцент / 100 КАК ЧИСЛО(31, 2))";
		ВыражениеСуммаБонусВсего = "ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.СуммаПлан
		|	* ВТ_ПодготовленныеДанные.БонусПроцент / 100 КАК ЧИСЛО(31, 2))";
		ВыражениеСуммаНарастающимИтогом = "ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Сумма, 0)";
		ВыражениеКоличествоНарастающимИтогом = "0";
		ВыражениеКоличествоПакетов = "0";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.Количество Тогда
		
		ВыражениеКоличествоПланИсходный = "ВТ_ПодготовленныеДанные.КоличествоПлан";
		ВыражениеКоличествоПланСУчетомКратностиПродаж = "ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоПланСУчетомКратностиПродаж";
		ВыражениеКоличествоДляРасчетаБонуса = "ВЫБОР
		|		КОГДА ВТ_ПодготовленныеДанные.ПланВыполнен
		|		И ВТ_ПодготовленныеДанные.Приоритет <= ЕСТЬNULL(ВТ_МинимальныйПриоритетВыполненияПлана.Приоритет, -1)
		|			ТОГДА ВЫБОР
		|				КОГДА ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) <= ВТ_ПодготовленныеДанные.КоличествоПлан
		|					ТОГДА ВТ_ПодготовленныеДанные.Количество
		|				КОГДА ВТ_ПодготовленныеДанные.Количество - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) - ВТ_ПодготовленныеДанные.КоличествоПлан) < 0
		|					ТОГДА 0
		|				ИНАЧЕ ВТ_ПодготовленныеДанные.Количество - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) - ВТ_ПодготовленныеДанные.КоличествоПлан)
		|			КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ";
		ВыражениеРасчетнаяБаза = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
		ВыражениеСуммаБонусПоСтроке = "0";
		ВыражениеСуммаБонусВсего = "0";
		ВыражениеСуммаНарастающимИтогом = "0";
		ВыражениеКоличествоНарастающимИтогом = "ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0)";
		ВыражениеКоличествоПакетов = "0";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.ПакетноеПредложение Тогда
		
		ВыражениеКоличествоПланИсходный = "ВТ_ПодготовленныеДанные.КоличествоПлан";
		ВыражениеКоличествоПланСУчетомКратностиПродаж = "ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоПланСУчетомКратностиПродаж";
		ВыражениеКоличествоДляРасчетаБонуса = "ВЫБОР
		|		КОГДА ВТ_ПодготовленныеДанные.ПланВыполнен
		|		И ВТ_ПодготовленныеДанные.Приоритет <= ЕСТЬNULL(ВТ_МинимальныйПриоритетВыполненияПлана.Приоритет, -1)
		|			ТОГДА ВЫБОР
		|				КОГДА ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) <= ВТ_ПодготовленныеДанные.КоличествоПланСУчетомКратностиПродаж
		|					ТОГДА ВТ_ПодготовленныеДанные.Количество
		|				КОГДА ВТ_ПодготовленныеДанные.Количество - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) - ВТ_ПодготовленныеДанные.КоличествоПланСУчетомКратностиПродаж) < 0
		|					ТОГДА 0
		|				ИНАЧЕ ВТ_ПодготовленныеДанные.Количество - (ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0) - ВТ_ПодготовленныеДанные.КоличествоПланСУчетомКратностиПродаж)
		|			КОНЕЦ
		|		ИНАЧЕ 0
		|	КОНЕЦ";
		ВыражениеРасчетнаяБаза = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
		ВыражениеСуммаБонусПоСтроке = "0";
		ВыражениеСуммаБонусВсего = "0";
		ВыражениеСуммаНарастающимИтогом = "0";
		ВыражениеКоличествоНарастающимИтогом = "ЕСТЬNULL(ВТ_ПревышениеЛимитаНарастающийИтог.Количество, 0)";
		ВыражениеКоличествоПакетов = "ВТ_ПодготовленныеДанные.КоличествоПакетов";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Не удалось определить показатель товаров (""%1"") для документа ""%2""';
							|en = 'Cannot determine the goods indicator (%1) for the %2 document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПараметрыУсловийРетроБонусов.ПоказательТоваровПредставление,
			ПараметрыУсловийРетроБонусов.ДокументУсловийРетроБонусовПредставление);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПланИсходный", ВыражениеКоличествоПланИсходный);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПланСУчетомКратностиПродаж", ВыражениеКоличествоПланСУчетомКратностиПродаж);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоДляРасчетаБонуса", ВыражениеКоличествоДляРасчетаБонуса);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРасчетнаяБаза", ВыражениеРасчетнаяБаза);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаБонусПоСтроке", ВыражениеСуммаБонусПоСтроке);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаБонусВсего", ВыражениеСуммаБонусВсего);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаНарастающимИтогом", ВыражениеСуммаНарастающимИтогом);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоНарастающимИтогом", ВыражениеКоличествоНарастающимИтогом);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПакетов", ВыражениеКоличествоПакетов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОкруглениеРаспределенияПоФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ЦелевыеПоказатели = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1) КАК МаксимальныйПриоритет,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаИсточник.РасчетнаяБаза
	|		КОНЕЦ) КАК РасчетнаяБазаКромеМаксимальногоПриоритета,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1)
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаИсточник.СуммаБонус
	|		КОНЕЦ) КАК СуммаБонусКромеМаксимальногоПриоритета
	|ПОМЕСТИТЬ ВТ_ОкруглениеРаспределенияПоФИФО
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПриоритетОкругления КАК ВТ_ПриоритетОкругления
	|	ПО ТаблицаИсточник.ПериодРасчета = ВТ_ПриоритетОкругления.ПериодРасчета
	|		И ТаблицаИсточник.УсловиеРетроБонуса = ВТ_ПриоритетОкругления.УсловиеРетроБонуса
	|		И ТаблицаИсточник.Организация = ВТ_ПриоритетОкругления.Организация
	|		И &ПоляСоединенияПоУчастникам
	|		И &ПоляСоединенияПоТоварам
	|		И &ПолеСоединенияНомерДиапазона
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.ПериодРасчета,
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	&ПолеГруппировкиНомерДиапазона,
	|	ТаблицаИсточник.Организация,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ПриоритетОкругления.МаксимальныйПриоритет";
	
	Если (ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.Количество
	 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ЦелевыеПоказатели.ПакетноеПредложение) Тогда
		ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса";
	Иначе
		ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияПоФИФО";
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	
	ПодставитьПоляПоУчастникам(	ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговоеРаспределениеПоФИФОПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляРаспределенияПоФИФО.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Период КАК Период,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Договор КАК Договор,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Соглашение КАК Соглашение,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Валюта КАК Валюта,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Количество КАК Количество,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент КАК БонусПроцент,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПланВыполнен КАК ПланВыполнен,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.Приоритет = ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.МаксимальныйПриоритет, -1)
	|			ТОГДА ВТ_ДанныеДляРаспределенияПоФИФО.СуммаПлан - ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.РасчетнаяБазаКромеМаксимальногоПриоритета, 0)
	|		ИНАЧЕ ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	0 КАК КоличествоПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Количество КАК КоличествоДляРасчетаБонуса,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.Приоритет = ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.МаксимальныйПриоритет, -1)
	|			ТОГДА ВТ_ДанныеДляРаспределенияПоФИФО.СуммаБонусВсего - ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.СуммаБонусКромеМаксимальногоПриоритета, 0)
	|		ИНАЧЕ ВТ_ДанныеДляРаспределенияПоФИФО.СуммаБонус
	|	КОНЕЦ КАК СуммаБонус,
	|	0 КАК КоличествоПакетов
	|ПОМЕСТИТЬ ВТ_ИтоговоеРаспределениеПоФИФО
	|ИЗ
	|	ВТ_ДанныеДляРаспределенияПоФИФО КАК ВТ_ДанныеДляРаспределенияПоФИФО
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОкруглениеРаспределенияПоФИФО КАК ВТ_ОкруглениеРаспределенияПоФИФО
	|	ПО ВТ_ДанныеДляРаспределенияПоФИФО.ПериодРасчета = ВТ_ОкруглениеРаспределенияПоФИФО.ПериодРасчета
	|		И &ПолеСоединенияНомерДиапазона
	|		И ВТ_ДанныеДляРаспределенияПоФИФО.УсловиеРетроБонуса = ВТ_ОкруглениеРаспределенияПоФИФО.УсловиеРетроБонуса
	|		И ВТ_ДанныеДляРаспределенияПоФИФО.Организация = ВТ_ОкруглениеРаспределенияПоФИФО.Организация
	|		И &ПоляСоединенияПоУчастникам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФО",
		"ВТ_ОкруглениеРаспределенияПоФИФО");
	ПодставитьПолеНомерДиапазона(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФО",
		"ВТ_ОкруглениеРаспределенияПоФИФО");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаМинимальныйПриоритетВыполненияПланаПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	МИНИМУМ(ТаблицаИсточник.Приоритет) КАК Приоритет
	|ПОМЕСТИТЬ ВТ_МинимальныйПриоритетВыполненияПлана
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ТаблицаИсточник
	|ГДЕ
	|	&ВыражениеУсловие
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПериодРасчета,
	|	ТаблицаИсточник.Организация,
	|	ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		ВыражениеУсловие = "ТаблицаИсточник.Количество >= ТаблицаИсточник.КоличествоПланСУчетомКратностиПродаж";
	Иначе
		ВыражениеУсловие = "ТаблицаИсточник.Количество >= ТаблицаИсточник.КоличествоПлан";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеУсловие", ВыражениеУсловие);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеДляРаспределенияПоФИФОСуммаБонусаПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляРаспределенияПоФИФО.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Период КАК Период,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Договор КАК Договор,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Соглашение КАК Соглашение,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета КАК БазаРасчета,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Валюта КАК Валюта,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Количество КАК Количество,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент КАК БонусПроцент,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.Приоритет КАК Приоритет,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоПакетов КАК КоличествоПакетов,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса = 0
	|			ТОГДА ВЫБОР
	|				КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.ПланВыполнен
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.Количество = 0
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета <> ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета <> ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.Приоритет <= ВТ_ДанныеДляРаспределенияПоФИФО.МинимальныйПриоритет
	|					ТОГДА ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|				ИЛИ ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|				ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса * ВТ_ДанныеДляРаспределенияПоФИФО.БазоваяЦена КАК ЧИСЛО(31, 2))
	|			КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса = ВТ_ДанныеДляРаспределенияПоФИФО.Количество
	|				ТОГДА ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза
	|			ИНАЧЕ ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза * ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса / ВТ_ДанныеДляРаспределенияПоФИФО.Количество КАК ЧИСЛО(31, 2))
	|		КОНЕЦ
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса = 0
	|			ТОГДА ВЫБОР
	|				КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.ПланВыполнен
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.Количество = 0
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета <> ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета <> ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|					И ВТ_ДанныеДляРаспределенияПоФИФО.Приоритет <= ВТ_ДанныеДляРаспределенияПоФИФО.МинимальныйПриоритет
	|					ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза * ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|			КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|				ИЛИ ВТ_ДанныеДляРаспределенияПоФИФО.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|				ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса * ВТ_ДанныеДляРаспределенияПоФИФО.БазоваяЦена * ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|			КОГДА ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса = ВТ_ДанныеДляРаспределенияПоФИФО.Количество
	|				ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза * ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|			ИНАЧЕ ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.РасчетнаяБаза * ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоДляРасчетаБонуса * ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент / 100 / ВТ_ДанныеДляРаспределенияПоФИФО.Количество КАК ЧИСЛО(31, 2))
	|		КОНЕЦ
	|	КОНЕЦ КАК СуммаБонус,
	|	ВЫРАЗИТЬ(ВТ_ДанныеДляРаспределенияПоФИФО.КоличествоПланСУчетомКратностиПродаж * ВТ_ДанныеДляРаспределенияПоФИФО.БазоваяЦена * ВТ_ДанныеДляРаспределенияПоФИФО.БонусПроцент / 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонусВсего
	|ПОМЕСТИТЬ ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса
	|ИЗ
	|	ВТ_ДанныеДляРаспределенияПоФИФО КАК ВТ_ДанныеДляРаспределенияПоФИФО";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФО");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговоеРаспределениеПоФИФОПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Период КАК Период,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Договор КАК Договор,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Соглашение КАК Соглашение,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.БазаРасчета КАК БазаРасчета,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Валюта КАК Валюта,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Количество КАК Количество,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.БонусПроцент КАК БонусПроцент,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.КоличествоПакетов КАК КоличествоПакетов,
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.РасчетнаяБаза КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Приоритет = ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.МаксимальныйПриоритет, -1)
	|			И (ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|				ИЛИ ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|			)
	|			ТОГДА ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаБонусВсего - ЕСТЬNULL(ВТ_ОкруглениеРаспределенияПоФИФО.СуммаБонусКромеМаксимальногоПриоритета, 0)
	|		ИНАЧЕ ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.СуммаБонус
	|	КОНЕЦ КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_ИтоговоеРаспределениеПоФИФО
	|ИЗ
	|	ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса КАК ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОкруглениеРаспределенияПоФИФО КАК ВТ_ОкруглениеРаспределенияПоФИФО
	|	ПО ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.ПериодРасчета = ВТ_ОкруглениеРаспределенияПоФИФО.ПериодРасчета
	|		И ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.УсловиеРетроБонуса = ВТ_ОкруглениеРаспределенияПоФИФО.УсловиеРетроБонуса
	|		И ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса.Организация = ВТ_ОкруглениеРаспределенияПоФИФО.Организация
	|		И &ПоляСоединенияПоУчастникам
	|		И &ПолеСоединенияНомерДиапазона
	|		И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса",
		"ВТ_ОкруглениеРаспределенияПоФИФО");
	ПодставитьПолеНомерДиапазона(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса",
		"ВТ_ОкруглениеРаспределенияПоФИФО");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляРаспределенияПоФИФОСуммаБонуса",
		"ВТ_ОкруглениеРаспределенияПоФИФО");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеДляПропорциональногоРаспределенияПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.Период КАК Период,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
	|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ТаблицаИсточник.Договор КАК Договор,
	|	ТаблицаИсточник.Соглашение КАК Соглашение,
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
	|	ТаблицаИсточник.Характеристика КАК Характеристика,
	|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
	|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ТаблицаИсточник.Количество КАК Количество,
	|	ТаблицаИсточник.Количество КАК КоличествоДляРасчетаБонуса,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
	|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаИсточник.ПланВыполнен КАК ПланВыполнен,
	|	ТаблицаИсточник.Приоритет КАК Приоритет,
	|	ТаблицаИсточник.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ТаблицаИсточник.РасчетнаяБазаВсего КАК РасчетнаяБазаБезОграничений,
	|	ТаблицаИсточник.СуммаПлан КАК РасчетнаяБазаВсего,
	|	ВЫРАЗИТЬ(ТаблицаИсточник.СуммаПродажи * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонусБезОграничения,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.РасчетнаяБазаВсего = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаИсточник.СуммаПлан / ТаблицаИсточник.РасчетнаяБазаВсего КАК ЧИСЛО(31, 14))
	|	КОНЕЦ КАК Коэффициент,
	|	ВЫРАЗИТЬ(ТаблицаИсточник.СуммаПлан * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонусВсего,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.РасчетнаяБазаВсего = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаИсточник.РасчетнаяБаза * ТаблицаИсточник.СуммаПлан / ТаблицаИсточник.РасчетнаяБазаВсего КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.РасчетнаяБазаВсего = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаИсточник.РасчетнаяБаза * ТаблицаИсточник.БонусПроцент * ТаблицаИсточник.СуммаПлан / ТаблицаИсточник.РасчетнаяБазаВсего / 100 КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_ДанныеДляПропорциональногоРаспределения
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ТаблицаИсточник
	|ГДЕ
	|	ТаблицаИсточник.ПланВыполнен";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеДляПропорциональногоРаспределенияПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеСУчетомПревышенияЛимита.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Период КАК Период,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Договор КАК Договор,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Соглашение КАК Соглашение,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Характеристика КАК Характеристика,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Количество КАК Количество,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоВсего КАК КоличествоВсего,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Валюта КАК Валюта,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеКоличествоПлан КАК КоличествоПлан,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.Приоритет КАК Приоритет,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.РасчетнаяБаза КАК РасчетнаяБаза,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.РасчетнаяБазаВсего КАК РасчетнаяБазаВсего,
	|	ВЫРАЗИТЬ(ВТ_ДанныеСУчетомПревышенияЛимита.СуммаПродажи * ВТ_ДанныеСУчетомПревышенияЛимита.БонусПроцент / 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонусБезОграничения,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоПакетов КАК КоличествоПакетов,
	|	ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоПланСУчетомКратностиПродаж КАК КоличествоПланСУчетомКратностиПродаж
	|ПОМЕСТИТЬ ВТ_ДанныеДляПропорциональногоРаспределения
	|ИЗ
	|	ВТ_ДанныеСУчетомПревышенияЛимита КАК ВТ_ДанныеСУчетомПревышенияЛимита
	|ГДЕ
	|	ВТ_ДанныеСУчетомПревышенияЛимита.ПланВыполнен";
	
	Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
		
		ВыражениеКоличествоПлан =
		"ВЫБОР
		|	КОГДА ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонПо = 0
		|		ТОГДА ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоВсего - ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонОт
		|	КОГДА ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоВсего <= ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонПо
		|		ТОГДА ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоВсего - ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонОт
		|	ИНАЧЕ ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонПо - ВТ_ДанныеСУчетомПревышенияЛимита.ДиапазонОт
		|КОНЕЦ";
		
	Иначе
		
		ВыражениеКоличествоПлан = "ВТ_ДанныеСУчетомПревышенияЛимита.КоличествоПлан";
		
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПлан", ВыражениеКоличествоПлан);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеСУчетомПревышенияЛимита");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеСУчетомПревышенияЛимита");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаБазаПропорциональногоРаспределенияПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ПоказателиПродаж = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	&ПоляТоваров,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Организация КАК Организация,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ВыражениеКоличествоПлан КАК КоличествоПлан,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.БонусПроцент КАК БонусПроцент,
	|	СУММА(ВТ_ДанныеДляПропорциональногоРаспределения.Количество) КАК КоличествоВсего,
	|	СУММА(ВТ_ДанныеДляПропорциональногоРаспределения.РасчетнаяБаза) КАК РасчетнаяБаза
	|ПОМЕСТИТЬ ВТ_БазаПропорциональногоРаспределения
	|ИЗ
	|	ВТ_ДанныеДляПропорциональногоРаспределения КАК ВТ_ДанныеДляПропорциональногоРаспределения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПериодРасчета,
	|	&ПолеГруппировкиНомерДиапазона,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.УсловиеРетроБонуса,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Организация,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ВыражениеКоличествоПлан,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.КоличествоПлан,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.БонусПроцент";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ПоказателиПродаж.ПакетноеПредложение Тогда
		ВыражениеКоличествоПлан = "ВТ_ДанныеДляПропорциональногоРаспределения.КоличествоПланСУчетомКратностиПродаж";
	Иначе
		ВыражениеКоличествоПлан = "ВТ_ДанныеДляПропорциональногоРаспределения.КоличествоПлан";
	КонецЕсли;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеКоличествоПлан", ВыражениеКоличествоПлан);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеДляПропорциональногоРаспределения");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеДляПропорциональногоРаспределения");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ДанныеДляПропорциональногоРаспределения");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаРасчетПропорциональногоРаспределенияПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_БазаПропорциональногоРаспределения.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_БазаПропорциональногоРаспределения.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	&ПоляТоваров,
	|	ВТ_БазаПропорциональногоРаспределения.Организация КАК Организация,
	|	ВТ_БазаПропорциональногоРаспределения.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	ВТ_БазаПропорциональногоРаспределения.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_БазаПропорциональногоРаспределения.КоличествоВсего КАК КоличествоВсего,
	|	ВЫБОР
	|		КОГДА ВТ_БазаПропорциональногоРаспределения.КоличествоВсего = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_БазаПропорциональногоРаспределения.РасчетнаяБаза * ВТ_БазаПропорциональногоРаспределения.КоличествоПлан / ВТ_БазаПропорциональногоРаспределения.КоличествоВсего КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК РасчетнаяБазаВсего,
	|	ВЫБОР
	|		КОГДА ВТ_БазаПропорциональногоРаспределения.КоличествоВсего = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВЫРАЗИТЬ(ВТ_БазаПропорциональногоРаспределения.РасчетнаяБаза * ВТ_БазаПропорциональногоРаспределения.КоличествоПлан / ВТ_БазаПропорциональногоРаспределения.КоличествоВсего КАК ЧИСЛО(31, 2)) * ВТ_БазаПропорциональногоРаспределения.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаБонусВсего
	|ПОМЕСТИТЬ ВТ_РасчетПропорциональногоРаспределения
	|ИЗ
	|	ВТ_БазаПропорциональногоРаспределения КАК ВТ_БазаПропорциональногоРаспределения";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_БазаПропорциональногоРаспределения");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_БазаПропорциональногоРаспределения");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_БазаПропорциональногоРаспределения");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПропорциональноеРаспределениеПоПозициям(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ДанныеДляПропорциональногоРаспределения.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Период КАК Период,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Договор КАК Договор,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Соглашение КАК Соглашение,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Номенклатура КАК Номенклатура,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Характеристика КАК Характеристика,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.СуммаПлан КАК СуммаПлан,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Количество КАК Количество,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Валюта КАК Валюта,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.БонусПроцент КАК БонусПроцент,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.КоличествоПлан КАК КоличествоПлан,
	|	ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) КАК КоличествоВсего,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) <> 0
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляПропорциональногоРаспределения.Количество * ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоПлан, 0)
	|				/ ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) КАК ЧИСЛО(15, 3))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоличествоДляРасчетаБонуса,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.Приоритет КАК Приоритет,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.СуммаБонусБезОграничения КАК СуммаБонусБезОграничения,
	|	ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.РасчетнаяБазаВсего, 0) КАК РасчетнаяБазаВсего,
	|	ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.СуммаБонусВсего, 0) КАК СуммаБонусВсего,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) <> 0
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляПропорциональногоРаспределения.РасчетнаяБаза * ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоПлан, 0)
	|				/ ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) <> 0
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ДанныеДляПропорциональногоРаспределения.РасчетнаяБаза * ВТ_ДанныеДляПропорциональногоРаспределения.БонусПроцент
	|				* ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоПлан, 0)
	|				/ ЕСТЬNULL(ВТ_РасчетПропорциональногоРаспределения.КоличествоВсего, 0) / 100 КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаБонус,
	|	ВТ_ДанныеДляПропорциональногоРаспределения.КоличествоПакетов КАК КоличествоПакетов
	|ПОМЕСТИТЬ ВТ_ПропорциональноеРаспределениеПоПозициям
	|ИЗ
	|	ВТ_ДанныеДляПропорциональногоРаспределения КАК ВТ_ДанныеДляПропорциональногоРаспределения
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РасчетПропорциональногоРаспределения КАК ВТ_РасчетПропорциональногоРаспределения
	|	ПО ВТ_ДанныеДляПропорциональногоРаспределения.ПериодРасчета = ВТ_РасчетПропорциональногоРаспределения.ПериодРасчета
	|		И &ПолеСоединенияНомерДиапазона
	|		И ВТ_ДанныеДляПропорциональногоРаспределения.УсловиеРетроБонуса = ВТ_РасчетПропорциональногоРаспределения.УсловиеРетроБонуса
	|		И &ПоляСоединенияПоТоварам
	|		И ВТ_ДанныеДляПропорциональногоРаспределения.Организация = ВТ_РасчетПропорциональногоРаспределения.Организация
	|		И ВТ_ДанныеДляПропорциональногоРаспределения.Валюта = ВТ_РасчетПропорциональногоРаспределения.Валюта
	|		И &ПоляСоединенияПоУчастникам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляПропорциональногоРаспределения",
		"ВТ_РасчетПропорциональногоРаспределения");
	ПодставитьПолеНомерДиапазона(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляПропорциональногоРаспределения",
		"ВТ_РасчетПропорциональногоРаспределения");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ДанныеДляПропорциональногоРаспределения",
		"ВТ_РасчетПропорциональногоРаспределения");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтогиПоДиапазонамШкалы(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.Приоритет КАК Приоритет,
	|	МАКСИМУМ(ТаблицаИсточник.ДиапазонОт) КАК ПоследнийДиапазонОт,
	|	СУММА(ТаблицаИсточник.Количество) КАК КоличествоИтогПоШкале,
	|	СУММА(ТаблицаИсточник.РасчетнаяБаза) КАК РасчетнаяБазаИтогПоШкале
	|ПОМЕСТИТЬ ВТ_ИтогиПоДиапазонамШкалы
	|ИЗ
	|	ВТ_ИтоговоеПропорциональноеРаспределение КАК ТаблицаИсточник
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.Валюта,
	|	ТаблицаИсточник.ПериодРасчета,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.Приоритет";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаКорректировкаПогрешностиПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
	|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ТаблицаИсточник.Период КАК Период,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
	|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	ТаблицаИсточник.Договор КАК Договор,
	|	ТаблицаИсточник.Соглашение КАК Соглашение,
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
	|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаИсточник.Характеристика КАК Характеристика,
	|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.ДиапазонОт = 0
	|			ТОГДА ТаблицаИсточник.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
	|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.ПланВыполнен КАК ПланВыполнен,
	|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	ТаблицаИсточник.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	ТаблицаИсточник.КоличествоПакетов КАК КоличествоПакетов,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	&ПоляУчастники,
	|	ТаблицаИсточник.Приоритет КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.ДиапазонОт = ЕСТЬNULL(ВТ_ИтогиПоДиапазонамШкалы.ПоследнийДиапазонОт, -1)
	|			ТОГДА ТаблицаИсточник.РасчетнаяБаза + ТаблицаИсточник.РасчетнаяБазаИсходная
	|				- ЕСТЬNULL(ВТ_ИтогиПоДиапазонамШкалы.РасчетнаяБазаИтогПоШкале, 0)
	|		ИНАЧЕ ТаблицаИсточник.РасчетнаяБаза
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ТаблицаИсточник.СуммаБонус КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_КорректировкаПогрешностиПоДиапазонам
	|ИЗ
	|	ВТ_ИтоговоеПропорциональноеРаспределение КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиПоДиапазонамШкалы КАК ВТ_ИтогиПоДиапазонамШкалы
	|		ПО ТаблицаИсточник.Приоритет = ВТ_ИтогиПоДиапазонамШкалы.Приоритет
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ИтогиПоДиапазонамШкалы");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ИтогиПоДиапазонамШкалы");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОкруглениеПропорциональногоРаспределения(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ТаблицаИсточник.Организация КАК Организация,
	|	ВТ_ТаблицаИсточник.Валюта КАК Валюта,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1) КАК МаксимальныйПриоритет,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1)
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_ТаблицаИсточник.РасчетнаяБаза
	|		КОНЕЦ) КАК РасчетнаяБазаКромеМаксимальногоПриоритета,
	|	СУММА(ВЫБОР
	|			КОГДА ВТ_ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ПриоритетОкругления.МаксимальныйПриоритет, -1)
	|				ТОГДА 0
	|			ИНАЧЕ ВТ_ТаблицаИсточник.СуммаБонус
	|		КОНЕЦ) КАК СуммаБонусКромеМаксимальногоПриоритета
	|ПОМЕСТИТЬ ВТ_ОкруглениеПропорциональногоРаспределения
	|ИЗ
	|	&ТаблицаИсточник КАК ВТ_ТаблицаИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПриоритетОкругления КАК ВТ_ПриоритетОкругления
	|	ПО ВТ_ТаблицаИсточник.ПериодРасчета = ВТ_ПриоритетОкругления.ПериодРасчета
	|		И ВТ_ТаблицаИсточник.УсловиеРетроБонуса = ВТ_ПриоритетОкругления.УсловиеРетроБонуса
	|		И ВТ_ТаблицаИсточник.Организация = ВТ_ПриоритетОкругления.Организация
	|		И ВТ_ТаблицаИсточник.Валюта = ВТ_ПриоритетОкругления.Валюта
	|		И &ПолеСоединенияНомерДиапазона
	|		И &ПоляСоединенияПоУчастникам
	|		И &ПоляСоединенияПоТоварам
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ТаблицаИсточник.ПериодРасчета,
	|	&ПолеГруппировкиНомерДиапазона,
	|	ВТ_ТаблицаИсточник.УсловиеРетроБонуса,
	|	ВТ_ТаблицаИсточник.Организация,
	|	ВТ_ТаблицаИсточник.Валюта,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_ПриоритетОкругления.МаксимальныйПриоритет";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ТаблицаИсточник", "ВТ_ПриоритетОкругления");
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Количество
	 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		
		ТаблицаИсточник = "ВТ_ПропорциональноеРаспределениеПоПозициям";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Сумма Тогда
		
		ТаблицаИсточник = "ВТ_ДанныеДляПропорциональногоРаспределения";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Не удалось определить показатель продаж (""%1"") для документа ""%2""';
							|en = 'Cannot determine the sales indicator (""%1"") for the ""%2"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПараметрыУсловийРетроБонусов.ПоказательТоваровПредставление,
			ПараметрыУсловийРетроБонусов.ДокументУсловийРетроБонусовПредставление);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговоеПропорциональноеРаспределение(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ТаблицаИсточник.Период КАК Период,
	|	ВТ_ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПолеНомерДиапазона,
	|	ВТ_ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
	|	ВТ_ТаблицаИсточник.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ТаблицаИсточник.Договор КАК Договор,
	|	ВТ_ТаблицаИсточник.Соглашение КАК Соглашение,
	|	ВТ_ТаблицаИсточник.Номенклатура КАК Номенклатура,
	|	ВТ_ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ТаблицаИсточник.Характеристика КАК Характеристика,
	|	ВТ_ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ВТ_ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ТаблицаИсточник.Количество КАК Количество,
	|	ВТ_ТаблицаИсточник.Валюта КАК Валюта,
	|	ВТ_ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ВТ_ТаблицаИсточник.ПланВыполнен КАК ПланВыполнен,
	|	ВТ_ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
	|	ВТ_ТаблицаИсточник.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	ВТ_ТаблицаИсточник.Приоритет КАК Приоритет,
	|	ВТ_ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ТаблицаИсточник.РасчетнаяБазаВсего КАК РасчетнаяБазаВсего,
	|	ВТ_ТаблицаИсточник.РасчетнаяБазаИсходная КАК РасчетнаяБазаИсходная,
	|	ВТ_ТаблицаИсточник.СуммаБонусВсего КАК СуммаБонусВсего,
	|	ЕСТЬNULL(ВТ_ОкруглениеПропорциональногоРаспределения.МаксимальныйПриоритет, -1) КАК МаксимальныйПриоритет,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ОкруглениеПропорциональногоРаспределения.МаксимальныйПриоритет, -1)
	|			ТОГДА ВТ_ТаблицаИсточник.РасчетнаяБазаВсего -
	|				ЕСТЬNULL(ВТ_ОкруглениеПропорциональногоРаспределения.РасчетнаяБазаКромеМаксимальногоПриоритета, 0)
	|		ИНАЧЕ ВТ_ТаблицаИсточник.РасчетнаяБаза
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ВТ_ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ОкруглениеПропорциональногоРаспределения.МаксимальныйПриоритет, -1)
	|			ТОГДА ВТ_ТаблицаИсточник.СуммаБонусВсего -
	|				ЕСТЬNULL(ВТ_ОкруглениеПропорциональногоРаспределения.СуммаБонусКромеМаксимальногоПриоритета, 0)
	|		ИНАЧЕ ВТ_ТаблицаИсточник.СуммаБонус
	|	КОНЕЦ КАК СуммаБонус,
	|	&ВыражениеПроданоПакетов КАК КоличествоПакетов
	|ПОМЕСТИТЬ ВТ_ИтоговоеПропорциональноеРаспределение
	|ИЗ
	|	&ТаблицаИсточник КАК ВТ_ТаблицаИсточник
	|	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОкруглениеПропорциональногоРаспределения КАК ВТ_ОкруглениеПропорциональногоРаспределения
	|	ПО ВТ_ТаблицаИсточник.ПериодРасчета = ВТ_ОкруглениеПропорциональногоРаспределения.ПериодРасчета
	|		И &ПолеСоединенияНомерДиапазона
	|		И ВТ_ТаблицаИсточник.УсловиеРетроБонуса = ВТ_ОкруглениеПропорциональногоРаспределения.УсловиеРетроБонуса
	|		И ВТ_ТаблицаИсточник.Организация = ВТ_ОкруглениеПропорциональногоРаспределения.Организация
	|		И ВТ_ТаблицаИсточник.Валюта = ВТ_ОкруглениеПропорциональногоРаспределения.Валюта
	|		И &ПоляСоединенияПоУчастникам
	|		И &ПоляСоединенияПоТоварам";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Количество
	 ИЛИ ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		
		ТаблицаИсточник = "ВТ_ПропорциональноеРаспределениеПоПозициям";
		ВыражениеПроданоПакетов = "ВТ_ТаблицаИсточник.КоличествоПакетов";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Сумма Тогда
		
		ТаблицаИсточник = "ВТ_ДанныеДляПропорциональногоРаспределения";
		ВыражениеПроданоПакетов = "0";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Не удалось определить показатель продаж (""%1"") для документа ""%2""';
							|en = 'Cannot determine the sales indicator (""%1"") for the ""%2"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПараметрыУсловийРетроБонусов.ПоказательТоваровПредставление,
			ПараметрыУсловийРетроБонусов.ДокументУсловийРетроБонусовПредставление);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеПроданоПакетов", ВыражениеПроданоПакетов);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ТаблицаИсточник",
		"ВТ_ОкруглениеПропорциональногоРаспределения");
	
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ТаблицаИсточник",
		"ВТ_ОкруглениеПропорциональногоРаспределения");
	
	ПодставитьПолеНомерДиапазона(
		ПараметрыУсловийРетроБонусов,
		ТекстЗапроса,
		"ВТ_ТаблицаИсточник",
		"ВТ_ОкруглениеПропорциональногоРаспределения");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговыеДанныеБезЦелевогоПоказателя(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПроцентСкидки.Период КАК Период,
	|	ВТ_ПроцентСкидки.ПериодРасчета КАК ПериодРасчета,
	|	0 КАК НомерДиапазона,
	|	"""" КАК ПредставлениеЕдиницыДиапазона,
	|	ВТ_ПроцентСкидки.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПроцентСкидки.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПроцентСкидки.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПроцентСкидки.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ЗНАЧЕНИЕ(Перечисление.ПериодичностиРетроБонусов.ПустаяСсылка) КАК ПериодичностьУсловий,
	|	ЛОЖЬ КАК ЗапретНачисленияСверхПлана,
	|	ИСТИНА КАК ПланВыполнен,
	|	ВТ_ПроцентСкидки.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПроцентСкидки.НомерСтроки КАК НомерСтроки,
	|	0 КАК ДиапазонОт,
	|	0 КАК ДиапазонПо,
	|	ВТ_ПроцентСкидки.Организация КАК Организация,
	|	ВТ_ПроцентСкидки.Контрагент КАК Контрагент,
	|	ВТ_ПроцентСкидки.Партнер КАК Партнер,
	|	ВТ_ПроцентСкидки.Договор КАК Договор,
	|	ВТ_ПроцентСкидки.Соглашение КАК Соглашение,
	|	ВТ_ПроцентСкидки.Номенклатура КАК Номенклатура,
	|	ВТ_ПроцентСкидки.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПроцентСкидки.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПроцентСкидки.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПроцентСкидки.Характеристика КАК Характеристика,
	|	ВТ_ПроцентСкидки.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПроцентСкидки.Количество КАК Количество,
	|	&ВыражениеВалютаРасчета КАК Валюта,
	|	&ВыражениеСуммаБезУточнения КАК СуммаПродажи,
	|	&ВыражениеСуммаСНДС КАК СуммаОборотСНДС,
	|	&ВыражениеСуммаБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ПроцентСкидки.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПроцентСкидки.БонусПроцент КАК БонусПроцент,
	|	0 КАК СуммаПлан,
	|	0 КАК КоличествоПлан,
	|	ВТ_ПроцентСкидки.Количество КАК КоличествоДляРасчетаБонуса,
	|	&ВыражениеСуммаБезУточнения КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаУпрУчет)
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ПроцентСкидки.Сумма * ВТ_ПроцентСкидки.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|		КОГДА ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаРеглУчет)
	|			ИЛИ ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиРегУчет)
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ПроцентСкидки.СуммаРегл * ВТ_ПроцентСкидки.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|		КОГДА ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты)
	|			ИЛИ ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиВзаиморасчеты)
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ПроцентСкидки.СуммаВВалютеВзаиморасчетов * ВТ_ПроцентСкидки.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|		КОГДА ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.ВыручкаБазовыеЦены)
	|			ИЛИ ВТ_ПроцентСкидки.БазаРасчета = ЗНАЧЕНИЕ(Перечисление.БазыРасчетаРетроБонусов.СуммаЗакупкиБазовыеЦены)
	|			ТОГДА ВЫРАЗИТЬ(ВТ_ПроцентСкидки.Количество * ВТ_ПроцентСкидки.БазоваяЦена * ВТ_ПроцентСкидки.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаБонус,
	|	0 КАК КоличествоПакетов
	|ПОМЕСТИТЬ ВТ_ИтоговыеДанные
	|ИЗ
	|	ВТ_ПроцентСкидки КАК ВТ_ПроцентСкидки";
	
	ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	ПодставитьВыражениеВалютаРасчета(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПроцентСкидки");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговыеДанные(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	Если (НЕ ПараметрыУсловийРетроБонусов.ЗапретНачисленияСверхПлана
	   И НЕ ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам)
	   И ПараметрыУсловийРетроБонусов.ПоказательТоваров <> ВидыПоказателей.ПакетноеПредложение Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТ_ДанныеБезЗапретаПревышения.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ВТ_ДанныеБезЗапретаПревышения.ПоказательТоваров КАК ПоказательТоваров,
		|	ВТ_ДанныеБезЗапретаПревышения.Период КАК Период,
		|	ВТ_ДанныеБезЗапретаПревышения.ПериодРасчета КАК ПериодРасчета,
		|	0 КАК НомерДиапазона,
		|	&ПолеПредставлениеЕдиницыДиапазона,
		|	ВТ_ДанныеБезЗапретаПревышения.ДокументРегистратор КАК ДокументРегистратор,
		|	ВТ_ДанныеБезЗапретаПревышения.НомерСтроки КАК НомерСтроки,
		|	0 КАК ДиапазонОт,
		|	0 КАК ДиапазонПо,
		|	ВТ_ДанныеБезЗапретаПревышения.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ВТ_ДанныеБезЗапретаПревышения.Договор КАК Договор,
		|	ВТ_ДанныеБезЗапретаПревышения.Соглашение КАК Соглашение,
		|	ВТ_ДанныеБезЗапретаПревышения.Номенклатура КАК Номенклатура,
		|	ВТ_ДанныеБезЗапретаПревышения.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ДанныеБезЗапретаПревышения.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ВТ_ДанныеБезЗапретаПревышения.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ВТ_ДанныеБезЗапретаПревышения.Характеристика КАК Характеристика,
		|	ВТ_ДанныеБезЗапретаПревышения.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ВТ_ДанныеБезЗапретаПревышения.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ВТ_ДанныеБезЗапретаПревышения.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ВТ_ДанныеБезЗапретаПревышения.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ВТ_ДанныеБезЗапретаПревышения.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ВТ_ДанныеБезЗапретаПревышения.Количество КАК Количество,
		|	ВТ_ДанныеБезЗапретаПревышения.Валюта КАК Валюта,
		|	ВТ_ДанныеБезЗапретаПревышения.СуммаПродажи КАК СуммаПродажи,
		|	ВТ_ДанныеБезЗапретаПревышения.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ВТ_ДанныеБезЗапретаПревышения.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ВТ_ДанныеБезЗапретаПревышения.БонусПроцент КАК БонусПроцент,
		|	ВТ_ДанныеБезЗапретаПревышения.БазоваяЦена КАК БазоваяЦена,
		|	ВТ_ДанныеБезЗапретаПревышения.СуммаПлан КАК СуммаПлан,
		|	ВТ_ДанныеБезЗапретаПревышения.ПланВыполнен КАК ПланВыполнен,
		|	ВТ_ДанныеБезЗапретаПревышения.РасчетнаяБаза КАК РасчетнаяБаза,
		|	ВТ_ДанныеБезЗапретаПревышения.КоличествоПлан КАК КоличествоПлан,
		|	ВТ_ДанныеБезЗапретаПревышения.Количество КАК КоличествоДляРасчетаБонуса,
		|	ВТ_ДанныеБезЗапретаПревышения.СуммаБонус КАК СуммаБонус,
		|	0 КАК КоличествоПакетов
		|ПОМЕСТИТЬ ВТ_ИтоговыеДанные
		|ИЗ
		|	ВТ_ДанныеБезЗапретаПревышения КАК ВТ_ДанныеБезЗапретаПревышения";
		
		ПодставитьПоляПоУчастникам(
			ПараметрыУсловийРетроБонусов,
			ТекстЗапроса,
			"ВТ_ДанныеБезЗапретаПревышения",
			"",
			Истина);
		ПодставитьПолеПредставлениеЕдиницыДиапазона(
			ПараметрыУсловийРетроБонусов,
			ТекстЗапроса,
			"ВТ_ДанныеБезЗапретаПревышения");
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РаспределениеПоFIFO Тогда
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	ВТ_ИтоговоеРаспределениеПоФИФО.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ПоказательТоваров КАК ПоказательТоваров,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Период КАК Период,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ПериодРасчета КАК ПериодРасчета,
		|	&ПолеНомерДиапазона,
		|	&ПолеПредставлениеЕдиницыДиапазона,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ДокументРегистратор КАК ДокументРегистратор,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.НомерСтроки КАК НомерСтроки,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ДиапазонОт КАК ДиапазонОт,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ДиапазонПо КАК ДиапазонПо,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Договор КАК Договор,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Соглашение КАК Соглашение,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Номенклатура КАК Номенклатура,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Характеристика КАК Характеристика,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Количество КАК Количество,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.Валюта КАК Валюта,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.СуммаПродажи КАК СуммаПродажи,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.БонусПроцент КАК БонусПроцент,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.БазоваяЦена КАК БазоваяЦена,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.СуммаПлан КАК СуммаПлан,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.ПланВыполнен КАК ПланВыполнен,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.РасчетнаяБаза КАК РасчетнаяБаза,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.КоличествоПлан КАК КоличествоПлан,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.СуммаБонус КАК СуммаБонус,
		|	ВТ_ИтоговоеРаспределениеПоФИФО.КоличествоПакетов КАК КоличествоПакетов
		|ПОМЕСТИТЬ ВТ_ИтоговыеДанные
		|ИЗ
		|	ВТ_ИтоговоеРаспределениеПоФИФО КАК ВТ_ИтоговоеРаспределениеПоФИФО";
		
		ПодставитьПоляПоУчастникам(
			ПараметрыУсловийРетроБонусов,
			ТекстЗапроса,
			"ВТ_ИтоговоеРаспределениеПоФИФО",
			"",
			Истина);
		ПодставитьПолеНомерДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ИтоговоеРаспределениеПоФИФО");
		ПодставитьПолеПредставлениеЕдиницыДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ИтоговоеРаспределениеПоФИФО");
		
	Иначе
		
		ТекстПодзапросаПланВыполнен =
		"ВЫБРАТЬ
		|	ВТ_ИтоговоеПропорциональноеРаспределение.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ПоказательТоваров КАК ПоказательТоваров,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Период КАК Период,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ПериодРасчета КАК ПериодРасчета,
		|	&ПолеНомерДиапазона,
		|	&ПолеПредставлениеЕдиницыДиапазона,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ДокументРегистратор КАК ДокументРегистратор,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.НомерСтроки КАК НомерСтроки,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ДиапазонОт КАК ДиапазонОт,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ДиапазонПо КАК ДиапазонПо,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Договор КАК Договор,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Соглашение КАК Соглашение,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Номенклатура КАК Номенклатура,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Характеристика КАК Характеристика,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.СуммаПлан КАК СуммаПлан,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Количество КАК Количество,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.Валюта КАК Валюта,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.СуммаПродажи КАК СуммаПродажи,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.БазоваяЦена КАК БазоваяЦена,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.БонусПроцент КАК БонусПроцент,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.ПланВыполнен КАК ПланВыполнен,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.РасчетнаяБаза КАК РасчетнаяБаза,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.КоличествоПлан КАК КоличествоПлан,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.СуммаБонус КАК СуммаБонус,
		|	ВТ_ИтоговоеПропорциональноеРаспределение.КоличествоПакетов КАК КоличествоПакетов
		|ПОМЕСТИТЬ ВТ_ИтоговыеДанные
		|ИЗ
		|	&ТаблицаИсточник КАК ВТ_ИтоговоеПропорциональноеРаспределение";
		
		Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
			ТаблицаИсточник = "ВТ_КорректировкаПогрешностиПоДиапазонам";
		Иначе
			ТаблицаИсточник = "ВТ_ИтоговоеПропорциональноеРаспределение";
		КонецЕсли;
		ТекстПодзапросаПланВыполнен = СтрЗаменить(ТекстПодзапросаПланВыполнен, "&ТаблицаИсточник", ТаблицаИсточник);
		
		ПодставитьПоляПоУчастникам(
			ПараметрыУсловийРетроБонусов,
			ТекстПодзапросаПланВыполнен,
			"ВТ_ИтоговоеПропорциональноеРаспределение",
			"",
			Истина);
		ПодставитьПолеНомерДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстПодзапросаПланВыполнен, "ВТ_ИтоговоеПропорциональноеРаспределение");
		ПодставитьПолеПредставлениеЕдиницыДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстПодзапросаПланВыполнен, "ВТ_ИтоговоеПропорциональноеРаспределение");
		
		ТекстПодзапросаПланНеВыполнен =
		"ВЫБРАТЬ
		|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
		|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ТаблицаИсточник.Период КАК Период,
		|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
		|	&ПолеНомерДиапазона,
		|	&ПолеПредставлениеЕдиницыДиапазона,
		|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
		|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
		|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
		|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
		|	ТаблицаИсточник.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ТаблицаИсточник.Договор КАК Договор,
		|	ТаблицаИсточник.Соглашение КАК Соглашение,
		|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
		|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ТаблицаИсточник.Характеристика КАК Характеристика,
		|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
		|	ТаблицаИсточник.Количество КАК Количество,
		|	ТаблицаИсточник.Валюта КАК Валюта,
		|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
		|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
		|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
		|	ТаблицаИсточник.ПланВыполнен КАК ПланВыполнен,
		|	0 КАК РасчетнаяБаза,
		|	ТаблицаИсточник.КоличествоПлан КАК КоличествоПлан,
		|	0 КАК КоличествоДляРасчетаБонуса,
		|	0 КАК СуммаБонус,
		|	0 КАК КоличествоПакетов
		|ИЗ
		|	&ТаблицаИсточник КАК ТаблицаИсточник
		|ГДЕ
		|	&УсловиеЗапретНачисленияСверхПлана
		|	И НЕ ТаблицаИсточник.ПланВыполнен
		|	И НЕ ТаблицаИсточник.РаспределениеПоФИФО";
		
		Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Сумма Тогда
			ТаблицаИсточник = "ВТ_ПодготовленныеДанные";
		Иначе
			ТаблицаИсточник = "ВТ_ДанныеСУчетомПревышенияЛимита";
		КонецЕсли;
		ТекстПодзапросаПланНеВыполнен = СтрЗаменить(ТекстПодзапросаПланНеВыполнен, "&ТаблицаИсточник", ТаблицаИсточник);
		
		Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение
		 ИЛИ ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
			УсловиеЗапретНачисленияСверхПлана = "ИСТИНА";
		Иначе
			УсловиеЗапретНачисленияСверхПлана = "ТаблицаИсточник.ЗапретНачисленияСверхПлана";
		КонецЕсли;
		ТекстПодзапросаПланНеВыполнен = СтрЗаменить(
			ТекстПодзапросаПланНеВыполнен, "&УсловиеЗапретНачисленияСверхПлана", УсловиеЗапретНачисленияСверхПлана);
		
		ПодставитьПоляПоУчастникам(
			ПараметрыУсловийРетроБонусов,
			ТекстПодзапросаПланНеВыполнен,
			"ТаблицаИсточник",
			"",
			Истина);
		ПодставитьПолеНомерДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстПодзапросаПланНеВыполнен, "ТаблицаИсточник");
		ПодставитьПолеПредставлениеЕдиницыДиапазона(
			ПараметрыУсловийРетроБонусов, ТекстПодзапросаПланНеВыполнен, "ТаблицаИсточник");
		
		ТекстЗапроса = ТекстПодзапросаПланВыполнен
			+ ОбщегоНазначенияУТ.РазделительЗапросовВОбъединении()
			+ ТекстПодзапросаПланНеВыполнен;
		
	КонецЕсли;
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаСтрокиНаГраницахДиапазоновПоСумме(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_ПревышениеЛимитаНарастающийИтог.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Валюта КАК Валюта,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет КАК Приоритет,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Сумма КАК СуммаНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК КоличествоНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки КАК СуммаСтроки,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки КАК КоличествоСтроки,
	|	ВТ_ШкалаРасчета.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ШкалаРасчета.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ШкалаРасчета.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ШкалаРасчета.БонусПроцент КАК БонусПроцент,
	|	0 КАК БазоваяЦена,
	|	ВЫБОР
	|		КОГДА ВТ_ШкалаРасчета.ДиапазонОт = 0
	|			ТОГДА ВТ_ШкалаРасчета.ДиапазонПо - ВТ_ПревышениеЛимитаНарастающийИтог.Сумма + ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки
	|		КОГДА ВТ_ШкалаРасчета.ДиапазонПо = 0
	|			ТОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ШкалаРасчета.ДиапазонОт
	|		КОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Сумма < ВТ_ШкалаРасчета.ДиапазонПо
	|			ТОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ШкалаРасчета.ДиапазонОт
	|		ИНАЧЕ ВТ_ШкалаРасчета.ДиапазонПо - ВТ_ШкалаРасчета.ДиапазонОт
	|	КОНЕЦ КАК ДиапазонОстаток 
	|ПОМЕСТИТЬ ВТ_СтрокиНаГраницахДиапазонов
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ШкалаРасчета.ДиапазонОт >= ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки
	|			И ВТ_ШкалаРасчета.ДиапазонОт <= ВТ_ПревышениеЛимитаНарастающийИтог.Сумма
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПревышениеЛимитаНарастающийИтог.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Валюта КАК Валюта,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет КАК Приоритет,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Сумма КАК СуммаНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК КоличествоНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки КАК СуммаСтроки,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки КАК КоличествоСтроки,
	|	ВТ_ШкалаРасчета.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ШкалаРасчета.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ШкалаРасчета.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ШкалаРасчета.БонусПроцент КАК БонусПроцент,
	|	0 КАК БазоваяЦена,
	|	ВТ_ШкалаРасчета.ДиапазонПо - (ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки) КАК ДиапазонОстаток
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
	|		ПО ВТ_ШкалаРасчета.ДиапазонПо > ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки
	|			И ВТ_ШкалаРасчета.ДиапазонПо <= ВТ_ПревышениеЛимитаНарастающийИтог.Сумма
	|			И ВТ_ШкалаРасчета.ДиапазонПо > 0
	|			И ВТ_ШкалаРасчета.ДиапазонОт < ВТ_ПревышениеЛимитаНарастающийИтог.Сумма - ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_ПревышениеЛимитаНарастающийИтог");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаСтрокиНаГраницахДиапазоновПоКоличеству(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстыПодзапросов = Новый Массив(); // Массив Из Строка
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_ПревышениеЛимитаНарастающийИтог.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Организация КАК Организация,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Валюта КАК Валюта,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет КАК Приоритет,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Сумма КАК СуммаНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК КоличествоНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки КАК СуммаСтроки,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки КАК КоличествоСтроки,
	|	ВТ_ТоварыДиапазоны.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ТоварыДиапазоны.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ТоварыДиапазоны.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ТоварыДиапазоны.БонусПроцент КАК БонусПроцент,
	|	ВТ_ТоварыДиапазоны.БазоваяЦена КАК БазоваяЦена,
	|	ВЫБОР
	|		КОГДА ВТ_ТоварыДиапазоны.ДиапазонОт = 0
	|			ТОГДА ВТ_ТоварыДиапазоны.ДиапазонПо - ВТ_ПревышениеЛимитаНарастающийИтог.Количество + ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки
	|		КОГДА ВТ_ТоварыДиапазоны.ДиапазонПо = 0
	|			ТОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Количество - ВТ_ТоварыДиапазоны.ДиапазонОт
	|		КОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Количество < ВТ_ТоварыДиапазоны.ДиапазонПо
	|			ТОГДА ВТ_ПревышениеЛимитаНарастающийИтог.Количество - ВТ_ТоварыДиапазоны.ДиапазонОт
	|		ИНАЧЕ ВТ_ТоварыДиапазоны.ДиапазонПо - ВТ_ТоварыДиапазоны.ДиапазонОт
	|	КОНЕЦ КАК ДиапазонОстаток 
	|ПОМЕСТИТЬ ВТ_СтрокиНаГраницахДиапазонов
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
	|		ПО ВТ_ТоварыДиапазоны.ДиапазонОт >= ВТ_ПревышениеЛимитаНарастающийИтог.Количество - ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки
	|			И ВТ_ТоварыДиапазоны.ДиапазонОт <= ВТ_ПревышениеЛимитаНарастающийИтог.Количество
	|			И &ПоляСоединенияПоТоварам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодам КАК ОборотыПоПериодам
	|		ПО &УсловияСоединенияОборотыПоПериодам
	|ГДЕ
	|	ОборотыПоПериодам.Количество > ВТ_ТоварыДиапазоны.ДиапазонОт";
	
	УсловияСоединенияОборотыПоПериодам =
	"ВТ_ПревышениеЛимитаНарастающийИтог.Валюта = ОборотыПоПериодам.Валюта
	|	И ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета = ОборотыПоПериодам.ПериодРасчета
	|	И &ПоляСоединенияПоУчастникам
	|	И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		УсловияСоединенияОборотыПоПериодам,
		"ВТ_ПревышениеЛимитаНарастающийИтог",
		"ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		УсловияСоединенияОборотыПоПериодам,
		"ВТ_ПревышениеЛимитаНарастающийИтог",
		"ОборотыПоПериодам");
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&УсловияСоединенияОборотыПоПериодам", УсловияСоединенияОборотыПоПериодам);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_ПревышениеЛимитаНарастающийИтог.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Организация КАК Организация,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Валюта КАК Валюта,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет КАК Приоритет,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Сумма КАК СуммаНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК КоличествоНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки КАК СуммаСтроки,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки КАК КоличествоСтроки,
	|	ВТ_ТоварыДиапазоны.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ТоварыДиапазоны.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ТоварыДиапазоны.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ТоварыДиапазоны.БонусПроцент КАК БонусПроцент,
	|	ВТ_ТоварыДиапазоны.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ТоварыДиапазоны.ДиапазонПо - (ВТ_ПревышениеЛимитаНарастающийИтог.Количество 
	|		- ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки) КАК ДиапазонОстаток
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
	|		ПО ВТ_ТоварыДиапазоны.ДиапазонПо > ВТ_ПревышениеЛимитаНарастающийИтог.Количество - ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки
	|			И ВТ_ТоварыДиапазоны.ДиапазонПо <= ВТ_ПревышениеЛимитаНарастающийИтог.Количество
	|			И ВТ_ТоварыДиапазоны.ДиапазонПо > 0
	|			И ВТ_ТоварыДиапазоны.ДиапазонОт < ВТ_ПревышениеЛимитаНарастающийИтог.Количество - ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки
	|			И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_ПревышениеЛимитаНарастающийИтог.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Организация КАК Организация,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Валюта КАК Валюта,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Приоритет КАК Приоритет,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Сумма КАК СуммаНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК КоличествоНарастающимИтогом,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.СуммаСтроки КАК СуммаСтроки,
	|	ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки КАК КоличествоСтроки,
	|	ВТ_ТоварыДиапазоны.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ТоварыДиапазоны.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ТоварыДиапазоны.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ТоварыДиапазоны.БонусПроцент КАК БонусПроцент,
	|	ВТ_ТоварыДиапазоны.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ТоварыДиапазоны.ДиапазонПо - ВТ_ПревышениеЛимитаНарастающийИтог.Количество КАК ДиапазонОстаток
	|ИЗ
	|	ВТ_ПревышениеЛимитаНарастающийИтог КАК ВТ_ПревышениеЛимитаНарастающийИтог
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
	|		ПО ВТ_ТоварыДиапазоны.ДиапазонПо = ВТ_ПревышениеЛимитаНарастающийИтог.Количество
	|			И ВТ_ПревышениеЛимитаНарастающийИтог.КоличествоСтроки = 0
	|			И ВТ_ТоварыДиапазоны.ДиапазонПо > 0
	|			И ВТ_ТоварыДиапазоны.ДиапазонОт < ВТ_ПревышениеЛимитаНарастающийИтог.Количество
	|			И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПревышениеЛимитаНарастающийИтог", "ВТ_ТоварыДиапазоны");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаРаспределениеСтрокПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ВыражениеВеличинаПроверки = "ВТ_ПодготовленныеДанные.КоличествоПроверки";
		ВыражениеВеличинаСтрокиОт = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиОт";
		ВыражениеВеличинаСтрокиПо = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиПо";
		ВыражениеБазоваяЦена = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена";
		
		Если ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
			
			ВыражениеРасчетнаяБазаСправа = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				| * ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт КАК ЧИСЛО(31, 2))";
			ВыражениеБонусСправа = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт / 100 КАК ЧИСЛО(31, 2))";
			
			ВыражениеРасчетнаяБазаСлева = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо КАК ЧИСЛО(31, 2))";
			ВыражениеБонусСлева = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо / 100 КАК ЧИСЛО(31, 2))";
			
			ВыражениеРасчетнаяБазаВнутри = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				|* ВТ_ПодготовленныеДанные.Количество КАК ЧИСЛО(31, 2))";
			ВыражениеБонусВнутри = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БазоваяЦена
				|* ВТ_ПодготовленныеДанные.Количество / 100 КАК ЧИСЛО(31, 2))";
			
		Иначе
			
			ВыражениеРасчетнаяБазаСправа = "ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.РасчетнаяБаза
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт
				|/ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиОт КАК ЧИСЛО(31, 2))";
			ВыражениеБонусСправа = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПодготовленныеДанные.РасчетнаяБаза
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт
				|/ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиОт / 100 КАК ЧИСЛО(31, 2))";
			
			ВыражениеРасчетнаяБазаСлева = "ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.РасчетнаяБаза
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо 
				|/ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиПо КАК ЧИСЛО(31, 2))";
			ВыражениеБонусСлева = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПодготовленныеДанные.РасчетнаяБаза
				|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо
				|/ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.КоличествоСтрокиПо / 100 КАК ЧИСЛО(31, 2))";
			
			ВыражениеРасчетнаяБазаВнутри = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
			ВыражениеБонусВнутри = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
				|* ВТ_ПодготовленныеДанные.РасчетнаяБаза / 100 КАК ЧИСЛО(31, 2))";
			
		КонецЕсли;
		
	Иначе
		
		ВыражениеВеличинаПроверки = "ВТ_ПодготовленныеДанные.СуммаПроверки";
		ВыражениеВеличинаСтрокиОт = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.СуммаСтрокиОт";
		ВыражениеВеличинаСтрокиПо = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.СуммаСтрокиПо"; 
		ВыражениеБазоваяЦена = "ВТ_ПодготовленныеДанные.БазоваяЦена";
		
		ВыражениеРасчетнаяБазаСправа = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт";
		ВыражениеБонусСправа = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
			|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт / 100 КАК ЧИСЛО(31, 2))";
		
		ВыражениеРасчетнаяБазаСлева = "ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо";
		ВыражениеБонусСлева = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
			|* ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо / 100 КАК ЧИСЛО(31, 2))";
		
		ВыражениеРасчетнаяБазаВнутри = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
		ВыражениеБонусВнутри = "ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент
			|* ВТ_ПодготовленныеДанные.РасчетнаяБаза / 100 КАК ЧИСЛО(31, 2))";
		
	КонецЕсли;
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	// Для правых частей строк на границах диапазон берем остаток нарастающего итога в диапазоне
	ТекстПодзапросаПравыеЧастиСтрок =
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК Распределено,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	0 КАК Количество,
	|	0 КАК КоличествоПланОстаток,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	0 КАК СуммаПродажи,
	|	0 КАК СуммаОборотСНДС,
	|	0 КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ВЫБОР
	|		КОГДА ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		ИНАЧЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.СуммаПроверки КАК СуммаПроверки,
	|	ВТ_ПодготовленныеДанные.КоличествоПроверки КАК КоличествоПроверки,
	|	ЛОЖЬ КАК ПервыйДиапазонДокумента,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиОт = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт
	|	КОНЕЦ КАК КоличествоДляРасчетаБонуса,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиОт = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ВыражениеРасчетнаяБазаСправа
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиОт = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ВыражениеБонусСправа
	|	КОНЕЦ КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_РаспределениеСтрокПоДиапазонам
	|ИЗ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ВТ_ПриоритетыОстаткиНаГраницахДиапазонов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.Валюта = ВТ_ПодготовленныеДанные.Валюта
	|			И ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПериодРасчета = ВТ_ПодготовленныеДанные.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт = ВТ_ПодготовленныеДанные.Приоритет
	|ГДЕ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокОт <> 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПравыеЧастиСтрок,
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов",
		"ВТ_ПодготовленныеДанные");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПравыеЧастиСтрок,
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов",
		"ВТ_ПодготовленныеДанные");
	ТекстыПодзапросов.Добавить(ТекстПодзапросаПравыеЧастиСтрок);
	
	// Для левых частей строк на границах диапазон берем остаток нарастающего итога в диапазоне
	ТекстПодзапросаЛевыеЧастиСтрок =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Распределено,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ВЫБОР
	|		КОГДА (ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт <> ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо)
	|			ТОГДА ВТ_ПодготовленныеДанные.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	0 КАК КоличествоПланОстаток,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	ВЫБОР
	|		КОГДА (ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт <> ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо)
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаПродажи
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПродажи,
	|	ВЫБОР
	|		КОГДА (ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт <> ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо)
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотСНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОборотСНДС,
	|	ВЫБОР
	|		КОГДА (ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт <> ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо)
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ВЫБОР
	|		КОГДА ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		ИНАЧЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.СуммаПроверки КАК СуммаПроверки,
	|	ВТ_ПодготовленныеДанные.КоличествоПроверки КАК КоличествоПроверки,
	|	(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт <> ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо) КАК ПервыйДиапазонДокумента,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиПо = 0
	|			ТОГДА 0
	|		ИНАЧЕ ВЫРАЗИТЬ(ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо * ВТ_ПодготовленныеДанные.Количество
	|			/ &ВыражениеВеличинаСтрокиПо КАК ЧИСЛО(15, 6))
	|	КОНЕЦ КАК КоличествоДляРасчетаБонуса,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиПо = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ВыражениеРасчетнаяБазаСлева
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА &ВыражениеВеличинаСтрокиПо = 0
	|			ТОГДА 0
	|		ИНАЧЕ &ВыражениеБонусСлева
	|	КОНЕЦ КАК СуммаБонус
	|ИЗ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ВТ_ПриоритетыОстаткиНаГраницахДиапазонов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.Валюта = ВТ_ПодготовленныеДанные.Валюта
	|			И ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПериодРасчета = ВТ_ПодготовленныеДанные.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо = ВТ_ПодготовленныеДанные.Приоритет
	|ГДЕ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОстатокПо <> 0
	|		ИЛИ &ВыражениеВеличинаСтрокиПо = 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧастиСтрок,
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов",
		"ВТ_ПодготовленныеДанные");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧастиСтрок,
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов",
		"ВТ_ПодготовленныеДанные");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаЛевыеЧастиСтрок);
	
	ТекстПодзапросаДокументыВнутриДиапазонов =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Распределено,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ВТ_ПодготовленныеДанные.Количество КАК Количество,
	|	0 КАК КоличествоПланОстаток,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	ВТ_ПодготовленныеДанные.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ПодготовленныеДанные.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ПодготовленныеДанные.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ВЫБОР
	|		КОГДА ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		ИНАЧЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.СуммаПроверки КАК СуммаПроверки,
	|	ВТ_ПодготовленныеДанные.КоличествоПроверки КАК КоличествоПроверки,
	|	ИСТИНА КАК ПервыйДиапазонДокумента,
	|	ВТ_ПодготовленныеДанные.Количество КАК КоличествоДляРасчетаБонуса,
	|	&ВыражениеРасчетнаяБазаВнутри КАК РасчетнаяБаза,
	|	&ВыражениеБонусВнутри КАК СуммаБонус
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ВТ_ПриоритетыОстаткиНаГраницахДиапазонов
	|		ПО ВТ_ПодготовленныеДанные.Валюта = ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.Валюта
	|			И ВТ_ПодготовленныеДанные.ПериодРасчета = ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ВТ_ПодготовленныеДанные.Приоритет > ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт
	|			И ВТ_ПодготовленныеДанные.Приоритет < ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо
	|";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаДокументыВнутриДиапазонов,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаДокументыВнутриДиапазонов,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов");
	ТекстыПодзапросов.Добавить(ТекстПодзапросаДокументыВнутриДиапазонов);
	
	ТекстПодзапросаДокументыВПоследнемДиапазоне =
	"ВЫБРАТЬ
	|	ИСТИНА КАК Распределено,
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ВТ_ПодготовленныеДанные.Количество КАК Количество,
	|	0 КАК КоличествоПланОстаток,
	|	0 КАК КоличествоПланСУчетомКратностиПродаж,
	|	ВТ_ПодготовленныеДанные.СуммаПродажи КАК СуммаПродажи,
	|	ВТ_ПодготовленныеДанные.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ВТ_ПодготовленныеДанные.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.РасчетнаяБаза КАК РасчетнаяБазаИсходная,
	|	ВЫБОР
	|		КОГДА ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|		ИНАЧЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо - ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.СуммаПроверки КАК СуммаПроверки,
	|	ВТ_ПодготовленныеДанные.КоличествоПроверки КАК КоличествоПроверки,
	|	ИСТИНА КАК ПервыйДиапазонДокумента,
	|	ВТ_ПодготовленныеДанные.Количество КАК КоличествоДляРасчетаБонуса,
	|	&ВыражениеРасчетнаяБазаВнутри КАК РасчетнаяБаза,
	|	&ВыражениеБонусВнутри КАК СуммаБонус
	|ИЗ
	|	ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ВТ_ПриоритетыОстаткиНаГраницахДиапазонов
	|		ПО ВТ_ПодготовленныеДанные.Приоритет > ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетОт
	|			И ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПриоритетПо = -1
	|			И ВТ_ПодготовленныеДанные.Валюта = ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.Валюта
	|			И ВТ_ПодготовленныеДанные.ПериодРасчета = ВТ_ПриоритетыОстаткиНаГраницахДиапазонов.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И &ВыражениеВеличинаПроверки > 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаДокументыВПоследнемДиапазоне,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаДокументыВПоследнемДиапазоне,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ПриоритетыОстаткиНаГраницахДиапазонов");
	ТекстыПодзапросов.Добавить(ТекстПодзапросаДокументыВПоследнемДиапазоне);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеВеличинаПроверки", ВыражениеВеличинаПроверки);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеВеличинаСтрокиОт", ВыражениеВеличинаСтрокиОт);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеВеличинаСтрокиПо", ВыражениеВеличинаСтрокиПо);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРасчетнаяБазаСправа", ВыражениеРасчетнаяБазаСправа);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРасчетнаяБазаСлева", ВыражениеРасчетнаяБазаСлева);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРасчетнаяБазаВнутри", ВыражениеРасчетнаяБазаВнутри);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусСправа", ВыражениеБонусСправа);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусСлева", ВыражениеБонусСлева);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБонусВнутри", ВыражениеБонусВнутри);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеБазоваяЦена", ВыражениеБазоваяЦена);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтогиБазыПоШиринеДиапазонов(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ШиринаДиапазона КАК ШиринаДиапазона,
	|	МАКСИМУМ(
	|		ВЫБОР
	|			КОГДА ТаблицаИсточник.РасчетнаяБаза > 0
	|				ТОГДА ТаблицаИсточник.Приоритет
	|			ИНАЧЕ -1
	|		КОНЕЦ) КАК ПриоритетОкругления,
	|	СУММА(ТаблицаИсточник.Количество) КАК КоличествоИтог,
	|	СУММА(ТаблицаИсточник.КоличествоДляРасчетаБонуса) КАК КоличествоДляРасчетаБонусаИтог,
	|	СУММА(ТаблицаИсточник.РасчетнаяБаза) КАК РасчетнаяБазаИтог
	|ПОМЕСТИТЬ ВТ_ИтогиБазыПоШиринеДиапазонов
	|ИЗ
	|	ВТ_РаспределениеСтрокПоДиапазонам КАК ТаблицаИсточник
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.Валюта,
	|	ТаблицаИсточник.ПериодРасчета,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.НомерДиапазона,
	|	ТаблицаИсточник.ШиринаДиапазона";
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаКорректировкаПогрешностиБазыПоДиапазонамФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.Распределено КАК Распределено,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
	|	ТаблицаИсточник.Приоритет КАК Приоритет,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ШиринаДиапазона, 0) КАК ШиринаДиапазона,
	|	ТаблицаИсточник.ПервыйДиапазонДокумента КАК ПервыйДиапазонДокумента,
	|	ТаблицаИсточник.СуммаПроверки КАК СуммаПроверки,
	|	ТаблицаИсточник.КоличествоПроверки КАК КоличествоПроверки,
	|	ТаблицаИсточник.РасчетнаяБазаИсходная КАК РасчетнаяБазаИсходная,
	|	ТаблицаИсточник.РасчетнаяБаза КАК РасчетнаяБазаДоКорректировки,
	|	ТаблицаИсточник.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонусаДоКорректировки,
	|	ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ПриоритетОкругления, -1) КАК ПриоритетОкругления,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ПриоритетОкругления, -1)
	|			ТОГДА &ВыражениеРасчетнаяБаза
	|		ИНАЧЕ ТаблицаИсточник.РасчетнаяБаза
	|	КОНЕЦ КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ПриоритетОкругления, -1)
	|			ТОГДА ТаблицаИсточник.КоличествоДляРасчетаБонуса + ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ШиринаДиапазона, 0)
	|				- ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.КоличествоДляРасчетаБонусаИтог, 0)
	|		ИНАЧЕ ТаблицаИсточник.КоличествоДляРасчетаБонуса
	|	КОНЕЦ КАК КоличествоДляРасчетаБонуса,
	|	ТаблицаИсточник.СуммаБонус КАК СуммаБонусДоКорректировки,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ПриоритетОкругления, -1)
	|			ТОГДА ВЫРАЗИТЬ(&ВыражениеРасчетнаяБаза * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаИсточник.РасчетнаяБаза * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2))
	|	КОНЕЦ КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО
	|ИЗ
	|	ВТ_РаспределениеСтрокПоДиапазонам КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиБазыПоШиринеДиапазонов КАК ВТ_ИтогиБазыПоШиринеДиапазонов
	|		ПО ТаблицаИсточник.Валюта = ВТ_ИтогиБазыПоШиринеДиапазонов.Валюта
	|			И ТаблицаИсточник.ПериодРасчета = ВТ_ИтогиБазыПоШиринеДиапазонов.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ТаблицаИсточник.НомерДиапазона = ВТ_ИтогиБазыПоШиринеДиапазонов.НомерДиапазона";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		Если ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
			
			ВыражениеРасчетнаяБаза = "(ТаблицаИсточник.РасчетнаяБаза + ВЫРАЗИТЬ(ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ШиринаДиапазона, 0) * ТаблицаИсточник.БазоваяЦена КАК ЧИСЛО(31, 2))
				|- ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.РасчетнаяБазаИтог, 0))";
			
		Иначе
			
			ВыражениеРасчетнаяБаза = "ТаблицаИсточник.РасчетнаяБаза";
			
		КонецЕсли;
		
	Иначе
		
		ВыражениеРасчетнаяБаза = "ТаблицаИсточник.РасчетнаяБаза + ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.ШиринаДиапазона, 0)
			|- ЕСТЬNULL(ВТ_ИтогиБазыПоШиринеДиапазонов.РасчетнаяБазаИтог, 0)";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеРасчетнаяБаза", ВыражениеРасчетнаяБаза);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ИтогиБазыПоШиринеДиапазонов");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник", "ВТ_ИтогиБазыПоШиринеДиапазонов");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаОстаткиБазыПоДиапазонамФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	ТекстПодзапросаЛевыеЧастиСтрок =
	"ВЫБРАТЬ
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Распределено КАК Распределено,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Валюта КАК Валюта,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Приоритет КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ТаблицаИсточник.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ТаблицаИсточник.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ТаблицаИсточник.ДиапазонОт
	|		ИНАЧЕ ТаблицаИсточник.ДиапазонПо - ТаблицаИсточник.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПриоритетОкругления КАК ПриоритетОкругления,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	-1 * ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	-1 * ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.РасчетнаяБаза КАК РасчетнаяБаза
	|ПОМЕСТИТЬ ВТ_ОстаткиБазыПоДиапазонамФИФО
	|ИЗ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ТаблицаИсточник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО КАК ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО
	|		ПО ТаблицаИсточник.Валюта = ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Валюта
	|			И ТаблицаИсточник.ПериодРасчета = ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПериодРасчета
	|			И ТаблицаИсточник.ПриоритетОт = ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Приоритет
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И (ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Распределено = ИСТИНА)
	|ГДЕ
	|	ТаблицаИсточник.ДиапазонОстатокОт <> 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧастиСтрок,
		"ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО",
		"ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧастиСтрок,
		"ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО",
		"ТаблицаИсточник");
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		ВыражениеВеличинаПроверки = "ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.КоличествоПроверки";
	Иначе
		ВыражениеВеличинаПроверки = "ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.СуммаПроверки";
	КонецЕсли;
	ТекстПодзапросаЛевыеЧастиСтрок = СтрЗаменить(
		ТекстПодзапросаЛевыеЧастиСтрок, "&ВыражениеВеличинаПроверки", ВыражениеВеличинаПроверки);
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаЛевыеЧастиСтрок);
	
	ТекстПодзапросаИсходныеСтроки =
	"ВЫБРАТЬ
	|	ЛОЖЬ КАК Распределено,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.ДиапазонПо = 0
	|			ТОГДА &ВыражениеВеличинаПроверки - ТаблицаИсточник.ДиапазонОт
	|		КОГДА &ВыражениеВеличинаПроверки <= ТаблицаИсточник.ДиапазонПо
	|			ТОГДА &ВыражениеВеличинаПроверки - ТаблицаИсточник.ДиапазонОт
	|		ИНАЧЕ ТаблицаИсточник.ДиапазонПо - ТаблицаИсточник.ДиапазонОт
	|	КОНЕЦ КАК ШиринаДиапазона,
	|	-1 КАК ПриоритетОкругления,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПодготовленныеДанные.Количество КАК КоличествоДляРасчетаБонуса,
	|	&ВыражениеРасчетнаяБаза КАК РасчетнаяБаза
	|ИЗ
	|	ВТ_ПриоритетыОстаткиНаГраницахДиапазонов КАК ТаблицаИсточник
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ТаблицаИсточник.Валюта = ВТ_ПодготовленныеДанные.Валюта
	|			И ТаблицаИсточник.ПериодРасчета = ВТ_ПодготовленныеДанные.ПериодРасчета
	|			И ТаблицаИсточник.ПриоритетОт = ВТ_ПодготовленныеДанные.Приоритет
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам 
	|ГДЕ
	|	ТаблицаИсточник.ДиапазонОстатокОт <> 0";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаИсходныеСтроки,
		"ВТ_ПодготовленныеДанные",
		"ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаИсходныеСтроки,
		"ВТ_ПодготовленныеДанные",
		"ТаблицаИсточник");
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ВыражениеВеличинаПроверки = "ВТ_ПодготовленныеДанные.КоличествоПроверки";
		
		Если ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
			
			ВыражениеРасчетнаяБаза = "ВЫРАЗИТЬ(ВТ_ПодготовленныеДанные.Количество * ТаблицаИсточник.БазоваяЦена КАК ЧИСЛО(31, 2))";
			
		Иначе
			
			ВыражениеРасчетнаяБаза = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
			
		КонецЕсли;
		
	Иначе
		
		ВыражениеВеличинаПроверки = "ВТ_ПодготовленныеДанные.СуммаПроверки";
		ВыражениеРасчетнаяБаза = "ВТ_ПодготовленныеДанные.РасчетнаяБаза";
		
	КонецЕсли;
	
	ТекстПодзапросаИсходныеСтроки = СтрЗаменить(ТекстПодзапросаИсходныеСтроки, "&ВыражениеВеличинаПроверки", ВыражениеВеличинаПроверки);
	ТекстПодзапросаИсходныеСтроки = СтрЗаменить(ТекстПодзапросаИсходныеСтроки, "&ВыражениеРасчетнаяБаза", ВыражениеРасчетнаяБаза);
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаИсходныеСтроки);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаДанныеДляРаспределенияБонусаПоДиапазонамФИФО(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	// В первой выборке разворачиваем "левые" части строк граничных документов по диапазонам, базу берем как остаток после распределения по диапазонам.
	// Во второй выборке дополняем уже распределенными "правыми" частями строк.
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	ТекстПодзапросаЛевыеЧасти =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ШиринаДиапазона КАК ШиринаДиапазона,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.Приоритет КАК Приоритет,
	|	МАКСИМУМ(ВТ_ОстаткиБазыПоДиапазонамФИФО.ПриоритетОкругления) КАК ПриоритетОкругления,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	0 КАК Количество,
	|	0 КАК СуммаПродажи,
	|	0 КАК СуммаОборотСНДС,
	|	0 КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ИСТИНА КАК ПланВыполнен,
	|	0 КАК КоличествоПлан,
	|	СУММА(ВТ_ОстаткиБазыПоДиапазонамФИФО.КоличествоДляРасчетаБонуса) КАК КоличествоДляРасчетаБонуса,
	|	0 КАК КоличествоПакетов,
	|	СУММА(ВТ_ОстаткиБазыПоДиапазонамФИФО.РасчетнаяБаза) КАК РасчетнаяБаза,
	|	ВЫРАЗИТЬ(СУММА(ВТ_ОстаткиБазыПоДиапазонамФИФО.РасчетнаяБаза) * ВТ_ОстаткиБазыПоДиапазонамФИФО.БонусПроцент
	|		/ 100 КАК ЧИСЛО(31, 2)) КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_ДанныеДляРаспределенияБонусаПоДиапазонамФИФО
	|ИЗ
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО КАК ВТ_ОстаткиБазыПоДиапазонамФИФО
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ВТ_ОстаткиБазыПоДиапазонамФИФО.ДокументРегистратор = ВТ_ПодготовленныеДанные.ДокументРегистратор
	|			И ВТ_ОстаткиБазыПоДиапазонамФИФО.Приоритет = ВТ_ПодготовленныеДанные.Приоритет
	|			И &ПоляСоединенияПоТоварам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ПериодРасчета,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.НомерДиапазона,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДиапазонОт,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДиапазонПо,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.БонусПроцент,
	|	&ВыражениеБазоваяЦена,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ШиринаДиапазона,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки,
	|	ВТ_ОстаткиБазыПоДиапазонамФИФО.Приоритет,
	|	ВТ_ПодготовленныеДанные.Организация,
	|	&ПоляГруппировкиПоУчастникам,
	|	ВТ_ПодготовленныеДанные.Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.Валюта,
	|	ВТ_ПодготовленныеДанные.СуммаПлан,
	|	ВТ_ПодготовленныеДанные.Количество";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		ВыражениеБазоваяЦена = "ВТ_ОстаткиБазыПоДиапазонамФИФО.БазоваяЦена";
	Иначе
		ВыражениеБазоваяЦена = "ВТ_ПодготовленныеДанные.БазоваяЦена";
	КонецЕсли;
	ТекстПодзапросаЛевыеЧасти = СтрЗаменить(ТекстПодзапросаЛевыеЧасти, "&ВыражениеБазоваяЦена", ВыражениеБазоваяЦена);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧасти,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ОстаткиБазыПоДиапазонамФИФО");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаЛевыеЧасти,
		"ВТ_ПодготовленныеДанные",
		"ВТ_ОстаткиБазыПоДиапазонамФИФО");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаЛевыеЧасти);
	
	ТекстПодзапросаПравыеЧасти =
	"ВЫБРАТЬ
	|	ВТ_ПодготовленныеДанные.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ВТ_ПодготовленныеДанные.ПоказательТоваров КАК ПоказательТоваров,
	|	ВТ_ПодготовленныеДанные.Период КАК Период,
	|	ВТ_ПодготовленныеДанные.ПериодРасчета КАК ПериодРасчета,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ШиринаДиапазона КАК ШиринаДиапазона,
	|	ВТ_ПодготовленныеДанные.ДокументРегистратор КАК ДокументРегистратор,
	|	ВТ_ПодготовленныеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ПодготовленныеДанные.Приоритет КАК Приоритет,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПриоритетОкругления КАК ПриоритетОкругления,
	|	ВТ_ПодготовленныеДанные.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ВТ_ПодготовленныеДанные.Договор КАК Договор,
	|	ВТ_ПодготовленныеДанные.Соглашение КАК Соглашение,
	|	ВТ_ПодготовленныеДанные.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ВТ_ПодготовленныеДанные.Номенклатура КАК Номенклатура,
	|	ВТ_ПодготовленныеДанные.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ВТ_ПодготовленныеДанные.Характеристика КАК Характеристика,
	|	ВТ_ПодготовленныеДанные.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ВТ_ПодготовленныеДанные.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ВТ_ПодготовленныеДанные.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ВТ_ПодготовленныеДанные.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ВТ_ПодготовленныеДанные.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ВТ_ПодготовленныеДанные.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ВТ_ПодготовленныеДанные.Валюта КАК Валюта,
	|	ВЫБОР
	|		КОГДА ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПервыйДиапазонДокумента
	|			ТОГДА ВТ_ПодготовленныеДанные.Количество
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Количество,
	|	ВЫБОР
	|		КОГДА ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПервыйДиапазонДокумента
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаПродажи
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПродажи,
	|	ВЫБОР
	|		КОГДА ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПервыйДиапазонДокумента
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотСНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОборотСНДС,
	|	ВЫБОР
	|		КОГДА ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ПервыйДиапазонДокумента
	|			ТОГДА ВТ_ПодготовленныеДанные.СуммаОборотБезНДС
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаОборотБезНДС,
	|	ВТ_ПодготовленныеДанные.СуммаПлан КАК СуммаПлан,
	|	ИСТИНА КАК ПланВыполнен,
	|	0 КАК КоличествоПлан,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.КоличествоДляРасчетаБонуса КАК КоличествоДляРасчетаБонуса,
	|	0 КАК КоличествоПакетов,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.РасчетнаяБаза КАК РасчетнаяБаза,
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.СуммаБонус КАК СуммаБонус
	|ИЗ
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО КАК ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПодготовленныеДанные КАК ВТ_ПодготовленныеДанные
	|		ПО ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.ДокументРегистратор = ВТ_ПодготовленныеДанные.ДокументРегистратор
	|			И ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Приоритет = ВТ_ПодготовленныеДанные.Приоритет
	|			И &ПоляСоединенияПоТоварам
	|ГДЕ
	|	ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.Распределено = ИСТИНА";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		ВыражениеБазоваяЦена = "ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО.БазоваяЦена";
	Иначе
		ВыражениеБазоваяЦена = "ВТ_ПодготовленныеДанные.БазоваяЦена";
	КонецЕсли;
	ТекстПодзапросаПравыеЧасти = СтрЗаменить(ТекстПодзапросаПравыеЧасти, "&ВыражениеБазоваяЦена", ВыражениеБазоваяЦена);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПравыеЧасти,
		"ВТ_ПодготовленныеДанные",
		"ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПравыеЧасти,
		"ВТ_ПодготовленныеДанные",
		"ВТ_КорректировкаПогрешностиБазыПоДиапазонамФИФО");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаПравыеЧасти);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтогиБонусаПоШиринеДиапазонов(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ШиринаДиапазона КАК ШиринаДиапазона,
	|	&ВыражениеСуммаБонусПоШиринеДиапазона КАК СуммаБонусПоШиринеДиапазона,
	|	МАКСИМУМ(
	|		ВЫБОР
	|			КОГДА ТаблицаИсточник.РасчетнаяБаза > 0
	|				ТОГДА ТаблицаИсточник.Приоритет
	|			ИНАЧЕ -1
	|		КОНЕЦ) КАК ПриоритетОкругления,
	|	СУММА(ТаблицаИсточник.СуммаБонус) КАК СуммаБонусИтог
	|ПОМЕСТИТЬ ВТ_ИтогиБонусаПоШиринеДиапазонов
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаИсточник.Валюта,
	|	ТаблицаИсточник.ПериодРасчета,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ТаблицаИсточник.НомерДиапазона,
	|	ТаблицаИсточник.ШиринаДиапазона,
	|	&ВыражениеГруппировкиПоБазовойЦене,
	|	ТаблицаИсточник.БонусПроцент";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		Если ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
			
			ВыражениеСуммаБонусПоШиринеДиапазона = "ВЫРАЗИТЬ(ТаблицаИсточник.ШиринаДиапазона * ТаблицаИсточник.БазоваяЦена * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2))";
			ВыражениеГруппировкиПоБазовойЦене = "ТаблицаИсточник.БазоваяЦена";
			
		Иначе
			
			ВыражениеСуммаБонусПоШиринеДиапазона = "ВЫРАЗИТЬ(СУММА(ТаблицаИсточник.РасчетнаяБаза) * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2))";
			ВыражениеГруппировкиПоБазовойЦене = "NULL";
			
		КонецЕсли;
		
		ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияБонусаПоДиапазонамФИФО";
		
	Иначе
		
		ВыражениеСуммаБонусПоШиринеДиапазона = "ВЫРАЗИТЬ(ТаблицаИсточник.ШиринаДиапазона * ТаблицаИсточник.БонусПроцент / 100 КАК ЧИСЛО(31, 2))";
		ВыражениеГруппировкиПоБазовойЦене = "NULL";
		ТаблицаИсточник = "ВТ_РаспределениеСтрокПоДиапазонам";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаБонусПоШиринеДиапазона", ВыражениеСуммаБонусПоШиринеДиапазона);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеГруппировкиПоБазовойЦене", ВыражениеГруппировкиПоБазовойЦене);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ТаблицаИсточник", ТаблицаИсточник);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ТаблицаИсточник");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаИтоговоеРаспределениеПоФИФОПоДиапазонам(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	ТекстПодзапросаПоложительныеОбороты =
	"ВЫБРАТЬ
	|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
	|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
	|	ТаблицаИсточник.Период КАК Период,
	|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
	|	ТаблицаИсточник.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаИсточник.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаИсточник.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаИсточник.БонусПроцент КАК БонусПроцент,
	|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
	|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
	|	ТаблицаИсточник.Приоритет КАК Приоритет,
	|	ТаблицаИсточник.Организация КАК Организация,
	|	&ПоляУчастники,
	|	ТаблицаИсточник.Договор КАК Договор,
	|	ТаблицаИсточник.Соглашение КАК Соглашение,
	|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
	|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
	|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаИсточник.Характеристика КАК Характеристика,
	|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
	|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
	|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
	|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
	|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
	|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
	|	ТаблицаИсточник.Валюта КАК Валюта,
	|	ТаблицаИсточник.Количество КАК Количество,
	|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
	|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
	|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
	|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
	|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
	|	ИСТИНА КАК ПланВыполнен,
	|	0 КАК КоличествоПлан,
	|	ТаблицаИсточник.Количество КАК КоличествоДляРасчетаБонуса,
	|	0 КАК КоличествоПакетов,
	|	ТаблицаИсточник.РасчетнаяБаза КАК РасчетнаяБаза,
	|	ВЫБОР
	|		КОГДА ТаблицаИсточник.Приоритет = ЕСТЬNULL(ВТ_ИтогиБонусаПоШиринеДиапазонов.ПриоритетОкругления, -1)
	|			ТОГДА (ТаблицаИсточник.СуммаБонус + ЕСТЬNULL(ВТ_ИтогиБонусаПоШиринеДиапазонов.СуммаБонусПоШиринеДиапазона, 0)
	|				- ЕСТЬNULL(ВТ_ИтогиБонусаПоШиринеДиапазонов.СуммаБонусИтог, 0)
	|			)
	|		ИНАЧЕ ТаблицаИсточник.СуммаБонус
	|	КОНЕЦ КАК СуммаБонус
	|ПОМЕСТИТЬ ВТ_ИтоговоеРаспределениеПоФИФО
	|ИЗ
	|	&ТаблицаИсточник КАК ТаблицаИсточник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиБонусаПоШиринеДиапазонов КАК ВТ_ИтогиБонусаПоШиринеДиапазонов
	|		ПО ТаблицаИсточник.Валюта = ВТ_ИтогиБонусаПоШиринеДиапазонов.Валюта
	|			И ТаблицаИсточник.ПериодРасчета = ВТ_ИтогиБонусаПоШиринеДиапазонов.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|			И ТаблицаИсточник.НомерДиапазона = ВТ_ИтогиБонусаПоШиринеДиапазонов.НомерДиапазона";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		ТаблицаИсточник = "ВТ_ДанныеДляРаспределенияБонусаПоДиапазонамФИФО";
		
	Иначе
		
		ТаблицаИсточник = "ВТ_РаспределениеСтрокПоДиапазонам";
		
	КонецЕсли;
	ТекстПодзапросаПоложительныеОбороты = СтрЗаменить(ТекстПодзапросаПоложительныеОбороты, "&ТаблицаИсточник", ТаблицаИсточник);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПоложительныеОбороты,
		"ТаблицаИсточник",
		"ВТ_ИтогиБонусаПоШиринеДиапазонов");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов,
		ТекстПодзапросаПоложительныеОбороты,
		"ТаблицаИсточник",
		"ВТ_ИтогиБонусаПоШиринеДиапазонов");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаПоложительныеОбороты);
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		ТекстПодзапросаОтрицательныеОбороты =
		"ВЫБРАТЬ
		|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
		|	ТаблицаИсточник.Период КАК Период,
		|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
		|	ЕСТЬNULL(ВТ_ШкалаРасчета.НомерДиапазона, 0) КАК НомерДиапазона,
		|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонОт, 0) КАК ДиапазонОт,
		|	ЕСТЬNULL(ВТ_ШкалаРасчета.ДиапазонПо, 0) КАК ДиапазонПо,
		|	ЕСТЬNULL(ВТ_ШкалаРасчета.БонусПроцент, 0) КАК БонусПроцент,
		|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
		|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
		|	ТаблицаИсточник.Приоритет КАК Приоритет,
		|	ТаблицаИсточник.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ТаблицаИсточник.Договор КАК Договор,
		|	ТаблицаИсточник.Соглашение КАК Соглашение,
		|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
		|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаИсточник.Характеристика КАК Характеристика,
		|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ТаблицаИсточник.Валюта КАК Валюта,
		|	ТаблицаИсточник.Количество КАК Количество,
		|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
		|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ТаблицаИсточник.БазоваяЦена КАК БазоваяЦена,
		|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
		|	ЛОЖЬ КАК ПланВыполнен,
		|	0 КАК КоличествоПлан,
		|	ТаблицаИсточник.Количество КАК КоличествоДляРасчетаБонуса,
		|	0 КАК КоличествоПакетов,
		|	0 КАК РасчетнаяБаза,
		|	0 КАК СуммаБонус
		|ИЗ
		|	ВТ_ПодготовленныеДанные КАК ТаблицаИсточник
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ШкалаРасчета КАК ВТ_ШкалаРасчета
		|		ПО ВТ_ШкалаРасчета.ДиапазонОт = 0
		|ГДЕ
		|	ТаблицаИсточник.СуммаПроверки <= 0";
		
	Иначе
		
		ТекстПодзапросаОтрицательныеОбороты =
		"ВЫБРАТЬ
		|	ТаблицаИсточник.УсловиеРетроБонуса КАК УсловиеРетроБонуса,
		|	ТаблицаИсточник.ПоказательТоваров КАК ПоказательТоваров,
		|	ТаблицаИсточник.Период КАК Период,
		|	ТаблицаИсточник.ПериодРасчета КАК ПериодРасчета,
		|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.НомерДиапазона, 0) КАК НомерДиапазона,
		|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонОт, 0) КАК ДиапазонОт,
		|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.ДиапазонПо, 0) КАК ДиапазонПо,
		|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БонусПроцент, 0) КАК БонусПроцент,
		|	ТаблицаИсточник.ДокументРегистратор КАК ДокументРегистратор,
		|	ТаблицаИсточник.НомерСтроки КАК НомерСтроки,
		|	ТаблицаИсточник.Приоритет КАК Приоритет,
		|	ТаблицаИсточник.Организация КАК Организация,
		|	&ПоляУчастники,
		|	ТаблицаИсточник.Договор КАК Договор,
		|	ТаблицаИсточник.Соглашение КАК Соглашение,
		|	ТаблицаИсточник.ХарактеристикиИспользуются КАК ХарактеристикиИспользуются,
		|	ТаблицаИсточник.Номенклатура КАК Номенклатура,
		|	ТаблицаИсточник.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
		|	ТаблицаИсточник.Характеристика КАК Характеристика,
		|	ТаблицаИсточник.ЗначениеСвойства КАК ЗначениеСвойства,
		|	ТаблицаИсточник.ХарактеристикаПродажЗакупок КАК ХарактеристикаПродажЗакупок,
		|	ТаблицаИсточник.ДетализацияРасчетаУчастников КАК ДетализацияРасчетаУчастников,
		|	ТаблицаИсточник.ПериодичностьНачислений КАК ПериодичностьНачислений,
		|	ТаблицаИсточник.ПериодичностьУсловий КАК ПериодичностьУсловий,
		|	ТаблицаИсточник.ЗапретНачисленияСверхПлана КАК ЗапретНачисленияСверхПлана,
		|	ТаблицаИсточник.Валюта КАК Валюта,
		|	ТаблицаИсточник.Количество КАК Количество,
		|	ТаблицаИсточник.СуммаПродажи КАК СуммаПродажи,
		|	ТаблицаИсточник.СуммаОборотСНДС КАК СуммаОборотСНДС,
		|	ТаблицаИсточник.СуммаОборотБезНДС КАК СуммаОборотБезНДС,
		|	ЕСТЬNULL(ВТ_ТоварыДиапазоны.БазоваяЦена, 0) КАК БазоваяЦена,
		|	ТаблицаИсточник.СуммаПлан КАК СуммаПлан,
		|	ЛОЖЬ КАК ПланВыполнен,
		|	0 КАК КоличествоПлан,
		|	ТаблицаИсточник.Количество КАК КоличествоДляРасчетаБонуса,
		|	0 КАК КоличествоПакетов,
		|	0 КАК РасчетнаяБаза,
		|	0 КАК СуммаБонус
		|ИЗ
		|	ВТ_ПодготовленныеДанные КАК ТаблицаИсточник
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ТоварыДиапазоны КАК ВТ_ТоварыДиапазоны
		|		ПО ВТ_ТоварыДиапазоны.ДиапазонОт = 0
		|			И &ПоляСоединенияПоТоварам
		|ГДЕ
		|	ТаблицаИсточник.КоличествоПроверки <= 0";
		
		ПодставитьПоляПоТоварам(
			ПараметрыУсловийРетроБонусов,
			ТекстПодзапросаОтрицательныеОбороты,
			"ТаблицаИсточник",
			"ВТ_ТоварыДиапазоны");
		
	КонецЕсли;
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстПодзапросаОтрицательныеОбороты, "ТаблицаИсточник");
	
	ТекстыПодзапросов.Добавить(ТекстПодзапросаОтрицательныеОбороты);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПриоритетыНаГраницахДиапазонов(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВТ_СтрокиНаГраницахДиапазонов.Валюта КАК Валюта,
	|	ВТ_СтрокиНаГраницахДиапазонов.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_СтрокиНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_СтрокиНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_СтрокиНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_СтрокиНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	ВТ_СтрокиНаГраницахДиапазонов.БазоваяЦена КАК БазоваяЦена,
	|	ЕСТЬNULL(МИНИМУМ(
	|		ВЫБОР
	|			КОГДА ВТ_СтрокиНаГраницахДиапазонов.ДиапазонОт = 0
	|				ТОГДА -1
	|			КОГДА &ВыражениеИтогДоДокумента < ВТ_СтрокиНаГраницахДиапазонов.ДиапазонОт
	|				ТОГДА ВТ_СтрокиНаГраницахДиапазонов.Приоритет
	|			ИНАЧЕ NULL
	|		КОНЕЦ), -1) КАК ПриоритетОт,
	|	ЕСТЬNULL(МАКСИМУМ(
	|		ВЫБОР
	|			КОГДА ВТ_СтрокиНаГраницахДиапазонов.ДиапазонПо = 0
	|				ТОГДА -1
	|			КОГДА &ВыражениеОборотыЗаПериод <= ВТ_СтрокиНаГраницахДиапазонов.ДиапазонПо
	|				ТОГДА -1
	|			КОГДА &ВыражениеИтогПослеДокумента >= ВТ_СтрокиНаГраницахДиапазонов.ДиапазонПо
	|				ТОГДА ВТ_СтрокиНаГраницахДиапазонов.Приоритет
	|			ИНАЧЕ NULL
	|		КОНЕЦ), -1) КАК ПриоритетПо
	|ПОМЕСТИТЬ ВТ_ПриоритетыНаГраницахДиапазонов
	|ИЗ
	|	ВТ_СтрокиНаГраницахДиапазонов КАК ВТ_СтрокиНаГраницахДиапазонов
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ОборотыПоПериодам КАК ОборотыПоПериодам
	|		ПО ВТ_СтрокиНаГраницахДиапазонов.Валюта = ОборотыПоПериодам.Валюта
	|			И ВТ_СтрокиНаГраницахДиапазонов.ПериодРасчета = ОборотыПоПериодам.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_СтрокиНаГраницахДиапазонов.Валюта,
	|	ВТ_СтрокиНаГраницахДиапазонов.ПериодРасчета,
	|	&ПоляГруппировкиПоУчастникам,
	|	&ПоляГруппировкиПоПолямТоваров,
	|	ВТ_СтрокиНаГраницахДиапазонов.НомерДиапазона,
	|	ВТ_СтрокиНаГраницахДиапазонов.ДиапазонОт,
	|	ВТ_СтрокиНаГраницахДиапазонов.ДиапазонПо,
	|	ВТ_СтрокиНаГраницахДиапазонов.БонусПроцент,
	|	ВТ_СтрокиНаГраницахДиапазонов.БазоваяЦена";
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		ВыражениеИтогДоДокумента = "ВТ_СтрокиНаГраницахДиапазонов.СуммаНарастающимИтогом - ВТ_СтрокиНаГраницахДиапазонов.СуммаСтроки";
		ВыражениеИтогПослеДокумента = "ВТ_СтрокиНаГраницахДиапазонов.СуммаНарастающимИтогом";
		ВыражениеОборотыЗаПериод = "ОборотыПоПериодам.Сумма";
		
	Иначе
		
		ВыражениеИтогДоДокумента = "ВТ_СтрокиНаГраницахДиапазонов.КоличествоНарастающимИтогом - ВТ_СтрокиНаГраницахДиапазонов.КоличествоСтроки";
		ВыражениеИтогПослеДокумента = "ВТ_СтрокиНаГраницахДиапазонов.КоличествоНарастающимИтогом";
		ВыражениеОборотыЗаПериод = "ОборотыПоПериодам.Количество";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеИтогДоДокумента", ВыражениеИтогДоДокумента);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеИтогПослеДокумента", ВыражениеИтогПослеДокумента);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеОборотыЗаПериод", ВыражениеОборотыЗаПериод);
	
	ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СтрокиНаГраницахДиапазонов", "ОборотыПоПериодам");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, "ВТ_СтрокиНаГраницахДиапазонов", "ОборотыПоПериодам");
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

// Параметры:
//  ПараметрыУсловийРетроБонусов - См. НовыеПараметрыУсловийРетроБонусов
//  ПараметрыВарианта - См. НовыеПараметрыВарианта
//  ТекстыЗапросов - Массив Из Строка
//
Процедура ДобавитьТекстЗапросаПриоритетыОстаткиНаГраницахДиапазонов(ПараметрыУсловийРетроБонусов, ПараметрыВарианта, ТекстыЗапросов) Экспорт
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		ТаблицаШкалаРасчета = "ВТ_ШкалаРасчета";
		ВыражениеБазоваяЦена = "0";
		
	Иначе
		
		ТаблицаШкалаРасчета = "ВТ_ТоварыДиапазоны";
		ВыражениеБазоваяЦена = "ТаблицаШкалаРасчета.БазоваяЦена";
		
	КонецЕсли;
	
	ТекстыПодзапросов = Новый Массив; // Массив Из Строка
	
	//@skip-check bsl-ql-hub - текст запроса собирается из нескольких частей
	ТекстПодзапроса =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ_ПриоритетыНаГраницахДиапазонов.Валюта КАК Валюта,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.НомерДиапазона КАК НомерДиапазона,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ДиапазонОт КАК ДиапазонОт,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ДиапазонПо КАК ДиапазонПо,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.БонусПроцент КАК БонусПроцент,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.БазоваяЦена КАК БазоваяЦена,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ПриоритетОт КАК ПриоритетОт,
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ПриоритетПо КАК ПриоритетПо,
	|	ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновОт.СуммаСтроки, 0) КАК СуммаСтрокиОт,
	|	ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновОт.КоличествоСтроки, 0) КАК КоличествоСтрокиОт,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновОт.Приоритет, -1) <> ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновПо.Приоритет, -1)
	|			ТОГДА ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновОт.ДиапазонОстаток, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ДиапазонОстатокОт,
	|	ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновПо.СуммаСтроки, 0) КАК СуммаСтрокиПо,
	|	ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновПо.КоличествоСтроки, 0) КАК КоличествоСтрокиПо,
	|	ЕСТЬNULL(ВТ_СтрокиНаГраницахДиапазоновПо.ДиапазонОстаток, 0) КАК ДиапазонОстатокПо
	|ПОМЕСТИТЬ ВТ_ПриоритетыОстаткиНаГраницахДиапазонов
	|ИЗ
	|	ВТ_ПриоритетыНаГраницахДиапазонов КАК ВТ_ПриоритетыНаГраницахДиапазонов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтрокиНаГраницахДиапазонов КАК ВТ_СтрокиНаГраницахДиапазоновОт
	|		ПО ВТ_ПриоритетыНаГраницахДиапазонов.ПриоритетОт = ВТ_СтрокиНаГраницахДиапазоновОт.Приоритет
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.НомерДиапазона = ВТ_СтрокиНаГраницахДиапазоновОт.НомерДиапазона
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.Валюта = ВТ_СтрокиНаГраницахДиапазоновОт.Валюта
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.ПериодРасчета = ВТ_СтрокиНаГраницахДиапазоновОт.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам";
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПриоритетыНаГраницахДиапазонов", "ВТ_СтрокиНаГраницахДиапазоновОт");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ПриоритетыНаГраницахДиапазонов", "ВТ_СтрокиНаГраницахДиапазоновОт");
	
	ТекстСоединенияГраницаПо = "
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтрокиНаГраницахДиапазонов КАК ВТ_СтрокиНаГраницахДиапазоновПо
	|		ПО ВТ_ПриоритетыНаГраницахДиапазонов.ПриоритетПо = ВТ_СтрокиНаГраницахДиапазоновПо.Приоритет
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.НомерДиапазона = ВТ_СтрокиНаГраницахДиапазоновПо.НомерДиапазона
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.Валюта = ВТ_СтрокиНаГраницахДиапазоновПо.Валюта
	|			И ВТ_ПриоритетыНаГраницахДиапазонов.ПериодРасчета = ВТ_СтрокиНаГраницахДиапазоновПо.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам";
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстСоединенияГраницаПо, "ВТ_ПриоритетыНаГраницахДиапазонов", "ВТ_СтрокиНаГраницахДиапазоновПо");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстСоединенияГраницаПо, "ВТ_ПриоритетыНаГраницахДиапазонов", "ВТ_СтрокиНаГраницахДиапазоновПо");
	
	ТекстПодзапроса = ТекстПодзапроса + ТекстСоединенияГраницаПо;
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	ТекстПодзапроса =
	"ВЫБРАТЬ
	|	ВТ_ОборотыПоПериодам.Валюта КАК Валюта,
	|	ВТ_ОборотыПоПериодам.ПериодРасчета КАК ПериодРасчета,
	|	&ПоляУчастники,
	|	&ПоляТоваров,
	|	ТаблицаШкалаРасчета.НомерДиапазона КАК НомерДиапазона,
	|	ТаблицаШкалаРасчета.ДиапазонОт КАК ДиапазонОт,
	|	ТаблицаШкалаРасчета.ДиапазонПо КАК ДиапазонПо,
	|	ТаблицаШкалаРасчета.БонусПроцент КАК БонусПроцент,
	|	&ВыражениеБазоваяЦена КАК БазоваяЦена,
	|	-1 КАК ПриоритетОт,
	|	-1 КАК ПриоритетПо,
	|	0 КАК СуммаДокументаОт,
	|	0 КАК КоличествоДокументаОт,
	|	0 КАК ДиапазонОстатокОт,
	|	0 КАК СуммаДокументаПо,
	|	0 КАК КоличествоДокументаПо,
	|	0 КАК ДиапазонОстатокПо
	|ИЗ
	|	ВТ_ОборотыПоПериодам КАК ВТ_ОборотыПоПериодам
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ &ТаблицаШкалаРасчета КАК ТаблицаШкалаРасчета
	|		ПО ТаблицаШкалаРасчета.ДиапазонОт = 0
	|			И &ПоляСоединенияПоТоварам";
	
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ТаблицаШкалаРасчета", ТаблицаШкалаРасчета);
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&ВыражениеБазоваяЦена", ВыражениеБазоваяЦена);
	
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ОборотыПоПериодам", "ВТ_ПриоритетыНаГраницахДиапазонов");
	ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстПодзапроса, "ВТ_ОборотыПоПериодам", "ТаблицаШкалаРасчета");
	
	ТекстСоединенияПриоритеты = "
	|			И &УсловиеОборотовПоПериоду
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПриоритетыНаГраницахДиапазонов КАК ВТ_ПриоритетыНаГраницахДиапазонов
	|		ПО ТаблицаШкалаРасчета.ДиапазонОт = ВТ_ПриоритетыНаГраницахДиапазонов.ДиапазонОт
	|			И ВТ_ОборотыПоПериодам.Валюта = ВТ_ПриоритетыНаГраницахДиапазонов.Валюта
	|			И ВТ_ОборотыПоПериодам.ПериодРасчета = ВТ_ПриоритетыНаГраницахДиапазонов.ПериодРасчета
	|			И &ПоляСоединенияПоУчастникам
	|			И &ПоляСоединенияПоТоварам
	|ГДЕ
	|	ВТ_ПриоритетыНаГраницахДиапазонов.ПриоритетОт ЕСТЬ NULL";
	ПодставитьПоляПоУчастникам(
		ПараметрыУсловийРетроБонусов, ТекстСоединенияПриоритеты, "ВТ_ОборотыПоПериодам", "ВТ_ПриоритетыНаГраницахДиапазонов");
	ПодставитьПоляПоТоварам(
		ПараметрыУсловийРетроБонусов, ТекстСоединенияПриоритеты, "ВТ_ОборотыПоПериодам", "ВТ_ПриоритетыНаГраницахДиапазонов");
	ТекстПодзапроса = ТекстПодзапроса + ТекстСоединенияПриоритеты;
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		УсловиеОборотовПоПериоду = "ВТ_ОборотыПоПериодам.Сумма >= 0";
		
	Иначе
		
		УсловиеОборотовПоПериоду = "ВТ_ОборотыПоПериодам.Количество >= 0";
		
	КонецЕсли;
	
	ТекстПодзапроса = СтрЗаменить(ТекстПодзапроса, "&УсловиеОборотовПоПериоду", УсловиеОборотовПоПериоду);
	
	ТекстыПодзапросов.Добавить(ТекстПодзапроса);
	
	РазделительПодзапросов = РазделительПодзапросов();
	ТекстЗапроса = СтрСоединить(ТекстыПодзапросов, РазделительПодзапросов);
	
	ТекстыЗапросов.Добавить(ТекстЗапроса);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПодставитьПоляПоУчастникам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыСвязи = "", ПустыеЗначения = Ложь)
	
	ДетализацияРасчетаУчастников = ПараметрыУсловийРетроБонусов.ДетализацияРасчетаУчастников;
	ДетализацииУчастников = Перечисления.ДетализацияРасчетаУчастниковРетроБонусов;
	
	Если ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКлиенту Тогда
		
		ПоляУчастники = ИмяТаблицы + ".Партнер КАК Партнер";
		Если ПустыеЗначения Тогда
			
			ПоляУчастники = ПоляУчастники + ",
			|ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка) КАК Контрагент";
			
		КонецЕсли;
		
		ПоляСоединенияПоУчастникам = СтрШаблон("%1.Партнер = %2.Партнер", ИмяТаблицы, ИмяТаблицыСвязи);
		ПоляГруппировкиПоУчастникам = ИмяТаблицы + ".Партнер";
		ПоляИндексированияПоУчастникам = "Партнер";
		
	ИначеЕсли ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКонтрагенту Тогда
		
		ПоляУчастники = ИмяТаблицы + ".Контрагент КАК Контрагент";
		Если ПустыеЗначения Тогда
			
			ПоляУчастники = ПоляУчастники + ",
			|ЗНАЧЕНИЕ(Справочник.Партнеры.ПустаяСсылка) КАК Партнер";
			
		КонецЕсли;
		
		ПоляСоединенияПоУчастникам = СтрШаблон("%1.Контрагент = %2.Контрагент", ИмяТаблицы, ИмяТаблицыСвязи);
		ПоляГруппировкиПоУчастникам = ИмяТаблицы + ".Контрагент";
		ПоляИндексированияПоУчастникам = "Контрагент";
		
	ИначеЕсли ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКонтрагентуКлиенту Тогда
		
		ПоляУчастники = СтрШаблон(
			"%1.Контрагент КАК Контрагент,
			|%1.Партнер КАК Партнер",
			ИмяТаблицы);
			
		ПоляСоединенияПоУчастникам = СтрШаблон(
			"%1.Контрагент = %2.Контрагент
			|И %1.Партнер = %2.Партнер",
			ИмяТаблицы,
			ИмяТаблицыСвязи);
		
		ПоляГруппировкиПоУчастникам = СтрШаблон(
			"%1.Контрагент,
			|%1.Партнер",
			ИмяТаблицы);
		
		ПоляИндексированияПоУчастникам =
		"Контрагент,
		|Партнер";
		
	ИначеЕсли ДетализацияРасчетаУчастников = ДетализацииУчастников.ПустаяСсылка() Тогда
		
		ПоляУчастники = "NULL КАК Контрагент";
		ПоляСоединенияПоУчастникам = "ИСТИНА";
		ПоляГруппировкиПоУчастникам = "NULL";
		ПоляИндексированияПоУчастникам = "1";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Не удалось определить детализацию участников (""%1"") для документа ""%2""';
							|en = 'Cannot determine the participant details (""%1"") for the ""%2"" document'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки,
			ПараметрыУсловийРетроБонусов.ДетализацияРасчетаУчастниковПредставление,
			ПараметрыУсловийРетроБонусов.ДокументУсловийРетроБонусовПредставление);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляУчастники", ПоляУчастники);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляСоединенияПоУчастникам", ПоляСоединенияПоУчастникам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляГруппировкиПоУчастникам", ПоляГруппировкиПоУчастникам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляИндексированияПоУчастникам", ПоляИндексированияПоУчастникам);
	
КонецПроцедуры

Процедура ПодставитьПоляПоТоварам(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыСвязи = "", СоединениеПоСвойствуИТовару = Ложь)
	
	Если ПараметрыУсловийРетроБонусов.ПоказательТоваровКоличественный
	   И НЕ ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		ПоляТоваров = СтрШаблон(
			"%1.Номенклатура КАК Номенклатура,
			|%1.Характеристика КАК Характеристика",
			ИмяТаблицы);
		
		ПоляСоединенияПоТоварам = СтрШаблон(
			"%1.Номенклатура = %2.Номенклатура
			|И %1.Характеристика = %2.Характеристика",
			ИмяТаблицы,
			ИмяТаблицыСвязи);
		
		ПоляГруппировкиПоТоварам = СтрШаблон(
			"%1.Номенклатура,
			|%1.Характеристика",
			ИмяТаблицы);
		
		ПоляИндексированияПоТоварам =
		"Номенклатура,
		|Характеристика";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.ПоказательТоваровКоличественный
	        И ПараметрыУсловийРетроБонусов.РасчетПоСвойствам Тогда
		
		ПоляТоваров = СтрШаблон("%1.ЗначениеСвойства КАК ЗначениеСвойства", ИмяТаблицы);
		
		Если СоединениеПоСвойствуИТовару Тогда
			
			ПоляСоединенияПоТоварам = СтрШаблон(
				"%1.ЗначениеСвойства = %2.ЗначениеСвойства
				|И %1.Номенклатура = %2.Номенклатура",
				ИмяТаблицы,
				ИмяТаблицыСвязи);
			
		Иначе
			
			ПоляСоединенияПоТоварам = СтрШаблон("%1.ЗначениеСвойства = %2.ЗначениеСвойства",  ИмяТаблицы, ИмяТаблицыСвязи);
			
		КонецЕсли;
		
		ПоляГруппировкиПоТоварам = СтрШаблон("%1.ЗначениеСвойства",  ИмяТаблицы);
		ПоляИндексированияПоТоварам = "1";
		
	Иначе
		
		ПоляТоваров = "NULL КАК Номенклатура";
		ПоляСоединенияПоТоварам = "ИСТИНА";
		ПоляГруппировкиПоТоварам = "NULL";
		ПоляИндексированияПоТоварам = "1";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляТоваров", ПоляТоваров);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляСоединенияПоТоварам", ПоляСоединенияПоТоварам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляГруппировкиПоПолямТоваров", ПоляГруппировкиПоТоварам);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПоляИндексированияПоТоварам", ПоляИндексированияПоТоварам);
	
КонецПроцедуры

Процедура ПодставитьПолеНомерДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыСвязи = "")
	
	Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
		
		ПолеНомерДиапазона = СтрШаблон(
			"%1.НомерДиапазона КАК НомерДиапазона",
			ИмяТаблицы);
		
		ПолеСоединенияНомерДиапазона = СтрШаблон(
			"%1.НомерДиапазона = %2.НомерДиапазона",
			ИмяТаблицы,
			ИмяТаблицыСвязи);
		
		ПолеГруппировкиНомерДиапазона = СтрШаблон(
			"%1.НомерДиапазона",
			ИмяТаблицы);
			
		ПолеИндексированияНомерДиапазона =
		"НомерДиапазона";
		
	Иначе
		
		ПолеНомерДиапазона = "0 КАК НомерДиапазона";
		ПолеСоединенияНомерДиапазона = "ИСТИНА";
		ПолеГруппировкиНомерДиапазона = "NULL";
		ПолеИндексированияНомерДиапазона = "1";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеНомерДиапазона", ПолеНомерДиапазона);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеСоединенияНомерДиапазона", ПолеСоединенияНомерДиапазона);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеГруппировкиНомерДиапазона", ПолеГруппировкиНомерДиапазона);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеИндексированияНомерДиапазона", ПолеИндексированияНомерДиапазона);
	
КонецПроцедуры

Процедура ПодставитьПолеПредставлениеЕдиницыДиапазона(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы)
	
	Если ПараметрыУсловийРетроБонусов.ШкалаРасчетаПоДиапазонам Тогда
		
		ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
		
		Если ПараметрыУсловийРетроБонусов.ПоказательТоваров = ВидыПоказателей.Сумма Тогда
			
			ПолеПредставлениеЕдиницыДиапазона = СтрШаблон(
				"ПРЕДСТАВЛЕНИЕ(%1.Валюта) КАК ПредставлениеЕдиницыДиапазона", ИмяТаблицы);
			
		Иначе
			
			ПолеПредставлениеЕдиницыДиапазона = """"" КАК ПредставлениеЕдиницыДиапазона";
			
		КонецЕсли;
		
	Иначе
		
		ПолеПредставлениеЕдиницыДиапазона = """"" КАК ПредставлениеЕдиницыДиапазона";
		
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ПолеПредставлениеЕдиницыДиапазона", ПолеПредставлениеЕдиницыДиапазона);
	
КонецПроцедуры

Процедура ПодставитьПолеВыбораСуммаБезУточнения(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыБазоваяЦена = "")
	
	Если ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаУпрУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.Сумма";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаРеглУчет
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиРегУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаРегл";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиВзаиморасчеты Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаВВалютеВзаиморасчетов";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
		
		ТекстПоля = "ВЫРАЗИТЬ(ЕСТЬNULL(&ТаблицаБазоваяЦена.БазоваяЦена, 0) * &ИмяТаблицы.Количество КАК ЧИСЛО(31, 2))";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Неизвестная база расчета ""%1""';
							|en = '""%1"" unknown calculation base'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки, Строка(ПараметрыУсловийРетроБонусов.БазаРасчета));
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если ИмяТаблицыБазоваяЦена = "" Тогда
		ИмяТаблицыБазоваяЦена = ИмяТаблицы;
	КонецЕсли;
	
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ИмяТаблицы", ИмяТаблицы);
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ТаблицаБазоваяЦена", ИмяТаблицыБазоваяЦена);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаБезУточнения", ТекстПоля);
	
КонецПроцедуры

Процедура ПодставитьПолеВыбораСуммаСНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыБазоваяЦена = "")
	
	Если ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаУпрУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаОборотУпрСНДС";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаРеглУчет
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиРегУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаОборотРеглСНДС";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиВзаиморасчеты Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаВВалютеВзаиморасчетов";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
		
		ТекстПоля = "ВЫРАЗИТЬ(ЕСТЬNULL(&ТаблицаБазоваяЦена.БазоваяЦена, 0) * &ИмяТаблицы.Количество КАК ЧИСЛО(31, 2))";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Неизвестная база расчета ""%1""';
							|en = '""%1"" unknown calculation base'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки, Строка(ПараметрыУсловийРетроБонусов.БазаРасчета));
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если ИмяТаблицыБазоваяЦена = "" Тогда
		ИмяТаблицыБазоваяЦена = ИмяТаблицы;
	КонецЕсли;
	
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ИмяТаблицы", ИмяТаблицы);
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ТаблицаБазоваяЦена", ИмяТаблицыБазоваяЦена);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаСНДС", ТекстПоля);
	
КонецПроцедуры

Процедура ПодставитьПолеВыбораСуммаБезНДС(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы, ИмяТаблицыБазоваяЦена = "")
	
	Если ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаУпрУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаОборотУпрБезНДС";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаРеглУчет
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиРегУчет Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаОборотРеглБезНДС";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты
	      ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиВзаиморасчеты Тогда
		
		ТекстПоля = "&ИмяТаблицы.СуммаОборотВВалютеВзаиморасчетовБезНДС";
		
	ИначеЕсли ПараметрыУсловийРетроБонусов.РасчетВБазовыхЦенах Тогда
		
		ТекстПоля = "ВЫРАЗИТЬ(ЕСТЬNULL(&ТаблицаБазоваяЦена.БазоваяЦена, 0) * &ИмяТаблицы.Количество КАК ЧИСЛО(31, 2))";
		
	Иначе
		
		ШаблонОшибки = НСтр("ru = 'Неизвестная база расчета ""%1""';
							|en = '""%1"" unknown calculation base'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонОшибки, Строка(ПараметрыУсловийРетроБонусов.БазаРасчета));
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Если ИмяТаблицыБазоваяЦена = "" Тогда
		ИмяТаблицыБазоваяЦена = ИмяТаблицы;
	КонецЕсли;
	
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ИмяТаблицы", ИмяТаблицы);
	ТекстПоля = СтрЗаменить(ТекстПоля, "&ТаблицаБазоваяЦена", ИмяТаблицыБазоваяЦена);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеСуммаБезНДС", ТекстПоля);
	
КонецПроцедуры

Функция ЗначениеПустойСсылкиРегистратора(ПараметрыУсловийРетроБонусов)
	
	Если ПараметрыУсловийРетроБонусов.РасчетБонусовПоставщиков
	   И ПараметрыУсловийРетроБонусов.ТипБонуса = Перечисления.ТипыРетроБонусовПоставщиков.Закупки Тогда
		
		Результат = "ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)";
		
	Иначе
		
		Результат = "ЗНАЧЕНИЕ(Документ.ПриобретениеТоваровУслуг.ПустаяСсылка)";
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция РасчетПоПрогрессивнойШкале(ПараметрыУсловий)
	
	Если ПараметрыУсловий.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		Возврат (ПараметрыУсловий.ВидШкалыРасчета = 1);
		
	ИначеЕсли ПараметрыУсловий.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		Возврат (ПараметрыУсловий.ВидШкалыРасчета = 0);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция РасчетПоДиапазонам(ПараметрыУсловий)
	
	Если ПараметрыУсловий.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма Тогда
		
		Возврат (ПараметрыУсловий.ВидШкалыРасчета = 2);
		
	ИначеЕсли ПараметрыУсловий.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Количество Тогда
		
		Возврат (ПараметрыУсловий.ВидШкалыРасчета = 1);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Функция ПоляУпорядочиванияПоХронологии(ПараметрыУсловий, ТаблицаИсточник)
	
	ПоляУпорядочивания = Новый Массив(); // Массив Из Строка
	
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Валюта");
	
	ДетализацииУчастников = Перечисления.ДетализацияРасчетаУчастниковРетроБонусов;
	
	Если ПараметрыУсловий.ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКлиенту
	 ИЛИ ПараметрыУсловий.ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКонтрагентуКлиенту Тогда
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Партнер");
	КонецЕсли;
	Если ПараметрыУсловий.ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКонтрагенту
	 ИЛИ ПараметрыУсловий.ДетализацияРасчетаУчастников = ДетализацииУчастников.ПоКонтрагентуКлиенту Тогда
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Контрагент");
	КонецЕсли;
	
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".ПериодРасчета");
	
	Если ПараметрыУсловий.ПоказательТоваровКоличественный
	   И ПараметрыУсловий.РасчетПоСвойствам Тогда
		
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".ЗначениеСвойства");
		
	КонецЕсли;
	
	Если ПараметрыУсловий.ПоказательТоваровКоличественный
	   И НЕ ПараметрыУсловий.РасчетПоСвойствам Тогда
		
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Номенклатура");
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Характеристика");
		
	КонецЕсли;
	
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".ЗнакОборотов");
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Период");
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".ДокументРегистратор");
	
	Если ПараметрыУсловий.ПоказательТоваров = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам.Сумма
	 ИЛИ (ПараметрыУсловий.ПоказательТоваровКоличественный
	      И ПараметрыУсловий.РасчетПоСвойствам) Тогда
		
		ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".Номенклатура");
		
	КонецЕсли;
	
	ПоляУпорядочивания.Добавить(ТаблицаИсточник + ".ХарактеристикаПродажЗакупок");
	
	Возврат ПоляУпорядочивания;
	
КонецФункции

Функция РасчетПоСвойствам(ПараметрыУсловий)
	
	Возврат (ПараметрыУсловий.СоставТоваров = Перечисления.СоставыТоваровРетроБонусов.СвойствоНоменклатуры);
	
КонецФункции

Функция ПараметрыРеквизитаНоменклатуры(ПараметрыУсловий)
	
	Результат = Новый Структура;
	Результат.Вставить("СвойствоНоменклатурыДополнительное", Ложь);
	Результат.Вставить("СвойствоНоменклатурыСсылка", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.ПустаяСсылка());
	Результат.Вставить("СвойствоНоменклатурыТипЗначения", Новый ОписаниеТипов(Неопределено));
	Результат.Вставить("СвойствоНоменклатурыЗаголовок", "");
	
	Если НЕ ПараметрыУсловий.РасчетПоСвойствам Тогда
		Возврат Результат;
	КонецЕсли;
	
	РезультатПроверки = РетроБонусыСервер.ДанныеПроверкиСвойстваНоменклатуры(ПараметрыУсловий.СвойствоНоменклатуры);
	Если НЕ РезультатПроверки.РазрешеноИспользовать Тогда
		
		ШаблонОшибки = НСтр("ru = 'Не найден реквизит номенклатуры с идентификатором ""%1"". Проверьте настройки вида ретро-бонусов';
							|en = 'The item attribute with ID ""%1""  is not found. Please check the rebate kind settings'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОшибки, ПараметрыУсловий.СвойствоНоменклатуры);
		ВызватьИсключение ТекстОшибки;
		
	КонецЕсли;
	
	Результат.СвойствоНоменклатурыДополнительное = ЗначениеЗаполнено(РезультатПроверки.Свойство);
	Результат.СвойствоНоменклатурыСсылка = РезультатПроверки.Свойство;
	Результат.СвойствоНоменклатурыТипЗначения = РезультатПроверки.ТипЗначения;
	Результат.СвойствоНоменклатурыЗаголовок = РезультатПроверки.Заголовок;
	
	Возврат Результат;
	
КонецФункции

Функция ПоказательТоваровКоличественный(ПоказательТоваров)
	
	ВидыПоказателей = Перечисления.ЦелевыеПоказателиРетроБонусовПоТоварам;
	
	Если ПоказательТоваров = ВидыПоказателей.Количество
	 ИЛИ ПоказательТоваров = ВидыПоказателей.КоличествоСовокупно
	 ИЛИ ПоказательТоваров = ВидыПоказателей.ПакетноеПредложение Тогда
		
		Возврат Истина;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

Процедура ПодставитьВыражениеВалютаРасчета(ПараметрыУсловийРетроБонусов, ТекстЗапроса, ИмяТаблицы)
	
	Если ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.ВыручкаВзаиморасчеты
	 ИЛИ ПараметрыУсловийРетроБонусов.БазаРасчета = Перечисления.БазыРасчетаРетроБонусов.СуммаЗакупкиВзаиморасчеты Тогда
	 
		ВыражениеВалютаРасчета = "&ИмяТаблицы.ВалютаВзаиморасчетов";
		
	Иначе
		
		ВыражениеВалютаРасчета = "&ИмяТаблицы.ВалютаУсловий";
		
	КонецЕсли;
	
	ВыражениеВалютаРасчета = СтрЗаменить(ВыражениеВалютаРасчета, "&ИмяТаблицы", ИмяТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВыражениеВалютаРасчета", ВыражениеВалютаРасчета);
	
КонецПроцедуры

Функция ВыражениеТипизацииПоляСоставногоТипа(ОписаниеТипаЗначения, ИмяПоля)
	
	ВыраженияТиповПоля = Новый Массив; // Массив Из Строка
	
	ШаблонВыраженияСоставногоТипа = "КОГДА %1 Ссылка %2
	|ТОГДА %3";
	ШаблонВыраженияПростогоТипа = "ВЫРАЗИТЬ(%1 КАК %2)";
	
	Для Каждого ТипПоля Из ОписаниеТипаЗначения.Типы() Цикл
		
		МетаданныеТипа = Метаданные.НайтиПоТипу(ТипПоля);
		Если МетаданныеТипа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТипПоляДляЗапроса = МетаданныеТипа.ПолноеИмя();
		ВыражениеПростогоТипа = СтрШаблон(ШаблонВыраженияПростогоТипа, ИмяПоля, ТипПоляДляЗапроса);
		
		ВыражениеСоставногоТипа = СтрШаблон(ШаблонВыраженияСоставногоТипа, ИмяПоля, ТипПоляДляЗапроса, ВыражениеПростогоТипа);
		ВыраженияТиповПоля.Добавить(ВыражениеСоставногоТипа);
		
	КонецЦикла;
	
	Если ОписаниеТипаЗначения.СодержитТип(Тип("Строка")) Тогда
		
		ШаблонВыраженияПростогоТипа = "ВЫРАЗИТЬ(%1 КАК СТРОКА(%2))";
		ДлинаСтроки = ОписаниеТипаЗначения.КвалификаторыСтроки.Длина;
		ВыражениеПростогоТипа = СтрШаблон(ШаблонВыраженияПростогоТипа, ИмяПоля, ДлинаСтроки);
		
		ШаблонВыраженияСоставногоТипа = "КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(Строка)
		|ТОГДА %2";
		ВыражениеСоставногоТипа = СтрШаблон(ШаблонВыраженияСоставногоТипа, ИмяПоля, ВыражениеПростогоТипа);
		ВыраженияТиповПоля.Добавить(ВыражениеСоставногоТипа);
		
	КонецЕсли;
	
	Если ОписаниеТипаЗначения.СодержитТип(Тип("Число")) Тогда
		
		ШаблонВыраженияПростогоТипа = "ВЫРАЗИТЬ(%1 КАК ЧИСЛО(%2, %3))";
		Разрядность = ОписаниеТипаЗначения.КвалификаторыЧисла.Разрядность;
		Точность = ОписаниеТипаЗначения.КвалификаторыЧисла.РазрядностьДробнойЧасти;
		ВыражениеПростогоТипа = СтрШаблон(ШаблонВыраженияПростогоТипа, ИмяПоля, Разрядность, Точность);
		
		ШаблонВыраженияСоставногоТипа = "КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(Число)
		|ТОГДА %2";
		
		ВыражениеСоставногоТипа = СтрШаблон(ШаблонВыраженияСоставногоТипа, ИмяПоля, ВыражениеПростогоТипа);
		ВыраженияТиповПоля.Добавить(ВыражениеСоставногоТипа);
		
	КонецЕсли;
	
	Если ОписаниеТипаЗначения.СодержитТип(Тип("Дата")) Тогда
		
		ШаблонВыраженияПростогоТипа = "ВЫРАЗИТЬ(%1 КАК ДАТА)";
		ВыражениеПростогоТипа = СтрШаблон(ШаблонВыраженияПростогоТипа, ИмяПоля);
		
		ШаблонВыраженияСоставногоТипа = "КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(Дата)
		|ТОГДА %2";
		ВыражениеСоставногоТипа = СтрШаблон(ШаблонВыраженияСоставногоТипа, ИмяПоля, ВыражениеПростогоТипа);
		ВыраженияТиповПоля.Добавить(ВыражениеСоставногоТипа);
		
	КонецЕсли;
	
	Если ОписаниеТипаЗначения.СодержитТип(Тип("Булево")) Тогда
		
		ШаблонВыраженияПростогоТипа = "ВЫРАЗИТЬ(%1 КАК БУЛЕВО)";
		ВыражениеПростогоТипа = СтрШаблон(ШаблонВыраженияПростогоТипа, ИмяПоля);
		
		ШаблонВыраженияСоставногоТипа = "КОГДА ТИПЗНАЧЕНИЯ(%1) = ТИП(Булево)
		|ТОГДА %2";
		ВыражениеСоставногоТипа = СтрШаблон(ШаблонВыраженияСоставногоТипа, ИмяПоля, ВыражениеПростогоТипа);
		ВыраженияТиповПоля.Добавить(ВыражениеСоставногоТипа);
		
	КонецЕсли;
	
	Если ОписаниеТипаЗначения.Типы().Количество() > 1 Тогда
		
		ВыражениеТипизацииСоставногоТипа = "ВЫБОР
		|" + СтрСоединить(ВыраженияТиповПоля, Символы.ПС) + "
		|КОНЕЦ";
		
		Возврат ВыражениеТипизацииСоставногоТипа;
		
	Иначе
		
		Возврат ВыражениеПростогоТипа;
		
	КонецЕсли;
	
КонецФункции

Функция ПустоеЗначениеПоляСоставногоТипа(ОписаниеТипаЗначения)
	
	Если ОписаниеТипаЗначения.Типы().Количество() > 1
	 ИЛИ ОписаниеТипаЗначения.Типы().Количество() = 0 Тогда
		
		Возврат "НЕОПРЕДЕЛЕНО";
		
	ИначеЕсли ОписаниеТипаЗначения.СодержитТип(Тип("Строка")) Тогда
		
		Возврат """""";
		
	ИначеЕсли ОписаниеТипаЗначения.СодержитТип(Тип("Число")) Тогда
		
		Возврат "0";
		
	ИначеЕсли ОписаниеТипаЗначения.СодержитТип(Тип("Дата")) Тогда
		
		Возврат "ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)";
		
	ИначеЕсли ОписаниеТипаЗначения.СодержитТип(Тип("Булево")) Тогда
		
		Возврат "ЛОЖЬ";
		
	Иначе
		
		ТипПоля = ОписаниеТипаЗначения.Типы()[0];
		МетаданныеТипа = Метаданные.НайтиПоТипу(ТипПоля);
		Если МетаданныеТипа = Неопределено Тогда
			ВызватьИсключение НСтр("ru = 'Не удалось определить тип поля';
									|en = 'Cannot determine a field type'");
		КонецЕсли;
		
		ТипПоляДляЗапроса = МетаданныеТипа.ПолноеИмя();
		ПустоеЗначение = СтрШаблон("ЗНАЧЕНИЕ(%1.ПустаяСсылка)", ТипПоляДляЗапроса);
		Возврат ПустоеЗначение;
		
	КонецЕсли;
	
КонецФункции

#КонецОбласти
