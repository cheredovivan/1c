
// @strict-types

#Область СлужебныйПрограммныйИнтерфейс

// Параметры:
//  ДлительнаяОперация - См. ДлительныеОперации.ВыполнитьФункцию
//  ОповещениеОЗавершении - ОписаниеОповещения
Процедура ОжидатьПолученияДанныхКомпонентовПросмотраПослеДлительнойОперации(ДлительнаяОперация, 
	ОповещениеОЗавершении) Экспорт

	Если ДлительнаяОперация = Неопределено Тогда // Прервана
		Возврат;
	КонецЕсли; 
	
	Результат = НовыйРезультатОжиданияДанныхКомпонентовПросмотраДокумента();

	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		Результат.ОперацияПолученияДанных = ДлительнаяОперация;
		Результат.Успех = Истина;
	Иначе
		ВидОперации = НСтр("ru = 'Получение данных электронного документа';
							|en = 'Get electronic document data'");
		ТекстСообщения = ?(ДлительнаяОперация = Неопределено,
			НСтр("ru = 'Не удалось получить данные электронного документа.';
				|en = 'Cannot get the electronic document data.'"),
			ДлительнаяОперация.КраткоеПредставлениеОшибки);
		Ошибка = ОбработкаНеисправностейБЭДКлиент.НоваяОшибка(ВидОперации,
			ОбработкаНеисправностейБЭДКлиентСервер.ВидОшибкиНеизвестнаяОшибка(), ТекстСообщения, ТекстСообщения);
		ОбработкаНеисправностейБЭДКлиент.ДобавитьОшибку(Результат.КонтекстДиагностики, Ошибка,
			ОбщегоНазначенияБЭДКлиентСервер.ПодсистемыБЭД().ОбменСКонтрагентами);
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);

КонецПроцедуры

#Область События

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияИзмененияОбъектовУчетаДокумента() Экспорт
	Возврат "ОбновитьОбъектыУчетаЭД";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияИзменениеОбщегоСостоянияДокумента() Экспорт
	Возврат "ОбновитьОбщееСостояниеЭД";
КонецФункции

// Возвращаемое значение:
//  Строка
Функция ИмяСобытияИзменениеСвязанныхДокументов() Экспорт
	Возврат "ОбновитьСвязанныеДокументыЭД";
КонецФункции

// См. ОбменСКонтрагентамиКлиент.ОповеститьОбИзмененииСвязанныхДокументовОснованийЭлектронныхДокументов
//
Процедура ОповеститьОбИзмененииСвязанныхДокументовОснованийЭлектронныхДокументов(СвязанныеДокументы) Экспорт
	
	Для Каждого СвязанныйДокумент Из СвязанныеДокументы Цикл
		Оповестить(ИмяСобытияИзменениеСвязанныхДокументов(), СвязанныйДокумент);
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция:
//   ** ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//   ** СпособОбработки - Строка
//  Параметр - Неопределено
//           - Структура - параметр передаваемый в обработку оповещение формы:
//             * ДокументыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
// 
// Возвращаемое значение:
//  Булево - Требуется обновление связанных документов
Функция ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСостоянияЭДО(Форма, Параметр) Экспорт
	
	Если ТипЗнч(Параметр) <> Тип("Структура") Или Не Параметр.Свойство("ДокументыУчета")
		Или Не ТипЗнч(Параметр.ДокументыУчета) = Тип("Массив") Тогда
		Возврат Истина;
	КонецЕсли;
	
	ОбъектыУчетаТекущегоЭлектронногоДокументаОбновлены = Ложь;
	
	ОбновленныеОбъектыУчета = Параметр.ДокументыУчета;
	Для Каждого ОбъектУчетаЭлектронногоДокумента Из Форма.ОбъектыУчетаДокумента Цикл
		Если ОбновленныеОбъектыУчета.Найти(ОбъектУчетаЭлектронногоДокумента.ОбъектУчета) <> Неопределено Тогда
			ОбъектыУчетаТекущегоЭлектронногоДокументаОбновлены = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОбъектыУчетаТекущегоЭлектронногоДокументаОбновлены;
	
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция:
//   ** ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//   ** СпособОбработки - Строка
//  * СвязанныеДокументы - СписокЗначений Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  Параметр - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//           - ФормаКлиентскогоПриложения
// 
// Возвращаемое значение:
//  Булево - Требуется обновление связанных документов
Функция ТребуетсяОбновлениеСвязанныхДокументовПриИзмененииСвязанныхДокументов(Форма, Параметр) Экспорт
	
	Результат = Ложь;
	
	Если Параметр = Форма.Объект.Ссылка
		Или Параметр = Форма
		Или Форма.ОбъектыУчетаДокумента.НайтиСтроки(Новый Структура("ОбъектУчета", Параметр)).Количество()
		Или Форма.СвязанныеДокументы.НайтиПоЗначению(Параметр) <> Неопределено Тогда
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//  ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//  ** ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * ОповеститьОбИзмененииСвязанныхДокументов - Булево
Процедура ОповеститьОбИзмененииСвязанныхДокументовИзКарточкиДокумента(Форма) Экспорт
	
	Форма.ОповеститьОбИзмененииСвязанныхДокументов = Ложь;
	
	ОповеститьОбИзмененииСвязанныхДокументовОснованийЭлектронныхДокументов(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка));
	
КонецПроцедуры

#КонецОбласти // События

#Область ОбработкаКоманд

#Область ДополнительныеКоманды

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса
//  СтандартнаяОбработка - Булево
Процедура ОткрытьЖурналРегистрацииПоОшибкамПредставленияИзКарточкиДокумента(Форма, СтандартнаяОбработка) Экспорт

	СтандартнаяОбработка = Ложь;

	ОтборЖурналаРегистрации = Новый Структура;
	ОтборЖурналаРегистрации.Вставить("Данные", "ЭлектронныеДокументыЭДО.ПредставлениеДанныхДокумента");
	ЖурналРегистрацииКлиент.ОткрытьЖурналРегистрации(ОтборЖурналаРегистрации, Форма);

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СоставПакета - ДанныеФормыКоллекция Из ТаблицаЗначений
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ОповещениеОЗавершении - ОписаниеОповещения
//  ЭтоПакетнаяОбработка - Булево
Процедура СменитьОтветственногоИзКарточкиДокумента(Форма, ОповещениеОЗавершении, 
	ЭтоПакетнаяОбработка = Ложь) Экспорт

	Если ЭтоПакетнаяОбработка Тогда
		ЭлектронныеДокументы = ЭлектронныеДокументыИзСоставаПакета(Форма.СоставПакета);
	Иначе
		ЭлектронныеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка);
	КонецЕсли;

	ИнтерфейсДокументовЭДОКлиент.ПеренаправитьЭлектронныеДокументы(ЭлектронныеДокументы, ОповещениеОЗавершении);

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ВыводитьБанковскиеРеквизиты - Булево
//  * ОтключитьВыводДопДанных - Булево
//  * ОтключитьВыводКопияВерна - Булево
Процедура ОткрытьФормуПечатиИзКарточкиДокумента(Форма) Экспорт

	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для печати необходимо сохранить электронный документ';
				|en = 'Save the electronic document to print'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыВизуализации = ИнтерфейсДокументовЭДОВызовСервера.НовыеПараметрыВизуализацииДокумента();
	ПараметрыВизуализации.ВыводитьБанковскиеРеквизиты = Форма.ВыводитьБанковскиеРеквизиты;
	ПараметрыВизуализации.ВыводитьДопДанные = Не Форма.ОтключитьВыводДопДанных;
	ПараметрыВизуализации.ВыводитьКопияВерна = Не Форма.ОтключитьВыводКопияВерна;
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьФормуПечатиЭлектронныхДокументов(Форма.Объект.Ссылка, ПараметрыВизуализации);

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * УникальныйИдентификатор - УникальныйИдентификатор
Процедура ВыгрузитьДокументооборотЦеликомИзКарточкиДокумента(Форма) Экспорт

	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для выгрузки необходимо сохранить электронный документ';
				|en = 'Save the electronic document to export'"));
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка);
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьДокументооборотЦеликом(ЭлектронныеДокументы);

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * УникальныйИдентификатор- УникальныйИдентификатор
Процедура ВыгрузитьДокументыВФорматеPDFИзКарточкиДокумента(Форма) Экспорт

	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для выгрузки необходимо сохранить электронный документ';
				|en = 'Save the electronic document to export'"));
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка);
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьЭлектронныеДокументыВФорматеPDF(ЭлектронныеДокументы,
		Форма.УникальныйИдентификатор);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
Процедура ВыгрузитьДокументыДляФНСИзКарточкиДокумента(Форма) Экспорт

	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для выгрузки необходимо сохранить электронный документ';
				|en = 'Save the electronic document to export'"));
		Возврат;
	КонецЕсли;
	
	ЭлектронныеДокументы = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка);
	ИнтерфейсДокументовЭДОКлиент.ВыгрузитьЭлектронныеДокументыДляФНС(ЭлектронныеДокументы);

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** ИдентификаторКонтрагента - Строка
//   ** ИдентификаторОрганизации - Строка
//   ** Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   ** Организация - ОпределяемыйТип.Организация
Процедура ОткрытьНастройкиОтраженияВУчетеИзКарточкиДокумента(Форма) Экспорт

	ПараметрыФормы = НастройкиЭДОКлиент.НовыеПараметрыФормыНастройкиОтраженияВУчете();
	ПараметрыФормы.ИдентификаторКонтрагента = Форма.Объект.ИдентификаторКонтрагента;
	ПараметрыФормы.ИдентификаторОрганизации = Форма.Объект.ИдентификаторОрганизации;
	ПараметрыФормы.Контрагент = Форма.Объект.Контрагент;
	ПараметрыФормы.Организация = Форма.Объект.Организация;
	
	НастройкиЭДОКлиент.ОткрытьНастройкуОтраженияВУчете(ПараметрыФормы);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   ** ИдентификаторОрганизации - Строка
//   ** ИдентификаторКонтрагента - Строка
//   ** ВыгружатьДополнительныеСведения - Булево
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//   ** ОбменБезПодписи - Булево
//   ** СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//   ** ТребуетсяИзвещение - Булево
//   ** ТребуетсяПодтверждение - Булево
//   ** ФорматОсновногоТитула - Строка
//  ОбработчикЗавершения - ОписаниеОповещения - процедура, которая будет вызвана после закрытия формы настройки обмена:
//  * Настройки - Неопределено - если настройки не были изменены
//              - См. НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки
//  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения
Процедура ОткрытьНастройкиОтправкиИзКарточкиДокумента(Форма, ОбработчикЗавершения) Экспорт
	
	КлючНастроекОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастроекОтправки.Отправитель = Форма.Объект.Организация;
	КлючНастроекОтправки.Получатель = Форма.Объект.Контрагент;
	КлючНастроекОтправки.Договор = Форма.Объект.ДоговорКонтрагента;
	КлючНастроекОтправки.ВидДокумента = Форма.Объект.ВидДокумента;
	
	ПараметрыОткрытия = НастройкиЭДОКлиент.НовыеПараметрыТранспортныхНастроек();
	ПараметрыОткрытия.КлючНастроек = КлючНастроекОтправки;
	ПараметрыОткрытия.ИдентификаторОтправителя = Форма.Объект.ИдентификаторОрганизации;
	ПараметрыОткрытия.ИдентификаторПолучателя = Форма.Объект.ИдентификаторКонтрагента;
	ПараметрыОткрытия.НастройкаДокумента = Истина;
	
	НастройкаОтправки = НастройкаОтправкиПоДаннымКарточкиДокумента(Форма);
	
	НастройкиЭДОКлиент.ОткрытьТранспортныеНастройкиОтправки(ПараметрыОткрытия, Форма, ОбработчикЗавершения,
		НастройкаОтправки);
		
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   ** ИдентификаторОрганизации - Строка
//   ** ИдентификаторКонтрагента - Строка
//   ** ВыгружатьДополнительныеСведения - Булево
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//   ** ОбменБезПодписи - Булево
//   ** СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//   ** ТребуетсяИзвещение - Булево
//   ** ТребуетсяПодтверждение - Булево
//   ** ФорматОсновногоТитула - Строка
//  ОбработчикЗавершения - ОписаниеОповещения - процедура, которая будет вызвана после закрытия формы настройки обмена:
//  * Настройки - Неопределено - если настройки не были изменены
//              - См. НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки
//  * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения
Процедура ОткрытьНастройкиФормированияИзКарточкиДокумента(Форма, ОбработчикЗавершения) Экспорт
	
	КлючНастроекОтправки = НастройкиЭДОКлиентСервер.НовыйКлючНастроекОтправки();
	КлючНастроекОтправки.Отправитель = Форма.Объект.Организация;
	КлючНастроекОтправки.Получатель = Форма.Объект.Контрагент;
	КлючНастроекОтправки.Договор = Форма.Объект.ДоговорКонтрагента;
	КлючНастроекОтправки.ВидДокумента = Форма.Объект.ВидДокумента;
	
	НастройкаОтправки = НастройкаОтправкиПоДаннымКарточкиДокумента(Форма);
	
	НастройкиЭДОКлиент.НастроитьРегламентЭДО(КлючНастроекОтправки, Форма, ОбработчикЗавершения, Истина,
		НастройкаОтправки);
	
КонецПроцедуры

// Параметры:
//  ИдентификаторПакета - УникальныйИдентификатор
//  ДокументКУдалениюИзПакета - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//                            - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
Процедура УдалитьДокументИзПакета(ИдентификаторПакета, ДокументКУдалениюИзПакета) Экспорт
	
	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	
	ИнтерфейсДокументовЭДОВызовСервера.УдалитьДокументИзПакета(ИдентификаторПакета,
		ДокументКУдалениюИзПакета, КонтекстДиагностики);
		
	Если ОбработкаНеисправностейБЭДКлиентСервер.ЕстьОшибки(КонтекстДиагностики) Тогда
		ИнтерфейсДокументовЭДОКлиент.ПоказатьПредставлениеОшибокКонтекстаДиагностики(КонтекстДиагностики);
	Иначе		
		Оповестить(ПакетыДокументовЭДОКлиент.ИмяСобытияУдалениеДокументаИзПакета(), ДокументКУдалениюИзПакета,
			ИдентификаторПакета);
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * РежимСозданияПакетаПоПредварительнымДанным - Булево
//  * ПредварительныйПакетДокументов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ИдентификаторДокумента - УникальныйИдентификатор
//  * СоставПакета - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ИдентификаторДокумента - УникальныйИдентификатор
//  ИдентификаторДокумента - УникальныйИдентификатор
//
Процедура УдалитьДокументИзПредварительногоПакетаКарточкиДокумента(Форма, ИдентификаторДокумента) Экспорт
	
	Если Не Форма.РежимСозданияПакетаПоПредварительнымДанным Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ИдентификаторДокумента", ИдентификаторДокумента);
	
	НайденныеСтроки = Форма.ПредварительныйПакетДокументов.НайтиСтроки(Отбор);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Форма.ПредварительныйПакетДокументов.Удалить(НайденнаяСтрока);
	КонецЦикла;
	
	НайденныеСтроки = Форма.СоставПакета.НайтиСтроки(Отбор);
	Для Каждого НайденнаяСтрока Из НайденныеСтроки Цикл
		Форма.СоставПакета.Удалить(НайденнаяСтрока);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти // ДополнительныеКоманды

#Область ОтражениеВУчете

#Область Открытие

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  НомерОбъектаУчета - Число
Процедура ОткрытьОбъектУчетаИзКарточкиДокумента(Форма, НомерОбъектаУчета) Экспорт

	Если Форма.ОбъектыУчетаДокумента.Количество() >= НомерОбъектаУчета Тогда
		ПоказатьЗначение(, Форма.ОбъектыУчетаДокумента[НомерОбъектаУчета - 1].ОбъектУчета);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти // Открытие

#Область Создание

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ЕстьНоменклатураНаКонтроле - Булево
//  * СписокСоздания - СписокЗначений Из Строка
//  * СоставПакета - ДанныеФормыКоллекция Из ТаблицаЗначений
Процедура ПоказатьВыборСпособаОбработкиСозданияОбъектовУчетаИзКарточкиДокумента(Форма) Экспорт

	ПараметрыОтраженияВУчете = НовыеПараметрыСозданияОбъектовУчета();
	ПараметрыОтраженияВУчете.ЭлектронныйДокумент = Форма.Объект.Ссылка;
	ПараметрыОтраженияВУчете.ЕстьНоменклатураНаКонтроле = Форма.ЕстьНоменклатураНаКонтроле;
	ПараметрыОтраженияВУчете.ДокументыПакета = ЭлектронныеДокументыИзСоставаПакета(Форма.СоставПакета);

	ОбработкаВыбора = Новый ОписаниеОповещения("СоздатьОбъектыУчетаПослеВыбораСпособаОбработки", ЭтотОбъект,
		ПараметрыОтраженияВУчете);

	Если Форма.СписокСоздания.Количество() > 1 Тогда
		Форма.ПоказатьВыборИзМеню(ОбработкаВыбора, Форма.СписокСоздания, Форма.Элементы.КнопкаСоздатьДокументУчета);
	Иначе
		ВыполнитьОбработкуОповещения(ОбработкаВыбора, Форма.СписокСоздания[0]);
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма текущих дел ЭДО легкого интерфейса:
//  * ТекущийЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ЕстьНоменклатураНаКонтролеТекущегоДокумента - Булево
//  * СписокСоздания - СписокЗначений Из Строка
//  * СоставПакетаТекущегоДокумента - ДанныеФормыКоллекция Из ТаблицаЗначений
Процедура ПоказатьВыборСпособаОбработкиСозданияОбъектовУчетаИзСпискаТекущихДел(Форма) Экспорт

	ПараметрыОтраженияВУчете = НовыеПараметрыСозданияОбъектовУчета();
	ПараметрыОтраженияВУчете.ЭлектронныйДокумент = Форма.ТекущийЭлектронныйДокумент;
	ПараметрыОтраженияВУчете.ЕстьНоменклатураНаКонтроле = Форма.ЕстьНоменклатураНаКонтролеТекущегоДокумента;
	ПараметрыОтраженияВУчете.ДокументыПакета = ЭлектронныеДокументыИзСоставаПакета(Форма.СоставПакетаТекущегоДокумента);

	ОбработкаВыбора = Новый ОписаниеОповещения("СоздатьОбъектыУчетаПослеВыбораСпособаОбработки", ЭтотОбъект,
		ПараметрыОтраженияВУчете);

	Если Форма.СписокСоздания.Количество() > 1 Тогда
		Форма.ПоказатьВыборИзМеню(ОбработкаВыбора, Форма.СписокСоздания, Форма.Элементы.КнопкаСоздатьДокументУчета);
	Иначе
		ВыполнитьОбработкуОповещения(ОбработкаВыбора, Форма.СписокСоздания[0]);
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  ВыбранныйЭлемент - ЭлементСпискаЗначений:
//   * Значение - Строка,Массив Из Строка
//  ПараметрыОтраженияВУчете - См. НовыеПараметрыСозданияОбъектовУчета
Процедура СоздатьОбъектыУчетаПослеВыбораСпособаОбработки(ВыбранныйЭлемент, ПараметрыОтраженияВУчете) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СпособыОбработкиДокументов = Новый Соответствие;
	СпособыОбработкиДокументов.Вставить(ПараметрыОтраженияВУчете.ЭлектронныйДокумент, ВыбранныйЭлемент.Значение);
	
	ПараметрыЗавершения = Новый Структура("ЭлектронныйДокумент", ПараметрыОтраженияВУчете.ЭлектронныйДокумент);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОбработатьРезультатСозданияОбъектовУчета", 
		ЭтотОбъект, ПараметрыЗавершения);
	
	ОбщийМодульОтражениеВУчетеЭДОКлиент = ИнтеграцияЭДОКлиент.ОбщийМодульОтражениеВУчетеЭДОКлиент();
	ОбщийМодульОтражениеВУчетеЭДОКлиент.ОтразитьЭлектронныеДокументы(
		ОповещениеОЗавершении, СпособыОбработкиДокументов);
	
КонецПроцедуры

// Параметры:
//  Результат - Структура:
//  * ОбъектыУчетаДокументов - Соответствие из КлючИЗначение:
//    ** Ключ - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//    ** Значение - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  ДополнительныеПараметры - Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
Процедура ОбработатьРезультатСозданияОбъектовУчета(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат.ОбъектыУчетаДокументов) Тогда
		
		Оповестить(ИмяСобытияИзмененияОбъектовУчетаДокумента(), ДополнительныеПараметры.ЭлектронныйДокумент);
		Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());
		
		#Если Не МобильноеПриложениеКлиент Тогда
		ЗаголовокОповещения = НСтр("ru = 'Создан документ учета:';
									|en = 'Created accounting document:'");
		ОбъектыУчета = Результат.ОбъектыУчетаДокументов[ДополнительныеПараметры.ЭлектронныйДокумент];
		Если ЗначениеЗаполнено(ОбъектыУчета) Тогда
			Для Каждого ОбъектУчета Из ОбъектыУчета Цикл
				НавигационнаяСсылка = ПолучитьНавигационнуюСсылку(ОбъектУчета);
				ПоказатьОповещениеПользователя(ЗаголовокОповещения, НавигационнаяСсылка, Строка(ОбъектУчета),
					БиблиотекаКартинок.ЭмблемаСервиса1СЭДО48, СтатусОповещенияПользователя.Важное);
			КонецЦикла;
		КонецЕсли;
		#КонецЕсли
		
	КонецЕсли;
	
	ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(Результат.КонтекстДиагностики);
	
КонецПроцедуры

#КонецОбласти // Создание

#Область Подбор

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.КонтрагентБЭД
//  * СписокПодбора - СписокЗначений Из Строка
//  * СпособыОбработкиОсновные - СписокЗначений Из Строка
//  * СпособыОбработкиДополнительные - СписокЗначений Из Строка
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция Из ТаблицаЗначений
//  Номер - Число
Процедура ПоказатьВыборСпособаОбработкиПодбораОбъектаУчетаИзКарточкиДокумента(Форма, Номер) Экспорт

	ПараметрыПодбора = НовыеПараметрыПодбораОбъектовУчета();
	ПараметрыПодбора.ЭлектронныйДокумент = Форма.Объект.Ссылка;
	ПараметрыПодбора.Организация = Форма.Объект.Организация;
	ПараметрыПодбора.Контрагент = Форма.Объект.Контрагент;
	
	ОбработкаВыбора = Новый ОписаниеОповещения("ПоказатьПодборОбъектаУчетаПослеВыбораСпособаОбработки", ЭтотОбъект,
		ПараметрыПодбора);
	
	ТекущийСписокПодбора = Форма.СписокПодбора;
	НомерПодбораВторогоОбъектаУчета = 2;
	Если Номер = НомерПодбораВторогоОбъектаУчета Тогда
		ЕстьОсновнойДокумент = ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ЕстьОтражениеВОбъектУчетаКарточкиДокумента(
			Форма.ОбъектыУчетаДокумента, Форма.СпособыОбработкиОсновные);
		ТекущийСписокПодбора = ?(ЕстьОсновнойДокумент,
			Форма.СпособыОбработкиДополнительные, Форма.СпособыОбработкиОсновные);
	КонецЕсли;

	Если ТекущийСписокПодбора.Количество() > 1 Тогда

		Элемент = ?(Номер = 1,
			Форма.Элементы.КнопкаПодобратьДокументУчета1, Форма.Элементы.КнопкаПодобратьДокументУчета2);

		Форма.ПоказатьВыборИзМеню(ОбработкаВыбора, ТекущийСписокПодбора, Элемент);

	ИначеЕсли ТекущийСписокПодбора.Количество() = 1 Тогда

		ВыполнитьОбработкуОповещения(ОбработкаВыбора, ТекущийСписокПодбора[0]);

	КонецЕсли;

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма текущих дел ЭДО легкого интерфейса:
//  * ТекущийЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ОрганизацияТекущегоДокумента - ОпределяемыйТип.Организация
//  * КонтрагентТекущегоДокумента - ОпределяемыйТип.КонтрагентБЭД
//  * СписокПодбора - СписокЗначений Из Строка
//  * СпособыОбработкиОсновные - СписокЗначений Из Строка
//  * СпособыОбработкиДополнительные - СписокЗначений Из Строка
//  * ОбъектыУчетаТекущегоДокумента - ДанныеФормыКоллекция Из ТаблицаЗначений
//  Номер - Число
Процедура ПоказатьВыборСпособаОбработкиПодбораОбъектаУчетаИзСпискаТекущихДел(Форма, Номер) Экспорт

	ПараметрыПодбора = НовыеПараметрыПодбораОбъектовУчета();
	ПараметрыПодбора.ЭлектронныйДокумент = Форма.ТекущийЭлектронныйДокумент;
	ПараметрыПодбора.Организация = Форма.ОрганизацияТекущегоДокумента;
	ПараметрыПодбора.Контрагент = Форма.КонтрагентТекущегоДокумента;
	
	ОбработкаВыбора = Новый ОписаниеОповещения("ПоказатьПодборОбъектаУчетаПослеВыбораСпособаОбработки", ЭтотОбъект,
		ПараметрыПодбора);
	
	ТекущийСписокПодбора = Форма.СписокПодбора;
	НомерПодбораВторогоОбъектаУчета = 2;
	Если Номер = НомерПодбораВторогоОбъектаУчета Тогда
		ЕстьОсновнойДокумент = ЛегкийИнтерфейсДокументовЭДОКлиентСервер.ЕстьОтражениеВОбъектУчетаКарточкиДокумента(
			Форма.ОбъектыУчетаТекущегоДокумента, Форма.СпособыОбработкиОсновные);
		ТекущийСписокПодбора = ?(ЕстьОсновнойДокумент,
			Форма.СпособыОбработкиДополнительные, Форма.СпособыОбработкиОсновные);
	КонецЕсли;

	Если ТекущийСписокПодбора.Количество() > 1 Тогда

		Элемент = ?(Номер = 1,
			Форма.Элементы.КнопкаПодобратьДокументУчета1, Форма.Элементы.КнопкаПодобратьДокументУчета2);

		Форма.ПоказатьВыборИзМеню(ОбработкаВыбора, ТекущийСписокПодбора, Элемент);

	ИначеЕсли ТекущийСписокПодбора.Количество() = 1 Тогда

		ВыполнитьОбработкуОповещения(ОбработкаВыбора, ТекущийСписокПодбора[0]);

	КонецЕсли;

КонецПроцедуры

// Параметры:
//  ВыбранноеЗначение - Неопределено - отказ от выбора.
//                    - ЭлементСпискаЗначений:
//  * Значение - Строка,Массив Из Строка
//  ПараметрыПодбора - См. НовыеПараметрыПодбораОбъектовУчета
Процедура ПоказатьПодборОбъектаУчетаПослеВыбораСпособаОбработки(ВыбранноеЗначение, ПараметрыПодбора) Экспорт

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	СпособОбработки = ВыбранноеЗначение.Значение;
	НастройкиПодбора = НастройкиПодбораОбъектаУчета(ПараметрыПодбора, СпособОбработки);

	Если ЗначениеЗаполнено(НастройкиПодбора) Тогда
		ДополнительныеПараметры = Новый Структура("ЭлектронныйДокумент, СпособОбработки",
			ПараметрыПодбора.ЭлектронныйДокумент, СпособОбработки);
		ОбработкаВыбора = Новый ОписаниеОповещения("ОбработатьВыборОбъектаУчета", ЭтотОбъект, ДополнительныеПараметры);
		ИнтерфейсДокументовЭДОКлиент.ПоказатьПодборУчетногоДокумента(НастройкиПодбора, ОбработкаВыбора);
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  ОбъектУчета - Неопределено - отказ от выбора.
//              - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  ДополнительныеПараметры - Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СпособОбработки - Строка
Процедура ОбработатьВыборОбъектаУчета(ОбъектУчета, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(ОбъектУчета) Тогда

		ИнтеграцияЭДОВызовСервера.УстановитьСвязьЭлектронногоДокументаСОбъектомУчета(
			ДополнительныеПараметры.ЭлектронныйДокумент, ОбъектУчета, ДополнительныеПараметры.СпособОбработки);

		Оповестить(ИмяСобытияИзмененияОбъектовУчетаДокумента(), ДополнительныеПараметры.ЭлектронныйДокумент);
		Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());

	КонецЕсли;

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//  * ЕстьВозможностьОтраженияВУчете - Булево
Процедура ПоказатьРасширенныйПодборОбъектовУчетаИзКарточкиДокумента(Форма) Экспорт

	ДополнительныеПараметры = Новый Структура("ЭлектронныйДокумент", Форма.Объект.Ссылка);
	ОбработкаРасширенногоПодбора = Новый ОписаниеОповещения("ОбработатьРасширенныйПодборОбъектовУчета", ЭтотОбъект,
		ДополнительныеПараметры);

	ПараметрыФормы = Новый Структура("ЭлектронныйДокумент, ВидДокумента, ДокументРаспознан",
		Форма.Объект.Ссылка, Форма.Объект.ВидДокумента, Форма.ЕстьВозможностьОтраженияВУчете);
	ИнтерфейсДокументовЭДОКлиент.ОткрытьПодборОбъектовУчетаЭлектронногоДокумента(ПараметрыФормы,
		ОбработкаРасширенногоПодбора);

КонецПроцедуры

// Параметры:
//  Результат - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  ДополнительныеПараметры - Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//                        - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
Процедура ОбработатьРасширенныйПодборОбъектовУчета(Результат, ДополнительныеПараметры) Экспорт

	Оповестить(ИмяСобытияИзмененияОбъектовУчетаДокумента(), ДополнительныеПараметры.ЭлектронныйДокумент);
	ИнтерфейсДокументовЭДОКлиент.ПослеПодбораУчетногоДокумента(ДополнительныеПараметры.ЭлектронныйДокумент, Результат);

КонецПроцедуры

#КонецОбласти // Подбор

#Область РазрывСвязи

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ОбъектыУчетаДокумента - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  НомерОбъектаУчета - Число
Процедура ПоказатьВопросОРазрывеСвязиСОбъектомУчетаИзКарточкиДокумента(Форма, НомерОбъектаУчета) Экспорт

	Если Форма.ОбъектыУчетаДокумента.Количество() < НомерОбъектаУчета Тогда
		Возврат;
	КонецЕсли;

	ОбъектУчета = Форма.ОбъектыУчетаДокумента[НомерОбъектаУчета - 1].ОбъектУчета;

	ДополнительныеПараметры = Новый Структура("ЭлектронныйДокумент, ОбъектУчета", 
		Форма.Объект.Ссылка, ОбъектУчета);
	ОписаниеОповещения = Новый ОписаниеОповещения("РазорватьСвязьСОбъектомУчетаПослеОтветаНаВопрос",
		ЭтотОбъект, ДополнительныеПараметры);

	ТекстВопроса = НСтр("ru = 'Связь между документами разорвется. Повторно связать документы возможно только в ручном режиме. Продолжить?';
						|en = 'Documents will be unlinked. You can link the documents again only manually. Continue?'");
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Нет);

КонецПроцедуры

// Параметры:
//  Ответ - КодВозвратаДиалога
//  ДополнительныеПараметры - Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//                        - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
Процедура РазорватьСвязьСОбъектомУчетаПослеОтветаНаВопрос(Ответ, ДополнительныеПараметры) Экспорт

	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ИнтеграцияЭДОВызовСервера.РазорватьСвязьЭлектронногоДокументаСОбъектомУчета(
		ДополнительныеПараметры.ЭлектронныйДокумент, ДополнительныеПараметры.ОбъектУчета);

	Оповестить(ИмяСобытияИзмененияОбъектовУчетаДокумента(), ДополнительныеПараметры.ЭлектронныйДокумент);
	Оповестить(ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияТекущихДелЭДО());

КонецПроцедуры

#КонецОбласти // РазрывСвязи

#КонецОбласти // ОтражениеВУчете

#Область ВыполнениеДействийПоЭДО

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса
//  Заблокировать - Булево
Процедура НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма, Заблокировать = Истина) Экспорт
	
	Форма.Элементы.ГруппаПанельКоманд.Доступность = Не Заблокировать;
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеАннулироватьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().Аннулировать;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЭтоВходящийДокумент = ТипЗнч(Форма.Объект.Ссылка) = Тип("ДокументСсылка.ЭлектронныйДокументВходящийЭДО");
	Если ЭтоВходящийДокумент И Не ЭлектронныеДокументыЭДОВызовСервера.КаждыйДокументБезПодтвержденияИлиОтветнаяПодписьОтправлена(
		ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка), ЭтоПакетнаяОбработка) Тогда
		ПользовательСогласен = Ждать ИнтерфейсДокументовЭДОКлиент.ПредложитьОтклонение(ЭтоПакетнаяОбработка);
		Если ПользовательСогласен Тогда
			ВыполнитьДействиеОтклонитьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Если ЭтоПакетнаяОбработка Тогда
		ПараметрыВыполнения.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Аннулировать");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПараметрыВыполнения", Форма, ПараметрыВыполнения);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента",
		ЭтотОбъект, ДополнительныеПараметры);
		
	ПараметрыВводаСтроки = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причины аннулирования документа';
												|en = 'Specify the reasons for document cancellation'");
	ПараметрыВводаСтроки.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Аннулировать';
															|en = 'Cancel'");
	ПараметрыВводаСтроки.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для аннулирования документа необходимо указать причину.';
			|en = 'Specify a reason for the document cancellation.'");
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеВернутьВРаботуИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ВернутьВРаботу;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ВернутьВРаботу"));
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеЗакрытьПринудительноИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ЗакрытьПринудительно;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Если ЭтоПакетнаяОбработка Тогда
		ПараметрыВыполнения.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ЗакрытьПринудительно");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПараметрыВыполнения", Форма, ПараметрыВыполнения);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента",
		ЭтотОбъект, ДополнительныеПараметры);
		
	ПараметрыВводаСтроки = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причины принудительного закрытия документа';
												|en = 'Specify reasons for the document force closing'");
	ПараметрыВводаСтроки.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Закрыть принудительно';
															|en = 'Forcefully close'");
	ПараметрыВводаСтроки.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для принудительного закрытия документа необходимо указать причину.';
			|en = 'Specify a reason for the document force closing.'");
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеОтклонитьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().Отклонить;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Если ЭтоПакетнаяОбработка Тогда
		ПараметрыВыполнения.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отклонить");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПараметрыВыполнения", Форма, ПараметрыВыполнения);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента",
		ЭтотОбъект, ДополнительныеПараметры);
		
	ПараметрыВводаСтроки = ПараметрыВводаСтрокиДляОтклоненияДокумента();
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//                      - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//
Асинх Процедура ВыполнитьДействиеОтклонитьЭлектронныйДокумент(ЭлектронныйДокумент) Экспорт
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(ЭлектронныйДокумент);
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Отклонить");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтроки",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ПараметрыВыполнения);
		
	ПараметрыВводаСтроки = ПараметрыВводаСтрокиДляОтклоненияДокумента();
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеОтклонитьПодписаниеИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ОтклонитьПодписание;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Если ЭтоПакетнаяОбработка Тогда
		ПараметрыВыполнения.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ОтклонитьПодписание");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПараметрыВыполнения", Форма, ПараметрыВыполнения);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента",
		ЭтотОбъект, ДополнительныеПараметры);
		
	ПараметрыВводаСтроки = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причины отклонения документа';
												|en = 'Specify the reasons for the document rejection'");
	ПараметрыВводаСтроки.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Отклонить';
															|en = 'Reject'");
	ПараметрыВводаСтроки.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для отказа от подписания документа необходимо указать причину.';
			|en = 'Specify a reason for document signing rejection.'");
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеОтклонитьАннулированиеИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ОтклонитьАннулирование;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Если ЭтоПакетнаяОбработка Тогда
		ПараметрыВыполнения.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполнения.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ОсновноеДействие = ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.ОтклонитьАннулирование");
	
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ОсновноеДействие);
	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ПараметрыВыполнения.ДополнительныеПараметрыДействий.Вставить(ОсновноеДействие,
		ИнтерфейсДокументовЭДОКлиентСервер.НовыеДополнительныеПараметрыДействия());
		
	ДополнительныеПараметры = Новый Структура("ФормаВладелец, ПараметрыВыполнения", Форма, ПараметрыВыполнения);
	ОбработчикЗавершения = Новый ОписаниеОповещения("ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента", 
		ЭтотОбъект, ДополнительныеПараметры);
		
	ПараметрыВводаСтроки = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причины отклонения предложения об аннулировании';
												|en = 'Specify the reasons for cancellation offer rejection'");
	ПараметрыВводаСтроки.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Отклонить аннулирование';
															|en = 'Reject cancellation'");
	ПараметрыВводаСтроки.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для отклонения предложения об аннулировании документа необходимо указать причину.';
			|en = 'Specify a reason for the document cancellation offer rejection.'");
	ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//   ** ОбменБезПодписи - Булево
//  * СостоянияПакетаОднородно - Булево
//  * ИдентификаторПакета - УникальныйИдентификатор
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеОтправитьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().Отправить;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	Если Форма.Объект.ОбменБезПодписи Тогда
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
			"Перечисление.ДействияПоЭДО.СформироватьОтвет"));
	КонецЕсли;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить"));
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеОтправитьПовторноИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ОтправитьПовторно;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	ПараметрыВыполнения = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	НаборДействий = ПараметрыВыполнения.НаборДействий;
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ОтправитьПовторно"));
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеПодписатьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().Подписать;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.СформироватьОтвет"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Подписать"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  * СформироватьИзвещение - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействияПодписатьОтправитьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ПодписатьОтправить;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.СформироватьОтвет"));

	Если Форма.СформироватьИзвещение Тогда
		ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
			"Перечисление.ДействияПоЭДО.СформироватьИзвещение"));
	КонецЕсли;

	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеПринятьАннулированиеИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().ПринятьАннулирование;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПринятьАннулирование"));
	ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий);
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
Асинх Процедура ВыполнитьДействиеУтвердитьИзКарточкиДокумента(Форма, ЭтоПакетнаяОбработка = Ложь) Экспорт
	
	ВидВыполняемогоДействия = ВидыВыполняемыхДействийСДокументом().Утвердить;
	Если Не ВыполнениеДействийПоЭДОДоступноДляПакета(Форма.СостоянияПакетаОднородно, ЭтоПакетнаяОбработка) Тогда
		ТекстВопроса = ТекстВопросаОВыполненииДействияДляДокументаПакета(ВидВыполняемогоДействия);
		Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНет); // КодВозвратаДиалога
		Если Ответ = КодВозвратаДиалога.Да Тогда
			ЭтоПакетнаяОбработка = Ложь;
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(Форма);
	
	НаборДействий = ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО();
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Утвердить"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.СформироватьОтвет"));
	
	ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка);
	
КонецПроцедуры

// Параметры:
//  Комментарий - Неопределено,Строка
//  ДополнительныеПараметры - Структура:
//  * ФормаВладелец -  ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса
//  * ПараметрыВыполнения - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
Процедура ВыполнитьДействияПоЭДОПослеВводаСтрокиИзКарточкиДокумента(Комментарий, ДополнительныеПараметры) Экспорт
	
	Если Комментарий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастроитьБлокировкуПанелиКомандКарточкиДокумента(ДополнительныеПараметры.ФормаВладелец);
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент);
	ДополнительныеПараметрыДействий = 
		ДополнительныеПараметры.ПараметрыВыполнения.ДополнительныеПараметрыДействий;
	Для Каждого ДополнительныйПараметрДействия Из ДополнительныеПараметрыДействий Цикл
		ДополнительныйПараметрДействия.Значение.Комментарий = Комментарий;
	КонецЦикла;
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ДополнительныеПараметры.ПараметрыВыполнения);
	
КонецПроцедуры

// Параметры:
//  ВыполняемоеДействие - Строка - См. ВидыВыполняемыхДействийСДокументом
// 
// Возвращаемое значение:
//  Строка
Функция ТекстВопросаОВыполненииДействияДляДокументаПакета(ВыполняемоеДействие) Экспорт
	
	ВидыВыполняемыхДействий = ВидыВыполняемыхДействийСДокументом();
	
	Если ВыполняемоеДействие = ВидыВыполняемыхДействий.ПринятьАннулирование
		Или ВыполняемоеДействие = ВидыВыполняемыхДействий.ОтклонитьАннулирование Тогда
		ТекстВопроса = СтрШаблон(НСтр("ru = 'По этому пакету нельзя %1, т.к. документы пакета имеют разные состояния.
			|%2 только для выбранного документа?';
			|en = 'You cannot %1 for this batch as the batch documents have different states.
			|%2 only for the selected document?'"), НРег(ВыполняемоеДействие), ВыполняемоеДействие);
	Иначе
		ТекстВопроса = СтрШаблон(НСтр("ru = 'Этот пакет нельзя %1, т.к. документы пакета имеют разные состояния.
			|%2 только выбранный документ?';
			|en = 'You cannot %1 this batch as the batch documents have different states.
			|%2 only the selected document?'"), НРег(ВыполняемоеДействие), ВыполняемоеДействие);
	КонецЕсли;
	
	Возврат ТекстВопроса;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
// * Утвердить - Строка
// * Подписать - Строка
// * ПодписатьОтправить - Строка
// * Отправить - Строка
// * Отклонить - Строка
// * ОтклонитьПодписание - Строка
// * ОтправитьПовторно - Строка
// * ВернутьНаПодготовкуКОтправке - Строка
// * ПринятьАннулирование - Строка
// * ОтклонитьАннулирование - Строка
// * Аннулировать - Строка
// * ЗакрытьПринудительно - Строка
// * ВернутьВРаботу - Строка
Функция ВидыВыполняемыхДействийСДокументом() Экспорт

	ВидыВыполняемыхДействий = Новый Структура;
	ВидыВыполняемыхДействий.Вставить("Утвердить", НСтр("ru = 'Утвердить';
														|en = 'Approve'"));
	ВидыВыполняемыхДействий.Вставить("Подписать", НСтр("ru = 'Подписать';
														|en = 'Sign'"));
	ВидыВыполняемыхДействий.Вставить("ПодписатьОтправить", НСтр("ru = 'Подписать и отправить';
																|en = 'Sign and send'"));
	ВидыВыполняемыхДействий.Вставить("Отправить", НСтр("ru = 'Отправить';
														|en = 'Send'"));
	ВидыВыполняемыхДействий.Вставить("Отклонить", НСтр("ru = 'Отклонить';
														|en = 'Reject'"));
	ВидыВыполняемыхДействий.Вставить("ОтклонитьПодписание", НСтр("ru = 'Отклонить подписание';
																|en = 'Reject signing'"));
	ВидыВыполняемыхДействий.Вставить("ОтправитьПовторно", НСтр("ru = 'Отправить повторно';
																|en = 'Send again'"));
	ВидыВыполняемыхДействий.Вставить("ВернутьНаПодготовкуКОтправке", НСтр("ru = 'Вернуть на подготовку к отправке';
																			|en = 'Return to sending preparation'"));
	ВидыВыполняемыхДействий.Вставить("ПринятьАннулирование", НСтр("ru = 'Подтвердить аннулирование';
																	|en = 'Confirm canceling'"));
	ВидыВыполняемыхДействий.Вставить("ОтклонитьАннулирование", НСтр("ru = 'Отклонить аннулирование';
																	|en = 'Reject cancellation'"));
	ВидыВыполняемыхДействий.Вставить("Аннулировать", НСтр("ru = 'Аннулировать';
															|en = 'Cancel'"));
	ВидыВыполняемыхДействий.Вставить("ЗакрытьПринудительно", НСтр("ru = 'Закрыть принудительно';
																	|en = 'Forcefully close'"));
	ВидыВыполняемыхДействий.Вставить("ВернутьВРаботу", НСтр("ru = 'Вернуть в работу';
															|en = 'Return'"));
	Возврат ВидыВыполняемыхДействий;

КонецФункции

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область Подписи

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ОбработкаЗавершения - ОписаниеОповещения
Процедура ВыполнитьПроверкуПодписейИзКарточкиДокумента(Форма, ОбработкаЗавершения) Экспорт
	
	Если Не ЗначениеЗаполнено(Форма.Объект.Ссылка) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для проверки подписей необходимо подписать электронный документ';
				|en = 'To check signatures, sign an electronic document'"));
		Возврат;
	КонецЕсли;
	
	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	ЭлектронныеДокументыЭДОКлиент.ПроверитьПодписиДокумента(
		ОбработкаЗавершения, Форма.Объект.Ссылка, КонтекстДиагностики);
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура
//  * ВизуализацияТабличныйДокумент - ТабличныйДокумент
//  * УникальныйИдентификатор - УникальныйИдентификатор
//  Элемент - См. Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс.Элементы.Подписи
//  Поле - ПолеВвода - элемент таблицы подписей
//  СтандартнаяОбработка - Булево
//  ОбработчикЗавершения - Неопределено,ОписаниеОповещения
Процедура ОбработатьВыборВТаблицеПодписейИзКарточкиДокумента(Форма, Элемент, Поле, СтандартнаяОбработка = Ложь, 
	ОбработчикЗавершения = Неопределено) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле = Форма.Элементы.ПодписиДоверенностьПредставление Тогда

		ПоказатьДоверенностьИзКарточкиДокумента(Форма);

	ИначеЕсли Поле = Форма.Элементы.ПодписиПодписьВернаПредставление Тогда 

		ПоказатьРезультатПроверкиПодписиИзКарточкиДокумента(Форма, ОбработчикЗавершения);

	Иначе
	
		ПоказатьСертификатИзКарточкиДокумента(Форма);
	
	КонецЕсли;
	
КонецПроцедуры

//@skip-check property-return-type, invocation-parameter-type-intersect
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * УникальныйИдентификатор - УникальныйИдентификатор
//  * Элементы - ВсеЭлементыФормы:
//   ** Подписи - ТаблицаФормы - таблица подписей формы
Процедура ПоказатьСертификатИзКарточкиДокумента(Форма) Экспорт
	
	ТекущиеДанные = Форма.Элементы.Подписи.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИнтеграцияБСПБЭДКлиент.ПоказатьСертификатПодписиПодписанногоОбъекта(ТекущиеДанные.ПодписанныйОбъект,
		ТекущиеДанные.ПорядковыйНомер, Форма.УникальныйИдентификатор);
	
КонецПроцедуры

//@skip-check property-return-type, invocation-parameter-type-intersect
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * ЕстьПравоИзмененияМЧД - Булево
//  * Элементы - ВсеЭлементыФормы:
//   ** КнопкаПодписиПроверкаДоверенностиВыполненаВручную - КнопкаФормы
//  Элемент - ТаблицаФормы - таблица подписей формы:
Процедура ОбработатьАктивациюСтрокиВТаблицеПодписейКарточкиДокумента(Форма, Элемент) Экспорт
	
	ТекущиеДанные = Элемент.ТекущиеДанные;

	Форма.Элементы.КнопкаПодписиПроверкаДоверенностиВыполненаВручную.Видимость = Ложь;
	
	Если ТекущиеДанные = Неопределено Или Не Форма.ЕстьПравоИзмененияМЧД
		Или Не ТекущиеДанные.ЭтоВходящаяПодпись Или Не ТекущиеДанные.ЭтоПодписьПоДоверенности Тогда
		Возврат;
	КонецЕсли;
	
	ПроверкаДоверенностиВыполненаВручную = ТекущиеДанные.ПроверкаДоверенностиВыполненаВручную; // Булево
	
	ДоступнаКомандаРучнойПроверки = ПроверкаДоверенностиВыполненаВручную 
		Или Не (ТекущиеДанные.ДоверенностьПроверенаУспешно И ТекущиеДанные.ПроверкаПолномочийВыполненаБезОшибок);
	Форма.Элементы.КнопкаПодписиПроверкаДоверенностиВыполненаВручную.Видимость = ДоступнаКомандаРучнойПроверки;
	
	Если ДоступнаКомандаРучнойПроверки Тогда
		Форма.Элементы.КнопкаПодписиПроверкаДоверенностиВыполненаВручную.Пометка = ПроверкаДоверенностиВыполненаВручную;
	КонецЕсли;
	
КонецПроцедуры

//@skip-check property-return-type, invocation-parameter-type-intersect
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Элементы - ВсеЭлементыФормы:
//   ** Подписи - ТаблицаФормы - таблица подписей формы
//  ОбработкаЗавершения - ОписаниеОповещения
Процедура ПроверитьПодписьПоДоверенностиВручнуюИзКарточкиДокумента(Форма, ОбработкаЗавершения) Экспорт
	
	Если Форма.Элементы.КнопкаПодписиПроверкаДоверенностиВыполненаВручную.Пометка Тогда
		
		ВыполнитьПроверкуПодписейИзКарточкиДокумента(Форма, ОбработкаЗавершения);
		
	Иначе
		
		ТекущиеДанные = Форма.Элементы.Подписи.ТекущиеДанные;
		
		ДанныеПодписиОбъекта = ЛегкийИнтерфейсДокументовЭДОВызовСервера.ДанныеПодписиОбъектаДляОткрытияРезультатаПроверки(
			ТекущиеДанные.ПодписанныйОбъект, ТекущиеДанные.ПорядковыйНомер);
		
		МашиночитаемыеДоверенностиВызовСервера.ЗаписатьРезультатРучнойПроверкиПодписи(
			ДанныеПодписиОбъекта.ДанныеПодписи);
		
		Результат = Новый Структура("Успех, КонтекстДиагностики",
			Истина, ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики());
			
		ВыполнитьОбработкуОповещения(ОбработкаЗавершения, Результат);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // Подписи

#Область ОткрытиеФайла

//@skip-check property-return-type, invocation-parameter-type-intersect
// Параметры:
//  ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  ЭтоОблачныйЭДО - Булево
//  ИдентификаторФормы - УникальныйИдентификатор
Процедура ОткрытьФайлИнформацииОтправителяДокумента(ЭлектронныйДокумент, ЭтоОблачныйЭДО, ИдентификаторФормы) Экспорт
	
	ДлительнаяОперация = ЛегкийИнтерфейсДокументовЭДОВызовСервера.ДанныеФайлаИнформацииОтправителяДокумента(
		ЭлектронныйДокумент, ИдентификаторФормы, ЭтоОблачныйЭДО);
	
	Если ДлительнаяОперация.Статус = "Выполнено" Тогда
		ОткрытьФайлИнформацииОтправителяДокументаПослеОжидания(ДлительнаяОперация, ИдентификаторФормы);
		Возврат;
	КонецЕсли;
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОткрытьФайлИнформацииОтправителяДокументаПослеОжидания",
		ЭтотОбъект, ИдентификаторФормы);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(Неопределено);
	ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

// @skip-check invocation-parameter-type-intersect, property-return-type
//
// Параметры:
//  Результат - См. ДлительныеОперацииКлиент.НовыйРезультатДлительнойОперации
//  ИдентификаторФормы - УникальныйИдентификатор
Процедура ОткрытьФайлИнформацииОтправителяДокументаПослеОжидания(Результат, ИдентификаторФормы) Экспорт

	КонтекстДиагностики = ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики();
	
	РезультатПолучения = ОбщегоНазначенияБЭДКлиент.РезультатВыполненияФоновогоЗадания(
		НСтр("ru = 'Получение данных файла информации отправителя электронного документа';
			|en = 'Get information file data of the electronic document sender'"),
		Результат, КонтекстДиагностики);

	Если РезультатПолучения = Неопределено Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(КонтекстДиагностики);
		Возврат;
	КонецЕсли;
	
	Если РезультатПолучения.ДанныеФайла = Неопределено Тогда
		ОбработкаНеисправностейБЭДКлиент.ОбработатьОшибки(РезультатПолучения.КонтекстДиагностики);
		Возврат;
	КонецЕсли;
	
	ДанныеФайла = РезультатПолучения.ДанныеФайла; // См. ЛегкийИнтерфейсДокументовЭДО.НовыеДанныеФайлаИнформацииОтправителя
	ПолныеДанныеФайла = ДанныеФайла.ПолныеДанныеФайла; // См. РаботаСФайлами.ДанныеФайла
	ДанныеФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(ДанныеФайла.ДвоичныеДанные, ИдентификаторФормы);
	Если ДанныеФайла.ПомеченНаУдаление Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			ИнтерфейсДокументовЭДОКлиентСервер.ТекстСообщенияОНеобходимостиСнятьПометкуУдаленияДляОткрытияФайла());
	ИначеЕсли ЗначениеЗаполнено(ПолныеДанныеФайла) Тогда
		ПолныеДанныеФайла.СсылкаНаДвоичныеДанныеФайла = ДанныеФайлаВоВременномХранилище;
		РаботаСФайламиКлиент.ОткрытьФайл(ПолныеДанныеФайла);
	Иначе
		ФайловаяСистемаКлиент.ОткрытьФайл(ДанныеФайлаВоВременномХранилище,, ДанныеФайла.ИмяФайла);
	КонецЕсли;

КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * УникальныйИдентификатор - УникальныйИдентификатор
//  * РежимСозданияПакетаПоПредварительнымДанным - Булево
//  * ПредварительныйПакетДокументов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ИдентификаторДокумента - УникальныйИдентификатор
//   ** АдресОписанияФайла - Строка
//   ** ИмяФайла - Строка
//  * СоставПакета - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ИдентификаторДокумента - УникальныйИдентификатор
//  * ТекущийИдентификаторДокумента - УникальныйИдентификатор
//
Процедура ОткрытьФайлПредварительногоПакетаКарточкиДокумента(Форма) Экспорт
	
	Если Не Форма.РежимСозданияПакетаПоПредварительнымДанным Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ИдентификаторДокумента", Форма.ТекущийИдентификаторДокумента);
	
	НайденныеСтроки = Форма.ПредварительныйПакетДокументов.НайтиСтроки(Отбор);
	Если Не НайденныеСтроки.Количество() Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПредварительногоДокумента = НайденныеСтроки[0];

	ОписаниеФайла = ПолучитьИзВременногоХранилища(ДанныеПредварительногоДокумента.АдресОписанияФайла); // См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
	
	ДанныеФайлаВоВременномХранилище = ПоместитьВоВременноеХранилище(
		ОписаниеФайла.ДвоичныеДанные, Форма.УникальныйИдентификатор);
	ФайловаяСистемаКлиент.ОткрытьФайл(ДанныеФайлаВоВременномХранилище,, ОписаниеФайла.ИмяФайла);
	
КонецПроцедуры

#КонецОбласти // ОткрытиеФайла

#Область СформироватьПакетЭДИзФайлаСДискаИОбъектаУчета

// Параметры:
//  ПараметрКоманды - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  ПараметрыВыполненияКоманды - Структура
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчета(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран объект учета для формирования электронного документа';
										|en = 'An accounting object to generate the electronic document is not selected'"));
		Возврат;
	КонецЕсли;
	
	ОбработчикПослеВыбораФайлов = Новый ОписаниеОповещения(
		"СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеВыбораФайлов",
		ЛегкийИнтерфейсДокументовЭДОКлиент, ПараметрКоманды);
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файлы для формирования пакета документов';
												|en = 'Select the files to generate document packages'");
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Истина;
	ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОбработчикПослеВыбораФайлов, ПараметрыЗагрузки);
	
КонецПроцедуры

#КонецОбласти // СформироватьПакетЭДИзФайловСДискаИОбъектаУчета

#Область СоздатьДокументПоФайлу

// Параметры:
//  ИдентификаторВладельца - УникальныйИдентификатор
Процедура СоздатьДокументыПоФайламКоманда(ИдентификаторВладельца) Экспорт
	
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = ИдентификаторВладельца;
	
	Если ИнтерфейсДокументовЭДОВызовСервера.ТребуетсяВыборФайлаПередСозданиемНовогоПроизвольногоДокумента() Тогда
		ОбработчикПослеВыбораФайлов = Новый ОписаниеОповещения("СоздатьДокументыПоФайламПослеВыбораФайловСДиска",
			ЛегкийИнтерфейсДокументовЭДОКлиент);
		
		ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файлы для создания документов';
													|en = 'Select the files to create the documents'");
		ФайловаяСистемаКлиент.ЗагрузитьФайлы(ОбработчикПослеВыбораФайлов, ПараметрыЗагрузки);
	Иначе
		ОбработчикПослеВыбораФайла = Новый ОписаниеОповещения("СоздатьДокументПоФайлуПослеВыбораФайлаСДиска",
			ЛегкийИнтерфейсДокументовЭДОКлиент);
		
		ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Выберите файл для создания документа';
													|en = 'Select the file to create the document'");
		ФайловаяСистемаКлиент.ЗагрузитьФайл(ОбработчикПослеВыбораФайла, ПараметрыЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СоздатьДокументПоФайлу

#КонецОбласти // ОбработкаКоманд

#Область ИнтеграцияСИнтерфейсомУчетныхДокументов

// Параметров оповещения для формы документа.
//
// Возвращаемое значение:
//  Структура:
//    * Форма - ФормаКлиентскогоПриложения
//    * ДокументСсылка - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//
Функция ПараметрыОповещенияЭДО_ФормаДокумента() Экспорт

	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Форма");
	ПараметрыОповещения.Вставить("ДокументСсылка");
	//@skip-check constructor-function-return-section
	Возврат ПараметрыОповещения;

КонецФункции

// Обработчик события "ОбработкаОповещения" формы документа.
//
// Параметры:
//  ИмяСобытия - Строка
//  Параметр - Неопределено
//           - Структура:
//             * ДокументыУчета - Массив из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//           - См. ИнтерфейсДокументовЭДОКлиент.НовыеПараметрыОповещенияПриФормированииПодписанииОтправкеДокумента
//           - См. ПакетыДокументовЭДОКлиент.НовыеПараметрыОповещенияПриИзмененииПакетаДокументовЭДОПоОбъектуУчета
//  Источник - Произвольный
//  ПараметрыОповещения - См. ПараметрыОповещенияЭДО_ФормаДокумента
//
Процедура ОбработкаОповещения_ФормаДокумента(ИмяСобытия, Параметр, Источник, ПараметрыОповещения) Экспорт

	ОбъектУчета = ПараметрыОповещения.ДокументСсылка;
	
	Если ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияЭлектронныйДокументСформированПодписанОтправлен() Тогда
		
		Если Параметр.ОбъектыУчета.Найти(ОбъектУчета) <> Неопределено Тогда
			ПараметрыОповещения.Форма.Закрыть();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ПакетыДокументовЭДОКлиент.ИмяСобытияИзмененияПакетаДокументовЭДОПоОбъектуУчета() Тогда
		
		Если Параметр.ОбъектыУчета.Найти(ОбъектУчета) <> Неопределено Тогда
			ПараметрыОповещения.Форма.Закрыть();
		КонецЕсли;
			
		Если Параметр.ДокументыПакета.Количество() Тогда
			ИнтерфейсДокументовЭДОКлиент.ОткрытьЭлектронныйДокумент(Параметр.ДокументыПакета[0]);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = ИнтерфейсДокументовЭДОКлиент.ИмяСобытияОбновленияСостоянияЭДО()
		Или ИмяСобытия = "ОбновитьСостояниеПриглашений" Тогда
		
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыУчета")
			И ТипЗнч(Параметр.ДокументыУчета) = Тип("Массив")
			И Параметр.ДокументыУчета.Найти(ОбъектУчета) = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		ПараметрыОповещения.Форма.Прочитать();
		
		ПодключаемыеКомандыЭДОКлиент.ОбновитьКоманды(ПараметрыОповещения.Форма, ПараметрыОповещения.Форма.Объект);
		
	КонецЕсли;
	
	ОбработкаОповещенияДляПризнакаОтсутствияУчетныхЗаписейЭДО(ПараметрыОповещения.Форма, ИмяСобытия);

КонецПроцедуры

#КонецОбласти // ИнтеграцияСИнтерфейсомУчетныхДокументов

#Область ОбработчикиСобытийФорм

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * РежимСозданияПакетаПоПредварительнымДанным - Булево
//  * Объект - ДанныеФормыСтруктура:
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.УчастникЭДО
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//  * ПредварительныйПакетДокументов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** Контрагент - ОпределяемыйТип.УчастникЭДО
//   ** Организация - ОпределяемыйТип.Организация
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//
Процедура ОбновитьКлючевыеРеквизитыВДокументахПредварительногоПакетаКарточкиДокумента(Форма) Экспорт
	
	Если Не Форма.РежимСозданияПакетаПоПредварительнымДанным Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ДанныеПредварительногоДокумента Из Форма.ПредварительныйПакетДокументов Цикл
		ДанныеПредварительногоДокумента.Организация = Форма.Объект.Организация;
		ДанныеПредварительногоДокумента.Контрагент = Форма.Объект.Контрагент;
		ДанныеПредварительногоДокумента.ДоговорКонтрагента = Форма.Объект.ДоговорКонтрагента;
		
		Если Не ЗначениеЗаполнено(ДанныеПредварительногоДокумента.МаршрутПодписания) Тогда
			ДанныеПредварительногоДокумента.МаршрутПодписания = Форма.Объект.МаршрутПодписания;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * РежимСозданияПакетаПоПредварительнымДанным - Булево
//  * ТекущийИдентификаторДокумента - УникальныйИдентификатор
//  * ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//  * СопроводительнаяЗапискаТекст - Строка
//  * Объект - ДанныеФормыСтруктура:
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.УчастникЭДО
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   ** ДатаДокумента - Дата
//   ** НомерДокумента - Строка
//   ** СуммаДокумента - Число
//   ** ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//   ** СписокПодписантов - ТаблицаЗначений:
//    *** Подписант - ОпределяемыйТип.Пользователь
//    *** Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//  * ПредварительныйПакетДокументов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** ИдентификаторДокумента - УникальныйИдентификатор
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   ** ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//   ** ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//   ** ДатаДокумента - Дата
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** Контрагент - ОпределяемыйТип.УчастникЭДО
//   ** НомерДокумента - Строка
//   ** Организация - ОпределяемыйТип.Организация
//   ** СуммаДокумента - Число
//   ** СопроводительнаяЗаписка - Строка
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//   ** СписокПодписантов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//    *** Подписант - ОпределяемыйТип.Пользователь
//    *** Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
//
Процедура КэшироватьЗначенияРеквизитовВПредварительномПакетеКарточкиДокумента(Форма) Экспорт
	
	Если Не Форма.РежимСозданияПакетаПоПредварительнымДанным Тогда
		Возврат;
	КонецЕсли;
	
	Объект = Форма.Объект;
	
	Отбор = Новый Структура("ИдентификаторДокумента", Форма.ТекущийИдентификаторДокумента);
	НайденныеСтроки = Форма.ПредварительныйПакетДокументов.НайтиСтроки(Отбор);
	
	Для Каждого ДанныеПредварительногоДокумента Из НайденныеСтроки Цикл
		ДанныеПредварительногоДокумента.ВидДокумента = Объект.ВидДокумента;
		ДанныеПредварительногоДокумента.ДатаДокумента = Объект.ДатаДокумента;
		ДанныеПредварительногоДокумента.ДоговорКонтрагента = Объект.ДоговорКонтрагента;
		ДанныеПредварительногоДокумента.Контрагент = Объект.Контрагент;
		ДанныеПредварительногоДокумента.НомерДокумента = Объект.НомерДокумента;
		ДанныеПредварительногоДокумента.Организация = Объект.Организация;
		ДанныеПредварительногоДокумента.СуммаДокумента = Объект.СуммаДокумента;
		ДанныеПредварительногоДокумента.ТипРегламента = Объект.ТипРегламента;
		ДанныеПредварительногоДокумента.ТипДокумента = Форма.ТипДокумента;
		ДанныеПредварительногоДокумента.СопроводительнаяЗаписка = Форма.СопроводительнаяЗапискаТекст;
		ДанныеПредварительногоДокумента.МаршрутПодписания = Объект.МаршрутПодписания;
		
		Для Каждого ДанныеПодписанта Из Объект.СписокПодписантов Цикл
			НовыеДанныеПодписанта = ДанныеПредварительногоДокумента.СписокПодписантов.Добавить();
			ЗаполнитьЗначенияСвойств(НовыеДанныеПодписанта, ДанныеПодписанта);
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти // ОбработчикиСобытийФорм

#Область СозданиеДокументовПакетаПоПредварительнымДанным

// Параметры:
//  Форма - ФормаКлиентскогоПриложения:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//   ** ЭтоОблачныйЭДО - Булево
//  * ТекущийИдентификаторДокумента - УникальныйИдентификатор
//  * ПредварительныйПакетДокументов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//  ** ИдентификаторДокумента - УникальныйИдентификатор
//  ** АдресОписанияФайла - Строка
//  ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//  ** ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//  ** ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//  ** ДатаДокумента - Дата
//  ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//  ** Контрагент - ОпределяемыйТип.УчастникЭДО
//  ** НомерДокумента - Строка
//  ** Организация - ОпределяемыйТип.Организация
//  ** ИмяФайла - Строка
//  ** СуммаДокумента - Число
//  ** СопроводительнаяЗаписка - Строка
//  ОбработчикПослеФормированияПакета - ОписаниеОповещения - процедура, которая будет вызвана после создания пакета:
//  * РезультатСоздания - Неопределено
//                      - Структура:
//                        * ИдентификаторПакета - Неопределено - если пакет не удалось создать.
//                                              - УникальныйИдентификатор
//                        * КонтекстДиагностики - См. ОбработкаНеисправностейБЭД.НовыйКонтекстДиагностики
//  * ПараметрыОповещения - Структура:
//                          * ОбработчикЗавершения - ОписаниеОповещения
//                          * ЭтоНовыйДокумент - Булево
//
Процедура СоздатьДокументыПакетаПоПредварительнымДаннымИзКарточкиДокумента(Форма,
	ОбработчикПослеФормированияПакета) Экспорт
	
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	
	Для Каждого ДанныеПредварительногоДокумента Из Форма.ПредварительныйПакетДокументов Цикл

		Если ДанныеПредварительногоДокумента.ИдентификаторДокумента = Форма.ТекущийИдентификаторДокумента
			И Не Форма.Объект.ЭтоОблачныйЭДО Тогда
			Продолжить;
		КонецЕсли;
		
		ОписаниеДокументаПоФайлам = ОписаниеДокументаДляФормированияПоПредварительнымДанным(
			ДанныеПредварительногоДокумента);
		ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОписанияДокументовПоФайлам.Добавить(ОписаниеДокументаПоФайлам);
		
	КонецЦикла;
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(ПараметрыВыполненияДействийПоЭДО.НаборДействий,
		ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Сформировать"));
	
	ПараметрыОповещенияПослеФормированияПоПредварительнымДанным = Новый Структура;
	ПараметрыОповещенияПослеФормированияПоПредварительнымДанным.Вставить("ТекущийДокумент", Форма.Объект.Ссылка);
	ПараметрыОповещенияПослеФормированияПоПредварительнымДанным.Вставить("ЭтоОблачныйЭДО", Форма.Объект.ЭтоОблачныйЭДО);
	ПараметрыОповещенияПослеФормированияПоПредварительнымДанным.Вставить("ОбработчикПослеФормированияПакета",
		ОбработчикПослеФормированияПакета);
	
	ОбработчикПослеВыполненияДействияПоЭДО = Новый ОписаниеОповещения(
		"ОбъединитьДокументыВПакетПослеФормированияПоПредварительнымДанным", ЛегкийИнтерфейсДокументовЭДОКлиент,
		ПараметрыОповещенияПослеФормированияПоПредварительнымДанным);
	
	ПараметрыОповещенияПослеВыполненияДействийПоЭДО = Новый Структура("ОповещениеУспешногоЗавершения",
		ОбработчикПослеВыполненияДействияПоЭДО);
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент,
		ПараметрыОповещенияПослеВыполненияДействийПоЭДО);
	
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

#КонецОбласти // СозданиеДокументовПакетаПоПредварительнымДанным

// Параметры:
//  Форма - ФормаКлиентскогоПриложения
//  ИмяСобытия - Строка
Процедура ОбработкаОповещенияДляПризнакаОтсутствияУчетныхЗаписейЭДО(Форма, ИмяСобытия) Экспорт
	
	//@skip-check property-return-type
	Если ИмяСобытия = УчетныеЗаписиЭДОКлиент.ИмяСобытияСозданаУчетнаяЗаписьЭДО()
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Форма, "ПараметрыОбменаСКонтрагентами")
		И Форма.ПараметрыОбменаСКонтрагентами.Свойство("ОтсутствуютУчетныеЗаписиЭДО") Тогда
		
		Форма.ПараметрыОбменаСКонтрагентами.ОтсутствуютУчетныеЗаписиЭДО = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  Структура:
// * Успех - Булево
// * ОперацияПолученияДанных - Неопределено
//                           - См. ДлительныеОперации.ВыполнитьФункцию
// * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
Функция НовыйРезультатОжиданияДанныхКомпонентовПросмотраДокумента()

	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ОперацияПолученияДанных", Неопределено);
	Результат.Вставить("КонтекстДиагностики", ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики());
	Возврат Результат;

КонецФункции

#Область ОбработкаКоманд

#Область ДополнительныеКоманды

// Электронные документы из состава пакета.
// 
// Параметры:
//  СоставПакета - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//                         - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
// 
// Возвращаемое значение:
//  Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО
Функция ЭлектронныеДокументыИзСоставаПакета(СоставПакета) 

	ЭлектронныеДокументы = Новый Массив; // Массив Из ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
	Для Каждого ДанныеПакета Из СоставПакета Цикл
		ЭлектронныеДокументы.Добавить(ДанныеПакета.ЭлектронныйДокумент);
	КонецЦикла;
	
	Возврат ЭлектронныеДокументы;

КонецФункции

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Организация - ОпределяемыйТип.Организация
//   ** Контрагент - ОпределяемыйТип.КонтрагентБЭД
//   ** ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//   ** ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//   ** ИдентификаторОрганизации - Строка
//   ** ИдентификаторКонтрагента - Строка
//   ** ВыгружатьДополнительныеСведения - Булево
//   ** МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//   ** ОбменБезПодписи - Булево
//   ** СпособОбмена - ПеречислениеСсылка.СпособыОбменаЭД
//   ** ТребуетсяИзвещение - Булево
//   ** ТребуетсяПодтверждение - Булево
//   ** ФорматОсновногоТитула - Строка
//
// Возвращаемое значение:
//  См. НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки
Функция НастройкаОтправкиПоДаннымКарточкиДокумента(Форма)
	
	Настройка = НастройкиЭДОКлиентСервер.НоваяНастройкаОтправки();
	Настройка.ИдентификаторОтправителя = Форма.Объект.ИдентификаторОрганизации;
	Настройка.ИдентификаторПолучателя = Форма.Объект.ИдентификаторКонтрагента;
	Настройка.ВидДокумента = Форма.Объект.ВидДокумента;
	Настройка.ВыгружатьДополнительныеСведения = Форма.Объект.ВыгружатьДополнительныеСведения;
	Настройка.Договор = Форма.Объект.ДоговорКонтрагента;
	Настройка.МаршрутПодписания = Форма.Объект.МаршрутПодписания;
	Настройка.ОбменБезПодписи = Форма.Объект.ОбменБезПодписи;
	Настройка.Отправитель = Форма.Объект.Организация;
	Настройка.Получатель = Форма.Объект.Контрагент;
	Настройка.СпособОбмена = Форма.Объект.СпособОбмена;
	Настройка.ТребуетсяИзвещениеОПолучении = Форма.Объект.ТребуетсяИзвещение;
	Настройка.ТребуетсяОтветнаяПодпись = Форма.Объект.ТребуетсяПодтверждение;
	Настройка.ЭтоНастройкаОтправки = Истина;
	Настройка.Формат = Форма.Объект.ФорматОсновногоТитула;
	
	Возврат Настройка;
	
КонецФункции

#КонецОбласти // ДополнительныеКоманды

#Область ВыполнениеДействийПоЭДО

// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ИдентификаторПакета - УникальныйИдентификатор
//  НаборДействий - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыйНаборДействийПоЭДО
//  ЭтоПакетнаяОбработка - Булево
Процедура ВыполнитьДействияПоЭДОИзКарточкиДокумента(Форма, НаборДействий, ЭтоПакетнаяОбработка = Ложь)
	
	ПараметрыВыполненияДействийПоЭДО = ЭлектронныеДокументыЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	
	Если ЭтоПакетнаяОбработка И ЗначениеЗаполнено(Форма.ИдентификаторПакета) Тогда
		ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ПакетыДокументов.Добавить(Форма.ИдентификаторПакета);
	Иначе
		ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ЭлектронныеДокументы.Добавить(Форма.Объект.Ссылка);
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура("ПараметрыВыполненияДействийПоЭДО", ПараметрыВыполненияДействийПоЭДО);
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент,
		ДополнительныеПараметры);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

// Параметры:
//  ПараметрыВводаСтроки - См. ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки
//  ОбработчикЗавершения - ОписаниеОповещения
Процедура ПоказатьВводСтрокиДляВыполненияДействийПоЭДО(ПараметрыВводаСтроки, ОбработчикЗавершения)
	
	ПараметрыВводаСтроки.Многострочность = Истина;
	ПараметрыВводаСтроки.Обязательность = Истина;
	ОбщегоНазначенияБЭДКлиент.ПоказатьВводСтрокиБЭД(ОбработчикЗавершения, ПараметрыВводаСтроки);
	
КонецПроцедуры

// Параметры:
//  НаборДействий - Соответствие Из КлючИЗначение:
//  * Ключ - ПеречислениеСсылка.ДействияПоЭДО
//  * Значение - Булево
Процедура ДобавитьДействияПодписатьОтправитьПодготовитьКОтправке(НаборДействий)
	
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Подписать"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.ПодготовитьКОтправке"));
	ИнтерфейсДокументовЭДОКлиентСервер.ДобавитьДействие(НаборДействий, ПредопределенноеЗначение(
		"Перечисление.ДействияПоЭДО.Отправить"));
	
КонецПроцедуры

// Параметры:
//  СостоянияПакетаОднородно - Булево
//  ЭтоПакетнаяОбработка - Булево
// 
// Возвращаемое значение:
//  Булево
Функция ВыполнениеДействийПоЭДОДоступноДляПакета(СостоянияПакетаОднородно, ЭтоПакетнаяОбработка)
	
	Если Не ЭтоПакетнаяОбработка
		Или СостоянияПакетаОднородно Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Параметры:
//  Комментарий - Неопределено,Строка
//  ПараметрыВыполнения - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
Процедура ВыполнитьДействияПоЭДОПослеВводаСтроки(Комментарий, ПараметрыВыполнения) Экспорт
	
	Если Комментарий = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеВыполненияДействийПоЭДО", ИнтерфейсДокументовЭДОКлиент);
	ДополнительныеПараметрыДействий = ПараметрыВыполнения.ДополнительныеПараметрыДействий;
	Для Каждого ДополнительныйПараметрДействия Из ДополнительныеПараметрыДействий Цикл
		ДополнительныйПараметрДействия.Значение.Комментарий = Комментарий;
	КонецЦикла;
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполнения);
	
КонецПроцедуры

// Возвращаемое значение:
//  См. ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки
Функция ПараметрыВводаСтрокиДляОтклоненияДокумента()
	
	ПараметрыВводаСтроки = ОбщегоНазначенияБЭДКлиент.ПараметрыВводаСтроки();
	ПараметрыВводаСтроки.ЗаголовокФормы = НСтр("ru = 'Укажите причины отклонения документа';
												|en = 'Specify the reasons for the document rejection'");
	ПараметрыВводаСтроки.НазваниеКнопкиПоУмолчанию = НСтр("ru = 'Отклонить';
															|en = 'Reject'");
	ПараметрыВводаСтроки.КомментарийОбязательностиВвода =
		НСтр("ru = 'Для отклонения документа необходимо указать причину.';
			|en = 'Specify a reason for the document rejection.'");
	Возврат ПараметрыВводаСтроки;
	
КонецФункции

#КонецОбласти // ВыполнениеДействийПоЭДО

#Область ОтражениеВУчете

// Возвращаемое значение:
//  Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ЭтоДокументОблачногоЭДО - Булево
//  * ЕстьНоменклатураНаКонтроле - Булево 
//  * ДокументыПакета - Массив Из ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * ИдентификаторФормы - УникальныйИдентификатор
Функция НовыеПараметрыСозданияОбъектовУчета()

	ПараметрыОтраженияВУчете = Новый Структура;
	ПараметрыОтраженияВУчете.Вставить("ЭлектронныйДокумент",
		ПредопределенноеЗначение("Документ.ЭлектронныйДокументВходящийЭДО.ПустаяСсылка"));
	ПараметрыОтраженияВУчете.Вставить("ЭтоДокументОблачногоЭДО", Ложь);
	ПараметрыОтраженияВУчете.Вставить("ЕстьНоменклатураНаКонтроле", Ложь);
	ПараметрыОтраженияВУчете.Вставить("ДокументыПакета", Новый Массив);
	ПараметрыОтраженияВУчете.Вставить("ИдентификаторФормы", Новый УникальныйИдентификатор);
	Возврат ПараметрыОтраженияВУчете;
	
КонецФункции

// Возвращаемое значение:
//  Структура:
//  * ЭлектронныйДокумент - ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * Организация - Неопределено,ОпределяемыйТип.Организация
//  * Контрагент - Неопределено,ОпределяемыйТип.КонтрагентБЭД
Функция НовыеПараметрыПодбораОбъектовУчета()

	ПараметрыПодбора = Новый Структура;
	ПараметрыПодбора.Вставить("ЭлектронныйДокумент",
		ПредопределенноеЗначение("Документ.ЭлектронныйДокументВходящийЭДО.ПустаяСсылка"));
	ПараметрыПодбора.Вставить("Организация", Неопределено);
	ПараметрыПодбора.Вставить("Контрагент", Неопределено);
	Возврат ПараметрыПодбора;
	
КонецФункции

// Параметры:
//  ПараметрыПодбора - См. НовыеПараметрыПодбораОбъектовУчета
//  СпособОбработки - Строка
//
// Возвращаемое значение:
// - Неопределено
// - Структура:
//  * СпособОбработки - Строка
//  * ИмяОбъектаМетаданных - Строка
//  * ИмяТипаСсылки - Строка
//  * Организация - Неопределено,ОпределяемыйТип.Организация
//  * Контрагент - Неопределено,ОпределяемыйТип.УчастникЭДО
Функция НастройкиПодбораОбъектаУчета(ПараметрыПодбора, СпособОбработки)

	Настройки = Новый Структура;

	ПараметрыОбъектаУчета = ЛегкийИнтерфейсДокументовЭДОВызовСервера.ПараметрыОбъектаУчетаПоСпособуОбработки(
		СпособОбработки);
	Если Не ЗначениеЗаполнено(ПараметрыОбъектаУчета) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Настройки.Вставить("СпособОбработки", "");
	Настройки.Вставить("ИмяОбъектаМетаданных", "");
	Настройки.Вставить("ИмяТипаСсылки", "");
	Настройки.Вставить("Организация", Неопределено);
	Настройки.Вставить("Контрагент", Неопределено);

	Настройки.СпособОбработки = СпособОбработки;
	Настройки.ИмяОбъектаМетаданных = ПараметрыОбъектаУчета.ИмяОбъектаМетаданных;
	Настройки.ИмяТипаСсылки = ПараметрыОбъектаУчета.ИмяТипаСсылки;
	Настройки.Организация = ПараметрыПодбора.Организация;
	Настройки.Контрагент = ПараметрыПодбора.Контрагент;

	Возврат Настройки;

КонецФункции

#КонецОбласти // ОтражениеВУчете

#Область Подписи

//@skip-check property-return-type, statement-type-change
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Объект - ДанныеФормыСтруктура:
//   ** Ссылка - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО,ДокументСсылка.ЭлектронныйДокументВходящийЭДО
//  * Элементы - ВсеЭлементыФормы:
//   ** Подписи - См. Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс.Элементы.Подписи
//              - См. Документ.ЭлектронныйДокументИсходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс.Элементы.Подписи
//  * ВизуализацияТабличныйДокумент - ТабличныйДокумент
//  ОбработчикЗавершения - Неопределено,ОписаниеОповещения
Процедура ПоказатьРезультатПроверкиПодписиИзКарточкиДокумента(Форма, ОбработчикЗавершения = Неопределено)
	
	ТекущиеДанные = Форма.Элементы.Подписи.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеПодписиОбъекта = ЛегкийИнтерфейсДокументовЭДОВызовСервера.ДанныеПодписиОбъектаДляОткрытияРезультатаПроверки(
		ТекущиеДанные.ПодписанныйОбъект, ТекущиеДанные.ПорядковыйНомер);

	Если Не ЗначениеЗаполнено(ДанныеПодписиОбъекта.ДанныеПодписи) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось получить данные подписи';
														|en = 'Cannot get the signature data'"));
		Возврат;
	КонецЕсли;

	ПараметрыФормы = МашиночитаемыеДоверенностиКлиент.НовыеПараметрыФормыРезультатовПроверкиПодписи();	
	
	ПараметрыФормы.СвойстваПодписи = ДанныеПодписиОбъекта.ДанныеПодписи.СвойстваПодписи; 
	ПараметрыФормы.СвойстваДоверенности = ДанныеПодписиОбъекта.ДанныеПодписи.СвойстваДоверенности; 
	ПараметрыФормы.РезультатПроверки = ДанныеПодписиОбъекта.ДанныеПодписи.РезультатПроверкиПоМЧД;
	
	Если ТекущиеДанные.ЭтоПодписьПоДоверенности Тогда
		
		ПараметрыФормы.ЭлектронныйДокумент = Форма.Объект.Ссылка;
		ПараметрыФормы.ПредставлениеДокумента = Форма.ВизуализацияТабличныйДокумент;
		ПараметрыФормы.ПодписанныйОбъект = ТекущиеДанные.ПодписанныйОбъект;
		ПараметрыФормы.СообщениеЭДО = ДанныеПодписиОбъекта.СообщениеЭДО;
		
	КонецЕсли;
	
	ПараметрыОткрытия = ОбщегоНазначенияБЭДКлиент.НовыеПараметрыОткрытияФормы();
	ПараметрыОткрытия.Владелец = Форма;
	ПараметрыОткрытия.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ПараметрыОткрытия.ОписаниеОповещенияОЗакрытии = ОбработчикЗавершения;

	МашиночитаемыеДоверенностиКлиент.ОткрытьРезультатыПроверкиПодписи(ПараметрыФормы, ПараметрыОткрытия);
	
КонецПроцедуры

//@skip-check property-return-type, variable-value-type, invocation-parameter-type-intersect
// Параметры:
//  Форма - ФормаКлиентскогоПриложения - форма просмотра входящего/исходящего электронного документа легкого интерфейса:
//  * Элементы - ВсеЭлементыФормы:
//   ** Подписи - См. Документ.ЭлектронныйДокументВходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс.Элементы.Подписи
//              - См. Документ.ЭлектронныйДокументИсходящийЭДО.Форма.ФормаПросмотраЛегкийИнтерфейс.Элементы.Подписи
Процедура ПоказатьДоверенностьИзКарточкиДокумента(Форма)

	ТекущиеДанные = Форма.Элементы.Подписи.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПодписанныйОбъект = ТекущиеДанные.ПодписанныйОбъект; // СправочникСсылка.СообщениеЭДОПрисоединенныеФайлы
	ДанныеПодписиОбъекта = ЛегкийИнтерфейсДокументовЭДОВызовСервера.ДанныеПодписиОбъектаДляОткрытияРезультатаПроверки(
		ПодписанныйОбъект, ТекущиеДанные.ПорядковыйНомер);

	Если Не ЗначениеЗаполнено(ДанныеПодписиОбъекта.ДанныеПодписи) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Не удалось получить данные подписи';
														|en = 'Cannot get the signature data'"));
		Возврат;
	КонецЕсли;

	РезультатПроверкиПоМЧД = ДанныеПодписиОбъекта.ДанныеПодписи.РезультатПроверкиПоМЧД; // Неопределено,См. МашиночитаемыеДоверенности.НовыйРезультатПроверкиПодписи

	Если РезультатПроверкиПоМЧД <> Неопределено 
		И ЗначениеЗаполнено(РезультатПроверкиПоМЧД.Доверенность) Тогда
		ПоказатьЗначение(, РезультатПроверкиПоМЧД.Доверенность);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти // Подписи

#Область СформироватьПакетЭДИзФайлаСДискаИОбъектаУчета

// Параметры:
//  ПомещенныеФайлы - Неопределено
//                  - Массив из Структура:
//                    * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                    * Имя       - Строка
//                    * ПолноеИмя - Строка
//                    * ИмяФайла  - Строка - имя файла с расширением.
//  ОбъектУчета - ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеВыбораФайлов(ПомещенныеФайлы, ОбъектУчета) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	РезультатыПроверкиИмен = РаботаСФайламиБЭДКлиент.ОписанияФайловПослеПроверкиКорректностиИмен(ПомещенныеФайлы,
		ТранслитерацияОтключена);
	
	ОбъектыУчета = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ОбъектУчета);
	АктуальныйЭлектронныйДокумент = ИнтеграцияЭДОВызовСервера.ОсновнойЭлектронныйДокументОбъектаУчета(ОбъектУчета);
		
	Если ЗначениеЗаполнено(АктуальныйЭлектронныйДокумент) Тогда
		СформироватьПакетЭДИзФайловСДискаИОбъектаУчета_ДобавлениеВПакетФайловСКомпьютера(
			АктуальныйЭлектронныйДокумент, РезультатыПроверкиИмен.ОписанияФайлов, ОбъектыУчета,
			РезультатыПроверкиИмен.СообщенияДляПользователя);
	Иначе
		СформироватьПакетЭДИзФайловСДискаИОбъектаУчета_СформироватьЭДПоОбъектуУчета(ОбъектыУчета,
			РезультатыПроверкиИмен.ОписанияФайлов, РезультатыПроверкиИмен.СообщенияДляПользователя);
	КонецЕсли;
	
КонецПроцедуры

// @skip-check statement-type-change, property-return-type
// 
// Параметры:
//  ЭлектронныйДокументПоОбъектуУчета - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчета_ДобавлениеВПакетФайловСКомпьютера(
	ЭлектронныйДокументПоОбъектуУчета, ОписанияФайлов, ОбъектыУчета, СообщенияДляПользователя)
	
	РеквизитыЭлектронногоДокумента = ЭлектронныеДокументыЭДОВызовСервера.РеквизитыДокументаДляФормированияПакета(
		ЭлектронныйДокументПоОбъектуУчета);
	
	ПараметрыДобавленияВПакет = ПакетыДокументовЭДОКлиент.НовыеПараметрыДобавленияВПакет();
	ПараметрыДобавленияВПакет.ЭлектронныйДокумент = ЭлектронныйДокументПоОбъектуУчета;
	ПараметрыДобавленияВПакет.Отправитель = РеквизитыЭлектронногоДокумента.Организация;
	ПараметрыДобавленияВПакет.Получатель = РеквизитыЭлектронногоДокумента.Контрагент;
	ПараметрыДобавленияВПакет.Договор = РеквизитыЭлектронногоДокумента.ДоговорКонтрагента;
	ПараметрыДобавленияВПакет.ЭтоОблачныйЭДО = РеквизитыЭлектронногоДокумента.ЭтоОблачныйЭДО;
	ПараметрыДобавленияВПакет.ИдентификаторПакета = РеквизитыЭлектронногоДокумента.ИдентификаторПакета;
	ПараметрыДобавленияВПакет.ОбъектыУчета = ОбъектыУчета;
	
	ПакетыДокументовЭДОКлиент.СформироватьЭДПоФайламСДискаДобавитьВПакет(ПараметрыДобавленияВПакет, ОписанияФайлов,
		СообщенияДляПользователя);
	
	РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчета_СформироватьЭДПоОбъектуУчета(ОбъектыУчета, ОписанияФайлов,
	СообщенияДляПользователя)
	
	НаборДействий = Новый Соответствие;
	ЭлектронныеДокументыЭДОКлиентСервер.ДобавитьДействие(НаборДействий,
		ПредопределенноеЗначение("Перечисление.ДействияПоЭДО.Сформировать"));
		
	ПараметрыВыполненияДействийПоЭДО = ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО();
	ПараметрыВыполненияДействийПоЭДО.НаборДействий = НаборДействий;
	ПараметрыВыполненияДействийПоЭДО.ОбъектыДействий.ОбъектыУчета = ОбъектыУчета;
		
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ОбъектыУчета", ОбъектыУчета);
	ПараметрыОповещения.Вставить("ОписанияФайлов", ОписанияФайлов);
	ПараметрыОповещения.Вставить("ПараметрыВыполненияДействийПоЭДО", ПараметрыВыполненияДействийПоЭДО);
	ПараметрыОповещения.Вставить("СообщенияДляПользователя", СообщенияДляПользователя);
	
	Оповещение = Новый ОписаниеОповещения(
		"СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеФормированияЭДПоОбъектуУчета",
		ЭтотОбъект, ПараметрыОповещения);
	
	ЭлектронныеДокументыЭДОКлиент.НачатьВыполнениеДействийПоЭДО(Оповещение, ПараметрыВыполненияДействийПоЭДО);
	
КонецПроцедуры

// Параметры:
//  Результат - Неопределено,Структура:
//  * Итог - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  * ОшибкиФормирования - Массив Из Структура
//  * КонтекстДиагностики - См. ОбработкаНеисправностейБЭДКлиент.НовыйКонтекстДиагностики
//  Параметры - Структура:
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * ПараметрыВыполненияДействийПоЭДО - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеФормированияЭДПоОбъектуУчета(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеУспешногоРезультатаВыполненияДействийПоЭДО",
		ЭтотОбъект, Параметры);
	
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("ОбъектыУчета", Параметры.ОбъектыУчета);
	ПараметрыОповещения.Вставить("ОповещениеУспешногоЗавершения", Оповещение);
	
	ИнтерфейсДокументовЭДОКлиент.ПослеВыполненияДействийПоЭДО(Результат, ПараметрыОповещения);
	
КонецПроцедуры

// Параметры:
//  Результат - Неопределено
//            - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  Параметры - Структура:
//  * ОбъектыУчета - Массив Из ОпределяемыйТип.ОснованияЭлектронныхДокументовЭДО
//  * ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
//  * ПараметрыВыполненияДействийПоЭДО - См. ИнтерфейсДокументовЭДОКлиентСервер.НовыеПараметрыВыполненияДействийПоЭДО
//  * СообщенияДляПользователя - Массив Из ФорматированнаяСтрока
//
Процедура СформироватьПакетЭДИзФайловСДискаИОбъектаУчетаПослеУспешногоРезультатаВыполненияДействийПоЭДО(Результат,
	Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭлектронныйДокументПоОбъектуУчета = Неопределено;
	Для Каждого ОбработанныйДокумент Из Результат.ОбработанныеДокументы Цикл
		ЭлектронныйДокументПоОбъектуУчета = ОбработанныйДокумент.Ключ; // ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ЭлектронныйДокументПоОбъектуУчета) Тогда
		Возврат;
	КонецЕсли;
	
	СформироватьПакетЭДИзФайловСДискаИОбъектаУчета_ДобавлениеВПакетФайловСКомпьютера(ЭлектронныйДокументПоОбъектуУчета,
		Параметры.ОписанияФайлов, Параметры.ОбъектыУчета, Параметры.СообщенияДляПользователя);
	
КонецПроцедуры

#КонецОбласти // СформироватьПакетЭДИзФайловСДискаИОбъектаУчета

#Область СоздатьДокументПоФайлу

// Параметры:
//  ПомещенныйФайл - Неопределено
//                 - Структура:
//                   * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                   * Имя       - Строка - в тонком клиенте и в веб-клиенте с установленным
//                                          расширением работы с 1С:Предприятием - локальный путь, по которому
//                                          был получен файл. В веб-клиенте без расширения для работы с 1С:Предприятием
//                                          передается имя файла с расширением.
//  ДополнительныеПараметры - Произвольный
Процедура СоздатьДокументПоФайлуПослеВыбораФайлаСДиска(ПомещенныйФайл, ДополнительныеПараметры) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураИмениФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ПомещенныйФайл.Имя);
	ИмяФайла = СтруктураИмениФайла.Имя;
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	РезультатыПроверкиИмени = 
		РаботаСФайламиБЭДКлиент.ОписаниеФайлаПослеПроверкиКорректностиИмениПоИмениИАдресуВоВременномХранилище(
			ИмяФайла, ПомещенныйФайл.Хранение, ТранслитерацияОтключена);
	
	Если Не ЗначениеЗаполнено(РезультатыПроверкиИмени.ОписаниеФайла) Тогда
		РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
			РезультатыПроверкиИмени.СообщенияДляПользователя);
		Возврат;
	КонецЕсли;
	
	ОписаниеФайла = РезультатыПроверкиИмени.ОписаниеФайла;
	
	ИнтерфейсДокументовЭДОКлиент.ОткрытьНовыйЭлектронныйДокументПоФайлу(ОписаниеФайла.ИмяФайла,
		ПоместитьВоВременноеХранилище(ОписаниеФайла.ДвоичныеДанные, Новый УникальныйИдентификатор),, Истина);
		
	РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
		РезультатыПроверкиИмени.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ПомещенныеФайлы - Неопределено
//                  - Массив из Структура:
//                    * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                    * Имя       - Строка
//                    * ПолноеИмя - Строка
//                    * ИмяФайла  - Строка - имя файла с расширением.
//  ДополнительныеПараметры - Произвольный
Асинх Процедура СоздатьДокументыПоФайламПослеВыбораФайловСДиска(ПомещенныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПомещенныеФайлы) Тогда
		Возврат;
	КонецЕсли;
	
	Если ПомещенныеФайлы.Количество() = 1 Тогда
		СоздатьДокументыПоФайлам(ПомещенныеФайлы);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Хотите объединить выбранные файлы в пакет?';
						|en = 'Combine the selected files into a package?'");
	Ответ = Ждать ВопросАсинх(ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена); // КодВозвратаДиалога
	Если Ответ = КодВозвратаДиалога.Да Тогда
		СоздатьПакетДокументовПоФайлам(ПомещенныеФайлы);
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		СоздатьДокументыПоФайлам(ПомещенныеФайлы);
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

// Параметры:
//  ПомещенныеФайлы - Массив из Структура:
//                    * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                    * Имя       - Строка
//                    * ПолноеИмя - Строка
//                    * ИмяФайла  - Строка - имя файла с расширением.
Процедура СоздатьДокументыПоФайлам(ПомещенныеФайлы)
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	РезультатыПроверкиИмен = РаботаСФайламиБЭДКлиент.ОписанияФайловПослеПроверкиКорректностиИмен(ПомещенныеФайлы,
		ТранслитерацияОтключена);
	
	Для Каждого ОписаниеФайла Из РезультатыПроверкиИмен.ОписанияФайлов Цикл
		ИнтерфейсДокументовЭДОКлиент.ОткрытьНовыйЭлектронныйДокументПоФайлу(ОписаниеФайла.ИмяФайла,
			ПоместитьВоВременноеХранилище(ОписаниеФайла.ДвоичныеДанные, Новый УникальныйИдентификатор),, Истина);
	КонецЦикла;
	
	РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
		РезультатыПроверкиИмен.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ПомещенныеФайлы - Массив из Структура:
//                    * Хранение  - Строка - адрес временного хранилища с двоичными данными файла.
//                    * Имя       - Строка
//                    * ПолноеИмя - Строка
//                    * ИмяФайла  - Строка - имя файла с расширением.
Процедура СоздатьПакетДокументовПоФайлам(ПомещенныеФайлы)
	
	ТранслитерацияОтключена = Не ИнтерфейсДокументовЭДОВызовСервера.ВключенаТранслитерацияИменФайлов();
	
	РезультатыПроверкиИмен = РаботаСФайламиБЭДКлиент.ОписанияФайловПослеПроверкиКорректностиИмен(ПомещенныеФайлы,
		ТранслитерацияОтключена);
	
	ОткрытьНовыйПакетЭлектронныхДокументовПоФайлам(РезультатыПроверкиИмен.ОписанияФайлов);
	
	РаботаСФайламиБЭДКлиент.ПоказатьСообщенияРезультатаПроверкиИменФайловПользователю(
		РезультатыПроверкиИмен.СообщенияДляПользователя);
	
КонецПроцедуры

// Параметры:
//  ОписанияФайлов - Массив Из См. РаботаСФайламиБЭДКлиентСервер.НовоеОписаниеФайла
Процедура ОткрытьНовыйПакетЭлектронныхДокументовПоФайлам(ОписанияФайлов)
	
	ПараметрыФормы = Новый Структура("ОписанияФайлов", ОписанияФайлов);
	ИнтерфейсДокументовЭДОКлиент.ОткрытьФормуЭлектронногоДокумента(ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти // СоздатьДокументПоФайлу

#КонецОбласти // ОбработкаКоманд

#Область СозданиеДокументовПакетаПоПредварительнымДанным

// Параметры:
//  Результат - Неопределено
//            - См. ЭлектронныеДокументыЭДОКлиентСервер.НовыйИтогВыполненияДействийПоЭДО
//  ПараметрыОповещения - Структура:
//                        * ТекущийДокумент - ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
//                        * ЭтоОблачныйЭДО - Булево
//                        * ОбработчикПослеФормированияПакета - ОписаниеОповещения
//
Процедура ОбъединитьДокументыВПакетПослеФормированияПоПредварительнымДанным(Результат, ПараметрыОповещения) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументыДляОбъединения = Новый Массив; // Массив Из ДокументСсылка.ЭлектронныйДокументИсходящийЭДО
	
	Для Каждого ОбработанныйДокумент Из Результат.ОбработанныеДокументы Цикл
		//@skip-check invocation-parameter-type-intersect
		ДокументыДляОбъединения.Добавить(ОбработанныйДокумент.Ключ);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(ДокументыДляОбъединения) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыОповещения.ЭтоОблачныйЭДО Тогда
		ДокументыДляОбъединения.Добавить(ПараметрыОповещения.ТекущийДокумент);
	КонецЕсли;
	
	ПакетыДокументовЭДОКлиент.СоздатьПакетДокументов(ДокументыДляОбъединения,
		ПараметрыОповещения.ОбработчикПослеФормированияПакета);
	
КонецПроцедуры

// Параметры:
//  ДанныеПредварительногоДокумента - ДанныеФормыЭлементКоллекции:
//  * ИдентификаторДокумента - УникальныйИдентификатор
//  * АдресОписанияФайла - Строка
//  * ВидДокумента - СправочникСсылка.ВидыДокументовЭДО
//  * ТипДокумента - ПеречислениеСсылка.ТипыДокументовЭДО
//  * ТипРегламента - ПеречислениеСсылка.ТипыРегламентовЭДО
//  * ДатаДокумента - Дата
//  * ДоговорКонтрагента - ОпределяемыйТип.ДоговорСКонтрагентомЭДО
//  * Контрагент - ОпределяемыйТип.УчастникЭДО
//  * НомерДокумента - Строка
//  * Организация - ОпределяемыйТип.Организация
//  * ИмяФайла - Строка
//  * СуммаДокумента - Число
//  * СопроводительнаяЗаписка - Строка
//  * МаршрутПодписания - СправочникСсылка.МаршрутыПодписания
//  * СписокПодписантов - ДанныеФормыКоллекция Из ДанныеФормыЭлементКоллекции:
//   ** Подписант - ОпределяемыйТип.Пользователь
//   ** Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования
// 
// Возвращаемое значение:
//  См. ИнтерфейсДокументовЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам
Функция ОписаниеДокументаДляФормированияПоПредварительнымДанным(ДанныеПредварительногоДокумента)
	
	ОписаниеДокумента = ИнтерфейсДокументовЭДОКлиентСервер.НовоеОписаниеДокументаДляФормированияПоФайлам();
	
	ОписаниеДокумента.Идентификатор = Строка(ДанныеПредварительногоДокумента.ИдентификаторДокумента);
	ОписаниеДокумента.Организация = ДанныеПредварительногоДокумента.Организация;
	ОписаниеДокумента.Контрагент = ДанныеПредварительногоДокумента.Контрагент;
	ОписаниеДокумента.Договор = ДанныеПредварительногоДокумента.ДоговорКонтрагента;
	ОписаниеДокумента.ВидДокумента = ДанныеПредварительногоДокумента.ВидДокумента;
	ОписаниеДокумента.Номер = ДанныеПредварительногоДокумента.НомерДокумента;
	ОписаниеДокумента.Дата = ДанныеПредварительногоДокумента.ДатаДокумента;
	ОписаниеДокумента.Сумма = ДанныеПредварительногоДокумента.СуммаДокумента;
	ОписаниеДокумента.СопроводительнаяЗаписка = ДанныеПредварительногоДокумента.СопроводительнаяЗаписка;
	ОписаниеДокумента.МаршрутПодписания = ДанныеПредварительногоДокумента.МаршрутПодписания;
	
	Подписанты = Новый Массив; // Массив Из ОпределяемыйТип.Пользователь
	Для Каждого ДанныеПодписанта Из ДанныеПредварительногоДокумента.СписокПодписантов Цикл
		Подписанты.Добавить(ДанныеПодписанта.Подписант);
	КонецЦикла;
	ОписаниеДокумента.Подписанты = Подписанты;
	
	ОписаниеФайла = ПолучитьИзВременногоХранилища(ДанныеПредварительногоДокумента.АдресОписанияФайла);
	НовыеФайлыТитула = ИнтерфейсДокументовЭДОКлиентСервер.НовыеФайлыТитулаДокументаДляФормирования();
	НовыеФайлыТитула.Основной = ОписаниеФайла;
	ОписаниеДокумента.ФайлыТитулов.Добавить(НовыеФайлыТитула);
	
	Возврат ОписаниеДокумента;
	
КонецФункции

#КонецОбласти // СозданиеДокументовПакетаПоПредварительнымДанным

#КонецОбласти // СлужебныеПроцедурыИФункции
