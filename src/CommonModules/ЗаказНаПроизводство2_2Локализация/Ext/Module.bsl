
#Область ПрограммныйИнтерфейс

#Область ОбработчикиСобытий

// Вызывается из соответствующего обработчика документа
//
// Параметры:
//  Объект - ДокументОбъект - Обрабатываемый объект
//  Отказ - Булево - Если в теле процедуры-обработчика установить данному параметру значение Истина,
//                   то будет выполнен отказ от продолжения работы после выполнения проверки заполнения.
//  ПроверяемыеРеквизиты - Массив - Массив путей к реквизитам, для которых будет выполнена проверка заполнения.
//
Процедура ОбработкаПроверкиЗаполнения(Объект, Отказ, ПроверяемыеРеквизиты) Экспорт
	
	//++ Локализация
	ПроверитьНаличиеПрослеживаемойНоменклатурыВТабличнойЧасти(Объект, Отказ);
	//-- Локализация
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

//++ Локализация

Процедура ПроверитьНаличиеПрослеживаемойНоменклатурыВТабличнойЧасти(Объект, Отказ)
	
	Если Не УчетПрослеживаемыхТоваровЛокализация.ИспользоватьУчетПрослеживаемыхИмпортныхТоваров(Дата(1, 1, 1))
		Или (Не Объект.ХозяйственнаяОперация = ДавальческаяСхемаКлиентСервер.ХозяйственнаяОперацияДоговора()
			//++ Устарело_Переработка24
			И Не Объект.ХозяйственнаяОперация = Перечисления.ХозяйственныеОперации.ПроизводствоИзДавальческогоСырья
			//-- Устарело_Переработка24
			И Истина) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки  КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура,
	|	ТаблицаТоваров.Назначение   КАК Назначение
	|ПОМЕСТИТЬ ТаблицаТоварыДляЗапроса
	|ИЗ
	|	&ТаблицаТоваров КАК ТаблицаТоваров
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТоваров.НомерСтроки  КАК НомерСтроки,
	|	ТаблицаТоваров.Номенклатура КАК Номенклатура
	|ИЗ
	|	ТаблицаТоварыДляЗапроса КАК ТаблицаТоваров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СправочникНоменклатура
	|	ПО ТаблицаТоваров.Номенклатура = СправочникНоменклатура.Ссылка
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Назначения КАК СправочникНазначения
	|	ПО ТаблицаТоваров.Назначение = СправочникНазначения.Ссылка
	|
	|ГДЕ
	|	СправочникНоменклатура.ПрослеживаемыйТовар
	|	И СправочникНазначения.ТипНазначения В(
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.Давальческое2_5),
	//++ Устарело_Переработка24
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеПродукция22),
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалы22),
	//-- Устарело_Переработка24
	|		ЗНАЧЕНИЕ(Перечисление.ТипыНазначений.ДавальческоеМатериалыПодЭтап22))
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|";
	
	Запрос.УстановитьПараметр("ТаблицаТоваров", Объект.Продукция.Выгрузить(, "НомерСтроки, Номенклатура, Назначение"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ШаблонСообщения =
		НСтр("ru = 'В строке %1 списка ""%2"" выбрана прослеживаемая номенклатура.
			|При включенном учете прослеживаемых импортных товаров выбор такой номенклатуры запрещен.';
			|en = 'Traceable item is selected in line %1 of the ""%2"" list.
			|If traceable imported goods accounting is enabled, you cannot select this item.'");
	
	КлючДанных = ОбщегоНазначенияУТ.КлючДанныхДляСообщенияПользователю(Объект);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ТекстСообщения = СтрШаблон(ШаблонСообщения, Выборка.НомерСтроки, НСтр("ru = 'Продукция';
																				|en = 'Manufactured products'"));
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("Продукция", Выборка.НомерСтроки, "Номенклатура");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, КлючДанных, Поле, "Объект", Отказ);
		
	КонецЦикла;
	
КонецПроцедуры

//-- Локализация
